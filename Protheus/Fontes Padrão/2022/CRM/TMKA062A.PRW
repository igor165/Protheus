#INCLUDE "TMKA062A.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TMKDEF.CH"
#INCLUDE "MSOle.Ch"

#DEFINE VISUAL  	2
#DEFINE INCLUIR 	3
#DEFINE ALTERAR 	4
#DEFINE EXCLUIR 	5
#DEFINE PENDENTE   2

Static lSQL			:= !(Upper(TcSrvType())$"AS/400%ISERIES") .And. !(Upper(TCGetDB()) $ "DB2|INFORMIX")
Static _lTMK062Lis	:= ExistBlock("TK062LIS")

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk062OutraLista ºAutor  ³Vendas Clientes ºData³  04/22/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Verifica se o contato ja esta em alguma outra lista		  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062OutraLista(cContato,lTmkList,lSimultaneo,cFilSU4,cFilSU6)

Local aArea  		:= GetArea()
Local lCodeBase 	:= .T.
Local lRet   		:= .F.
Local cAliasAux		:= GetNextAlias()

Default cFilSU4		:= xFilial("SU4")
Default cFilSU6		:= xFilial("SU6")
Default lTmkList	:= GetNewPar("MV_TMKLIST" ,.F.)		    //Determina não utilização de contatos ja presente em lista de cobrança ativa/ em andamento
Default lSimultaneo	:= TkPosto(TkOperador(),"U0_VALCONT") == "2"

If lTmkList

	BeginSql alias cAliasAux
		SELECT SU4.U4_FILIAL, SU4.U4_TIPO, SU4.U4_STATUS, SU4.U4_LISTA, SU6.U6_FILIAL, SU6.U6_LISTA, SU6.U6_CONTATO, SU6.U6_DATA, SU6.U6_STATUS
		  FROM %Table:SU4% SU4, %Table:SU6% SU6
		 WHERE SU4.U4_FILIAL = %xfilial:SU4%
		   AND SU6.U6_FILIAL = %xfilial:SU6%
		   AND SU6.U6_CONTATO = %exp:cContato%
		   AND SU4.U4_TIPO = '2'
		   AND SU6.U6_LISTA = SU4.U4_LISTA
		   AND ( SU6.U6_DATA = %exp:DtoS(dDatabase)% OR ( SU6.U6_DATA <> %exp:DtoS(dDatabase)% AND SU4.U4_STATUS <> '2' ) )
		   AND SU4.%NotDel%
		   AND SU6.%NotDel%
	EndSql

	If (cAliasAux)->(! Eof())
		lRet:= .T.
	EndIf
	(cAliasAux)->(DbCloseArea())

Else

	If lSQL .And. !lA062Auto	// se for dbAccess
		If Type("cTblSU6")=="C" // Private declarado na TMKA062
			dbSelectArea(cTblSU6)
			If MsSeek(cContato)
				lRet := (cTblSU6)->TOTALREG > 0
			Else
				lRet := .F.
			EndIF
			lCodeBase := .F. // Já foi validado, não precisa passar pela validacao via codebase
		EndIf
	EndIf

	If lCodeBase .Or. lA062Auto
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se ja existe alguma lista para o contato no mesmo dia.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("SU6")
		DbSetOrder(2)
		If MsSeeK(cFilSU6 + Dtos(M->U4_DATA) + cContato)
			While	SU6->(! Eof())				.AND.;
					SU6->U6_FILIAL == cFilSU6 	.AND.;
					SU6->U6_DATA == dDataBase	.AND.;
					SU6->U6_CONTATO == cContato
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Independente do Status, se o contato existir em outra Lista  ³
				//³para o mesmo dia ele nao sera incluido nesta geracao.        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If (SU6->U6_STATUS == "1").OR.(SU6->U6_STATUS == "2").OR.(SU6->U6_STATUS == "3") //Nao-Enviado,Em Uso ou Enviado - Este laço pode ser substituido por um lRet := .T.
					lRet:= .T.
					Exit
				Endif
				DbSkip()
			End
		Endif
	EndIf

	lData := .F.

	//Permite que o contato esteja em duas lista de formas de contato diferentes.
	//Se for selecionado 1 ( Exclusivo ) a avaliacao abaixo nao sera efetuada.
	If lRet .AND. lSimultaneo // U0_VALCONT == 2
		DbSelectArea("SU4")
		DbSetOrder(1)
		If MsSeeK(cFilSU4 + SU6->U6_LISTA)
			If SU4->U4_FORMA <> M->U4_FORMA
				lRet := .F.
			Endif
		Endif
	Endif

EndIf

RestArea(aArea)
Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Tk062Grava ³ Autor ³ Vendas Clientes       ³ Data ³ 02/07/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Grava a Lista e os itens no SU4 e SU6                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		    	            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Tk062Grava(	nOpc,    aLista,    cAssunto, cMensagem,;
						cAttach, aAttach,   cFiltro,  cSelOper,;
						lGrvCab, nChamadas, nSaveSX8, nTipo)
Local nCampo:= 0
Local aAux := {}
Local lPosU6Dt	:= SU6->(ColumnPos("U6_DTBASE")) > 0
Local cFilialSU4 := xFilial("SU4")
Local cFilialSK1 := xFilial("SK1")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Realiza a gravacao da geracao de co ntatos.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
BEGIN TRANSACTION

	If !lGrvCab
		Do Case
			Case nOpc == INCLUIR
				While (GetSx8Len() > nSaveSx8)
					ConfirmSx8()
				EndDo
				
			Case nOpc == ALTERAR
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Apaga o memo da mensagem .³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				MSMM(SU4->U4_CODMENS,80,,,2) //nao ha alteracao de dados de lista ja gerada.
		Endcase
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Grava o cabecalho da Lista de Contatos.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RecLock("SU4",IIF(nOpc#3, .F., .T.))
		For nCampo := 1 To FCount()
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Caso não seja uma alteração grava normalmente o cabeçalho.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			IF nOpc <> 4
				
				Do Case
					Case (FieldName(nCampo) == "U4_FILIAL" )
						FieldPut(nCampo, cFilialSU4)
						
					Case (FieldName(nCampo) == "U4_ASSUNTO" )
						FieldPut(nCampo, cAssunto)
						
					Case (FieldName(nCampo) == "U4_ATTACH" )
						FieldPut(nCampo, cAttach)
						
					Case (FieldName(nCampo) == "U4_FILTRO" )
						FieldPut(nCampo, cFiltro)
						
					Case (FieldName(nCampo) == "U4_STATUS" )
						FieldPut(nCampo, "1")
						
					Case (FieldName(nCampo) == "U4_OPERAD" )
						FieldPut(nCampo, cSelOper)
						
					Case (FieldName(nCampo) == "U4_LOJA" )
						FieldPut(nCampo, IIF(nChamadas>=0 .And. nTipo==2,StrZero(nChamadas,2),""))
						
					OtherWise
						FieldPut(nCampo, M->&(FieldName(nCampo)))
				EndCase
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Caso seja Alteração grava somente os campos que podem ser alterados. ³
				//³Neste caso a funcao de Montagem da Lista foi pulada, pois nao poderao³
				//³ser gravados campos que influenciem em seus dados já selecionados.   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
			Else

				Do case
					Case (FieldName(nCampo) == "U4_ASSUNTO" )
						FieldPut(nCampo, cAssunto)
						
					Case (FieldName(nCampo) == "U4_ATTACH" )
						FieldPut(nCampo, cAttach)
						
					Case (FieldName(nCampo) == "U4_OPERAD" )
						FieldPut(nCampo, cSelOper)
						
					Case (FieldName(nCampo) == "U4_MALADIR" )
						FieldPut(nCampo, M->&(FieldName(nCampo)))
						
					Case (FieldName(nCampo) == "U4_ETIQUET" )
						FieldPut(nCampo, M->&(FieldName(nCampo)))
												
					Case (FieldName(nCampo) == "U4_CODCAMP" )
						FieldPut(nCampo, M->&(FieldName(nCampo)))
						
					Case (FieldName(nCampo) == "U4_DESCCAM" )
						FieldPut(nCampo, M->&(FieldName(nCampo)))
						
					Case (FieldName(nCampo) == "U4_SCRIPT" )
						FieldPut(nCampo, M->&(FieldName(nCampo)))
						
					Case (FieldName(nCampo) == "U4_EVENTO" )
						FieldPut(nCampo, M->&(FieldName(nCampo)))
						
					Case (FieldName(nCampo) == "U4_DESC" )
						FieldPut(nCampo, M->&(FieldName(nCampo)))
						
					Case (FieldName(nCampo) == "U4_CONFIG" )
						FieldPut(nCampo, M->&(FieldName(nCampo)))
						
				EndCase
				
			Endif
		Next nCampo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Grava a mensagem do e-mail.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(cMensagem)
			MSMM(,80,,cMensagem,1,,,"SU4","U4_CODMENS") //Mensagem do Email
		Endif
		
		cMensagem	:= Space(400)
		cAssunto	:= CriaVar("U4_ASSUNTO",.F.)
		aAttach	:= {}
		cAttach	:= ""
		cFiltro	:= ""
		
		lGrvCab	:= .T.
		FkCommit()
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Funcao para ordernar o array alista. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
	If _lTMK062Lis
		aAux := ExecBlock("TK062LIS", .F., .F.,{aClone(aLista), cAssunto, cMensagem,;
		                                        cAttach, aAttach, cFiltro, cSelOper,;
		                                        lGrvCab, nChamadas, nSaveSX8})
		If ValType(aAux) == "A"
			aLista := aAux
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Grava as chamadas a serem executadas.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nOpc <> 4  // Na alteracao nao sao gravados os itens.
		DbSelectArea("SU6")
		For nCampo := 1 TO Len(aLista)
			
			RecLock("SU6",.T.)
			
			Replace U6_FILIAL  With aLista[nCampo][1]  //Filial
			Replace U6_LISTA   With aLista[nCampo][2]  //Codigo da Lista
			Replace U6_CODIGO  With aLista[nCampo][3]  //Codigo de Interacao
			Replace U6_FILENT  With aLista[nCampo][4]  //Filial da Entidade
			Replace U6_ENTIDA  With aLista[nCampo][5]  //Entidade
			Replace U6_CODENT  With aLista[nCampo][6]  //Codigo da Entidade
			Replace U6_ORIGEM  With aLista[nCampo][7]  //Origem da Interacao
			Replace U6_CONTATO With aLista[nCampo][8]  //Contato
			Replace U6_DATA    With aLista[nCampo][9]  //Data
			Replace U6_HRINI   With aLista[nCampo][10] //Hora Inicio
			Replace U6_HRFIM   With aLista[nCampo][11] //Hora Fim
			Replace U6_STATUS  With aLista[nCampo][12] //Status da Interacao
			
			If lPosU6Dt
				Replace U6_DTBASE	With dDataBase
			EndIf

			MsUnlock()
			
			DbSelectArea("SK1")
			DbSetOrder(4)		// K1_FILIAL+K1_CLIENTE+K1_LOJA+DTOS(K1_VENCREA)
			MSSeeK(cFilialSK1 + AllTrim(SU6->U6_CODENT))
			While	!Eof()										.AND.;
				SK1->K1_FILIAL == cFilialSK1				.AND.;
				SK1->K1_CLIENTE == SubStr(SU6->U6_CODENT,1,6)	.AND.;
				SK1->K1_LOJA == SubStr(SU6->U6_CODENT,7,2)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica se o título está em exceção.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SK1->K1_OPERAD == "XXXXXX"
					SK1->(DbSkip())
					Loop
				Endif
				
				RecLock("SK1", .F.)
				Replace SK1->K1_OPERAD With SU4->U4_OPERAD
				MsUnLock()
				
				DbSkip()
			EndDo
			
		Next nCampo
	Endif
	
END TRANSACTION

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Tk062SA12  ³ Autor ³ Vendas Clientes       ³ Data ³ 02/07/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Realiza a selecao dos parametros para o SA1                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca).                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/ 

Function Tk062SA12(	ceMv_Par01,	ceMv_Par02,	ceMv_Par03,	ceMv_Par04,;
						ceMv_Par05,	ceMv_Par06,	ceMv_Par07,	ceMv_Par08,;
						ceMv_Par09,	ceMv_Par10,	ceMv_Par11,	ceMv_Par12,;
						dDataLista,	nChamadas,		lLog,			ceMv_Par13,;
						cTipo,			cCodEnt, 		cFilialSK1,	cFilialACG,;
						cFilOrig, nTipo )

Local aArea		:= GetArea()
Local lRet			:= .F.									// Se o retorno for TRUE grava, senao avanca o registro do AC8
Local nTotCham	:= 0
Local lPosK1FilO	:= SK1->(FieldPos("K1_FILORIG")) > 0
Local lCodeBase  := .T.

Default cTipo		:= UPPER(GetNewPar("MV_TMKCOBR","")) 	// Contem os tipos de titulos que devem ser utilizados para cobranca
Default cFilialSK1 := xFilial("SK1")
Default cFilialACG := xFilial("ACG")
Default cFilOrig := xFilial("SE1")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Parametros enviados para a selecao                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Pergunte TMK062
//Mv_Par01 	//Do Prefixo
//Mv_Par02 	//Ate o Prefixo
//Mv_Par03 	//Do Titulo
//Mv_Par04 	//Ate o Titulo
//Mv_Par05 	//Do Banco
//Mv_Par06 	//Ate o Banco
//Mv_Par07 	//Do Vencimento
//Mv_Par08 	//Ate o Vencimento
//Mv_Par09 	//Da Natureza
//Mv_Par10 	//Ate a Natureza
//Mv_Par11 	//Da Emissao
//Mv_Par12 	//Ate a Emissao

If lSQL // se for dbAccess
	If !lLog
		lCodeBase := .F. // Já foi validado, não precisa passar pela validacao via codebase
		lRet := .T.		// Se for top, esta validado na query
	Else
		If Type("cTblSK1")=="C" // Private declarado na TMKA062
			dbSelectArea(cTblSK1)
			If MsSeek(Alltrim(cCodEnt))
				lRet := (cTblSK1)->TOTALREG > 0
			Else
				lRet := .F.
			EndIF
			lCodeBase := .F. // Já foi validado, não precisa passar pela validacao via codebase
		EndIf
	Endif
EndIf
	
// se for codebase
If lCodeBase
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Somente os titulos que estiverem no SK1 podem ser cobrados³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	  
	DbSelectArea("SK1")
	DbSetOrder(4) //K1_FILIAL+K1_CLIENTE+K1_LOJA+DTOS(K1_VENCREA)
	MSSeeK(cFilialSK1+ AllTrim(cCodEnt))
	While (!EOF() .AND.;
			cFilialSK1 == SK1->K1_FILIAL .AND.;
			AllTrim(cCodEnt) == AllTrim(SK1->K1_CLIENTE+SK1->K1_LOJA))
				
		If lPosK1FilO
			cFilorig := SK1->K1_FILORIG
		EndIf
	
		DbSelectArea("SE1")
		DbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
		MSSeeK(cFilorig + SK1->K1_PREFIXO + SK1->K1_NUM + SK1->K1_PARCELA + SK1->K1_TIPO, .T.)
		
		While	(!Eof()) .AND.;
				(cFilorig == SE1->E1_FILIAL) .AND.;
				(SE1->E1_PREFIXO == SK1->K1_PREFIXO) .AND.;
				(SE1->E1_NUM == SK1->K1_NUM) .AND.;
				(SE1->E1_PARCELA == SK1->K1_PARCELA) .AND.;
				(SE1->E1_TIPO == SK1->K1_TIPO)
		
			If	SE1->E1_PREFIXO >= ceMv_Par01 .AND. SE1->E1_PREFIXO <= ceMv_Par02 .AND.;
				SE1->E1_NUM     >= ceMv_Par03 .AND. SE1->E1_NUM     <= ceMv_Par04 .AND.;
				SE1->E1_PORTADO >= ceMv_Par05 .AND. SE1->E1_PORTADO <= ceMv_Par06 .AND.;
				SE1->E1_VENCREA >= ceMv_Par07 .AND. SE1->E1_VENCREA <= ceMv_Par08 .AND.;
				SE1->E1_NATUREZ >= ceMv_Par09 .AND. SE1->E1_NATUREZ <= ceMv_Par10 .AND.;
				SE1->E1_EMISSAO >= ceMv_Par11 .AND. SE1->E1_EMISSAO <= ceMv_Par12 .AND.;
				SE1->E1_VENCREA <  dDataLista .AND. SE1->E1_SALDO   >  0          .AND.;
				IIf(!Empty(ceMv_Par13), SE1->E1_SITUACA == ceMv_Par13, .T.)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Separa o que e abatimento quando o parametro MV_TMKCOBR  que contem	³
				//³os tipos de titulos a serem cobrados nao foi criado, caso contrario	³
				//³considera apenas os tipos informados no parametro.                 	³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Empty(cTipo)
					If SE1->E1_TIPO $ MVPROVIS+"/"+MVRECANT+"/"+MV_CRNEG+"/"+MVABATIM
						DbSelectArea("SE1")
						SE1->(DbSkip())
						Loop
					Endif
				Else
					If !(Alltrim(SE1->E1_TIPO) $ cTipo)
						DbSelectArea("SE1")
						SE1->(DbSkip())
						Loop
					Endif
				Endif
	
				//ÚÄÄTELECOBRANCAÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Neste ponto da query esta sendo tratando se a lista de inadimplentes eh diferenciada, onde³
				//³a quantidade de cobrancas feitas para cada titulo eh considerada, pois influencia no trata³
				//³mento dado pelo operador que estah gerando a lista de inadimplencia. A variavel nChamadas ³
				//³esta no botao Detalhes da Toolbar da Lista de Cobranca.                                   ³
				//³Se nChamadas = 0, os títulos que já possuem atendimento ficarão fora da lista. Pode ser	 ³
				//³utilizado para filtrar os títulos que foram reagendados para uma data futura.			 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("ACG")
				DbSetOrder(2)
				If MSSeeK(cFilialACG + SE1->E1_PREFIXO + SE1->E1_NUM + SE1->E1_PARCELA + SE1->E1_TIPO)
					While	!Eof() .AND.;
						ACG->ACG_PREFIX == SE1->E1_PREFIXO .AND.;
						ACG->ACG_TITULO == SE1->E1_NUM .AND.;
						ACG->ACG_PARCEL == SE1->E1_PARCELA .AND.;
						ACG->ACG_TIPO == SE1->E1_TIPO
						nTotCham++
						DbSkip()
					EndDo
				Endif
				If (nTotCham == nChamadas .And. nTipo == 2) .Or. nTipo == 1
					lRet := .T.
				Endif
				nTotCham := 0
				Exit
			Endif
			DbSelectArea("SE1")
			DbSkip()
		EndDo
		DbSelectArea("SK1")
		DbSkip()
	EndDo
EndIf

RestArea(aArea)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062Mala ºAutor ³Vendas Clientes      º Data ³  08/08/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Integracao com o Microsoft Word para criacao dos arquivos  º±±
±±º          ³ de Mala Direta                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º          ³        ³      ³                                            º±±
±±º          ³        ³      ³                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062Mala(cLista,cArquivo,cLabel,cEtiqueta)

Local cWord     := Nil 							// Versao do Office
Local lTmkMala  := ExistBlock("TMKMALA")
Local lRet      := .F.
Local cEnvServ  := GetEnvServer()
Local cIniFile  := GetADV97()
Local cRootPath := GetPvProfString(cEnvServ,"RootPath","",cIniFile)
Local aSArea    := GetArea()
Local cFilialSU6 := xFilial("SU6")

If Subs(cArquivo,1,1) == "\"
	If Rat("\",cRootPath) == Len(Alltrim(cRootPath))
		cRootPath := Substr(cRootPath,1,Len(Alltrim(cRootPath))-1)
	Endif
	cArquivo := cRootPath + cArquivo
Endif

If lTmkMala
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Abre o link com o word³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cWord := OLE_CreateLink()
	
	If ValType(cWord) == "C" .AND. (cWord >= "0")
		OLE_NewFile( cWord,cArquivo)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Funcao que faz o Word aparecer na Area de Transferencia do Windows,     ³
		//³sendo que para habilitar/desabilitar e so colocar .T. ou .F.            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		OLE_SetProperty(cWord, oleWdVisible  ,.F. )
		OLE_SetProperty(cWord, oleWdPrintBack,.F. )
		
		DbSelectArea("SU6")
		DbSetOrder(1)
		MSSeeK(cFilialSU6 + cLista)
		While !Eof() .AND. (SU6->U6_FILIAL == cFilialSU6) .AND. (SU6->U6_LISTA == cLista)
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Executa o ponto de entrada TMKMALA que ira         ³
			//³atualizar as variaveis do documento de mala direta.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			U_TMKMALA(cWord,SU6->U6_ENTIDA,SU6->U6_CODENT,SU6->U6_CONTATO) //Entidade,Cod.Entidade,Contato
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³-Funcao que atualiza os campos da memoria para o Documento, utilizada logo apos a  ³
			//³funcao OLE_SetDocumentVar().														  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			OLE_UpdateFields(cWord)
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³-Funcao que imprime o Documento corrente podendo ser especificado o numero de copias,  ³
			//³podedo tambem imprimir com um intervalo especificado nos parametros "nPagInicial" ate  ³
			//³"nPagFinal" retirando o parametro "ALL".												  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			OLE_PrintFile(cWord,"ALL",,,)
			
			DbSkip()
		EndDo
	
		OLE_CloseFile(cWord)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Funcao que fecha o Link com o Word.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		OLE_CloseLink(cWord)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza o status da lista de mala direta para encerrado.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Tk062Status(cLista,.T.)
		lRet := .T.

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Impressao do arquivo de etiquetas.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
		If cLabel = "1"
			If TmkOk(STR0001) //"Deseja imprimir o arquivo de etiquetas agora?"
				DbSelectArea("SU6")
				DbSetOrder(1)
				MSSeeK(cFilialSU6 + cLista)
				Tk062Etiq(cEtiqueta,cLista)
			Else
				Help(" ",1,"TK061NOIMP")
			Endif
		Endif
	Else
		Help(" ",1,"TK061MALA") //("Problemas na comunicacao com o editor de mala direta")
	Endif
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se nao existir o rdmake padrao de mala avisa o usuario.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Help(" ",1,"NO_IX",,"TMKMALA",3,5)
Endif

RestArea(aSArea)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062Etiq ºAutor ³Vendas Clientes      º Data ³  08/09/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Integracao com o Microsoft Word para criacao dos arquivos  º±±
±±º          ³ de Etiqueta                                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function	Tk062Etiq(cEtiqueta,cLista)

Local cWord           							// Versao do Office
Local lTmkEtiq  := ExistBlock("TMKETIQ")
Local cEnvServ  := GetEnvServer()
Local cIniFile  := GetADV97()
Local cRootPath := GetPvProfString(cEnvServ,"RootPath","",cIniFile)
Local aSArea    := GetArea()

If Subs(cEtiqueta,1,1) == "\"
	If Rat("\",cRootPath) == Len(Alltrim(cRootPath))
		cRootPath := Substr(cRootPath,1,Len(Alltrim(cRootPath))-1)
	Endif
	cEtiqueta := cRootPath + cEtiqueta
Endif

If lTmkEtiq
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Abre o link com o word³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cWord:= OLE_CreateLink()
	
	If ValType(cWord) == "C" .AND. (cWord >= "0")
		OLE_NewFile( cWord,cEtiqueta)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Funcao que faz o Word aparecer na Area de Transferencia do Windows,     ³
		//³sendo que para habilitar/desabilitar e so colocar .T. ou .F.            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		OLE_SetProperty(cWord, oleWdVisible  ,.F. )
		OLE_SetProperty(cWord, oleWdPrintBack,.F. )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Executa o ponto de entrada TMKMALA que ira         ³
		//³atualizar as variaveis do documento de mala direta.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		U_TMKETIQ(cWord,SU6->U6_ENTIDA,SU6->U6_CODENT,SU6->U6_CONTATO,cLista) //Entidade,Cod.Entidade,Contato,Numero da Etiqueta

		Tk062Status(cLista,.F.)
		
		OLE_CloseFile(cWord)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Funcao que fecha o Link com o Word.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		OLE_CloseLink(cWord)
	Else
		Help(" ",1,"TK061MALA") //("Problemas na comunicacao com o editor de mala direta")
	Endif
Else
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se nao existir o rdmake padrao de mala avisa o usuario.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Help(" ",1,"NO_IX",,"TMKETIQ",3,5)
Endif

RestArea(aSArea)

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062StatusºAutor ³Vendas Clientes      º Data ³  08/08/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Atualiza o status da lista para enviado					  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk062Status(cLista, lMala)

Local aArea:= GetArea()
Local cStatus := "2"
Local cFilialSU6 := xFilial("SU6")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiza o status da lista para Enviado.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SU4")
DbSetOrder(1)
If MSSeeK(xFilial("SU4") + cLista)

	// Códigos de Status
	// 1 - Nada impresso
	// 2 - Mala e Etiqueta impressos
	// 3 - Somente mala impressa
	// 4 - Somente Etiqueta impressa
	If SU4->U4_STATUS == "3" .OR. SU4->U4_STATUS == "4"
		cStatus := "2"
	Elseif lMala
		cStatus := "3"
	Elseif !lMala
		cStatus := "4"
	Endif
	
	RecLock("SU4",.F.)
	Replace SU4->U4_STATUS With cStatus
	MsUnLock()
	DbCommit()
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiza o status de cada contato como Encerrado.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SU6")
DbSetOrder(1)
If MSSeeK(cFilialSU6 + cLista,.T.)
	While !Eof() .AND. (cFilialSU6 == SU6->U6_FILIAL) .AND. (cLista == SU6->U6_LISTA)
		RecLock("SU6",.F.)
		Replace SU6->U6_STATUS With "3"
		MsUnLock()
		DbCommit()
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza o historico de marketing.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Tk400Hist(SU6->U6_LISTA,SU6->U6_CODIGO,"8")
		
		DbSkip()
	EndDo
Endif

RestArea(aArea)
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062Imp   ºAutor ³Vendas Clientes      º Data ³  09/08/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Imprime a mala direta a partir do menu   				  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062Imp(cAlias,nReg,nOpc,Param4,cMens,lUseVarMem)

Local oDlg
Local lEtiq    := .F.
Local lMala    := .F.
Local oEtiq
Local oMala
Local cLblEtiq := STR0002  //"Etiquetas"
Local cLblMala := STR0003  //"Mala Direta"
Local cMyAlias := "SU4"

Default cMens := STR0004   //"Escolha abaixo o que deseja imprimir"
Default lUseVarMem := .F.

If lUseVarMem
	cMyAlias := "M"
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o arquivo esta vazio.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lUseVarMem .AND. SU4->(Eof())
	Help(" ",1,"A000FI")

Elseif ((cMyAlias)->U4_FORMA <> "4")// Mala Direta
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Valida se a lista de contatos é mala direta.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Help(" ",1,"TK061NOMA") //("Esta lista nao e mala direta !")

Else
	DEFINE MSDIALOG oDlg FROM 0,0 TO 150,300 TITLE STR0012 PIXEL //"Impressão Mala Direta/Etiquetas"
	
		@ 03,10 SAY cMens OF oDlg PIXEL SIZE 145,25
		
		@ 25,0 TO 26,250 OF oDlg PIXEL
		
		@ 33,10 CHECKBOX oMala VAR lMala SIZE 130,8 PIXEL OF oDlg PROMPT cLblMala
		
		@ 45,10 CHECKBOX oEtiq VAR lEtiq SIZE 130,8 PIXEL OF oDlg PROMPT cLblEtiq
		
		// Códigos de Status
		// 1 - Nada impresso
		// 2 - Mala e Etiqueta impressos
		// 3 - Somente mala impressa
		// 4 - Somente Etiqueta impressa
		If ((cMyAlias)->U4_STATUS == "2")
			lEtiq := .F.
			lMala := .F.
			oEtiq:Disable()
			oMala:Disable()
			cLblEtiq := STR0013 //"Etiquetas (impressas)"
			oEtiq:cTitle := cLblEtiq
			cLblMala := STR0014 //"Mala Direta (Impressa)"
			oMala:cTitle := cLblMala

		Elseif ((cMyAlias)->U4_STATUS == "3")

			If ((cMyAlias)->U4_LABEL == "1")
				lEtiq := .T.
			Else
				lEtiq := .F.
				oEtiq:Disable()
				cLblEtiq := STR0019			//"Etiquetas (Não Imprimir)"
				oEtiq:cTitle := cLblEtiq
			Endif
			lMala := .F.
			oMala:Disable()
			cLblMala := STR0020			//"Mala Direta (Já Impresso)"
			oMala:cTitle := cLblMala

		Elseif ((cMyAlias)->U4_STATUS == "4")
			lEtiq := .F.
			lMala := .T.
			oEtiq:Disable()
			cLblEtiq := STR0021			//"Etiquetas (Já impresso)"
			oEtiq:cTitle := cLblEtiq
		Endif
		
		DEFINE SBUTTON FROM 60,088 TYPE 1 ENABLE OF oDlg ACTION (TK062Imprime( lEtiq, lMala, cMyAlias ),oDlg:End()) //Fecha a tela
		DEFINE SBUTTON FROM 60,120 TYPE 2 ENABLE OF oDlg ACTION oDlg:End() //Fecha a tela
		
	ACTIVATE MSDIALOG oDlg CENTER
Endif

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TK062Imprime ºAutor  ³Vendas Clientes  º Data ³  22/04/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Monta a lista para a impressao da mala direta.              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function TK062Imprime( lEtiq, lMala, cMyAlias )

Local aArea  := GetArea()

If (lEtiq .OR. lMala)
	DbSelectArea("SU6")
	DbSetOrder(1)
	If MSSeeK(xFilial("SU6") + SU4->U4_LISTA)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Executa a impressao de mala direta.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lMala
			Tk062Mala((cMyAlias)->U4_LISTA,(cMyAlias)->U4_MALADIR,(cMyAlias)->U4_LABEL,(cMyAlias)->U4_ETIQUET)  // imprimir a mala
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Imprime a etiqueta caso necessario.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lEtiq
			Tk062Etiq((cMyAlias)->U4_ETIQUET,(cMyAlias)->U4_LISTA)
		Endif
	Else
		Help(" ",1,"TK061CONT") //"Esta lista nao possui contatos para a impressao da mala direta"
	Endif
	
Endif
			
RestArea(aArea)
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062Itens  ºAutor ³Vendas Clientes      º Data ³ 09/08/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Exibe os itens da lista de contatos						  º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Tk062Itens(ExpC1, ExpC2, ExpC3, ExpC4,                     ³±±
±±³          ³            ExpC5, ExpL6, ExpL7, ExpN8)                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo da lista a ser exibida                      ³±±
±±³          ³ ExpC2 = Codigo da Entidade                                 ³±±
±±³          ³ ExpC3 = Mensagem a ser exibida                             ³±±
±±³          ³ ExpC4 = Assunto                                            ³±±
±±³          ³ ExpC6 = Attach                                             ³±±
±±³          ³ ExpL7 = Se deve exibir vazio o acols                       ³±±
±±³          ³ ExpN8 = Opcao referente a opcao                            ³±±
±±³          ³                                                            ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062Itens(cLista,cEntidade,cMensagem,cAssunto,cAttach,lFillEmpty,lEnd, nOpcX)

Local aArea		:= GetArea()
Local nRecAtu   := 0
Local nRecTot   := 0
Local cQryTot   := ""
Local cQuery    := ""
Local bCond //Condicao para o registro ser adicionado no Acols
Local cFilialSU6 := xFilial('SU6')

Default lFillEmpty := .F.

cQuery := "SELECT * "
cQuery += "FROM "+RetSqlName("SU6")+" SU6  "
cQuery += "WHERE SU6.U6_FILIAL='"+cFilialSU6+"' AND "
cQuery += "SU6.U6_LISTA='"+cLista+"' AND "
cQuery += "SU6.D_E_L_E_T_ = ' ' "

// O filtro esta montado e o ORDER BY nao esta na query entao é hora de montar o contator da regua
// Monto o SELECT que conta e aproveito o filtro da outra query
cQryTot := "SELECT COUNT(*) U6TOTAL " + SubStr(cQuery,AT("FROM ",cQuery),Len(cQuery))

cQuery += "ORDER BY "+SqlOrder(SU6->(IndexKey()))

cQryTot := ChangeQuery(cQryTot)
DbSelectArea("SU6")
DbCloseArea()
DbUseArea(.T., "TOPCONN", TCGenQry(,,cQryTot), "SU6", .F., .T.)
nRecTot := SU6->U6TOTAL

DbSelectArea("SU6")
DbCloseArea()

cSeek	:= cFilialSU6 + cLista
cWhile	:= "SU6->U6_FILIAL + SU6->U6_LISTA"
bCond	:= {||IIf(SU6->U6_FILIAL == cFilialSU6 .AND. SU6->U6_LISTA = cLista,.T.,.F.)}

aHeader := {}
aCols	:= {}
FillGetDados(	nOpcX /*nOpcX*/, "SU6"/*cAlias*/, 1/*nIndex*/, cSeek/*cSeek*/,;
				{||&(cWhile)}/*{||&cWhile}*/, bCond/*{|| bCond,bAct1,bAct2}*/, /*aNoFields*/,;
				/*aYesFields*/, /*lOnlyYes*/, cQuery/*cQuery*/, /*bMontAcols*/, lFillEmpty/*lEmpty*/,; 
				/*aHeaderAux*/, /*aColsAux*/, {||TK062SetProc(@nRecAtu, nRecTot)}/*bAfterCols*/, /*bBeforeCols*/,;
				/*bAfterHeader*/, "SU6"/*cAliasQry*/)

FATPDLogUser('TK062ITENS')	// Log de Acesso LGPD

DbSelectArea("SU6")
DbCloseArea()	
RestArea(aArea)
Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    |TK062SetProcºAutor ³Vendas Clientes      º Data ³ 16/01/07  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Incrementa a contador de atividades       				  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Parametros³nExp1: Numero do processo atual.                            ³±±
±±³          ³cExp2: Total de processos a serem executados                ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Lista de Contatos                                     	  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function TK062SetProc(nRecAtu, nRecTot)  

nRecAtu++
IncProc( STR0022 + StrZero(nRecAtu,6) + STR0023 + StrZero(nRecTot,6) )		// "Lendo reg. ", " ate "
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062ExibeArquivo ºAutor ³Vendas Clientes      º Data ³ 10/08/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Exibe o arquivo de mala direta/etiqueta selecionado				º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	                ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062ExibeArquivo(cArquivo)

Local cWord		:= ""
Local cEnvServ	:= GetEnvServer()
Local cIniFile	:= GetADV97()
Local cRootPath	:= GetPvProfString(cEnvServ,"RootPath","",cIniFile)

If Subs(cArquivo,1,1) == "\"
	If Rat("\",cRootPath) == Len(Alltrim(cRootPath))
		cRootPath := Substr(cRootPath,1,Len(Alltrim(cRootPath))-1)
	Endif
	cArquivo := cRootPath + cArquivo
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Abre o link com o word³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cWord	:= OLE_CreateLink()

If (cWord > "0")
	OLE_NewFile( cWord,cArquivo)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Funcao que faz o Word aparecer na Area de Transferencia do Windows,     ³
	//³sendo que para habilitar/desabilitar e so colocar .T. ou .F.            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	OLE_SetProperty(cWord, oleWdVisible  ,.T. )
	OLE_SetProperty(cWord, oleWdPrintBack,.T. )
	
Else
	Help(" ",1,"TK061MALA") //("Problemas na comunicacao com o editor de mala direta")
Endif
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062Excluir ºAutor ³Vendas Clientes      º Data ³ 10/08/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Exclui a lista de contatos								   º±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ Ù±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062Excluir()

Local lRet				:= .F.
Local cU4_Operad	 	:= ""
Local aCodEntidade	:= {}				//Codigos das Entidades existentes no SU6
Local nY				:= 0        		//Controle
Local aArea			:= GetArea("SK1")  // Guarda a area do SK1
Local cFilialSU6		:= xFilial("SU6")
Local cFilialSK1		:= xFilial("SK1")
Local cQuery			:= ""
Local cAliasAux		:= ""      			//Arquivo de trabalho
Local cLista			:= ""

If TmkOk(STR0015)	 		   			//"Confirma a exclusão da lista de contatos ?"

	BEGIN TRANSACTION

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta query para armazenar os codigos das entidades,³
		//³que serao utilizadas para pesquisa  no SK1          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cAliasAux	:= GetNextAlias()
		cLista		:= SU4->U4_LISTA
		BeginSql alias cAliasAux
			SELECT SU6.U6_CODENT CODENT
			  FROM %Table:SU6% SU6
			 WHERE SU6.U6_FILIAL = %xfilial:SU6%
			   AND SU6.U6_LISTA = %exp:cLista%
			   AND SU6.%NotDel%
			 ORDER BY CODENT
		EndSql

		While (cAliasAux)->(! Eof())
			aAdd(aCodEntidade,{(cAliasAux)->CODENT})
			(cAliasAux)->(DbSkip())
		EndDo
		(cAliasAux)->(DbCloseArea())

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Apaga os itens da lista de contatos.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
		DbSelectArea("SU6")
		cQuery += "DELETE FROM "
		cQuery += RetSqlName("SU6")+" "
		cQuery += "WHERE U6_FILIAL='"+cFilialSU6+"' AND "
		cQuery += "U6_LISTA='"+SU4->U4_LISTA+"' "
			
		TcSqlExec(cQuery)
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Apaga o memo da mensagem do EMAIL³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Val(SU4->U4_FORMA) == CROSSPOSTING
			MSMM(SU4->U4_CODMENS,80,,,2)
		Endif
        
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se a lista ja foi finalizada para posterior retirada de tarefa do Operador³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If  SU4->U4_STATUS == "1" // Ativa
			cU4_Operad := SU4->U4_OPERAD
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Apaga o cabecalho da lista de contatos.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("SU4")
		Reclock( "SU4" ,.F.,.T.)
		DbDelete()
		MsUnlock()
		If !EMPTY(cU4_Operad)
			Tk062GrvTarefa(cU4_Operad,2)
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Limpa o codigo do operador no arquivo de referencia - SK1    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nY := 1 to Len(aCodEntidade)
			DbSelectArea("SK1")
			DbSetOrder(4)		// K1_FILIAL+K1_CLIENTE+K1_LOJA+DTOS(K1_VENCREA)
			If MSSeeK(cFilialSK1 + Alltrim(aCodEntidade[nY,1]))
				While	!Eof()										.AND.;
					SK1->K1_FILIAL == cFilialSK1				.AND.;
					SK1->K1_CLIENTE == SubStr(aCodEntidade[nY,1],1,6)	.AND.;
					SK1->K1_LOJA == SubStr(aCodEntidade[nY,1],7,2)
					RecLock("SK1", .F.)
					Replace SK1->K1_OPERAD With ""
					MsUnLock()
					DbSkip()
				EndDo
			Endif
		Next nY
					
		lRet:= .T.
	                                                       
	END TRANSACTION
Endif

RestArea(aArea)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062DesformaºAutor ³Vendas Clientes      º Data ³ 10/08/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Exclui a lista de contatos								   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	           ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062DescForma(cForma)

Local cDesc:= ""

Do Case
	Case (cForma == "1") //Voz
		cDesc:= STR0005 //"Voz"
		
	Case (cForma == "2") //Fax
		cDesc:= STR0006 //"Fax"
		
	Case (cForma == "3") //Cross Posting
		cDesc:= STR0007 //"Cross Posting"
		
	Case (cForma == "4") //Mala Direta
		cDesc:= STR0003 //"Mala Direta"
		
	Case (cForma == "5") //Pendencia
		cDesc:= STR0016 //"Pendência"
		
	Case (cForma == "6") //WebSite
		cDesc:= STR0017  //"WebSite"
EndCase

Return(cDesc)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062VldAuto  ºVendas Clientes             º Data ³ 10/08/01 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Verifica o acols informado pela rotina automatica    	   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	           ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062VldAuto(	nOpc		,aLista		,cSelOper	,cAssunto,;
						cMensagem	,cAttach	,aAttach	,cFiltro,;
						nSaveSX8, nTipo) 

Local lRet 	   := .F.
Local nFor 	   := 0
Local cContato := ""
Local cFilEnt  := ""
Local cEntida  := ""
Local cCodEnt  := ""
Local lGrvCab  := .F.
Local cFilialSU5 := xFilial("SU5")

INCLUI := IIf (nOpc == 3, .T., .F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Valida se o contato ja esta em alguma lista de pendencia.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

For nFor := 1 to Len(aAutoItens)
	
	cContato := (aAutoItens[nFor][ASCAN(aAutoItens[nFor],{|z| z[1] == "U6_CONTATO"} )][2]	)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Valida Codigo do Contato³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Tk062OutraLista(cContato)
		Loop
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Percorre o cadastro de contatos :                    ³
	//³1) Se nao tiver o telefone selecionado gravado SKIP  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SU5")
	DbSetOrder(1)
	If MSSeeK(cFilialSU5+cContato)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Valida o tipo de telefone para contato.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Do Case
			Case (M->U4_FORMA == "1")   	//Voz
				
				If M->U4_TIPOTEL == "1" 	// Residencial
					cTel := SU5->U5_FONE
					
				ElseIf M->U4_TIPOTEL == "2" // Celular
					cTel := SU5->U5_CELULAR
					
				ElseIf M->U4_TIPOTEL == "3" // Fax
					cTel := SU5->U5_FAX
					
				ElseIf M->U4_TIPOTEL == "4" // Comercial 1
					cTel := SU5->U5_FCOM1
					
				ElseIf M->U4_TIPOTEL == "5" // Comercial 2
					cTel := SU5->U5_FCOM2
				Endif
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se o tipo de telefone para a geracao da lista nao³
				//³existir no contato, passa para o proximo contato.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Empty(cTel)
					Loop
				Endif
				
			Case (M->U4_FORMA == "2")   //Fax
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se o contato nao possuir numero de fax passa para o proximo contato.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Empty(SU5->U5_FAX)
					Loop
				Endif
				
				
			Case (M->U4_FORMA == "3")  //Cross-Posting
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se o contato nao possuir e-mail passa para o proximo contato.       ³
				//³Ou se o contato não autorizar a interação via e-mail.               ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				If Empty(SU5->U5_EMAIL) .OR. (SU5->U5_AUTORIZ <> "1")// SIM
					Loop
				Endif
				
			Case (M->U4_FORMA == "4")  //Mala-Direta
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se o contato nao possuir endereco passa para o proximo contato.     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Empty(SU5->U5_END)
					Loop
				Endif
				
		Endcase
	Endif
	
	cFilEnt  := (aAutoItens[nFor][ASCAN(aAutoItens[nFor],{|z| z[1] = "U6_FILENT"} )][2])
	cEntida  := (aAutoItens[nFor][ASCAN(aAutoItens[nFor],{|z| z[1] = "U6_ENTIDA"} )][2])
	cCodEnt  := (aAutoItens[nFor][ASCAN(aAutoItens[nFor],{|z| z[1] = "U6_CODENT"} )][2])
	
	AAdd(aLista,{	xFilial("SU6"),;			    // Filial
					M->U4_LISTA,;       			// Lista
					GetSxeNum("SU6","U6_CODIGO"),;  // Codigo
					cFilEnt,;  						// Filial da Entidade
					cEntida,;  						// Chave da entidade
					cCodEnt,;						// Codigo da Entidade
					"1",;							// Origem da Interacao
					cContato,;  					// Codigo do Contato
					M->U4_DATA,;					// Data
					CriaVar("U6_HRINI",.T.),; 		// Hora Inicial
					CriaVar("U6_HRFIM",.T.),; 		// Hora Final
					"1"})					  		// Status da Interacao
					
Next nFor

lRet	:= Tk062Grava(nOpc, aLista, @cAssunto, @cMensagem,;
    	              @cAttach, @aAttach, @cFiltro, @cSelOper,;
    	              @lGrvCab, -1, nSaveSX8, nTipo)

CursorArrow()
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk062RetU4Cmp ºVendas Clientes             º Data ³ 05/04/02 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Retorna os campos da MsMGet que poderao ser alterados.      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062RetU4Cmp(nOpc,cAlias,nReg)

Local aArea 	:= GetArea()
Local aCampos 	:= {}
Local cCampos   := "U4_OPERAD;U4_MALADIR;U4_ETIQUET,U4_CODCAMP;U4_DESCCAM;U4_SCRIPT;U4_EVENTO"	 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Em caso de alteracao verifica  se a lista ja foi executada. Se o retorno for³
//³positivo a campanha nao podera ser alterada.                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpc == ALTERAR
	If Tk062ValAlt(cAlias,nReg,nOpc)
		cCampos   := "U4_OPERAD;U4_MALADIR;U4_ETIQUET,U4_CODCAMP;U4_DESCCAM;U4_SCRIPT;U4_EVENTO"
	Else
		cCampos   := "U4_OPERAD;U4_MALADIR;U4_ETIQUET"
	Endif
Endif

DbSelectArea("SX3")
DbSetOrder(1)
If MSSeeK("SU4")
	While !Eof() .AND. SX3->X3_ARQUIVO == "SU4"
		If X3USO(SX3->X3_USADO)
	  		//Carrega todos os campos exceto o U4_CONFIG
			If nOpc <> 4
				If !(ALLTRIM(Upper(SX3->X3_CAMPO)) == "U4_CONFIG")
					Aadd( aCampos, SX3->X3_CAMPO )
				Endif
			Else
				If (ALLTRIM(Upper(SX3->X3_CAMPO)) $ cCampos)
					Aadd( aCampos, SX3->X3_CAMPO )
				Endif
			Endif
		Endif
		DbSkip()
	End
Endif

RestArea(aArea)
Return(aCampos)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062SELOPERºAutor  ³Vendas Clientes     º Data ³  07/13/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Efetua a selecao automatica do operador que recebera a tarefa.º±±
±±º          ³                                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	            ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062SelOper()

Local aArea 		:= GetArea()
Local aOper 		:= {}       // Array com operadores
Local aOper2		:= {}       // Operadores selecionados
Local aSortOper 	:= {}   // Ordena operadores por ordem crescente de tarefas.
Local nOper 		:= 1
Local nForOp		:= 0
Local oDlg			:= Nil
Local oOpers		:= Nil
Local oOk        	:= LoaDbitmap(GetResources(),"LBOK")	// LOGO Verde
Local oNo        	:= LoaDbitmap(GetResources(),"LBNO")
Local cFilialSU7 	:= xFilial("SU7")
Local aPDCols		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega os operadores para a selecao.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SU7")
DbSetOrder(1)
MSSeeK(cFilialSU7)

While (!Eof()) .AND. cFilialSU7 == SU7->U7_FILIAL
    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Para a Lista de Cobranca sao selecionados Operadores validos com Rotina = TELECOBRANCA e TODOS³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Val(SU7->U7_VALIDO) == VALIDO .AND. (SU7->U7_TIPOATE == "3" .OR. SU7->U7_TIPOATE == "4")
		Aadd(aOper,{.F.,SU7->U7_COD,SU7->U7_NOME,SU7->U7_TAREFA}) //LOGO,codigo,Nome,Qtd de Tarefas
	Endif
	DbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se nao for rotina automatica apresenta as telas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lA062Auto
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Esta tela permite a selecao dos operadores que participarao da avaliacao³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aOper)> 0
		
		DEFINE MSDIALOG oDlg TITLE STR0008 FROM 00,00 TO 250,500 PIXEL OF oMainWnd  //"Operadores - Avaliação/Gravação automática"
		
		@02,02 TO 120,210 LABEL "" PIXEL OF oDlg
		@05,04 LISTBOX oOpers FIELDS ;
										HEADER ;
										"",;
										STR0009,;		//Codigo
										STR0010,;		//Nome
										STR0011; 		//Listas
										ON DbLCLICK (aOper[oOpers:nAt,1]:= !aOper[oOpers:nAt,1], oOpers:Refresh());
										ON CHANGE (nOper:= oOpers:nAt) SIZE 205,115 OF oDlg PIXEL NOSCROLL
		
		oOpers:SetArray(aOper)
		oOpers:bLine:={||{IIF(aOper[oOpers:nAt,1],oOk,oNo),;
		                  aOper[oOpers:nAt,2],;
		                  aOper[oOpers:nAt,3],;
						  aOper[oOpers:nAt,4]}}
		
		If FATPDActive() .And. FTPDUse(.T.)
			aPDCols := {"","","U7_NOME",""}    
			oOpers:aObfuscatedCols := FATPDColObfuscate(aPDCols)
		Endif
		oOpers:Refresh() 
		
		DEFINE SBUTTON FROM 05,215 TYPE 1 ENABLE OF oDlg ACTION oDlg:End()
		
		FATPDLogUser('TK062SELOP')	// Log de Acesso LGPD
		ACTIVATE MSDIALOG oDlg CENTERED
		
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Carrega Array com os operadores selecionados³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nForOp := 1 To Len(aOper)
		IF aOper[nForOp][1]   // Avalia se foi selecionado
			Aadd(aOper2,aOper[nForOp])
		Endif
	Next nForOp
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se nenhum operador for selecionado todos serao considerados³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aOper2) == 0
		aOper2 := aClone(aOper)
	Endif
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Rotina automatica. Nao ha interface. Todos os operadores serao selecionados    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aOper2 := aClone(aOper)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ordena array.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aOper2)>0
	aSortOper := ASORT(aOper2,,, { |x, y| x[4] < y[4] }) //CRESCENTE
Else
	Aadd(aSortOper,{"",""})
Endif

RestArea(aArea)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ O primeiro item do array sera o operador que possui a menor quantidade de tarefas.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Return (aSortOper[1][2])

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk062GrvTarefaºAutor  ³Vendas Clientes     º Data ³  07/15/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Grava a tarefa no SU7 para o operador.                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Logico                                                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpC1: Codigo do operador                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º   DATA   ³ Programador   ³Manutencao Efetuada                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º          ³               ³                                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	              ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062GrvTarefa(cSelOper,nOpX)

Local lRet := .F.
Local aSArea := GetArea()
Local nVal   := 0

DbSelectArea("SU7")
DbSetOrder(1)
IF MSSeeK(xFilial("SU7")+ cSelOper)
    nVal := SU7->U7_TAREFA
    
    If nOpX == 1
 	   nVal++
	Else
	   If nVal > 0
	      nVal--
	   Endif
	Endif
	
	RecLock("SU7",.F.)
	Replace U7_TAREFA  With  nVal
	MsUnlock()
	DbCommit()
	lRet := .T.
Endif
RestArea(aSArea)

Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk062Vld  ºAutor  ³Vendas Clientes     º Data ³  30/07/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida o campo U4_MALA para quando a opção for diferente de º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
/*
 Funcao Tk061VLD esta no fonte Tmka061 da Lista de Contatos padrao.
*/
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk062Help ºAutor  ³Vendas Clientes     º Data ³  30/07/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Exibe todas as inconformidades de dados que nao permitiram aº±±
±±º          ³geracao da Lista.                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062Help(cText,nHandle)

Local cPath		:= AllTrim(GetTempPath())
Local nErase	:= 1
Local lHtml		:= FindFunction("ISHTML") .AND. IsHTML()		// Indica se e remote HTML
Local lSrvUnix	:= IsSrvUnix()								//Verifica se o server é Linux
Local cOrigem	:= ""
Local cArqLog	:= "TMKSU4LISTA_" + AllTrim(cEmpAnt) + AllTrim(cFilAnt) + ".LOG"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se o SmartClient for HTML, bloqueia o log (poque nao consegue acessar diretorio local) |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If lHtml
	Aviso(STR0026,STR0027,{"OK"},2)
	Return(NIL)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Apago arquivo antigo do temp³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If File(cPath + cArqLog)
	nErase := FERASE(cPath + cArqLog)

	While nErase = -1
		nErase := FERASE(cPath + cArqLog)
	EndDo
Endif

If lSrvUnix
	cOrigem := Lower(GetSrvProfString("Startpath","") + cArqLog)
	CpyS2T( cOrigem ,cPath, .F.)
Else
	CpyS2T(GetSrvProfString("Startpath","") + cArqLog, cPath, .T.)
EndIf

Help(" ",1,"TK062OCORR")
WinExec("NOTEPAD "+ cPath + cArqLog, 1)
cText := ""
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062SETFILTºAutor  ³Vendas Clientes     º Data ³  08/13/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Executa o filtro selecionado.                                º±±
±±º          ³                                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	            ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062SetFilt(cFiltro,cEntidade)

Local aArea := GetArea()

DbSelectArea(cEntidade)
DbCloseArea()
ChkFile(cEntidade)

Set Filter To &(cFiltro)

RestArea(aArea)
Return Nil 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk062RetEveºAutor  ³Vendas Clientes     º Data ³  08/26/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Consulta customizada de Eventos.  Utilizada para verificar a º±±
±±º          ³amarracao que existe no SUW.                                 º±±
±±º          ³Exibe somente os eventos amarrados a campanhas.              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Codigo do Evento selecionado.                                º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso		 ³ Lista de Inadimplentes (TeleCobranca)		   	           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062RetEve()

Local nEvento    := 0		//Evento selecionado no ListBox
Local oDlgEve				//Objeto do dialogo
Local aEventos   := {}		//Array com eventos para exibicao na ListBox
Local lRet       := .F.		//Retorno da funcao
Local cFilialSUW := xFilial("SUW")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Os dados de Campanha e Script devem estar preenchidos para o relacionamento.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty(M->U4_CODCAMP) .OR. Empty(M->U4_SCRIPT)
	Help(" ",1,"TMK061EVEN")//"A Campanha ou o Script não foram selecionados. Para a seleção do Evento é necessário o preenchimento dos campos."
	Return (lRet)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega o array de eventos.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SUW")
DbSetOrder(1)
If MSSeeK(cFilialSUW+ M->U4_CODCAMP+ M->U4_SCRIPT )
	While !(SUW->(EOF())) .AND. (cFilialSUW == SUW->UW_FILIAL) .AND. (SUW->UW_CODCAMP == M->U4_CODCAMP) .AND. (SUW->UW_CODSCRI == M->U4_SCRIPT)
    	Aadd(aEventos,{SUW->UW_CODEVE, SUW->UW_DESCEVE})
		SUW->(DbSkip())
	End
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se foram encontrados eventos³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aEventos) == 0
	MsgInfo(STR0025) //"Nenhum evento foi encontrado para a campanha selecionada."
	Return (lRet)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Dialog.                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlgEve FROM  00,00 TO 230,440 TITLE STR0018 PIXEL  //"Consulta de Eventos"
	
	@ 05,05 LISTBOX oLbx FIELDS ;
		    HEADER ;
			    STR0009,;   //"C¢digo"
			    STR0010;	//"Nome"
		    SIZE 180,100  NOSCROLL OF oDlgEve PIXEL ;
	        ON DBLCLICK(nEvento := oLbx:nAt,oDlgEve:End())
	       	
			oLbx:SetArray(aEventos)
			oLbx:nAt:= Len(aEventos)
			oLbx:bLine:={||{aEventos[oLbx:nAt,1],;  //C¢digo
							aEventos[oLbx:nAt,2] }} //Evento

	DEFINE SBUTTON FROM 05,190 TYPE 1  	ENABLE OF oDlgEve ACTION (nEvento:=oLbx:nAt ,oDlgEve:End())
	DEFINE SBUTTON FROM 20,190 TYPE 2  	ENABLE OF oDlgEve ACTION (oDlgEve:End())

ACTIVATE MSDIALOG oDlgEve CENTERED

If nEvento > 0
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Posiciona no registro correto para ser atualizado o campo .³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lRet := .T.
	DbSelectArea("ACD")
	DbSetOrder(1)
	MSSeeK(xFilial("ACD") + Alltrim(aEventos[nEvento,1]))
	
Endif
 
Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk062ContDescºAutor  ³Vendas Clientes     º Data ³  30/07/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a descricao do contato. Objetivo Salvar a area anteriorº±±
±±º          ³                                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	             ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062ContDesc(cContato)

Local aArea := GetArea()
Local cDesc	:= ""

SU5->(DbSetOrder(1))
If	SU5->(MsSeek(xFilial("SU5")+cContato))
	cDesc	:= Alltrim(SU5->U5_CONTAT)
	FATPDLogUser('TK062CONTD')	// Log de Acesso LGPD
EndIf

RestArea(aArea)
Return(cDesc+" ")

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TK062VALSUCºAutor  ³Vendas Clientes    º Data ³  11/20/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida se existem registros pendentes do Atend. TeleMarke- º±±
±±º          ³ting. Caso exista este contato não sera incluso na lista.   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TK062VALSUC(cContato, cFilialSUC)

Local lRet 		:= .T.
Local lCodeBase 	:= .T.

Default cFilialSUC := xFilial("SUC")

If lSQL // se for dbAccess
	If Type("cTblSUC")=="C" // Private declarado na TMKA062
		If (cTblSUC)->(MsSeek(cContato))
			lRet := !( (cTblSUC)->TOTALREG > 0 ) // se encontou mais de um registro com UC_STATUS='2'
		Else
			lRet := .T.
		EndIF
		lCodeBase := .F. // Já foi validado, não precisa passar pela validacao via codebase
	EndIf
EndIf

If lCodeBase
	SUC->(DbSetOrder(6))
	If 	SUC->(MsSeek(cFilialSUC + cContato))
		While SUC->(!Eof()) .AND. cFilialSUC == SUC->UC_FILIAL .AND. cContato == SUC->UC_CODCONT
			If Val(SUC->UC_STATUS) == 2
				lRet  := .F.
				Exit
			Endif
			SUC->(DbSkip())
		EndDo
	Endif
EndIf
Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk062WriteºAutor  ³Vendas Clientes     º Data ³  27/06/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³  Avalia o tamanho do texto de log, armazena em disco e     º±±
±±º          ³ limpa variáel para nao existir estouro de pilha.           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TK062Write(cText,nHandle,lFim)

DEFAULT lFim := .F.

//Se tamanho do texto for maior que 200 bites grava no arquivo texto
If ((Len(cText)>200) .OR. (lFim))
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Escreve arquivo de Log.                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FWrite(nHandle, cText)
	cText := ""
Endif

If lFim
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Fecho arquivo para poder movelo.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FClose(nHandle)
Endif

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk062AtivaºAutor  ³Vendas Clientes     º Data ³  08/18/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Controla a geracao de listas para OutBound.Listas Ativas    º±±
±±º          ³                                                            º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cAlias: Alias da Rotina                  	              	  ³±±
±±³          ³nReg:   Registro posicionado          	              	  ³±±
±±³          ³nOpc:   Opcao selecionada             	              	  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062Ativa(cAlias,nReg,nOpc)

Local lTk062Out  := ExistBlock("TK062OUT") // Ponto de entrada para geracao de listas OutBound

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se esta lista ja foi trabalhada. Caso contrario ela podera³
//³ser utilizada.                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Tk062ValAlt(cAlias,nReg,nOpc)
	Help(" ",1,"TK062LSTEX")
	Return(.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona dicionario³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea(cAlias)
DbSetOrder(1)
MsGoTo(nReg)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Executa o ponto de entrada e aguarda retorno.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lTk062Out
	If U_Tk062OUT(SU4->U4_LISTA)// RdMake
		Help(" ",1,"TK062SUCES")
	Else
		Help(" ",1,"TK062ERROR")
	Endif
Else
	Help(" ",1,"NO_IX",,"TK062OUT",3,5)
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK062ArqOutºAutor ³Vendas Clientes     º Data ³  08/18/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Gera arquivo de comunicacao para ligações preditivas        º±±
±±º          ³                                                            º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cLista: Codigo da Lista de Contatos      	              	  ³±±
±±³          ³cDir  : Endereco onde sera gravado o arquivo texto    	  ³±±
±±³          ³cFile : Nome do arquivo texto. Este deve conter a extensao. ³±±
±±³          ³lAgente:Identificacao do Agente no arquivo. True-Identifica.³±±
±±ÌÍÍÍÍÍÍÍÍ	ÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062ARQOUT(cLista,cDir,cFile,lAgente)

Local lRet 		:= .T. 	// Retorno da Funcao
Local nHandle	:= 0	// Arquivo txt
Local nErase	:= 1	// Controle de delecao
Local cTpFoneU4	:= ""   // Variavel de auxilio para geracao das informacoes
Local cFone		:= ""	// Variavel de auxilio para geracao das informacoes
Local nCont		:= 0	// Contador
Local aUser   	:= {}	// Array com os dados do usuário
Local cAgentID	:= "" 	// Id do Agente para arquivo // Ramal do configurador.
Local aFone		:= {}	// Array com os telefones para contato
Local cComplTel := ""	// Complemento para discagem externa
Local cFilialSU6 := xFilial("SU6")
Local cFilialSU5 := xFilial("SU5")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o arquivo ja existe e nao permite uma nova geracao.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If File(cDir+cFile)
	Help(" ",1,"TK062ARQUI",,cDir + cFile,3,0)
	Return .F.
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Primeiramente o arquivo sera criado no SigaAdv e posteriormente³
//³copiado para o diretorio informado pelo usuário.               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//Verifica se o arquivo esta no SigaAdv
If File(cFile)
	// Limpa SigaAdv
	nErase := FERASE(cFile)
	
	While nErase = -1
		nErase := FERASE(cFile)
	EndDo
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria novo Arquivo  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nHandle := FCREATE(cFile,1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Detalhes da informacao     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SU4")
DbSetOrder(1)
If MSSeeK(xFilial("SU4")+cLista)

	Do Case
		Case SU4->U4_TIPOTEL == "1"
			cTpFoneU4	:= "HomePhone"
		Case SU4->U4_TIPOTEL == "2"
			cTpFoneU4	:= "Mobile"
		Case SU4->U4_TIPOTEL == "3"
			cTpFoneU4	:= "NoContactType"
		Case SU4->U4_TIPOTEL == "4"
			cTpFoneU4	:= "DirectBusinessPhone"
		Case SU4->U4_TIPOTEL == "5"
			cTpFoneU4	:= "DirectBusinessPhone"
	EndCase
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Avalia o complemento do Telefone. Caso seja necessario utilizar o campo U0_EXTERNA³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cComplTel := TkPosto(SU4->U4_OPERAD,"U0_EXTERNA")
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Busca o login do agente com base no operador.			³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If lAgente
		DbSelectArea("SU7")
		DbSetOrder(1)
		If MSSeeK(xFilial("SU7")+SU4->U4_OPERAD)
			cChave := SU7->U7_CODUSU
		Endif

		PswOrder(1)
		PswSeek(cChave)
		aUser:= PswRet(1)
		If Len(aUser) > 0
			cAgentID:= aUser[1][20]
		Endif
	Endif
    		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Busca informacoes da Lista para geração do Arquivo Texto.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SU6")
	DbSetOrder(1)
	If MSSeeK(cFilialSU6+cLista)
		While (	 !EOF()							 .AND.;
				 cFilialSU6==SU6->U6_FILIAL .AND.;
				 cLista==SU6->U6_LISTA )
                                    
			DbSelectArea("SU5")
			DbSetOrder(1)
			If MSSeeK(cFilialSU5 + SU6->U6_CONTATO)
				aFone:= SGReadTel("SU5",SU6->U6_CONTATO)
				Do Case
					Case SU4->U4_TIPOTEL == "1"
						cFone:=	ALLTRIM(cComplTel) + aFone[1]
					Case SU4->U4_TIPOTEL == "2"
						cFone:= ALLTRIM(cComplTel) + aFone[2]
					Case SU4->U4_TIPOTEL == "4"
						cFone:=	ALLTRIM(cComplTel) + aFone[3]
					Case SU4->U4_TIPOTEL == "5"
						cFone:=	ALLTRIM(cComplTel) + aFone[4]
				EndCase

			Endif

		    // Volta laco para SU6
			DbSelectArea("SU6")

			nCont++

			FWrite(nHandle,;
					"record_id="+AllTrim(STR(nCont))					+;//Transform(nCont,"999999")
					"|phone="+Alltrim(cFone)							+;
					"|phone_type="+"DirectBusinessPhone"				+;//Alltrim(cTpFoneU4)
					"|record_type="+"General"							+;
					"|record_status="+"Ready"							+;
					"|call_result="+"Unknown"							+;
					"|attempt=0"										+;
					"|dial_sched_time="			 						+;
					"|call_time="										+;
					"|daily_from=" + SU6->U6_HRINI						+;
					"|daily_till=" + SU6->U6_HRFIM						+;
					"|tz_dbid=-10800"    		 						+;
					"|campaign_id="	    		 						+;
					"|agent_id="+cAgentID  		 						+;
					"|chain_id="+AllTrim(STR(nCont))					+;
					"|chain_n=0"	    		 						+; // envio sempre um unico telefone
					"|ID_Cliente="	    		 						+;
					CRLF)
			DbSkip()
		EndDo
	Endif
Endif

//Zera contador.
nCont	:= 0

// Fecha arquivo para copia.
FClose(nHandle)
__CopyFIle(cFile,cDir+cFile)

FATPDLogUser('TK062ARQOU',2)	// Log de Acesso LGPD

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TkValAlt  ºAutor  ³Vendas Clientes     º Data ³  07/07/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³  Verifica se a Lista ja foi executada, caso positivo o     º±±
±±º          ³ cadastro  nao sera aberto para alteracao.                  º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cAlias: Alias da Rotina                  	              	  ³±±
±±³          ³nReg:   Registro posicionado          	              	  ³±±
±±³          ³nOpc:   Opcao selecionada             	              	  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso		 ³ Lista de Inadimplentes (TeleCobranca)		   	          ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062ValAlt(cAlias,nReg,nOpc)

Local lRet			:= .T.    					// Flag que define se pode alterar.
Local aArea		:= GetArea("SU4")
Local cLista		:= ""
Local cAliasAux	:= ""

DbSelectArea(cAlias)
DbSetOrder(1)
MsGoTo(nReg)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Nessa rotina somente as Listas de Cobranca pode ser manipuladas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Val(SU4->U4_TIPO) == COBRANCA
	cAliasAux	:= GetNextAlias()
	cLista		:= SU4->U4_LISTA
	BeginSql alias cAliasAux
		SELECT count(*) REGS
		  FROM %Table:SU6% SU6
		 WHERE SU6.U6_FILIAL = %xfilial:SU6%
		   AND SU6.U6_LISTA = %exp:cLista%
		   AND SU6.U6_STATUS <> '1'
		   AND SU6.%NotDel%
	EndSql

	lRet := !( (cAliasAux)->REGS > 0 )
	(cAliasAux)->(DbCloseArea())
Else
	Help(" ",1,"TK062NOCOB") // Essa lista nao e de Cobranca
	lRet := .F.
Endif

RestArea(aArea)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TK062Legenda ºAutor  ³Vendas Clientes  º Data ³  23/07/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Legendas dos browses de Lista de Cobrancas                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CALL CENTER                                                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk062Legenda()

BrwLegenda(cCadastro,STR0024,{{"BR_VERDE",    STR0028},;	//"Legenda"##"Vendas"
                              {"BR_AZUL",     STR0029},;	//"Marketing"
                              {"BR_VERMELHO", STR0030}})	//"Cobrança"
Return(.T.)

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} FATPDColObfuscate
    @description
    Verifica se a coluna de um grid deve ser ofuscado, tendo como base uma lista de
    campos, esta função deve utilizada somente após a inicialização das variaveis 
    atravez da função FATPDLoad.
	Remover essa função quando não houver releases menor que 12.1.27

    @type  Function
    @author Squad CRM & Faturamento
    @since  05/12/2019
    @version P12.1.27
    @param cField, Caractere, Campo que sera validado
    @param cSource, Caractere, Nome do recurso que buscar dados protegidos.

    @return lObfuscate, Lógico, Retorna se o campo será ofuscado.
    @example FATPDIsObfuscate({"A1_COD","A1_NOME","A1_CGC"})
/*/
//-----------------------------------------------------------------------------------
Static Function FATPDColObfuscate(aFields, cSource)  
    
	Local aPDColObf	:= {}

    If FATPDActive()
		aPDColObf := FTPDColObfuscate(aFields, cSource)  
    EndIf 

Return aPDColObf

//-----------------------------------------------------------------------------
/*/{Protheus.doc} FATPDLogUser
    @description
    Realiza o log dos dados acessados, de acordo com as informações enviadas, 
    quando a regra de auditoria de rotinas com campos sensíveis ou pessoais estiver habilitada
	Remover essa função quando não houver releases menor que 12.1.27

   @type  Function
    @sample FATPDLogUser(cFunction, nOpc)
    @author Squad CRM & Faturamento
    @since 06/01/2020
    @version P12
    @param cFunction, Caracter, Rotina que será utilizada no log das tabelas
    @param nOpc, Numerico, Opção atribuída a função em execução - Default=0

    @return lRet, Logico, Retorna se o log dos dados foi executado. 
    Caso o log esteja desligado ou a melhoria não esteja aplicada, também retorna falso.

/*/
//-----------------------------------------------------------------------------
Static Function FATPDLogUser(cFunction, nOpc)

	Local lRet := .F.

	If FATPDActive()
		lRet := FTPDLogUser(cFunction, nOpc)
	EndIf 

Return lRet  

//-----------------------------------------------------------------------------
/*/{Protheus.doc} FATPDActive
    @description
    Função que verifica se a melhoria de Dados Protegidos existe.

    @type  Function
    @sample FATPDActive()
    @author Squad CRM & Faturamento
    @since 17/12/2019
    @version P12    
    @return lRet, Logico, Indica se o sistema trabalha com Dados Protegidos
/*/
//-----------------------------------------------------------------------------
Static Function FATPDActive()

    Static _lFTPDActive := Nil
  
    If _lFTPDActive == Nil
        _lFTPDActive := ( GetRpoRelease() >= "12.1.027" .Or. !Empty(GetApoInfo("FATCRMPD.PRW")) )  
    Endif

Return _lFTPDActive
