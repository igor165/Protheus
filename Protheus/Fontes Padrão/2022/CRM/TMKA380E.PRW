#INCLUDE "TMKA380E.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWIZARD.CH"
#INCLUDE "TMKDEF.CH"


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TMKA380E  ºAutor  ³Vendas Clientes     º Data ³  01/12/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Agenda do operador. Marketing Ativo.                        º±±
±±º          ³Tela de execucao do script dinamico.                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Call Center                                                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380Script   ³Autor  ³Vendas Clientes        ³Data  ³  19/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Tela de execucao de Scripts dinamicos do Modulo Call Center.       º±±
±±º          ³Utilizada na Agenda do Operador e Atendimento Receptivo.           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Ap8- Call Center - Agenda do Operador e Atendimento Receptivo      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk380Script( 	nOpc		,cCampanha	,cScript	,cCodCont	,;
						cEntidade	,cChave		,cAlias		,nReg		,;
						cOperador	,cLista		,cSU6Cod	,cCodAte	,;
						aParScript )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Variaveis utilizadas por todo o fonte.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local lRet				:= .F.						// Retorno da funcao
Local l380				:= .F.						// Flag para controlar quem esta executando a rotina
Local nCont 			:= 0						//Contador para criacao dos paineis.
Local aSizeDlg      	:= MsAdvSize(.T.,.F.,400)	//Array com as dimensoes da tela principal.
Local aBtn          	:= Array(7)					//Array com os botoes
Local oBar               							//Toolbar do Script
Local cTipTel       	:= ""                       // Tipo do Telefone - como DEFAULT vai pegar o "telefone comercial 1" do contato para exibir na tela 
Local cTel       		:= ""						//Telefone do Contato
Local cDDD       		:= ""						//DDD do Telefone do Contato
Local cPais       		:= ""						//Pais do Telefone do Contato
Local cDescChave   		:= ""						//Nome da Chave da Entidade
Local cDesEntida		:= ""						//Descricao da Entidade
Local cScore			:= CriaVar("UP_SCORE",.F.)  //Pontuacao
Local aObjects			:={}						//Array com os objetos da tela
Local aInfo				:={}						//Array para utilizacao do MsAdvSize
Local aPosObj			:= {}						//Array com as coordenadas para posicionamento dos objetos
Local cDescCont			:= ""						//Descricao do Contato
Local chTitle			:= ""						//Mensagem wizard. Descricao da campanha.
Local aPerguntas		:= {}						//Array com o questionario
Local oScroll   									//Scroll onde serao apresentadas as respostas                                                       
Local oFont1										//Tipo de Fonte da pergunta
Local cFormato			:= ""						//Formato do Script
Local aControl			:= {} 						//Array com as informacoes das respostas.
Local aMemos			:= {}						//Array com os memos da tela de scripts dinamicos 
Local bVarM				:= {||}						//Bloco de codigo para criacao de memo.
Local aMRes				:= {}						//Array com as inf. dos memos das respostas.
Local nLastPanel		:= 1						//Ultimo painel apresentado.
Local nPos				:= 0						//Posicao do array aParScript a ser modificada
Local oScrEnd										// ScrollBox da tela
Local oMemoFim			
Local cMemoFim			:= ""						//Resultado do Script.		
Local cCodigo			:= ""						
Local cCodLig			:= ""
Local cFixHeader		:= ""						// Texto fixo para apresentacao das mensgens do wizard.

// Novos Objetos Criados
Local oSayObsPerg									// Objeto say da Str da Observação da pergunta
Local aScroll			:= {}						// Array de Scroll das telas do wizard
Local aMemoPerg			:= {}						// Array do Memo para conter o enunciado de cada pergunta
Local aPanel			:= {}

Local aCoord 			:= {}						// Array auxiliar com as coordenadas de criacao de novos objetos do script
Local aPanelScore 		:= {} 						// Array para controle do score conforme as telas navegadas para o Tipo Atendimento

Local lUseTreeView		:= GetMv("MV_TK380T",,.F.) // Determina se ira mostrar o tree para facilitar o atendimento
Local aPosWizard		:= {}
Local oTree				:= Nil
Local aTreeXPanel		:= {}
Local nX				:= 0

DEFAULT cCampanha  		:= ""						// Codigo da Campanha
DEFAULT cScript  		:= ""						// Codigo do Script
DEFAULT cCodCont 		:= ""                		// Codigo do Contato
DEFAULT cEntidade		:= ""						// Entidade
DEFAULT cChave   		:= ""						// Chave da Entidade
DEFAULT cAlias   		:= "SU5"					// Alias Default do Contato para a execucao do atendimento
DEFAULT nReg	 	  	:= 0						// Registro do arquivo para a execucao do atendimento
DEFAULT cLista          := ""
DEFAULT cSU6Cod			:= ""
DEFAULT aParScript      := {}						// Array com as informacoes para alteracao e gravacao pelo atendimento receptivo.
           
DEFINE FONT oFont1   NAME "Tahoma" SIZE 0,15 BOLD   // Fontes para os Say dos STR - Negrito 
DEFINE FONT oFont2   NAME "Tahoma" SIZE 0,14 		// Fontes para os Say dos STR	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Valida quem esta executando essa funcao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Upper(FUNNAME()) == "TMKA380"
	l380 := .T.
Endif	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tipo do Telefone - como DEFAULT vai pegar o "telefone comercial 1" do contato para exibir na tela³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cTipTel  := Posicione("SU4",1,xFilial("SU4") + cLista,"U4_TIPOTEL")  

If Empty(Alltrim(cTipTel))
	cTipTel := "1"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega as perguntas e respostas			   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Tk380LeScript(aPerguntas,@cFormato,cScript,cLista)
	Help(" ",1,"SEMLIGACAO")
	Return(lRet)
Endif

aPosWizard := {aSizeDlg[7],000,aSizeDlg[6]-60,aSizeDlg[5]}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calculo do posicionamento as perguntas e respostas ³
//³nos paineis.                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aObjects := {}
AAdd( aObjects, { 100, 019, .T., .F. } )
AAdd( aObjects, { 100, 040, .T., .F. } )
AAdd( aObjects, { 100, 020, .T., .F. } ) 
AAdd( aObjects, { 100, 065, .T., .F. } )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³O calculo do posicionamento dos objetos sera feito depois     ³
//³da criacao do wizard devido a necessidade da criacao do objeto³
//³panel.                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Informacoes da tela.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cCodCont)
	cDescCont := Left(TkDadosContato(cCodCont,6,.T.),26)
	cDescChave:= Left(TkEntidade(cEntidade,cChave,1,,.T.),25)
	SX2->( DbSetOrder( 1 ) )
	SX2->( MsSeek( cEntidade ) )
	cDesEntida := Capital( X2NOME() )
	cDDD      := AllTrim(TkDadosContato(cCodCont,8,.T.))
	cPais     := AllTrim(TkDadosContato(cCodCont,9,.T.))
	cTel      := cPais + cDDD + AllTrim(TkDadosContato(cCodCont,Val(cTipTel),.T.))
Endif
                                                                                                                   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Titulo da tela para Script		  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
chTitle := STR0001+ cCampanha+"  "+ Posicione("SUO",1,xFilial("SUO") + cCampanha,"UO_DESC")   //"Campanha: "

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Mensagem do Header fixa para concatenacao com o Score.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFixHeader:= STR0002+Upper(cDescCont)+Space(10)+STR0003+Upper(cDescChave)+" - "+cDesEntida+STR0004+cTel+Space(15)+STR0018

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Declaracao do objeto Wizard³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oWizard:= APWizard():New( 	chTitle+CRLF+STR0002+UPPER(cDescCont)+Space(10)+STR0003+UPPER(cDescChave)+" - "+cDesEntida+STR0004+cTel,; //"Contato: ","Entidade: ","      Fone: "
							,; 
						  	STR0005,; //"Resposta de Scripts Dinâmicos Call Center"
							 ,;
							 {||Tk380Next(	aPerguntas	,cFormato	,@oWizard	,aControl	,;
							 				@nLastPanel	,@oMemoFim	,@cMemoFim	,@cScore	,;
							 				cFixheader	,@aPanelScore )},; 
							 {||.F.},.T., , , .T.,aPosWizard ,Iif (lUseTreeView,30,0))
            
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calculo de coordenadas dos objetos da tela de acordo com as coordenada do painel.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aInfo   := { 0, 0, oWizard:GetPanel(1):nClientHeight, oWizard:GetPanel(1):nClientWidth, 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Criacao do arrays dos objetos variaveis          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Aadd(aScroll   ,NIL)     	// Array do Scroll de cada tela do Wizard
		Aadd(aMemos    ,{NIL, NIL}) // Array do memos para conter a observasao da Pergunta
		Aadd(aMemoPerg ,{NIL,NIL}) 	// Array do Memo para conter o enunciado de cada pergunta
		Aadd(aMRes     ,{NIL, NIL}) // Array do memos para conter a observasao da Resposta
		Aadd(aPanel   ,NIL)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Scroll para a criacao de todos os objetos da  resposta.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		@ 0,0 SCROLLBOX aScroll[1] OF oWizard:GETPANEL(1) PIXEL 
		aScroll[1]:Align:=CONTROL_ALIGN_ALLCLIENT

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Toolbar do primeiro painel.                              ³
		//³As barras de ferramentas deverao ser criadas por painel. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DEFINE BUTTONBAR oBar 3D TOP OF IIf(!lUseTreeView,oWizard:GETPANEL(1),aScroll[1])

		DEFINE BUTTON aBtn[1] RESOURCE "BMPUSER"	OF oBar TOOLTIP STR0007  ACTION Tk380VisContato(cCodCont) WHEN .T. 			//"Alteracao de Contatos"
		aBtn[1]:cTitle:=""

		DEFINE BUTTON aBtn[2] RESOURCE "VENDEDOR"	OF oBar TOOLTIP STR0009 ACTION Tk380VisEntidade(cEntidade,cChave) WHEN .T. 	//"Alteracao de Entidades"
		aBtn[2]:cTitle:=""
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se a rotina nao foi executada a partir da Agenda do Operador nao exibe o Botao de atendimento receptivo³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If l380
			DEFINE BUTTON aBtn[3] RESOURCE "NOTE"	OF oBar TOOLTIP STR0011 ACTION Tk380CallCenter(cCodCont,cEntidade,cChave,0,3)  WHEN .T. //"Call Center Receptivo"
			aBtn[3]:cTitle:=""
		Else
			aBtn[3] := ""
		Endif	
		
		DEFINE BUTTON aBtn[4] RESOURCE "POSCLI"		OF oBar TOOLTIP STR0012 ACTION Tk380VisEvento(cLista) WHEN .T. //"Evento"
		aBtn[4]:cTitle:=""
		
		DEFINE BUTTON aBtn[5] RESOURCE "SIMULACAO"	OF oBar TOOLTIP STR0014 ACTION Tk380SituaC(cEntidade,cChave)  WHEN .T. //"Consulta posição de Clientes"
		aBtn[5]:cTitle:=""
		
		DEFINE BUTTON aBtn[6] RESOURCE "BUDGET"		OF oBar TOOLTIP STR0015  ACTION Tk380Titulo(cChave,cLista) WHEN .T.//"Consulta de Titulos"
		aBtn[6]:cTitle:=""

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cria o painel para ser utilizado no Tree.       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lUseTreeView 
			@ 0,0 MSPANEL aPanel[1] OF oWizard:oDlg SIZE 237,90 RAISED
			aPanel[1]:Align := CONTROL_ALIGN_LEFT
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Titulo da Observacao da pergunta                 ³
		//³Observação da Pergunta                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		@ 0,0 SAY oSayObsPerg VAR STR0016 SIZE 60,10 FONT oFont2 COLOR CLR_RED OF aScroll[1] PIXEL //"Observação da Pergunta"
		oSayObsPerg:Align := CONTROL_ALIGN_TOP

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Memo da Observacao da pergunta                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Tk380ObsPerg(aPerguntas[1,1,5],aScroll,oFont2,1,aMemos)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Memo do enunciado da pergunta                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Tk380Pergunta(aPerguntas[1,1,4],aScroll,oFont1,1,aMemoPerg)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Apresent o titulo da Observacao da pergunta      ³
		//³"Observação da Pergunta"                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		@ 0,0 SAY oSayObsResp VAR STR0017 SIZE 60,10 FONT oFont2 COLOR CLR_RED OF aScroll[1] PIXEL //"Observação da Resposta"
		oSayObsResp:Align := CONTROL_ALIGN_TOP

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Memo da Observação da resposta                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Tk380MemRes(aPerguntas[1],aScroll,1,@aMRes,oFont2)		

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Avalia o tipo de objeto da resposta e cria a resposta no scroll.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aCoord := {55,03,aPosObj[4,3]-10,aPosObj[4,4]-10}

		If lUseTreeView                    
			oTree := xTree():New( 0 ,0, 10,10, aPanel[1])
			oTree:AddTree (aPerguntas[1,1,4], "FOLDER516","FOLDER616", aPerguntas[1,1,2],{||Tk380ChgTr(oTree,oWizard,aTreeXPanel,@aControl)})
			oTree:Align:=CONTROL_ALIGN_ALLCLIENT
			aAdd(aTreeXPanel,{1,aPerguntas[1,1,2],{}})
		Endif

		Tk380Resposta(	aPerguntas[1]	,aCoord		,aScroll	,@aControl	,;
						1				,nOpc		,cCodigo	,@oTree		,;
						@aTreeXPanel	,@oWizard	,@aMRes	)
		If lUseTreeView
			oTree:EndTree()		
		Endif
		
		For nCont := 2 TO Len(aPerguntas)
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Criacao dos paineis com as questoes e respostas.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			CREATE PANEL oWizard  HEADER cFixHeader+Transform(cScore,"999999")  MESSAGE " " ;  //"Contato: ","Entidade: ","      Fone: ","Score: "
					 			 BACK {||Tk380Prev(	aPerguntas	,cFormato	,oWizard	,@aControl	,;
					 			 					nLastPanel	,@cScore	,@aPanelScore)};
								 NEXT {||Tk380Next(	aPerguntas	,cFormato	,@oWizard	,aControl	,;
								 					@nLastPanel	,@oMemoFim	,@cMemoFim	,@cScore	,;
								 					cFixHeader	,@aPanelScore)};
								 FINISH {||.T.,lRet:=.T.} PANEL

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Criacao do arrays dos objetos variaveis          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Aadd(aScroll   ,NIL)     	// Array do Scroll de cada tela do Wizard
			Aadd(aMemoPerg ,{NIL,NIL}) 	// Array do Memo para conter o enunciado de cada pergunta
			Aadd(aMRes     ,{NIL,NIL}) // Array do memos para conter a observasao da Resposta
			Aadd(aMemos    ,{NIL,NIL}) //Criacao de memos de acordo com a quantidade de panels
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Scroll para a criacao de todos os objetos da  resposta.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			@ 0,0 SCROLLBOX aScroll[nCont] SIZE aPosObj[4,3],(oWizard:GetPanel(1):nClientWidth/2)-5 OF oWizard:GETPANEL(nCont) PIXEL 
			aScroll[nCont]:Align:=CONTROL_ALIGN_ALLCLIENT
					
			DEFINE BUTTONBAR oBar 3D TOP OF IIf(!lUseTreeView,oWizard:GETPANEL(nCont),aScroll[nCont])

			DEFINE BUTTON aBtn[1] RESOURCE "BMPUSER"	OF oBar TOOLTIP STR0007  ACTION Tk380VisContato(cCodCont) WHEN .T. //"Alteração de Contatos"
			aBtn[1]:cTitle:=""
			DEFINE BUTTON aBtn[2] RESOURCE "VENDEDOR"	OF oBar TOOLTIP STR0009 ACTION Tk380VisEntidade(cEntidade,cChave) WHEN .T. //"Alteração de Entidades"
			aBtn[2]:cTitle:=""
			If l380
				DEFINE BUTTON aBtn[3] RESOURCE "NOTE"		OF oBar TOOLTIP STR0011 ACTION Tk380CallCenter(cCodCont,cEntidade,cChave,0,3)  WHEN .T. //"Call Center Receptivo"
				aBtn[3]:cTitle:=""
			Else
				aBtn[3]:= ""
			EndIf
			DEFINE BUTTON aBtn[4] RESOURCE "POSCLI"		OF oBar TOOLTIP STR0012 ACTION Tk380VisEvento(cLista) WHEN .T. //"Evento"
			aBtn[4]:cTitle:=""
			DEFINE BUTTON aBtn[5] RESOURCE "SIMULACAO"	OF oBar TOOLTIP STR0014 ACTION Tk380SituaC(cEntidade,cChave)  WHEN .T.//"Consulta posição de Clientes"
			aBtn[5]:cTitle:=""
			DEFINE BUTTON aBtn[6] RESOURCE "BUDGET"		OF oBar TOOLTIP STR0015 ACTION Tk380Titulo(cChave,cLista) WHEN .T. //"Consulta de Titulos"
			aBtn[6]:cTitle:=""

			If lUseTreeView
				oTree:AddTree (aPerguntas[nCont,1,4], "FOLDER516","FOLDER616", aPerguntas[nCont,1,2],{||Tk380ChgTr(oTree,oWizard,aTreeXPanel,@aControl)})
				aAdd(aTreeXPanel,{nCont,aPerguntas[nCont,1,2],{}})
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Titulo da Observacao da pergunta                 ³
			//³Observação da Pergunta                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			@ 0,0 SAY oSayObsPerg VAR STR0016 SIZE 60,10 FONT oFont2 COLOR CLR_RED OF aScroll[nCont] PIXEL //"Observação da Pergunta"
			oSayObsPerg:Align := CONTROL_ALIGN_TOP

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Memo da Observacao da pergunta                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Tk380ObsPerg(aPerguntas[nCont,1,5],aScroll,oFont2,nCont,aMemos)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Memo do enunciado da pergunta                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Tk380Pergunta(aPerguntas[nCont,1,4],aScroll,oFont1,nCont,aMemoPerg)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Titulo da Observacao da pergunta                 ³
			//³Observação da Pergunta                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			@ 0,0 SAY oSayObsResp VAR STR0017 SIZE 60,10 FONT oFont2 COLOR CLR_RED OF aScroll[nCont] PIXEL //"Observação da Resposta"
			oSayObsResp:Align := CONTROL_ALIGN_TOP
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Memo da Observação da resposta                   ³
  			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Tk380MemRes(aPerguntas[nCont],aScroll,nCont,@aMRes,oFont2)		

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Avalia o tipo de objeto da resposta e cria a resposta no scroll.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aCoord := {55,03,aPosObj[4,3]-10,aPosObj[4,4]-10}
			
			Tk380Resposta(	aPerguntas[nCont]	,aCoord		,aScroll	,@aControl	,;
							nCont				,nOpc		,cCodigo	,@oTree		,;
							@aTreeXPanel		,@oWizard	,@aMRes	)
							
			If lUseTreeView
				oTree:EndTree()
			Endif					
			
		Next nCont
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Criacao de painel da conclusao do Script.	   ³   
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		CREATE PANEL oWizard  HEADER STR0002+Upper(cDescCont)+Space(10)+STR0003+Upper(cDescChave)+" - "+cDesEntida+STR0004+cTel+Space(15)+STR0018+Transform(cScore,"999999")  MESSAGE " " ;  //"Contato: "###"Entidade: "###"      Fone: "###"Score: "
				 			 BACK {||Tk380Prev(	aPerguntas	,cFormato	,oWizard	,@aControl,;
				 			 					nLastPanel	,@cScore    ,@aPanelScore)};
							 NEXT {||};
							 FINISH {||.T.,;
							 			lRet:=.T.,;
							 			If(l380,Tk380Grava(	aControl	,aPerguntas	,cOperador	,cCodigo	,;
											                cFormato	,cCampanha	,cScript	,cEntidade	,;
											                cChave		,cCodCont	,cCodLig	,cLista	  	,;
											                cCodAte		),;
							    				 (nPos := aScan(aParScript,{|x| x[6]==cCampanha .AND. x[7]==cScript .AND. x[13]==cCodAte }),;
							    				 IIf(nPos==0,	Aadd(	aParScript,{aControl	,aPerguntas	,cOperador	,cCodigo,;
															 		 				cFormato	,cCampanha	,cScript	,cEntidade,;
       												              					cChave	,cCodCont	,cCodLig	,cLista   ,;
       												              					cCodAte}),;
       															aParScript[nPos][1]:=aControl )));
							    		,.T.} PANEL
							 
		
		DEFINE BUTTONBAR oBar 3D TOP OF oWizard:GETPANEL(nCont)
		DEFINE BUTTON aBtn[1] RESOURCE "BMPUSER"	OF oBar TOOLTIP STR0007  ACTION Tk380VisContato(cCodCont) WHEN .T. //"Alteração de Contatos"
		aBtn[1]:cTitle:=""
		DEFINE BUTTON aBtn[2] RESOURCE "VENDEDOR"	OF oBar TOOLTIP STR0009 ACTION Tk380VisEntidade(cEntidade,cChave) WHEN .T.//"Alteração de Entidades"
		aBtn[2]:cTitle:=""
		If l380
			DEFINE BUTTON aBtn[3] RESOURCE "NOTE"		OF oBar TOOLTIP STR0011 ACTION Tk380CallCenter(cCodCont,cEntidade,cChave,0,3)  WHEN .T. //"Call Center Receptivo"
			aBtn[3]:cTitle:=""
		Else
			aBtn[3]:= ""
		EndIf
		DEFINE BUTTON aBtn[4] RESOURCE "POSCLI"		OF oBar TOOLTIP STR0012 ACTION Tk380VisEvento(cLista) WHEN .T. //"Evento"
		aBtn[4]:cTitle:=""
		DEFINE BUTTON aBtn[5] RESOURCE "SIMULACAO"	OF oBar TOOLTIP STR0014 ACTION Tk380SituaC(cEntidade,cChave)  WHEN .T. //"Consulta posição de Clientes"
		aBtn[5]:cTitle:=""
		DEFINE BUTTON aBtn[6] RESOURCE "BUDGET"		OF oBar TOOLTIP STR0015  ACTION Tk380Titulo(cChave,cLista) WHEN .T. //"Consulta de Titulos"
		aBtn[6]:cTitle:=""
		
		@aPosObj[2,1],02 SCROLLBOX oScrEnd SIZE (oWizard:GetPanel(1):nClientHeight/2)-5,(oWizard:GetPanel(1):nClientWidth/2)-5 OF oWizard:GETPANEL(nCont) PIXEL 
		oMemoFim:=	TMultiGet():New( 	01,01, {||cMemoFim},;
			 							oScrEnd, (oWizard:GetPanel(1):nClientWidth/2)-10, (oWizard:GetPanel(1):nClientHeight/2)-20, NIL, .F.,;
										NIL, NIL, NIL, .T.,;
										NIL, .F., {||}, .F.,;
										.F., .T., NIL,;
										NIL, .F., NIL, NIL)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se é uma alteracao e carrega as informacoes para ³
		//³os objetos.                                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Tk380LRes(@aControl		,nOpc		,cCampanha	,cScript	,cCodCont	,;
				   cEntidade	,cChave		,cAlias		,nReg		,cOperador	,;
		    	   cLista   	,cSU6Cod	,cCodAte	,aParScript ,@cCodigo 	,;
				   aPerguntas)

		If lUseTreeView
			Tk380LoadT(@oTree, @aControl,@aTreeXPanel)
		EndIf

ACTIVATE WIZARD oWizard WHEN {||.T.} 

//Atribui as informacoes do script
If l380 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se eh uma alteracao de um script ja respondido neste³
	//³atendimento                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nPos := aScan(aParScript,{|x| x[6]==cCampanha .AND. x[7]==cScript .AND. x[13]==cCodAte })

	If nPos == 0
		Aadd(aParScript,{	aControl	,aPerguntas	,cOperador	,cCodigo,;
							cFormato	,cCampanha	,cScript	,cEntidade,;
       						cChave	,cCodCont	,cCodLig	,cLista   ,;
       						cCodAte}	)
	Else
		aParScript[nPos][1] := aControl 
	EndIf

EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380Pergunta   ³Autor  ³Vendas Clientes         ³Data  ³  20/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria as perguntas do script                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cPergunta	: Texto para pergunta.                                 		º±±
±±º          ³aScroll	: Array de Scroll das telas                                 º±± 
±±º          ³oFont1	: Fonte do Memo                                             º±± 
±±º          ³nCont		: Contador da questao					    				º±± 
±±º          ³aMemoPerg	: Array do Objeto + Variavel GET							º±± 
±±º          ³           [1] Armazema o objeto GET         							º±± 
±±º          ³           [2] Armazema a variavel do objeto GET						º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Ap8 - Agenda do Operador                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380Pergunta(cPergunta,aScroll,oFont1,nCont,aMemoPerg)

aMemoPerg[nCont,2]:=cPergunta

@ 0,0 GET aMemoPerg[nCont,1] VAR aMemoPerg[nCont,2] MEMO SIZE 280,20 FONT oFont1 READONLY OF aScroll[nCont] PIXEL 
aMemoPerg[nCont,1]:Align := CONTROL_ALIGN_TOP

Return(.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380Pergunta   ³Autor  ³Vendas Clientes         ³Data  ³  20/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria memo da observacao da pergunta                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cPergunta	: Texto da observacao                                       º±±
±±º          ³aScroll	: Array de Scroll das telas                                 º±± 
±±º          ³oFont2	: Fonte do Memo da pergunta (Negrito)                       º±± 
±±º          ³nCont		: Contador da questao					    				º±± 
±±º          ³aMemos	: Array do Objeto + Variavel GET							º±± 
±±º          ³           [1] Armazema o objeto GET         							º±± 
±±º          ³           [2] Armazema a variavel do objeto GET						º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Ap8 - Agenda do Operador                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380ObsPerg(cPergunta,aScroll,oFont2,nCont,aMemos)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Armazena os dados do campo observaco SYP						  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aMemos[nCont,2]:= MSMM(cPergunta,If( ((TamSx3("UP_OBS")[1]) < 80 ),80 , TamSx3("UP_OBS")[1]))

@ 0,0 GET aMemos[nCont,1] VAR aMemos[nCont,2] MEMO SIZE 280,30 FONT oFont2 READONLY OF aScroll[nCont] PIXEL 
aMemos[nCont,1]:Align := CONTROL_ALIGN_TOP

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380Resposta   ³  Autor³Vendas Clientes         ³Data  ³  21/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria as respostas do script                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpA1: Array com a pergunta e suas respostas.               			º±±
±±º          ³ExpA2: Array com as coordenadas para criacao dos objetos.   			º±± 
±±º          ³ExpO1: Objeto Scroll das respostas.                                   º±± 
±±º          ³ExpA3: Array com os controles das perguntas e respostas.              º±± 
±±º          ³ExpN1: Variavel que indica o posicionamento do laco.                  º±± 
±±º          ³ExpN2: Opcao de menu.                                                 º±± 
±±º          ³ExpC1: Codigo para busca de informacoes.(proveniente do atendimento)  º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³AP8 - Agenda do Operador                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Tk380Resposta(	aItPerg		,aCoord	,aScroll	,aControl	,;
								nItems		,nOpc	,cCodigo	,oTree		,;
								aTreeXPanel	,oWizard,aMRes)

Local nResposta	:= 0      				// Contador auxiliar
Local nTop      := aCoord[1]			// Coordenada inicial do complemento de objeto (TOP)
Local aResposta := {}					// Array com as repostas
Local cTpObj	:= aItPerg[1,7]			// Tipo de objeto para o item de resposta
Local aMemos	:= {}   				// Memo
Local lUseTree  := (oTree <> Nil)
Local nRespResta                        
Local nPosTree, cChave

If Len(aItPerg[2])>0
	aResposta := aClone(aItPerg[2])
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica qual o tipo do objeto a ser criado para cada resposta.³
//³1 - Checkbox (Multipla Escolha)                                ³
//³2 - Radiobutton (Unica Escolha)                                ³
//³3 - Memo (Dissertativa)                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (Len(aResposta) > 0)

	For nResposta:= 1 To Len(aResposta)
    
		Do Case
	
			Case (cTpObj == "1") //Unica Escolha (Radio)
				
				If lUseTree                                  
					For nRespResta := 1 to Len(aResposta) 
						cChave := aResposta[nResposta][2] + "-"+Alltrim(Str(nRespResta))
						oTree:AddTreeItem(aResposta[nRespResta][4],"BPMSEDT4_mdi",cChave,,,{||Tk380ChgTr(oTree,oWizard,aTreeXPanel,@aControl)})
						nPosTree := Len(aTreeXPanel)
						aAdd(aTreeXPanel[nPosTree][3],cChave)

		
					Next nRespResta
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Cria um radio unico com todas as respostas da pergunta.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ				
					Tk380Radio(	aResposta	,@aCoord	,aScroll	,@aControl	,;
								@aMemos 	,nItems		,nOpc		,cCodigo	,;
								oTree		,aTreeXPanel,@aMRes)
					
                Exit

			Case (cTpObj == "2") //Multipla Escolha (Checkbox)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Cria um checkbox para cada resposta das perguntas.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Tk380Check(	aResposta[nResposta]	,@nTop		,aScroll	,@aControl,;
							aItPerg[1,2]			,aCoord		,nOpc		,cCodigo,; 
							nItems					,nResposta	,oTree		,aTreeXPanel)
				If lUseTree                                  
					oTree:AddTreeItem(aResposta[nResposta][4],"BPMSEDT4_mdi",aResposta[nResposta][2],,,{||Tk380ChgTr(oTree,oWizard,aTreeXPanel,@aControl)})

					nPosTree := Len(aTreeXPanel)
					aAdd(aTreeXPanel[nPosTree][3],aResposta[nResposta][2])

				Endif
		EndCase

	Next nResposta

Else
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria um memo para a inclusao da resposta do cliente.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Tk380Memo(aCoord,aScroll,@aControl,aItPerg[1,2],nOpc,cCodigo,nItems) 

Endif

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380Radio      ³  Autor³Vendas Clientes         ³Data  ³  21/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria radio para as respostas multipla escolha                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpA1: Array com as respostas                               			º±±
±±º          ³ExpA2: Array com as coordenadas para criacao dos objetos.   			º±± 
±±º          ³ExpO1: Objeto Scroll das respostas.                                   º±± 
±±º          ³ExpA3: Array com os controles das perguntas e respostas.              º±± 
±±º          ³ExpA4: Array om as informacoes dos Memos.                             º±± 
±±º          ³ExpN1: Controle do laco das questoes.                                 º±± 
±±º          ³ExpN2: Opcao de menu selecionada                                      º±± 
±±º          ³ExpC1: Codigo para busca de informacoes.(proveniente do atendimento)  º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³AP8 - Agenda do Operador                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Tk380Radio(	aResposta	,aCoord		,aScroll	,aControl	,;
							aMemos		,nItems		,nOpc		,cCodigo	,;
							oTree		,aTreeXPanel,aMRes)
							
Local oRadio					//Objeto RADIO da resposta
Local nWidth	:= 0			//Tamanho da largura do objeto
Local bVar						//Execblock	
Local bVarMemo
Local cControl  := ""
Local cIDResp	:= aResposta[1][2]
Local aIdResp 	:= aClone(aResposta)
Local bItem     := {|| Tk380Search(cIdResp,aIdResp,"R",0,nOpc,cCodigo,NIL,oRadio:nOption) }
Local nTop		:= aCoord[1]	//Coordenada linha superior
Local nVal		:= 0 			//Variavel auxiliara para Ascan
Local oMemoY 					//Objetos Memo para as respostas
Local cMemoY	:= ""			//Variavel dos objetos Memo para as respostas.
Local bBlockTree :={||}
Local cChave
Local aIdMemo	:= {}			// Array com os Ids de Memo
Local nCont		:= 0			// Variavel auxiliar

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona o objeto no array de controle			  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³1 - Id da Pergunta															    ³
³2 - Id da Resposta															    ³
³3 - Conteudo do Checkbox (.T. ou .F.)											    ³
³4 - Numero da Opcao do Radio Selecionado										    ³
³5 - Texto da resposta dissertativa												    ³
³6 - Texto do get de Complemento de Resposta										    ³
³7 - Tipo de objeto (C - Checkbox, R - Radiobutton M - Memo)						    ³
³																			    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/    
cChave	:= aResposta[1,3]
AAdd(aControl,{ cChave ,; //para todas as respostas o ID da pergunta eh o mesmo.
				NIL,;
				NIL,;
				NIL,;
				Space(255),;
				NIL,;
				"R"})

For nCont := 1 TO LEN(aResposta)
	Aadd(aIdMemo,(MSMM(aResposta[nCont][5],If( ((TamSx3("UP_OBS")[1]) < 80 ),80 , TamSx3("UP_OBS")[1])) + CRLF + CRLF ))
Next nCont

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica o tamanho da string para ajusta-la		  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AEval(aResposta,{|x| x := Alltrim(x[4]),nWidth:= MAX(GetTextWidth(0,x)/1.5,nWidth)})
nWidth   := If(nWidth > 200,200,nWidth)

aControl[Len(aControl)][4]:= Tk380Search(cIdResp,aIdResp,"R",0,nOpc,cCodigo,NIL,NIL)
cControl := AllTrim(Str(Len(aControl)))

bVar     := &( "{|x|If(ValType(x)=='U',aControl["+cControl+"][4],aControl["+cControl+"][4]:=x) }" )

If  oTree <> Nil
	bBlockTree  := &( "{||Tk380ChgCt(oTree,cChave,aControl["+cControl+"],aTreeXPanel)}")
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria um radiobutton para cada resposta da pergunta ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

/*
[ <oRadMenu> := ] TRadMenu():New(<nRow>, <nCol>, {<cItems>},;
								 [bSETGET(<nVar>)], <oWnd>, [{<nHelpId>}], <{uChange}>,;
								 <nClrFore>, <nClrBack>, <cMsg>, <.update.>, <{uWhen}>,;
								 <nWidth>, <nHeight>, <{uValid}>, <.lDesign.>, <.lLook3d.>, <.lPixel.> )
*/                             

@0,0 MSPANEL oPanelRadio SIZE 100,Len(aResposta)*10 OF aScroll[nItems]
oPanelRadio:Align := CONTROL_ALIGN_TOP

oRadio:= TRadMenu():New( 	0, 0, aResposta,;
							bVar, oPanelRadio , NIL, {||bItem,;
							IIf(oRadio:nOption<>0,aMRes[nItems,2]:=aIdMemo[oRadio:nOption],aMRes[nItems,2]:=""),aMRes[nItems,1]:Refresh()},;
							CLR_BLUE, NIL, NIL, .F., NIL,;
							nWidth+20,10, Nil, .F., .T., .T. )          
							                                        
oRadio:bWhen:= {|| If(nOpc == 2,.F.,.T.)}
oRadio:bChange 	:= {|| If(Ascan(aResposta,{|x| ALLTRIM(x) == UPPER(STR0021)}) > 0,;		//"RESPOSTA ABERTA"
							If(Ascan(aResposta,{|x| ALLTRIM(x) == UPPER(STR0021)}) == oRadio:nOption,; //"RESPOSTA ABERTA"
								(oMemoY:Enable(),oMemoY:SetFocus()),;
							oMemoY:Disable()),;
                        	""),Eval(bBlockTree),;
                        	IIf(oRadio:nOption<>0,aMRes[nItems,2]:=aIdMemo[oRadio:nOption],aMRes[nItems,2]:=""),aMRes[nItems,1]:Refresh()}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ajusta o TOP do proximo objeto.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCoord[1] += 10

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria o objeto MEMO quando a descricao for "RESPOSTA ABERTA"³
//³Nesse memo quando houver uma alteracao da descricao o ARRAY³
//³sera atualizado na 5 posicao - Resposta Dissertativa       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se nas respostas do Panel existe a descricao RESPOSTA ABERTA.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nVal := Ascan(aResposta,{|x| ALLTRIM(x) == UPPER(STR0021)})  			//"RESPOSTA ABERTA"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria um memo de resposta aberta para cada painel.		     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

@ aCoord[1]+(Len(aResposta)*10), 10 GET oMemoY VAR cMemoY OF aScroll[nItems] MULTILINE  SIZE 250, 40 PIXEL
oMemoY:bSETGET := &( "{|x|If(ValType(x)=='U',aControl["+cControl+"][5],aControl["+cControl+"][5]:=x) }" )
oMemoY:Align:=CONTROL_ALIGN_TOP
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso exista resposta aberta, apresenta objeto memo. 			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nVal > 0  	
	oMemoY:Show()
Else
	oMemoY:Hide()
Endif

Return(.T.)                 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk380Search     ³  Autor³Vendas Clientes         ³Data  ³  03/04/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna os valores das respostas dadas pelo cliente                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA061 - Agenda do Operador                                       	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380Search(cIdResp,xIdAnswer,cTipo,cDefault,nOpc,cCodigo,aIdMemo,nPos)

Local xRet  := cDefault
Local nItem := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Le da base as respostas dadas quando nao for inclusao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SUK")
DbSetOrder(1)
If MsSeek(xFilial("SUK") + cCodigo)
	While !Eof() .AND. (xFilial("SUK") == SUK->UK_FILIAL) .AND. (cCodigo == SUK->UK_CODIGO)
		
		If (SUK->UK_CODPERG == cIdResp)
			Do Case
				Case (cTipo == "C")
					If (SUK->UK_CODRESP == xIdAnswer)
						xRet:= .T.
					Endif
					
				Case (cTipo == "R")
					For nItem:= 1 to Len(xIdAnswer)
						If (SUK->UK_CODRESP == xIdAnswer[nItem][2])
							xRet:= nItem
							Exit
						Endif
					Next
					
				Case (cTipo == "M")
					xRet:= MSMM(SUK->UK_CODMEMO,TamSX3("UK_RESMEMO")[1])
					
				Case (cTipo == "G")
					xRet:= SUK->UK_COMPRES
					
			EndCase
			
			Exit
		Endif
		
		SUK->(DbSkip())
	End
Endif

Return(xRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380Check      ³  Autor³Vendas Clientes         ³Data  ³  21/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria CheckBox para as respsotas do tipo multipla escolha.             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpA1: Array com as respostas                               			º±±
±±º          ³ExpN1: Indica linha inicial para criacao do objeto.         			º±± 
±±º          ³ExpO1: Objeto Scroll das respostas.                                   º±± 
±±º          ³ExpA2: Array com os controles das perguntas e respostas.              º±± 
±±º          ³ExpC1: Identificacao da resposta                                      º±± 
±±º          ³ExpA4: Array com as coordenadas para objeto                           º±± 
±±º          ³ExpN2: Opcao de menu selecionada                                      º±± 
±±º          ³ExpC1: Codigo para busca de informacoes.(proveniente do atendimento)  º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³AP8 - Agenda do Operador                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
*/
Static Function Tk380Check(	aResposta	,nTop		,aScroll	,aControl,;
							cIdResp		,aCoord		,nOpc		,cCodigo,;
							nItems		,nResposta	,oTree		,aTreeXPanel)


Local oCheck              										// Objeto de CHEKBOX
Local oGet														// Objeto para o GET
Local bVar                 										// Execblock
Local cControl  := ""											// Variavel auxiliar para controle de posicao dos objetos na tela
Local bWhen     := {|| If(nOpc == 2,.F.,.T.)}  				// Desabilita na Visualizacao
Local cIdAnswer := ""											// Codigo da Resposta - Codigo do Cargo (TREE)
Local cAnswer   := ""											// Descricao da Resposta
Local nHeight   := 0											// Calculo da ALTURA
Local nWidth	:= 0 											// Calcula a largura
Local nTopAsk   := 0                            				// Calculo do TOP
Local nString   := 0											// Contador 
Local aString   := {}
Local bBlockTree :={||}
Static oPanelCheck

cIdAnswer:= aResposta[2]										//Codigo do Cargo (TREE)
cAnswer	 := AllTrim(aResposta[4])								//Descricao da Resposta
nHeight  := Tk380CalcHeight(cAnswer,@aString,aCoord[4]-5)
nWidth   := Tk380CalcWidth(cAnswer,aCoord[4]-5)
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona o objeto no array de controle.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Aadd(aControl,{	cIdResp		,;		// 01
				cIdAnswer	,;		// 02
				NIL			,;		// 03
				NIL			,;		// 04
				NIL			,;		// 05
				SPACE(40)	,;		// 06
				"C" 	} )			// 07

cControl := AllTrim(Str(Len(aControl)))

aControl[Len(aControl)][3]:= Tk380Search(cIdResp,cIdAnswer,"C",.F.,nOpc,cCodigo)

bVar     	:= &( "{|x|If(ValType(x)=='U',aControl["+cControl+"][3],aControl["+cControl+"][3]:=x) }" )
If  oTree <> Nil
	bBlockTree  := &( "{||Tk380ChgCt(oTree,cIdAnswer,aControl["+cControl+"],aTreeXPanel)}")
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montar o Panel somente na primeira vez (Alternativa)³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nResposta=1
	@0,0 MSPANEL oPanelCheck SIZE 300,Len(aResposta)*40 OF aScroll[nItems]
	oPanelCheck:Align := CONTROL_ALIGN_TOP
Endif	


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria um checkbox para cada resposta da pergunta.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/* 

[ <oCbx> := ] TCheckBox():New(  <nRow>, <nCol>, <cCaption>,;
									[bSETGET(<lVar>)], <oWnd>, <nWidth>, <nHeight>, <nHelpId>,;  //Largura Altura
									[<{uClick}>], <oFont>, <{ValidFunc}>, <nClrFore>, <nClrBack>,;
									<.design.>, <.pixel.>, <cMsg>, <.update.>, <{WhenFunc}> )
*/


oCheck   := TCheckBox():New(	nTop, 10, aString[1],;
								bVar, oPanelCheck, nWidth, 10, NIL,;
								bBlockTree, NIL, NIL, CLR_BLUE, NIL,;
								.T., .T., NIL, .F., bWhen )
oCheck:Align := CONTROL_ALIGN_TOP

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se a RESPOSTA for muito grande, quebra em varias linha ³
//³utilizando-se do Checbox para a primeira linha e do Say³
//³para as outras linhas.                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTopAsk:= nTop
For nString:= 2 To Len(aString)

	nTopAsk += 10
	Tk380Pergunta(aString[nString],aScroll,nTopAsk,20)

Next nString
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria um get para as respostas complementares.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aControl[Len(aControl)][6] := Tk380Search(	cIdResp	,cIdAnswer	,"G"	,Space(TamSX3("UK_COMPRES")[1])	,;
											nOpc	,cCodigo)
											
bVar:= &( "{|x|If(ValType(x)=='U',aControl["+cControl+"][6],aControl["+cControl+"][6]:=x) }" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se o parametro para complemento de resposta existir e for ³
//³verdadeiro cria o get para complemento de respostas.      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


If aResposta[8] == "1" // Sim

/*
 [ <oGet> := ] TGet():New( 	<nRow>, <nCol>, bSETGET(<uVar>),;
							[<oWnd>], <nWidth>, <nHeight>, <cPict>, <{ValidFunc}>,;
							<nClrFore>, <nClrBack>, <oFont>, <.design.>,;
							<oCursor>, <.pixel.>, <cMsg>, <.update.>, <{uWhen}>,;
							<.lCenter.>, <.lRight.>,[\{|nKey, nFlags, Self| <uChange>\}], <.readonly.>,;
							<.pass.>, [<.lNoBorder.>], <nHelpId> )
*/


	oGet:= TGet():New( 	nTop, nWidth + 20, bVar,;
  						oPanelCheck, TamSX3("UK_COMPRES")[1], 6, "@!", NIL,;		//aScroll[nItems]
						NIL, NIL, NIL, .F.,;
						NIL, .T., NIL, .F., bWhen,;
	 					.F., .F., NIL, .F., .F., NIL, NIL)
	oGet:Align := CONTROL_ALIGN_TOP
Endif


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ajusta o Top do proximo objeto.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTop+= nHeight

Return(.T.)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380Memo       ³  Autor³Vendas Clientes         ³Data  ³  21/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria memo para resposta dissertativa.                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpA1: Array com as coordenadas para o objeto.              			º±±
±±º          ³ExpN1: Indica linha inicial para criacao do objeto.         			º±± 
±±º          ³ExpA2: Array com os controles das perguntas e respostas.              º±± 
±±º          ³ExpC1: Identificacao da resposta                                      º±± 
±±º          ³ExpN2: Opcao de menu selecionada                                      º±± 
±±º          ³ExpC2: Codigo para busca de informacoes.(proveniente do atendimento)  º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³AP8 - Agenda do Operador                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Tk380Memo(aCoord,aScroll,aControl,cIdResp,nOpc,cCodigo,nItems) 

Local oMemoX									//Objeto MEMO
Local bVar										//Execblock
Local cControl 	:= "" 
Local nWidth	:= 0
Local nHeigth	:= 0		
Local bWhen	  	:= {|| If(nOpc == 2,.F.,.T.)} 	//Desabilita na visualizacao

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona o objeto no array de controle.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Aadd(aControl,{cIdResp,NIL,NIL,NIL,Space(255),NIL,"M"})

nWidth   := aCoord[4]-5
nHeigth  := aCoord[3] - aCoord[1]-5
cControl := AllTrim(Str(Len(aControl)))
aControl[Len(aControl)][5]:= Tk380Search(cIdResp,NIL,"M","",nOpc,cCodigo)
bVar     := &( "{|x|If(ValType(x)=='U',aControl["+cControl+"][5],aControl["+cControl+"][5]:=x) }" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria um memo para que possa ser armazenado a resposta dada  pelo   ³
//³cliente, ja que esta pergunta não possui respostas pre-formatadas  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*   [ <oGet> := ] TMultiGet():New( <nRow>, <nCol>, bSETGET(<uVar>),;
									[<oWnd>], <nWidth>, <nHeight>, <oFont>, <.hscroll.>,;
									<nClrFore>, <nClrBack>, <oCursor>, <.pixel.>,;
									<cMsg>, <.update.>, <{uWhen}>, <.lCenter.>,;
									<.lRight.>, <.readonly.>, <{uValid}>,;
									[\{|nKey, nFlags, Self| <uChange>\}], <.lDesign.>,;
									[<.lNoBorder.>], [<.lNoVScroll.>] )
								
*/

oMemoX := TMultiGet():New( 	aCoord[1], aCoord[2], bVar,;
							aScroll[nItems], 280,30, NIL, .F.,;
							NIL, NIL, NIL, .T.,;
							NIL, .F., bWhen, .F.,;
							.F., NIL, NIL,;
							NIL, .F., NIL, NIL)
oMemoX:Align := CONTROL_ALIGN_TOP
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk380CalcHeight ³  Autor³Vendas Clientes         ³Data  ³  03/04/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Calcula a altura dos objetos do script                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA380 - Agenda do Operador                                       	º±±
±±ÌÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º          ³        ³      ³                                            	     	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380CalcHeight(cCaption,aString,nWidthArea)

Local nHeight:= 0		//ALTURA
Local nWidth := 0		//LARGURA
Local nTam   := 0		//TAMANHO
Local cAux   := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula a altura dos objetos a serem criados para o script de pesquisa³ 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nWidth:= GetTextWidth(0,cCaption)
nWidthArea+= (nWidthArea * 1.5)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se o tamanho da string for maior que o tamanho maximo permitido, quebra a string em varias linhas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (nWidth > nWidthArea)

	For nTam:= 1 To Len(cCaption)
		cAux+= SubStr(cCaption,nTam,1)
		
		If (GetTextWidth(0,cAux) >= nWidthArea)
			Aadd(aString,cAux)
			cAux:= ""
		Endif
	Next nTam
	
	Aadd(aString,cAux)
	nHeight:= 10 * Len(aString)
Else
	Aadd(aString,cCaption)
	nHeight:= 10
Endif

Return(nHeight)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk380CalcWidth  ³  Autor³Vendas Clientes         ³Data  ³  03/04/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Calcula a largura dos objetos do script                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA380 - Agenda do Operador                                       	º±±
±±ÌÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º          ³        ³      ³                                            	     	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380CalcWidth(cCaption,nWidthArea)

Local nWidth := 0		//LARGURA

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula a altura dos objetos a serem criados para o script de pesquisa ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nWidth	:= GetTextWidth(0,cCaption)   
nWidthArea += (nWidthArea * 4)

If nWidth > nWidthArea
	nWidth	:= nWidthArea
Else
	nWidth	:= nWidth	
Endif

Return(nWidth)



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380MemResºAutor ³Vendas Clientes     º Data ³  21/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria objetos com o memo das respostas.                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpA1: Array com as perguntas                    			  º±±
±±º          ³ExpA2: Array com as coordenadas para objetos                º±± 
±±º          ³ExpO1: Objeto Scroll onde estaao os detalhes da resposta    º±± 
±±º          ³ExpN1: Identifica qual a questao trabalhada                 º±± 
±±º          ³ExpA3: Array com os objetos Memo                            º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8 Agenda do Operador                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380MemRes(aPerg,aScroll,nCont,aMRes,oFont2)		

Local nX		:= 0 // Contador para for.

@ 0,0 GET aMRes[nCont,1] VAR aMRes[nCont,2] MEMO SIZE 280,30 FONT oFont2 READONLY OF aScroll[nCont] PIXEL 
aMRes[nCont,1]:Align := CONTROL_ALIGN_TOP

Return .T.
                                                                                 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380NExt	 ºAutor ³Vendas Clientes     º Data ³  21/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Avanca para o painel correto.                               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpA1: Array com as perguntas                    			  º±±
±±º          ³ExpC2: Formato do Script                                    º±± 
±±º          ³ExpO3: Objeto Wizard.    									  º±± 
±±º          ³ExpA4: Array com os controles das perguntas e respostas	  º±± 
±±º          ³ExpN5: Controle do ultimo painel          	              º±± 
±±º          ³ExpO6: Objeto Memo do resumo.                               º±± 
±±º          ³ExpC7: Texto para resumo.                                   º±± 
±±º          ³ExpC8: Controle do Score                                    º±± 
±±º          ³ExpC9: Texto fixo para atualizacao do header dos paineis    º±± 
±±º          ³ExpA10: Score por tela navegada para o tipo Atendimento     º±±
±±º          ³ExpA11: Array de Score por tela navegada para o tipo 		  º±± 
±±º          ³		 atendimento                                  		  º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8 Agenda do Operador                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380Next(	aPerguntas	,cFormato	,oWizard	,aControl	,;
							nLastPanel	,oMemoFim	,cMemoFim	,cScore		,;
							cFixHeader	,aPanelScore)
Local nPos    		:= 0 		
Local cSup    		:= ""
Local nControl		:= 0		//Contador
Local nOpcao  		:= 0
Local nItem	  		:= 1
Local nPanel  		:= oWizard:nPanel
Local lEncontrou	:= .F.
Local lRet			:= .T.
Local lFinaliza		:= .F. 
Local nScan			:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica qual o proximo panel a ser exibido.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (cFormato <> "2")

	For nControl:= 1 To Len(aControl)//Array de controle dos itens das respostas

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Pesquisa em todas as perguntas a resposta efetuada através do ID que define a amarracao(aControl[X][1] )         ³
		//³Pesquisa com base no panel (aControl[nPanel][1]) ja que os panels possuem a mesma ordem de criacao das perguntas.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		nPos:= Ascan(aPerguntas,{|aVal| aVal[nControl][2]== aControl[nPanel][1]})  
		If (nPos > 0)
			nOpcao:= aControl[nPos][4]
			Exit
		Endif

	Next nControl
	
	If (nOpcao > 0 )
	
		cSup  := aPerguntas[nPanel][2][nOpcao][2]
	
	
		For nItem:= 1 To Len(aPerguntas)
        	
			If (aPerguntas[nItem][1][3] == cSup)
				If nItem = Len(aPerguntas)
					nLastPanel := nItem
					lFinaliza := .T.
				Endif
				oWizard:nPanel:= nItem-1
				lEncontrou := .T.
				Exit
			Endif

		Next nItem
	
		If !lEncontrou
			nLastPanel:=nPanel
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Nao troca de painel e habilita o botao finalizar.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oWizard:nPanel:= Len(aPerguntas)
			lFinaliza := .T.
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calculo do Score³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nControl:= 1 To Len(aControl)//Array de controle dos itens das respostas

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Pesquisa em todas as perguntas a resposta efetuada através do ID que define a amarracao(aControl[X][1] )         ³
			//³Pesquisa com base no panel (aControl[nPanel][1]) ja que os panels possuem a mesma ordem de criacao das perguntas.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
			If aControl[nControl][1] == aPerguntas[nPanel][1][2]// encontrou a  Pergunta
	
				
				If aControl[nControl][7] == 'R'                 
				    If aControl[nControl][4]>0
						cScore := cScore+aPerguntas[nPanel][2][ aControl[nControl][4] ][6]
						AAdd(aPanelScore,{aPerguntas[nPanel][2][ aControl[nControl][4] ][6],aPerguntas[nPanel][1][2]})
						Exit
					Endif	
				Endif
			Endif	
		Next nControl	
        
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Altera texto do header do wizard para alteracao do score.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oWizard:SetHeader(cFixHeader+TRANSFORM(cScore,"999999"),nItem,.T.)			
	Else
		Help(" ",1,"TMK380NITE") //MsgInfo("Selecione uma resposta para prosseguir com o Script.")
		oWizard:nPanel:=oWizard:nPanel-1
	Endif	
Else	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza controle para funcao Tk380Prev³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nLastPanel :=  nPanel
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calculo do Score³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nControl:= 1 To Len(aControl)//Array de controle dos itens das respostas

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Pesquisa em todas as perguntas a resposta efetuada através do ID que define a amarracao(aControl[X][1] )         ³
		//³Pesquisa com base no panel (aControl[nPanel][1]) ja que os panels possuem a mesma ordem de criacao das perguntas.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		If aControl[nControl][1] == aPerguntas[nPanel][1][2]// encontrou a  Pergunta
		
			If aControl[nControl][7] == 'R'                 
			    If aControl[nControl][4]>0
					cScore := cScore+aPerguntas[nPanel][2][ aControl[nControl][4] ][6]
					Exit
				Endif	
			Endif

			If aControl[nControl][7] == 'C'                 
			    If aControl[nControl][3]
					nScan := Ascan(aPerguntas[nPanel][2],{|aVal| aVal[2]== aControl[nControl][2]})  
					cScore := cScore+aPerguntas[nPanel][2][ nScan ][6]
				Endif
			Endif
		Endif
	Next nControl

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Altera texto do header do wizard para alteracao do score.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ       
	oWizard:SetHeader(cFixHeader+TRANSFORM(cScore,"999999"),nPanel+1,.T.) 	
	         
	                               
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de entrada para validar respostas do Script Dinâmico
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
	
	If ExistBlock("TK380VLD")
		lRet := ExecBlock("TK380VLD",.F.,.F.,{cFormato,nPanel,aControl,aPerguntas})
	Endif

	If nPanel=Len(aPerguntas)
		lFinaliza := .T.
	Endif
Endif

If lFinaliza
	Tk380InfEnd(@oMemoFim,@cMemoFim,aPerguntas,aControl,cFormato, aPanelScore)
Endif

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380Prev  	  ³  Autor³Vendas Clientes         ³Data  ³  03/04/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Exibe o panel anterior que conterao os objetos do script              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpA1: Array com as perguntas                    					    º±±
±±º          ³ExpC2: Formato do Script                                   		    º±± 
±±º          ³ExpO3: Objeto Wizard.    									 		    º±± 
±±º          ³ExpA4: Array de controle da perguntas e respostas  				    º±± 
±±º          ³ExpN5: Controle do ultimo painel          	             			º±± 
±±º          ³ExpC6: Controle do Score                                  			º±±
±±º          ³ExpA7: Array de Score por tela navegada para o tipo Atendimento 		º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Ap8 - Agenda do Operador                                           	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380Prev(aPerguntas,	cFormato,	oWizard,	aControl,;
 						  nLastPanel,	cScore,		aPanelScore)

Local nPos 		:= 0
Local cSup 		:= ""
Local nItem		:= 0
Local nPanel	:= oWizard:nPanel
Local nScCont   := 0 
Local nControl  := 0

If (cFormato = "2")
	//Formato Pesquisa
	For nScCont := 1 To Len(aControl)
		If aPerguntas[nPanel-1][1][2] == aControl[nScCont][1]
			If aControl[nScCont][7] == 'R'                 
		    	If aControl[nScCont][4]>0
					cScore := cScore-aPerguntas[nPanel-1][2][ aControl[nScCont][4] ][6]
					Exit
				Endif	
			Endif
			If aControl[nScCont][7] == 'C'                 
		    	If aControl[nScCont][3]
					nScan := Ascan(aPerguntas[nPanel-1][2],{|aVal| aVal[2]== aControl[nScCont][2]})  
					cScore := cScore-aPerguntas[nPanel-1][2][ nScan ][6]
				Endif
			Endif
		Endif	
	Next nScCont
Else                  
	//Formato Atendimento	      
	If nPanel == (Len(aPerguntas)+1)			
		oWizard:nPanel:= nlastPanel+1 
		If Len(aPanelScore) > 0
			cScore -= aPanelScore[Len(aPanelScore)][1]
		Endif 
	Else   

		cSup  := aPerguntas[nPanel][1][3]		
		For nItem:= 1 To Len(aPerguntas)		
			nPos:= Ascan(aPerguntas[nItem][2],{|aVal| aVal[2] == cSup})
			If (nPos > 0)
				oWizard:nPanel:= nItem+1
				Exit
			Endif			
		Next nItem	
		If Len(aPanelScore) > 0
			cScore -= aPanelScore[Len(aPanelScore)][1] 
		Endif
	Endif 
	//Retira o úultimo elemento do array
	If Len(aPanelScore) > 0
		ASize(aPanelScore,Len(aPanelScore)-1)
	Endif	 
Endif

	
Return(.T.)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk380InfEnd	  ³  Autor³Vendas Clientes         ³Data  ³  03/04/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega as informacoes do script no painel final.                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1: Objeto Memo do resumo.                    					    º±±
±±º          ³ExpC2: Texto do resumo final.                              		    º±± 
±±º          ³ExpO3: Array com as perguntas e respostas                             º±± 
±±º          ³ExpA4: Array de controle da perguntas e respostas  				    º±± 
±±º          ³ExpC5: Formato do Script Dinamico                         			º±± 
±±º          ³ExpA6: Array de Score por tela navegada para o tipo Atendimento 		º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Ap8 - Agenda do Operador                                           	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380InfEnd(	oMemoFim	,cMemoFim	,aPerguntas	,aControl,;
								cFormato	,aPanelScore)

Local nCont 		:= 0				// Contador auxiliar para o FOR
Local nControl  	:= 0      			// Contador auxiliar para os controles de objetos
Local nCol			:= 10				// Alinhamento da Coluna
Local nColR			:= 20				// Alinhamento da Coluna Direita
Local nX			:= 0				// Contador de colunas
Local nY			:= 0				// Contador de linhas
Local nAuxFor   	:= 0				// Contador para o FOR de objetos
Local nAuxPer		:= 0				// Contador para o FOR de perguntas
Local nPosaControl	:= 0       			// Posicao do campo no Array de controle dos itens das respostas
Local cIdRespAnt	:= ""

cMemoFim := STR0020 + CRLF+CRLF+CRLF	// Resumo das Informacoes


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se o formato do SCRIPT for de DECISAO - para o formato de ATENDIMENTO³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If VAL(cFormato) == DECISAO // "1"
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Percorre os objetos criados dinamicamente³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nAuxFor := Len(aControl) 
	For nControl := 1 TO nAuxFor
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Percorre as perguntas do SCRIPT³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nAuxPer  := Len(aPerguntas)
		For nCont := 1 TO nAuxPer
			
			If aPerguntas[nCont][1][2] == aControl[nControl][1]
				
				If aControl[nControl][4] > 0 
					//Verifica se a pergunta participou da pesquisa, senao nao imprime no final.
					If AScan(aPanelScore,{|x|x[2] == aPerguntas[nCont][1][2]}) <= 0
						If aControl[nControl][7] == "R"
							aControl[nControl][4] := 0
						EndIf
						Exit
					Endif  
										
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Carrega as perguntas³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cMemoFim += Space(nCol) + aPerguntas[nCont][1][4]+CRLF+CRLF
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Se as perguntas forem diferentes de MEMO³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If aPerguntas[nCont][1][7] <> "M"
						If aControl[nControl][4] > 0
							cMemoFim += Space(nColR)+ aPerguntas[nCont][2][aControl[nControl][4]][4]+CRLF+CRLF
						Endif
						
					Else
						If aControl[nControl][4] > 0
							cMemoFim += Space(nColR)+ aPerguntas[nCont][2][aControl[nControl][4]][5]+CRLF+CRLF
						Endif
					Endif
					
					EXIT
				Endif
			Endif
		Next nCont
		
	Next nControl

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se o formato do SCRIPT for de PESQUISA ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Percorre os objetos criados dinamicamente³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nAuxFor := Len(aControl)
	For nControl:= 1 TO nAuxFor
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Percorre as perguntas do SCRIPT³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nAuxPer  := Len(aPerguntas)
		For nX := 1 TO nAuxPer
			If (aPerguntas[nX][1][2] == aControl[nControl][1])
				If aControl[nControl][7] <> "C"
					cMemoFim += Space(nCol) + aPerguntas[nX][1][4] + CRLF+CRLF
					Exit
				ElseIf (aControl[nControl][1] <> cIdRespAnt)
					cMemoFim += Space(nCol) + aPerguntas[nX][1][4] + CRLF+CRLF
				EndIf
				cIdRespAnt := aControl[nControl][1]
				Exit
			Endif
		Next nX
		
		If aPerguntas[nX][1][7] == "1" 	// Unica Escolha
			nPosaControl:= aControl[nControl][4]
			If nPosaControl > 0
				cMemoFim += Space(nColR)+ aPerguntas[nX][2][aControl[nControl][4]][4]+CRLF+CRLF
				If AllTrim(aPerguntas[nX][2][aControl[nControl][4]][4]) == "RESPOSTA ABERTA"
					cMemoFim += Space(nColR)+ aControl[nControl][5] +CRLF+CRLF
				EndIf
			Endif	
		ElseiF aPerguntas[nX][1][7] == "2"	// Multipla Escolha

			If aControl[nControl][3]   		// Posicao logica do array que indica se a alternativa foi selecionada              
			
				For nY := 1 To Len(aPerguntas[nX][2])
					If ( aPerguntas[nX][2][nY][2] == aControl[nControl][2] )
						cMemoFim += Space(nColR)+ aPerguntas[nX][2][nY][4]+CRLF+CRLF
					Endif
				Next nY
				
			Endif
			
		ElseiF aPerguntas[nX][1][7] == "3"
			cMemoFim += Space(nColR)+ aControl[nControl][5]+CRLF+CRLF
		Endif                 
		
	Next nAuxFor
	
Endif

oMemoFim:Refresh()

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk380ChgTrºAutor  ³Vendas CRM          º Data ³  13/10/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Atualiza as perguntas a partir da tree                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1 - Objeto da xTree                                     º±±
±±º          ³ExpC2 - Identificado do elemento da arvore correspondente   º±±
±±º          ³ExpA3 - Array com as respostas do script                    º±±
±±º          ³ExpA4 - Array de amarracao entre o no da tree e o painel    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA380                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380ChgTr(oTree,oWizard,aTreeXPanel,aControl)  

Local cIdPergunta := oTree:GetCargo()
Local nPainel := aScan(aTreeXPanel,{|x| x[2] == cIdPergunta})
Local nPos
Local aTmp := {}
Local cPai
Local nPosPai

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Array aTreeXPanel:                                             ³
//³1 - Numero do painel                                           ³
//³2 - Codigo do Raiz do paiz									  ³
//³3 - Array com o(s) Codigo(s) do No da arvore (filho)           ³
//³Caso seja Check Box, senao os dois (pai e filho) sao os mesmos.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If nPainel > 0 //Atualizacao da Pergunta
	oWizard:SetPanel(nPainel)
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Determina o Pai do No³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nPos 	:= aScan(otree:aCargo,{|x| x[1] == cIdPergunta})
	nPosPai := aScan(otree:anodes,{|x| x[2] == oTree:ANODES[npos][1]})
	cPai 	:= oTree:aCargo[nPosPai][1]
	nPos    := aScan(aTreeXPanel,{|x| x[2] == cPai})
	oWizard:SetPanel(nPos)
	If at("-",cIdPergunta) >0 // Radio
		nPos 	:= aScan(aControl,{|x| x[1] == cPai}) //Determina o primeiro Radio
		nSelec := Val(SubStr(cIdPergunta,at("-",cIdPergunta)+1))
		If nSelec > 0
			If aControl[npos][4] > 0 //Desmarca na tree a opcao antiga
				oTree:ChangeBmp("BPMSEDT4","BPMSEDT4",oTree:aCargo[(nPosPai+aControl[npos][4])][1])
			Endif
			oTree:ChangeBmp("BPMSEDT3","BPMSEDT3",cIdPergunta)
			aControl[npos][4] := nSelec
		Endif
	Else
		aTmp    := aTreeXPanel[nPos][3]
		nPainel := aScan(aTmp,{|x| x == cIdPergunta})
		nPos 	 := aScan(aControl,{|x| x[2] == aTmp[nPainel]})
		If nPos >0
			aControl[nPos][3] := !aControl[nPos][3]
			If aControl[nPos][3]
				oTree:ChangeBmp("BPMSEDT3","BPMSEDT3",cIdPergunta)
			Else
				oTree:ChangeBmp("BPMSEDT4","BPMSEDT4",cIdPergunta)
			Endif
		Endif
	Endif
Endif

Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk380ChgCtºAutor  ³Vendas CRM          º Data ³  13/10/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Atualizacao da tree a partir das respostas do usuario       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1 - Objeto da xTree                                     º±±
±±º          ³ExpC2 - Identificado do elemento da arvore correspondente   º±±
±±º          ³ExpA3 - Array com as respostas do script                    º±±
±±º          ³ExpA4 - Array de amarracao entre o no da tree e o painel    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA380                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk380ChgCt(oTree,cCargotree,aControl,aTreeXPanel)

Local cResource    
Local nPos
Local nInicio
Local nX 

If ( aControl[7]  == "R") //Radio
	nInicio := aScan(aTreeXPanel,{|x| x[2] == cCargotree})
	nPos := aScan(oTree:aCargo,{|x| x[1] == cCargotree})//Acha o pai
	For nX := 1 to Len(aTreeXPanel[nInicio][3]) 
		oTree:ChangeBmp("BPMSEDT4"  ,"BPMSEDT4"  ,oTree:aCargo[nPos+nX][1])
	Next nX
	nPos += aControl[4]                            
	oTree:ChangeBmp("BPMSEDT3" ,"BPMSEDT3" ,oTree:aCargo[nPos][1],.T.)
ElseIf  ( aControl[7]  == "C") 
	If aControl[3]
		cResource := "BPMSEDT3" 
	Else
		cResource := "BPMSEDT4" 
	Endif                                       
	oTree:BeginUpdate()
	oTree:ChangeBmp(cResource,cResource,cCargotree,.T.)		
	oTree:EndUpdate()
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk380LoadTºAutor  ³Vendas CRM          º Data ³  13/10/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carga da arvore(xTree) respeitando as cores dos semaforos   º±±
±±º          ³previamente carregados                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1 - Objeto da xTree                                     º±±
±±º          ³ExpA2 - Array com as respostas do script                    º±±
±±º          ³ExpA3 - Array de amarracao entre o no da tree e o painel    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA380                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk380LoadT(oTree, aControl, aTreeXPanel)

Local nPos    
    
For nPos := 1 to Len(aControl)
	If aControl[nPos][7]  == "R"
		If aControl[nPos][4] != Nil
			Tk380ChgCt(@oTree, aControl[nPos][1] ,aControl[nPos],@aTreeXPanel)
		Endif
	Else			                                                              
		Tk380ChgCt(@oTree, aControl[nPos][2],aControl[nPos],@aTreeXPanel)
	Endif
Next nPos

Return Nil
