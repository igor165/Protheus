#INCLUDE "TMKA360B.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "DBTREE.CH"

#DEFINE  PESQUISA "2" // UZ_FORMATO
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao   ³Tk360SimulacaºAutor³Vendas Clientes     º Data ³  07/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.    ³Tela de simulacao o script dinamico. Permite a visualizacao  º±±
±±º         ³do script no formato de execucao.                            º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametro³ EXPN1: Opcao selecionada no Menu.                           º±±
±±º         ³ EXPA2: Array com os dados do Tree.                          º±±
±±º         ³ EXPO3: Objeto do Tree.                                      º±±
±±º         ³ EXPA4: Array com os dados do cargo das respostas para       º±±
±±º         ³        controle de Itens.                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso      ³ AP8-Call Center-Cadastro de Script                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TK360Simulaca(nOpc,aTreeScript,oTreeScript,aAltCargo)

Local oDlgSimula	:= Nil	 					//Dialog de simulacao.

//Array com os panels objeto.
Local aPanel		:= {}						// Array com os panel para exibicao.
Local oPanel		:= Nil						// Objetos para exibicao do Panel
Local nPanel 		:= 1						// Contador do panel ativo.

//Array com os controles dos objetos.
Local aControl		:= {}	    	            // Array de controles do panel - armazena os dados executados.

// Objeto tree da simulacao
Local oTreeSimu		:= Nil						// Objeto tree de simulacao

// Size da Dialog
Local aSizeDlg   	:= MsAdvSize(.T.,.F.,400)	//Array com as dimensoes da tela
Local aSize      	:= {}						// Size da Dialog - Funcao de calculo para coordenadas de tela.
Local aObjects   	:= {} 						// Array de controle utilizado nos calculos de didmensao da Dialog

// Arrays de Posicionamento dos objetos
Local aPosSay   	:= {}						// Array com as posicoes para SAY
Local aPosCab   	:= {}						// Array com as posicoes para Score
Local aPostree  	:= {}						// Array com as posicoes para Tree
Local aPosPanel 	:= {}						// Array com as posicoes para panel
Local aPosLabel 	:= {}						// Array com as posicoes para label
Local aPosMemo  	:= {}						// Array com as posicoes para Memos
Local aPosButton	:= {}						// Array com as posicoes para Botoes

// Score da tela
Local oScore		:= Nil
Local cScore		:= ""

// Controle de largura do tree
Local nWidthTree	:= 0

//Blocos para Ok e Cancel
Local bOk			:= {||(oDlgSimula:End())}
Local bCancel		:= {||(oDlgSimula:End())}

// Variaveis de Memo da tela.
Local oMemoAsk		:= Nil
Local oMemoResp		:= Nil
Local cMemoAsk		:= ""
Local cMemoResp		:= ""

// Objetos do GROUP dos memos
Local oGrp1
Local oGrp2

// Objetos dos botoes de Anterior e Proximo
Local oAnterior
Local oProximo

Local aScript  := aClone(aTreeScript)			// Usa um espelho do tree original do cadastro de Script

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se existem Perguntas e respostas para a exibicao da tela. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Len(aScript) > 0
	MsgStop(STR0001) //"Não existem dados para exibicao do Script."
	Return(.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula automaticamente as dimensoes dos objetos            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ARRAY ASIZE - Tamanho da Dialog e Area de Trabalho										 ³
³	1 - Linha inicial area trabalho 													 ³
³	2 - Coluna inicial area trabalho													 ³
³	3 - Linha final area trabalho														 ³
³	4 - Coluna final area trabalho														 ³
³	5 - Coluna final dialog																 ³
³	6 - Linha final dialog																 ³
³	7 - Linha inicial dialog															 ³
³																						 ³
³ARRAY AOBJECTS - Tamanho "Default" dos Objetos para o calculo das posicoes				 ³
³	1 - Tamanho X																		 ³
³	2 - Tamanho Y																		 ³
³	3 - Dimensiona X																	 ³
³	4 - Dimensiona Y																	 ³
³	5 - Retorna dimensoes X e Y (SIZE) ao inves de linha / coluna final					 ³
³																						 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula as dimensoes do Label do cabecalho                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aObjects := {}

Aadd( aObjects, { 100, 20 , .T., .F. } )
Aadd( aObjects, { 300, 100, .T., .T. } )

aSize      := { aSizeDlg[ 1 ], aSizeDlg[ 2 ], aSizeDlg[ 3 ], aSizeDlg[ 4 ], 0, 0 }
aPosCab  := MsObjSize( aSize, aObjects, .T., .F. )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula as dimensoes entre os campos do cabecalho:Nome,Entidade,Pontos e Telefone  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aObjects := {}

Aadd( aObjects, { 100, 100, .T., .T. } )

aSize      := { aPosCab[1,2], aPosCab[1,1], aPosCab[1,4], aPosCab[1,3], 0, 0 }
aPosSay    := MsObjSize( aSize, aObjects, .T., .T. )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula as dimensoes para as areas que conterao o Tree e os Panels  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aObjects := {}

Aadd( aObjects, { 100, 100, .T., .T. } )
Aadd( aObjects, { 300, 100, .T., .T. } )

aSize      := { aPosCab[2,2], aPosCab[2,1], aPosCab[2,4], aPosCab[2,3], 0, 0 }
aPosTree   := MsObjSize( aSize, aObjects, .T., .T. )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Divide a area do panel entre os objetos oPanel e buttons            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aObjects := {}

Aadd( aObjects, { 150, 100,.T., .T.,.T. } )
Aadd( aObjects, { 100, 50 ,.T., .F.,.F. } )
Aadd( aObjects, { 100, 15, .T., .F.,.T.} )

aSize      := { aPosTree[2,2], aPosTree[2,1], aPosTree[2,4], aPosTree[2,3], 0, 0 }
aPosPanel  := MsObjSize( aSize, aObjects, .T., .F. )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula as dimensoes para os labels dos memos       			    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aObjects := {}

Aadd( aObjects, { 100, 100, .T., .T., .F.} )
Aadd( aObjects, { 100, 100, .T., .T., .F.} )

aSize      := { aPosPanel[2,2], aPosPanel[2,1], aPosPanel[2,4], aPosPanel[2,3], 0, 0 }
aPosLabel  := MsObjSize( aSize, aObjects, .T., .T. )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Divide a area entre os memos de pergunta e resposta 				³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aObjects := {}

Aadd( aObjects, { 100, 100, .T., .T., .T.} )
Aadd( aObjects, { 100, 100, .T., .T., .T.} )

aSize      := { aPosPanel[2,2], aPosPanel[2,1], aPosPanel[2,4], aPosPanel[2,3], 0, 0 }
aPosMemo   := MsObjSize( aSize, aObjects, .T., .T. )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Divide a area entre os botoes de navegacao       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aObjects := {}

Aadd( aObjects, { 35, 11, .F., .F., .T.} )
Aadd( aObjects, { 35, 11, .F., .F., .T.} )
Aadd( aObjects, { 35, 11, .F., .F., .T.} )

aSize      := { aPosPanel[3,2], aPosPanel[3,1], aPosPanel[3,4], aPosPanel[3,3], 0, 0 }
aPosButton := MsObjSize( aSize, aObjects, .T., .T. )


DEFINE MSDIALOG oDlgSimula FROM aSizeDlg[7], 000 TO aSizeDlg[6],aSizeDlg[5] TITLE STR0002 PIXEL //"Simulacao"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria os label para delimitar os objetos do cabecalho             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@aPosCab[1,1],aPosCab[1,2] TO aPosCab[1,3],aPosCab[1,4] LABEL "" OF oDlgSimula PIXEL
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria os say do cabecalho: Nome do Contato, Pontuacao, Entidade e Timer.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@aPosSay[1,1]+5,aPosSay[1,2]+10 SAY STR0003 OF oDlgSimula PIXEL //"Pontuacao: " //"Pontuacao"
	@aPosSay[1,1]+5,aPosSay[1,2]+40 SAY oScore VAR cScore Picture "999999" OF oDlgSimula PIXEL COLOR CLR_RED
	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria o tree com as respostas dadas pelo cliente.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE DBTREE oTreeSimu FROM aPosTree[1,1],aPosTree[1,2] TO aPosTree[1,3], aPosTree[1,4] OF oDlgSimula CARGO
	
	nWidthTree := aPosTree[1,4]*2
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria os Panel e os seus respectivos objetos³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aPanel := Tk360Panel( oDlgSimula ,aScript 	  ,@aControl   ,@aPosPanel,;
	@cMemoResp ,@oMemoResp  ,@oPanel)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria os memos para as perguntas e respostas.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@aPosLabel[1,1],aPosLabel[1,2] GROUP oGrp1 TO aPosLabel[1,3],aPosLabel[1,4] LABEL STR0004 COLOR CLR_RED  OF oDlgSimula PIXEL //"Observações da Pergunta"
	@aPosMemo[1,1]+7,aPosMemo[1,2]+5 GET oMemoAsk    VAR cMemoAsk    MEMO SIZE aPosMemo[1,3]-10,aPosMemo[1,4]-10 READONLY PIXEL OF oDlgSimula
	
	@aPosLabel[2,1],aPosLabel[2,2] GROUP oGrp2 TO aPosLabel[2,3],aPosLabel[2,4] LABEL STR0005 COLOR CLR_BLUE OF oDlgSimula PIXEL //"Observacoes da Resposta"
	@aPosMemo[2,1]+7,aPosMemo[2,2]+5 GET oMemoResp VAR cMemoResp MEMO SIZE aPosMemo[2,3]-10,aPosMemo[2,4]-10 READONLY PIXEL OF oDlgSimula
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria os botoes de navegacao³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@aPosButton[1,1],aPosButton[1,2] BUTTON oAnterior   PROMPT STR0006  SIZE aPosButton[1,3],aPosButton[1,4]+3 OF oDlgSimula PIXEL ; //"<< Voltar"
	ACTION Tk360Show(	1			,@aPanel		,@nPanel	,oAnterior,;
						oProximo	,aScript 		,@oTreeSimu	,aControl,;
						@cMemoAsk  	,@oMemoAsk		,@cMemoResp	,@oMemoResp,;
						@cScore     ,@oScore) //"<< Voltar"
	
	@aPosButton[2,1],aPosButton[2,2] BUTTON oProximo   PROMPT STR0007  SIZE aPosButton[2,3],aPosButton[2,4]+3 OF oDlgSimula PIXEL ; //"Avancar >>"
	ACTION Tk360Show(	2			,@aPanel	 	,@nPanel		,oAnterior,;
						oProximo	,aScript 		,@oTreeSimu		,aControl,;
						@cMemoAsk   ,@oMemoAsk   	,@cMemoResp		,@oMemoResp,;
						@cScore     ,@oScore) //"Avancar >>"
	
	Tk360Show(	0				,@aPanel	 	,@nPanel	,oAnterior,;
				oProximo		,aScript 		,@oTreeSimu	,aControl,;
				@cMemoAsk   	,@oMemoAsk	  	,@cMemoResp	,@oMemoResp,;
				@cScore         ,@oScore)
	

ACTIVATE MSDIALOG oDlgSimula ON INIT (EnchoiceBar(oDlgSimula,bOk,bCancel))

Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360Panel      ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria os panel's que conterao os objetos para simulacao.               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Retorno com um ARRAY de Panel - com a pergunta e respostas criadas    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametro ³EXPO1: Dialog de simulacao para (dialogo pai).               			º±±
±±º          ³EXPA2: Array com as perguntas e respostas.                   			º±±
±±º          ³EXPA3: Array com os controles das respostas dadas.           			º±±
±±º          ³EXPA4: Array com as posicoes para criacao dos objetos.       			º±±
±±º          ³EXPC5: Variavel para memo de resposta.                       			º±±
±±º          ³EXPO6: Objeto do Memo de resposta.                           			º±±
±±º          ³EXPO7: Objeto do Panel.                                      			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP7-Call Center-Cadastro de Script                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º                Alteracoes desde sua criacao                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º   DATA   ³ Programador   ³Manutencao Efetuada                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º26/06/03  ³ RAFAEL Q.     ³Tratamento de aspas nos textos de perguntas.          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360Panel(	oDlgSimula ,aScript   ,aControl  ,aPosObj	,;
						cMemoResp  ,oMemoResp ,oPanel)

Local nItem     := 0				// Posicao dentro do ARRAY com as perguntas e respostas
Local oScroll	:= Nil				// Objeto SCROLL para rolagem da tela.
Local aMemos    := {}				// Array com os memos da tela.
Local aResultado:= {}				// Array com os objetos Panel criados de acordo com o numero de perguntas do script
Local cPergunta := ""				// Descricao da pergunta que sera criada para o panel
Local aResposta := {}				// Array com com as respostas da pergunta criada
Local cTpObj 	:= ""				// Tipo do objeto de resposta
Local bBlock	:= {||}				// Exeblock
Local oSay		:= Nil				// Objeto SAY que sera criado dinamicamente para cada Pergunta
Local aSaveScr  := aClone(aScript)	// Salva array para posterior restauracao.
Local cPergAux	:= ""				// Variavel auxiliar para tratamento de perguntas
Local nAspas	:= 0				// Variavel para For utilizado no tratamento das perguntas.

For nItem := 1 To Len(aScript)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria um panel para conter cada conjunto de perguntas e respostas.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	/* 
	Painel criado atraves do construtor devido a necessidade da utilizacao do laco.
	
	  [ <oPanel> := ] TPanel():New( <nRow>, <nCol>, <cText>,;
										[<oWnd>], <oFont>, <.lCenter.>, <.lRight.>, ;
										<nClrText>, <nClrBack>, <nWidth>, <nHeight>, <.lLowered.>,;
										<.lRaised.> )
	*/
	oPanel:= &("oPanel"+ StrZero(nItem)):= TPanel():New(	aPosObj[1,1], aPosObj[1,2], "",;
															oDlgSimula, NIL, .T., .F.,;
															NIL, NIL, aPosObj[1,3], aPosObj[1,4], .F., .F. )
	oPanel:Cargo := ''
	
	Aadd(aResultado,oPanel)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria um scrollbox para conter os objetos.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@00,00 SCROLLBOX oScroll VERTICAL SIZE aPosObj[1,4],aPosObj[1,3] OF oPanel BORDER
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria a pergunta do Script 				³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cPergunta := ""         
	cPergAux  := ""
	
	If aScript[nItem][3] <> NIL
		cPergunta := aScript[nItem][3]
		

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Tratamento para o controle do texto da pergunta.            ³
		//³Se existirem aspas identicas a utilizada na Macro o sistema ³
		//³acusara erro de sintaxe.                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nAspas:=1 To Len(cPergunta)
			If SubStr(cPergunta,nAspas,1)= "'"
				cPergAux += '"'
			Else
				cPergAux += SubStr(cPergunta,nAspas,1)
			EndIf
		Next	
		cPergunta := cPergAux
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Cria um objeto say para a pergunta com o texto em vermelho   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		bBlock  := &("{|| '" + AllTrim(cPergunta) + "'}")
		
		/*Cria as perguntas (Objeto SAY)
		
		Say criado atraves de Contrutor devido a utilizacao do bloco de codigo.
		
		[ <oSay> := ] TSay():New( <nRow>, <nCol>, <{cText}>,;
								[<oWnd>], [<cPict>], <oFont>, <.lCenter.>, <.lRight.>, <.lBorder.>,;
								<.lPixel.>, <nClrText>, <nClrBack>, <nWidth>, <nHeight>,;
								<.design.>, <.update.>, <.lShaded.>, <.lBox.>, <.lRaised.> )
		*/
		
		oSay:= TSay():New(	10, 5, bBlock,;
							oScroll, NIL, NIL, .F., .F., .F.,;
							.T., CLR_RED, NIL, 215, 60,;
							.F., .F., .F., .F., .F. )
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria as respostas do Script  			³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (aScript[nItem,5] <> NIL) .AND. !Empty(aScript[nItem,5])
		
		cIdResp		:= aScript[nItem][2]
		cTpObj  	:= aScript[nItem][5]
		aResposta 	:= aScript[nItem][6]
		
		Tk360Resposta(	aResposta	,cTpObj			,@oScroll	,@aControl,;
						cIdResp 	,aPosObj[1]		,@cMemoResp	,@oMemoResp,;
						nItem 		,@aMemos)
		
		aResposta := {}
	Endif
	
Next nItem
aScript  := aClone(aSaveScr)

Return(aResultado)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360Resposta   ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria as respostas para apresentacao no panel da tela de simulacao.    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametro ³EXPA1: Array com as respostas.                               			º±±
±±º          ³EXPC2: Tipo do objeto para respostas.                        			º±±
±±º          ³EXPO3: Objeto Scroll.                                        			º±±
±±º          ³EXPA4: Array de controle das respostas informadas.           			º±±
±±º          ³EXPC5: Id de controle da resposta (cargo)                   			º±±
±±º          ³EXPA6: Coordenada para Panel.                                			º±±
±±º          ³EXPC7: Texto para memo de resposta.                          			º±±
±±º          ³EXPO8: Objeto para memo de respostas.                        			º±±
±±º          ³EXPN9: Item do tree onde esta posicionada a resposta.         		º±±
±±º          ³EXPN10:Array de Memos para Resposta Aberta.	              			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360Resposta(	aResposta	,cTpObj		,oScroll	,aControl,;
						cIdResp		,aAreaPanel	,cMemoResp	,oMemoResp,;
						nItems      ,aMemos )

Local nResposta	:= 0					// Controle das respostas
Local nTop      := 50					// Posicicao para criacao dos objetos

Local aSaveResp := aClone(aResposta)	// Efetua backup das resposta para restauracao posterior.

Local bVar		:= {||}        			// Execblock
Local cControl  := ""					// Utilizado para calcular a linha da observação com maior tamanho

//Detalhes do Radio
Local oRadio	:= Nil					// Objeto RADIO da resposta
Local aIdResp 	:= {}					// Array icn os Textos das respostas
Local aIdMemo	:= {}					// Array com os Ids de Memo
Local nCont		:= 0					// Variavel auxiliar
Local nPos      := 0					// Variavel auxiliar
Local nFoundMM  := 0					// Variavel auxiliar

//Detalhes do CheckBox
Local aRespAux	:= {}					// Array utilizada para auxiliar no tratamento da resposta
Local oCheck	:= Nil  				// Objeto de CHEKBOX
Local oGet		:= Nil					// Objeto para o GET
Local cIdAnswer := ""					// Codigo da Resposta - Codigo do Cargo (TREE)
Local cAnswer   := ""					// Descricao da Resposta
Local nHeight   := 0					// Calculo da ALTURA
Local nWidth    := 0					// Calculo da LARGURA
Local nMaxWidth	:= 0					// Calculo da LARGURA máxima
Local nTopAsk   := 0    				// Calculo do TOP
Local nString   := 0					// Contador
Local aString   := {}					// Se a resposta for muito grande ela será quebrada nessa array
Local bWhen     := {|| .T.} 			// Habilita get

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se o tipo de objeto a ser criado é Multipla Escolha, já    ³
//³calcula o tamanho máximo da resposta, assim os complementos³
//³serão apresentados de forma alinhada.                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (cTpObj == "2")
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calcula o tamanho máximo das respostas.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCont:=1 To Len(aResposta)
		nMaxWidth := Max(Tk360CalcWidth(Alltrim(aResposta[nCont][2])+Space(5),aAreaPanel[3]), nMaxWidth)
	Next nCont	
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica qual o tipo do objeto a ser criado para cada resposta.³
//³1 - Radiobutton (Unica Escolha)                                ³
//³2 - Checkbox (Multipla Escolha)                                ³
//³3 - Memo (Dissertativa)                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (cTpObj == "3")
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria um memo para a inclusao da resposta do cliente.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Tk360MemRes(@nTop,oScroll,@aControl,cIdResp,aAreaPanel)
	
Else
	
	For nResposta:= 1 To Len(aResposta)
		
		Do Case
			
			Case (cTpObj == "1") //Unica Escolha (Radio)
							
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Cria um radio unico com todas as respostas da pergunta.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Aadd(aMemos,{ ("oMemoRadio"+Alltrim(Transform(nItems,"99"))),""}) //Criacao de memos de acordo com a quantidade de panels
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Adiciona o objeto no array de controle			  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³1 - Id da Pergunta																		 ³
				³2 - Id da Resposta																		 ³
				³3 - Conteudo do Checkbox (.T. ou .F.)													 ³
				³4 - Numero da Opcao do Radio Selecionado												 ³
				³5 - Texto da resposta dissertativa														 ³
				³6 - Texto do get de Complemento de Resposta											 ³
				³7 - Tipo de objeto (C - Checkbox, R - Radiobutton M - Memo)							 ³
				³																						 ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				*/
				AAdd(aControl,{ cIdResp,;
									NIL,;
									NIL,;
									NIL,;
									Space(255),;
									NIL,;
									"R"})
				
				nPos := Len(aControl)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica o tamanho da string para ajusta-la		  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				aIdResp:= aClone(aResposta)
				For nCont := 1 TO LEN(aResposta)
					Aadd(aIdMemo,aResposta[nCont][5])
				Next nCont
				
				AEval(aResposta,{|x| x := x[2],nWidth:= MAX(GetTextWidth(0,x)/1.5,nWidth)})
				
				nWidth   := IIf(nWidth > 200,200,nWidth)
				
				aControl[Len(aControl)][4]:= 0
				cControl := AllTrim(Str(Len(aControl)))
				
				bVar     := &( "{|x|If(ValType(x)=='U',aControl["+cControl+"][4],aControl["+cControl+"][4]:=x) }" )
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Cria um radiobutton para cada resposta da pergunta ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				/*
				
				Radio criado atraves do construtor devido a utilizacao do bloco de codigo.
				
				[ <oRadMenu> := ] TRadMenu():New(<nRow>, <nCol>, {<cItems>},;
												[bSETGET(<nVar>)], <oWnd>, [{<nHelpId>}], <{uChange}>,;
												<nClrFore>, <nClrBack>, <cMsg>, <.update.>, <{uWhen}>,;
												<nWidth>, <nHeight>, <{uValid}>, <.lDesign.>, <.lLook3d.>, <.lPixel.> )
				*/
				
				
				oRadio:= TRadMenu():New( 	nTop, 10, aResposta,;
											bVar, oScroll , NIL, {||(	Tk360ItemMemo(@cMemoResp,@oMemoResp,,aIDMemo,oRadio:nOption),;		//observacao da REPOSTA - Cadastro
											Tk360DesMemo(aResposta,@aMemos[nItems][1],oRadio:nOption))},; //Habilita/Desabilita o MEMO de RADIO
											CLR_BLUE, NIL, NIL, .F., {||.T.},;
											nWidth, 10, NIL, .F., .T., .T. )
				
				oRadio:bChange 	:= {||	Tk360ItemMemo(@cMemoResp,@oMemoResp,,aIdMemo,oRadio:nOption),;
				Iif(Ascan(aResposta,{|x| ALLTRIM(x) == UPPER(STR0008)})>0,(Tk360DesMemo(aResposta,@aMemos[nItems][1],oRadio:nOption)),"")} //"RESPOSTA ABERTA"
				
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Ajusta o TOP do proximo objeto.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nTop+= 10
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Cria o objeto MEMO quando a descricao for "RESPOSTA ABERTA"³
				//³Nesse memo quando houver uma alteracao da descricao o ARRAY³
				//³sera atualizado na 5 posicao - Resposta Dissertativa       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				Tk360CriaMemo(@aMemos,nTop,oScroll,aResposta,@aControl, nItems)
				
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica se existem respostas abertas, que caracterizam a criacao do objeto Memo para que a funcao de   ³
				//³abaixo seja executada.                      															   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nFoundMM := Ascan(aResposta,{|x| ALLTRIM(x) == UPPER(STR0008)})  //RESPOSTA ABERTA //"RESPOSTA ABERTA"
				If nFoundMM > 0
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Habilita e desabilita o MEMO do RADIO                      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Tk360DesMemo(aResposta,@aMemos[nItems][1] ,oRadio:nOption)
					
				Endif	
				
				Exit
				
			Case (cTpObj == "2") //Multipla Escolha (Checkbox)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Reinicia as variáveis.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				aRespAux	:= aClone(aResposta[nResposta])		// Array com a resposta que será tratada
				cControl	:= ""								// Número do controle
				cIdAnswer	:= ""								// Codigo da Resposta - Codigo do Cargo (TREE)
				cAnswer		:= ""								// Descricao da Resposta
				nHeight		:= 0								// Calculo da ALTURA
				nWidth		:= 0								// Calculo da LARGURA
				nTopAsk		:= 0								// Calculo do TOP
				nString		:= 0								// Contador
				aString		:= {}								// Texto da resposta dividido. Cada posição contém uma string com o máximo permitido de caracteres.
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Cria um checkbox para cada resposta das perguntas.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cIdAnswer:= aRespAux[1]											// Codigo do Cargo (TREE)
				cAnswer	 := AllTrim(aRespAux[2])								// Descricao da Resposta
				nHeight  := Tk360CalcHeight(cAnswer,@aString,aAreaPanel[3])	// Calcula a ALTURA
				nWidth   := Tk360CalcWidth(cAnswer+Space(5),aAreaPanel[3])		// Calcula a LARGURA
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Adiciona o objeto no array de controle.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Aadd(aControl,{cIdResp,cIdAnswer,NIL,NIL,NIL,Space(40),"C"})
				
				cControl := AllTrim(Str(Len(aControl)))
				
				aControl[Len(aControl)][3]:= .F.
				
				bVar     := &( "{|x|If(ValType(x)=='U',aControl["+cControl+"][3],aControl["+cControl+"][3]:=x) }" )
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Cria um checkbox para cada resposta da pergunta.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				/* 

				CheckBox criado atraves do construtor devido a utilizacao do bloco de codigo devido a criacao dinamica.

				 [ <oCbx> := ] TCheckBox():New(  <nRow>, <nCol>, <cCaption>,;
													[bSETGET(<lVar>)], <oWnd>, <nWidth>, <nHeight>, <nHelpId>,;  //Largura Altura
													[<{uClick}>], <oFont>, <{ValidFunc}>, <nClrFore>, <nClrBack>,;
													<.design.>, <.pixel.>, <cMsg>, <.update.>, <{WhenFunc}> )
				*/
				
				oCheck   := TCheckBox():New(	nTop, 10, aString[1],;
												bVar, oScroll, nWidth, 10, NIL,;
												&('{||Tk360ItemMemo(@cMemoResp,@oMemoResp,"'+aRespAux[5]+'")}'), NIL, NIL, CLR_BLUE, NIL,;
												.T., .T., NIL, .F., {||.T.} )
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se a RESPOSTA for muito grande, quebra em varias linha ³
				//³utilizando-se do Checbox para a primeira linha e do Say³
				//³para as outras linhas.                                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nTopAsk:= nTop
				For nString:= 2 To Len(aString)
					
					nTopAsk += 10
					
				Next nString
				
				If aRespAux[4] == "1"
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Cria um get para as respostas complementares.³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aControl[Len(aControl)][6]:= Space(150)
					bVar:= &( "{|x|If(ValType(x)=='U',aControl["+cControl+"][6],aControl["+cControl+"][6]:=x) }" )
				
					/*                      
					
					Get criado atraves do construtor devido a utilizacao do bloco de codigo.
					
					[ <oGet> := ] TGet():New( 	<nRow>, <nCol>, bSETGET(<uVar>),;
												[<oWnd>], <nWidth>, <nHeight>, <cPict>, <{ValidFunc}>,;
												<nClrFore>, <nClrBack>, <oFont>, <.design.>,;
												<oCursor>, <.pixel.>, <cMsg>, <.update.>, <{uWhen}>,;
												<.lCenter.>, <.lRight.>,[\{|nKey, nFlags, Self| <uChange>\}], <.readonly.>,;
												<.pass.>, [<.lNoBorder.>], <nHelpId> )
					*/
	
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Se a resposta utilziar complemento  cria o get para complemento de respostas.     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						oGet:= TGet():New( 	nTop, nMaxWidth, bVar,;
											oScroll, TamSX3("UK_COMPRES")[1], 6, "@!", NIL,;
											NIL, NIL, NIL, .F.,;
											NIL, .T., NIL, .F., bWhen,;
											.F., .F., NIL, .F., .F., NIL, NIL)
						
				EndIf
	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Ajusta o Top do proximo objeto.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nTop+= nHeight
		EndCase
		
	Next nResposta
Endif

aResposta := aClone(aSaveResp)

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360ItemMemo   ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Atualiza os textos nos memos para apresentacao.                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametro ³EXPC1:Variavel do objeto memo.                               			º±±
±±º          ³EXPO2: Objeto memo de resposta.                              			º±±
±±º          ³EXPC3: Texto para Memo.                                      			º±±
±±º          ³EXPC4: Array com todos os textos de memo. Param. utilizado pelo radio.º±±
±±º          ³EXPC5: Posicao selecionada no radio e que sera a mesma do array de memº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360ItemMemo(cMemoResp,oMemoResp,cMemoItem,aIdMemo,nPos)

cMemoResp:= ""		//Texto para memos
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Avalia se o array de memos esta preenchido.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If (aIdMemo) <> NIL
	cMemoResp := aIdMemo[nPos]
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza o Memo de auxilio. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ElseIf  !Empty(cMemoItem)
	cMemoResp:= cMemoItem
Endif

oMemoResp:Refresh()

Return(.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360MemRes     ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria os Memos para respostas dissertativas.                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³EXPN1: Coodenada para criacao do objeto.                     			º±±
±±º          ³EXPO2: Objeto Scroll.                                        			º±±
±±º          ³EXPA3: Array de controle das respostas informadas.           			º±±
±±º          ³EXPC4: Id de controle da resposta (cargo)                   			º±±
±±º          ³EXPA5: Coordenada para Panel.                                			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360MemRes(nTop,oScroll,aControl,cIdResp,aAreaPanel)

Local oMemo										//Objeto MEMO
Local bVar										//Execblock
Local cControl := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona o objeto no array de controle.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Aadd(aControl,{cIdResp,NIL,NIL,NIL,Space(255),NIL,"M"})

nWidth   := aAreaPanel[3] - 15
nHeigth  := aAreaPanel[4] - nTop - 15
cControl := AllTrim(Str(Len(aControl)))
aControl[Len(aControl)][5]:= ""
bVar     := &( "{|x|If(ValType(x)=='U',aControl["+cControl+"][5],aControl["+cControl+"][5]:=x) }" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria um memo para que possa ser armazenado a resposta dada  pelo   ³
//³cliente, ja que esta pergunta não possui respostas pre-formatadas  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*
  MultiGet criado atraves do construtor devido a utilizacao do bloco de codigo e criacao dinamica.

   [ <oGet> := ] TMultiGet():New( <nRow>, <nCol>, bSETGET(<uVar>),;
									[<oWnd>], <nWidth>, <nHeight>, <oFont>, <.hscroll.>,;
									<nClrFore>, <nClrBack>, <oCursor>, <.pixel.>,;
									<cMsg>, <.update.>, <{uWhen}>, <.lCenter.>,;
									<.lRight.>, <.readonly.>, <{uValid}>,;
									[\{|nKey, nFlags, Self| <uChange>\}], <.lDesign.>,;
									[<.lNoBorder.>], [<.lNoVScroll.>] )

*/

oMemo:=	TMultiGet():New( 	nTop, 10, bVar,;
							oScroll, nWidth, nHeigth, NIL, .F.,;
							NIL, NIL, NIL, .T.,;
							NIL, .F., {||.T.}, .F.,;
							.F., NIL, NIL,;
							NIL, .F., NIL, NIL)


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ajusta o TOP do proximo objeto.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTop:= nTop + 10

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360DesMemo    ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Habilita e dasabilita os Memos de respostas abertas de acordo com a   º±±
±±º          ³selecao do radio.                                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³EXPA1: Array com as respostas disponiveis.                   			º±±
±±º          ³EXPO2: Objeto Memo.                                          			º±±
±±º          ³EXPN3: Selecao do radio.                                     			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
function Tk360DesMemo(aResposta,oMemoRadio,nSel)

Local nContFor:= 0  // variavel auxiliar.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se a existe resposta aberta.                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

nContFor := Ascan(aResposta,{|x| ALLTRIM(x) == UPPER(STR0008)}) //"RESPOSTA ABERTA"


If nContFor > 0
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se a resposta aberta foi selecionada.                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	If nContFor == nSel
		oMemoRadio:Enable()
		oMemoRadio:SetFocus()
	Else
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³So permite a digitacao se usuario escolher o RADIO desse MEMO (o ultimo)³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oMemoRadio:Disable()
	Endif
Endif

Return(.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TK360CriaMemoºAutor³Vendas Clientes    º Data ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Cria o objeto Memo para as respostas abertas de Scripts    º±±
±±º          ³de atendimento.                                             º±±
±±º          ³  O objeto memo e proveniente de um array pois a quantidade º±±
±±º          ³de panels sao indefinidas de acordo com o cadastro.         º±±
±±º          ³  Para cada objeto MEMO existira uma posicao no array aMemosº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ aMemos: Array com os objetos e Variaveis  dos MEMOS.       º±±
±±º          ³          aMemos[1]- objeto                                 º±±
±±º          ³          aMemos[2]- Conteudo do Memo                       º±±
±±º          ³ nTop    :Variavel contendo a posicao TOP do primeiro Radio º±±
±±º          ³         do Panel                                           º±±
±±º          ³oScroll: Panel                                              º±±
±±º          ³aResposta: Array com os textos dos RADIOS.                  º±±
±±º          ³aControl : Array de controle das acoes no Panel.            º±±
±±º          ³nItems   : Panel para criacao do objeto.                    º±±
±±º          ³nOpc     : Opcao selecionada no Menu.                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360CriaMemo(	aMemos		,nTop	,oScroll	,aResposta,;
						aControl	,nItems)//nItems = panel
Local nContFor:= 0		//Variavel auxiliar do for


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se nas respostas do Panel existe a descricao RESPOSTA ABERTA.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

nContFor := Ascan(aResposta,{|x| ALLTRIM(x) == UPPER(STR0008)}) //"RESPOSTA ABERTA"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se existir uma resposta aberta cria o Memo para esta resposta.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nContFor > 0
	@ nTop+(Len(aResposta)*10), 10 GET (aMemos[nItems][1]) VAR  aMemos[nItems][2] OF oScroll MULTILINE   SIZE 250, 40 PIXEL VALID aControl[nItems][5] := aMemos[nItems][2]
	
Endif

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360CalcHeight ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Calcula a altura dos objetos.                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Numerico                                                     			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³EXPC1: Texto                                                 			º±±
±±º          ³EXPO2: Array de textos.                                      			º±±
±±º          ³EXPN3: Largura possivel.                                     			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360CalcHeight(cCaption,aString,nWidthArea)

Local nHeight:= 0		//ALTURA
Local nWidth := 0		//LARGURA
Local nTam   := 0		//TAMANHO
Local cAux   := ""		//Variavel de auxilio

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula a altura dos objetos a serem criados para o script de pesquisa³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nWidth:= GetTextWidth(0,cCaption)
nWidthArea+= (nWidthArea * 1.5)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se o tamanho da string for maior que o tamanho maximo permitido, quebra a string em varias linhas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (nWidth > nWidthArea)
	
	For nTam:= 1 To Len(cCaption)
		cAux+= SubStr(cCaption,nTam,1)
		
		If (GetTextWidth(0,cAux) >= nWidthArea)
			Aadd(aString,cAux)
			cAux:= ""
		Endif
	Next nTam
	
	Aadd(aString,cAux)
	nHeight:= 10 * Len(aString)
Else
	Aadd(aString,cCaption)
	nHeight:= 10
Endif

Return(nHeight)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360CalcWidth  ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Calcula a altura dos objetos.                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Numerico                                                     			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³EXPC1: Texto                                                 			º±±
±±º          ³EXPN2: Largura possivel.                                     			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360CalcWidth(cCaption,nWidthArea)

Local nWidth := 0		//LARGURA

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula a altura dos objetos a serem criados para o script de pesquisa ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nWidth	:= GetTextWidth(0,cCaption)
nWidthArea += (nWidthArea * 4)

If nWidth > nWidthArea
	nWidth	:= nWidthArea
Else
	nWidth	:= nWidth
Endif

Return(nWidth)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360Show       ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Exibe Panel com objetos.                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³EXPN1:  Botao selecionado.                                       		º±±
±±º          ³EXPA2:  Array com os panel para exibicao.                     		º±±
±±º          ³EXPN3:  Panel posicionado.                                      		º±±
±±º          ³EXPO4:  Objeto do botao voltar.                               		º±±
±±º          ³EXPO5:  Objeto do botao avancar.                              		º±±
±±º          ³EXPA6:  Array com os itens para exibicao.                     		º±±
±±º          ³EXPO7:  Objeto tree.                                          		º±±
±±º          ³EXPA8:  Array de controle.                                    		º±±
±±º          ³EXPC9:  Variavel para objeto memo da pergunta.                		º±±
±±º          ³EXPO10: Objeto Memo de pergunta.                              		º±±
±±º          ³EXPC11:  Variavel para objeto memo da resposta.                		º±±
±±º          ³EXPC12: Objeto Memo das respostas .                              		º±±
±±º          ³EXPO13: Variavel de controle da pontuacao do score             		º±±
±±º          ³EXPC14: Objeto para atualizacoa do Score.                        		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360Show(	nAction		,aPanel		,nPanel   	,oAnterior,;
					oProximo	,aScript	,oTreeSimu	,aControl,;
					cMemoAsk	,oMemoAsk	,cMemoResp	,oMemoResp,;
					cScore      ,oScore)

Local nWindow	:= nPanel		//Indica o panel
Local nI		:= 0            //Controle de contador
Local lNext		:= .F.			//Flag de controle
Local lNotChg   := .F.			//Variável de controle para troca de panel. Caso Nao seja
//selecionada nenhuma resposta nao podera ser selecionado o botão Next.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica qual o panel que devera ser ativo, habilita e desabilita o atual³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Do Case
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Habilita o primeiro panel pois e a tela inicial ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nAction == 0 )
		For nI := 2 To Len(aPanel)
			aPanel[nI]:Hide()
		Next nI
		
		aPanel[1]:Show()
		oAnterior:Disable()
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Habilita o panel ANTERIOR ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nAction == 1 )
		Tk360Prev(@nWindow,aScript,aControl)
		oProximo:Enable()
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Habilita o PROXIMO panel  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nAction == 2 )
		lNext := Tk360Next(@nWindow,aScript,aControl,@lNotChg)
		oAnterior:Enable()
		
EndCase



//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se nenhuma resposta for informada o panel nao sera alterado.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lNotChg
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ ¿
	//³Habilita/Desabilita os botoes <<Voltar e >>Avancar.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ Ù
	If ( nWindow >= Len(aPanel) ) .OR. (lNext .AND. nPanel == nWindow)
		If !(lNext .AND. nPanel == nWindow)
			nWindow := Len(aPanel)
		Endif
		oProximo:Disable()
		oProximo:Refresh()
	Endif
	
	If ( nWindow <= 1 ) 
		nWindow := 1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Habilita o botao Voltar caso o usuario esteja no primeiro painel e nao ³
		//³existam perguntas a serem executadas para a resposta selecionada.      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nAction == 2 //Botao Avancar
			oAnterior:Enable()
			oAnterior:Refresh()
		Else
			oAnterior:Disable()
			oAnterior:Refresh()
		Endif	
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza o Memo da Pergunta ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cMemoAsk:= ""
	If !Empty(aScript[nWindow,4])
		cMemoAsk:= aScript[nWindow,4]
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Limpa o Memo da Resposta    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cMemoResp:= ""
	oMemoAsk:Refresh()
	oMemoResp:Refresh()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados do historico das perguntas ja respondidas ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Tk360SiTree(nPanel,aScript,@oTreeSimu,aControl,@cScore,@oScore)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Habilita/Desabilita os panels. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aPanel[nPanel]:Hide()
	aPanel[nWindow]:Show()
	
	nPanel := nWindow
Endif
Return(.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360SiTree     ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Atualiza Tree de Simulacao com as respostas ja dadas.                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³EXPN1:  Painel posicionado.                                      		º±±
±±º          ³EXPA2:  Array com os dados do Tree.                           		º±±
±±º          ³EXPO3:  Objeto do Tree.                                         		º±±
±±º          ³EXPA4:  Array com os controles de respostas.                  		º±±
±±º          ³EXPO5:  Variavel de controle do score.                          		º±±
±±º          ³EXPA6:  Objeto para score.                                    		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360SiTree(	nPanel	,aScript	,oTreeSimu	,aControl,;
						cScore	,oScore)

Local nControl  := 0		//Controle da pergunta
Local nItem     := 0		//Laco do array aScript que possui os itens do tree.
Local nScore    := 0		//Score
Local nResposta := 0		//Controle das respostas
Local cAsk      := ""		//Texto da Pergunta
Local cAnswer   := ""		//Texto da Resposta
Local cCodAsk   := ""		//Codigo da pergunta
Local cCodAnswer:= ""		//codigo da resposta
Local cCodPai   := ""		//Codigo da pergunta pai
Local cTipoObj  := ""		//Tipo do objeto selecionado
Local lFirst    := .T.		//Flag de controle

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Limpa o tree.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oTreeSimu:Reset()
oTreeSimu:BeginUpdate()
CursorWait()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ ¿
//³Monta o tree atraves do array com todas as perguntas e respostas.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ Ù
For nItem:= 1 To Len(aScript)
	cCodPai := aScript[nItem][1]
	cCodAsk := aScript[nItem][2]
	cAsk    := aScript[nItem][3]
	cTipoObj:= aScript[nItem][5]
	lFirst  := .T.
	
	Do Case
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se a resposta for um radiobutton verifica qual a opcao que foi³
		//³marcada para montar o tree.                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case (cTipoObj == "1")        //RadioButton
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se existe a pergunta. ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nControl:= Ascan(aControl,{|aVal| aVal[1]== cCodAsk})
			If (nControl > 0)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica se foi selecionado alguma resposta.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nResposta:= aControl[nControl][4]
				If(nResposta > 0)
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Adiciona a Pergunta no Tree.³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If oTreeSimu:TreeSeek(cCodPai)
						oTreeSimu:AddItem(Alltrim(cAsk) , cCodAsk, "FOLDER7","FOLDER8", , ,2)
					ElseIf (cCodPai=='0000000')
						oTreeSimu:AddItem(Alltrim(cAsk)+Space(10) , cCodAsk, "FOLDER7","FOLDER8", , ,1)
					Endif
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Adiciona a Resposta no Tree.³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cCodAnswer:= aScript[nItem][6][nResposta][1]
					cAnswer   := AllTrim(aScript[nItem][6][nResposta][2])
					nScore    += aScript[nItem][6][nResposta][3]
					
					If oTreeSimu:TreeSeek(cCodAsk)
						oTreeSimu:AddItem(Alltrim(cAnswer),cCodAnswer, "FOLDER12","FOLDER13", , ,2)
					Endif
				Endif
			Endif
			
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a resposta for um checkbox, exibe todas as respostas³
			//³que foram selecionadas.                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case (cTipoObj == "2")        //Checkbox
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica quais as respostas que foram selecionadas.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			For nControl:= 1 To Len(aControl)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica se existe a pergunta. ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If (aControl[nControl][1] == cCodAsk) .AND. aControl[nControl][3]
					nResposta:= Ascan(aScript[nItem][6],{|aVal| aVal[1]== aControl[nControl][2]})
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Verifica se a resposta foi selecionada.³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If (nResposta > 0)
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Imprime a peregunta somente uma vez.³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If lFirst
							
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Adiciona a Pergunta no Tree.³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If oTreeSimu:TreeSeek(cCodPai)
								oTreeSimu:AddItem(Alltrim(cAsk) , cCodAsk, "FOLDER7","FOLDER8", , ,2)
							Else
								oTreeSimu:AddItem(Alltrim(cAsk) , cCodAsk, "FOLDER7","FOLDER8", , ,1)
							Endif
							
							lFirst:= .F.
						Endif
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Adiciona a Resposta no Tree.³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						cCodAnswer:= aScript[nItem][6][nResposta][1]
						cAnswer   := AllTrim(aScript[nItem][6][nResposta][2]) + " - "  + Alltrim(aControl[nControl][6])
						nScore    += aScript[nItem][6][nResposta][3]
						
						oTreeSimu:TreeSeek(cCodAsk)
						oTreeSimu:AddItem(Alltrim(cAnswer),cCodAnswer, "FOLDER12","FOLDER13", , ,2)
					Endif
				Endif
				
			Next nControl
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a resposta for um memo exibe o que foi informado pelo³
			//³contato.                                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case (cTipoObj == "3")         //Memo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se existe a pergunta. ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nControl:= Ascan(aControl,{|aVal| aVal[1]== cCodAsk})
			If (nControl > 0)
				If !Empty(aControl[nControl][5])
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Adiciona a Pergunta no Tree.³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If oTreeSimu:TreeSeek(cCodPai)
						oTreeSimu:AddItem(Alltrim(cAsk) , cCodAsk, "FOLDER7","FOLDER8", , ,2)
					Else
						oTreeSimu:AddItem(Alltrim(cAsk) , cCodAsk, "FOLDER7","FOLDER8", , ,1)
					Endif
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Adiciona a Resposta no Tree.³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cCodAnswer:= "M" + cCodAsk
					cAnswer   := AllTrim(aControl[nControl][5])
					nScore    += 0
					
					oTreeSimu:TreeSeek(cCodAsk)
					oTreeSimu:AddItem(Alltrim(cAnswer),cCodAnswer, "FOLDER12","FOLDER13", , ,2)
				Endif
			Endif
	EndCase
Next nItem

cScore:= AllTrim(Str(nScore))
oScore:Refresh()

oTreeSimu:EndUpdate()
CursorArrow()

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360Next       ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Avanca panel                                                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³EXPN1:  Painel posicionado.                                      		º±±
±±º          ³EXPA2:  Array com os dados do Tree.                           		º±±
±±º          ³EXPA3:  Array com os controles de respostas.                  		º±±
±±º          ³EXPl4:  Flag que define se houve alteracao no objeto.         		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360Next(nPanel,aItems,aControl,lNotChg)

Local nPos    := 0		//Variavel de auxilio, com base nesta variavel sera definido o proximo panel a ser exibido.
Local cSup    := ""		//Armazena a resposta efetuada.
Local nControl:= 0		//Contador
Local nOpcao  := 0      //Opcao selecionada.
Local nItem	  := 0 		//Contador
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica qual o proximo panel a ser exibido.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (M->UZ_FORMATO == PESQUISA)
	nPanel ++
Else
	
	For nControl:= 1 To Len(aControl)//Array de controle dos itens das respostas
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Pesquisa em todas as perguntas a resposta efetuada através do ID que define a amarracao(aControl[X][1] )         ³
		//³Pesquisa com base no panel (aControl[nPanel][1]) ja que os panels possuem a mesma ordem de criacao das perguntas.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		nPos:= Ascan(aItems,{|aVal| aVal[2]== aControl[nPanel][1]})
		If (nPos > 0)
			nOpcao:= aControl[nPos][4]
			Exit
		Endif
		
	Next nControl
	
	If (nOpcao > 0 )
		
		cSup  := aItems[nPanel][6][nOpcao][1]
		
		
		For nItem:= 1 To Len(aItems)
			
			If (aItems[nItem][1] == cSup)
				nPanel:= nItem
				Exit
			Endif
			
		Next nItem
		
	Else
		Aviso(STR0010,STR0009,{"OK"}) //"ATENCAO"###"Selecione uma resposta para prosseguir com o Script."
		lNotChg := .T.
	Endif
	
Endif

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³Tk360Prev       ³  Autor³Vendas Clientes         ³Data  ³  24/03/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Volta panel.                                                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                          			º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³EXPN1:  Painel posicionado.                                      		º±±
±±º          ³EXPA2:  Array com os dados do Tree.                           		º±±
±±º          ³EXPA3:  Array com os controles de respostas.                  		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8-Call Center-Cadastro de Script                          			º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk360Prev(nPanel,aScript,aControl)

Local nPos := 0		//Armazena se foi ou nao encontrada a pergunta vigente
Local cSup := ""	//Armazena o codigo pai
Local nItem:= 0		//controle do laco.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica qual o panel anterior.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (M->UZ_FORMATO == PESQUISA)
	nPanel --
Else
	cSup  := aScript[nPanel][1]
	
	For nItem:= 1 To Len(aScript)
		
		nPos:= Ascan(aScript[nItem][6],{|aVal| aVal[1] == cSup})
		If (nPos > 0)
			nPanel:= nItem
			Exit
		Endif
		
	Next nItem
	
Endif

Return(.T.)
