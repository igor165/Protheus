#INCLUDE "TMKA090.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TMKDEF.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TmkA90   ³ Autor ³ Vendas Clientes       ³ Data ³17/09/03  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Cadastro de Operadores                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TmkA090()

Local nI 			:= 0 			// Contador
Local aCores 		:= {	{"VAL(SU7->U7_TIPOATE) == 1", "BR_AZUL"     },;// Telemarketing
   							{"VAL(SU7->U7_TIPOATE) == 2", "BR_VERDE"    },;// Televendas
   							{"VAL(SU7->U7_TIPOATE) == 3", "BR_VERMELHO" },;// Telecobranca
   							{"VAL(SU7->U7_TIPOATE) == 4", "BR_BRANCO"   },;// Todos    
   							{"VAL(SU7->U7_TIPOATE) == 5", "BR_AMARELO"  },;// Tmk x Tlv 
   							{"VAL(SU7->U7_TIPOATE) == 6", "BR_CINZA"  	}} // TeleAtendimento 
Local aRotAdic 	:= {} 
Local cMsg	   		:= ""
Local lRetorno 	:= .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aAC := { STR0001,STR0002 } //"Abandona","Confirma"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro := STR0009 //"Atualiza‡„o de Operadores"
Private aTmkUsers := {} // Array(ordenado por nome) com todos os usuarios do sistema mas somente os dados necessarios para a tela.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Valida o modo de acesso das tabelas do TMK³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !TmkVerModo()
	lRetorno := .F.
EndIf

If lRetorno 
	
	If ExistBlock("TMK90ROT")
		aRotAdic := ExecBlock("TMK90ROT",.f.,.F.)
		If ValType(aRotAdic) == "A"
			AEval(aRotAdic,{|x| AAdd(aRotina,x)})
		EndIf
	Endif
	      	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Endereca a funcao de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	mBrowse( 6, 1,22,75,"SU7",,,,,,aCores)

EndIf

Return(lRetorno) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MenuDef   ³ Autor ³ Vendas Clientes       ³ Data ³08/12/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de definição do aRotina                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina   retorna a array com lista de aRotina             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATMK                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef() 

Local aRotina:=	{	{ STR0004 ,"AxPesqui" 		, 0 , 1 ,  , .F.},;    		// "Pesquisar"
						{ STR0005 ,"Tk090Visual"		, 0 , 2 ,  , .T.},;    		// "Visualizar"
						{ STR0006 ,"Tk090Inclui"		, 0 , 3 ,  , .T.},;    		// "Incluir"
						{ STR0007 ,"Tk090Altera"		, 0 , 4, 82, .T.},;  		// "Alterar"
						{ STR0008 ,"Tk090Deleta"		, 0 , 5, 3 , .T.},;   		// "Excluir" 
						{ STR0104 ,"TMKR047"  		, 0 , 5, 3 , .T.},;   		// "Rel.Inativ." 
						{ STR0068 ,"TK090Legenda" 	, 0 ,2 ,   , .T. }} 	   		// "Legenda"

Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Tk090Inclui³Autor  ³ Vendas Clientes      ³ Data ³ 03/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Inclusao de Operadores                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Tk090Inclui(cAlias,nReg,nOpc)

Local cEmail	 := CriaVar("U7_CONTA",.F.)
Local cSenha	 := CriaVar("U7_SENHA",.F.)
Local aButtons := {}
Local cCfgBtn	 := Replicate("1", Len(SU7->U7_CFGBTN))
Local aParam	 := {}
Local nSaveSx8 := GetSX8Len()                      // Funcao de numeracao
Local cCodUsu	 := CriaVar("U7_CODUSU",.F.)
Local aHabild	 := {}
Local cEsta		:= ""			// Codigo da Estacao ATUAL
Local lCpEstc		:= SU7->(ColumnPos("U7_ESTACAO")) > 0

Aadd(aParam, {|| .T.} )
Aadd(aParam, {|| .T.} )
Aadd(aParam, {|| Tk090GrvInc(cEmail,cSenha,cCfgBtn,nSaveSX8,cCodUsu,aHabild) } )		// Executa dentro da transacao
Aadd(aParam, {|| .T.} )

AAdd(aButtons,{ "EDITABLE",		{|| Tk090Conta(@cEmail,@cSenha,nOpc,nReg)},	STR0012, STR0058 } )	// Configuracao de e-mail, "e-mail"
AAdd(aButtons,{ "BMPGROUP",		{|| Tk090User(nOpc,@cCodUsu)}			  ,	STR0017, STR0059 } )	// Cadastro de Usuarios, "Usuarios"
AAdd(aButtons,{ "FERRAM",  		{|| TK090CfgBtn(nOpc,@cCfgBtn)}			  ,	STR0021, STR0060 } )	// Configura Barra de Ferramentas, "Tool Bar"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona o botão para configurar múltiplos grupos de atendimento.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AAdd(aButtons,{ "GPEIMG32",  		{|| TK090Groups(nOpc)}			  ,	STR0071, STR0072 } )	// "Grupos de atendimento" "Grupos"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona botao para selecionar habilidades do operador³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AAdd(aButtons,{ "OFIIMG32", {|| Tk090Habil(@aHabild,nOpc)}, STR0112, STR0113} )	//"Habilidades deste operador" "Habilidade"

If lCpEstc
	cEsta	:= SU7->U7_ESTACAO
EndIf

AxInclui(  cAlias, nReg, nOpc ,NIL  ,NIL   ,NIL  ,"Tk090TudOk()", NIL ,NIL      ,aButtons,aParam)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a utilizacao do TEF e abre se necessario.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCpEstc .AND. (nOpc == 3) .AND. ( Alltrim(cEsta) <> Alltrim(SU7->U7_ESTACAO) )
	Tmk273AbreTEF()
Endif

Return(.T.)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Tk090Altera³ Autor ³ Vendas Clientes      ³ Data ³ 03/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Alteracao de Operadores                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³CALL CENTER                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Tk090Altera(cAlias,nReg,nOpc)

Local aButtons	:= {}
Local aParam	:= {}
Local cEmail	:= SU7->U7_CONTA
Local cSenha	:= SU7->U7_SENHA
Local cCfgBtn	:= SU7->U7_CFGBTN
Local cCodUsu	:= SU7->U7_CODUSU
Local aCpos		:= Tk090Cpos()
Local aHabild	:= {}
Local cEsta 	:= ""		// Codigo atual da Estacao (TEF)
Local lCpEstc	:= SU7->(ColumnPos("U7_ESTACAO")) > 0

Aadd(aParam, {|| .T. } )
Aadd(aParam, {|| Tk090VldAlt() } )							 		  		// Executa na confirmacao do OK
Aadd(aParam, {|| Tk090GrvAlt(cEmail, cSenha, cCfgBtn ,cCodUsu,aHabild) } )	// Executa dentro da transacao
Aadd(aParam, {|| .T. } )

AAdd(aButtons,{ "EDITABLE",		{|| Tk090Conta(@cEmail,@cSenha,nOpc,nReg)}	,	STR0012, STR0058 } )	// Configuracao de e-mail, "e-mail"
AAdd(aButtons,{ "BMPGROUP",		{|| Tk090User(nOpc,@cCodUsu)}				,	STR0017, STR0059 } )	// Cadastro de Usuarios, "Usuarios"
AAdd(aButtons,{ "FERRAM",  		{|| TK090CfgBtn(nOpc,@cCfgBtn)}				,	STR0021, STR0060 } )	// Configura Barra de Ferramentas, "Tool Bar"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona o botão para configurar múltiplos grupos de atendimento.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AAdd(aButtons,{ "GPEIMG32",  	{|| TK090Groups(nOpc)},	STR0071, STR0072 } )	// "Grupos de atendimento" "Grupos"
AAdd(aButtons,{ "AFASTAMENTO",	{|| Tk090Status()},     STR0086, STR0085 } )	// "Altera o status do operador para uso nas transferencias de chamados"#"Status"
      
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona botao para selecionar habilidades do operador³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AAdd(aButtons,{ "OFIIMG32", {|| Tk090Habil(@aHabild,nOpc)}, STR0112, STR0113} )	//"Habilidades deste operador" "Habilidade"

If lCpEstc
	cEsta	:= SU7->U7_ESTACAO
EndIf

AxAltera(  cAlias ,nReg, nOpc, NIL  , NIL  , aCpos  , NIL     ,"Tk090TudOk()", NIL     ,NIL  ,aButtons,aParam)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a utilizacao do TEF e abre se necessario ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCpEstc .AND. (nOpc == 4) .AND. ( Alltrim(cEsta) <> Alltrim(SU7->U7_ESTACAO) )
	Tmk273AbreTEF()
Endif

Return(.T.)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Tk090Deleta ³ Autor ³ Vendas Clientes     ³ Data ³03/03/00  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de exclusao de Operadores                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Tk090Deleta(cAlias,nReg,nOpc)

Local aButtons	:= {}
Local aParam	:= {}
Local cEmail	:= SU7->U7_CONTA
Local cSenha	:= SU7->U7_SENHA
Local cCfgBtn	:= SU7->U7_CFGBTN
Local cCodUsu	:= SU7->U7_CODUSU 
Local nTMKPMS	:= Str(GetNewPar("MV_QTMKPMS",1),1)  
Local cPartic   := ""
Local aArea     := GetArea()  
Local lOperQNC  := FindFunction("QN070VldDel")
Local lOperPMS  := FindFunction("PMSDelRec")

dbSelectArea("RDZ")
dbSetOrder(1)
If RDZ->(DbSeek(xFilial("RDZ")+cEmpAnt+xFilial("SU7")+"SU7"+xFilial("SU7")+ SU7->U7_COD))
	cPartic := RDZ->RDZ_CODRD0 
EndIf

Aadd(aParam, {|| .T.} )
Aadd(aParam, {|| (Tk090VldDel("02", cEmail, cSenha, cCfgBtn) .And. If(nTMKPMS $ "2|4" .And. lOperQNC .And. !Empty(cPartic),QN070VldDel(cPartic),.T.) .And. If(nTMKPMS $ "4" .And. lOperPMS .And. !Empty(cPartic),PMSDelRec(cPartic),.T.))})	// Validacao do botao de confirmacao
Aadd(aParam, {|| (Tk090VldDel("03", cEmail, cSenha, cCfgBtn) .And. If(nTMKPMS $ "2|4" .And. lOperQNC .And. !Empty(cPartic),QN070VldDel(cPartic),.T.) .And. If(nTMKPMS $ "4" .And. lOperPMS .And. !Empty(cPartic),PMSDelRec(cPartic),.T.))})	// Executa dentro da transacao
Aadd(aParam, {|| .T.} )

AAdd(aButtons,{ "EDITABLE",		{|| Tk090Conta(cEmail,cSenha,nOpc,nReg)}	,	STR0012, STR0058 } )	// Configuracao de e-mail, "e-mail"
AAdd(aButtons,{ "BMPGROUP",		{|| Tk090User(nOpc,@cCodUsu)}				,	STR0017, STR0059 } )	// Cadastro de Usuarios, "Usuarios"
AAdd(aButtons,{ "FERRAM",  		{|| TK090CfgBtn(nOpc,@cCfgBtn)}				,	STR0021, STR0060 } )	// Configura Barra de Ferramentas, "Tool Bar"

AxDeleta( cAlias, nReg, nOpc, "TK090DelGroup", , aButtons, aParam)

RestArea(aArea)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Tk90Visual ³ Autor ³Vendas Clientes        ³ Data ³03/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Visualiza‡ao                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Tk090Visual(cAlias,nReg,nOpc)

Local aButtons 	:= {}  
Local aHabild	:= {}
Local cEmail   	:= SU7->U7_CONTA
Local cSenha   	:= SU7->U7_SENHA
Local cCfgBtn 	:= SU7->U7_CFGBTN
Local cCodUsu	:= SU7->U7_CODUSU	

AAdd(aButtons,{ "EDITABLE",		{|| Tk090Conta(cEmail,cSenha,nOpc,nReg)}	,STR0012, STR0058 } )	// Configuracao de e-mail, "e-mail"
AAdd(aButtons,{ "BMPGROUP",		{|| Tk090User(nOpc,@cCodUsu)}				,STR0017, STR0059 } )	// Cadastro de Usuarios, "Usuarios"
AAdd(aButtons,{ "FERRAM",  		{|| TK090CfgBtn(nOpc,@cCfgBtn)}				,STR0021, STR0060 } )	// Configura Barra de Ferramentas, "Tool Bar"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona botao para selecionar habilidades do operador³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AAdd(aButtons,{ "OFIIMG32", {|| Tk090Habil(@aHabild,nOpc)}, STR0112, STR0113} )	//"Habilidades deste operador" "Habilidade"

AxVisual( cAlias, nReg, nOpc,,,,,aButtons)

DbSelectArea(cAlias)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Tk090TudOk ³ Autor ³ Vendas Clientes      ³ Data ³ 03/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Consistˆncia geral ao clicar o botao de Ok.                 ³±±
±±³          ³- Campos obrigatorios e CODIDO DE USUARIO ASSOCIADO         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³CALL CENTER                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Tk090TudOk(nOpc)

Local aArea   := GetArea()
Local lRet := .T.

If INCLUI
	DbSelectArea("SU7")
	DbSetOrder(1)
	If DbSeek(xFilial("SU7") + M->U7_COD)
		Help( " ", 1, "EXISTOPER" )
		lRet := .F.
	Endif
Endif

If Empty(M->U7_CODUSU)
	Help( " ", 1, "OPERADOR")
	lRet := .F.
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retorna o Registro, o Order e o Alias anterior.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK090ContaºAutor  ³Vendas Clientes     º Data ³  08/14/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Dialog para  conta de e-mail e password do  operador.      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CALL CENTER                                                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090Conta(cEmail,cSenha,nOpc,nReg)

Local aSaveArea := GetArea()
Local lGetEdit	:= ( (nOpc == 3) .OR. (nOpc == 4) )	// Inclusão ou Alteração: Habilita os Gets
Local lPDEmail	:= .F.
Local oDlg1		:= NIL
Local oConta	:= Nil

lPDEmail := FATPDIsObfuscate("U7_CONTA",,.T.) 

DEFINE MSDIALOG oDlg1 FROM 05,05 TO 130,300 TITLE STR0012 PIXEL
	@ 005,005 TO 040,145 LABEL ""                     OF oDlg1 PIXEL
	@ 012,010 SAY STR0015                 SIZE 050,07 OF oDlg1 PIXEL
	@ 011,040 MSGET oConta VAR cEmail     SIZE 100,10 OF oDlg1 PIXEL WHEN IIF(lPDEmail,!lPDEmail,lGetEdit)
	If FATPDActive() .And. FTPDUse(.T.)
		oConta:lObfuscate := lPDEmail
	Endif
	@ 027,010 SAY STR0016                 SIZE 050,07 OF oDlg1 PIXEL
	@ 026,040 GET cSenha   PIXEL PASSWORD SIZE 050,10 OF oDlg1       WHEN lGetEdit
	DEFINE SBUTTON TYPE 01 FROM 45,080 ENABLE OF oDlg1 PIXEL ACTION (IIF(Tk090GPass(@cEmail,@cSenha),oDlg1:End(),""))
	DEFINE SBUTTON TYPE 02 FROM 45,110 ENABLE OF oDlg1 PIXEL ACTION oDlg1:End()
ACTIVATE MSDIALOG oDlg1 CENTERED

RestArea(aSaveArea)
aSize(aSaveArea, 0)

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK090GPASSºAutor  ³Vendas Clientes     º Data ³  08/14/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validacao da conta e Criptografia dos dados.               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6 - Call Center                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090GPass(cEmail,cSenha)

Local lRet := .T.

If !Empty(cEmail)
	If !Empty(cSenha)
		cSenha   := Embaralha(cSenha,0)
	Endif
Else
	Help(" ",1,"NO_DADOS",,"",3,5)
	lRet := .F.
Endif

Return (lRet)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TK090User ºAutor  ³Vendas Clientes     º Data ³  24/07/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Visualizacao dos usuarios do sistema.                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CALL CENTER                                                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090User(nOpc,cCodUsu)

Local nOpcA     := 0  			// Avalia o botao utilizado
Local nUser     := 1  			// Variavel para o List
Local oDlg		:= Nil 			// Dialog Principal
Local oUsers	:= Nil 			// Objeto List
Local cNome     := "" 			// Nome completo selecionado
Local cNReduz   := "" 			// Nome reduzido
Local oSearch 	:= Nil			// Objeto TGet com o texto da procura
Local cSearch 	:= Space(60)	// Texto da procura
Local oBtnLoc	:= Nil			// Botão para localizar
Local oBtnPro	:= Nil			// Botão para localizar o próximo
Local oPanelInf	:= Nil			// Painel inferior com os botoes
Local oPanelRest:= Nil	    	// Paines superior com o listbox
Local lRet		:= .T.	    	// Retorno da funcao
Local aPDFields	:= {"U7_NOME"}	// Campos que serão ofuscados
Local aPDCols	:= {}			// Colunas do Listbox que serão ofuscadas

FATPDLoad(/*cUserPDA*/, /*aAlias*/, aPDFields)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Usuarios                                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DEFINE MSDIALOG oDlg TITLE STR0017 FROM 00,00 TO 320,720 PIXEL OF oMainWnd //Cadastro de Usuarios
	
	oPanelInf:= TPanel():New(0,0,"",oDlg,,,,,,0,15)
	oPanelInf:Align:= CONTROL_ALIGN_BOTTOM
	
	oPanelRest:= TPanel():New(0,0,"",oDlg,,,,,,0,0)
	oPanelRest:Align:= CONTROL_ALIGN_ALLCLIENT

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Carrega os usuários do sistema (SIGAPSS.SPF) ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aTmkUsers) == 0
		MsgRun(STR0122,STR0017,{|| Tk090AllUsers()}) //"Carregando usuários... Aguarde..."####"Cadastro de Usuarios"
	EndIf
	
	If ExistBlock("TMK90USR")
		aTMKUsers := ExecBlock("TMK90USR", .F., .F., { aTMKUsers })
	Endif

	@05,04 LISTBOX oUsers FIELDS ;
				HEADER ;  	
				STR0018,;	// Nome Completo 
				STR0019,;	// Cargo 
				STR0020,;   // Departamento
				"ID";       // Codigo do Usuario
		   		COLSIZES 80,65,65,6;
				ON DbLCLICK ( IIF( FindFunction("TkVldUser") .And. TkVldUser( aTMKUsers[oUsers:nAt,4] ),;
								( nOpcA 	:= 1,;
								cNome	:= aTMKUsers[oUsers:nAt,1],;
								cNReduz	:= aTMKUsers[oUsers:nAt,5],;
								cCodUsu	:= aTMKUsers[oUsers:nAt,4],;
								oDlg:End()),.F.)  );
				ON CHANGE (nUser:= oUsers:nAt) SIZE 0,0 OF oPanelRest PIXEL NOSCROLL					
	oUsers:Align := CONTROL_ALIGN_ALLCLIENT
	oUsers:SetArray(aTMKUsers)
	oUsers:bLine:={||{ aTMKUsers[oUsers:nAt,1],;	// Nome 	
					   aTMKUsers[oUsers:nAt,2],;	// Cargo
					   aTMKUsers[oUsers:nAt,3],;	// Departamento
					   aTMKUsers[oUsers:nAt,4]}}	// Codigo do Usuario
	If FATPDActive() .And. FTPDUse(.T.)
		aPDCols := {"U7_NOME","","",""}
		oUsers:aObfuscatedCols:= FATPDColObfuscate(aPDCols)
	Endif
	oUsers:Refresh()

	If (nOpc <> 2) .AND. (nOpc <> 5) 
		DEFINE SBUTTON FROM 03,295 TYPE 1 ENABLE OF oPanelInf ACTION ( IIF( FindFunction("TkVldUser") .And. TkVldUser( aTMKUsers[oUsers:nAt,4] ),;
																	( nOpcA	:= 1,;
																	cNome	:= aTMKUsers[oUsers:nAt,1],;
																	cNReduz	:= aTMKUsers[oUsers:nAt,5],;
																	cCodUsu	:= aTMKUsers[oUsers:nAt,4],;
																	oDlg:End()), .F.))
		DEFINE SBUTTON FROM 03,327 TYPE 2 ENABLE OF oPanelInf ACTION (nOpcA := 2, oDlg:End())
		
		@ 004,005 SAY STR0081	 				SIZE 22,10	OF oPanelInf PIXEL	// "Procurar"
	    @ 003,028 MSGET oSearch    	VAR cSearch	SIZE 50,8	OF oPanelInf PIXEL COLOR CLR_BLACK
		@ 003,080 BUTTON oBtnLoc PROMPT STR0082	SIZE 30,10	OF oPanelInf PIXEL ACTION MsgRun(STR0084,"", {||If(TK091LocUser( oUsers, cSearch, .T. ),oSearch:SetColor(CLR_BLACK,CLR_WHITE),oSearch:SetColor(CLR_WHITE,CLR_HRED))})	// "Localizar" "Localizando..."
		@ 003,112 BUTTON oBtnPro PROMPT STR0083	SIZE 30,10	OF oPanelInf PIXEL ACTION MsgRun(STR0084,"", {||If(TK091LocUser( oUsers, cSearch, .F. ),oSearch:SetColor(CLR_BLACK,CLR_WHITE),oSearch:SetColor(CLR_WHITE,CLR_HRED))})	// "Próximo" "Localizando..."
	Else
		DEFINE SBUTTON FROM 03,327 TYPE 2 ENABLE OF oPanelInf ACTION (nOpcA := 2, oDlg:End())	
	Endif

ACTIVATE MSDIALOG oDlg CENTERED

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄT¿
//³Verifica se o operador selecionado ja possui amarracao com algum usuário³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄTÙ
DbSelectArea("SU7")
DbSetOrder(4)

If((nOpcA == 1) .And. DbSeek(xFilial("SU7")+cCodUsu))
    Help(" ", 1, "TMKOPERAD")
    lRet := .F.
Else
//Se o usuario confirmou Ok e a rotina for <> Visualizacao e <> Exclusao
	If (nOpcA == 1)
		M->U7_NOME 	:= cNome			// Atualiza os campos da tela
		M->U7_NREDUZ:= cNReduz 
		M->U7_CODUSU:= cCodUsu
	Endif
Endif

FATPDUnLoad()
aSize(aPDFields, 0)
aSize(aPDCols, 0)

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090CfgBtnº Autor ³ Vendas Clientes   º Data ³  03/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao utilizada para validar o conteudo do campo e para    º±±
±±º          ³abrir uma dialog de auxilio de configuracao dos botoes.     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Call Center                                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090CfgBtn(nOpc, cCfgBtn,lTela)

Local nI		:= 0										// Contador
Local nJ		:= 0                                        // Contador         	
Local oOk		:= LoaDbitmap( GetResources(), "LBOK" ) 	// OK
Local oNo		:= LoaDbitmap( GetResources(), "LBNO" )		// CANCELA
Local lOk		:= .F.										// Retorno
Local aCfgBtn	:= {}										// Array de retorno	
Local oDlgCfg					                            // objeto da Tela
Local oListBox												// Listbox com as opcoes de selecao dos botoes - objeto 
Local aListBox  := {}										// Listbox com as opcoes de selecao dos botoes - array
Local lTodos	:= .F.                        				// Variavel para trazer o checkbox selecionado OU nao na opcao de Marca/Desmarca Todos
Local oTodos												// Objeto CheckBox
Local lPls		:= GetNewPar("MV_PLSATIV",.F.)

Default lTela := .T.

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³   BOTOES DA SUPERIOR     						SEQUENCIA NESSE ARRAY      		    ³
³																				    ³
³	Copiar                                             01                           ³ 
³	Colar                                              02                           ³ 
³	Calculadora...                                     03                           ³ 
³	Agenda...                                          04                           ³ 
³	Gerenciador de Impressao...                        05                           ³ 
³	Help de Programa...                                06                           ³ 
³   Limpa o Atendimento                                07                           ³ 
³	Cancela o Atendimento							   08						    ³
³	Marketing Ativo									   09							³
³	Envio de Tarefa									   10							³
³	Script da Campanha                                 11                           ³ 
³	Alteracao de Entidades                             12                           ³ 
³	Historico das ligacoes                             13                           ³ 
³	Situacao Financeira                                14                           ³ 
³	Cadastro de Contatos                               15                           ³ 
³	Banco de Conhecimento                              16                           ³ 
³	Tracker da Entidade                                17                           ³ 
³	Visualiza Eventos                                  18                           ³ 
³	OK                                                                              ³ 
³	CANCELA                                                                         ³  
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³   BOTOES DA LATERAL 															    ³
³																					³
³	Encerramento									   19							³
³	Chamado Tecnico									   20							³
³	Ligacoes Pendentes								   21							³	
³	Cadastro do Produto                                22              	            ³ 
³	Cadastro de Concorrentes                           23                           ³    
³	Cadastro de Acessorios							   24							³
³	Condicoes de Pagamento							   25					 		³
³	Vendedor                                           26                           ³ 
³	Pedido                                             27                           ³ 
³	Rastreamento                                       28                           ³ 
³	Lotes                                              29                           ³ 
³   Sub-Lotes                                          30                           ³ 
³   Vendas Lojas                                       31                           ³ 
³   Bonus                                              32                           ³ 
³	Consulta TEF                                       33                           ³ 
³	Plano de Saúde                                     34                           ³ 
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa o array que contem todos os botoes de acordo com o que esta³
//³configurado para o operador                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nI := 1 TO Len(cCfgBtn)
	Aadd(aCfgBtn, SubStr(cCfgBtn,nI,1))
Next nI

Aadd(aListBox, {.T., STR0022, STR0023} ) //"Superior","Copiar"
Aadd(aListBox, {.T., STR0022, STR0024} ) //"Superior","Colar"
Aadd(aListBox, {.T., STR0022, STR0025} ) //"Superior","Calculadora..."
Aadd(aListBox, {.T., STR0022, STR0026} ) //"Superior","Agenda..."
Aadd(aListBox, {.T., STR0022, STR0027} ) //"Superior","Gerenciador de Impressao..."
Aadd(aListBox, {.T., STR0022, STR0028} ) //"Superior","Help de Programa..."
Aadd(aListBox, {.T., STR0022, STR0029} ) //"Superior","Limpa o Atendimento"
Aadd(aListBox, {.T., STR0022, STR0030} ) //"Superior","Cancela o Atendimento"
Aadd(aListBox, {.T., STR0022, STR0031} ) //"Superior","Marketing Ativo"
Aadd(aListBox, {.T., STR0022, STR0032} ) //"Superior","Envio de Tarefa"
Aadd(aListBox, {.T., STR0022, STR0033} ) //"Superior","Script da Campanha"
Aadd(aListBox, {.T., STR0022, STR0034} ) //"Superior","Alteracao de Entidades"
Aadd(aListBox, {.T., STR0022, STR0035} ) //"Superior","Historico das ligacoes"
Aadd(aListBox, {.T., STR0022, STR0036} ) //"Superior","Situacao Financeira"
Aadd(aListBox, {.T., STR0022, STR0037} ) //"Superior","Cadastro de Contatos"
Aadd(aListBox, {.T., STR0022, STR0038} ) //"Superior","Banco de Conhecimento"
Aadd(aListBox, {.T., STR0022, STR0061} ) //"Superior","Tracker da Entidade"
Aadd(aListBox, {.T., STR0022, STR0062} ) //"Superior","Visualiza Eventos"

If M->U7_TIPOATE $ "145"		// Telemarketing, Todos ou TMK x TLV
	Aadd(aListBox, {.T., STR0039, STR0040} ) 			//"Lateral","Encerramento"
	Aadd(aListBox, {.T., STR0039, STR0041} ) 			//"Lateral","Chamado Tecnico"
	Aadd(aListBox, {.T., STR0039, STR0042} ) 			//"Lateral","Ligacoes Pendentes"
Else							//Televendas
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se for Televendas, marca os botoes comuns para TELEMARKETING e TELECOBRANCA com X para nao exibir ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nI := 19 TO 21
		aCfgBtn[nI] := "X"
	Next nI
Endif

If M->U7_TIPOATE $ "1245"		// Telemarketing, Televendas, Todos ou TMK x TLV
	Aadd(aListBox, {.T., STR0039, STR0043} ) //"Lateral","Cadastro do Produto"
	Aadd(aListBox, {.T., STR0039, STR0044} ) //"Lateral","Cadastro de Concorrentes"
	Aadd(aListBox, {.T., STR0039, STR0045} ) //"Lateral","Cadastro de Acessorios"
Else							//Telecobranca
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se for Telecobranca, marca os botoes COMUNS para TELEMARKETING e TELEVENDAS com X para nao exibir³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nI := 22 TO 24
		aCfgBtn[nI] := "X"
	Next nI               
	aCfgBtn[19] := "1"	//Encerramento
	Aadd(aListBox, {.T., STR0039, STR0040} ) //"Lateral","Encerramento"
Endif

If M->U7_TIPOATE $ "245" 		// Televendas, Todos ou TMK x TLV
	Aadd(aListBox, {.T., STR0039, STR0046} ) //"Lateral","Condicoes de Pagamento"
	Aadd(aListBox, {.T., STR0039, STR0047} ) //"Lateral","Vendedor"
	Aadd(aListBox, {.T., STR0039, STR0048} ) //"Lateral","Pedido"
	Aadd(aListBox, {.T., STR0039, STR0049} ) //"Lateral","Rastreamento"
	Aadd(aListBox, {.T., STR0039, STR0050} ) //"Lateral","Lotes"
	Aadd(aListBox, {.T., STR0039, STR0051} ) //"Lateral","Sub-Lotes"
	Aadd(aListBox, {.T., STR0039, STR0052} ) //"Lateral","Vendas Pelo Loja"
	Aadd(aListBox, {.T., STR0039, STR0053} ) //"Lateral","Bonus"
	Aadd(aListBox, {.T., STR0039, STR0054} ) //"Lateral","Consulta TEF"
Else
	// Botoes Somente para TELEVENDAS
	For nI := 25 TO 33
		aCfgBtn[nI] := "X"
	Next nI
Endif

If M->U7_TIPOATE $ "145" .and. lPLS		// Telemarketing, Todos ou TMK x TLV
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o PLS esta integrado com o TMK e inclui configuracao³
	//³de botao para o usuario                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Aadd(aListBox, {.T., STR0039, STR0070} )//"Lateral","Plano de Saude"
ElseIf Len(aCfgBtn) >= 34
	aCfgBtn[34] := "X"				
EndIf

For nI := 1 TO Len(aCfgBtn)
	If aCfgBtn[nI] <> "X"
		nJ++
		aListBox[nJ][1] := IIF(aCfgBtn[nI]=="1",.T.,.F.)
	Endif
Next nI

If lTela
	DEFINE MSDIALOG oDlgCfg TITLE STR0055 FROM 0,0 TO 320,650 PIXEL //"Configuracao dos botoes da barra de ferramentas"
		
		@ 35,03 LISTBOX oListBox FIELDS HEADER;
						"",;
						STR0056,;	//"ToolBar"
						STR0057;	//"Descrição dos Botoes"	
				SIZE 318,105 OF oDlgCfg Pixel; 
				ON DbLCLICK (aListBox[oListBox:nAt,1]:= !aListBox[oListBox:nAt,1], oListBox:Refresh())
		oListBox:SetArray(aListBox)
		oListBox:bLine 	:= { || { IIF(aListBox[oListBox:nAt,1],oOk,oNo), aListBox[oListBox:nAt,2], aListBox[oListBox:nAt,3] } }
		oListBox:bLDbLClick	:= { || (aListBox[oListBox:nAt][1] := !aListBox[oListBox:nAt][1], oListBox:Refresh()) }

		@ 145,03 CHECKBOX oTodos   VAR lTodos 	SIZE 130,9 PIXEL OF oDlgCfg Prompt STR0069 ON CHANGE;//"Marca / Desmarca Todos"
		Tk090Tools(oListBox, lTodos)
			
	ACTIVATE MSDIALOG oDlgCfg CENTERED ON INIT EnchoiceBar(oDlgCfg, {|| (lOk:=.T.,oDlgCfg:End())}, {|| (lOk:=.F.,oDlgCfg:End())} )
EndIf	
If (lOk.OR.!lTela) .AND. (nOpc == 3 .OR. nOpc == 4)

	nJ		:= 0                                                                                      
	cCfgBtn	:= ""
	For nI := 1 TO Len(aCfgBtn)
		If aCfgBtn[nI] <> "X"
			nJ++
			aCfgBtn[nI] := IIF(aListBox[nJ][1],"1","0")
		Else
			aCfgBtn[nI] := "0"
		Endif
		cCfgBtn := cCfgBtn + aCfgBtn[nI]
	Next nI

Endif

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³UsrTmkExcludeºAutor³Vendas Clientes     º Data ³  01/12/03  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao que verifica se o usuario do protheu e um operador doº±±
±±º          ³Call Center.                                                º±±
±±º          ³                                                            º±±
±±º          ³ESTA FUNCAO NAO PODE SER STATIC PORQUE EH UTILIZADA PELO 	  º±±
±±º          ³MODULO SIGACFG.                                             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function UsrTmkExclude(cID)

Local aArea	:= GetArea()
Local lRet	:= .F.

Default cID := ""

DbSelectArea("SU7")
DbSetOrder(4)		// U7_FILIAL+U7_CODUSU
lRet := !DbSeek(xFilial("SU7") + cID)

RestArea(aArea)
aSize(aArea,0)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090GrvIncºAutor ³Vendas Clientes     º Data ³  03/12/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Executa as gravacoes dentro do controle de transacao da fun-º±±
±±º          ³cao AXINCLUI.                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090GrvInc(cEmail,cSenha,cCfgBtn,nSaveSX8,cCodUsu,aHabild)

Local aArea		:= GetArea()
Local cCodVen	:= ""							// Código
Local nOpc		:= 4							// Opção do usuário (INCLUSÃO)
Local lAchouSA3	:= .T.							// Se o código não está disponível
Local lTmk090	:= FindFunction("U_TK090FIM")	// P.E. Depois da gravacao
Local cFilSA3	:= xFilial("SA3")
Local aRetTel	:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Configurar somemnte os botoes referente a rotina       ³
//³que o usuario pode acessar, para que as teclas SETKEY()|
//³nao fiquem habilitada e os botos desabilitado          |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Tk090CfgBtn(nOpc, @cCfgBtn,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Continua a gravacao do Operador³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RecLock("SU7",.F.)
REPLACE SU7->U7_CONTA	With cEmail 
REPLACE SU7->U7_SENHA	With cSenha
REPLACE SU7->U7_CFGBTN	With cCfgBtn
REPLACE SU7->U7_CODUSU 	With cCodUsu 
MsUnlock()

If M->U7_VEND == "1"  //Sim  
	If Empty(M->U7_CODVEN)
		//Se já existir um vendedor vinculado ao usuário do sistema, preenche com o código encontrado.
		SA3->( DbSetOrder(7) ) 
		If SA3->( DbSeek( cFilSA3 + cCodUsu ) )
			RecLock("SU7",.F.)
			REPLACE SU7->U7_CODVEN 	With  SA3->A3_COD
			MsUnlock()
		ElseIf Empty(M->U7_CODVEN)	//Cria um novo vendedor, caso não encontre nenhum vinculado ao usuário do sistema.
			DbSelectArea("SA3")
			SA3->( DbSetOrder(1) )
			While lAchouSA3
				cCodVen := GetSx8Num("SA3","A3_COD",,1)
				lAchouSA3 := SA3->( MsSeek( cFilSA3+cCodVen,.F. ) )
			End
			RecLock("SA3",.T.)
			REPLACE SA3->A3_FILIAL	With cFilSA3
			REPLACE SA3->A3_COD		With cCodVen
			REPLACE	SA3->A3_NOME	With M->U7_NOME
			REPLACE	SA3->A3_NREDUZ	With M->U7_NREDUZ
			REPLACE	SA3->A3_END		With M->U7_END
			REPLACE SA3->A3_BAIRRO	With M->U7_BAIRRO
			REPLACE SA3->A3_MUN		With M->U7_MUN
			REPLACE SA3->A3_EST		With M->U7_EST
			REPLACE SA3->A3_CEP		With M->U7_CEP
			REPLACE SA3->A3_CGC		With M->U7_CGC
			REPLACE SA3->A3_FAX		With M->U7_FAX
			REPLACE SA3->A3_CODUSR	With cCodUsu

			aRetTel := RemDDDTel( M->U7_TEL )
			REPLACE SA3->A3_TEL		With aRetTel[1]
			REPLACE SA3->A3_DDDTEL	With aRetTel[2]
			REPLACE SA3->A3_DDI		With aRetTel[3]

			MsUnLock()
			FkCommit()

			While (GetSx8Len() > nSaveSx8)
				ConfirmSX8()
			End	

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Grava o codigo do vendedor³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			REPLACE SU7->U7_CODVEN 	With cCodVen
		Endif
	EndIf
Endif

If !Empty(M->U7_CODRD0)
	FUpdateEnt(M->U7_CODRD0, "SU7", SU7->U7_FILIAL+SU7->U7_COD, 1, cEmpAnt , cFilAnt)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Grava a amarracao com as habilidades³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Tk090GrHab(aHabild)

If lTmk090
	ExecBlock("TK090FIM",.F.,.F.,{3})
EndIf

RestArea(aArea)
aSize(aArea,0)
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090GrvAltºAutor ³Vendas Clientes     º Data ³  03/12/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Executa as gravacoes dentro do controle de transacao da fun-º±±
±±º          ³cao AXALTERA.                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090GrvAlt(cEmail, cSenha, cCfgBtn, cCodUsu, aHabild)

Local cCodVen 	:= ""							// Código para a gravação
Local nSaveSx8 	:= GetSX8Len()					// Funcao de numeracao
Local nOpc		:= 4							// Opção do usuário (INCLUSÃO)
Local lAchouSA3	:= .T.							// Se o código está disponível
Local lTmk090	:= FindFunction("U_TK090FIM")		// P.E. Depois da gravacao
Local cFilSA3	:= xFilial("SA3")
Local aRetTel	:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Configurar somemnte os botoes referente a rotina       ³
//³que o usuario pode acessar, para que as teclas SETKEY()|
//³nao fiquem habilitada e os botos desabilitado          |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Tk090CfgBtn(nOpc, @cCfgBtn,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Continua a gravacao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
REPLACE SU7->U7_CONTA	With cEmail
REPLACE SU7->U7_SENHA	With cSenha
REPLACE SU7->U7_CFGBTN	With cCfgBtn
REPLACE SU7->U7_CODUSU	With cCodUsu

If M->U7_VEND == "1"  // Sim
	If Empty(M->U7_CODVEN)
		//Se já existir um vendedor vinculado ao usuário do sistema, preenche com o código encontrado.
		SA3->( DbSetOrder(7) )
		If SA3->( DbSeek( cFilSA3 + cCodUsu ) )
			RecLock("SU7",.F.)
			REPLACE SU7->U7_CODVEN 	With  SA3->A3_COD
			MsUnlock()
		ElseIf Empty(M->U7_CODVEN)	//Cria um novo vendedor, caso não encontre nenhum vinculado ao usuário do sistema.
			DbSelectArea("SA3")
			SA3->( DbSetOrder(1) )
			While lAchouSA3
				cCodVen := GetSx8Num("SA3","A3_COD",,1)
				lAchouSA3 := SA3->( MsSeek( cFilSA3+cCodVen,.F. ) )
			End		
			RecLock("SA3",.T.)
			REPLACE SA3->A3_FILIAL	With cFilSA3
			REPLACE SA3->A3_COD		With cCodVen
			REPLACE SA3->A3_NOME	With M->U7_NOME
			REPLACE SA3->A3_NREDUZ	With M->U7_NREDUZ
			REPLACE SA3->A3_END		With M->U7_END
			REPLACE SA3->A3_BAIRRO	With M->U7_BAIRRO
			REPLACE SA3->A3_MUN		With M->U7_MUN
			REPLACE SA3->A3_EST		With M->U7_EST
			REPLACE SA3->A3_CEP		With M->U7_CEP
			REPLACE SA3->A3_CGC		With M->U7_CGC
			REPLACE SA3->A3_FAX		With M->U7_FAX
			REPLACE SA3->A3_CODUSR	With cCodUsu
			
			aRetTel := RemDDDTel( M->U7_TEL )
			REPLACE SA3->A3_TEL		With aRetTel[1]
			REPLACE SA3->A3_DDDTEL	With aRetTel[2]
			REPLACE SA3->A3_DDI		With aRetTel[3]

			MsUnLock()
			FkCommit()
			While (GetSx8Len() > nSaveSx8)
				ConfirmSX8()
			End	
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Grava o codigo do vendedor³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			REPLACE SU7->U7_CODVEN With SA3->A3_COD 
		EndIf
	EndIf
Else
	While (GetSx8Len() > nSaveSx8)
		RollBackSX8()
	End	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se foi alterado o status de vendedor e existe o cod. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(M->U7_CODVEN)
		REPLACE SU7->U7_CODVEN With ""
	Endif
	
Endif

FUpdateEnt(M->U7_CODRD0, "SU7", xFilial("SU7")+SU7->U7_COD, 1, cEmpAnt , cFilAnt)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Grava a amarracao com as habilidades³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Tk090GrHab(aHabild)

If lTmk090
	ExecBlock("TK090FIM",.F.,.F.,{3})
EndIf

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090VldDelºAutor ³Vendas Clientes     º Data ³  09/12/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida se o Registro atual do Cadastro de Operadores pode   º±±
±±º          ³ser deletado - Validacao de movimentos e relacionamentos    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³CALL CENTER                                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090VldDel(cPonto, cEmail, cSenha, cCfgBtn)

Local lRet		:= .T.
Local cFilAGQ	:= ""
Local aArea		:= GetArea()
Local aAreaACF	:= {}
Local aAreaADE	:= {}
Local aAreaSA3	:= {}
Local aAreaSUA	:= {}
Local aAreaSUC	:= {}

Do Case
	Case cPonto == "02"

		// Verifica se existe atendimento realizado para esse operador
		aAreaSUC	:= SUC->(GetArea())
		SUC->(DbSetOrder(4))
		If SUC->(DbSeek(xFilial("SUC")+SU7->U7_COD))
			lRet := .F.
		Endif
		RestArea(aAreaSUC)
		
		If lRet
			aAreaADE	:= ADE->(GetArea())
			ADE->(dbSetOrder(2))		// ADE_FILIAL+ADE_OPERAD+ADE_GRUPO				
			IF ADE->(dbSeek(xFilial("ADE")+SU7->U7_COD))
				lRet := .F.
			EndIf
			RestArea(aAreaADE)
		EndIf
		
		If lRet
			aAreaSUA	:= SUA->(GetArea())
			SUA->(DbSetOrder(5))
			If SUA->(DbSeek(xFilial("SUA")+SU7->U7_COD))
				lRet := .F.
			Endif
			RestArea(aAreaSUA)
		Endif
		
		If lRet
			aAreaACF	:= ACF->(GetArea())
			ACF->(DbSetOrder(6))		// ACF_FILIAL+ACF_OPERAD
			If ACF->(DbSeek(xFilial("ACF")+SU7->U7_COD))
				lRet := .F.
			Endif
			RestArea(aAreaACF)
		Endif

		// Chama a função para verificar as condições para exclusão
		// do vendedor que está associado ao operador.
		If lRet .AND. ! Empty(SU7->U7_CODVEN)
			aAreaSA3	:= SA3->(GetArea())
			SA3->(DbSetOrder(1))
			If SA3->(DbSeek(xFilial("SA3") + SU7->U7_CODVEN))
				lRet	:= SA3->(fCanDelSA3())
			EndIf
			RestArea(aAreaSA3)
		EndIf   

		If !lRet
			Help( " ", 1, "TMKATENDIM" )
		Endif

	Case cPonto == "03"

		// Verifico se existe comissão para o vendedor
		If SU7->U7_VEND == "1" //Sim
			SE3->(DbSetOrder(1))
			If SE3->( (! DbSeek(xFilial("SE3")+SU7->U7_CODVEN)) .OR. (! Empty(SE3->E3_DATA)) )
				SA3->(DbSetOrder(1))
				If SA3->(Dbseek(xFilial("SA3")+SU7->U7_CODVEN))
					SA3->(RecLock("SA3",.F.,.T.))
					SA3->(DbDelete())
					SA3->(MsUnlock())
				Endif
			Endif
		Endif

		// Exclui amarração com as habilidades do operador
		cFilAGQ	:= xFilial("AGQ")
		AGQ->(DbSetOrder(1))
		AGQ->(DbSeek(cFilAGQ+SU7->U7_COD))
		While AGQ->(! Eof()) .AND. AGQ->AGQ_FILIAL == cFilAGQ .AND. AGQ->AGQ_OPERAD == SU7->U7_COD
			AGQ->(RecLock("AGQ",.F.))
			AGQ->(DbDelete())
			AGQ->(MsUnLock())
			AGQ->(DbSkip())
		EndDo

		// Caso haja relacionamento com participante ele sera eliminado.
		If !Empty(M->U7_CODRD0)
			SU7->( FUpdateEnt("", "SU7", xFilial("SU7") + SU7->U7_COD, 1, cEmpAnt , cFilAnt) )
		Endif

Endcase

RestArea(aArea)

aSize(aArea,0)
aSize(aAreaACF,0)
aSize(aAreaADE,0)
aSize(aAreaSA3,0)
aSize(aAreaSUA,0)
aSize(aAreaSUC,0)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090Cpos ºAutor  ³Vendas Clientes     º Data ³  09/12/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Os campos que estiverem relacionados com o cadastro de partiº±±
±±º          ³cipantes nao deverao estar editaveis na alteracao.          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090Cpos()

Local aCpos		:= {}
Local aDados	:= {}
Local cDestino	:= ""
Local cFile		:= "MSRELRDZ.INI"
Local cLinha	:= ""
Local nY		:= 0
Local nPos		:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica o arquivo de transferencia                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If File(cFile)
	FT_FUse(cFile)
	FT_FGotop()
	While ( !FT_FEof() )
		cLinha := FT_FREADLN()
		If SubStr(cLinha,1,3) == "U7_"
			nY := At("=",cLinha)
			cDestino := SubStr(cLinha,1,nY-1)
			Aadd(aDados, cDestino )
		Endif
		FT_FSkip()
	End
	FT_FUse()
Endif

DbSelectArea("SX3")
DbSetOrder(1)
DbSeek("SU7")
While !Eof() .AND. SX3->X3_ARQUIVO == "SU7"
	
	nPos := Ascan(aDados, { |x| AllTrim(x) == AllTrim(SX3->X3_CAMPO) } )
	
	If !X3Uso(SX3->X3_CAMPO) .OR. SX3->X3_NIVEL > cNivel .OR. nPos > 0
		DbSkip()
		Loop
	Endif
	
	Aadd(aCpos, SX3->X3_CAMPO )
	DbSkip()
	
End

aSize(aDados,0)
Return(aCpos)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090Rd0NomeºAutor³Vendas Clientes     º Data ³  11/12/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Inicializador padrao do nome do participante.               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090Rd0Nome()

Local cRet		:= ""
Local cCodRd0	:= ""

If !INCLUI
	cCodRd0	:= FGetPessoa("SU7",xFilial("SU7")+SU7->U7_COD)
	DbSelectArea("RD0")
	DbSetOrder(1)
	If DbSeek(xFilial("RD0")+cCodRd0)
		cRet := RD0->RD0_NOME
	Endif
Endif

Return(cRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090VldAltºAutor ³Vendas Clientes     º Data ³  28/01/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida a alteracao do Grupo de Atendimento no cadastro de O-º±±
±±º          ³peradores.                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP8                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090VldAlt()

Local aArea		:= GetArea()		// Registra a base atual
Local lRet		:= .T.				// Retorno logico da funcao
Local cFilSU4	:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Avalia se o Grupo de atendimento podera ser alterado no cadastro de Operadores.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(SU7->U7_POSTO) .AND. M->U7_POSTO <> SU7->U7_POSTO
	SK1->(DbSetOrder(3))		// K1_FILIAL+K1_OPERAD
	If SK1->(DbSeek(xFilial("SK1") + M->U7_COD))
		lRet := .F.
	Endif

	If lRet
		cFilSU4	:= xFilial("SU4")
		SU4->(DbSetOrder(5))		// U4_FILIAL+U4_OPERAD+U4_STATUS
		SU4->(DbSeek(cFilSU4 + M->U7_COD))
		While	SU4->(! Eof())				.AND.;
				SU4->U4_FILIAL == cFilSU4	.AND.;
				SU4->U4_OPERAD == M->U7_COD
			If SU4->U4_STATUS <> "2" .AND. Val(SU4->U4_TIPO) == COBRANCA		// Nao encerrada e de Cobranca
				lRet := .F.
				Exit
			Endif
			SU4->(DbSkip())
		End
	Endif
Endif

If !lRet
	Help( " ", 1, "TMKTLCRGA" )
Endif

RestArea(aArea)
aSize(aArea,0)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TK090Legenda ºAutor  ³Vendas Clientes  º Data ³  23/03/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Legendas do cadastro                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³CALL CENTER                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090Legenda()

//"Legenda"
BrwLegenda(cCadastro,STR0068,	{	{"BR_AZUL"		,STR0063	},;		//"Telemarketing"
									{"BR_VERDE"		,STR0064	},;     //"Televendas" 
									{"BR_VERMELHO"	,STR0065	},;     //"Telecobrança"  
									{"BR_BRANCO"	,STR0066	},;     //"Todos" 
									{"BR_AMARELO"	,STR0067	},;     //"Telemarketing x Televendas" 
									{"BR_CINZA"		,STR0114	}})     //"TeleAtendimento" 


Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090ToolsºAutor  ³Vendas Clientes     º Data ³  01/06/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para marcar e desmarcar os botoes selecionados para  º±±
±±º          ³o operador.                                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1: Objeto do LisbBox                                    º±±
±±º          ³ExpL2: Controle para Marcar/Desmarcar todos                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³CALL CENTER                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090Tools( oListBox, lCheck)

Local nI	:= 0		//Variavel de Controle
Local lRet	:= .T.		//Retorno da Função

If lCheck
	For nI := 1 To Len(oListBox:aArray)
		oListBox:aArray[nI][1] := .T.
		oListBox:Refresh()
	Next nI
Else
	For nI := 1 To Len(oListBox:aArray)
		oListBox:aArray[nI][1] := .F.
		oListBox:Refresh()
	Next nI
Endif

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090Groupsº Autor ³ Vendas CRM         º Data ³  08/05/09  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para a seleção dos grupos de atendimento que o       º±±
±±º          ³operador poderá utilizar.                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³CALL CENTER                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090Groups( nOpc )

	Local aArea			:= GetArea()						// Salva a área atual
	Local cSeek			:= ""								// Seek utilizado na montagem da aCols
	Local cWhile		:= ""								// While utilizado na montagem da aCols
	Local aColsBkp		:= {}								// Backup da aCols
	Local aHeaderBkp	:= {}								// Backup do aHeader
	Local nBkp			:= 0								// Backup do N
	Local nPosCODSU0	:= 0								// Posição no aCols do campo AG9_CODSU0
	Local nPosOPERADOR 	:= 0								// Posição no aCols do campo AG9_OPERAD
	Local nPosCODSU7	:= 0								// Posição no aCols do campo AG9_CODSU7
	Local nPosGrupo		:= 0								// Posição no aCols do campo AG9_GRUPO
	Local lGravar		:= .F.								// Se as alterações serão gravadas ou não
	Local nCount		:= 0								// Contador temporário
	Local nCount2		:= 0								// Contador temporário
	Local oDlg			:= Nil								// Diálogo utilizado para a exibição
	Local oGroup		:= Nil								// Caixa de texto com o grupo pesquisado
	Local cGroup		:= Space(TamSX3("U0_CODIGO")[1])	// Código do grupo pesquisado
	Local oGrpName		:= Nil								// Caixa de texto com o nome do grupo pesquisado
	Local cGrpName		:= Space(TamSx3("U0_NOME")[1]) 	// Nome do grupo pesquisado
	Local oGetDados		:= Nil								// Objeto MsNewGetDados com os grupos que o operador pode trabalhar
	Local aSize 		:= {}								// Array com os tamanhos dos objeto
	Local aObjects 		:= {}								// Array com os objetos
	Local aInfo 		:= {}								// Array com as informações de posicionamento
	Local aPosObj 		:= {}								// Array com as posições dos objetos
	Local nTmCodSU7		:= TamSX3("AG9_CODSU7")[1]
	Local nTmCodSU0		:= TamSX3("AG9_CODSU0")[1]
	
	Private aCols
	Private aHeader
	Private N
	Private INCLUI
	Private ALTERA
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Faz os backups necessários.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aColsBkp	:= aClone(aCols)
	aHeaderBkp	:= aClone(aHeader)
	nBkp		:= n
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta o aCols e aHeader.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AG9")
	DbCloseArea()        
	
	cSeek	:= xFilial("AG9") + M->U7_COD
	cWhile	:= "AG9->AG9_FILIAL + AG9->AG9_CODSU7"	
		
	aHeader := {}
	aCols	:= {}
	If nOpc == 3
		INCLUI	:= .F.
		ALTERA	:= .T.
	EndIf
	FillGetDados(	4 /*nOpcX*/, "AG9"/*cAlias*/, 1/*nIndex*/, cSeek/*cSeek*/,; 
					{||&(cWhile)}/*{||&cWhile}*/, /*{|| bCond,bAct1,bAct2}*/, {"AG9_FILIAL","AG9_CODSU7","AG9_OPERAD"}/*aNoFields*/,; 
					/*aYesFields*/, /*lOnlyYes*/, /*cQuery*/, /*bMontAcols*/, .F./*lEmpty*/,; 
					/*aHeaderAux*/, /*aColsAux*/, /*bAfterCols*/, /*bBeforeCols*/,;
					/*bAfterHeader*/, /*cAliasQry*/)
	If nOpc == 3
		INCLUI := .T.
		ALTERA := .F.
	ElseIf nOpc == 4
		INCLUI := .F.
		ALTERA := .T.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Remove os últimos dois campos referentes ao Walk-Thru.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aCols) > 0 .And. Len(aHeader) > 0
		aDel(aHeader, Len(aHeader))
		aSize(aHeader, Len(aHeader)-1)
		aDel(aHeader, Len(aHeader))
		aSize(aHeader, Len(aHeader)-1)
		
		For nCount := 1 To Len(aCols)
			aDel(aCols[nCount], Len(aCols[nCount])-1)
			aSize(aCols[nCount], Len(aCols[nCount])-1)
			aDel(aCols[nCount], Len(aCols[nCount])-1)
			aSize(aCols[nCount], Len(aCols[nCount])-1)
		Next
	EndIf
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pega as pocições no aCols de campos utilizados.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nPosCODSU0		:= aScan(aHeader, {|x| AllTrim(x[2]) == "AG9_CODSU0"})
	nPosGRUPO		:= aScan(aHeader, {|x| AllTrim(x[2]) == "AG9_GRUPO"}) 		
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Limpa o primeiro campo em branco.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aCols) > 0 .And. Empty(aCols[1][nPosCODSU0])
		aDel(aCols, 1)
		aSize(aCols, Len(aCols)-1)
	EndIf
								
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o grupo de atendimento existente no campo M->U7_POSTO³
	//³já está adicionado na lista dos grupos que o operador pode       ³
	//³trabalhar, e se não estiver, já o adiciona.                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(M->U7_POSTO)
		If aScan(aCols, {|x| AllTrim(x[nPosCODSU0]) == AllTrim(M->U7_POSTO)}) == 0 
			aAdd(aCols, Array(Len(aHeader)+1))
			aCols[Len(aCols)][nPosCODSU0]		:= M->U7_POSTO
			aCols[Len(aCols)][nPosGRUPO]		:= POSICIONE("SU0",1,XFILIAL("SU0") + M->U7_POSTO,"U0_NOME")
			aCols[Len(aCols)][Len(aHeader)+1]	:= .F.
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ordena a aCols pela descrição do grupo.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSort(aCols,,,{|x,y| AllTrim(x[2]) < AllTrim(y[2])})	
	
	DEFINE MSDIALOG oDlg FROM 50,003 TO 400,255 TITLE STR0073 PIXEL	// "Selecionar grupos de atendimento"
	
		aSize := MsAdvSize(.F.,.F.,010)
		AAdd( aObjects, { 001, 005, .T., .F. } )   
		AAdd( aObjects, { 001, 002, .T., .F. } ) 	  
		AAdd( aObjects, { 001, 005, .T., .F. } )   
		AAdd( aObjects, { 001, 002, .T., .F. } ) 		 
		AAdd( aObjects, { 001, 005, .T., .F. } )   
		AAdd( aObjects, { 000, 000, .T., .F. } )   
		aAdd( aObjects, { 120, 100, .F., .F. } )
		AAdd( aObjects, { 001, 002, .T., .F. } )    
		AAdd( aObjects, { 001, 005, .T., .F. } )   	
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
		aPosObj := MsObjSize( aInfo, aObjects )
	
		@ aPosObj[1,1],aPosObj[1,2]  	SAY STR0074 PIXEL SIZE 55,9 OF oDlg	// "Equipe"
		@ aPosObj[1,1],aPosObj[1,2]+45 MSGET oGroup Var cGroup SIZE 55,9 PICTURE PesqPict("SU0", "U0_CODIGO") OF oDlg Pixel F3 "SU0" VALID Tk090GetGpName(cGroup, @cGrpName, oDlg)
		@ aPosObj[3,1],aPosObj[3,2]  	SAY STR0075 PIXEL SIZE 55,9 OF oDlg	// "Nome"
		@ aPosObj[3,1],aPosObj[3,2]+45 MSGET oGrpName Var cGrpName SIZE 55,9 PICTURE PesqPict("SU0", "U0_NOME") OF oDlg Pixel When .F.
		
		@ aPosObj[5,1],aPosObj[5,2]	 	BUTTON STR0076 SIZE 40,12 OF oDlg PIXEL ACTION (Tk090IGroup(@cGroup, @cGrpName, oGetDados))	// "Incluir"
		@ aPosObj[5,1],aPosObj[5,2]+45	BUTTON STR0077 SIZE 40,12 OF oDlg PIXEL ACTION (Tk090EGroup(oGetDados:nAT, oGetDados))	// "Apagar"
	
		oGetDados := MsNewGetDados():New(	aPosObj[7,1]+15, aPosObj[7,2], aPosObj[7,3], aPosObj[7,4], ;
											0,,,,,, 100,,,, oDlg, aHeader, aCols)			

		@ aPosObj[9,1],aPosObj[9,2]+35	BUTTON STR0078	SIZE 40,12 OF oDlg PIXEL ACTION (lGravar:= .T.,oDlg:End())	// "OK"
		@ aPosObj[9,1],aPosObj[9,2]+80	BUTTON STR0079	SIZE 40,12 OF oDlg PIXEL ACTION (lGravar:= .F.,oDlg:End())	// "Cancela"
	
	ACTIVATE MSDIALOG oDlg CENTERED		
	
	If lGravar
		If Len(oGetDados:aCols) > 0
			For nCount := 1 To Len(oGetDados:aCols)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se o item em análise é válido.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty(oGetDados:aCols[nCount][nPosCODSU0])
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Se o campo M->U7_POSTO não estiver preenchido,³
					//³já preenche com o primeiro grupo da lista.    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If Empty(M->U7_POSTO)
						M->U7_POSTO := oGetDados:aCols[nCount][nPosCODSU0] 
					EndIf				
					DbSelectArea("AG9")
					DbSetOrder(1)
					If DbSeek( xFilial("AG9") + PadR(M->U7_COD,nTmCodSU7) + PadR(oGetDados:aCols[nCount][nPosCODSU0],nTmCodSU0) )
						If oGetDados:aCols[nCount][Len(oGetDados:aCols[nCount])]
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Se o grupo deletado estiver no cadastro do operador, apaga o mesmo.³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If M->U7_POSTO == AG9->AG9_CODSU0
							   M->U7_POSTO := CriaVar("U7_POSTO")
							EndIf
							RecLock("AG9", .F.)
							DbDelete()
							MsUnLock()
						EndIf
					Else
						If !oGetDados:aCols[nCount][Len(oGetDados:aCols[nCount])]
							RecLock("AG9", .T.)
							Replace AG9->AG9_FILIAL With xFilial("AG9")
							Replace AG9->AG9_CODSU7 With M->U7_COD
							For nCount2 := 1 To Len(oGetDados:aHeader)
								IF !oGetDados:aHeader[nCount2][10] == "V"
									FieldPut(ColumnPos(oGetDados:aHeader[nCount2][2]), oGetDados:aCols[nCount][nCount2])
								Endif
							Next 
							MsUnLock()
						Else
							oGetDados:aCols[nCount][nPosCODSU0] == M->U7_POSTO
							M->U7_POSTO := CriaVar("U7_POSTO")
						EndIf
					EndIf				
				EndIf
			Next
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se o campo M->U7_POSTO não estiver preenchido, já preenche com o primeiro grupo da lista. ³
			//³ocorre quando é excluido o ultimo grupo e o mesmo constava no cadastro do operador		  ³	  
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Empty(M->U7_POSTO)
				For nCount := 1 To Len(oGetDados:aCols)
					If !oGetDados:aCols[nCount][Len(oGetDados:aCols[nCount])]
						M->U7_POSTO := oGetDados:aCols[nCount][nPosCODSU0] 
					Endif			
				Next
			Endif
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Restaura o backup.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCols	:= aClone(aColsBkp)
	aHeader := aClone(aHeaderBkp)
	n		:= nBkp
	
	RestArea(aArea)
	aSize(aArea,0)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090IGroupºAutor  ³ Vendas CRM         º Data ³  08/05/09  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para incluir o grupo na lista de grupos.             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpC1: Código do grupo a ser incluído                       º±±
±±º          ³ExpC2: Nome do grupo a ser incluído                         º±±
±±º          ³ExpO3: Objeto da MsNewGetDados                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³CALL CENTER                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090IGroup(cCodGrupo, cNomeGrupo, oGetDados)

	Local nPosCODSU0	:= aScan(oGetDados:aHeader, {|x| AllTrim(x[2]) == "AG9_CODSU0"})	// Posição do campo AG9_CODSU0
	Local nPosGRUPO		:= aScan(oGetDados:aHeader, {|x| AllTrim(x[2]) == "AG9_GRUPO"})	// Posição do campo AG9_GRUPO
	Local nPos			:= 0																// Posição genérica
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se o usuário preencheu o código do grupo.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cCodGrupo)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se o código preenchido já existe na lista.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (nPos:=aScan(oGetDados:aCols, {|x|x[nPosCODSU0]==cCodGrupo})) <= 0
			If	!Empty(oGetDados:aCols[Len(oGetDados:aCols)][nPosCODSU0]) .AND.;
				!Empty(oGetDados:aCols[Len(oGetDados:aCols)][nPosGRUPO]) 
					 
					aAdd(oGetDados:aCols, Array(Len(oGetDados:aHeader)+1) )
			Endif
			oGetDados:aCols[Len(oGetDados:aCols)][nPosCODSU0]	:= cCodGrupo
			oGetDados:aCols[Len(oGetDados:aCols)][nPosGRUPO]	:= POSICIONE("SU0",1,XFILIAL("SU0") + cCodGrupo,"U0_NOME")
			oGetDados:aCols[Len(oGetDados:aCols)][Len(oGetDados:aHeader)+1]	:= .F.
			aSort(oGetDados:aCols,,,{|x,y| AllTrim(x[2]) < AllTrim(y[2])})
			oGetDados:Refresh()
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se já existir e estiver deletado, recupera o item.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If oGetDados:aCols[nPos][Len(oGetDados:aHeader)+1]
				oGetDados:aCols[nPos][Len(oGetDados:aHeader)+1]	:= .F.
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Limpa os textbox.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCodGrupo 	:= Space(TamSX3("U0_CODIGO")[1])       
		cNomeGrupo 	:= Space(TamSx3("U0_NOME")[1]) 
	EndIf	
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090EGroupºAutor  ³ Vendas CRM         º Data ³  08/05/09  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para excluir o grupo na lista de grupos.             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpN1: Posição do cursor na MsNewGetDadis                   º±±
±±º          ³ExpO2: Objeto da MsNewGetDados                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³CALL CENTER                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090EGroup(nAt, oGetDados)

	Local nPosCODSU0	:= aScan(aHeader, {|x| AllTrim(x[2]) == "AG9_CODSU0"})		// Posição do campo AG9_CODSU0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se a posição do cursor não estoura a aCols.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(oGetDados:aCols) >= nAt
		If TmkOK(STR0080 + AllTrim(oGetDados:aCols[nAt][nPosCODSU0]) + "?") // "Confirmar a exclusão da Equipe " 
			oGetDados:aCols[nAt][Len(oGetDados:aHeader)+1]	:= .T. 
			oGetDados:Refresh()
		EndIf
	EndIf
Return      

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090GetGpNameº Autor ³ Vendas CRM      º Data ³  08/05/09  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para validar o grupo digitado.                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpC1: Código do grupo a ser incluído                       º±±
±±º          ³ExpC2: Nome do grupo a ser incluído                         º±±
±±º          ³ExpO3: Objeto da Dialog                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³CALL CENTER                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090GetGpName(cGroup, cGrpName, oDlg)

	Local lRet 		:= .F.	 				// Retorno da função
	Local aAreaSU0 	:= SU0->(GetArea())	// Salva a área atual
	
	DbSelectArea("SU0")
	DbSetOrder(1)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se o grupo é valido.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cGroup)
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica a sua existência no cadastro de grupos.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistCpo("SU0", cGroup) .AND. DbSeek(xFilial("SU0")+cGroup)
			cGrpName := SU0->U0_NOME
			lRet := .T.             
		EndIf
	Else
		lRet := .T.
	EndIf
	RestArea(aAreaSU0)
	aSize(aAreaSU0,0)
Return lRet    

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TK090DelGroupº Autor ³ Vendas CRM       º Data ³  08/05/09  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Apaga os grupos cadastrados para o operador posicionado     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³CALL CENTER                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TK090DelGroup()

Local cFilAG9	:= xFilial("AG9")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Apaga os grupos cadastrados no AG9.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AG9->(DbSetOrder(1))
If AG9->(DbSeek( cFilAG9 + SU7->U7_COD ))
	While	AG9->(! EOF()) .And. AG9->AG9_FILIAL == cFilAG9 .AND. AG9->AG9_CODSU7 == SU7->U7_COD
		AG9->(RecLock("AG9" , .F.))
		AG9->(DbDelete())
		AG9->(MsUnLock())
		AG9->(DbSkip())
	End				
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TK091LocUserº Autor ³ Vendas CRM       º Data ³  18/05/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Procura um usuário na lista de usuários.                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CALL CENTER                                                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function TK091LocUser( oList, cString, lInicio )

	Static nStartLine		// Controle de próxima procura
	Static nStartCol		// Coluna inicial

	Local nCount 	:= 0	// Contador temporário
	Local nCount2	:= 0	// Contador temporário
	Local lAchou	:= .F.	// Se encontrou a informação desejada

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Inicializa a variável da linha inicial de procura.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ValType(nStartLine) <> "N"
		nStartLine := 1
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Inicializa a variável da coluna inicial de procura.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ValType(nStartCol) <> "N"
		nStartCol := 1
	EndIf	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se é para procurar desde o início.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lInicio
		nStartLine	:= 1
		nStartCol	:= 1	
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Procura em todas as linhas e colunas pelo conteúdo solicitado.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCount := nStartLine To Len(oList:aArray)		
		For nCount2 := nStartCol To Len(oList:aArray[nCount]) - 1
			If ValType(oList:aArray[nCount][nCount2]) == "C"
				If Upper(AllTrim(cString)) $ Upper(AllTrim(oList:aArray[nCount][nCount2]))					
					oList:nAt := nCount
					oList:Refresh()
					nStartLine	:= nCount
					nStartCol	:= nCount2 + 1
					lAchou := .T.
					Exit
				EndIf
			EndIf
		Next

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se já encontrou um resultado, saia.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lAchou
			Exit
		Else
			nStartCol := 1
		EndIf		
	Next

Return lAchou

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090IntDelºAutor ³Vendas Clientes     º Data ³  02/07/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡ao ³Validacao da exclusao de usuarios para integração			  º±±
±±º          ³TMK x PMS x QNC											  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºSintaxe	 ³Tk090IntDel (cPartic)                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1: Código do Participante                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso		 ³TMK x PMS x QNC                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090IntDel(cPartic)

Local lRet	:= .T.                                
Local cMat  := ""
Local aArea := GetArea()
Local cEmail  := ""
Local cSenha	:= ""
Local cCfgBtn := ""
Local cCodUsu := ""

Default cPartic := ""

If !Empty(cPartic)
	dbSelectArea("RDZ")
	dbSetOrder(2)
	If RDZ->(DbSeek(xFilial("RDZ")+cPartic+cEmpAnt+xFilial("SU7")+"SU7"))
		cMat := AllTrim(STUFF(RDZ->RDZ_CODENT,1,2,"")) // Retira filial
		If !Empty(cMat)
			dbSelectArea("SU7")
			dbSetOrder(1)
			If dbSeek(xFilial("SU7")+cMat)
				cEmail  := SU7->U7_CONTA
				cSenha	:= SU7->U7_SENHA
				cCfgBtn := SU7->U7_CFGBTN
				cCodUsu	:= SU7->U7_CODUSU
				lRet := If(Tk090VldDel("02", cEmail, cSenha, cCfgBtn),.T.,.F.) 
			EndIf
		EndIf
	EndIf
EndIf

RestArea(aArea)
aSize(aArea,0)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090StatusºAutor ³Bruno Daniel Borges º Data ³  13/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡ao ³Manutencao no status do operador para bloqueio nas rotinas  º±±
±±º          ³de transferencia   										  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºSintaxe	 ³Tk090Status()	                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso		 ³TMK x PMS x QNC                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090Status()

Local oDlgStatus   := Nil
Local oGetDados    := Nil
Local aCoordenadas := MsAdvSize(.T.) 
Local aHeader      := {}
Local aCols        := {}
Local nOpcClick    := 0   
Local nPosID       := 0
Local i            := 0
Local j            := 0
Local cFilAGF      := xFilial("AGF")

//Valida se alguma linha esta ATIVA (no periodo vigente) e nao tem operador substituto informado
If Empty(M->U7_SUBSTIT)
	MsgAlert(STR0091) //"Atenção, existe uma ou mais linhas de status que estão em vigor na presente data. E esse operador não tem um operador substituto informado. Antes de atualizar o status, informe o operador substituto."
	Return(Nil)		
EndIf

dbSelectArea("SX3")
SX3->(dbSetOrder(1))
SX3->(dbSeek("AGF"))
While SX3->(!Eof()) .And. SX3->X3_ARQUIVO == "AGF"
	If X3Uso(SX3->X3_USADO) .And. cNivel >= SX3->X3_NIVEL
		Aadd(aHeader,	{	AllTrim(X3Titulo()),SX3->X3_CAMPO,SX3->X3_PICTURE,SX3->X3_TAMANHO,SX3->X3_DECIMAL,SX3->X3_VALID,SX3->X3_USADO,SX3->X3_TIPO,SX3->X3_F3,SX3->X3_CONTEXT,SX3->X3_CBOX,,SX3->X3_WHEN,SX3->X3_VISUAL,SX3->X3_VLDUSER, SX3->X3_PICTVAR,SX3->X3_OBRIGAT	})	     
	EndIf
	SX3->(dbSkip())
EndDo

dbSelectArea("AGF")
AGF->(dbSetOrder(1))
AGF->(dbSeek(cFilAGF+M->U7_COD ))
While AGF->(!Eof()) .And. AGF->AGF_FILIAL == cFilAGF .And. AGF->AGF_OPERAD == M->U7_COD
	AAdd(aCols,Array(Len(aHeader)+1))
	For i := 1 To Len(aHeader)  
		If aHeader[i,10] == "V"
			aCols[Len(aCols)][i] := CriaVar(aHeader[i,2])
		Else
			aCols[Len(aCols)][i] := AGF->&(aHeader[i,2])
		EndIf
	Next i
	aCols[Len(aCols)][Len(aHeader)+1] := .F.
	AGF->(dbSkip())
EndDo

If Len(aCols) <= 0
	AAdd(aCols,Array(Len(aHeader)+1))
	For i := 1 To Len(aHeader)   
		If AllTrim(aHeader[i,2]) == "AGF_ID"
			aCols[Len(aCols)][i] := "01"
		Else
			aCols[Len(aCols)][i] := CriaVar(aHeader[i,2])
		EndIf
	Next i
	aCols[Len(aCols)][Len(aHeader)+1] := .F.
EndIf
              
//Tela de manutencao no status do operador
oDlgStatus := TDialog():New(aCoordenadas[7],000,aCoordenadas[6]/2,aCoordenadas[5]/2,OemToAnsi(STR0087),,,,,,,,oMainWnd,.T.) //"Status do Operador"
	oGetDados := MsNewGetDados():New(032,003,oDlgStatus:nClientHeight/2-15,oDlgStatus:nClientWidth/2-5,GD_INSERT+GD_DELETE+GD_UPDATE,,,"+AGF_ID",,,9999,,,,oDlgStatus,aHeader,aCols)	
	oGetDados:bDelOk := {|| MsgAlert(STR0088),.F. } //"Não é permitido excluir histórico de mudança de status. Mude a data/hora de término para inativar um status."
	
oDlgStatus:Activate(,,,.T.,,,{||EnchoiceBar(oDlgStatus,{|| Iif(Tk090StatValida(oGetDados),(nOpcClick := 1, oDlgStatus:End()),Nil)},{|| oDlgStatus:End()})})

If nOpcClick == 1	
	dbSelectArea("AGF")
	AGF->(dbSetOrder(1))
	
	nPosID := AScan(oGetDados:aHeader,{|x| AllTrim(x[2]) == "AGF_ID" })
	
	For i := 1 To Len(oGetDados:aCols)
		If AGF->(dbSeek(cFilAGF+M->U7_COD+oGetDados:aCols[i,nPosID] ))
			AGF->(RecLock("AGF",.F.))
		Else
			AGF->(RecLock("AGF",.T.))
		EndIf
		
		AGF->AGF_FILIAL	:= cFilAGF
		AGF->AGF_OPERAD	:= M->U7_COD
		
		For j := 1 To Len(oGetDados:aHeader)
			AGF->&(oGetDados:aHeader[j,2]) := oGetDados:aCols[i,j]
		Next j
		
		AGF->(MsUnlock())
	Next i	
EndIf

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090StatValidaºAutor  ³Bruno Daniel Borges º Data ³  13/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao de validacao do status do atendente                       º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090StatValida(oGetDados)

Local nPosDataIni := AScan(oGetDados:aHeader,{|x| AllTrim(x[2]) == "AGF_DTINI" })
Local nPosHoraIni := AScan(oGetDados:aHeader,{|x| AllTrim(x[2]) == "AGF_HRINI" })
Local nPosDataFim := AScan(oGetDados:aHeader,{|x| AllTrim(x[2]) == "AGF_DTFIM" })
Local nPosHoraFim := AScan(oGetDados:aHeader,{|x| AllTrim(x[2]) == "AGF_HRFIM" })
Local nPosStatus  := AScan(oGetDados:aHeader,{|x| AllTrim(x[2]) == "AGF_CODIGO" })
Local i           := 0

For i := 1 To Len(oGetDados:aCols) 
	//Valida os intervalos informado
	If 	oGetDados:aCols[i,nPosDataFim] < oGetDados:aCols[i,nPosDataIni] .Or. ;
		(oGetDados:aCols[i,nPosDataFim] <= oGetDados:aCols[i,nPosDataIni] .And. oGetDados:aCols[i,nPosHoraFim] < oGetDados:aCols[i,nPosHoraIni])
		MsgAlert(STR0089) //"Atenção, existem linhas com datas de término e/ou horas de término inválidas."
		Return(.F.)
	EndIf
	
	//Valida os campos obrigatorios
	If Empty(oGetDados:aCols[i,nPosDataIni]) .Or. Empty(oGetDados:aCols[i,nPosHoraIni]) .Or. Empty(oGetDados:aCols[i,nPosDataFim]) .Or. Empty(oGetDados:aCols[i,nPosHoraFim]) .Or. Empty(oGetDados:aCols[i,nPosStatus])
		MsgAlert(STR0090) //"Atenção, campos obrigatórios não foram preenchidos."
		Return(.F.)
	EndIf
Next i

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Tk090FiltGrp  ºAutor  ³Bruno Daniel Borges º Data ³  13/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Monta consulta F3 filtrando apenas os atendentes dos grupos      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090FiltGrp()

Local oDlgSXB      := Nil   
Local oListBox     := Nil
Local aCoordenadas := MsAdvSize(.T.) 
Local aAtendentes  := {} 
Local nOpcClick    := 0
Local aDeptos      := {}
Local i                  
Local lRet			:= .F.
Local oCmbOrdem		:= Nil
Local aOrdem		:= {STR0095,STR0075} //"Código"###"Nome"
Local nOrdem		:= 1  
Local cPesq			:= Space(40)
Local bPesq			:= {||Tk90Pesq(@oCmbOrdem,@aAtendentes,@oListBox,cPesq)}
Local cQuery		:= ""
Local cGrupos		:= ""
Local cFilAG9		:= xFilial("AG9")
Local cFilSU7		:= xFilial("SU7")
Local cFilSU0		:= xFilial("SU0")

//Busca os departamentos do atendente
dbSelectArea("AG9")
AG9->(dbSetOrder(1))
AG9->(dbSeek(cFilAG9+M->U7_COD ))
While AG9->AG9_FILIAL == cFilAG9 .AND. AG9->AG9_CODSU7 == M->U7_COD
	If AScan(aDeptos,{|x| x == AG9->AG9_CODSU0 }) <= 0
		AAdd(aDeptos,AG9->AG9_CODSU0)
	EndIf
	AG9->(dbSkip())
EndDo

For i := 1 To Len(aDeptos)
	cGrupos += "'" + aDeptos[i] + "',"
Next i

cGrupos := SubStr(cGrupos,1,Len(cGrupos)-1)

If !(Empty(cGrupos))
	
	cQuery := "	SELECT AG9_FILIAL,AG9_CODSU7,AG9_CODSU0 "
	cQuery += "	FROM " + RetSQLName("AG9") + " AG9, " + RetSQLName("SU7") + " SU7"
	cQuery += "	WHERE SU7.U7_FILIAL  = '"  + cFilSU7        + "' AND"		
	cQuery += "		  AG9.AG9_FILIAL = '"  + cFilAG9        + "' AND"
	cQuery += "		  AG9.AG9_CODSU0 IN (" + cGrupos     	+ ") AND"
	cQuery += "		  AG9.AG9_CODSU7 <> '" + M->U7_COD 		+ "' AND"
	cQuery += "		  SU7.U7_COD = AG9.AG9_CODSU7 AND"
	cQuery += "		  SU7.U7_VALIDO <> '2' AND"
	cQuery += "		  AG9.D_E_L_E_T_ = ' ' AND"
	cQuery += "		  SU7.D_E_L_E_T_ = ' '"
				
	cQuery := ChangeQuery(cQuery)
	
	dbSelectArea("AG9")
	dbCloseArea()		
	dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),"AG9",.F.,.T.)		
		
	While AG9->(!Eof()) .AND. AG9->AG9_FILIAL == cFilAG9
		If AScan(aAtendentes,{|x| x[1] == AG9->AG9_CODSU7 }) <= 0
			Aadd(aAtendentes,{	AG9->AG9_CODSU7,;
								Posicione("SU7",1,cFilSU7+AG9->AG9_CODSU7,"SU7->U7_NOME"),;
								Posicione("SU0",1,cFilSU0+AG9->AG9_CODSU0,"SU0->U0_NOME") })
		EndIf
		AG9->(dbSkip())
	EndDo
	
	dbSelectArea("AG9")
	dbCloseArea()	 
	dbSelectArea("AG9")
	dbSetOrder(1)
	
EndIf

If Len(aAtendentes) <= 0
	MsgAlert(STR0094) //"Atenção, nenhum outro atendente foi localizado nos departamentos desse atendente."
Else
	lRet := .T. 
EndIf

If lRet
	aPDFields 	:= {"U7_NOME","U0_NOME"}
	FATPDLoad(/*cUserPDA*/, /*aAlias*/, aPDFields)
	//Tela de consulta
	oDlgSXB := TDialog():New(aCoordenadas[7],000,aCoordenadas[6]/2,aCoordenadas[5]/2-50,STR0093,,,,,,,,oMainWnd,.T.) //"Lista de Operadores"
		
	@ 003, 002 MSCOMBOBOX oCmbOrdem VAR nOrdem  ITEMS aOrdem SIZE 188, 010 OF oDlgSXB ON CHANGE Tk90Ordem(oCmbOrdem,@aAtendentes,@oListBox) PIXEL
	@ 002, 195 BUTTON "Pesquisar" SIZE 041, 011 OF oDlgSXB PIXEL Action Eval(bPesq)
	@ 015, 002 MSGET cPesq SIZE 188, 010 OF oDlgSXB PIXEL Valid (Eval(bPesq))
		
	oListBox := TWBrowse():New(030,002,oDlgSXB:nClientWidth/2-5,oDlgSXB:nClientHeight/2-65,,{STR0095,STR0096,STR0111},,oDlgSXB,,,,,,,,,,,,.F.,,.T.,,.F.,,,) //"Código"#"Nome operador"#"Grupo de Atendimento"
	oListBox:bLDblClick := {||	M->U7_SUBSTIT := aAtendentes[oListBox:nAt][1], oDlgSXB:End(),lRet := .T.  }
	If FATPDActive() .And. FTPDUse(.T.)
		aPDCols := {"","U7_NOME","U0_NOME"}
		oListBox:aObfuscatedCols := FATPDColObfuscate(aPDCols)    
	Endif
	Tk90Ordem(@oCmbOrdem,@aAtendentes,@oListBox)
			
	TButton():New(oDlgSXB:nClientHeight/2-30,003,OemToAnsi("&"+STR0078),oDlgSXB,{|| M->U7_SUBSTIT := aAtendentes[oListBox:nAt][1], oDlgSXB:End(),lRet := .T.},040,012,,,,.T.,,,,{|| })   
	TButton():New(oDlgSXB:nClientHeight/2-30,050,OemToAnsi("&"+STR0014),oDlgSXB,{|| oDlgSXB:End()},040,012,,,,.T.,,,,{|| })   
	
	FATPDLogUser('TK090FILTG') // log de acesso a dados protegidos
	oDlgSXB:Activate(,,,.T.)
	
	If lRet .AND. (INCLUI .OR. ALTERA)
		DbSelectArea("SU7")
		DbSetOrder(1)
		DbSeek(cFilSU7+M->U7_SUBSTIT)
	EndIf
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk90Ordem ºAutor  ³Vendas CRM          º Data ³  30/07/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Altera a ordem do array de operadores e atualiza o Listbox  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1 - Objeto do combo de ordenacao                        º±±
±±º          ³ExpA2 - Array de atendentes                                 º±±
±±º          ³ExpO3 - Objeto da listbox                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA090                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk90Ordem(oCmbOrdem,aAtendentes,oListBox)

Local nOrdem := oCmbOrdem:nAt

If nOrdem == 1 
	//Ordena pelo codigo
	ASort(aAtendentes,,,{|x,y| x[1] < y[1] })
ElseIf nOrdem == 2
	//Ordena pelo nome
	ASort(aAtendentes,,,{|x,y| x[2] < y[2] })
EndIf

oListBox:SetArray(aAtendentes)
oListBox:bLine := {||{	aAtendentes[oListBox:nAt][1],;
						aAtendentes[oListBox:nAt][2],;
						aAtendentes[oListBox:nAt][3] }} 
oListBox:GoTop()
oListBox:Refresh()

Return .T.  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk90Pesq  ºAutor  ³Vendas CRM          º Data ³  30/07/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Realiza a pesquisa na listagem de operadores                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1 - Objeto do combo de ordenacao                        º±±
±±º          ³ExpA2 - Array de atendentes                                 º±±
±±º          ³ExpO3 - Objeto da listbox                                   º±±
±±º          ³ExpC4 - Texto para pesquisa                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA090                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk90Pesq(oCmbOrdem,aAtendentes,oListBox,cPesq)

Local nPos		:= 0 
Local nOrdem	:= oCmbOrdem:nAt
Local nSize		:= 0

cPesq := AllTrim(Upper(cPesq))
nSize := Len(cPesq)

If nOrdem == 1		//Por codigo
	nPos := aScan(aAtendentes,{|x| Left(AllTrim(Upper(x[1])),nSize) == cPesq })
ElseIf nOrdem == 2	//Por nome
	nPos := aScan(aAtendentes,{|x| Left(AllTrim(Upper(x[2])),nSize) == cPesq })
EndIf

If nPos > 0
	oListBox:nAt := nPos
	oListBox:Refresh()
EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090OpFilºAutor  ³Vendas CRM          º Data ³  14/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Exibe os operadores de um grupo especifico, considerando    º±±
±±º          ³a filial deste grupo.                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090OpFil(cGrupo)             

Local aArea			:= GetArea()
Local aOperadores	:= {}
Local cFilAntBkp	:= cFilAnt
Local cGrupoDest	:= ""
Local cFilDest		:= xFilial("SU0")
Local oDlgSXB		:= Nil   
Local oListBox		:= Nil
Local aCoordenadas	:= MsAdvSize(.T.) 
Local nOpcClick		:= 0 
Local cOperad		:= "" 
Local lRet			:= .T.
Local lMultGroup 	:= .F.
Local cQuery		:= ""
Local cFilSU0		:= xFilial("SU0")
Local cFilAG9		:= xFilial("AG9")
Local cFilSU7		:= xFilial("SU7")
Local aPDFields		:= {}
Local aPDCols		:= {}

If Empty(cGrupo) .AND. (IsInCallStack("Tk510TransfAt") .OR. IsInCallStack("Acoes_Lote"))
	cGrupo := cGroup
EndIf

// TFEGRP Distribuicao automatica de chamados
If IsInCallStack("TMKA560")
	cGrupo := M->AAV_GRUPO
Endif	

cGrupoDest	:= cGrupo

DbSelectArea("SU0")
DbSetOrder(1)   
If DbSeek(cFilSU0+cGrupo)
	If ( !Empty(SU0->U0_GRPORI) .AND. !Empty(SU0->U0_FILORI) .AND. cFilSU0 <> SU0->U0_FILORI )
		cGrupoDest	:= SU0->U0_GRPORI
		cFilDest	:= SU0->U0_FILORI
	EndIf
EndIf

cFilAnt := cFilDest

cQuery := "	SELECT AG9_FILIAL,AG9_CODSU7,AG9_CODSU0 "
cQuery += "	FROM " + RetSQLName("AG9") + " AG9, " + RetSQLName("SU7") + " SU7"
cQuery += "	WHERE SU7.U7_FILIAL  = '" + cFilSU7 + "' AND"		
cQuery += "		  AG9.AG9_FILIAL = '" + cFilAG9 + "' AND"
cQuery += "		  AG9.AG9_CODSU0 = '" + cGrupoDest     + "' AND"
cQuery += "		  SU7.U7_COD = AG9.AG9_CODSU7 AND"
cQuery += "		  SU7.U7_VALIDO <> '2' AND"
cQuery += "		  AG9.D_E_L_E_T_ = ' ' AND"
cQuery += "		  SU7.D_E_L_E_T_ = ' '"
			
cQuery := ChangeQuery(cQuery)
dbSelectArea("AG9")
dbCloseArea()		
dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),"AG9",.F.,.T.)		
	
While AG9->(! Eof()) .AND. AG9->AG9_FILIAL == cFilAG9 .AND. AG9->AG9_CODSU0 == cGrupoDest
	Aadd(aOperadores,{ AG9->AG9_CODSU7, Posicione("SU7",1,cFilSU7+AG9->AG9_CODSU7,"SU7->U7_NOME") })
	AG9->(DbSkip())
End

dbSelectArea("AG9")
dbCloseArea()	 
dbSelectArea("AG9")
dbSetOrder(1)

If Len(aOperadores) == 0
	MsgAlert(STR0105) //"Nenhum operador foi encontrado para o grupo selecionado"
	lRet := .F.
Else
	//Ordena pelo codigo
	ASort(aOperadores,,,{|x,y| x[1] < y[1] })
EndIf

If lRet
	aPDFields	:= {"U7_NOME"}
	FATPDLoad(/*cUserPDA*/, /*aAlias*/, aPDFields)

	aPDCols		:= {}
	oDlgSXB := TDialog():New(aCoordenadas[7],000,aCoordenadas[6]/2,aCoordenadas[5]/2,STR0093,,,,,,,,oMainWnd,.T.) //"Lista de Operadores"
	oListBox := TWBrowse():New(001,003,oDlgSXB:nClientWidth/2-5,oDlgSXB:nClientHeight/2-50,,{STR0095,STR0096},,oDlgSXB,,,,,,,,,,,,.F.,,.T.,,.F.,,,) //"Código"#"Nome operador"
	oListBox:SetArray(aOperadores)                                                                            
	oListBox:bLine 		  := {||{ aOperadores[oListBox:nAt][1], aOperadores[oListBox:nAt][2] }}  
	oListBox:bLDblClick   := {|| cOperad := aOperadores[oListBox:nAt][1], oDlgSXB:End() }
	oListBox:bheaderclick := {|x,y| if(y ==1,ASort(aOperadores,,,{|x,y| x[1] < y[1] }),ASort(aOperadores,,,{|x,y| x[2] < y[2] })),oListBox:Refresh()}
	If FATPDActive() .And. FTPDUse(.T.)
		aPDCols := {"","U7_NOME"}
		oListBox:aObfuscatedCols := FATPDColObfuscate(aPDCols)    
	Endif
	
	TButton():New(oDlgSXB:nClientHeight/2-30,003,OemToAnsi("&"+STR0078),oDlgSXB,{|| cOperad := aOperadores[oListBox:nAt][1], oDlgSXB:End()},040,012,,,,.T.,,,,{|| })   
	TButton():New(oDlgSXB:nClientHeight/2-30,050,OemToAnsi("&"+STR0014),oDlgSXB,{|| oDlgSXB:End()},040,012,,,,.T.,,,,{|| })   
	FATPDLogUser('TK090OPFIL') // log de acesso a dados protegidos
	oDlgSXB:Activate(,,,.T.)
	
	If !Empty(cOperad)
		DbselectArea("SU7")
		DbSetOrder(1)
		Dbseek(xFilial("SU7") + cOperad)
	EndIf

	cFilAnt := cFilAntBkp 
	FATPDUnLoad()

EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Tk090Substit  ºAutor  ³Vendas CRM          º Data ³  13/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida a exclusao do substituto                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tk090Substit()

Local lRetorno	:= .T.
Local aAreaBKP	:= GetArea()
Local aGrupos	:= {}
Local cFilAG9	:= xFilial("AG9")

//Valida se operador possui historico de ausencia para excluir o substituto
If Empty(M->U7_SUBSTIT)
	dbSelectArea("AGF")
	AGF->(dbSetOrder(1))
	If AGF->(dbSeek(xFilial("AGF")+M->U7_COD ))
		lRetorno := .F.
	EndIf
EndIf

If lRetorno
	lRetorno := Empty(M->U7_SUBSTIT) .OR. (EXISTCPO("SU7",M->U7_SUBSTIT,1) .And. M->U7_SUBSTIT <> M->U7_COD)
EndIf           

If lRetorno .AND. !Empty(M->U7_SUBSTIT)
	
	lRetorno := .F.
	
	DbSelectarea("AG9")
	DbSetOrder(1) //AG9_FILIAL+AG9_CODSU7+AG9_CODSU0
	
	//Monta a lista com os grupos do operador atual
	DbSeek(cFilAG9+M->U7_COD)
	While AG9->(! Eof()) .AND. AG9->AG9_FILIAL == cFilAG9 .AND. AG9->AG9_CODSU7 == M->U7_COD
		AAdd(aGrupos,AG9->AG9_CODSU0)
		AG9->(DbSkip())
	End
	
	//Compara os grupos do operador substituto
	DbSeek(cFilAG9+M->U7_SUBSTIT)
	While AG9->(! Eof()) .AND. AG9->AG9_FILIAL == cFilAG9 .AND. AG9->AG9_CODSU7 == M->U7_SUBSTIT
		If aScan(aGrupos,AG9->AG9_CODSU0) > 0
			lRetorno := .T.
			Exit
		EndIf
		AG9->(DbSkip())
	End
	
	If !lRetorno
		MsgStop(STR0106) //"O operador substituto selecionado não atende o(s) mesmo(s) grupos que o operador atual"
	EndIf	
	
EndIf

RestArea(aAreaBKP)
aSize(aAreaBKP,0)
Return(lRetorno)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090HabilºAutor  ³Vendas CRM          º Data ³  22/11/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Interface para selecao das habilidades do operador          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA090                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090Habil(aHabild,nOpc)

Local cFilRBG	:= xFilial("RBG")
Local cFilRBK	:= xFilial("RBK")
Local cFilAGQ	:= xFilial("AGQ")
Local cEscala	:= ""
Local cImporta	:= ""
Local cBusca	:= Space(30) 

Local oDlg		:= Nil 
Local oLbx		:= Nil
Local oGtBusca	:= Nil
Local oBtBuscar	:= Nil
Local oBtCancel	:= Nil
Local oBtOk		:= Nil
Local oBtProx	:= Nil
Local oOk		:= LoaDbitmap( GetResources(), "LBOK" ) 	// OK
Local oNo		:= LoaDbitmap( GetResources(), "LBNO" )		// CANCELA

Local nX		:= 0 

Local lConf		:= .F.
Local lMarca	:= .F.

Local aBkHab	:= {}

DbSelectArea("AGQ")
DbSetOrder(1)

DbSelectArea("RBG")
DbSetOrder(1)
DbSeek(cFilRBG)
                        
If Len(aHabild) == 0
	//Seleciona as habilidades
	While !Eof() .AND. RBG->RBG_FILIAL == cFilRBG  

		cEscala	:= AllTrim(Posicione("RBK",3,cFilRBK+"1"+RBG->RBG_ESCALA,"RBK_DESCRI"))
		cImporta:= AllTrim(Posicione("RBK",3,cFilRBK+"2"+RBG->RBG_IMPORT,"RBK_DESCRI"))
		lMarca	:= AGQ->(DbSeek(cFilAGQ + M->U7_COD + RBG->RBG_HABIL))

		AAdd(aHabild,{lMarca,AllTrim(RBG->RBG_DESC),RBG->RBG_HABIL,cEscala,cImporta})
		DbSkip()
	EndDo
Else  
	//Armazena posicao das habilidades caso o usuario cancele
	aBkHab := aClone(aHabild)
EndIf

If Len(aHabild) == 0
	MsgInfo(STR0115) //"Não foram encontradas habilidades cadastradas"
	Return Nil
EndIf

//Ordena alfabeticamente
aSort(aHabild,,,{ |x,y| x[2]<y[2] })

DEFINE MSDIALOG oDlg TITLE STR0116 FROM 000, 000  TO 350, 570 PIXEL //"Habilidades deste operador"

	@ 004, 003 TO 155, 245 PROMPT STR0117 OF oDlg  PIXEL //"Habilidades disponíveis"
	@ 014, 007 LISTBOX oLbx FIELDS HEADER "",STR0113,STR0095,STR0118,STR0119; //"Habilidade"###"Código"###"Escala"###"Importância"
		SIZE 232,135 OF oDlg Pixel //ON DbLCLICK (oLbx:aArray[oLbx:nAt,1]:= !oLbx:aArray[oLbx:nAt,1], oLbx:Refresh())
	
	If nOpc == 3 .OR. nOpc == 4
		oLbx:bLDblClick := {|NROW,NCOL,NFLAGS|(OLBX:AARRAY[OLBX:NAT,1]:=!OLBX:AARRAY[OLBX:NAT,1],OLBX:REFRESH())}
	EndIf   
	
	oLbx:SetArray(aHabild)
	oLbx:aColSizes	:= {5,65,30,65,65}
	oLbx:bLine 	:= {||{	Iif(aHabild[oLbx:nAt,1],oOk,oNo)	,;
						aHabild[oLbx:nAt,2]					,;
						aHabild[oLbx:nAt,3]					,;
						aHabild[oLbx:nAt,4]					,;
						aHabild[oLbx:nAt,5] 				}}

	@ 160, 005 SAY STR0120 SIZE 021, 009 OF oDlg  PIXEL //"Busca"
	@ 158, 026 MSGET oGtBusca VAR cBusca SIZE 129, 011 OF oDlg  PIXEL  
    
	@ 158, 161 BUTTON oBtBuscar	PROMPT STR0121	SIZE 033, 012 OF oDlg PIXEL ACTION Tk090Busca(@oLbx,cBusca,oGtBusca,.T.)	//"Buscar"
	@ 158, 204 BUTTON oBtProx	PROMPT STR0083	SIZE 033, 012 OF oDlg PIXEL ACTION Tk090Busca(@oLbx,cBusca,oGtBusca,.F.)	//"Próximo"
	@ 007, 249 BUTTON oBtOk 	PROMPT STR0078	SIZE 033, 012 OF oDlg PIXEL ACTION (lConf:=.T.,oDlg:End())					//"OK"
	@ 022, 249 BUTTON oBtCancel	PROMPT STR0079	SIZE 033, 012 OF oDlg PIXEL ACTION oDlg:End()								//"Cancela"

ACTIVATE MSDIALOG oDlg CENTERED

If !lConf
	aHabild := aClone(aBkHab)
EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090BuscaºAutor  ³Vendas CRM          º Data ³  23/11/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Pesquisa um valor na lista de habilidades                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1 - Listbox das habilidades                             º±±
±±º          ³ExpC2 - Texto pesquisado                                    º±±
±±º          ³ExpO3 - Get da pesquisa                                     º±±
±±º          ³ExpL4 - Indica se deve pesquisar do inicio(.T.) ou nao(.F.) º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA090                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090Busca(oLbx,cBusca,oGtBusca,lInicio)

Local nStart	:= 1
Local lAchou	:= .F.
Local nCols		:= 0 
Local nCount    := 0
Local nCount2   := 0

If !lInicio
	nStart := oLbx:nAt+1
EndIf

nCols := Len(oLbx:aArray[1])

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Procura em todas as linhas e colunas pelo conteudo solicitado.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCount := nStart To Len(oLbx:aArray)		
	For nCount2 := 2 To nCols
		If Upper(AllTrim(cBusca)) $ Upper(AllTrim(oLbx:aArray[nCount][nCount2]))					
			oLbx:nAt := nCount
			oLbx:Refresh()
			lAchou := .T.
			Exit
		EndIf
	Next

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se encontrou um resultado abandona.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lAchou
		Exit
	EndIf		
Next

If lAchou
	oGtBusca:SetColor(CLR_BLACK,CLR_WHITE)
Else
	oGtBusca:SetColor(CLR_WHITE,CLR_HRED)
endif

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Tk090GrHabºAutor  ³Vendas CRM          º Data ³  23/11/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Grava as habilidades relacionadas ao operador               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TMKA090                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Tk090GrHab(aHabild)

Local nX		:= 0
Local cFilAGQ	:= xFilial("AGQ")
             
If Len(aHabild) > 0
	DbSelectArea("AGQ")
	DbSetOrder(1)

	For nX := 1 to Len(aHabild)
		If aHabild[nX][1] //Marcado 
			If !AGQ->(DbSeek(cFilAGQ + M->U7_COD + aHabild[nX][3])) //Na existe ainda
				RecLock("AGQ",.T.)
				AGQ->AGQ_FILIAL	:= cFilAGQ
				AGQ->AGQ_OPERAD	:= M->U7_COD
				AGQ->AGQ_HABILI	:= aHabild[nX][3]
				MsUnLock()
			EndIf
		Else //Desmarcado
			If AGQ->(DbSeek(cFilAGQ + M->U7_COD + aHabild[nX][3])) //Existe, entao deleta
				RecLock("AGQ",.F.)
				DbDelete()
				MsUnLock()
			EndIf
		EndIf
	Next nX
EndIf
Return Nil


//-------------------------------------------------------------------
/*/{Protheus.doc} Tk090AllUsers
Carrega os usuário do sistema (SIGAPSS.SPF)
@param nulo
@return nulo
@author Vendas & CRM
@since 27/04/2015
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function Tk090AllUsers()

Local nI		:= 0	// Contador
Local aUsers	:= {} 	// Array com todos os usuarios do sistema
Local aTmkUSR1  := {} 	// Array com todos os usuarios do sistema mas somente os dados necessarios para a tela.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna array com os usuarios.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aUsers:= FWSFAllUsers()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega as informacoes necessarias.³
//³                                   ³
//³aTmkUsers[1] = Nome Completo       ³
//³aTmkUsers[2] = Cargo               ³
//³aTmkUsers[3] = Departamento        ³
//³aTmkUsers[4] = ID                  ³ 
//³aTmkUsers[5] = Usuario             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nI:=1 TO Len(aUsers)
	aAdd(aTmkUSR1,{aUsers[nI,4],aUsers[nI,7],aUsers[nI,6],aUsers[nI,2],aUsers[nI,3]})
Next nI
	
aTMKUsers := aSort(aTmkUSR1,,, { |x, y| x[1] < y[1] }) //CRESCENTE Alfabetica

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TK090TriggerºAutor³Vendas Clientes     º Data ³  13/08/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Função para gatilhar o nome do participante na inclusão,    º±±
±±º          ³chamado do valid do campo U7_CODRD0.                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TMK                                                       º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TK090Trigger()

If INCLUI .AND. ! Empty(M->U7_CODRD0)
	M->U7_DESCRD0:= Posicione("RD0",1,xFilial("RD0")+M->U7_CODRD0,"RD0_NOME")
EndIf
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} Tk090Estac
Controla a edição da estação do operador
@param nulo
@return Lógico
@author CRM/Fat
@since 17/12/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Function Tk090Estac()

Local lRet		:= .F.

If M->U7_TIPOATE $ "245"
	lRet := .T.
Else
	M->U7_ESTACAO := "   "
EndIf

Return lRet
//-----------------------------------------------------------------------------------
/*/{Protheus.doc} FATPDLoad
    @description
    Inicializa variaveis com lista de campos que devem ser ofuscados de acordo com usuario.
	Remover essa função quando não houver releases menor que 12.1.27

    @type  Function
    @author Squad CRM & Faturamento
    @since  05/12/2019
    @version P12.1.27
    @param cUser, Caractere, Nome do usuário utilizado para validar se possui acesso ao 
        dados protegido.
    @param aAlias, Array, Array com todos os Alias que serão verificados.
    @param aFields, Array, Array com todos os Campos que serão verificados, utilizado 
        apenas se parametro aAlias estiver vazio.
    @param cSource, Caractere, Nome do recurso para gerenciar os dados protegidos.
    
    @return cSource, Caractere, Retorna nome do recurso que foi adicionado na pilha.
    @example FATPDLoad("ADMIN", {"SA1","SU5"}, {"A1_CGC"})
/*/
//-----------------------------------------------------------------------------------
Static Function FATPDLoad(cUser, aAlias, aFields, cSource)
	Local cPDSource := ""

	If FATPDActive()
		cPDSource := FTPDLoad(cUser, aAlias, aFields, cSource)
	EndIf

Return cPDSource

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} FATPDUnload
    @description
    Finaliza o gerenciamento dos campos com proteção de dados.
	Remover essa função quando não houver releases menor que 12.1.27

    @type  Function
    @author Squad CRM & Faturamento
    @since  05/12/2019
    @version P12.1.27
    @param cSource, Caractere, Remove da pilha apenas o recurso que foi carregado.
    @return return, Nulo
    @example FATPDUnload("XXXA010") 
/*/
//-----------------------------------------------------------------------------------
Static Function FATPDUnload(cSource)    

    If FATPDActive()
		FTPDUnload(cSource)    
    EndIf

Return Nil

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} FATPDIsObfuscate
    @description
    Verifica se um campo deve ser ofuscado, esta função deve utilizada somente após 
    a inicialização das variaveis atravez da função FATPDLoad.
	Remover essa função quando não houver releases menor que 12.1.27

    @type  Function
    @author Squad CRM & Faturamento
    @since  05/12/2019
    @version P12.1.27
    @param cField, Caractere, Campo que sera validado
    @param cSource, Caractere, Nome do recurso que buscar dados protegidos.
    @param lLoad, Logico, Efetua a carga automatica do campo informado
    @return lObfuscate, Lógico, Retorna se o campo será ofuscado.
    @example FATPDIsObfuscate("A1_CGC",Nil,.T.)
/*/
//-----------------------------------------------------------------------------------
Static Function FATPDIsObfuscate(cField, cSource, lLoad)
    
	Local lObfuscate := .F.

    If FATPDActive()
		lObfuscate := FTPDIsObfuscate(cField, cSource, lLoad)
    EndIf 

Return lObfuscate

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} FATPDColObfuscate
    @description
    Verifica se a coluna de um grid deve ser ofuscado, tendo como base uma lista de
    campos, esta função deve utilizada somente após a inicialização das variaveis 
    atravez da função FATPDLoad.
	Remover essa função quando não houver releases menor que 12.1.27

    @type  Function
    @author Squad CRM & Faturamento
    @since  05/12/2019
    @version P12.1.27
    @param cField, Caractere, Campo que sera validado
    @param cSource, Caractere, Nome do recurso que buscar dados protegidos.

    @return lObfuscate, Lógico, Retorna se o campo será ofuscado.
    @example FATPDIsObfuscate({"A1_COD","A1_NOME","A1_CGC"})
/*/
//-----------------------------------------------------------------------------------
Static Function FATPDColObfuscate(aFields, cSource)  
    
	Local aPDColObf	:= {}

    If FATPDActive()
		aPDColObf := FTPDColObfuscate(aFields, cSource)  
    EndIf 

Return aPDColObf

//-----------------------------------------------------------------------------
/*/{Protheus.doc} FATPDLogUser
    @description
    Realiza o log dos dados acessados, de acordo com as informações enviadas, 
    quando a regra de auditoria de rotinas com campos sensíveis ou pessoais estiver habilitada
	Remover essa função quando não houver releases menor que 12.1.27

   @type  Function
    @sample FATPDLogUser(cFunction, nOpc)
    @author Squad CRM & Faturamento
    @since 06/01/2020
    @version P12
    @param cFunction, Caracter, Rotina que será utilizada no log das tabelas
    @param nOpc, Numerico, Opção atribuída a função em execução - Default=0

    @return lRet, Logico, Retorna se o log dos dados foi executado. 
    Caso o log esteja desligado ou a melhoria não esteja aplicada, também retorna falso.

/*/
//-----------------------------------------------------------------------------
Static Function FATPDLogUser(cFunction, nOpc)

	Local lRet := .F.

	If FATPDActive()
		lRet := FTPDLogUser(cFunction, nOpc)
	EndIf 

Return lRet  

//-----------------------------------------------------------------------------
/*/{Protheus.doc} FATPDActive
    @description
    Função que verifica se a melhoria de Dados Protegidos existe.

    @type  Function
    @sample FATPDActive()
    @author Squad CRM & Faturamento
    @since 17/12/2019
    @version P12    
    @return lRet, Logico, Indica se o sistema trabalha com Dados Protegidos
/*/
//-----------------------------------------------------------------------------
Static Function FATPDActive()

    Static _lFTPDActive := Nil
  
    If _lFTPDActive == Nil
        _lFTPDActive := ( GetRpoRelease() >= "12.1.027" .Or. !Empty(GetApoInfo("FATCRMPD.PRW")) )  
    Endif

Return _lFTPDActive  
