#INCLUDE "Matr966.ch"
#Include "FIVEWIN.Ch"
#INCLUDE "INKEY.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR966B ³ Autor ³    Marcos Simidu      ³ Data ³ 23.07.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ GI-ICMS                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR966B                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION MATR966
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva Ambiente                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL aSavEnv	:=	MSSavEnv() 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao do Programa                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE	titulo	:=	STR0001 //"Guia de Informacao das Operacoes e Prestacoes Interestaduais - GI-ICMS"
PRIVATE	cDesc1	:=	STR0002 //"Ir  imprimir Guia de Informa‡„o das Opera‡oes e Prestacoes"
PRIVATE	cDesc2	:=	STR0003 //"Interestaduais - GI-ICMS conforme os parametros informados"
PRIVATE	cDesc3	:=	""
PRIVATE	nTamanho	:=	""
PRIVATE	cabec1	:=	""
PRIVATE	cabec2	:=	""
PRIVATE	cString	:=	"SF3"
PRIVATE	lEnd		:=	.f.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis de Controle de Impressao                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE	aReturn 	:=	{ STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
PRIVATE	cPerg		:=	"MTR966"
PRIVATE	nLastKey	:=	0
PRIVATE	wnrel		:=	"MATR966"
PRIVATE	m_pag		:=	1
PRIVATE	nLin		:=	80
PRIVATE	cbcont	:= 0
PRIVATE	cbtxt		:=	SPACE(10)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parametros utilizados:                                       ³
//ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
//³ mv_par01 = De Data                                           ³
//³ mv_par02 = Ate Data                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SetPrint                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel	:=	SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.)
If ( nLastKey==27 )
	MSSavEnv(aSavEnv) 
	Return
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica impressora                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetDefault(aReturn,cString)
If ( nLastKey==27 )
	MSSavEnv(aSavEnv) 
	Return
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa gauge de processamento                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

RptStatus({|lEnd| R966Imp(@lEnd,ntamanho,wnrel,cString)},Titulo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura Ambiente                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MSSavEnv(aSavEnv) 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Spool de Impressao                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aReturn[5] == 1 
	Set Printer TO 
	dbcommitAll()
	ourspool(wnrel)
Endif

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R966Imp()³ Autor ³    Marcos Simidu      ³ Data ³ 23.07.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ GI-ICMS                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ R966Imp()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function R966Imp()
LOCAL aTotEst	:=	{}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Totaliza CFO's e resume por estado                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aTotEst	:=	R966BTotal(mv_par01,mv_par02,lEnd)

If Len(aTotEst)=0
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Detalhe das Operacoes     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IncRegua()

R966DetOperac(aTotEst)

If !lEnd
	Roda(cbcont,cbtxt,nTamanho)
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R966Cabec³ Autor ³ Juan Jose Pereira     ³ Data ³ 25.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime cabecalho                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC FUNCTION R966Cabec(nLin,lFirst)
lFirst	:=	IIf(Empty(lFirst),.f.,lFirst)
If nLin>=55 .and. !lEnd
    If !lFirst
        @ ++nLin,117 PSAY STR0006 //"( Continua... )"
        lFirst	:=	.f.
    Endif
    Cabec(titulo,cabec1,cabec2,wnrel,nTamanho)
    nLin	:=	PRow()
    @ ++nLin,00 PSAY Replic("*",132)
    nLin++
Endif
Return (nLin)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fim da Funcao                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R966DetOperac³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime detalhamento das operacoes por unidade da Federacao³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC FUNCTION R966DetOperac(aTotEst) 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local i			:=	1
Local aTotal	:=	{0,0,0,0,0,0,0,0,0}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica Interrupcao                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lEnd .or. Empty(aTotEst)
    Return
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime cabecalho                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aTotEst)>=1
    nLin	:=	80
    R966Cabec(@nLin,.T.)
Endif

@ ++nLin,00 PSAY "+---------------------------------------------------------------------------------------------------------------------------------+"
@ ++nLin,00 PSAY STR0007 //"|                                       Entradas de Mercadorias, Bens ou Aquisicoes de Servicos                                                                 |"
@ ++nLin,00 PSAY "|----+-----------------+-----------------+-----------------+----------------------+-----------------+-----------------------------+"
@ ++nLin,00 PSAY STR0008 //"|    |                    |                   |                   |                               |                          |         ICMS Cobrado por         |"
@ ++nLin,00 PSAY STR0009 //"|    |                    |                   |                   |                               |                          |     Substituicao Tributaria      |"
@ ++nLin,00 PSAY STR0010 //"| UF |       Valor        |     Base de       |      Outras       |   Bens de Uso de Consumo      |    Ativo Imobilizado     |+---------------+-----------------+"
@ ++nLin,00 PSAY STR0011 //"|    |      Contabil      |     Calculo       |                   |                               |                          |    Petroleo    |      Outros     |"
@ ++nLin,00 PSAY STR0012 //"|    |                    |                   |                   |                               |                          |    Energia     |      Produtos   |"
@ ++nLin,00 PSAY "|----+-----------------+-----------------+-----------------+----------------------+-----------------+-----------------------------+"
//                | XX |  99,999,999,999.99 | 99,999,999,999.99 | 99,999,999,999.99 |             99,999,999,999.99 |        99,999,999,999.99 |        99,999,999,999.99         |"
//                |----+--------------------+-------------------+-------------------+-------------------------------+--------------------------+----------------------------------+"
//                x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789
//                0         10        20        30        40        50        60        70        80        90        100       110       120       130       140       150  
For i:=1 to Len(aTotEst)
	If (aTotEst[i,2]<=0)
		Loop
	Endif
	 
	If lEnd
		@PROW()+1,001 PSAY STR0013 //"CANCELADO PELO OPERADOR"
		Return
	Endif
	
	R966Cabec(@nLin)
	@ ++nLin,00 PSAY "|"
	@ nLin,002 PSAY aTotEst[i,1]
	@ nLin,005 PSAY "|"
	@ nLin,006 PSAY aTotEst[i,2] PICTURE "@E 99,999,999,999.99"
	@ nLin,023 PSAY "|"
	@ nLin,024 PSAY aTotEst[i,3] PICTURE "@E 99,999,999,999.99"
	@ nLin,041 PSAY "|"
	@ nLin,041 PSAY aTotEst[i,4] PICTURE "@E 99,999,999,999.99"
	@ nLin,058 PSAY "|"
	@ nLin,064 PSAY aTotEst[i,14] PICTURE "@E 99,999,999,999.99" // Bens de Uso ou Consumo
	@ nLin,081 PSAY "|"
	@ nLin,082 PSAY aTotEst[i,13] PICTURE "@E 99,999,999,999.99" // Ativo Imobilizado
	@ nLin,099 PSAY "|"
	@ nLin,100 PSAY aTotEst[i,5] PICTURE "@E 999,999,999.99"
	@ nLin,114 PSAY "|"
	@ nLin,115 PSAY aTotEst[i,6] PICTURE "@E 999,999,999.99"
	@ nLin,128 PSAY "|"
	@ ++nLin,00 PSAY "|----+-----------------+-----------------+-----------------+----------------------+-----------------+--------------+--------------+"
	aTotal[1]	+=	aTotEst[i,2]
	aTotal[2]	+=	aTotEst[i,3]
	aTotal[3]	+=	aTotEst[i,4]
	aTotal[4]	+=	aTotEst[i,5]
	aTotal[5]	+=	aTotEst[i,6]
	aTotal[8]	+=	aTotEst[i,13]
	aTotal[9]	+=	aTotEst[i,14]
Next

@ ++nLin,00 PSAY STR0014 //"|TOTAL"
@   nLin,130 PSAY "|"
@ ++nLin,00 PSAY "|----+-----------------+-----------------+-----------------+----------------------+-----------------+-----------------------------+"
@ ++nLin,00 PSAY "|"
@ nLin,006 PSAY aTotal[1] PICTURE "@E 99,999,999,999.99"
@ nLin,023 PSAY "|"
@ nLin,024 PSAY aTotal[2] PICTURE "@E 99,999,999,999.99"
@ nLin,041 PSAY "|"
@ nLin,041 PSAY aTotal[3] PICTURE "@E 99,999,999,999.99"
@ nLin,058 PSAY "|"
@ nLin,064 PSAY aTotal[9] PICTURE "@E 99,999,999,999.99" // Bens de Uso ou Consumo
@ nLin,081 PSAY "|"
@ nLin,082 PSAY aTotal[8] PICTURE "@E 99,999,999,999.99" // Ativo Imobilizado
@ nLin,099 PSAY "|"
@ nLin,100 PSAY aTotal[4] PICTURE "@E 999,999,999.99"
@ nLin,114 PSAY "|"
@ nLin,115 PSAY aTotal[5] PICTURE "@E 999,999,999.99"
@ nLin,128 PSAY "|"
@ ++nLin,00 PSAY "|----+-----------------+-----------------+-----------------+----------------------+-----------------+-----------------------------+"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicia Impressao de Registros de saidas                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aTotal:=	{0,0,0,0,0,0,0}
nLin	:=	80
R966Cabec(@nLin,.T.)
nLin++
@ ++nLin,00 PSAY "+----------------------------------------------------------------------------------------------------------------------------------+"
@ ++nLin,00 PSAY STR0015 //"|    Saidas de Mercadorias e/ou Prestacao de Servicos                                                                              |"
@ ++nLin,00 PSAY "|----+-----------------------------------------+-----------------------------------------+--------------------+--------------------+"
@ ++nLin,00 PSAY "|    |                                         |                                         |                    |                    |"
@ ++nLin,00 PSAY STR0016 //"|    |            Valor Contabil               |                Base de Calculo          |                    |       ICMS         |"
@ ++nLin,00 PSAY STR0017 //"| UF |                                         |                                         |      Outras        |    Cobrado por     |"
@ ++nLin,00 PSAY STR0018 //"|    +--------------------+--------------------+--------------------+--------------------+                    |  Subst. Tributaria |"
@ ++nLin,00 PSAY STR0019 //"|    |   Nao Contribuinte |       Contribuinte |   Nao Contribuinte |       Contribuinte |                    |                    |"
@ ++nLin,00 PSAY "|----+--------------------+--------------------+--------------------+--------------------+--------------------+--------------------+"
//               | XX |  99,999,999,999.99 |  99,999,999,999.99 |  99,999,999,999.99 |  99,999,999,999.99 |  99,999,999,999.99 |  99,999,999,999.99 |
//               |----+--------------------+--------------------+--------------------+--------------------+--------------------+--------------------+
//               x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123
//               0         10        20        30        40        50        60        70        80        90        100       110       120 
For i:=1 to Len(aTotEst)
	If (aTotEst[i,9]<=0).And.(aTotEst[i,10]<=0).And.(aTotEst[i,7]<=0).And.(aTotEst[i,8]<=0)
		Loop
	Endif
	If lEnd
		@PROW()+1,001 PSAY STR0013 //"CANCELADO PELO OPERADOR"
		Return 
	Endif
 
	R966Cabec(@nLin)
	@ ++nLin,00 PSAY "|"
	@ nLin,002 PSAY aTotEst[i,01]
	@ nLin,005 PSAY "|"
	@ nLin,008 PSAY aTotEst[i,09] PICTURE "@E 99,999,999,999.99"
	@ nLin,026 PSAY "|"
	@ nLin,029 PSAY aTotEst[i,07] PICTURE "@E 99,999,999,999.99"
	@ nLin,047 PSAY "|"
	@ nLin,050 PSAY aTotEst[i,10] PICTURE "@E 99,999,999,999.99"
	@ nLin,068 PSAY "|"
	@ nLin,071 PSAY aTotEst[i,08] PICTURE "@E 99,999,999,999.99"
	@ nLin,089 PSAY "|"
	@ nLin,092 PSAY aTotEst[i,12] PICTURE "@E 99,999,999,999.99"
	@ nLin,110 PSAY "|"
	@ nLin,113 PSAY aTotEst[i,11] PICTURE "@E 99,999,999,999.99"
	@ nLin,131 PSAY "|"		 
	@ ++nLin,00 PSAY "|----+--------------------+--------------------+--------------------+--------------------+--------------------+--------------------+"
	aTotal[1]	+=	aTotEst[i,09]
	aTotal[2]	+=	aTotEst[i,07]
	aTotal[3]	+=	aTotEst[i,10]
	aTotal[4]	+=	aTotEst[i,08]
	aTotal[5]	+=	aTotEst[i,12]
	aTotal[6]	+=	aTotEst[i,11]
Next

@ ++nLin,00 PSAY STR0014 //"|TOTAL"
@ nLin,131 PSAY "|"
@ ++nLin,00 PSAY "|----+--------------------+--------------------+--------------------+--------------------+--------------------+--------------------+"
@ ++nLin,00 PSAY "|"
@ nLin,005 PSAY "|"
@ nLin,008 PSAY aTotal[1] PICTURE "@E 99,999,999,999.99"
@ nLin,026 PSAY "|"
@ nLin,029 PSAY aTotal[2] PICTURE "@E 99,999,999,999.99"
@ nLin,047 PSAY "|"
@ nLin,050 PSAY aTotal[3] PICTURE "@E 99,999,999,999.99"
@ nLin,068 PSAY "|"
@ nLin,071 PSAY aTotal[4] PICTURE "@E 99,999,999,999.99"
@ nLin,089 PSAY "|"
@ nLin,092 PSAY aTotal[5] PICTURE "@E 99,999,999,999.99"
@ nLin,110 PSAY "|"
@ nLin,113 PSAY aTotal[6] PICTURE "@E 99,999,999,999.99"
@ nLin,131 PSAY "|"
@ ++nLin,00 PSAY "|----+--------------------+--------------------+--------------------+--------------------+--------------------+--------------------+"

aTotEst	:=	NIL
aTotal	:=	NIL

Return 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fim da Funcao                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R966BTotal   ³ Autor ³   Marcos Simidu   ³ Data ³ 23.07.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria Array com totalizacao por CFO e Periodo               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION R966BTotal(dDataIni,dDataFim,lEnd)
Local aCFO			:=	{}
Local aEstado		:=	{}

Local nPos			:=	0
Local i				:=	0

Local	cTipo		:=	""
Local cEstado		:=	""
Local cNContr 		:=	A965LeIni("999")
Local	lContr 		:=	.F.
Local cAllCFO		:=	A965AllCFO("SIGADMEF.CFP")
Local cPetroleo		:=	A965LeIni("998")
Local cMV_ESTADO   	:=	GetMv("MV_ESTADO")
Local cAliasSF3		:=	"SF3"
Local cAtivo 		:=	A965LeIni("114")
Local cBens 		:=	A965LeIni("115")

Local lQuery 		:= 	.F.

#IFDEF TOP
	Local aStruSF3	:=	{}
	Local cQuery	:=	""
	Local nX		:=	0
#ELSE
	Local cIndex    :=	""
	Local cCondicao :=	""
#ENDIF

Local bError

dbSelectArea("SF3")
dbSetOrder(1)

SetRegua(RecCount())
#IFDEF TOP
    If TcSrvType()<>"AS/400"
	    lQuery 		:= .T.
		cAliasSF3	:= "SF3_MTR966"
		aStruSF3	:= SF3->(dbStruct())
		cQuery	:=	"SELECT SF3.F3_FILIAL, SF3.F3_CFO, SF3.F3_TIPO, SF3.F3_ESTADO,SF3.F3_VALCONT, SF3.F3_VALICM, "
		cQuery	+=	"SF3.F3_VALIPI, SF3.F3_BASEICM, SF3.F3_ICMSRET, SF3.F3_OUTRICM, SF3.F3_ISENICM, SF3.F3_CLIEFOR, SF3.F3_LOJA "
		cQuery	+=	"FROM " + RetSqlName("SF3") + " SF3 "
		cQuery	+=	"WHERE SF3.F3_FILIAL = '" + xFilial("SF3") + "' AND "
		cQuery	+=	"SF3.F3_ENTRADA >= '" + Dtos(dDataIni) + "' AND "
		cQuery	+=	"SF3.F3_ENTRADA <= '" + Dtos(dDataFim) + "' AND "
		cQuery	+=	"SF3.F3_DTCANC = '' AND NOT(SF3.F3_TIPO IN ('I','P','S')) AND "
		cQuery	+=	"SF3.F3_CFO IN " + FormatIN(cAllCFO) +" AND " 
		cQuery	+=	"SF3.D_E_L_E_T_ = ' ' "
		cQuery	+=	"ORDER BY " + SqlOrder(SF3->(IndexKey()))
		cQuery	:=	ChangeQuery(cQuery)                       
		
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF3)
		For nX := 1 To len(aStruSF3)
			If aStruSF3[nX][2] <> "C" .And. FieldPos(aStruSF3[nX][1])<>0
				TcSetField(cAliasSF3,aStruSF3[nX][1],aStruSF3[nX][2],aStruSF3[nX][3],aStruSF3[nX][4])
			EndIf
		Next nX
		
		dbSelectArea(cAliasSF3)	
	Else  
#ENDIF
		cIndex		:=	CriaTrab(NIL,.F.)
	    cCondicao	:=	'F3_FILIAL == "' + xFilial("SF3") + '" .And. ' 
	    cCondicao	+=	'DTOS(F3_ENTRADA) >= "' + DTOS(dDataIni) + '" .And. DTOS(F3_ENTRADA) <= "' + DTOS(dDataFim) + '" .And. '
	    cCondicao	+=	'empty(F3_DTCANC) .And. !(F3_Tipo$"IPS") .AND. (ALLTRIM(F3_CFO)$"' + cAllCFO + '")'
	    IndRegua(cAliasSF3,cIndex,SF3->(IndexKey()),,cCondicao)
	    dbSelectArea(cAliasSF3)
	    SetRegua(RecCount())
	    dbGoTop()
#IFDEF TOP
	Endif    
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Levantamento de Notas Normais e de Complemento ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While !Eof()

	IncRegua()

	If lEnd
		@PROW()+1,001 PSAY STR0013 //"CANCELADO PELO OPERADOR"
		Exit
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtros      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cCFO		:=	alltrim((cAliasSF3)->F3_CFO)
	cTipo		:=	(cAliasSF3)->F3_TIPO
	cEstado	:=	(cAliasSF3)->F3_ESTADO
	
	nValCont :=	(cAliasSF3)->F3_VALCONT
	nValICM	:=	(cAliasSF3)->F3_VALICM
	nValIPI	:=	(cAliasSF3)->F3_VALIPI
	nBaseICM :=	(cAliasSF3)->F3_BASEICM
	nIcmRet  :=	(cAliasSF3)->F3_ICMSRET
	nOutras  :=	If(cMV_ESTADO<>"RJ",(cAliasSF3)->F3_OUTRICM,(cAliasSF3)->F3_OUTRICM+(cAliasSF3)->F3_ISENICM)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Acumula valores por estado  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	// Estrutura Array aEstado
	// [1] Sigla do Estado
	// [2] Vlr. Contabil	- Entrada
	// [3] Base Icm			- Entrada
	// [4] Outros - Entrada
	// [5] ICMS cobrado p/ Subst. Combustiveis - Entrada
	// [6] ICMS cobrado p/ Subst. Outros       - Entrada
	// [7] Vlr. Contabil	- Saida
	// [8] Base Icm			- Saida
	// [9] Vlr. Contabil	- Saida Nao Contribuinte
	//[10] Base Icm			- Saida Nao Contribuinte
	//[11] ICMS cobrado p/ Subst. - Saida
	//[12] Outros - Saida

	If Val(Substr(cCFO,1,1)) < 5
		If ( cTipo $ "DB" )
			dbSelectArea("SA1")
			dbSetOrder(1)
			If DbSeek(F3Filial("SA1")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA)
				If SA1->A1_CONTRIB=="1"
					lContr:=.T.			 
				Endif  
			Endif
		Else
			dbSelectArea("SA2")
			dbSetOrder(1)
			dbSeek(F3Filial("SA2")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA)

			bError := ErrorBlock({|| lContr := ( At(cCFO,cNContr) > 0 )})
			Begin Sequence
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³controla se o registro de 999 (nao contribuintes) foi digitado o c¢digo	³
				//³dos CFOs ou uma expressao logica como SA1->A1_TIPO="F"						³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				lContr := &cNContr
				lContr := If(ValType(lContr)=="L",lContr,lContr := ( At(cCFO,cNContr) > 0 ))
			End Sequence
			ErrorBlock(bError)			
		EndIf
	Else
		If (!cTipo $ "DB" )
			dbSelectArea("SA1")
			dbSetOrder(1)
			If DbSeek(F3Filial("SA1")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA)
				lContr:=.F.
				If SA1->A1_CONTRIB=="2"
					lContr:=.T.
				Endif  
			Endif
		Else
			dbSelectArea("SA2")
			dbSetOrder(1)
			dbSeek(F3Filial("SA2")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA)

			bError := ErrorBlock({|| lContr := ( At(cCFO,cNContr) > 0 )})
			Begin Sequence
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³controla se o registro de 999 (nao contribuintes) foi digitado o c¢digo	³
				//³dos CFOs ou uma expressao logica como SA1->A1_TIPO="F"						³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				lContr := &cNContr
				lContr := If(ValType(lContr)=="L",lContr,lContr := ( At(cCFO,cNContr) > 0 ))
			End Sequence
			ErrorBlock(bError)			
		EndIf
	EndIf

	nPos	:=	Ascan(aEstado,{|x|x[1]==cEstado})

	If ( nPos == 0 )
		aadd(aEstado,{ cEstado, 0 ,0 ,0 ,0 ,0 ,0 ,0 ,0 , 0 , 0 , 0 , 0 , 0})
		nPos := Len(aEstado)
	EndIF

	If Val(Substr(cCFO,1,1)) < 5
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ NFs Entrada         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aEstado[nPos,2]	+=	nValCont
		aEstado[nPos,3]	+=	nBaseICM
		aEstado[nPos,4]	+=	nOutras
		If (cCfo$cPetroleo)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³  Combustivel        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aEstado[nPos,5]	+=	nIcmRet
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³  Outros             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aEstado[nPos,6]	+=	nIcmRet
		Endif
		If (cCfo$cAtivo)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³  Ativo Imobilizado  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aEstado[nPos,13]	+=	nValCont
		EndIf
		If (cCfo$cBens)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Bens de Uso ou Consumo  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aEstado[nPos,14]	+=	nValCont
		EndIf	
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ NFs Saidas          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lContr
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Nao Contribuinte    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aEstado[nPos,9]	+=	nValCont
			aEstado[nPos,10]+=	nBaseICM
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Contribuinte        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aEstado[nPos,7]	+=	nValCont
			aEstado[nPos,8]	+=	nBaseICM
		Endif
		aEstado[nPos,11]	+=	nIcmRet
		aEstado[nPos,12]	+=	nOutras
	Endif
	dbSelectArea(cAliasSF3)
	dbSkip()
EndDo

If !lEnd
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Retiradas as truncagens dvd problemas de arredondamento ³
	//³ As truncagens o usuario deve fazer manualmente quando   ³
	//³ transcrever os valores para o formulario oficial        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aEstado)>0
		ASort(aEstado,,,{|x,y|x[1]<y[1]})
	Endif
Endif

If lQuery
	dbSelectArea(cAliasSF3)
	dbCloseArea()
Else
   	dbSelectArea("SF3")
	RetIndex("SF3")
	dbClearFilter()
	Ferase(cIndex+OrdBagExt())
Endif	

Return (aEstado)     



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FormatIn  ºAutor  ³Microsiga           º Data ³  08/05/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Formata uma string(considerando q todo caracter nao         º±±
±±º          ³numerico e um separador)para ser usada com a clausula IN do º±±
±±º			 ³SQL.                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Generico                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
#IFDEF TOP
static function FormatIN(cRet)
Local nX 	:=	0 
Local cNovo	:=	""

For nX := 1 to Len(cRet)
		If SubStr(cRet,nX,1)$"0123456789"
			cNovo += SubStr(cRet,nX,1)
		Else 
			If SubStr(cRet,nX+1,1)$"0123456789"
				cNovo := Alltrim(cNovo) + "','"
			Endif
		Endif                                                          
	Next
	cNovo	:= "('" + Alltrim(cNovo) + "')"
Return(cNovo)
#ENDIF
