#include "SIGAWIN.CH"
#DEFINE X_IMPOSTO    01 //Nome do imposto
#DEFINE X_NUMIMP     02 //Sufixo do imposto

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁM100ISCP ╨Autor  ЁFELIPE V. NAMBARA   ╨ Data Ё 23/10/2009  ╨╠/╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁCalcula Imposto ISC para Peru NF Entrada                    ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       ЁSigaFis - Localizacao Peru                                  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠ 
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function M100ISCP(cCalculo,nItem,aInfo)

Local aImp 			:= {}
Local aItem 		:= {}                                                        
Local aArea			:= GetArea()
Local aImpRef 		:= {}
Local aImpVal 		:= {}

Local cImp			:= ""
Local cTes   		:= ""
Local cProd			:= ""
Local cImpIncid		:= ""

Local nOrdSFC   	:= 0    
Local nRegSFC   	:= 0
Local nImp   		:= 0
Local nBase			:= 0
Local nAliq 		:= 0
Local nDecs 		:= 0  
Local nPos	 		:= 0
Local nI			:= 0
Local nE			:= 0
Local lXFis			:= .F.
Local xRet                                             
Local nlAliqX       := 0

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁIdentifica se a chamada da funcao do calculo do imposto esta sendo Ё
//Ёfeita pela matxfis ou pelas rotinas manuais do localizado.         Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
lXFis := (MafisFound() .And. ProcName(1)!="EXECBLOCK")

/*
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Observacao :                                                                  Ё
Ё                                                                               Ё
Ё A variavel ParamIxb tem como conteudo um Array[2], contendo :                 Ё
Ё [1,1] > Quantidade Vendida                                                    Ё
Ё [1,2] > Preco Unitario                                                        Ё
Ё [1,3] > Valor Total do Item, com Descontos etc...                             Ё
Ё [1,4] > Valor do Frete rateado para este Item                                 Ё
Ё         Para Portugal, o imposto do frete e calculado em separado do item     Ё
Ё [1,5] > Valor das Despesas rateado para este Item                             Ё
Ё         Para Portugal, o imposto das despesas e calculado em separado do item Ё
Ё [1,6] > Array Contendo os Impostos jА calculados, no caso de incidЙncia de    Ё
Ё         outros impostos.                                                      Ё
Ё [2,1] > Array aImposto, contendo as InformaГУes do Imposto que serА calculado.Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
*/
If !lXfis
	aItem		:= ParamIxb[1]
	aImp		:= ParamIxb[2]
	cImp		:= aImp[1]
	cImpIncid	:= aImp[10]
	cTes		:= SF4->F4_CODIGO
	cProd 		:= SB1->B1_COD
Else
   cImp			:= aInfo[1]
   cTes			:= MaFisRet(nItem,"IT_TES")
   cProd		:= MaFisRet(nItem,"IT_PRODUTO")   
   cImpIncid	:= ""
Endif           
               

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica a base de calculo do imposto e se ha o cadastro de impostos incidentes nesta base de calculoЁ
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !lXFis 	 
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁBase de calculo composta pelo valor da mercadoria + frete + seguro  Ё
	//ЁObservacao Importante: em Angola nao ha a figura de frete e seguro, Ё
	//Ёporem o sistema deve estar preparado para utilizar esses valores no Ё
	//Ёcalculo do imposto.                                                 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nBase := aItem[3]+aItem[4]+aItem[5] 

	//здддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁReduz os descontos concedidos da base de calculoЁ
	//юдддддддддддддддддддддддддддддддддддддддддддддддды
	If Subs(aImp[5],4,1) == "S" .And. Len(aImp) >= 18 .And. ValType(aImp[18]) == "N"
		nBase -= aImp[18]
	Endif                                              

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁSoma na base de calculo todos os demais impostos incidentes.        Ё
	//ЁObservacao Importante: em Angola nao existem impostos que incidem umЁ
	//Ёsobre o outro, porem o sistema deve estar preparado para utilizar   Ё
	//Ёesses valores no calculo do imposto.                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nI := At(cImpIncid,";" )
	nI := If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
	While nI>1
		nE := AScan(aItem[6],{|x| x[1]==Left(cImpIncid,nI-1)})
		If nE > 0
			nBase += aItem[6,nE,4]
		Endif
		cImpIncid := Stuff(cImpIncid,1,nI,"")
		nI := At(cImpIncid,";")
		nI := If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
	Enddo

Else 

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁSomente efetua esse processamento se a chamada da funcao estiver sendo        Ё
	//Ёfeita para montagem da base de calculo do imposto. Como a matxfis chama       Ё
	//Ёsempre 3 vezes as funcoes de impostos, esse filtro eh importante para diminuirЁ
	//Ёa lentidao do processamento.                                                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If cCalculo == "B" .Or. cCalculo == "V"  
	
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁBase de calculo composta pelo valor da mercadoria + frete + seguro  Ё
		//ЁObservacao Importante: em Angola nao ha a figura de frete e seguro, Ё
		//Ёporem o sistema deve estar preparado para utilizar esses valores no Ё
		//Ёcalculo do imposto.                                                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды                        
		
		If FunName() $ "MATA121" 
			If cTpFrete == "C-CIF"
				nBase:=MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
			Else
				nBase:=MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
			EndIf
		Else
			nBase:=MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
		EndIf		
						
	
		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁEfetua o desconto tambem nos impostos incidentesЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		nOrdSFC := (SFC->(IndexOrd()))
		nRegSFC := (SFC->(Recno()))	
		SFC->(DbSetOrder(2))
		If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+cImp)))
			cImpIncid := Alltrim(SFC->FC_INCIMP)
			If SFC->FC_LIQUIDO == "S"
				nBase -= If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
				If	SF1->(FieldPos("F1_ADIANT")) > 0 
					nBase -= If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_ADIANTTOT"),MaFisRet(nItem,"IT_ADIANTTOT"))
				EndIf	
				
			Endif   
		Endif   
	
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁSoma na base de calculo todos os demais impostos incidentes.        Ё
		//ЁObservacao Importante: em Angola nao existem impostos que incidem umЁ
		//Ёsobre o outro, porem o sistema deve estar preparado para utilizar   Ё
		//Ёesses valores no calculo do imposto.                                Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		SFC->(DbSetOrder(nOrdSFC))
		SFC->(DbGoto(nRegSFC))
		If !Empty(cImpIncid)
			aImpRef:=MaFisRet(nItem,"IT_DESCIV")
			aImpVal:=MaFisRet(nItem,"IT_VALIMP")
			For nI:=1 to Len(aImpRef)
				If !Empty(aImpRef[nI])
					If Trim(aImpRef[nI][1])$cImpIncid
						nBase += aImpVal[nI]
					Endif
				Endif
			Next
		Endif 
		
	Endif

Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica os decimais da moeda para arredondamento do valor  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
nDecs := IIf(Type("nMoedaNf") # "U",MsDecimais(nMoedaNf),MsDecimais(1))
                     
//здддддддддддддддддддддддддддддддддддддддддддддд©
//ЁIncio de cАlculo do imposto ISC para entrada  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддды
                                                
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Abaixo И a condiГЦo caso o imposto seja calculado para notas fiscais Ё
//Ё de saida com pedido de venda                                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

If !lXFis

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Se houver alguma exceГЦo fiscal o valor da aliquota por uni-  Ё 
	//Ё dade И utilizado para cАlcularo imposto e o valor da aliquota Ё
	//Ё fica zerada		                                              Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
    
	
	DbSelectArea("SFF")
	DbSetOrder(3)
	If DbSeek(xFilial("SFF")+aImp[1])
	    If SFF->FF_VLRISC > 0
			nlAliqX := SFF->FF_VLRISC 
			
			aImp[2] := 0
			aImp[3] := aItem[3]+aItem[4]+aItem[5]
			aImp[4] := aItem[1]*nlAliqX
			
			xRet:=aImp						
		EndIf		                
	EndIf

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Caso nЦo haja nenhuma exceГЦo fiscal И verificado se hА valor Ё
	//Ё no campo B1_VLRISC. Se houver o cАlculo И feito por unidade   Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			
	If nlAliqX <= 0 .And. SB1->B1_VLRISC > 0
		nlAliqX := SB1->B1_VLRISC  
	
		aImp[2] := 0
		aImp[3] := aItem[3]+aItem[4]+aItem[5]
		aImp[4] := aItem[1]*nlAliqX		

		xRet:=aImp 
	EndIf

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Caso nЦo haja nenhuma exceГЦo fiscal e o valor do campo     Ё 
	//Ё B1_VLRISC esteja zerado, И verificado se o vampo B1_ALQISC  Ё
	//Ё possui valor. Se houver, o imposto И calculado por taxa uti-Ё
	//Ё lizando a aliquota do campo B1_VLRISC	                    Ё 
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	If nlAliqX <= 0 .And. SB1->B1_ALQISC > 0
			nlAliqX := SB1->B1_ALQISC
			nBase:=aItem[3]+aItem[4]+aItem[5]  //valor total + frete + outros impostos
	
			aImp[02]:=nlAliqX
			aImp[03]:=nBase
	
			If Subs(aImp[5],4,1) == "S" .And. Len(aImp) >= 18 .And. ValType(aImp[18])=="N"
				aImp[3]	-=aImp[18]
			Endif
			
			aImp[4] := Round(aImp[3]*aImp[2]/100,2)
			xRet:=aImp				
	EndIf
			

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Caso nЦo haja nenhuma exceГЦo fisca, o valor do campo       Ё 
	//Ё B1_VLRISC esteja zerado e o valor do campo B1_ALQISC esteja Ё
	//Ё zerado o imposto И calculado pela taxa utilizando como ali- Ё 
	//Ё quota o valor do campo FB_ALIQ (Aliquota padrЦo) 	        Ё 
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды			

				
	If nlAliqX <= 0 
		DbSelectArea("SFB")
		DbSetOrder(1)		
		If SFB->(Dbseek(xfilial()+aImp[1]))
			nlAliqX := SFB->FB_ALIQ
			nBase:=aItem[3]+aItem[4]+aItem[5]  //valor total + frete + outros impostos
	
			aImp[02]:=nlAliqX
			aImp[03]:=nBase
			
			If Subs(aImp[5],4,1) == "S" .And. Len(aImp) >= 18 .And. ValType(aImp[18])=="N"
				aImp[3]	-=aImp[18]
			Endif                                                                         
			
			aImp[4] := Round(aImp[3]*aImp[2]/100,2)
			xRet:=aImp						
		EndIf
	EndIf
               
Else 

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Abaixo И a condiГЦo caso o imposto seja calculado para notas fiscais Ё
	//Ё de saida manual e notas de deb/crИd.                                 Ё  
	//Ё O cАlculo do imposto respeita a mesma regra utilizada acima          Ё	
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	Do Case

		Case cCalculo=="B"
 					
			xRet := nBase

		Case cCalculo=="A"

			DbSelectArea("SFF")
			DbSetOrder(3)
			If DbSeek(xFilial("SFF")+cImp)
			    If SFF->FF_VLRISC > 0
					nlAliqX := SFF->FF_VLRISC
					xRet    := 0
				EndIf
			EndIf
			
			If nlAliqX <= 0 
				DbSelectArea("SB1")
				DbSetOrder(1)
				If DbSeek(xFilial("SB1")+cProd)
					If SB1->B1_VLRISC > 0
						nlAliqX := SB1->B1_VLRISC
						xRet    := 0
					EndIf
				EndIf	  
			EndIf
			
			If nlAliqX <= 0 .And. SB1->B1_ALQISC > 0
				nlAliqX := SB1->B1_ALQISC
				xRet    := nlAliqX
			EndIf
			
			If nlAliqX <= 0
				DbSelectArea("SFB")
				DbSetOrder(1)
				If Dbseek(xfilial()+cImp)
					nlAliqX := SFB->FB_ALIQ
					xRet    := nlAliqX
				EndIf
			EndIf
			
       Case cCalculo=="V"

			DbSelectArea("SFF")
			DbSetOrder(3)
			If DbSeek(xFilial("SFF")+cImp)
			    If SFF->FF_VLRISC > 0
					nlAliqX := SFF->FF_VLRISC
					xRet := Round(SFF->FF_VLRISC*MaFisRet(nItem,"IT_QUANT"),2)
				EndIf
			EndIf
							
			If nlAliqX <= 0
				DbSelectArea("SB1")
				DbSetOrder(1)
				If DbSeek(xFilial("SB1")+cProd)
					If SB1->B1_VLRISC > 0
						nlAliqX := SB1->B1_VLRISC
						xRet := Round(SB1->B1_VLRISC*MaFisRet(nItem,"IT_QUANT"),2)
					EndIf
				EndIf					
			EndIf
					
			If nlAliqX <= 0 .And. SB1->B1_ALQISC > 0
				nlAliqX := SB1->B1_ALQISC
				nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
				nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
            	            
				xRet := Round((( nAliq * nBase) /100 ),2)
			EndIf
			
			If nlAliqX <= 0 
				DbSelectArea("SFB")
				DbSetOrder(1)		
				If SFB->(Dbseek(xfilial()+cImp))
					nlAliqX := SFB->FB_ALIQ
					
					nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
					nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
	            	            
					xRet := Round((( nAliq * nBase) /100 ),2)										
				EndIf
			EndIf   
	EndCase   

Endif

RestArea(aArea)

Return(xRet)  