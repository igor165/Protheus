#INCLUDE "MATA281.CH"
#INCLUDE "PROTHEUS.CH"
#DEFINE _ENTER CHR(13)+CHR(10)

/*/


Ŀ
Funo    MATA281      Autor  Rodrigo T. Silva      	Data 20/07/12
Ĵ
Descriao  Aplica o clculo e correcao sobre o valor de custos mdio  
 		  dos produtos nacionais e importados conforme compras       
 		  realizadas durante o ano vigente.						  
Ĵ
 Uso       GENERICO                                                   
ٱ


/*/

Function MATA281()
//Ŀ
// Inicializa variaveis                                         
//
Local oTNewProc
Local cTexto    := ""
Local bProcess  := {|oself|MA281Exec(oSelf)}
Local cPerg 	:= "MTA281"

cTexto := OemToAnsi(STR0001) +_ENTER  //"Objetivo desta rotina  efetuar o clculo e correo monetria sobre o valor de custo mdio dos "
cTexto += OemToAnsi(STR0002) +_ENTER  //"produtos nacionais e importados conforme compras realizadas para os produtos no ano vigente." 
cTexto += OemToAnsi(STR0003) +_ENTER  //"O processamento desta rotina deve ser permitido somente para datas superiores ao ltimo fechamento "
cTexto += OemToAnsi(STR0004) +_ENTER  //"de estoque (conforme MV_ULMES)."
cTexto += OemToAnsi(STR0005) +_ENTER  //"Antes de continuar certifique-se de que no h nenhum outro usurio atualizando os arquivos de" +_ENTER
cTexto += OemToAnsi(STR0006) +_ENTER  //"movimentao de estoque."

//Ŀ
// Sintaxe da tNewProcess():New( cFunction, cTitle, bProcess, cDescription, cPerg, aInfoCustom ) |
//
oTNewProc := tNewProcess():New("MATA281",STR0007,bProcess,cTexto,cPerg) //"Clculo da Correo Monetria"

Return

/*/


Ŀ
Funo    MA281Exec    Autor  Rodrigo T. Silva	  	Data 05/01/11
Ĵ
Descrio  Inicializa o processamento de calculo da correcao monetaria
Ĵ
ParametrosoTNewProc = Objeto tNewProcess()                            
Ĵ
 Uso       MATA281                                                    
ٱ


/*/
Static Function MA281Exec(oTNewProc)
	A710GrvTm(oTNewProc, STR0008) //"Inicio Processamento"
	If (oTNewProc<>Nil)
		MA281Process(oTNewProc:lEnd,oTNewProc)
	Else
		Processa({|lEnd| MA281Process(@lEnd)},STR0007,STR0009,.F.)   //"Clculo da Correo Monetria"###"Calculando correo monetria dos produtos..."
	EndIf
	A710GrvTm(oTNewProc, STR0010) //"Fim Processamento"
Return

/*/


Ŀ
Funo    MA281Process Autor  Rodrigo T. Silva	  	Data 05/01/12
Ĵ
Descrio  Executa o clculo de correcao monetaria dos produtos 	   
			  nacionais e importados.									   
Ĵ
ParametroslEnd      = Indica se processamento devera abortar (logico) 
          oTNewProc = Objeto tNewProcess()                            
Ĵ
 Uso       MATA281                                                    
ٱ


/*/

Static Function MA281Process(lEnd,oTNewProc)
Local aAreaSB1 	 := GetArea()
Local aAreaSB2 	 := GetArea()
Local aAreaSDZ 	 := GetArea()
Local nTotRegs   := 0
Local cFilBack   := cFilAnt
Local nForFilial := 0
Local aFilsCalc	 := {}
Local aCposSDZ 	 := {}
Local dDatFec	 := mv_par01
Local dDtInic	 := CToD(StrZero(Day(mv_par01),2) + "/" + StrZero(Month(mv_par01),2) + "/" + StrZero(Year(mv_par01)-1,4))
Local aSldAtual	 := {}
Local lAchouProd := .F.

If mv_par03 == 3   //Seleciona filiais
	aFilsCalc := MatFilCalc(.T.)
ElseIf mv_par03 == 1 //Adiciona todas as filias da empresa
	dbSelectArea("SM0")
	dbSeek(cEmpAnt)
	Do While ! Eof() .And. SM0->M0_CODIGO == cEmpAnt
		aAdd(aFilsCalc,{.T.,SM0->M0_CODFIL,SM0->M0_FILIAL,SM0->M0_CGC}) //Adiciona todas as filias da empresa 
		dbSkip()
	EndDo	
Else
	aAdd(aFilsCalc,{.T.,SM0->M0_CODFIL,SM0->M0_FILIAL,SM0->M0_CGC}) //Adiciona a filial corrente
EndIf

If A281TudoOk(aFilsCalc,dDatFec)
	aAdd(aCposSDZ,{"DZ_FILIAL", Nil})
	aAdd(aCposSDZ,{"DZ_PRODUTO", Nil})
	aAdd(aCposSDZ,{"DZ_DATAFEC", Nil})
	aAdd(aCposSDZ,{"DZ_MOEDA",   Nil})
	aAdd(aCposSDZ,{"DZ_DTULCOM", Nil})
	aAdd(aCposSDZ,{"DZ_CUULCOM", Nil})
	aAdd(aCposSDZ,{"DZ_DTMACOM", Nil})
	aAdd(aCposSDZ,{"DZ_CUMACOM", Nil})
	aAdd(aCposSDZ,{"DZ_TPCORRE", Nil})
	aAdd(aCposSDZ,{"DZ_FTCORRE", Nil})
	aAdd(aCposSDZ,{"DZ_VLRBASE", Nil})
	aAdd(aCposSDZ,{"DZ_CUUNCOR", Nil})
	aAdd(aCposSDZ,{"DZ_CUTOTCR", Nil})
	aAdd(aCposSDZ,{"DZ_VLRCMON", Nil})
	aAdd(aCposSDZ,{"DZ_LOCAL", Nil})
	aAdd(aCposSDZ,{"ACHOUPROD",  .F.})
	If !Empty(aFilsCalc)
		For nForFilial := 1 To Len(aFilsCalc)
			If aFilsCalc[nForFilial, 1] .And. MA281Lock(aFilsCalc[nForFilial, 2])
				cFilAnt := 	aFilsCalc[nForFilial, 2]
				If mv_par02 <> 3					
					//Ŀ
					//Caso exista valores no periodo em fechamento o sitema apagara |
					//os registros para efetuar um novo processamento  		     |
					//				
					A281DelSDZ(DtoS(dDatFec))
					SB1->(dbSetOrder(1))
					SB1->(dbSeek(xFilial("SB1")))
					nTotRegs += SB1->(RecCount())
					If (oTNewProc<>Nil)
						oTNewProc:SetRegua1(nTotRegs)
					Else
						ProcRegua(nTotRegs,21,6)
					EndIf
		
					While SB1->(!Eof()) .And. SB1->B1_FILIAL == xFilial("SB1")
						If (oTNewProc<>Nil)
							oTNewProc:IncRegua1(STR0011 + aFilsCalc[nForFilial, 2] + " - " + aFilsCalc[nForFilial, 3]) // "Processando Filial:  "
						Else
							IncProc(STR0011 + aFilsCalc[nForFilial, 2] + " - " + aFilsCalc[nForFilial, 3]) // "Processando Filial:  "
						EndIf
						If !IsProdMOD(SB1->B1_COD,.T.) 
							A281MovSD1(SB1->B1_COD,dDtInic,dDatFec,aCposSDZ)
							
							SB2->(dbSetOrder(1))
							SB2->(dbSeek(xFilial("SB2")+SB1->B1_COD))
							While SB2->(!Eof()) .And. SB2->(B2_FILIAL+B2_COD) == xFilial("SB2")+SB1->B1_COD
								//Ŀ
								//|analisa a tabela SD3 para verificar se a correcao monetaria j foi lancada |
								//
								If A281VerSD3(dDatFec,SB1->B1_COD,SB2->B2_LOCAL)
									//Ŀ
									//|efetua o calculo da correcao monetaria conforme definicao das regras |
									//								
									If aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "ACHOUPROD"}),2] == .T.
										aSldAtual := CalcEst(SB1->B1_COD,SB2->B2_LOCAL,dDatFec+1)
										aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_FILIAL"}),2] := xFilial("SDZ")
										aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_PRODUTO"}),2] := SB1->B1_COD
										aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_DATAFEC"}),2] := dDatFec
										aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_LOCAL"}),2] := SB2->B2_LOCAL
										aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUTOTCR"}),2] := Round(aSldAtual[1] * aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUUNCOR"}),2],2)
										aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_VLRCMON"}),2] := Round(aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUTOTCR"}),2] - aSldAtual[2],2)
										//Ŀ
										//|Grava os registros que foram calculados na tabela SDZ |
										//
										A281GrvSDZ(aCposSDZ)
									EndIf
								EndIf
								SB2->(dbSkip())
							End
						EndIf
						SB1->(dbSkip())
					End
				EndIf
				//Ŀ
				//|Grava os registros na tabela SD3 conforme a tabela SDZ |
				//
				If mv_par02 > 1
					A281GrvSD3(dDatFec,oTNewProc,aFilsCalc[nForFilial,2],aFilsCalc[nForFilial,3])
				EndIf
			EndIf
			// Desbloqueio de filiais bloqueadas
			MA281UnLock()
		Next nForFilial
	EndIf
	cFilAnt := cFilBack
	MsUnlockAll()
EndIf

RestArea(aAreaSB1)
RestArea(aAreaSB2)
RestArea(aAreaSDZ)
Return

/*


Ŀ
Funo    A281MovSD1   Autor  Rodrigo T. Silva     Data  09/01/12 
Ĵ
Descrio  pega todo o movimento da tabela SD1                        
Ĵ
Parametros ExpC1 = codigo do produto                  				  
			  ExpC2 = codigo do armazem               					  
			  ExpD1 = data de inicio do fechamento        				  
			  ExpD2 = data final do fechamento        				      
			  ExpA1 = Array contendo os campos da tabela SDZ             
Ĵ
 Uso       MATA281                                                    
ٱ


*/
Static Function A281MovSD1(cProduto,dDtInic,dDatFec,aCposSDZ)
Local aAreaSD1  := GetArea()
Local aAreaSF1  := SF1->(GetArea())
Local cFator    := ""
Local dUltComp  := dDtInic
Local dDtMComp  := dDtInic
Local cSem1     := DToS(dDatFec - 184)
Local lAnual    := .F.
Local nVrBase   := 0
Local nCMaior   := 0
Local nUCusto   := 0
Local nMoedaSF1 := 0

#IFDEF TOP     
	//Ŀ
	//|busca a ultima aquisicao do periodo em fechamento|
	//	
	cAliasSD1 := CriaTrab(NIL,.F.)
	cQuery := "SELECT TOP 1 SD1.D1_DTDIGIT,SD1.D1_VUNIT,SF1.F1_MOEDA FROM "+RetSqlName("SD1")+" SD1 "
	cQuery += "JOIN "+RetSqlName("SF1")+" SF1 ON "
	cQuery += "		SF1.F1_FILIAL = '" +xFilial("SF1") +"' AND "
	cQuery += "		SF1.F1_DOC = SD1.D1_DOC AND "
	cQuery += "		SF1.F1_SERIE = SD1.D1_SERIE AND "
	cQuery += "		SF1.F1_FORNECE = SD1.D1_FORNECE AND "
	cQuery += "		SF1.F1_LOJA = SD1.D1_LOJA AND "
	cQuery += "		SF1.F1_ORIGLAN <> 'LF' "
	cQuery += "JOIN "+RetSqlName("SF4")+" SF4 ON "
	cQuery += "		SF4.F4_FILIAL = '" +xFilial("SF4") +"' AND "
	cQuery += "		SF4.F4_CODIGO = SD1.D1_TES AND "
	cQuery += "		SF4.F4_ESTOQUE = 'S' "
	cQuery += "WHERE SD1.D1_FILIAL = '" +xFilial("SD1") +"' AND "
	cQuery += "		SD1.D1_COD = '" +cProduto +"' AND "
	cQuery += "		SD1.D1_TIPO = 'N' AND "
	cQuery += "		SD1.D1_DTDIGIT BETWEEN '"+DToS(dDtInic)+"' AND '"+DtoS(dDatFec)+"' AND "
	cQuery += "		SD1.D_E_L_E_T_ <> '*' "
	cQuery += "ORDER BY SD1.D1_DTDIGIT DESC "
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1,.T.,.T.)
	If !Empty((cAliasSD1)->D1_DTDIGIT)
 		dUltComp  := SToD((cAliasSD1)->D1_DTDIGIT)
   		nUCusto   := (cAliasSD1)->D1_VUNIT
   		nMoedaSF1 := IIF(SB1->B1_IMPORT == "S", nMoedaSF1 := (cAliasSD1)->F1_MOEDA,1)
	EndIf
	(cAliasSD1)->(dbCloseArea())
		
	//Ŀ
	//|busca aquisicao mas cara do periodo em fechamento|
	//	
	cAliasSD1:= CriaTrab(NIL,.F.)
	cQuery := "SELECT TOP 1 SD1.D1_VUNIT AS VALOR_ALTO, SD1.D1_DTDIGIT FROM "+RetSqlName("SD1")+" SD1 "
	cQuery += "JOIN "+RetSqlName("SF4")+" SF4 ON "
	cQuery += "		SF4.F4_FILIAL = '" +xFilial("SF4") +"' AND "
	cQuery += "		SF4.F4_CODIGO = SD1.D1_TES AND "
	cQuery += "		SF4.F4_ESTOQUE = 'S' "
	cQuery += "WHERE SD1.D1_FILIAL = '" +xFilial("SD1") +"' AND "
	cQuery += "		SD1.D1_COD = '" +cProduto +"' AND "
	cQuery += "		SD1.D1_TIPO = 'N' AND "
	cQuery += "		SD1.D1_ORIGLAN <> 'LF' AND "
	cQuery += "		SD1.D1_DTDIGIT BETWEEN '" +DToS(dDtInic) +"' AND '" +DtoS(dDatFec) +"' AND "
	cQuery += "		SD1.D_E_L_E_T_ <> '*' "
	cQuery += "ORDER BY SD1.D1_VUNIT DESC"
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1,.T.,.T.)
	If (cAliasSD1)->VALOR_ALTO > 0
 		nCMaior := (cAliasSD1)->VALOR_ALTO
   		dDtMComp := StoD((cAliasSD1)->D1_DTDIGIT)
	EndIf
	(cAliasSD1)->(dbCloseArea())
	
	If nUCusto > 0
		aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "ACHOUPROD"}),2] := .T.
		cFator := IIF(DtoS(dUltComp) < cSem1, "1", "2")
		nVrBase := IIf(SB1->B1_IMPORT == "N",nCMaior,nUCusto)
	Else
		//Ŀ
		//|busca aquisicoes do periodo em fechamento do exercicio anterior|
		//	
		cFator := "3"
		
		//-- Grava branco nos dados do ano
		dUltComp  := SToD("")
   		dDtMComp  := StoD("")
   		nUCusto   := 0
   		nMoedaSF1 := 0  		
		nCMaior   := 0
		
		SB9->(dbSetOrder(1))
		SB9->(dbSeek(xFilial("SB9")+cProduto+DToS(dDatFec),.T.))
		SB9->(dbSkip(-1))
		If SB9->(B9_FILIAL+B9_COD) == xFilial("SB9")+cProduto
			nVrBase := SB9->B9_VINI1 / SB9->B9_QINI
		Else
			nVrBase := 0
		EndIf
	EndIf
#ELSE
	SD1->(dbSetOrder(5))
	If SD1->(dbSeek(xFilial("SD1")+cProduto))	
		While SD1->(D1_FILIAL+D1_COD) == xFilial("SD1")+cProduto
			If SD1->D1_TIPO == "N" .And. SD1->D1_DTDIGIT >= dDtInic .And. SD1->D1_DTDIGIT <= dDatFec .And. SD1->D1_ORIGLAN <> 'LF'
				If A281VerSF4(SD1->D1_TES)
					aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "ACHOUPROD"}),2] := .T.
					cFator := IIF(DToS(SD1->D1_DTDIGIT) < cSem1, "1", "2")
			  		If SB1->B1_IMPORT == "N"       // quando o produto eh nacional
				      	nMoedaSF1 := 1
			       		If SD1->D1_VUNIT > nVrBase
			         		nVrBase := SD1->D1_VUNIT		         		
			       		EndIf
			       	ElseIf SB1->B1_IMPORT == "S"    // quando o produto eh importado
			            If SD1->D1_DTDIGIT >= dDtMComp
			            	nVrBase := SD1->D1_VUNIT
			      		EndIf
			      	EndIf
			       	If SD1->D1_VUNIT > nCMaior
			        	nCMaior := SD1->D1_VUNIT
			        	dDtMComp := SD1->D1_DTDIGIT
			       	EndIf
		        	If SD1->D1_DTDIGIT > dUltComp  
		         		dUltComp := SD1->D1_DTDIGIT
		         		nUCusto  := SD1->D1_VUNIT
		         		If SB1->B1_IMPORT == "S"
							SF1->(dbSetOrder(1))
							If SF1->(dbSeek(SD1->(D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA)))
								nMoedaSF1 := SF1->F1_MOEDA
							EndIf
		         		EndIf
		      		EndIf
		     	EndIf
	   		EndIf
		   	SD1->(dbSkip())
		EndDo
	EndIf
	
	If nVrBase == 0
		cFator   := "3" 
		
		//-- Grava branco nos dados do ano
		dUltComp  := SToD("")
   		dDtMComp  := StoD("")
   		nUCusto   := 0
   		nMoedaSF1 := 0  		
		nCMaior   := 0
		
		SB9->(dbSetOrder(1))
		SB9->(dbSeek(xFilial("SB9")+cProduto+DToS(dDatFec),.T.))
		SB9->(dbSkip(-1))
		If SB9->(B9_FILIAL+B9_COD) == xFilial("SB9")+cProduto
			nVrBase := SB9->B9_VINI1 / SB9->B9_QINI
		Else
			nVrBase := 0
		EndIf
	EndIf
#ENDIF

If aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "ACHOUPROD"}),2] == .T.
	aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_MOEDA"}),2] := nMoedaSF1
	aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_DTULCOM"}),2] := dUltComp
	aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUULCOM"}),2] := nUCusto
	aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_DTMACOM"}),2] := dDtMComp
	aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUMACOM"}),2] := nCMaior
	aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_TPCORRE"}),2] := cFator 		
	aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_FTCORRE"}),2] := A281FtCorr(SB1->B1_IMPORT,cFator,nUCusto,nMoedaSF1,dDatFec,IIF(cFator == "1", cSem1, dDtInic))
	aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_VLRBASE"}),2] := Round(nVrBase, 2)
	aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUUNCOR"}),2] := Round((1 + aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_FTCORRE"}),2]) * nVrBase, 2)
EndIf

RestArea(aAreaSD1)
RestArea(aAreaSF1)
Return (aCposSDZ)

/*


Ŀ
Funo    |A281GrvSDZ   Autor  Rodrigo T. Silva     Data  09/01/12 
Ĵ
Descrio  Grava o calcula da correcao monetaria na tabela SDZ        
Ĵ
Parametros ExpA1 = array contendo as informacoes de calculo da        
 		  correcao monetaria.									      
Ĵ
 Uso       MATA281                                                    
ٱ


*/
Static Function A281GrvSDZ(aCposSDZ)
	RecLock("SDZ",.T.)
	Replace DZ_FILIAL   with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_FILIAL"}),2]
	Replace DZ_PRODUTO  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_PRODUTO"}),2]
	Replace DZ_DATAFEC  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_DATAFEC"}),2]
	Replace DZ_MOEDA    with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_MOEDA"}),2]
	Replace DZ_DTULCOM  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_DTULCOM"}),2]
	Replace DZ_CUULCOM  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUULCOM"}),2]
	Replace DZ_DTMACOM  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_DTMACOM"}),2]
	Replace DZ_CUMACOM  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUMACOM"}),2]
	Replace DZ_TPCORRE  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_TPCORRE"}),2]
	Replace DZ_FTCORRE  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_FTCORRE"}),2]
	Replace DZ_VLRBASE  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_VLRBASE"}),2]
	Replace DZ_CUUNCOR  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUUNCOR"}),2]
	Replace DZ_CUTOTCR  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_CUTOTCR"}),2]
	Replace DZ_VLRCMON  with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_VLRCMON"}),2]
	Replace DZ_LOCAL    with aCposSDZ[aScan(aCposSDZ,{|x| x[1] == "DZ_LOCAL"}),2]
	MsUnlock()
Return

/*


Ŀ
Funo    |A281DelSDZ   Autor  Rodrigo T. Silva     Data  09/01/12 
Ĵ
Descrio  Grava o calcula da correcao monetaria na tabela SDZ        
Ĵ
Parametros ExpC1 = data fechamento do calculo de correcao monetaria   
Ĵ
 Uso       MATA281                                                    
ٱ


*/
Static Function A281DelSDZ(cDataFec)
Local cQuery := ""

#IFDEF TOP
   	cQuery := "DELETE FROM "+RetSqlName("SDZ")
   	cQuery += " WHERE DZ_FILIAL = '" +xFilial("SDZ") +"'"
   	cQuery += " AND DZ_DATAFEC = '" +cDataFec +"'"
   	cQuery += " AND D_E_L_E_T_ <> '*'"
	TcSqlExec(cQuery)   
#ELSE
   	SDZ->(dbSetOrder(1))
   	While SDZ->(!Eof()) .And. SDZ->(DZ_FILIAL+DZ_DATAFEC) == xFilial("SDZ")+cDataFec
    	RecLock("SDZ")
      	dbDelete()
	  	MsUnlock()
      	SDZ->(dbSkip())
   	EndDo
#ENDIF

Return

/*/


Ŀ
Funcao    A281GrvSD3 Autor  Rodrigo T. Silva       Data  11.01.12 
Ĵ
Descricao  Gera movimento na tabela SD3 conforme registros gerados    
			  na tabela SDZ.										      
Ĵ
Parametros ExpC1: Codigo da Filial                                    
           ExpD1: Data de Fechamento                                  
           ExpD1: Data de Fechamento                                  
Ĵ
 Uso       MATA281                                                    
ٱ

/*/
Function A281GrvSD3(dDataFec,oTNewProc,cDescFil01,cDescFil02)
Local aAreaSDZ := GetArea()
Local aAreaSD3 := GetArea()
Local aCusto   := {}
Local aCM      := {}
Local cNumSeq  := ""
Local cReqDev  := ""
Local cMarca   := GetMark()
Local nTotRegs := 0
Local cTPMovCM := SuperGetMv("MV_TPMOVCM")
Local cAlias   := ""
local lTop	   := .T.

#IFDEF TOP
	//Ŀ
	//|busca quantidade de registros da tabela SDZ para passar para o oTNewProc |
	//	
	cAlias := CriaTrab("SDZ",.F.)
	cQuery := "SELECT COUNT(*) AS QTDE FROM " +RetSqlName("SDZ") +" SDZ "
	cQuery += "WHERE DZ_FILIAL = '" +xFilial("SDZ") +"' AND "
	cQuery += "DZ_DATAFEC = " +DtoS(dDataFec) +" AND "
	cQuery += "DZ_OK = ' ' AND "
	cQuery += "DZ_VLRCMON <> 0 AND "
	cQuery += "D_E_L_E_T_ = ' ' "
	cQuery += "GROUP BY DZ_DATAFEC"
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
	nTotRegs := (cAlias)->QTDE
	(cAlias)->(dbCloseArea())
	
	//Ŀ
	//|busca registros da tabela SDZ para inserir os movimentos na tabela SD3 |
	//	
	cAlias := CriaTrab("SDZ",.F.)
	cQuery := "SELECT * FROM " +RetSqlName("SDZ") +" SDZ "
	cQuery += "WHERE DZ_FILIAL = '" +xFilial("SDZ") +"' AND "
	cQuery += "DZ_DATAFEC = " +DtoS(dDataFec) +" AND "
	cQuery += "DZ_OK = ' ' AND "
	cQuery += "DZ_VLRCMON <> 0 AND "
	cQuery += "D_E_L_E_T_ = ' ' "
	cQuery += "ORDER BY DZ_PRODUTO"
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
#ELSE
	cAlias := "SDZ"
	(cAlias)->(dbSetOrder(1))
	(cAlias)->(DbSeek(xFilial("SDZ")+DtoS(dDataFec)))
	nTotRegs += (cAlias)->(RecCount())
	lTop := .F.
#ENDIF

If (oTNewProc<>Nil)
	oTNewProc:SetRegua1(nTotRegs)
Else
	ProcRegua(nTotRegs,21,6)
EndIf

While !(cAlias)->(EOF()) .And. IIF(lTop,(cAlias)->(DZ_FILIAL+DZ_DATAFEC) == xFilial("SDZ")+DtoS(dDataFec),(cAlias)->(DZ_FILIAL+DtoS(DZ_DATAFEC)) == xFilial("SDZ")+DtoS(dDataFec)) .And. Empty((cAlias)->DZ_OK)		
	If (oTNewProc<>Nil)
		oTNewProc:IncRegua1(STR0011 + cDescFil01 + " - " + cDescFil02) // "Processando Filial:  "
	Else
		IncProc(STR0011 + cDescFil01 + " - " + cDescFil02) // "Processando Filial:  "
	EndIf	
    //Ŀ
	// Pega os dados do cadastro de produtos      
	//		
	SB1->(dbSetOrder(1))
	SB1->(DbSeek(xFilial("SB1")+(cAlias)->DZ_PRODUTO))
	
	//Ŀ
	// Pega o numero sequencial do movimento      
	//		
	cNumseq := ProxNum()
	cReqDev := IIF((cAlias)->DZ_VLRCMON < 0,"RE6","DE6")
	
	//Ŀ
	// Pega o custo unitario das 5 moedas      
	//
	aCM := {}		
	aAdd(aCM,xMoeda((cAlias)->DZ_VLRCMON,(cAlias)->DZ_MOEDA,1,IIF(lTop,StoD((cAlias)->DZ_DATAFEC),(cAlias)->DZ_DATAFEC)))
	aAdd(aCM,xMoeda((cAlias)->DZ_VLRCMON,(cAlias)->DZ_MOEDA,2,IIF(lTop,StoD((cAlias)->DZ_DATAFEC),(cAlias)->DZ_DATAFEC)))
	aAdd(aCM,xMoeda((cAlias)->DZ_VLRCMON,(cAlias)->DZ_MOEDA,3,IIF(lTop,StoD((cAlias)->DZ_DATAFEC),(cAlias)->DZ_DATAFEC)))
	aAdd(aCM,xMoeda((cAlias)->DZ_VLRCMON,(cAlias)->DZ_MOEDA,4,IIF(lTop,StoD((cAlias)->DZ_DATAFEC),(cAlias)->DZ_DATAFEC)))
	aAdd(aCM,xMoeda((cAlias)->DZ_VLRCMON,(cAlias)->DZ_MOEDA,5,IIF(lTop,StoD((cAlias)->DZ_DATAFEC),(cAlias)->DZ_DATAFEC)))
	
	dbSelectArea("SD3")
	RecLock("SD3",.T.)		
	Replace	D3_FILIAL  With xFilial("SD3")	,;
			D3_COD     With (cAlias)->DZ_PRODUTO,;
			D3_DOC     With "CORMON",;
			D3_EMISSAO With IIF(lTop,StoD((cAlias)->DZ_DATAFEC),(cAlias)->DZ_DATAFEC),;
			D3_GRUPO   With SB1->B1_GRUPO	,;
			D3_LOCAL   With (cAlias)->DZ_LOCAL	,;
			D3_UM      With SB1->B1_UM		,;
			D3_NUMSEQ  With cNumSeq			,;
			D3_SEGUM   With SB1->B1_SEGUM	,;
			D3_CONTA   With SB1->B1_CONTA	,;
			D3_TIPO    With SB1->B1_TIPO	,;
			D3_USUARIO With CUSERNAME		,;
			D3_CUSTO1  With aCM[1]		,;
			D3_CUSTO2  With aCM[2]		,;
			D3_CUSTO3  With aCM[3]		,;
			D3_CUSTO4  With aCM[4]		,;
			D3_CUSTO5  With aCM[5]		,;
			D3_CF      With cReqDev    	    ,;
			D3_TM      With IIF(cReqDev == "RE6", Substr(cTPMovCM,5,3),Substr(cTPMovCM,1,3)),;
			D3_CHAVE   With SubStr(D3_CF,2,1)+"0"

	//Ŀ
	// Grava o custo da movimentacao              
	//
	aCusto := GravaCusD3(aCM,,,,.T.)
	
	//Ŀ
	// Atualiza o saldo atual do estoque com os dados do SD3 
	//
	B2FimComD3(aCusto)
	MsUnLockAll()
	
	SDZ->(dbSetOrder(1))
	SDZ->(DbSeek(xFilial("SB1")+DtoS(dDataFec)+(cAlias)->DZ_PRODUTO))
	RecLock("SDZ",.F.)
	Replace SDZ->DZ_OK  With cMarca
	MsUnlock()
	(cAlias)->(dbSkip())
End

#IFDEF TOP
	(cAlias)->(dbCloseArea())
#ENDIF

RestArea(aAreaSDZ)
RestArea(aAreaSD3)
Return

/*


Ŀ
Funo    |A281FtCorr   Autor  Rodrigo T. Silva     Data  12/01/12 
Ĵ
Descrio  calcula os valores de taxas para as moedas importadas      
Ĵ
Parametros ExpC1 = Indica se o produto eh nacional ou importado       
Parametros ExpC2 = Indica o fator de correcao                         
Parametros ExpN1 = Inidca o valor unitario do documento de entrada    
Parametros ExpN2 = Inidca a moeda do documento de entrada             
Parametros ExpD1 = data de fechamento             					  
Parametros ExpD2 = data final do primeiro semestre/exercicio anterior 
Ĵ
 Uso       MATA281                                                    
ٱ
Retorno    nFtCorr - retorna o valor de percentual ref. taxa de moeda 


*/
Static Function A281FtCorr(cPrdNac,cFator,nVUnitSD1,nMoedaSF1,dDatFec,dDatCalc)
Local nFtCorr   := 0
local nTxMoeda1 := 0
local nTxMoeda2 := 0

If cPrdNac == "N"
	If cFator == "2"
		nFtCorr := 0
	ElseIf cFator == "1"
		nFtCorr := 0.011 //1.1%
	Else
		nFtCorr := 0.025 //2.5%
	EndIf
Else
	If cFator == "2"
		nFtCorr := 0
	Else
		nTxMoeda1 := xMoeda(nVUnitSD1,nMoedaSF1,1,dDatCalc, TamSX3("D1_VUNIT")[2])
		nTxMoeda2 := xMoeda(nVUnitSD1,nMoedaSF1,1,dDatFec,TamSX3("D1_VUNIT")[2])
		nFtCorr := ((nTxMoeda1 - nTxMoeda2) * 100) / nTxMoeda1
	EndIf
EndIf

Return nFtCorr

/*


Ŀ
Funo    |A281VerSD3   Autor  Rodrigo T. Silva     Data  16/01/12 
Ĵ
Descrio  verifica se correcao monetaria ja foi lancada na tabela SD3
Ĵ
Parametros ExpD1 = data de fechamento do calculo                      
			  ExpC1 = codigo do produto                      			  
			  ExpC2 = codigo do armazem                      			  
Ĵ
 Uso       MATA281                                                    
ٱ
Retorno    lRet - retorna se encontrou lancamentos na tabela SD3      
ٱ


*/
Static Function A281VerSD3(dDataFec, cCodProd, cCodArmaz)
Local lRet  := .T.

SDZ->(dbSetOrder(1))
If SDZ->(dbSeek(xFilial("SDZ")+DToS(dDataFec)+cCodProd+cCodArmaz)) .And. !Empty(SDZ->DZ_OK)
	lRet := .F.
EndIf

Return lRet

/*


Ŀ
Funo     A281TudoOk  Autor Rodrigo T. Silva		 Data 19.01.2012
Ĵ
Descrio  Efetua a validao antes da inicializacao do calculo       
Ĵ
Sintaxe    A281TudoOk(ExpC1)                                          
Ĵ
Retorno    False caso ocorra algum problema na Validao, True C.C.   
Ĵ
Parametros ExpA1 = Array contendo as filiais que serao processadas    
Ĵ
 Uso       MATA281                                                    
ٱ

*/
Static Function A281TudoOk(aFilsCalc,dDtFech)
Local aArea    := GetArea()
Local lRet 	   := .T.
Local cTPMovCM := SuperGetMv("MV_TPMOVCM")
Local nX	   := 0
Local cFilBack := cFilAnt

//Ŀ
// Verifica se o processamento da rotina eh superior ao ultimo  |
// fechamento de estoque conforme o parametro MV_ULMES			 |
//
If lRet
	If SuperGetMv("MV_ULMES",.F.) >= mv_par01
		Aviso(STR0014,STR0019,{"Ok"},2) //ATENCAO - "A data de fechamento para execuo da rotina de clculo da correo monetria  menor que a data do ltimo fechamento de estoque conforme o parmetro MV_ULMES. Favor verifique se a data do ltimo fechamento de estoque j foi processada."
		lRet := .F.
	EndIf
	If mv_par02 == 3
		SDZ->(dbSetOrder(1))
		For nX := 1 To Len(aFilsCalc)
			If !SDZ->(dbSeek(xFilial("SDZ")+DtoS(dDtFech)))
				Aviso(STR0014,STR0023 + cFilAnt + STR0021 ,{"Ok"},2) //ATENCAO - "O clculo da correo monetria para o perodo em fechamento informado no foi executado. Favor processar o clculo para o perodo em fechamento informado."
				lRet := .F.
				Exit
			EndIf
		Next nX
	EndIf
EndIf

If mv_par02 <> 1 .And. lRet
	//Ŀ
	// Verifica se os tipos de movimentos que foram informados no parametro estao cadastrados. |
	//
	If Empty(cTPMovCM)
		Aviso(STR0014,STR0026,{"Ok"},2) //"Atencao"#"Informe os cdigos de tp de movimentos (entrada e sada) atravs do parmetro MV_TPMOVCM para gerao dos ajustes de correo monetria."
		lRet := .F.
	EndIf
	If lRet
		SF5->(dbSetOrder(1))
		For nX := 1 To 	Len(aFilsCalc)
			cFilAnt := aFilsCalc[nX,2]
			If !SF5->(DbSeek(cFilAnt+SubStr(cTPMovCM,1,3))) .Or. !SF5->(DbSeek(cFilAnt+SubStr(cTPMovCM,5,3)))
				Aviso(STR0014,STR0015 + cTPMovCM + STR0016 + aFilsCalc[nX, 2] + ".",{"Ok"},2) //"Atencao"#""Os tipos de movimentos "#"" informado no parmetro MV_TPMOVCM no esto cadastrados na rotina de tipos de movimentos."
				lRet := .F.
				Exit
			ElseIf SF5->(DbSeek(cFilAnt+SubStr(cTPMovCM,1,3)))
				If SF5->F5_VAL <> "S" .Or. SF5->F5_QTDZERO <> "1"
					Aviso(STR0014,STR0017 + SubStr(cTPMovCM,1,3) + " " + STR0022 + aFilsCalc[nX, 2] + STR0018,{"Ok"},2) //"Atencao"#"O tipo de movimento"#"deve ser quantidade zero e valorizado."
					lRet := .F.
					Exit
				ElseIf SF5->(DbSeek(cFilAnt+SubStr(cTPMovCM,5,3)))
					If SF5->F5_VAL <> "S" .Or. SF5->F5_QTDZERO <> "1"
						Aviso(STR0014,STR0017 + SubStr(cTPMovCM,5,3) + " " + STR0022 + aFilsCalc[nX, 2] + STR0018,{"Ok"},2) //"Atencao"#"O tipo de movimento"#"deve ser quantidade zero e valorizado."
						lRet := .F.	
						Exit
					EndIf
				EndIf
			EndIf
		Next nX
		// Restaura filial original apos processamento
		cFilAnt := cFilBack
	EndIf
EndIf

RestArea(aArea)
Return lRet

#IFNDEF TOP
	/*
	
	
	Ŀ
	Funo    |A281VerSF4   Autor  Rodrigo T. Silva     Data  19/01/12 
	Ĵ
	Descrio  verifica se a TES do documento de entrada movimenta estoque
	Ĵ
	Parametros ExpC1 = codigo da TES                      				  
	Ĵ
	 Uso       MATA281                                                    
	ٱ
	Retorno    lRet - retorna se o codigo da TES movimenta estoque        
	ٱ
	
	
	*/
	Static Function A281VerSF4(cCodTES)
	Local aArea := GetArea()
	Local lRet  := .T.
	
	dbSelectArea("SF4")
	SDZ->(dbSetOrder(1))
	If SDZ->(dbSeek(xFilial("SF4")+cCodTES))
		lRet := .F.
	EndIf
	
	RestArea(aArea)
	Return lRet
#ENDIF

/*


Ŀ
Funo    MA281Lock    Autor  Rodrigo T. Silva     Data  05/01/12 
Ĵ
Descrio  Bloqueio de Filial para processamento da correcao monetaria
Ĵ
Parametros ExpA1 = Array com a lista de filiais                       
Ĵ
 Uso       MATA281                                                    
ٱ


*/
Static Function MA281Lock(cFilLock)
Local lRet       := .T.
Local nTentativa := 0

While !LockByName("MATA281"+cEmpAnt+cFilLock,.T.,.T.,.T.)
	MsAguarde({|| Sleep(1000) }, STR0024+ALLTRIM(STR(nTentativas)), STR0025)//"Semaforo de processamento... tentativa "##"Aguarde, arquivo sendo alterado por outro usurio."
	nTentativas++
	If nTentativas > 5
		If MsgYesNo(STR0013) //"As seguintes filiais da empresa corrente j esto sendo utilizadas no processo de clculo da correo monetria. Deseja tentar novamente?"
			nTentativas := 0
			Loop
		Else
			Return(.F.)
		EndIf
	EndIf
EndDo
		
Return lRet 

/*


Ŀ
Funo    A281UnLock   Autor  Rodrigo T. Silva     Data  05/01/12 
Ĵ
Descrio  Desbloqueio de Filial                                      
Ĵ
Parametros ExpA1 = Array com a lista de filiais                       
Ĵ
 Uso       MATA281                                                    
ٱ


*/
Static Function MA281UnLock()
	UnLockByName("MATA281"+cEmpAnt+cFilAnt,.T.,.T.,.T.)
Return