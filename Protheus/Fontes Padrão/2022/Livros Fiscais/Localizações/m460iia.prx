#INCLUDE "SIGAWIN.CH"
#DEFINE X_IMPOSTO    01 //Nome do imposto
#DEFINE X_NUMIMP     02 //Sufixo do imposto
/*/
_____________________________________________________________________________
¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦
¦¦+-----------------------------------------------------------------------+¦¦
¦¦¦Funcion   ¦M460IIA    ¦ Autor ¦ Marcos Kato           ¦Fecha ¦ 16.08.10¦¦¦
¦¦+----------+------------------------------------------------------------¦¦¦
¦¦¦Descrip.  ¦ Programa que Calcula IIA - Imposto Importacao Antecipado   ¦¦¦
¦¦+----------+------------------------------------------------------------¦¦¦
¦¦¦Uso       ¦ MATA460                                                    ¦¦¦
¦¦+-----------------------------------------------------------------------¦¦¦
¦¦¦         ACTUALIZACIONES EFECTUADAS DESDE LA CODIFICACION INICIAL      ¦¦¦
¦¦+-----------------------------------------------------------------------¦¦¦
¦¦¦Programador ¦ Fecha  ¦ BOPS ¦  Motivo de la Alteracion                 ¦¦¦
¦¦+------------+--------+------+------------------------------------------¦¦¦
¦¦¦            ¦  /  /  ¦      ¦                                          ¦¦¦
¦¦+-----------------------------------------------------------------------+¦¦
¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦
*/
Function M460IIA(cCalculo,nItem,aInfo)
Local cAliasRot  := Alias()
Local cOrdemRot  := IndexOrd()
Local nDesconto,nBase,nAliq,nOrdSFC,nRegSFC,nVal,nVRet
nBase:=0
nAliq:=0
nDesconto:=0
nVRet:=0

DbSelectArea("SFB")
SFB->(DbSetOrder(1))
If dbSeek(xFilial("SFB")+aInfo[X_IMPOSTO])
	If cCalculo$"AB"
		//Tira os descontos se for pelo liquido
		nOrdSFC:=(SFC->(IndexOrd()))
		nRegSFC:=(SFC->(Recno()))
		SFC->(DbSetOrder(2))
		If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[X_IMPOSTO])))
			If SFC->FC_LIQUIDO=="S"
				nDesconto:=If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
			Endif
		Endif
		SFC->(DbSetOrder(nOrdSFC))
		SFC->(DbGoto(nRegSFC))
		nVal:=MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
		If GetNewPar('MV_DESCSAI','1')=='2'
			nVal-=nDesconto
		Endif
		
		nAliq:=0		
		DbSelectArea("SB1")
		SB1->(DbSetOrder(1))
		SB1->(DbGoTop())		
		If DbSeek(xFilial("SB1")+AvKey(MaFisRet(nItem,"IT_PRODUTO"),"B1_COD"))
			If SB1->(FieldPos("B1_CALCIIA"))>0 .And. SB1->B1_CALCIIA=="1"
				nAliq:=SFB->FB_ALIQ
			Endif
		Endif		
	Endif
Endif

Do Case
	Case cCalculo=="B"
		nVRet:=nVal
	Case cCalculo=="A"
		nVRet:=nAliq
	Case cCalculo=="V"
		nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
		nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
		nVRet:=nBase * (nAliq/100)
EndCase
DbSelectArea( cAliasRot )
DbSetOrder( cOrdemRot )
Return(nVRet)
