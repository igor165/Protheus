#INCLUDE "PROTHEUS.CH"
#INCLUDE "MATA335.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ MATA335  ³ Autor ³ Microsiga S/A         ³ Data ³ 06/09/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Ajuste do Custo de Reposicao (Argentina)         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA335                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³Data    ³ BOPS     ³ Motivo da Alteracao                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Jonathan Glz³08/07/15³PCREQ-4256³Se elimina la funcion AjustaSX1() que ³±±
±±³            ³        ³          ³hace modificacion a SX1 por motivo de ³±±
±±³            ³        ³          ³adecuacion a fuentes a nuevas estruc- ³±±
±±³            ³        ³          ³turas SX para Version 12.             ³±±
±±³M.Camargo   ³09.11.15³PCREQ-4262³Merge sistemico v12.1.8		           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATA335(lAutomato)
Local nX         := 0
Local nI         := 0
Local cTipoOper  := ''
Local oComboBox
Local oDlg

Private a335ParamZX := Array(4)
Private cCadastro   := STR0001 //"Ajuste do Custo de Reposição"
Private dDataAjust  := dDataBase
Default lAutomato 	:= .F.	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01 - Do Produto                                        ³
//³ mv_par02 - Ate o Produto                                     ³
//³ mv_par03 - Do Armazem                                        ³
//³ mv_par04 - Ate o Armazem                                     ³
//³ mv_par05 - Data do Ajuste                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MTA335Perg(.F.)

If !lAutomato
DEFINE MSDIALOG oDlg FROM  096,9 TO 350,600 TITLE STR0001 PIXEL //"Ajuste do Custo de Reposição"
@ 011,006  TO 120,287 LABEL "" OF oDlg  PIXEL
@ 016, 015 SAY STR0002 SIZE 268, 8 OF oDlg PIXEL //"Esta rotina ira realizar o ajuste do custo de reposição para fechamento de estoque, baseada"
@ 026, 015 SAY STR0003 SIZE 268, 8 OF oDlg PIXEL //"no custo de reposição informado no campo 'Custo Stand. (B1_CUSTD)' e na moeda informada no"
@ 036, 015 SAY STR0014 SIZE 268, 8 OF oDlg PIXEL //"campo 'Moeda C.Std (B1_MCUSTD)' atraves do cadastro de produtos."

@ 056, 015 SAY STR0009 SIZE 050,10 OF oDlg PIXEL //"Tipo de Operação :"
@ 056, 070 MSCOMBOBOX oComboBox VAR cTipoOper ITEMS {STR0010,STR0011} SIZE 050,10 OF oDlg PIXEL //"Inclusao"##"Exclusao"

@ 056,130 SAY STR0012 SIZE 268, 8 OF oDlg PIXEL //"Data do Ajuste: "
@ 056,180 MSGET DTOC(dDataAjust) SIZE 040,10 OF oDlg PIXEL WHEN .F.

@ 086, 015 SAY STR0025 SIZE 268, 8 OF oDlg PIXEL //"IMPORTANTE: Para efetivar os ajustes do custo de reposição deve-se executar a rotina "
@ 096, 015 SAY STR0026 SIZE 268, 8 OF oDlg PIXEL //"Recalculo do Custo Médio."


DEFINE SBUTTON FROM 103, 163 TYPE 6  ACTION MTA335Imp(.T.) ENABLE OF oDlg
DEFINE SBUTTON FROM 103, 193 TYPE 5  ACTION MTA335Perg(.T.) ENABLE OF oDlg
DEFINE SBUTTON FROM 103, 223 TYPE 1  ACTION If(MTA335TOk(cTipoOper),(Processa({|lEnd| MA335Process(@lEnd,cTipoOper)},STR0001,STR0013,.F.),oDlg:End()),) ENABLE OF oDlg //"Ajuste do Custo de Reposição"##"Processando ..."
DEFINE SBUTTON FROM 103, 253 TYPE 2  ACTION oDlg:End() ENABLE OF oDlg

	ACTIVATE MSDIALOG oDlg CENTERED
Else
   If FindFunction("GetParAuto")
	    aRetAuto := GetParAuto("MATA335TESTCASE")
	    cTipoOper 		:= aRetAuto[1]
		lEnd := .F.
		MTA335TOk(cTipoOper)
		MA335Process(lEnd,cTipoOper)
				
   EndIf 
Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³MA335Process³ Autor ³ Microsiga S/A         ³ Data ³06/09/09³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processa o ajuste do custo de reposicao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd      = Variavel que controla interrupcao do processo  ³±±
±±³          ³ cTipoOper = Tipo de Operacao = Inclusao / Exclusao         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA335                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MA335Process(lEnd,cTipoOper)
Local aAreaAnt   := GetArea()
Local aAreaSB2   := SB2->(GetArea())
Local aAreaSD3   := SD3->(GetArea())
Local cMoeda335C := GetMv('MV_MOEDACM',.F.,"2345")
Local lA335Custo := ExistBlock("A335CUSTO")
Local cDocumento := ''
Local cSeek      := ''
Local cMoeda     := ''
Local cCustoRep  := 0
Local uCustoRep

Private l240Auto := .T.
Private cCusMed  := GetMv("MV_CUSMED")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Estas variaveis indicam para as funcoes de validacao qual    ³
//³ programa as esta chamando                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private l240:=.T.,l250:=.F.,l241:=.F.,l242:=.F.,l261:=.F.,l185 :=.F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MTA335Perg(.F.)

dbSelectArea("SB2")
dbSetOrder(1)
dbGoTop()
Do While !Eof() 

	IncProc(STR0015) //"Processando Ajuste ..."
	
	If lEnd
		@PROW()+1,001 PSay STR0016 //"CANCELADO PELO OPERADOR"
		Exit
	EndIf

	If SB2->B2_COD < a335ParamZX[1] .Or. SB2->B2_COD > a335ParamZX[2]
		dbSkip()
		Loop
	EndIf	
	If SB2->B2_LOCAL < a335ParamZX[3] .Or. SB2->B2_LOCAL > a335ParamZX[4]
		dbSkip()
		Loop
	EndIf	

	//-- Posiciona na tabela SB1
	dbSelectArea("SB1")
	dbSetOrder(1)
	dbSeek(xFilial("SB1")+SB2->B2_COD)

	//-- Processa Inclusao do ajuste do custo de reposicao
	If Upper(cTipoOper) == Upper(STR0010) //"INCLUSAO"
		//-- Reinicia variaveis
		cCustoRep := 0
		cMoeda    := IIf(Empty(SB1->B1_MCUSTD),'1',SB1->B1_MCUSTD)
		Do Case
			Case cMoeda == '1'
				cCustoRep := SB1->B1_CUSTD
			Case cMoeda == '2' .And. "2"$ cMoeda335C
				cCustoRep := SB1->B1_CUSTD
			Case cMoeda == '3' .And. "3"$ cMoeda335C
				cCustoRep := SB1->B1_CUSTD
			Case cMoeda == '4' .And. "4"$ cMoeda335C
				cCustoRep := SB1->B1_CUSTD
			Case cMoeda == '5' .And. "5"$ cMoeda335C
				cCustoRep := SB1->B1_CUSTD
		EndCase	
		// Ponto de Entrada para manipular o custo de reposicao
		If lA335Custo
			uCustoRep := ExecBlock("A335CUSTO",.F.,{SB2->B2_COD,SB2->B2_LOCAL,cMoeda,cCustoRep})
			If ValType(uCustoRep) == "A"
				cCustoRep := uCustoRep
			EndIf
		EndIf
		//-- Processando o ajuste do custo de reposicao
		If !( ABS( cCustoRep ) == 0 .Or. cCustoRep < 0.009 )
			cDocumento := NextNumero("SD3",2,"D3_DOC",.T.)
			RecLock("SD3",.T.)
			SD3->D3_FILIAL	:= xFilial("SD3")
			SD3->D3_COD		:= SB2->B2_COD
			SD3->D3_TM		:= "499"
			SD3->D3_LOCAL	:= SB2->B2_LOCAL
			SD3->D3_DOC		:= cDocumento
			SD3->D3_EMISSAO	:= mv_par05
			SD3->D3_NUMSEQ	:= ProxNum()
			SD3->D3_UM		:= SB1->B1_UM
			SD3->D3_GRUPO	:= SB1->B1_GRUPO
			SD3->D3_TIPO	:= SB1->B1_TIPO
			SD3->D3_SEGUM	:= SB1->B1_SEGUM
			SD3->D3_CONTA	:= SB1->B1_CONTA
			SD3->D3_CF		:= "DE6"
			SD3->D3_STATUS	:= "RP" //AJUSTE CUSTO DE REPOSICAO
			SD3->D3_USUARIO	:= SubStr(cUsuario,7,15)
			SD3->D3_CMRP	:= cCustoRep
			SD3->D3_MOEDRP  := cMoeda
			MsUnLock()
        EndIf
	ElseIf Upper(cTipoOper) == Upper(STR0011) //"EXCLUSAO"
		dbSelectArea("SD3")
		dbSetOrder(7)
		dbSeek(cSeek:=xFilial("SD3")+SB2->B2_COD+SB2->B2_LOCAL+DTOS(mv_par05))
		Do While !Eof() .And. cSeek == SD3->D3_FILIAL+SD3->D3_COD+SD3->D3_LOCAL+DTOS(SD3->D3_EMISSAO)
			If SD3->D3_ESTORNO == 'S' .Or.;
			   !(SD3->D3_CF $ 'RE6|DE6') .Or.;
			   SD3->D3_STATUS # 'RP'
				dbSkip()
				Loop
			EndIf
			Begin Transaction
				dbSelectArea("SD3")
				a240Estorn("SD3",SD3->(Recno()),4) //-- Estornar Movimentos
			End Transaction
			dbSkip()
		EndDo	
	EndIf
	dbSelectArea("SB2")
	dbSkip()
EndDo

RestArea(aAreaSB2)
RestArea(aAreaSD3)
RestArea(aAreaAnt)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTA335TOk ³ Autor ³Microsiga S/A          ³ Data ³ 06/09/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida se pode efetuar o ajuste cambial de estoque          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cTipoOper = Tipo de Operacao = Inclusao / Exclusao         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA335                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTA335TOk(cTipoOper)
Local aAreaAnt   := GetArea()
Local aAreaSD3   := SD3->(GetArea())
Local dUlMes     := GetMv("MV_ULMES",.F.,.F.)
Local lRet       := .T.
Local cSeek      := ''

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MTA335Perg(.F.)

If mv_par05 <= dUlMes
	Help(" ",1,"A335DTFEC")
	lRet := .F.
EndIf

If lRet .And. cPaisLoc # "ARG"
	Help(" ",1,"A335NOARG")
	lRet := .F.
EndIf

//-- Verifica se pode incluir um novo ajuste cambial
If lRet .And. Upper(cTipoOper) == "INCLUSAO"
	dbSelectArea("SD3")
	dbSetOrder(6)
	dbSeek(cSeek:=xFilial("SD3")+DTOS(mv_par05))
	Do While !Eof() .And. cSeek == SD3->D3_FILIAL+DTOS(SD3->D3_EMISSAO)
		If SD3->D3_ESTORNO == 'S' .Or. !(SD3->D3_CF $ 'RE6|DE6')
			dbSkip()
			Loop
		EndIf
		If SD3->D3_COD < a335ParamZX[1] .Or. SD3->D3_COD > a335ParamZX[2]
			dbSkip()
			Loop
		EndIf	
		If SD3->D3_LOCAL < a335ParamZX[3] .Or. SD3->D3_LOCAL > a335ParamZX[4]
			dbSkip()
			Loop
		EndIf	
		If SD3->D3_STATUS == 'RP'
			Help(" ",1,"A335EXIST")
			lRet := .F.
			Exit
		EndIf
		dbSkip()
	EndDo	
EndIf

RestArea(aAreaSD3)
RestArea(aAreaAnt)
Return lRet 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³MTA335Imp ³ Autor ³Microsiga S/A          ³ Data ³ 06/09/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao do acerto de cambial de Estoque                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA335                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTA335Imp()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis obrigatorias dos programas de relatorio            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local titulo   := STR0017 //"Relatorio do Ajuste do Custo de Reposição"
Local cDesc1   := STR0018 //"O relatorio ira listar o custo unitario de reposição dos produtos"
Local cDesc2   := STR0019 //"a serem ajustados pela rotina."
Local cString  := "SB2"
Local wnrel    := "MATA335"
Local cPerg    := "MTR335"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private padrao de todos os relatorios         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aReturn := {STR0020,1,STR0021, 2, 2, 1, "", 1 } //"Zebrado"###"Administracao"
PRIVATE nLastKey:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,,.F.,/*aOrd*/,/*lCompres*/,/*cSize*/,/*aFilter*/,.F.)

If nLastKey = 27
	Set Filter to
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
	Set Filter to
	Return
Endif

RptStatus({|lEnd| A335Rel(@lEnd,wnRel,@titulo)},@titulo)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A335Rel   ³ Autor ³Microsiga S/A          ³ Data ³ 06/09/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao do relatorio de ajuste cambial de estoque         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA335                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A335Rel(lEnd,wnrel,titulo)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis locais exclusivas deste programa                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local Tamanho    := "M"
Local cSeekB2    := ""
Local cSeekD3    := ""
Local nTipo      := 0
Local nCntImpr   := 0
Local cPictCusRP := PesqPict("SD3","D3_CMRP")
Local cRodaTxt := STR0024 //"REGISTRO(S)"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os codigos de caracter Comprimido/Normal da impressora ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTipo := IIF(aReturn[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private li    := 80
Private m_pag := 01

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTR335",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria o cabecalho.                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//                   0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
//                            1         2         3         4         5         6         7         8         9        10         11        12          
cabec1 := STR0022 //"DATA      FL  PRODUTO         AMZ       MOEDA     CUSTO    "
cabec2 := STR0023 //"EMISSAO                                           UNITARIO "

dbSelectArea("SB2")
dbSetOrder(1)
dbGoTop()
dbSeek(cSeekB2 := xFilial("SB2"))
Do While !Eof() .And. cSeekB2 == B2_FILIAL
	If lEnd
		@ Prow()+1,001 PSAY STR0019 //"CANCELADO PELO OPERADOR"
		Exit
	EndIf
	If SB2->B2_COD < mv_par01 .Or. SB2->B2_COD > mv_par02
		dbSkip()
		Loop
	EndIf	
	If SB2->B2_LOCAL < mv_par03 .Or. SB2->B2_LOCAL > mv_par04
		dbSkip()
		Loop
	EndIf	
	If li > 58
		cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
	EndIf
	dbSelectArea("SD3")
	dbSetOrder(7)
	dbSeek(cSeekD3:=xFilial("SD3")+SB2->B2_COD+SB2->B2_LOCAL+DTOS(mv_par05))
	Do While !Eof() .And. cSeekD3 == SD3->D3_FILIAL+SD3->D3_COD+SD3->D3_LOCAL+DTOS(SD3->D3_EMISSAO)
		If SD3->D3_ESTORNO == 'S' .Or. !(SD3->D3_CF $ 'RE6|DE6') .Or. SD3->D3_STATUS <> 'RP'
			dbSkip()
			Loop
		EndIf
		@ Li, 000 PSay SD3->D3_EMISSAO
		@ Li, 010 PSay SB2->B2_FILIAL
		@ Li, 014 PSay SB2->B2_COD
		@ Li, 030 PSay SB2->B2_LOCAL
		@ Li, 040 PSay SD3->D3_MOEDRP
		@ Li, 050 PSay SD3->D3_CMRP Picture cPictCusRP
		Li++
    	dbSkip()
	EndDo
	dbSelectArea("SB2")
	dbSkip()
EndDo

If li != 80
	Roda(nCntImpr,cRodaTxt,Tamanho)
EndIf

Set Device to Screen

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
EndIf

MS_FLUSH()

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTA335PERG³ Autor ³ Microsiga S/A         ³ Data ³ 08/09/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chama a pergunte para o programa e inicializa variaveis    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MTA335Perg()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lTela = Variavel que verifica se visualiza na tela         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA335                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTA335Perg(lTela)
Local ni      := 0

Default lTela := .T.

If lTela
	If pergunte("MTA335",.T.)
		For ni := 1 to 4
			a335ParamZX[ni] := &("mv_par"+StrZero(ni,2))
		Next ni
		dDataAjust := mv_par05
	Else
		Return .F.
	EndIf      
Else
	Pergunte("MTA335",.F.)
	For ni := 1 to 04
		a335ParamZX[ni] := &("mv_par"+StrZero(ni,2))
	Next ni
	dDataAjust := mv_par05
EndIf

Return .T.
