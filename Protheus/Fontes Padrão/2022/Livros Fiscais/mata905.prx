#INCLUDE "MATA905.CH"
#INCLUDE "PROTHEUS.CH"
/*/           
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA905  ³ Autor ³ Eduardo Riera         ³ Data ³ 02.06.1998 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de atualizacao do Cadastro do CIAP                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void MATA905(void)                               admi        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Marcos Simidu³19/11/98³XXXXXX³ Validacao do ICMS com vari. de memoria.  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATA905
Local aCores	:=	{}
Local lVerpesssen := Iif(FindFunction("Verpesssen"),Verpesssen(),.T.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aAC := { STR0008,STR0009 },; //"Abandona"###"Confirma"
aCRA:= { STR0009,STR0010,STR0008 } //"Confirma"###"Redigita"###"Abandona"

PRIVATE aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemtoAnsi(STR0007)		//"Manutencao do CIAP"

If lVerpesssen
	dbSelectArea("SF9")
	dbSelectArea("SFA")

	If SF9->(FieldPos("F9_BAIXAPR"))==0 .Or. SFA->(FieldPos("FA_BAIXAPR"))==0
		xMagHelpFis(STR0018,STR0019,STR0020)
		Return
	EndIf

	aAdd (aCores, {'F9_QTDPARC=F9_SLDPARC.AND.F9_VLESTOR==0.AND.F9_BXICMS==0.AND.F9_VALICMS>0', 'BR_LARANJA' }) 		// Ativo adquirido mais sem ocorrencias
	aAdd (aCores, {'F9_BXICMS<>0.AND.F9_MOTIVO<>"3".AND.(Empty(F9_BAIXAPR).OR.Alltrim(F9_BAIXAPR)=="0") ','BR_VERMELHO'})		// Ativo baixado
	aAdd (aCores, {'F9_BXICMS<>0.AND.F9_MOTIVO=="3"','BR_AZUL'})							// Ativo transferido
	aAdd (aCores, {'EMPTY(F9_MOTIVO).AND.F9_SLDPARC==0.AND.F9_QTDPARC<>0','BR_PRETO'})		// Ativo baixado por período
	aAdd (aCores, {'F9_VALICMS==0.AND.F9_TIPO=="01".AND.F9_CODBAIX=="BFINAL"','BR_BRANCO'})	// Ativo Bem Principal 
	aAdd (aCores, {'F9_BXICMS<>0.AND.F9_MOTIVO<>"3".AND.F9_BAIXAPR=="1"','BR_AMARELO'})		// Ativo baixado parcialmente
	aAdd (aCores, {'F9_TIPO=="02"','BR_MARROM'})		// Ativo Bem em Construção
	aAdd (aCores, {'F9_QTDPARC<>F9_SLDPARC.OR.(F9_BXICMS==0.OR.F9_BXICMS==0)', 'ENABLE'})		// Ativo depreciado

	If SF9->(FieldPos("F9_STATUS"))>0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Carrega as perguntas selecionadas                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Pergunte("MTA905",.F.)
		SetKey( VK_F12,{|| Pergunte("MTA905",.T.)} )
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Endereca a funcao de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	mBrowse( 6, 1,22,75,"SF9",,"F9_MOTIVO",,,,aCores)
	If SF9->(FieldPos("F9_STATUS"))>0
		SetKey( VK_F12,Nil )
	EndIf
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A905TudOk ³ Autor ³ Eduardo Riera         ³ Data ³ 02.06.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Validacao do CIAP                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A905TudOk(Void)                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function a905TudOk()

Local cChaveSD1 := ""
Local lRetorno  := .T.
Local l905TOk   :=Existblock("A905TOK")
Local aAreaQSA1 := {}
Local nEndereco := Ascan(aGets,{ |x| Subs(x,9,9) == "F9_DOCNFE" } )
Local cSTCIAP	:= ""

DbSelectArea("SA1")
DbSetOrder(1)
aAreaQSA1:=  SA1->(GetArea())
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificando Nota de Entrada                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( !Empty(M->F9_ITEMNFE) )
	lRetorno := .F.
	cChaveSd1 := xFilial("SD1") + M->F9_DOCNFE + M->F9_SERNFE + M->F9_FORNECE + M->F9_LOJAFOR
	dbSelectArea("SD1")
	dbSetOrder(1)
	dbSeek(cChaveSd1,.F.)
	
	While (!SD1->( Eof() ) .And. cChaveSD1 == SD1->(D1_FILIAL + D1_DOC + D1_SERIE + D1_FORNECE + D1_LOJA) )
		If ( M->F9_ITEMNFE == SD1->D1_ITEM )
			lRetorno := .T.
			Exit
		EndIf
		dbSelectArea("SD1")
		dbSkip()
	EndDo

	cSTCIAP	:= AllTrim( SuperGetMV( "MV_STCIAP" ) )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verificando Valor do Credito                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lRetorno .And.;
		( ( ( SD1->D1_VALICM + SD1->D1_ICMSCOM + IIF( cSTCIAP == "S", SD1->D1_ICMSRET, 0 ) ) != M->F9_VALICMS ) .And.;
		( ( ( SD1->D1_VALICM + SD1->D1_ICMSCOM + IIF( cSTCIAP == "S", SD1->D1_ICMSRET, 0 ) ) / SD1->D1_QUANT ) != M->F9_VALICMS ) ) )

		lRetorno := .F.
	EndIf
EndIf

If ( !lRetorno )
	Help(" ",1,"A905TUDOK")
EndIf

DbSelectArea("SA1")
DbSetOrder(1)
If lRetorno .AND. !Empty(M->F9_CLIENTE) .AND.  !dbSeek(xFilial("SA1")+M->F9_CLIENTE+M->F9_LOJACLI)
	Help("",1,"Help","Help","Cliente e Loja não Encontrados!",1,0) //"Cliente e Loja não Encontrados!"
	lRetorno := .F.
EndIF

If lRetorno
	If l905TOk
		lRetorno := Execblock("A905TOK",.F.,.F.)
		If ValType(lRetorno) # "L"
			lRetorno :=.T.
		EndIf
	EndIf
EndIf

If lRetorno .And. M->F9_QTDPARC > M->F9_SLDPARC

M->F9_VLESTOR := Round(((M->F9_VALICMS * M->F9_SLDPARC)/M->F9_QTDPARC),2)

EndIf 
// QUANDO FOR INCLUIR UM BEM EM CONSTRUÇÃO SERÁ PERMITIDO INCLUIR SEM PREENCHER F9_DOCNFE, F9_PROPRIO E F9_VALICMS
If M->F9_TIPO == "02" .And. empty(M->F9_DOCNFE) 
	if Type("aGets") != "U"
		If nEndereco  > 0 
			aGets[nEndereco] := Left(aGets[nEndereco],24)+"FC "
		EndiF	
		If Len(aGets) >= 19 	
			aGets[8] := Left(aGets[8],24)+"FC "
			aGets[19] := Left(aGets[19],24)+"FN "
		EndIf
	Endif
Endif

RestArea(aAreaQSA1)                                  
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A905Inclui³ Autor ³ Eduardo Riera         ³ Data ³ 02.06.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Inclusao do Cadastro do CIAP                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A905Inclui(cAlias,nReg,nOpc)                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A905Inclui(cAlias,nReg,nOpc)

Local nCntFor   := 0
Local lContinua := .T.
Local nOpcA     := 0
Local oDlg
Local cChaveSD1 := "" 
Local aSize := MsAdvSize(.T.)
Local aInfo := {}
Local aPosObj := {}
Local aObjects := {} 
Local nGd1      := 2
Local nGd2 		:= 2
Local nGd3 		:= 0
Local nGd4 		:= 0

Private aTela[0][0],aGets[0][0]
Private bCampo := { |nField| FieldName(nField) }

aObjects := {} 
AAdd( aObjects, {100, 100, .t., .t. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
aPosObj := MsObjSize( aInfo, aObjects )

nGd1 := 2
nGd2 := 2
nGd3 := aPosObj[1,3]-aPosObj[1,1]
nGd4 := aPosObj[1,4]-aPosObj[1,2]
                  
dbSelectArea(cAlias)
For nCntFor:= 1 To FCount()
	M->&(EVAL(bCampo,nCntFor)) := CriaVar(FieldName(nCntFor),.T.)
Next nCntFor

aAdd( aObjects, { 315, 20, .T., .T. } )
aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects)

DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],000 TO aSize[6],aSize[5] OF oMainWnd PIXEL   //9,0 TO 28,80 OF oMainWnd
EnChoice( cAlias, nReg, nOpc, , , , ,aPosObj[1], , 3,,,"A905TudOk()")
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca:=1,If(A905TudOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=0)},{||nOpca:=0,oDlg:End()})
If ( nOpcA == 1 )
	Begin Transaction
	RecLock(cAlias,.T.)
	For nCntFor := 1 To FCount()
		FieldPut(nCntFor,M->&(FieldName(nCntFor)))
	Next nCntFor
	SF9->F9_FILIAL := xFilial("SF9")
	SF9->F9_ROTINA := "MATA905"
	If SF9->(FieldPos("F9_STATUS"))>0
		SF9->F9_STATUS	:= "1"
	EndIf
	If ( !Empty(M->F9_ITEMNFE) )
		cChaveSd1 := xFilial("SD1")+M->F9_DOCNFE+M->F9_SERNFE+M->F9_FORNECE+M->F9_LOJAFOR
		dbSelectArea("SD1")
		dbSetOrder(1)
		dbSeek(cChaveSd1,.F.)
		While (!Eof() .And. SD1->D1_FILIAL + SD1->D1_DOC + SD1->D1_SERIE + ;
							SD1->D1_FORNECE + SD1->D1_LOJA == cChaveSD1 )
			If ( M->F9_ITEMNFE == SD1->D1_ITEM )
				RecLock("SD1", .F. )
				SD1->D1_CODCIAP := M->F9_CODIGO
				SD1->( MsUnlock() ) 
			EndIf
			dbSelectArea("SD1")
			dbSkip()
		EndDo
	EndIf
	dbSelectArea("SF9")
	EvalTrigger()
	If ( __lSX8 )
		ConfirmSX8()
	EndIf
	End Transaction
	If SF9->(FieldPos("F9_STATUS"))>0
		A905IncCtb("770")
	EndIf
Else
	If ( __lSX8 )
		RollBackSX8()
	EndIf
EndIf
Return(nOpca)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A905Deleta³ Autor ³ Eduardo Riera         ³ Data ³ 02.06.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Exclusao do Cadastro do CIAP                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A905Deleta(cAlias,nReg,nOpc)                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A905Deleta(cAlias,nReg,nOpc)

Local nCntFor   := 0
Local lContinua := .T.
Local nOpcA     := 0
Local oDlg
Local cChaveSD1 := ""   
Local aInfo     := {}
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := MsAdvSize() 
Local nGd1      := 2
Local nGd2 		:= 2
Local nGd3 		:= 0
Local nGd4 		:= 0

Private aTela[0][0],aGets[0][0]
Private bCampo := { |nField| FieldName(nField) }

aObjects := {} 
AAdd( aObjects, {100, 100, .t., .t. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
aPosObj := MsObjSize( aInfo, aObjects )

nGd1 := 2
nGd2 := 2
nGd3 := aPosObj[1,3]-aPosObj[1,1]
nGd4 := aPosObj[1,4]-aPosObj[1,2]

dbSelectArea(cAlias)
For nCntFor:= 1 To FCount()
	M->&(EVAL(bCampo,nCntFor)) := FieldGet(nCntFor)
Next nCntFor
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificando se pode ser excluida                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( SF9->F9_MOTIVO != " " )
	lContinua := .F.
	Help(" ",1,"A905DELE_1")
EndIf
If ( lContinua .And. AllTrim(SF9->F9_ROTINA) != "MATA905" )
	lContinua := .F.
	Help(" ",1,"A905DELE_2")
EndIf
If ( lContinua .And. SF9->F9_ICMIMOB != 0 )
	lContinua := .F.
	Help(" ",1,"A905DELE_3")
EndIf
If ( lContinua .And. SF9->F9_VLESTOR != 0 )
	If !A905DelApr(.T.)	//Verifica se houve mais de uma apropriacao
		lContinua := .F.
		Help(" ",1,"A905DELE_4")
	Endif
EndIf 
 
aAdd( aObjects, { 315, 20, .T., .T. } )
aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects)

If ( lContinua )
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],000 TO aSize[6],aSize[5] OF oMainWnd PIXEL
	EnChoice( cAlias, nReg, nOpc )
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca:=2,oDlg:End()},{||nOpca:=0,oDlg:End()})
	If ( nOpcA == 2 )
		Begin Transaction
		If ( !Empty(SF9->F9_ITEMNFE) )
			cChaveSd1 := xFilial("SD1")+SF9->F9_DOCNFE+SF9->F9_SERNFE+SF9->F9_FORNECE+SF9->F9_LOJAFOR
			dbSelectArea("SD1")
			dbSetOrder(1)
			dbSeek(cChaveSd1,.F.)
			While (!SD1->( Eof() ) .And. cChaveSD1 == SD1->D1_FILIAL + SD1->D1_DOC + SD1->D1_SERIE + ;
					SD1->D1_FORNECE + SD1->D1_LOJA )
				If ( SF9->F9_ITEMNFE == SD1->D1_ITEM )
					RecLock("SD1", .F. )
					SD1->D1_CODCIAP := ""
					SD1->( MsUnLock()) 
				EndIf
				dbSelectArea("SD1")
				dbSkip()
			EndDo
		EndIf
		dbSelectArea("SF9")
		If SF9->(FieldPos("F9_STATUS"))>0 .And. SF9->F9_STATUS == "2"
			A905IncCtb("771")
		EndIf
		A905DelApr(.F.)		//Exclui a apropriacao
		RecLock(cAlias,.F.)
		dbDelete()
		MsUnlock()
		End Transaction
	EndIf
EndIf
Return(nOpca)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³GetNumSF9 ³ Autor ³ Nereu Humberto Junior ³ Data ³ 26.11.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa de Geracao do codigo do bem de acordo c/ MV_FSNCIAP³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void GetNumSF9(Void)                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function GetNumSF9()

Local cNum := ""
Local cMay := ""
Local cRetorno  := ""
Local cAno := Right(Str(Year(dDataBase)),2)
#IFDEF TOP
	Local cQuery    := ""
	Local cAliasSF9 := ""
	Local cLista    := ""
	Local dDtIni := CTOD("01/01/"+cAno)
	Local dDtFin := CTOD("31/12/"+cAno)
#ELSE
	Local cChave := AllTrim(Str(Year(dDataBase)+1))
#ENDIF

If GetNewPar("MV_FSNCIAP","1") == "1"
   cRetorno := GetSXENum("SF9","F9_CODIGO")
ElseIf GetNewPar("MV_FSNCIAP","1") == "3"
	#IFDEF TOP
		cLista    := A905FilGC()
      	cAliasSF9 := "AliasSF9"
		cQuery    := "SELECT MAX(F9_CODIGO) AS CODIGO "
		cQuery    += "FROM "+RetSqlName("SF9")+" SF9 "
		cQuery    += "WHERE SF9.F9_FILIAL IN ("+cLista+")"+" AND "
		cQuery    += "F9_DTENTNE BETWEEN '"+DTOS(dDtIni)+"' AND '"+DTOS(dDtFin)+"' AND "
		cQuery    += "SF9.D_E_L_E_T_=' ' "

		cQuery    := ChangeQuery(cQuery)
	
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF9,.T.,.T.)
		
		cNum  := IIF(Empty( (cAliasSF9)->CODIGO ),"0001",Soma1(Left( (cAliasSF9)->CODIGO,4)))+cAno
		(cAliasSF9)->(dbCloseArea())
		dbSelectArea("SF9")

		cMay := "SF9"+xFilial("SF9")+cNum
		While !MayIUseCode(cMay)
			cNum := Soma1(Left(cNum,4),4)+cAno
			cMay := "SF9"+xFilial("SF9")+cNum
		EndDo
		cRetorno := cNum
    #ENDIF
Else
	#IFDEF TOP
      cAliasSF9 := "AliasSF9"
		cQuery    := "SELECT MAX(F9_CODIGO) AS CODIGO "
		cQuery    += "FROM "+RetSqlName("SF9")+" SF9 "
		cQuery    += "WHERE SF9.F9_FILIAL='"+xFilial("SF9")+"' AND "
		cQuery    += "F9_DTENTNE BETWEEN '"+DTOS(dDtIni)+"' AND '"+DTOS(dDtFin)+"' AND "
		cQuery    += "SF9.D_E_L_E_T_=' ' "

		cQuery    := ChangeQuery(cQuery)
	
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF9,.T.,.T.)
		
		cNum  := IIF(Empty( (cAliasSF9)->CODIGO ),"0001",Soma1(Left( (cAliasSF9)->CODIGO,4)))+cAno
		(cAliasSF9)->(dbCloseArea())
		dbSelectArea("SF9")
	#ELSE
		dbSelectArea("SF9")
		dbSetOrder(2)
		dbSeek(xFilial("SF9")+cChave,.T.)

   	dbSkip(-1)

	   If Right(Str(Year(F9_DTENTNE)),2) == cAno
		   cNum := Soma1(Left(F9_CODIGO,4))+cAno
	   Else
			cNum := "0001"+cAno
	   Endif
	#ENDIF
	cMay := "SF9"+xFilial("SF9")+cNum
	While !MayIUseCode(cMay)
		cNum := Soma1(Left(cNum,4),4)+cAno
		cMay := "SF9"+xFilial("SF9")+cNum
	EndDo
	cRetorno := cNum
Endif   

Return(cRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A905DelApr³ Autor ³ Sergio S. Fuzinaka    ³ Data ³ 17.12.03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Exclui a apropriacao (SFA)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Void A905DelApr()                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³lOpc [ T=Verifica apropriacoes;F=Exclui ]                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Mata905()                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A905DelApr(lOpc)

Local aArea 	:= GetArea()
Local nAprop	:= 0
Local lRet		:= .T.
Local lContabil := .F.
Local lAglutina	:= .F.
Local lDigita	:= .F.
Local cLoteFis	:= ""
Local nHdlPrv	:= 0 
Local cArqFis	:= ""
Local nTotalFis	:= 0	
Local c755		:= ""
Local aCT5		:= {}

Pergunte("MTA906",.F.)
lContabil := If(mv_par06==1,.T.,.F.)
lAglutina := If(mv_par05==1,.T.,.F.)
lDigita   := If(mv_par04==1,.T.,.F.)

Begin Transaction
	dbSelectArea("SFA")
	dbSetOrder(1)
	If dbSeek(xFilial("SFA")+SF9->F9_CODIGO)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se ha mais de uma apropriacao                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lOpc
			dbEval( {|| ++nAprop},, {|| xFilial("SFA")==SFA->FA_FILIAL .And. SFA->FA_CODIGO==SF9->F9_CODIGO} )
			If nAprop>1
				lRet := .F.
			Endif
		Else
			If lContabil
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica o numero do lote contabil                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SX5")
				dbSetOrder(1)
				If MsSeek(xFilial("SX5")+"09FIS")
					cLoteFis := AllTrim(X5Descri())
				Else
					cLoteFis := "FIS "
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Inicializa o arquivo de contabilizacao                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
				nHdlPrv := HeadProva(cLoteFis,"MATA905",cUserName,@cArqFis)
				If nHdlPrv<=0
					Help(" ",1,"SEM_LANC")
				Else
					nTotalFis += DetProva(nHdlPrv,"756","MATA905",cLoteFis,,,,, @c755,@aCT5)
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Fecha os lancamentos contabeis                               ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					RodaProva(nHdlPrv,nTotalFis)
					If nTotalFis>0
						nTotalFis := 0
						cA100Incl(cArqFis,nHdlPrv,1,cLoteFis,lDigita,lAglutina)
					EndIf
				Endif
				dbSelectArea("SFA")
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui a apropriacao                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RecLock("SFA",.F.)
			dbDelete()
			MsUnlock()
		Endif
	Endif
End Transaction
RestArea(aArea)

Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A906Legenda³ Autor ³ Gustavo G. Rueda.    ³ Data ³27.02.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Legenda das movimentacoes com o ativo                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A906Legenda()                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A905Legenda()
Local aLegenda := {}

aAdd(aLegenda, {"BR_LARANJA",	STR0012}) //"Ativo adquirido, porém sem ocorrencias."
aAdd(aLegenda, {"BR_VERMELHO",	STR0013}) //"Ativo baixado"
aAdd(aLegenda, {"BR_AZUL",		STR0014}) //"Ativo transferido"
aAdd(aLegenda, {"BR_PRETO",		STR0016}) //"Ativo baixado por período." 
aAdd(aLegenda, {"ENABLE",		STR0015}) //"Ativo depreciado"
aAdd(aLegenda, {"BR_AMARELO",	STR0017}) //"Ativo baixado parcialmente"
aAdd(aLegenda, {"BR_MARROM",	STR0032})//STR0018}) //"Ativo baixado parcialmente"
aAdd(aLegenda, {"BR_BRANCO",	STR0021}) //"Ativo Principal"

BrwLegenda (cCadastro, STR0011, aLegenda) //"Legenda"

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Marco Bianchi         ³ Data ³01/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
     
Private aRotina := {	{ STR0001 	,"AxPesqui"  , 0 , 1 , 0 , .F.},;		    //"Pesquisar"
							{ STR0002	,"AxVisual"  , 0 , 2 , 0 , NIL},;		//"Visual"
							{ STR0003   ,"A905Inclui", 0 , 3 , 0 , NIL},;		//"Incluir"
							{ STR0004   ,"AxAltera"  , 0 , 4 , 0 , NIL},;		//"Alterar"
							{ STR0005   ,"A905Deleta", 0 , 5 , 0 , NIL},;		//"Excluir"
							{ STR0011   ,"A905Legenda", 0 , 2 , 0 , .F.},;		//"Legenda"
							{ STR0021	,"A905BPrin", 0 , 3 , 0 , NIL},; 		//"Bem Principal"
                            { STR0022	,"A905BaixC", 0 , 2},;				 	//"Estorno Transferência"
                            { STR0023	,"A905EBaix", 0 , 2}} 				 	//"Estorno Transferência"
							
If ExistBlock("MT905MNU")
	ExecBlock("MT905MNU",.F.,.F.)
EndIf

Return(aRotina)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³A905BPrin ³ Autor ³ Vitor Felipe          ³ Data ³02/07/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro do Bem Principal para a rotina de Componente.	  ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A905BPrin(cAlias,nReg,nOpc)

Local nCntFor	:= 0
Local cCadastro := STR0028
Local aCpos		:= {}
Local nOpca		:= 0
Local oDlg
Local aSize		:= MsAdvSize(.T.)
Local aInfo		:= {}
Local aPosObj	:= {}
Local aObjects	:= {} 

If !A905ValCa()
	Alert(STR0020)
	Return
EndIf

Private aTela[0][0],aGets[0][0]
Private bCampo := { |nField| FieldName(nField) }

dbSelectArea(cAlias)
For nCntFor:= 1 To FCount()
	If FieldName(nCntFor) <> "F9_VALICMS"
		AADD(aCpos,FieldName(nCntFor))
	EndIf	
	M->&(EVAL(bCampo,nCntFor)) := CriaVar(FieldName(nCntFor),.T.)
Next nCntFor

aAdd( aObjects, { 315, 20, .T., .T. } )
aInfo    := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects)

DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],000 TO aSize[6],aSize[5] OF oMainWnd PIXEL
EnChoice( cAlias, nReg, nOpc, , , , ,aPosObj[1] ,aCpos, 3,,,"A905TudOk()")
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca:=1,if(EXISTCHAV("SF9",M->F9_CODIGO),oDlg:End(),nOpca:=0)},{||nOpca:=0,oDlg:End()})
//como não tem validação na tela dos campos obrigatórios pois o campo de valor de icm é bloqueado
//faço validação para não dar erro de chave duplicada.
If ( nOpcA == 1 )
	Begin Transaction
	RecLock(cAlias,.T.)
	For nCntFor := 1 To FCount()
		FieldPut(nCntFor,M->&(FieldName(nCntFor)))
	Next nCntFor
	SF9->F9_FILIAL  := xFilial("SF9")
	SF9->F9_ROTINA  := "MATA905"
	SF9->F9_TIPO 	:= "01"
	SF9->F9_CODBAIX := "BFINAL"
	If ( !Empty(M->F9_ITEMNFE) )
		cChaveSd1 := xFilial("SD1")+M->F9_DOCNFE+M->F9_SERNFE+M->F9_FORNECE+M->F9_LOJAFOR
		dbSelectArea("SD1")
		dbSetOrder(1)
		dbSeek(cChaveSd1,.F.)
		While (!Eof() .And. SD1->D1_FILIAL + SD1->D1_DOC + SD1->D1_SERIE + ;
							SD1->D1_FORNECE + SD1->D1_LOJA == cChaveSD1 )
			If ( M->F9_ITEMNFE == SD1->D1_ITEM )
				RecLock("SD1", .F. )
				SD1->D1_CODCIAP := M->F9_CODIGO
				SD1->( MsUnlock() ) 
			EndIf
			dbSelectArea("SD1")
			dbSkip()
		EndDo
	EndIf
	dbSelectArea("SF9")
	EvalTrigger()
	If ( __lSX8 )
		ConfirmSX8()
	EndIf
	End Transaction
Else
	If ( __lSX8 )
		RollBackSX8()
	EndIf
EndIf

Return  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³A905BaixC ³ Autor ³ Vitor Felipe          ³ Data ³02/07/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Baixa de componentes agregando o valor no Bem	  ³±±
±±³          ³ Principal.										          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Array com opcoes da rotina.                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Parametros do array a Rotina:                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A905BaixC(aComp,cCodAtv,cDescAtv,cItemAtivo)

Local cUfFor 		:= ""
Local cBFinal		:= ""
Local cIndex 		:= ""
Local cFiltro		:= ""
Local cCFOPENT		:= ""
Local cAliasSF9 	:= "SF9"
Local nIndex		:= 0
Local nAliqEnt		:= 0
Local nValICMS		:= 0
Local nValIcmAtv	:= 0
Local lPzDifRS 		:= .F.    
Local aPerg			:= {}
Local aParam		:= {""}
Local cEstado		:= GetNewPar("MV_ESTADO", "")  
Local cParINF		:= SM0->M0_CODIGO+SM0->M0_CODFIL+"MTA905C"
Local nX := 0
Default cItemAtivo := ""

If IsInCallStack("MATA905")
	cCodAtv		:= ""
	cDescAtv	:= ""
	aComp		:= {}
Endif

If !A905ValCa()
	Alert(STR0020)
	Return
EndIf

If Empty(aComp)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Montagem da Interface                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MV_PAR01	:=	cBFinal	:= aParam[01] := PadR(ParamLoad(cParINF,aPerg,1,aParam[01]),6)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Montagem das perguntas                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aadd(aPerg,{1,STR0021,aParam[01],"",".T.","",".T.",6,.T.}) //"Bem Principal"
	
	If ParamBox(aPerg,STR0025,,,,,,,,cParINF,.T.,.T.)
	 	cBFinal	:= MV_PAR01
	 	
	 	dbSelectArea("SF9")
		dbSetOrder(1) 	
	 	#IFDEF TOP
	    If (TcSrvType ()<>"AS/400")
			cAliasSF9 := GetNextAlias()
			BeginSql Alias cAliasSF9
				
			SELECT * FROM %Table:SF9% SF9
					WHERE
					SF9.F9_FILIAL = %xFilial:SF9% AND
					SF9.F9_CODBAIX = %Exp:cBFinal% AND
					SF9.%notdel%
			EndSql
		Else
		#ENDIF
			    cIndex	:= CriaTrab(NIL,.F.)
			    cFiltro	:= 'F9_FILIAL=="'+xFilial ("SF9")+'".And. F9_CODBAIX=="'+cBFinal+'"'
			    IndRegua (cAliasSF9, cIndex, SF9->(IndexKey ()),, cFiltro)
			    nIndex := RetIndex(cAliasSF9)
				#IFNDEF TOP
					DbSetIndex (cIndex+OrdBagExt ())
				#ENDIF
				DbSelectArea (cAliasSF9)
			    DbSetOrder (nIndex+1)
		#IFDEF TOP
		Endif
		#ENDIF
		SA2->(DbSetOrder(1))
		IF SA2->(DbSeek(xFilial("SA2")+SF9->F9_FORNECE+SF9->F9_LOJAFOR))
			cUfFor	 := SA2->A2_EST
			IF cUfFor == "RS" .And. cEstado == "RS" .And. SF9->F9_DTEMINE >= CtoD('01/03/2014')
				lPzDifRS := .T. 
			EndIf	
		EndIf   
	 	If A905Mov(cBFinal)
			Alert("Não é possível utilizar Bens Baixados,Movimentados ou Apropriados")
			Return	 
		ElseIf SF9->(msSeek(xFilial("SF9")+cBFinal)) .And. SF9->F9_ROTINA = "MATA103"
			Alert("Não é possível Baixar, bem principal gerado através do Documento de Entrada")
			Return									
	 	Else
		 	While (cAliasSF9)->(!Eof())
		 		nValICMS += (cAliasSF9)->F9_VALICMS - (cAliasSF9)->F9_VLESTOR	
		 		cCFOPEnt := (cAliasSF9)->F9_CFOENT
		 		nAliqEnt := (cAliasSF9)->F9_PICM	
		 		If SF9->(msSeek(xFilial("SF9")+(cAliasSF9)->F9_CODIGO))
					RecLock("SF9", .F.)
						SF9->F9_BXICMS  := SF9->F9_VALICMS
						SF9->F9_VALICMS	:= 0
						SF9->F9_SLDPARC := 0
						SF9->F9_MOTIVO	:= "5"
					SF9->(MsUnlock())  		
			 		If !SFA->(msSeek(xFilial("SFA")+(cAliasSF9)->F9_CODIGO+DtoS(dDataBase)))
						RecLock("SFA",.T.)
						SFA->FA_FILIAL := xFilial("SFA")				
						SFA->FA_DATA   := dDataBase
						SFA->FA_TIPO   := "2"
						SFA->FA_CODIGO := (cAliasSF9)->F9_CODIGO
						SFA->FA_ROTINA := "MATA906"
						SFA->FA_MOTIVO := "5"
						SFA->FA_FATOR  := 1
						SFA->FA_CREDIT := "1"
						MsUnlock()
					EndIf
				EndIf								
				(cAliasSF9)->(dbSkip())
			EndDo
			If SF9->(msSeek(xFilial("SF9")+cBFinal))
				RecLock("SF9", .F.)
					SF9->F9_CODBAIX	:= "BFINAL"
					SF9->F9_VALICMS	+= nValICMS
					SF9->F9_QTDPARC	:= IIF(lPzDifRS,24, 48)
					SF9->F9_SLDPARC	:= IIF(lPzDifRS,24, 48)
					SF9->F9_VLESTOR	:= 0
					SF9->F9_BXICMS	:= 0
					SF9->F9_CFOENT	:= cCFOPENT
					SF9->F9_PICM	:= nAliqEnt
					SF9->F9_ICMIMOB	:= nValICMS
					SF9->F9_DTENTNE	:= dDataBase
					SF9->F9_DTEMINE	:= dDataBase
					SF9->F9_TIPO	:= "01"
				SF9->(MsUnlock()) 		
			EndIf
		EndIf
		msgInfo(STR0027+cBFinal,STR0025)
		#IFDEF TOP
			If (TcSrvType ()<>"AS/400")
				DbSelectArea (cAliasSF9)
				(cAliasSF9)->(DbCloseArea())
			Else
		#ENDIF
				RetIndex("SF9")
				FErase(cIndex+OrdBagExt())
		#IFDEF TOP
			EndIf
		#ENDIF
	EndIf
Else
	If cEstado == "RS"
		lPzDifRS := .T. 		
	Endif
	dbSelectArea("SF9")	
	dbSetOrder(1)
	If !Empty(aComp) .And. !Empty(cCodAtv)
		cCodCiap	 := GETNUMSF9()
		For nX:= 1 To Len(aComp)
			If SF9->(MsSeek(xFilial("SF9")+aComp[nX])) .And. SF9->F9_TIPO == "03"
				If (SF9->F9_SLDPARC = 48 .And. !lPzDifRS) .Or. (SF9->F9_SLDPARC = 24 .And. lPzDifRS)
					nValIcmAtv += SF9->F9_VALICMS
					RecLock("SF9", .F.)
					SF9->F9_CODBAIX := cCodCiap
					SF9->F9_BXICMS  := SF9->F9_VALICMS
					SF9->F9_VALICMS	:= 0
					SF9->F9_SLDPARC := 0
					SF9->F9_MOTIVO	:= "5"
					MsUnlock()
				Endif
				If !SFA->(msSeek(xFilial("SFA")+(cAliasSF9)->F9_CODIGO+DtoS(dDataBase)))
					RecLock("SFA",.T.)
					SFA->FA_FILIAL := xFilial("SFA")				
					SFA->FA_DATA   := dDataBase
					SFA->FA_TIPO   := "2"
					SFA->FA_CODIGO := aComp[nX]
					SFA->FA_ROTINA := "MATA906"
					SFA->FA_MOTIVO := "5"
					SFA->FA_FATOR  := 1
					SFA->FA_CREDIT := "1"
					MsUnlock()
				EndIf
			Endif
		Next nX
		If !SF9->(MsSeek(xFilial("SF9")+cCodCiap)) .And. nValIcmAtv >0
			RecLock("SF9", .T.)
			SF9->F9_FILIAL    := xFilial("SF9")
			SF9->F9_ROTINA    := "ATFA040"
			SF9->F9_TIPO 	  := "01"
			SF9->F9_CODBAIX   := "BFINAL"
			SF9->F9_CODIGO	  := cCodCiap
			SF9->F9_DESCRI 	  := cDescAtv
			SF9->F9_FORNECE	  := SuperGetMV("MV_RECEST")
			SF9->F9_LOJAFOR	  := "00"
			SF9->F9_DOCNFE	  := SF9->F9_CODIGO
			SF9->F9_PROPRIO	  := "N"
			SF9->F9_DTENTNE	  := ddatabase
			SF9->F9_DTEMINE	  := ddatabase
			SF9->F9_QTDPARC	  := IIF(lPzDifRS,24, 48)
			SF9->F9_SLDPARC	  := IIF(lPzDifRS,24, 48)		
			SF9->F9_VALICMS	  := nValIcmAtv
			If SF9->(FieldPos("F9_STATUS"))>0
				SF9->F9_STATUS	  := "1"
			EndIf
			MsUnlock()
		Endif		
		dbSelectArea("SN1")
		dbSetOrder(1)
		If MsSeek(xFilial("SN1")+cCodAtv+cItemAtivo) .And. nValIcmAtv >0
			RecLock("SN1",.F.)
			SN1->N1_CODCIAP := SF9->F9_CODIGO
			SN1->N1_ICMSAPR := nValIcmAtv
			If Empty(SN1->N1_NFISCAL)
				SN1->N1_NFISCAL := SF9->F9_DOCNFE
			EndiF
			MsUnlock()
		Endif
	Endif
Endif
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³A905EBaix ³ Autor ³ Vitor Felipe          ³ Data ³02/07/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Estorno Baixa de componentes. 					  ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A905EBaix()

Local cBFinal	:= ""
Local cParINF	:= SM0->M0_CODIGO+SM0->M0_CODFIL+"MTA905E"
Local aParam	:= {""}
Local aPerg		:= {}
Local cAliasSF9 := "SF9"
Local nValICMS	:= 0
Local cIndex	:= ""
Local cFiltro	:= ""
Local nIndex	:= 0

If !A905ValCa()
	Alert(STR0020)
	Return
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem da Interface                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MV_PAR01	:=	cBFinal	:= aParam[01] := PadR(ParamLoad(cParINF,aPerg,1,aParam[01]),6)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem das perguntas                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aadd(aPerg,{1,STR0021,aParam[01],"",".T.","",".T.",6,.T.}) //"Bem Principal"

If ParamBox(aPerg,STR0026,,,,,,,,cParINF,.T.,.T.)
   	cBFinal	:= MV_PAR01
 	
 	dbSelectArea("SF9")
	dbSetOrder(1) 	
 	#IFDEF TOP
    If (TcSrvType ()<>"AS/400")
		cAliasSF9 := GetNextAlias()
		BeginSql Alias cAliasSF9
			
		SELECT * FROM %Table:SF9% SF9
				WHERE
				SF9.F9_FILIAL = %xFilial:SF9% AND
				SF9.F9_CODBAIX = %Exp:cBFinal% AND
				SF9.%notdel%
		EndSql
	Else
	#ENDIF
		    cIndex	:= CriaTrab(NIL,.F.)
		    cFiltro	:= 'F9_FILIAL=="'+xFilial ("SF9")+'".And. F9_CODBAIX=="'+cBFinal+'"'
		    IndRegua (cAliasSF9, cIndex, SF9->(IndexKey ()),, cFiltro)
		    nIndex := RetIndex(cAliasSF9)
			#IFNDEF TOP
				DbSetIndex (cIndex+OrdBagExt ())
			#ENDIF
			DbSelectArea (cAliasSF9)
		    DbSetOrder (nIndex+1)
	#IFDEF TOP
	Endif
	#ENDIF
 	If A905Mov(cBFinal)
		Alert("Não é possível utilizar Bens Baixados,Movimentados ou Apropriados")
		Return	 	
 	ElseIf SF9->(msSeek(xFilial("SF9")+cBFinal))
 		While (cAliasSF9)->(!Eof())
	 		nValICMS += (cAliasSF9)->F9_BXICMS//(cAliasSF9)->F9_VALICMS - (cAliasSF9)->F9_VLESTOR	
	 		If SF9->(msSeek(xFilial("SF9")+(cAliasSF9)->F9_CODIGO))
				RecLock("SF9", .F.)
					SF9->F9_VALICMS	:= (cAliasSF9)->F9_BXICMS
					SF9->F9_BXICMS  := 0
					SF9->F9_MOTIVO	:= " "
					SF9->F9_CODBAIX := ""
				SF9->(MsUnlock())  		
			EndIf
			(cAliasSF9)->(dbSkip())
		EndDo
		
		If SF9->(msSeek(xFilial("SF9")+cBFinal))
			RecLock("SF9", .F.)
				SF9->F9_CODBAIX	:= "BFINAL"
				SF9->F9_VALICMS	-=  nValICMS
				SF9->F9_CODBAIX  := ""
				SF9->F9_ICMIMOB	:= 0 
			SF9->(MsUnlock()) 		
		EndIf
		
		msgInfo(STR0027+cBFinal,STR0026)
	Else
		// "Ativo principal codigo (cBFinal) não localizado. Operação de estorno não pode ser efetuada."
		Alert(STR0029 + cBFinal + STR0030 + Chr(13) + Chr(10) + STR0031) 
		Return	 		
	EndIf
	
	#IFDEF TOP
		If (TcSrvType ()<>"AS/400")
			DbSelectArea (cAliasSF9)
			(cAliasSF9)->(DbCloseArea())
		Else
	#ENDIF
			RetIndex("SF9")
			FErase(cIndex+OrdBagExt())
	#IFDEF TOP
		EndIf
	#ENDIF
EndIf      
Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A905ValCa ºAutor  ³ Vitor Felipe       º Data ³  08/05/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validar campos necessarios para rotina de componente.      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function A905ValCa()

Local lValid	:= .F. 

If SF4->(FieldPos("F4_COMPONE"))>0 .And. SF9->(FieldPos("F9_TIPO"))>0 .And. SF9->(FieldPos("F9_CODBAIX"))>0;
		 .And. SD1->(FieldPos("D1_CODBAIX"))>0 
	lValid := .T.
EndIf

Return(lValid) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A905Mov   ºAutor  ³ Vitor Felipe       º Data ³  08/05/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica bem Final foi Movimentado.						  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A905Mov(cBFinal)

Local lMovBem := .F.

dbSelectArea("SFA")
dbSetOrder(1)
If SFA->(msSeek(xFilial("SFA")+cBFinal))
	lMovBem := .T.
EndIf

Return(lMovBem)	
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    | A905FilGC ³ Autor ³ TOTVS S/A            ³ Data ³ 21.11.13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³Recupera as filiais do mesmo CNPJ+IE                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A905FilGC()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A905FilGC()
Local aAreaSM0	:= SM0->(GetArea())
Local aSM0      := FWLoadSM0(.T.,,.T.)
Local cCGC      := SM0->M0_CGC
Local cIE       := SM0->M0_INSC
Local cLista    := ""
Local lFirst    := .T.
Local nPos      := 0

//Seleciona as filiais do Grupo Corrente (CNPJ+IE)
For nPos:=1 to Len(aSM0)
	If	aSM0[nPos,SM0_GRPEMP] == cEmpAnt 	    .And.;
		aSM0[nPos,SM0_EMPRESA]== FWCompany()	.And.; 
		aSM0[nPos,SM0_CGC]    == cCGC			.And.; 
		aSM0[nPos,SM0_USEROK]                	.And.;
	   	Posicione("SM0",1,aSM0[nPos,SM0_GRPEMP]+aSM0[nPos,SM0_CODFIL],"M0_INSC")=cIE
		cLista += IIf(lFirst,"'"+aSM0[nPos,SM0_CODFIL]+"'",",'"+aSM0[nPos,SM0_CODFIL]+"'")
		lFirst := .F.		
	EndIf
Next nPos 

RestArea(aAreaSM0)
Return cLista		

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A905IncCtb³ Autor ³ Alex Sandro Fagundes  ³ Data ³ 11.10.19 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Inclui contabilização Bem CIAP                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Void A905IncCtb()                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametro ³cCodCtb - Código da Inclusão ou Exclusão do Bem             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Mata905()                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A905IncCtb(cCodCtb)
	Local lContabil := .F.
	Local lAglutina	:= .F.
	Local lDigita	:= .F.
	Local cLoteFis	:= ""
	Local nHdlPrv	:= 0 
	Local cArqFis	:= ""
	Local nTotalFis	:= 0	
	Local c770		:= ""
	Local aCT5		:= {}
	
	If SF9->(FieldPos("F9_STATUS"))>0
		Pergunte("MTA905",.F.)
		lContabil := If(mv_par01==1,.T.,.F.)
		lAglutina := If(mv_par02==1,.T.,.F.)
		lDigita   := If(mv_par03==1,.T.,.F.)
	EndIf

	If lContabil
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica o numero do lote contabil                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SX5")
		dbSetOrder(1)
		If MsSeek(xFilial("SX5")+"09FIS")
			cLoteFis := AllTrim(X5Descri())
		Else
			cLoteFis := "FIS "
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa o arquivo de contabilizacao                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		nHdlPrv := HeadProva(cLoteFis,"MATA905",cUserName,@cArqFis)
		If nHdlPrv<=0
			Help(" ",1,"SEM_LANC")
		Else
			nTotalFis += DetProva(nHdlPrv,cCodCtb,"MATA905",cLoteFis,,,,, @c770,@aCT5)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Fecha os lancamentos contabeis                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RodaProva(nHdlPrv,nTotalFis)
			If nTotalFis>0
				nTotalFis := 0
				cA100Incl(cArqFis,nHdlPrv,1,cLoteFis,lDigita,lAglutina)
				RecLock("SF9", .F.)
				If SF9->(FieldPos("F9_STATUS"))>0
					SF9->F9_STATUS  := "2"
				EndIf
				SF9->(MsUnlock())  		
			EndIf
		Endif

	EndIf

Return
