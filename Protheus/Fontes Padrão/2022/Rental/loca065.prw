/*/{PROTHEUS.DOC} LOCA065.PRW  
ITUP BUSINESS - TOTVS RENTAL
DUPLICA REGISTRO
@TYPE FUNCTION
@AUTHOR FRANK ZWARG FUGA
@SINCE 03/12/2020
@VERSION P12
@HISTORY 03/12/2020, FRANK ZWARG FUGA, FONTE PRODUTIZADO.
/*/

#INCLUDE "TOTVS.CH"

FUNCTION LOCA065( CALIAS , AINFO ) 
LOCAL   NI
LOCAL   NPOS
LOCAL   ADADOS := {}

DEFAULT AINFO  := {}

DBSELECTAREA(CALIAS)

// ARMAZENANDO TODAS AS INFORMAÇÕES DO REGISTRO POSICIONADO
FOR NI := 1 TO FCOUNT()
	AADD( ADADOS, FIELDGET(NI) )
NEXT

// CASO TENHA SIDO INFORMADO O ARRAY COM OS NOVOS VALORES, AJUSTAMOS O ARRAY COM OS VALORES INFORMADOS
FOR NI := 1 TO LEN( AINFO )
	IF ( NPOS := FIELDPOS( AINFO[NI][1] ) ) > 0
		ADADOS[NPOS] := AINFO[NI][2]
	ENDIF
NEXT

// REPLICANDO O REGISTRO (GERANDO NOVO REGISTRO)
RECLOCK(CALIAS,.T.)
FOR NI := 1 TO LEN( ADADOS )
	FIELDPUT( NI, ADADOS[NI] )
NEXT
MSUNLOCK()

RETURN NIL
