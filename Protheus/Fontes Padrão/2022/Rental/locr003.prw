/*/{PROTHEUS.DOC} LOCR003.PRW
ITUP BUSINESS - TOTVS RENTAL
DEMONSTRATIVO DE FATURAMENTO
@TYPE FUNCTION
@AUTHOR FRANK ZWARG FUGA
@SINCE 03/12/2020
@VERSION P12
@HISTORY 03/12/2020, FRANK ZWARG FUGA, FONTE PRODUTIZADO.
/*/

#INCLUDE "locr003.ch" 
#INCLUDE "TOTVS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "RPTDEF.CH"
#INCLUDE "FWPRINTSETUP.CH"

FUNCTION LOCR003()
LOCAL   _AAREAOLD := GETAREA()
LOCAL   _AAREASM0 := SM0->(GETAREA())
PRIVATE CPERG     := "LOCP013"

IF PERGPARAM(CPERG)
	DBSELECTAREA("SM0")
	SM0->(DBSETORDER(1))
	IF SM0->(DBSEEK(CEMPANT+CFILANT))
		PROCESSA({|| IMPREL() } , STR0001 , STR0002 , .T.)  //"IMPRIMINDO FATURA..."###"AGUARDE..."
	ENDIF
ENDIF

SM0->(RESTAREA( _AAREASM0 ))
RESTAREA( _AAREAOLD )

RETURN



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPROGRAMA  ³ IMPREL    º AUTOR ³ IT UP CONSULTORIA  º DATA ³ 03/08/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDESCRICAO ³ DEFINICAO DO LAYOUT DO RELATORIO                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/ 
STATIC FUNCTION IMPREL()

LOCAL LADJUSTTOLEGACY := .F.
LOCAL LDISABLESETUP   := .F.
LOCAL OREPORT
LOCAL CARQUIVO   	:= ""
LOCAL I           	:= 0
LOCAL NLIN        	:= 10
LOCAL CLOGO			:= ""
LOCAL _CQUERY      	:= ""
LOCAL _NTOTAL      	:= 0
LOCAL OBRUSH      	:= TBRUSH():NEW("",CLR_HGRAY)
LOCAL _CTIPO		:= ""
LOCAL _APRODUTOS  	:= {}
LOCAL _DDTINI		:= STOD("")
LOCAL _DDTFIM		:= STOD("")
LOCAL _CCONTRATO  	:= ""
LOCAL _CEMPRESA   	:= ""
LOCAL _COBSFAT   	:= ""
LOCAL ASM0			:= {}
LOCAL CCNPJ			:= ""
LOCAL CEND			:= ""
LOCAL CBAIRRO		:= ""
LOCAL CCIDADE		:= ""
LOCAL CCEP			:= ""
LOCAL CUF			:= ""
LOCAL CTEL			:= ""
LOCAL CFAX			:= ""
LOCAL AAREASM0		:= {}

PRIVATE NMINLEFT   	:= 10
PRIVATE NMAXWIDTH 	:= 584
PRIVATE NMAXHEIGHT	:= 2900
PRIVATE NMEIO		:= ((NMAXWIDTH+NMINLEFT)/2)

CARQUIVO := STR0003 + ALLTRIM(MV_PAR01) + " - " + ALLTRIM(MV_PAR02) + "_" + DTOS(DATE()) + ".PDF" //"FATURA "

OREPORT := FWMSPRINTER():NEW(CARQUIVO , 6 , LADJUSTTOLEGACY , , LDISABLESETUP , , , , , , .F. , ) 

OREPORT:SETPORTRAIT()
OREPORT:SETPAPERSIZE(9)
OREPORT:SETVIEWPDF( .T. )

//"TIMES NEW ROMAN"
OFONT1    := TFONTEX():NEW(OREPORT,"COURIER",10,10,.T.,.T.,.F.)// 1
OFONT2    := TFONTEX():NEW(OREPORT,"COURIER",08,08,.F.,.F.,.F.)// 1
OFONT3    := TFONTEX():NEW(OREPORT,"COURIER",12,12,.T.,.T.,.F.)// 1
OFONT4    := TFONTEX():NEW(OREPORT,"COURIER",12,12,.F.,.F.,.F.)// 1

// --> BUSCA AS INFORMACOES DAS FILIAIS.
AAREASM0 := SM0->(GETAREA())
DBSELECTAREA("SM0")
SM0->(DBSETORDER(1))
IF SM0->(DBSEEK(CEMPANT+MV_PAR04))	
	CEND     := ALLTRIM(SM0->M0_ENDCOB)
	CBAIRRO  := ALLTRIM(SM0->M0_BAIRCOB)
	CCIDADE  := ALLTRIM(SM0->M0_CIDCOB)
	CCEP     := SUBSTR(SM0->M0_CEPCOB,1,5) + '-' + SUBSTR(SM0->M0_CEPCOB,6,3)
	CUF	     := ALLTRIM(SM0->M0_ESTCOB)
	CTEL     := "(" + SUBSTR(SM0->M0_TEL,4,2) + ") " + SUBSTR(SM0->M0_TEL,7,4) + "-" + SUBSTR(SM0->M0_TEL,11,4)
	IF EMPTY(ALLTRIM(SM0->M0_FAX))
		CFAX := ""
	ELSE
	 	CFAX := "(" + SUBSTR(SM0->M0_FAX,4,2) + ") " + SUBSTR(SM0->M0_FAX,7,4) + "-" + SUBSTR(SM0->M0_FAX,11,4)
	ENDIF
	CCNPJ    := SUBSTR(SM0->M0_CGC,1,2) + "." + SUBSTR(SM0->M0_CGC,3,3) + "." + SUBSTR(SM0->M0_CGC,6,3) + "/" +;
			    SUBSTR(SM0->M0_CGC,9,4) + "-" + SUBSTR(SM0->M0_CGC,13,2)
ENDIF
SM0->(RESTAREA(AAREASM0))

// LOGO
CLOGO := GETSRVPROFSTRING("STARTPATH","") + "LOGO.JPG" 

IF SELECT("TRBFAT") > 0
	TRBFAT->(DBCLOSEAREA())
ENDIF
// --> (A) CORRECAO DO VALOR DOS IMPOSTOS RETIDOS NA EMISSAO DO RELATORIO.   (*INICIO*) 
_CQUERY := "SELECT " + CRLF
_CQUERY += "(SELECT SUM(E1_VALOR) FROM "+RETSQLNAME("SE1")+" SE1TMP1 (NOLOCK) WHERE SE1TMP1.E1_FILIAL = '"+XFILIAL("SE1")+"' AND XTEMP1.NUMFAT = SE1TMP1.E1_NUM AND XTEMP1.SERIE=SE1TMP1.E1_PREFIXO AND SE1TMP1.E1_TIPO IN ('IR-','CS-','PI-','CF-','IS-','IN-') AND SE1TMP1.D_E_L_E_T_ = '') AS 'VALRET2', " + CRLF
_CQUERY += "XTEMP1.* " + CRLF
_CQUERY += "FROM " + CRLF
_CQUERY += "( " + CRLF
_CQUERY += " SELECT F2_FILIAL FILIAL, F2_SERIE SERIE, F2_DOC NUMFAT, F2_EMISSAO EMISSAO, MAX(ISNULL(F4_TEXTO,'"+STR0004+"')) F4_TEXTO, MAX(C6_NUM) PEDIDO," + CRLF //'LOCAÇÃO DE BENS MÓVEIS'
_CQUERY += " 	   RTRIM(LTRIM(A1_NOME)) RAZSOC, A1_END A1_END, A1_BAIRRO, A1_MUN, A1_EST, " + CRLF
_CQUERY += " 	   SUBSTRING(A1_CEP,1,5) + '-' + SUBSTRING(A1_CEP,6,3) A1_CEP, " + CRLF
_CQUERY += " 	   SUBSTRING(A1_CGC,1,2) + '.' + SUBSTRING(A1_CGC,3,3) + '.' + SUBSTRING(A1_CGC,6,3) + '/' +" + CRLF
_CQUERY += " 	   SUBSTRING(A1_CGC,9,4) + '-' + SUBSTRING(A1_CGC,13,2) A1_CNPJ, " + CRLF
_CQUERY += " 	   CASE WHEN A1_INSCR = ''" + CRLF
_CQUERY += " 			THEN 'ISENTO'" + CRLF
_CQUERY += " 			ELSE A1_INSCR   END A1_INSCR   , " + CRLF
_CQUERY += "       SE1NF.E1_VENCTO VENCTO," + CRLF
_CQUERY += " 	   F2_VALBRUT, SE1NF.E1_DESCONT, SUM(ISNULL(SE1IMP.E1_VALOR,0)) VALRET," + CRLF
_CQUERY += " 	   F2_VALBRUT - ISNULL(SE1NF.E1_DESCONT,0) - SUM(ISNULL(SE1IMP.E1_VALOR,0)) - SE1NF.E1_IRRF TOTAL," + CRLF
_CQUERY += " 	   CASE WHEN A1_ENDCOB = ''" + CRLF
_CQUERY += " 			THEN A1_END" + CRLF
_CQUERY += " 			ELSE A1_ENDCOB  END A1_ENDCOB  , " + CRLF
_CQUERY += " 	   CASE WHEN A1_BAIRROC = ''" + CRLF
_CQUERY += " 			THEN A1_BAIRRO" + CRLF
_CQUERY += " 			ELSE A1_BAIRROC END A1_BAIRROC , " + CRLF
_CQUERY += " 	   CASE WHEN A1_MUNC = ''" + CRLF
_CQUERY += " 			THEN A1_MUN" + CRLF
_CQUERY += " 			ELSE A1_MUNC    END A1_MUNC    , " + CRLF
_CQUERY += " 	   CASE WHEN A1_ESTC = ''" + CRLF
_CQUERY += " 			THEN A1_EST" + CRLF
_CQUERY += " 			ELSE A1_ESTC    END A1_ESTC    , " + CRLF
_CQUERY += " 	   CASE WHEN A1_CEPC = ''" + CRLF
_CQUERY += " 			THEN SUBSTRING(A1_CEP,1,5) + '-' + SUBSTRING(A1_CEP,6,3)" + CRLF
_CQUERY += " 			ELSE SUBSTRING(A1_CEPC,1,5) + '-' + SUBSTRING(A1_CEPC,6,3) END A1_CEPC," + CRLF
_CQUERY += " 	   CASE WHEN A1_ENDENT = ''" + CRLF
_CQUERY += " 			THEN A1_END" + CRLF
_CQUERY += " 			ELSE A1_ENDENT  END A1_ENDENT  , " + CRLF
_CQUERY += " 	   CASE WHEN A1_BAIRROE = ''" + CRLF
_CQUERY += " 			THEN A1_BAIRRO" + CRLF
_CQUERY += " 			ELSE A1_BAIRROE END A1_BAIRROE , " + CRLF
_CQUERY += " 	   CASE WHEN A1_MUNE = ''" + CRLF
_CQUERY += " 			THEN A1_MUN" + CRLF
_CQUERY += " 			ELSE A1_MUNE    END A1_MUNE    , " + CRLF
_CQUERY += " 	   CASE WHEN A1_ESTE = ''" + CRLF
_CQUERY += " 			THEN A1_EST" + CRLF
_CQUERY += " 			ELSE A1_ESTE    END A1_ESTE    , " + CRLF
_CQUERY += " 	   CASE WHEN A1_CEPE = ''" + CRLF
_CQUERY += " 			THEN SUBSTRING(A1_CEP,1,5) + '-' + SUBSTRING(A1_CEP,6,3)" + CRLF
_CQUERY += " 			ELSE SUBSTRING(A1_CEPE,1,5) + '-' + SUBSTRING(A1_CEPE,6,3) END A1_CEPE," + CRLF
_CQUERY += " 		ISNULL(A6_NOME,'') BANCO, " + CRLF
_CQUERY += " 		CASE WHEN ISNULL(A6_DVAGE,'') = ''" + CRLF
_CQUERY += " 		     THEN ISNULL(A6_AGENCIA,'')" + CRLF
_CQUERY += " 		     ELSE RTRIM(LTRIM(A6_AGENCIA)) + '-' + RTRIM(LTRIM(A6_DVAGE)) END AGENCIA," + CRLF
_CQUERY += " 		CASE WHEN ISNULL(A6_DVCTA,'') = ''" + CRLF
_CQUERY += " 		     THEN ISNULL(A6_NUMCON,'')" + CRLF
_CQUERY += " 		     ELSE RTRIM(LTRIM(A6_NUMCON)) + '-' + RTRIM(LTRIM(A6_DVCTA)) END CONTA, SE1NF.E1_IRRF AS IRFAT" + CRLF
_CQUERY += " FROM " + RETSQLNAME("SF2") + " SF2 "
_CQUERY += "        INNER JOIN " + RETSQLNAME("SA1") + " SA1    ON  SF2.F2_CLIENTE = A1_COD       AND  SF2.F2_LOJA = SA1.A1_LOJA " + CRLF
_CQUERY += "                                                    AND SA1.D_E_L_E_T_ = '' "                                          + CRLF
If !empty(alltrim(xFilial("SA1")))
	_cQuery += " AND SA1.A1_FILIAL = '"+xFilial("SA1")+"' " + CRLF
EndIF
_CQUERY += "        INNER JOIN " + RETSQLNAME("SD2") + " SD2    ON  SD2.D2_FILIAL  = F2_FILIAL    AND  SD2.D2_DOC  = F2_DOC "      + CRLF
_CQUERY += "                                                    AND SD2.D2_SERIE   = F2_SERIE "                                    + CRLF
_CQUERY += "                                                    AND SD2.D_E_L_E_T_ = '' "                                          + CRLF
_CQUERY += " 	    INNER JOIN " + RETSQLNAME("SC6") + " SC6    ON  SC6.C6_FILIAL  = F2_FILIAL    AND  SC6.C6_NOTA = F2_DOC "      + CRLF
_CQUERY += "                                                    AND SC6.C6_SERIE   = F2_SERIE     AND  SC6.C6_ITEM = D2_ITEM "     + CRLF 
_CQUERY += "                                                    AND SC6.D_E_L_E_T_ = '' "                                          + CRLF
_CQUERY += "        LEFT  JOIN " + RETSQLNAME("SF4") + " SF4    ON  SF4.F4_CODIGO  = D2_TES "                                      + CRLF
_CQUERY += "                                                    AND SF4.D_E_L_E_T_ = '' "                                          + CRLF
_CQUERY += "        LEFT  JOIN " + RETSQLNAME("SE1") + " SE1NF  ON  F2_FILIAL = SE1NF.E1_FILIAL   AND  F2_DOC = SE1NF.E1_NUM "     + CRLF
_CQUERY += "                                                    AND F2_SERIE  = SE1NF.E1_PREFIXO "                                 + CRLF
_CQUERY += "                                                    AND SE1NF.E1_TIPO  = 'NF' "                                        + CRLF
_CQUERY += "                                                    AND SE1NF.D_E_L_E_T_ = '' "                                        + CRLF
_CQUERY += " 	    LEFT  JOIN " + RETSQLNAME("SE1") + " SE1IMP ON  F2_FILIAL = SE1IMP.E1_FILIAL  AND  F2_DOC = SE1IMP.E1_NUM "    + CRLF
_CQUERY += "                                                    AND F2_SERIE  = SE1IMP.E1_PREFIXO "                                + CRLF
//_CQUERY+="                                                    AND SE1IMP.E1_TIPO IN ('-IR','-CS','-PI','-CF','-IS','-IN')"       + CRLF
_CQUERY += "                                                    AND SE1IMP.E1_TIPO IN ('IR-','CS-','PI-','CF-','IS-','IN-')"       + CRLF
_CQUERY += "                                                    AND SE1IMP.D_E_L_E_T_ = '' "                                       + CRLF
_CQUERY += " 	    LEFT  JOIN " + RETSQLNAME("SA6") + " SA6    ON  SA6.A6_FILIAL  = SE1NF.E1_FILIAL "                             + CRLF
_CQUERY += "                                                    AND SA6.A6_COD     = SE1NF.E1_PORTADO "                            + CRLF
_CQUERY += "                                                    AND SA6.A6_AGENCIA = SE1NF.E1_AGEDEP "                             + CRLF
_CQUERY += "                                                    AND SA6.A6_NUMCON  = SE1NF.E1_CONTA "                              + CRLF
_CQUERY += "                                                    AND SA6.D_E_L_E_T_ = '' "                                          + CRLF
_CQUERY += " WHERE  F2_FILIAL = '" + XFILIAL("SF2") + "'" + CRLF
_CQUERY += "   AND  F2_DOC BETWEEN '" + MV_PAR01 + "' AND '" + MV_PAR02 + "'" + CRLF
_CQUERY += "   AND  F2_SERIE = '" + MV_PAR03 + "'" + CRLF
_CQUERY += "   AND  SF2.D_E_L_E_T_ = ''" + CRLF
_CQUERY += "   AND  SF2.F2_TIPO NOT IN ('D','B') " + CRLF

_CQUERY += "  GROUP BY F2_FILIAL , F2_SERIE, F2_DOC, F2_EMISSAO, A1_NOME, A1_END, A1_BAIRRO, A1_MUN, A1_EST, A1_CEP, A1_CGC, A1_INSCR, " + CRLF
_CQUERY += " 		  SE1NF.E1_VENCTO, F2_VALBRUT, SE1NF.E1_DESCONT, A1_ENDCOB, A1_BAIRROC, A1_MUNC, A1_ESTC, A1_CEPC," + CRLF
_CQUERY += " 		  A1_ENDENT, A1_BAIRROE, A1_MUNE, A1_ESTE, A1_CEPE, A6_NOME, A6_DVAGE, A6_AGENCIA, A6_NUMCON, A6_DVCTA,SE1NF.E1_IRRF" + CRLF

// --> (B) CORRECAO DO VALOR DOS IMPOSTOS RETIDOS NA EMISSAO DO RELATORIO.   (*INICIO*) 
_CQUERY += " UNION ALL " + CRLF

_CQUERY += " SELECT F2_FILIAL FILIAL, F2_SERIE SERIE, F2_DOC NUMFAT, F2_EMISSAO EMISSAO, MAX(ISNULL(F4_TEXTO,'"+STR0004+"')) F4_TEXTO, MAX(C6_NUM) PEDIDO," + CRLF //'LOCAÇÃO DE BENS MÓVEIS'
_CQUERY += " 	   RTRIM(LTRIM(A2_NOME)) RAZSOC, A2_END A1_END, A2_BAIRRO A1_BAIRRO, A2_MUN A1_MUN, A2_EST A1_EST, " + CRLF
_CQUERY += " 	   SUBSTRING(A2_CEP,1,5) + '-' + SUBSTRING(A2_CEP,6,3) A1_CEP, " + CRLF
_CQUERY += " 	   SUBSTRING(A2_CGC,1,2) + '.' + SUBSTRING(A2_CGC,3,3) + '.' + SUBSTRING(A2_CGC,6,3) + '/' +" + CRLF
_CQUERY += " 	   SUBSTRING(A2_CGC,9,4) + '-' + SUBSTRING(A2_CGC,13,2) A1_CNPJ, " + CRLF
_CQUERY += " 	   CASE WHEN A2_INSCR = ''" + CRLF
_CQUERY += " 			THEN 'ISENTO'" + CRLF
_CQUERY += " 			ELSE A2_INSCR END A2_INSCR, SE1NF.E1_VENCTO VENCTO," + CRLF
_CQUERY += " 	   F2_VALBRUT, SE1NF.E1_DESCONT, SUM(ISNULL(SE1IMP.E1_VALOR,0)) VALRET," + CRLF
_CQUERY += " 	   F2_VALBRUT - ISNULL(SE1NF.E1_DESCONT,0) - SUM(ISNULL(SE1IMP.E1_VALOR,0)) - SE1NF.E1_IRRF TOTAL," + CRLF
_CQUERY += " 	   CASE WHEN A2_END = ''" + CRLF
_CQUERY += " 			THEN A2_END" + CRLF
_CQUERY += " 			ELSE A2_END END A1_ENDCOB," + CRLF
_CQUERY += " 	   CASE WHEN A2_BAIRRO = ''" + CRLF
_CQUERY += " 			THEN A2_BAIRRO" + CRLF
_CQUERY += " 			ELSE A2_BAIRRO END A1_BAIRROC," + CRLF
_CQUERY += " 	   CASE WHEN A2_MUN = ''" + CRLF
_CQUERY += " 			THEN A2_MUN" + CRLF
_CQUERY += " 			ELSE A2_MUN END A1_MUNC," + CRLF
_CQUERY += " 	   CASE WHEN A2_EST = ''" + CRLF
_CQUERY += " 			THEN A2_EST" + CRLF
_CQUERY += " 			ELSE A2_EST END A1_ESTC," + CRLF
_CQUERY += " 	   CASE WHEN A2_CEP = ''" + CRLF
_CQUERY += " 			THEN SUBSTRING(A2_CEP,1,5) + '-' + SUBSTRING(A2_CEP,6,3)" + CRLF
_CQUERY += " 			ELSE SUBSTRING(A2_CEP,1,5) + '-' + SUBSTRING(A2_CEP,6,3) END A1_CEPC," + CRLF
_CQUERY += " 	   CASE WHEN A2_END = ''" + CRLF
_CQUERY += " 			THEN A2_END" + CRLF
_CQUERY += " 			ELSE A2_END END A1_ENDENT," + CRLF
_CQUERY += " 	   CASE WHEN A2_BAIRRO = ''" + CRLF
_CQUERY += " 			THEN A2_BAIRRO" + CRLF
_CQUERY += " 			ELSE A2_BAIRRO END A1_BAIRROE," + CRLF
_CQUERY += " 	   CASE WHEN A2_MUN = ''" + CRLF
_CQUERY += " 			THEN A2_MUN" + CRLF
_CQUERY += " 			ELSE A2_MUN END A1_MUNE," + CRLF
_CQUERY += " 	   CASE WHEN A2_EST = ''" + CRLF
_CQUERY += " 			THEN A2_EST" + CRLF
_CQUERY += " 			ELSE A2_EST END A1_ESTE," + CRLF
_CQUERY += " 	   CASE WHEN A2_CEP = ''" + CRLF
_CQUERY += " 			THEN SUBSTRING(A2_CEP,1,5) + '-' + SUBSTRING(A2_CEP,6,3)" + CRLF
_CQUERY += " 			ELSE SUBSTRING(A2_CEP,1,5) + '-' + SUBSTRING(A2_CEP,6,3) END A1_CEPE," + CRLF
_CQUERY += " 		ISNULL(A6_NOME,'') BANCO, " + CRLF
_CQUERY += " 		CASE WHEN ISNULL(A6_DVAGE,'') = ''" + CRLF
_CQUERY += " 		     THEN ISNULL(A6_AGENCIA,'')" + CRLF
_CQUERY += " 		     ELSE RTRIM(LTRIM(A6_AGENCIA)) + '-' + RTRIM(LTRIM(A6_DVAGE)) END AGENCIA," + CRLF
_CQUERY += " 		CASE WHEN ISNULL(A6_DVCTA,'') = ''" + CRLF
_CQUERY += " 		     THEN ISNULL(A6_NUMCON,'')" + CRLF
_CQUERY += " 		     ELSE RTRIM(LTRIM(A6_NUMCON)) + '-' + RTRIM(LTRIM(A6_DVCTA)) END CONTA, SE1NF.E1_IRRF AS IRFAT" + CRLF
_CQUERY += "   FROM " + RETSQLNAME("SF2") + " SF2 INNER JOIN " + RETSQLNAME("SA2") + " SA2" + CRLF
_CQUERY += "     ON F2_CLIENTE = A2_COD" + CRLF
_CQUERY += "    AND F2_LOJA    = A2_LOJA" + CRLF
_CQUERY += "    AND SA2.D_E_L_E_T_ = ''" + CRLF
_CQUERY += "        INNER JOIN " + RETSQLNAME("SD2") + " SD2" + CRLF
_CQUERY += "     ON D2_FILIAL = F2_FILIAL" + CRLF
_CQUERY += "    AND D2_DOC    = F2_DOC" + CRLF
_CQUERY += "    AND D2_SERIE  = F2_SERIE" + CRLF
_CQUERY += "    AND SD2.D_E_L_E_T_ = ''" + CRLF
_CQUERY += " 	   INNER JOIN " + RETSQLNAME("SC6") + " SC6" + CRLF
_CQUERY += " 	ON C6_FILIAL = F2_FILIAL" + CRLF
_CQUERY += "    AND C6_NOTA   = F2_DOC" + CRLF
_CQUERY += "    AND C6_SERIE  = F2_SERIE" + CRLF
_CQUERY += "    AND C6_ITEM   = D2_ITEM" + CRLF
_CQUERY += "    AND SC6.D_E_L_E_T_ = ''" + CRLF
_CQUERY += "        LEFT JOIN " + RETSQLNAME("SF4") + " SF4" + CRLF
_CQUERY += "     ON F4_CODIGO = D2_TES" + CRLF
_CQUERY += "    AND SF4.D_E_L_E_T_ = ''" + CRLF
_CQUERY += "        LEFT JOIN " + RETSQLNAME("SE1") + " SE1NF" + CRLF
_CQUERY += "     ON F2_FILIAL = SE1NF.E1_FILIAL" + CRLF
_CQUERY += "    AND F2_DOC    = SE1NF.E1_NUM" + CRLF
_CQUERY += "    AND F2_SERIE  = SE1NF.E1_PREFIXO" + CRLF
_CQUERY += "    AND SE1NF.E1_TIPO  = 'NF'" + CRLF
_CQUERY += "    AND SE1NF.D_E_L_E_T_ = ''" + CRLF
_CQUERY += " 	   LEFT JOIN " + RETSQLNAME("SE1") + " SE1IMP" + CRLF
_CQUERY += "     ON F2_FILIAL = SE1IMP.E1_FILIAL" + CRLF
_CQUERY += "    AND F2_DOC    = SE1IMP.E1_NUM" + CRLF
_CQUERY += "    AND F2_SERIE  = SE1IMP.E1_PREFIXO" + CRLF
_CQUERY += "    AND SE1IMP.E1_TIPO   IN ('IR-','CS-','PI-','CF-','IS-','IN-')" + CRLF
_CQUERY += "    AND SE1IMP.D_E_L_E_T_ = ''" + CRLF
_CQUERY += " 	   LEFT JOIN " + RETSQLNAME("SA6") + " SA6" + CRLF
_CQUERY += "     ON A6_FILIAL  = SE1NF.E1_FILIAL" + CRLF
_CQUERY += "    AND A6_COD     = SE1NF.E1_PORTADO" + CRLF
_CQUERY += "    AND A6_AGENCIA = SE1NF.E1_AGEDEP" + CRLF
_CQUERY += "    AND A6_NUMCON  = SE1NF.E1_CONTA" + CRLF
_CQUERY += "    AND SA6.D_E_L_E_T_ = ''" + CRLF
_CQUERY += "  WHERE F2_FILIAL = '" + XFILIAL("SF2") + "'" + CRLF
_CQUERY += "    AND F2_DOC BETWEEN '" + MV_PAR01 + "' AND '" + MV_PAR02 + "'" + CRLF
_CQUERY += "    AND F2_SERIE = '" + MV_PAR03 + "'" + CRLF
_CQUERY += "    AND SF2.D_E_L_E_T_ = ''" + CRLF
_CQUERY += "    AND SF2.F2_TIPO IN ('D','B') " + CRLF
// --> (B) CORRECAO DO VALOR DOS IMPOSTOS RETIDOS NA EMISSAO DO RELATORIO.   (*FINAL* ) 

_CQUERY += "  GROUP BY F2_FILIAL , F2_SERIE, F2_DOC, F2_EMISSAO, A2_NOME, A2_END, A2_BAIRRO, A2_MUN, A2_EST, A2_CEP, A2_CGC, A2_INSCR, " + CRLF
_CQUERY += " 		  SE1NF.E1_VENCTO, F2_VALBRUT, SE1NF.E1_DESCONT, A2_END, A2_BAIRRO, A2_MUN, A2_EST, A2_CEP," + CRLF
_CQUERY += " 		  A2_END, A2_BAIRRO, A2_MUN, A2_EST, A2_CEP, A6_NOME, A6_DVAGE, A6_AGENCIA, A6_NUMCON, A6_DVCTA,SE1NF.E1_IRRF" + CRLF

_CQUERY += ") AS XTEMP1 " + CRLF
_CQUERY += "  ORDER BY EMISSAO, NUMFAT" + CRLF
// --> (A) CORRECAO DO VALOR DOS IMPOSTOS RETIDOS NA EMISSAO DO RELATORIO.   (*FINAL* ) 
TCQUERY _CQUERY NEW ALIAS "TRBFAT"

_CQUERY := " SELECT @@ROWCOUNT TOTAL "

IF SELECT("TOTCONT") > 0
	TOTCONT->(DBCLOSEAREA())
ENDIF

TCQUERY _CQUERY NEW ALIAS "TOTCONT"

IF TOTCONT->(!EOF())
	_NTOTAL := TOTCONT->TOTAL
ENDIF

TOTCONT->(DBCLOSEAREA())

PROCREGUA(_NTOTAL)

WHILE TRBFAT->(!EOF())

	INCPROC()

	OREPORT:STARTPAGE()

	_COBSFAT   := "" // removido da 94 ALLTRIM(POSICIONE("SC5",1,XFILIAL("SC5")+TRBFAT->PEDIDO,"C5_XOBSFAT"))

	_CTIPO	   := ""
	_CCONTRATO := ""
	_CEMPRESA  := ""
	_APRODUTOS := {}
	_DDTINI	   := STOD("")
	_DDTFIM	   := STOD("")

	NLIN := 25

	// --> MONTA AS CAIXAS

	// --> CABEÇALHO
	OREPORT:BOX( NLIN, NMINLEFT, NLIN+50, NMAXWIDTH,"-1" )
	NLIN +=  50 			// NLIN TOTAL:  75

	// --> INFORMAÇÕES DO PRESTADOR DO SERVIÇO
	OREPORT:BOX( NLIN, NMINLEFT, NLIN+90, NMEIO     )
	OREPORT:BOX( NLIN, NMEIO   , NLIN+90, NMAXWIDTH )
	NLIN += 105 			// NLIN TOTAL: 180

	// --> INFORMAÇÕES DO TOMADOR DO SERVIÇO
	OREPORT:BOX( NLIN, NMINLEFT, NLIN+90, NMAXWIDTH )
	NLIN += 102 			// NLIN TOTAL: 282

	// --> SERVIÇOS
	//OREPORT:BOX( NLIN, NMINLEFT, NLIN+410, NMAXWIDTH )
	NLIN += 420 			// NLIN TOTAL: 702
	
	// FRANK 27/10/20
	//OREPORT:BOX( NLIN, NMINLEFT, NLIN+85, NMINLEFT+85 )
	//OREPORT:BOX( NLIN, NMINLEFT+85, NLIN+85, NMAXWIDTH )
	// --> INICIA A IMPRESSÃO DAS INFORMAÇÕES
	NLIN :=  30

	OREPORT:FILLRECT( {NLIN, NMINLEFT+3, NLIN+10,NMAXWIDTH }, OBRUSH)
	NLIN +=  15 			// NLIN TOTAL: 45

	OREPORT:FILLRECT( {NLIN, NMINLEFT+3, NLIN+10,NMAXWIDTH }, OBRUSH)
	NLIN +=  15 			// NLIN TOTAL: 60

	OREPORT:FILLRECT( {NLIN, NMINLEFT+3, NLIN+10,NMAXWIDTH }, OBRUSH)
	NLIN :=  38
	
	OREPORT:SAYBITMAP(NLIN-8,NMINLEFT+3,CLOGO,0098,0040 )
	
	//OREPORT:SAY( NLIN, ((NMEIO+NMAXWIDTH)/2)-20, STR0005, OFONT3:OFONT ) //"FATURA"
	OREPORT:SAY( NLIN, NMEIO-(len(STR0005)/2), STR0005, OFONT3:OFONT ) //"FATURA"
	
	_CEMPRESA := ALLTRIM(SM0->M0_NOMECOM)
	XT  := MLCOUNT(_CEMPRESA,30)
	FOR I:=1 TO XT
		OREPORT:SAY( NLIN, NMINLEFT+103, MEMOLINE(_CEMPRESA ,30, I ), OFONT4:OFONT )
		NLIN +=  15
	NEXT I 

	NLIN :=  53 			// NLIN TOTAL: 53
	
	//OREPORT:SAY( NLIN, ((NMEIO+NMAXWIDTH)/2)-22, TRBFAT->NUMFAT, OFONT3:OFONT )
	OREPORT:SAY( NLIN, NMEIO- (len(alltrim(TRBFAT->NUMFAT))/2), alltrim(TRBFAT->NUMFAT), OFONT3:OFONT )
	NLIN +=  40 			// NLIN TOTAL: 93
	
	OREPORT:SAY( NLIN, NMINLEFT+10, STR0006, OFONT1:OFONT ) //"ENDEREÇO: "
 //	OREPORT:SAY( NLIN, NMINLEFT+50, ALLTRIM(SM0->M0_ENDCOB), OFONT2:OFONT )
	OREPORT:SAY( NLIN, NMINLEFT+60, substr(CEND,1,51 ), OFONT2:OFONT )
	

	OREPORT:SAY( NLIN, NMEIO+10, STR0007, OFONT1:OFONT ) //"NATUREZA OPERAÇÃO: "
	OREPORT:SAY( NLIN, NMEIO+108, ALLTRIM(TRBFAT->F4_TEXTO), OFONT2:OFONT )
	NLIN +=  15 			// NLIN TOTAL: 108
	
	OREPORT:SAY( NLIN, NMINLEFT+10, STR0008, OFONT1:OFONT ) //"BAIRRO: "
 //	OREPORT:SAY( NLIN, NMINLEFT+50, ALLTRIM(SM0->M0_BAIRCOB), OFONT2:OFONT )
	OREPORT:SAY( NLIN, NMINLEFT+50, substr(CBAIRRO,1,20), OFONT2:OFONT )
	
	
	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2), STR0009, OFONT1:OFONT ) //"CIDADE: "
 //	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2)+30, ALLTRIM(SM0->M0_CIDCOB), OFONT2:OFONT )
	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2)+35, substr(CCIDADE,1,24), OFONT2:OFONT )
	NLIN +=  15 			// NLIN TOTAL: 123
	
 //	_CVAR := SUBSTR(SM0->M0_CEPCOB,1,5) + '-' + SUBSTR(SM0->M0_CEPCOB,6,3)
	OREPORT:SAY( NLIN, NMINLEFT+10, STR0010, OFONT1:OFONT ) //"CEP: "
 //	OREPORT:SAY( NLIN, NMINLEFT+50, _CVAR, OFONT2:OFONT )
	OREPORT:SAY( NLIN, NMINLEFT+50, CCEP, OFONT2:OFONT )
	
	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2), STR0011, OFONT1:OFONT ) //"UF: "
 //	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2)+30, ALLTRIM(SM0->M0_ESTCOB), OFONT2:OFONT )
	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2)+30, CUF, OFONT2:OFONT )
	NLIN +=  15 			// NLIN TOTAL: 138
	
 //	_CVAR := "(" + SUBSTR(SM0->M0_TEL,4,2) + ") " + SUBSTR(SM0->M0_TEL,7,4) + "-" + SUBSTR(SM0->M0_TEL,11,4)
	OREPORT:SAY( NLIN, NMINLEFT+10, STR0012, OFONT1:OFONT ) //"FONE: "
 //	OREPORT:SAY( NLIN, NMINLEFT+50, _CVAR, OFONT2:OFONT )
	OREPORT:SAY( NLIN, NMINLEFT+50, CTEL, OFONT2:OFONT )

	/*
	IF EMPTY(ALLTRIM(SM0->M0_FAX))
		_CVAR := ""
	ELSE
		_CVAR := "(" + SUBSTR(SM0->M0_FAX,4,2) + ") " + SUBSTR(SM0->M0_FAX,7,4) + "-" + SUBSTR(SM0->M0_FAX,11,4)
	ENDIF
	*/
	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2), STR0013, OFONT1:OFONT ) //"FAX: "
 //	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2)+30, _CVAR, OFONT2:OFONT )
	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2)+30, CFAX, OFONT2:OFONT )
	
	OREPORT:SAY( NLIN, NMEIO+10, STR0014, OFONT1:OFONT ) //"DATA EMISSÃO: "
	OREPORT:SAY( NLIN, NMEIO+80, DTOC(STOD(TRBFAT->EMISSAO)), OFONT2:OFONT )
	NLIN +=  15 			// NLIN TOTAL: 153

	/*
	_CVAR := SUBSTR(SM0->M0_CGC,1,2) + "." + SUBSTR(SM0->M0_CGC,3,3) + "." + SUBSTR(SM0->M0_CGC,6,3) + "/" +;
			 SUBSTR(SM0->M0_CGC,9,4) + "-" + SUBSTR(SM0->M0_CGC,13,2)
	*/
	OREPORT:SAY( NLIN, NMINLEFT+10, STR0015, OFONT1:OFONT ) //"CNPJ: "
 //	OREPORT:SAY( NLIN, NMINLEFT+50, _CVAR, OFONT2:OFONT )
	OREPORT:SAY( NLIN, NMINLEFT+50, CCNPJ, OFONT2:OFONT )
	
	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2), STR0016, OFONT1:OFONT ) //"CCM: "
	OREPORT:SAY( NLIN, ((NMINLEFT+NMEIO)/2)+30, ALLTRIM(SM0->M0_INSCM), OFONT2:OFONT )
	
	OREPORT:SAY( NLIN, NMEIO+10, STR0017, OFONT1:OFONT ) //"NÚMERO PEDIDO: "
	OREPORT:SAY( NLIN, NMEIO+80, TRBFAT->PEDIDO, OFONT2:OFONT )
	NLIN +=  25 			// NLIN TOTAL: 178
	
	OREPORT:FILLRECT( {NLIN-10, 0010, NLIN,NMAXWIDTH }, OBRUSH)	
	OREPORT:SAY( NLIN-1, NMEIO-30, STR0018, OFONT3:OFONT ) //"DESTINATÁRIO"
	NLIN +=  18 			// NLIN TOTAL: 196
	
	OREPORT:SAY( NLIN, NMINLEFT+10, STR0019, OFONT1:OFONT ) //"RAZÃO SOCIAL: "
	OREPORT:SAY( NLIN, NMINLEFT+75, ALLTRIM(TRBFAT->RAZSOC), OFONT2:OFONT )
	NLIN +=  15 			// NLIN TOTAL: 211
	
	OREPORT:SAY( NLIN, NMINLEFT+10, STR0006, OFONT1:OFONT ) //"ENDEREÇO: "
	OREPORT:SAY( NLIN, NMINLEFT+60, ALLTRIM(TRBFAT->A1_END), OFONT2:OFONT )
	NLIN +=  15 			// NLIN TOTAL: 226
	
	OREPORT:SAY( NLIN, NMINLEFT+10, STR0008, OFONT1:OFONT ) //"BAIRRO: "
	OREPORT:SAY( NLIN, NMINLEFT+50, substr(TRBFAT->A1_BAIRRO,1,35), OFONT2:OFONT )
	
	OREPORT:SAY( NLIN, NMEIO-80, STR0020, OFONT1:OFONT ) //"MUNICÍPIO: "
	OREPORT:SAY( NLIN, NMEIO-26, substr(TRBFAT->A1_MUN,1,38), OFONT2:OFONT )
	
	OREPORT:SAY( NLIN, ((NMEIO+NMAXWIDTH)/2), STR0011, OFONT1:OFONT ) //"UF: "
	OREPORT:SAY( NLIN, ((NMEIO+NMAXWIDTH)/2)+20, ALLTRIM(TRBFAT->A1_EST), OFONT2:OFONT )
	
	OREPORT:SAY( NLIN, ((NMEIO+NMAXWIDTH)/2)+60, STR0010, OFONT1:OFONT ) //"CEP: "
	OREPORT:SAY( NLIN, ((NMEIO+NMAXWIDTH)/2)+80, ALLTRIM(TRBFAT->A1_CEP), OFONT2:OFONT )
	NLIN +=  15 			// NLIN TOTAL: 241

	OREPORT:SAY( NLIN, NMINLEFT+10, STR0015, OFONT1:OFONT ) //"CNPJ: "
	OREPORT:SAY( NLIN, NMINLEFT+60, ALLTRIM(TRBFAT->A1_CNPJ), OFONT2:OFONT )
	
	OREPORT:SAY( NLIN, NMEIO-80, STR0021, OFONT1:OFONT ) //"INSCRIÇÃO ESTADUAL: "
	OREPORT:SAY( NLIN, NMEIO+15, ALLTRIM(TRBFAT->A1_INSCR), OFONT2:OFONT )
	NLIN +=  15 			// NLIN TOTAL: 256
	
	OREPORT:SAY( NLIN, NMINLEFT+10, STR0022, OFONT1:OFONT ) //"VENCTO: "
	OREPORT:SAY( NLIN, NMINLEFT+60, DTOC(STOD(TRBFAT->VENCTO)), OFONT2:OFONT )
 //	OREPORT:SAY( NLIN, NMINLEFT+100, TRANSFORM(TRBFAT->F2_VALBRUT,"@E 9,999,999,999.99"), OFONT2:OFONT ) COMENTADO POR GUILHERME CORONADO
	NLIN +=  25 			// NLIN TOTAL: 281
	
	IF SELECT("TRBPRD") > 0
		TRBPRD->(DBCLOSEAREA())
	ENDIF
	_CQUERY := " SELECT CASE WHEN MAX(ISNULL(FPN_COD,'')) = ''" + CRLF
	_CQUERY += " 			THEN CASE WHEN FP0_MINPFT = '1'" + CRLF
	_CQUERY += " 					  THEN '2'" + CRLF
	_CQUERY += " 					  ELSE '1' END" + CRLF
	_CQUERY += " 			ELSE '3' END TIPO," + CRLF
	_CQUERY += " 	   C6_ITEM, " + CRLF
	_CQUERY += " 	   CASE WHEN ISNULL(FPA_DESGRU,'') = ''" + CRLF
	_CQUERY += " 			THEN ''" + CRLF
	_CQUERY += " 			ELSE RTRIM(LTRIM(ISNULL(FPA_DESGRU,''))) + ' / ' END + " + CRLF
	_CQUERY += " 	   CASE WHEN ISNULL(FPA_GRUA,'') = ''" + CRLF
	_CQUERY += " 			THEN ''" + CRLF
	_CQUERY += " 			ELSE RTRIM(LTRIM(ISNULL(FPA_GRUA,''))) + ' / ' END +" + CRLF
	_CQUERY += " 	   CASE WHEN ISNULL(FPA_CARAC,'') = ''" + CRLF
	_CQUERY += " 			THEN ''" + CRLF
	_CQUERY += " 			ELSE RTRIM(LTRIM(ISNULL(FPA_CARAC,''))) END PRODUTO," + CRLF
	_CQUERY += " 	   C6_QTDVEN, C6_PRCVEN, C6_VALOR," + CRLF
	_CQUERY += " 	   CASE WHEN MAX(ISNULL(FPN_COD,'')) = '' AND MAX(ISNULL(FPA_DTINI,'')) = ''" + CRLF
	_CQUERY += " 			THEN MIN(ISNULL(FPA_DTINI,''))" + CRLF
	_CQUERY += " 			ELSE MIN(ISNULL(FPN_DTINIC,'')) END DTINI," + CRLF
	_CQUERY += " 	   CASE WHEN MAX(ISNULL(FPN_COD,'')) = '' AND MAX(ISNULL(FPA_DTFIM,'')) = ''" + CRLF
	_CQUERY += " 			THEN MAX(ISNULL(FPA_DTFIM,''))" + CRLF
	_CQUERY += " 			ELSE MAX(ISNULL(FPN_DTFIM,'')) END DTFIM, ISNULL(FP0_PROJET,'') CONTRATO" + CRLF
	_CQUERY += "   FROM " + RETSQLNAME("SC5") + " SC5 INNER JOIN " + RETSQLNAME("SC6") + " SC6 (NOLOCK)" + CRLF
	_CQUERY += "     ON C5_FILIAL = C6_FILIAL" + CRLF
	_CQUERY += "    AND C5_NUM    = C6_NUM" + CRLF
	_CQUERY += "    AND SC6.D_E_L_E_T_ = ''" + CRLF
	_CQUERY += "        LEFT JOIN " + RETSQLNAME("FPN") + " ZLF (NOLOCK)" + CRLF
	_CQUERY += "     ON C5_FILIAL = FPN_FILIAL" + CRLF
	_CQUERY += "    AND C5_NUM    = FPN_NUMPV" + CRLF
	_CQUERY += "    AND ZLF.D_E_L_E_T_ = ''" + CRLF
	_CQUERY += "        LEFT JOIN " + RETSQLNAME("FPA") + " ZAG (NOLOCK)" + CRLF
	_CQUERY += "     ON C6_FILIAL = FPA_FILIAL" + CRLF
	_CQUERY += "    AND C6_XAS    = FPA_AS" + CRLF
	_CQUERY += "    AND C6_XAS   <> ''" + CRLF
	_CQUERY += "    AND ZAG.D_E_L_E_T_ = ''" + CRLF
	_CQUERY += "        LEFT JOIN " + RETSQLNAME("FP0") + " ZA0 (NOLOCK)" + CRLF
	_CQUERY += "     ON FPA_FILIAL = FP0_FILIAL" + CRLF
	_CQUERY += "    AND FPA_PROJET = FP0_PROJET" + CRLF
	_CQUERY += "    AND ZA0.D_E_L_E_T_ = ''" + CRLF
	_CQUERY += "  WHERE C5_FILIAL = '" + XFILIAL("SC5") + "'" + CRLF
	_CQUERY += "    AND C6_NOTA   = '" + TRBFAT->NUMFAT + "'" + CRLF
	_CQUERY += "    AND C6_SERIE  = '" + MV_PAR03 + "'" + CRLF
	_CQUERY += "    AND SC5.D_E_L_E_T_ = ''" + CRLF
	_CQUERY += "  GROUP BY FP0_MINPFT, C6_ITEM, C6_QTDVEN,FPA_DESGRU, FPA_GRUA, FPA_CARAC, C6_PRCVEN, C6_VALOR, FP0_PROJET" + CRLF
	_CQUERY += "  ORDER BY C6_ITEM"
	TCQUERY _CQUERY NEW ALIAS "TRBPRD"
	
	WHILE TRBPRD->(!EOF())
		IF !EMPTY(ALLTRIM(TRBPRD->DTINI))
			IF EMPTY(_DDTINI) .OR. STOD(TRBPRD->DTINI) < _DDTINI
				_DDTINI := STOD(TRBPRD->DTINI)
			ENDIF
		ENDIF
		
		IF !EMPTY(ALLTRIM(TRBPRD->DTFIM))
			IF STOD(TRBPRD->DTFIM) > _DDTFIM
				_DDTFIM := STOD(TRBPRD->DTFIM)
			ENDIF
		ENDIF
		
		_CTIPO     := TRBPRD->TIPO
		_CCONTRATO := ALLTRIM(TRBPRD->CONTRATO)
		_CPRODUTO  := ALLTRIM(TRBPRD->PRODUTO)
		XT  := MLCOUNT(_CPRODUTO,65)
		/*
		FOR I:=1 TO XT
			IF I = 1
				//AADD(_APRODUTOS,{CVALTOCHAR(TRBPRD->C6_QTDVEN),MEMOLINE(_CPRODUTO ,65, I ),DTOC(STOD(TRBPRD->DTINI)) + " A " + DTOC(STOD(TRBPRD->DTFIM)),TRANSFORM(TRBPRD->C6_PRCVEN,"@E 9,999,999,999.99"),TRANSFORM(TRBPRD->C6_VALOR,"@E 9,999,999,999.99")})
				AADD(_APRODUTOS,{TRBPRD->C6_ITEM,MEMOLINE(_CPRODUTO ,65, I ),CVALTOCHAR(TRBPRD->C6_QTDVEN),TRANSFORM(TRBPRD->C6_PRCVEN,"@E 9,999,999,999.99"),TRANSFORM(TRBPRD->C6_VALOR,"@E 9,999,999,999.99")})
			ELSE
				AADD(_APRODUTOS,{"",MEMOLINE(_CPRODUTO ,65, I ),"","",""})
			ENDIF
		NEXT I
		*/

		// IDENTIFICACAO DOS ITENS - FRANK Z FUGA - 27/10/20
		SC6->(DBSETORDER(13))
		SC6->(DBSEEK(MV_PAR04+TRBFAT->NUMFAT))
		_APRODUTOS := {}
		WHILE !SC6->(EOF()) .AND. SC6->C6_NOTA == TRBFAT->NUMFAT .AND. SC6->C6_FILIAL == MV_PAR04
			IF SC6->C6_SERIE == MV_PAR03
				SB1->(DBSETORDER(1))
				SB1->(DBSEEK(XFILIAL("SB1")+SC6->C6_PRODUTO))
				//AADD(_APRODUTOS,{SC6->C6_ITEM,SB1->B1_DESC,CVALTOCHAR(SC6->C6_QTDVEN),TRANSFORM(SC6->C6_PRCVEN,"@E 9,999,999,999.99"),TRANSFORM(SC6->C6_VALOR,"@E 9,999,999,999.99")})
				FPA->(DBSETORDER(3))
				FPA->(DBSEEK(XFILIAL("FPA")+SC6->C6_XAS))
				AADD(_APRODUTOS,{SC6->C6_ITEM,SB1->B1_DESC,CVALTOCHAR(SC6->C6_QTDVEN),TRANSFORM(SC6->C6_PRCVEN,"@E 9,999,999,999.99"),TRANSFORM(SC6->C6_VALOR,"@E 9,999,999,999.99"),SC6->C6_XPERLOC    })
			ENDIF
			SC6->(DBSKIP())
		ENDDO 
		
		TRBPRD->(DBSKIP())
	ENDDO
	
	TRBPRD->(DBCLOSEAREA())
	
	OREPORT:FILLRECT( {NLIN-10, 0010, NLIN,NMAXWIDTH }, OBRUSH)
	
	IF _CTIPO == "2"
		OREPORT:SAY( NLIN-2, NMINLEFT+10, STR0023, OFONT1:OFONT ) //"ITEM"
		OREPORT:SAY( NLIN-2, NMINLEFT+40, STR0024, OFONT1:OFONT )	 //"DESCRIMINAÇÃO/ESPECIFICAÇÃO"

		OREPORT:SAY( NLIN-2, NMEIO-10, STR0025, OFONT1:OFONT ) //"PERÍODO"

		OREPORT:SAY( NLIN-2, NMEIO+94, STR0026, OFONT1:OFONT ) //"QTD."
		OREPORT:SAY( NLIN-2,((NMEIO+NMAXWIDTH)/2)-20, STR0027, OFONT1:OFONT ) //"PREÇO UNITÁRIO"
		OREPORT:SAY( NLIN-2,((NMEIO+NMAXWIDTH)/2)+90, STR0028, OFONT1:OFONT ) //"PREÇO TOTAL"
	ELSE
		OREPORT:SAY( NLIN-2, NMINLEFT+10, STR0024, OFONT1:OFONT )	 //"DESCRIMINAÇÃO/ESPECIFICAÇÃO"
	ENDIF
	NLIN +=  15 		// 296

	
	DO CASE
	CASE _CTIPO == "1"
		IF EMPTY(ALLTRIM(DTOS(_DDTINI))) .OR. EMPTY(ALLTRIM(DTOS(_DDTFIM)))
			OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0029 + ALLTRIM(POSICIONE("SC6",1,XFILIAL("SC6")+TRBFAT->PEDIDO,"C6_XPERLOC")), OFONT2:OFONT ) //"LOCAÇÃO DE EQUIPAMENTOS - "
		ELSE
			OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0029 + DTOC(_DDTINI) + STR0030 + DTOC(_DDTFIM), OFONT2:OFONT ) //"LOCAÇÃO DE EQUIPAMENTOS - "###" A "
		ENDIF
		NLIN += 10 		// 311
	CASE _CTIPO == "2"
		FOR I := 1 TO LEN(_APRODUTOS)
			OREPORT:SAY( NLIN-1, NMINLEFT+10, _APRODUTOS[I,1], OFONT2:OFONT )
			OREPORT:SAY( NLIN-1, NMINLEFT+40, _APRODUTOS[I,2], OFONT2:OFONT )	

			OREPORT:SAY( NLIN-1, NMEIO-10, _APRODUTOS[I,6], OFONT2:OFONT )

			OREPORT:SAY( NLIN-1, NMEIO+94, _APRODUTOS[I,3], OFONT2:OFONT )
			OREPORT:SAY( NLIN-1,((NMEIO+NMAXWIDTH)/2)-23, _APRODUTOS[I,4], OFONT2:OFONT )
			OREPORT:SAY( NLIN-1,((NMEIO+NMAXWIDTH)/2)+74, _APRODUTOS[I,5], OFONT2:OFONT )
			NLIN += 10 	// 311
			IF NLIN > 800
				OREPORT:ENDPAGE()
				OREPORT:STARTPAGE()
				NLIN := 30
			ENDIF
		NEXT

	CASE _CTIPO == "3"
		OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0031, OFONT2:OFONT ) //"COMPLEMENTO DE LOCAÇÃO"
		NLIN +=  10 	// 311
	ENDCASE

	//IF NLIN < 455
	//	NLIN := 465
	//ELSE
		NLIN +=  20
	//ENDIF
	
	XT  := MLCOUNT(ALLTRIM(_COBSFAT),90)
	FOR I:=1 TO XT
		OREPORT:SAY( NLIN-1, NMINLEFT+10, MEMOLINE(_COBSFAT ,90, I ), OFONT2:OFONT )
		NLIN +=  10
	NEXT
	
	//NLIN := 505 		// 505
	
	IF _CTIPO == "1" .AND. !EMPTY(_CCONTRATO)
		OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0032 + _CCONTRATO, OFONT2:OFONT )	 //"CONTRATO: "
	ENDIF
	NLIN +=  15 		// 520
	
	IF _CTIPO <> "3" .AND. !EMPTY(ALLTRIM(TRBFAT->BANCO))
		OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0033 + ALLTRIM(TRBFAT->BANCO) + " AG " + ALLTRIM(TRBFAT->AGENCIA) + " CC " + ALLTRIM(TRBFAT->CONTA), OFONT2:OFONT )		 //"PAGAMENTO ATRAVÉS DE DEPÓSITO BANCÁRIO "
	ENDIF
	NLIN +=  15 		// 535
	
	OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0034, OFONT1:OFONT ) //'"OPERAÇÃO NÃO SUJEITA A EMISSÃO DE NOTA FISCAL DE SERVIÇOS-VETADA A COBRANÇA DE ISS CONF.'
	//OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+20, 'OPERAÇÃO NÃO SUJEITA A EMISSÃO DE NOTA FISCAL DE SERVIÇOS-VETADA A COBRANÇA DE ISS CONF.', OFONT1:OFONT )
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+82, TRANSFORM(TRBFAT->F2_VALBRUT,"@E 9,999,999,999.99"), OFONT2:OFONT )
	NLIN +=  15 		// 550
	
	OREPORT:SAY( NLIN-6, NMINLEFT+10, 'LEI COMPLEMENTAR 116/03', OFONT1:OFONT )
		//OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2), STR0035, OFONT1:OFONT ) //"IMPOSTOS RETIDOS:"
		OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2), "IMPOSTOS RETIDOS:", OFONT1:OFONT )
		// --> CORRECAO DO VALOR DOS IMPOSTOS RETIDOS NA EMISSAO DO RELATORIO   (*INICIO*) 
	 //	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+90, TRANSFORM(TRBFAT->VALRET+TRBFAT->IRFAT,"@E 9,999,999,999.99"), OFONT2:OFONT )		
		OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+82, TRANSFORM(TRBFAT->VALRET2,"@E 9,999,999,999.99"), OFONT2:OFONT )
		// --> CORRECAO DO VALOR DOS IMPOSTOS RETIDOS NA EMISSAO DO RELATORIO   (*FINAL* ) 
	NLIN +=  15 		// 565
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+35, STR0036, OFONT1:OFONT ) //"DESCONTO:"
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+35, "DESCONTO:", OFONT1:OFONT )
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+82, TRANSFORM(TRBFAT->E1_DESCONT,"@E 9,999,999,999.99"), OFONT2:OFONT )
	NLIN +=  15 		// 580
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+50, STR0037, OFONT1:OFONT )  //"TOTAL:"
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+50, "TOTAL:", OFONT1:OFONT ) 
	// --> CORRECAO DO VALOR DOS IMPOSTOS RETIDOS NA EMISSAO DO RELATORIO   (*INICIO*) 
 //	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+90, TRANSFORM(TRBFAT->TOTAL,"@E 9,999,999,999.99"), OFONT2:OFONT )
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+82, TRANSFORM(TRBFAT->F2_VALBRUT - TRBFAT->E1_DESCONT - TRBFAT->VALRET2,"@E 9,999,999,999.99"), OFONT2:OFONT )
	// --> CORRECAO DO VALOR DOS IMPOSTOS RETIDOS NA EMISSAO DO RELATORIO   (*FINAL* ) 
	NLIN +=  30 		// 610
	OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0038, OFONT1:OFONT ) //"ENDEREÇO COBRANÇA"
	OREPORT:SAY( NLIN-1, NMINLEFT+10, "ENDEREÇO COBRANÇA", OFONT1:OFONT )
	NLIN +=  15 		// 625
	OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0006 + ALLTRIM(TRBFAT->A1_ENDCOB) + " / " + TRBFAT->A1_BAIRROC, OFONT2:OFONT ) //"ENDEREÇO: "
	OREPORT:SAY( NLIN-1, NMINLEFT+10, "ENDEREÇO: " + ALLTRIM(TRBFAT->A1_ENDCOB) + " / " + TRBFAT->A1_BAIRROC, OFONT2:OFONT )
	NLIN +=  15 		// 640
	OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0009 + ALLTRIM(TRBFAT->A1_MUNC) + "/" + TRBFAT->A1_ESTC + STR0039 + TRBFAT->A1_CEPC, OFONT2:OFONT ) //"CIDADE: "###"  CEP: "
	OREPORT:SAY( NLIN-1, NMINLEFT+10, "CIDADE: " + ALLTRIM(TRBFAT->A1_MUNC) + "/" + TRBFAT->A1_ESTC + "  CEP: " + TRBFAT->A1_CEPC, OFONT2:OFONT )
	NLIN +=  15 		// 655
	OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0040, OFONT1:OFONT ) //"ENDEREÇO ENTREGA"
	OREPORT:SAY( NLIN-1, NMINLEFT+10, "ENDEREÇO ENTREGA", OFONT1:OFONT )
	NLIN +=  15 		// 670
	OREPORT:SAY( NLIN-1, NMINLEFT+10, STR0006 + ALLTRIM(TRBFAT->A1_ENDENT) + " / " + ALLTRIM(TRBFAT->A1_BAIRROE) + ", " +ALLTRIM(TRBFAT->A1_MUNE) + ", " + TRBFAT->A1_ESTE + ", " + TRBFAT->A1_CEPE, OFONT2:OFONT ) //"ENDEREÇO: "
	OREPORT:SAY( NLIN-1, NMINLEFT+10, "ENDEREÇO: " + ALLTRIM(TRBFAT->A1_ENDENT) + " / " + ALLTRIM(TRBFAT->A1_BAIRROE) + ", " +ALLTRIM(TRBFAT->A1_MUNE) + ", " + TRBFAT->A1_ESTE + ", " + TRBFAT->A1_CEPE, OFONT2:OFONT )
	NLIN +=  15 		// 685
	
 //	OREPORT:SAY( NLIN-1, NMINLEFT+10, "NUM. PEDIDO DO CLIENTE", OFONT1:OFONT )
	//NLIN := 725 		// 725
	OREPORT:SAY( NLIN-1, NMINLEFT+20, STR0041, OFONT2:OFONT ) //"Nº FATURA"
	OREPORT:SAY( NLIN-1, NMINLEFT+100, STR0042 + ALLTRIM(SM0->M0_NOMECOM) + STR0043, OFONT2:OFONT ) //"RECEBI(EMOS) DE "###", OS SERVIÇOS CONSTANTES"
	OREPORT:SAY( NLIN-1, NMINLEFT+100, "RECEBI(EMOS) DE " + ALLTRIM(SM0->M0_NOMECOM) + ", OS SERVIÇOS CONSTANTES", OFONT2:OFONT )
	NLIN +=  30 		// 755

	OREPORT:SAY( NLIN-1, NMINLEFT+100, ALLTRIM(SM0->M0_CIDCOB) + " " + REPLICATE("_",7) + STR0044 + REPLICATE("_",30) + STR0045 + REPLICATE("_",10), OFONT2:OFONT ) //" , DE "###", DE "
	OREPORT:SAY( NLIN-1, NMINLEFT+100, ALLTRIM(SM0->M0_CIDCOB) + " " + REPLICATE("_",7) + " , DE " + REPLICATE("_",30) + ", DE " + REPLICATE("_",10), OFONT2:OFONT )
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)-15, REPLICATE("_",40), OFONT2:OFONT )
	NLIN +=  15 		// 770
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+30, STR0046, OFONT2:OFONT ) //"ASSINATURA"
	OREPORT:SAY( NLIN-1, ((NMEIO+NMAXWIDTH)/2)+30, "ASSINATURA", OFONT2:OFONT )

	OREPORT:ENDPAGE()

	TRBFAT->(DBSKIP())
ENDDO

TRBFAT->(DBCLOSEAREA())

OREPORT:SETVIEWPDF( .T. )
OREPORT:PREVIEW()
FREEOBJ(OREPORT)
OREPORT := NIL

RETURN



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPROGRAMA  ³ PERGPARAM º AUTOR ³ IT UP CONSULTORIA  º DATA ³ 03/08/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDESCRICAO ³ PERGUNTA DO RELATÓRIO.                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/ 
STATIC FUNCTION PERGPARAM(CPERG)
LOCAL APERGS  := {}
LOCAL _CNOTAI := IIF(FIELDPOS("F2_DOC")>0,SPACE(GETSX3CACHE("F2_DOC","X3_TAMANHO")),SPACE(09))
LOCAL _CNOTAF := IIF(FIELDPOS("F2_DOC")>0,REPLICATE("Z",GETSX3CACHE("F2_DOC","X3_TAMANHO")),REPLICATE("Z",09))
LOCAL _CSERIE := IIF(FIELDPOS("F2_SERIE")>0,SPACE(GETSX3CACHE("F2_SERIE","X3_TAMANHO")),SPACE(03))
LOCAL _CFILIAL:= SPACE(06)
LOCAL ARET    := {}
LOCAL LRET    := .F.
AADD( APERGS ,{1,STR0047 ,_CNOTAI ,"@!",".T.","SF2"  ,".T.", 50,.F.}) //"NOTA FISCAL DE: "
AADD( APERGS ,{1,STR0048,_CNOTAF ,"@!",".T.","SF2"  ,".T.", 50,.F.}) //"NOTA FISCAL ATÉ: "
AADD( APERGS ,{1,STR0049          ,_CSERIE ,"@!",".T.","SERNF",".T.", 50,.F.}) //"SÉRIE: "
AADD( APERGS ,{1,STR0050         ,_CFILIAL,"@!",".T.","SM0"  ,".T.", 50,.F.}) //"FILIAL: "
//AADD( APERGS ,{1,STR0050         ,_CFILIAL,"@!",".T.","SM0"  ,".T.", 50,.F.})
IF PARAMBOX(APERGS , STR0051 , ARET , /*4*/ , /*5*/ , /*6*/ , /*7*/ , /*8*/ , /*9*/ , /*10*/ , .F.)  //"PARAMETROS "
	MV_PAR01 := ARET[1] 	// NOTA FISCAL INICIAL
	MV_PAR02 := ARET[2] 	// NOTA FISCAL FINAL
	MV_PAR03 := ARET[3] 	// SÉRIE
	MV_PAR04 := ARET[4] 	// FILIAL
	LRET := .T.
ENDIF

RETURN (LRET)
