/*/{PROTHEUS.DOC} LOCA014.PRW 
ITUP BUSINESS - TOTVS RENTAL
RETORNA INFORMAÇÕES PARA INICIALIZADOR PADRÃO DE CAMPOS VIRTUAIS NO CADASTRO DE CLIENTES
@TYPE FUNCTION
@AUTHOR FRANK ZWARG FUGA
@SINCE 03/12/2020
@VERSION P12
@HISTORY 03/12/2020, FRANK ZWARG FUGA, FONTE PRODUTIZADO.
/*/

#INCLUDE "TOTVS.CH"

FUNCTION LOCA014(NINFO, CCLIENTE, CLOJA)
LOCAL   XRET     := ""

DEFAULT CCLIENTE := SA1->A1_COD
DEFAULT CLOJA    := SA1->A1_LOJA

DO CASE
CASE NINFO == 1
	XRET := ULTPRJFAT(NINFO, CCLIENTE, CLOJA)
CASE NINFO == 2
	XRET := ULTPRJFAT(NINFO, CCLIENTE, CLOJA)
CASE NINFO == 3
	XRET := ULTPRJVEND(NINFO, CCLIENTE, CLOJA)
CASE NINFO == 4
	XRET := ULTPRJVEND(NINFO, CCLIENTE, CLOJA)
CASE NINFO == 5
	XRET := ULTDESPVIS(NINFO, CCLIENTE, CLOJA)
CASE NINFO == 6
	XRET := ULTDESPVIS(NINFO, CCLIENTE, CLOJA)
CASE NINFO == 7
	XRET := ULTPRJVEND(NINFO, CCLIENTE, CLOJA)
ENDCASE

RETURN XRET



// ======================================================================= \\
STATIC FUNCTION ULTPRJFAT(NINFO , CCLIENTE , CLOJA)	
// ======================================================================= \\
// --> ULTIMO PROJETO FATURADO

LOCAL AAREA     := GETAREA()
LOCAL CQUERY
LOCAL CALIASQRY := GETNEXTALIAS()
LOCAL XRET      := IIF(NINFO==1, "", 0)

CQUERY := "SELECT TOP 1 F2_EMISSAO, F2_VALMERC, F2_DOC, F2_SERIE, ISNULL(FP4_PROJET,ZA0_PROJET) PROJETO"
CQUERY += " FROM " + RETSQLNAME("SF2") + " SF2"
CQUERY += " JOIN " + RETSQLNAME("SF4") + " SF4 ON F4_FILIAL='"+XFILIAL("SF4")+"' AND F4_DUPLIC='S' AND SF4.D_E_L_E_T_=' '"

CQUERY += " LEFT JOIN ("
CQUERY += 		" SELECT FQ5_AS, FQ5_NUMCTR, FQ5_SERCTR, FP4_PROJET FROM " + RETSQLNAME("FQ5") + " DTQ"
CQUERY +=		" JOIN " + RETSQLNAME("FP4") + " ZA5 ON FP4_FILIAL='"+XFILIAL("FP4")+"' AND FP4_AS=FQ5_AS AND FP4_VIAGEM=FQ5_VIAGEM"
CQUERY += 		" JOIN " + RETSQLNAME("FP0") + " ZA0 ON ZA0_FILIAL='"+XFILIAL("FP0")+"' AND ZA0_PROJET=FP4_PROJET"
CQUERY += 		" WHERE FQ5_FILIAL='"+XFILIAL("FQ5")+"'"
CQUERY += 		" AND ZA0_STATUS='8'"
CQUERY += 		" AND DTQ.D_E_L_E_T_ = ' '"
CQUERY += 		" AND ZA5.D_E_L_E_T_ = ' '"
CQUERY += 		" AND ZA0.D_E_L_E_T_ = ' '"
CQUERY += 		" ) DTQQ ON FQ5_NUMCTR=F2_DOC AND FQ5_SERCTR=F2_SERIE"
 
CQUERY += " LEFT JOIN ("
CQUERY += 		" SELECT FPN_PROJET, FPN_CLIENT, FPN_LOJA, D2_DOC, D2_SERIE, ZA0_PROJET FROM " + RETSQLNAME("SD2") + " SD2"
CQUERY += 		" JOIN " + RETSQLNAME("FPN") + " ZLF ON FPN_FILIAL='"+XFILIAL("FPN")+"' AND D2_PEDIDO=FPN_NUMPV"
CQUERY += 		" JOIN " + RETSQLNAME("FP0") + " ZA0 ON ZA0_FILIAL='"+XFILIAL("FP0")+"' AND ZA0_PROJET=FPN_PROJET"
CQUERY += 		" WHERE D2_FILIAL='"+XFILIAL("SD2")+"'"
CQUERY += 		" AND ZA0_STATUS='8'"
CQUERY += 		" AND SD2.D_E_L_E_T_=' '"
CQUERY += 		" AND ZLF.D_E_L_E_T_=' '"
CQUERY += 		" AND ZA0.D_E_L_E_T_ = ' '"
CQUERY += 		" ) ZLFQ ON D2_DOC=F2_DOC AND D2_SERIE=F2_SERIE AND FPN_CLIENT=F2_CLIENTE AND FPN_LOJA=F2_LOJA"

CQUERY += " WHERE F2_FILIAL='"+XFILIAL("SF2")+"'"
CQUERY += " AND F2_CLIENTE = '" + CCLIENTE + "'"
CQUERY += " AND F2_LOJA = '" + CLOJA + "'"
CQUERY += " AND SF2.D_E_L_E_T_ = ' '"
CQUERY += " AND ISNULL(ZA0_PROJET,FP4_PROJET) IS NOT NULL"

CQUERY += " ORDER BY F2_EMISSAO DESC"
DBUSEAREA( .T., "TOPCONN", TCGENQRY(,,CQUERY), CALIASQRY, .F., .T. )

IF ! (CALIASQRY)->( EOF() )
	XRET := IIF(NINFO==1, (CALIASQRY)->PROJETO, (CALIASQRY)->F2_VALMERC)
ENDIF

IF SELECT(CALIASQRY) > 0
	(CALIASQRY)->( DBCLOSEAREA() )
ENDIF

RESTAREA( AAREA )

RETURN XRET



// ======================================================================= \\
STATIC FUNCTION ULTPRJVEND(NINFO, CCLIENTE, CLOJA)	
// ======================================================================= \\
// --> ULTIMO PROJETO VENDIDO
LOCAL AAREA     := GETAREA()
LOCAL CQUERY
LOCAL CALIASQRY := GETNEXTALIAS()
LOCAL XRET		:= IIF(NINFO==3, "", IIF(NINFO==4,0, CTOD("  /  /  ")))

CQUERY := " SELECT ZA0_DATINC, FP4_PROJET, SUM(FP4_VALAS) VALOR "
CQUERY += " FROM " + RETSQLNAME("FP0") + " ZA0"
CQUERY +=        " JOIN " + RETSQLNAME("FP4") + " ZA5 ON FP4_FILIAL='"+XFILIAL("FP4")+"' AND ZA0_PROJET=FP4_PROJET"
CQUERY += " WHERE ZA0_FILIAL='"+XFILIAL("FP0")+"'"
CQUERY +=   " AND ZA0_CLI = '"+CCLIENTE+"'"
CQUERY +=   " AND ZA0_LOJA = '"+CLOJA+"'"
CQUERY +=   " AND EXISTS (SELECT 1 FROM " + RETSQLNAME("FQ5") + " DTQ WHERE FQ5_FILIAL='"+XFILIAL("FQ5")+"' AND FQ5_AS=FP4_AS AND FQ5_VIAGEM=FP4_VIAGEM AND DTQ.D_E_L_E_T_=' ')"
CQUERY +=   " AND ZA5.D_E_L_E_T_ = ' '"
CQUERY +=   " AND ZA0.D_E_L_E_T_ = ' '"
CQUERY += " GROUP BY FP4_PROJET, ZA0_DATINC"
CQUERY += " ORDER BY ZA0_DATINC DESC"

DBUSEAREA( .T., "TOPCONN", TCGENQRY(,,CQUERY), CALIASQRY, .F., .T. )

IF ! (CALIASQRY)->( EOF() )
	XRET := IIF(NINFO==3, (CALIASQRY)->FP4_PROJET, IIF(NINFO==4, (CALIASQRY)->VALOR, STOD((CALIASQRY)->ZA0_DATINC)))
ENDIF

IF SELECT(CALIASQRY) > 0
	(CALIASQRY)->( DBCLOSEAREA() )
ENDIF

RESTAREA( AAREA )

RETURN XRET



// ======================================================================= \\
STATIC FUNCTION ULTDESPVIS(NINFO, CCLIENTE, CLOJA)	
// ======================================================================= \\
// --> ULTIMO DESPESA /VISITA
LOCAL AAREA     := GETAREA()
LOCAL CQUERY
LOCAL CALIASQRY := GETNEXTALIAS()
LOCAL XRET		:= IIF(NINFO==5, 0, CTOD("  /  /  "))

CQUERY := " SELECT TOP 1 AD6_DATA , SUM(AD6_VLUNIT) VALOR "
CQUERY += " FROM " + RETSQLNAME("AD5") + " AD5"
CQUERY +=        " JOIN " + RETSQLNAME("AD6") + " AD6 ON AD6_FILIAL=AD5_FILIAL AND AD6_VEND=AD5_VEND AND AD6_DATA=AD5_DATA AND AD6_SEQUEN=AD5_SEQUEN"
CQUERY += " WHERE AD5_FILIAL='"+XFILIAL("AD5")+"'"
CQUERY +=   " AND AD5_CODCLI='"+CCLIENTE+"'"
CQUERY +=   " AND AD5_LOJA='"+CLOJA+"'"
CQUERY +=   " AND AD5.D_E_L_E_T_=' '"
CQUERY +=   " AND AD6.D_E_L_E_T_=' '"
CQUERY += " GROUP BY AD6_DATA"
CQUERY += " ORDER BY AD6_DATA DESC"

DBUSEAREA( .T., "TOPCONN", TCGENQRY(,,CQUERY), CALIASQRY, .F., .T. )

IF ! (CALIASQRY)->( EOF() )
	XRET := IIF(NINFO==5, (CALIASQRY)->VALOR, STOD((CALIASQRY)->AD6_DATA) )
ENDIF

IF SELECT(CALIASQRY) > 0
	(CALIASQRY)->( DBCLOSEAREA() )
ENDIF

RESTAREA( AAREA )

RETURN XRET
