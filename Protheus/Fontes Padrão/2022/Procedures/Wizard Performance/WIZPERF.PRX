#INCLUDE "TOTVS.CH"
#INCLUDE 'DBTREE.CH'
#INCLUDE "FWBROWSE.CH"
#INCLUDE 'WIZPERF.CH'    
#DEFINE _CRLF CHR(13)+CHR(10)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³PROGRAMA  ³WIZPERF  ³Autor ³TOTVS S.A.             ³Data ³ 28/03/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Wizard para Configuracao de Performance                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ PROTHEUS                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function WIZPERF()
Local oOk        	:= LoadBitmap( GetResources(), "LBOK" )
Local oNOk       	:= LoadBitmap( GetResources(), "LBNO" )
Local aTFolder      := {STR0093, STR0094, STR0095, STR0096}
Local i             := 0
Local j             := 0
Local nX            := 0
Local nY            := 0
Local nRadio        := 1
Local aModulos      := {}
Local aItens        := {}
Local aGrupos		:= {}
Local lContinua     := .F.
Local cFilBkp       := cFilAnt
Local cEmpBkp       := cEmpAnt
Local aSX6          := { }
Local cUsrGrpRl     := ""

Private aArrayCV8   := {{'','','','','','',''}}
Private aArrayDET   := {{'','','','','','',''}}

Private aEmpresas 	:= {}
Private aItGeral    := {}

Private oDlg
Private lProcessa   := .F.

Private cTitulo    	:= STR0001 //"Microsiga Protheus 11 | Versao do Wizard: 20140102.P11.80"
Private cItemAju   	:= STR0004
Private cEscEmp    	:= STR0005
Private cAcao      	:= ''
Private cApresenta 	:= ''
Private cTerAceite 	:= ''
Private cLogUpdate  := ''
Private cWRotina    := ''
Private cWModulo    := ''

Private lConcordo  	:= .F.
Private nTela      	:= 1

Private oTitulo
Private oAcao
Private oLBCabec
Private oLBDetalhe

Private oEmpAtu
Private oSelEmp
Private oMemo1
Private oMemo2
Private oMemo3
Private oMemo4

Private oPanel1
Private oPanel2
Private oPanel3
Private oPanel4
Private oPanel5
Private oPanel6

Private oApresenta

Private oTerAceite
Private oChkAceite
Private oSelProc

Private oBtnAvanca                                   
Private oBtnExecutar         

Private oTree
Private oTFolder

// Verifica as condições do grupo de usuário - 1=Priorizar,2=Desconsiderar,3=Somar
If FindFunction("FWUsrGrpRule")
	cUsrGrpRl := FWUsrGrpRule(__CUserId)
EndIf

// Somente o usuario administrador ou usuario que esta no grupo podera acessar o Wizard
If !(AllTrim(__CUserId) == "000000") // Nao e o usuario Administrador
	aGrupos := UsrRetGrp(__CUserId)
	For nX := 1 To Len(aGrupos)
		If AllTrim(aGrupos[nX])=="000000" .And. cUsrGrpRl <> "2" // Usuario esta no grupo de administradores
			lContinua := .T.
			Exit
		EndIf
	Next nX
Else
	lContinua := .T. // Usuario Administrador
EndIf
If !lContinua
	Aviso(STR0060,STR0163,{STR0065})	//"Usuario não possui permissão para acessar o Wizard de Performance.Somente o usuario Administrador ou usuarios que estejam no grupo de administradores podem acessar a rtoina."
	Return Nil
EndIf

cApresenta := " " + STR0003 +_CRLF //"O Wizard para configuração de performance ira auxiliar na parametrização do sistema para atingir a melhor performance na execução das rotinas do modulo abaixo:"
cApresenta += _CRLF
Do Case
	Case AllTrim(OAPP:CMODNAME) == 'SIGAATF'
		cApresenta += STR0032 + _CRLF	//" - Ativo Fixo (SIGAATF)"
	Case AllTrim(OAPP:CMODNAME) == 'SIGACOM'
		cApresenta += STR0069 + _CRLF	//" - Compras (SIGACOM)"
	Case AllTrim(OAPP:CMODNAME) == 'SIGACTB'
		cApresenta += STR0008 + _CRLF	//" - Contabilidade Gerencial (SIGACTB)"	
	Case AllTrim(OAPP:CMODNAME) == 'SIGAEST'
		cApresenta += STR0007 + _CRLF	//" - Estoque e Custo (SIGAEST)"
	Case AllTrim(OAPP:CMODNAME) == 'SIGAFAT'
		cApresenta += STR0070 + _CRLF	//" - Faturamento (SIGAFAT)"
	Case AllTrim(OAPP:CMODNAME) == 'SIGAFIN'
		cApresenta += STR0071 + _CRLF	//" - Financeiro (SIGAFIN)"
	Case AllTrim(OAPP:CMODNAME) == 'SIGAPMS'
		cApresenta += STR0009 + _CRLF	//" - Gestão de Projetos (SIGAPMS)"
	Case AllTrim(OAPP:CMODNAME) == 'SIGAPCO'
		cApresenta += STR0037 + _CRLF	//" - Planejamento e Controle Orçamentario (SIGAPCO)"
	Case AllTrim(OAPP:CMODNAME) == 'SIGAGPE'
		cApresenta += STR0117 + _CRLF	//" - Gestao de Pessoal (SIGAGPE)"
	Case AllTrim(OAPP:CMODNAME) == 'SIGAPON'
		cApresenta += STR0150 + _CRLF	//" - Ponto Eletronico (SIGAPON)"
	Otherwise
		Aviso(STR0060,STR0161,{STR0065}) //"Este modulo não esta relacionado nos modulos disponiveis para o Wizard."
		Return Nil
EndCase
cApresenta += _CRLF
cApresenta += " " + STR0072 +_CRLF //"Observação: Alem da parametrização o Wizard tambem ira trazer diversas informações sobre a rotina para melhorar sua performance ( Procedures, Logs de Execução, Links com o Portal TDN e parametros que influenciam na performance da rotina)" 
cApresenta += _CRLF
cApresenta += " " + STR0073 +_CRLF //"Importante: Antes de executar o Wizard certifique-se que nenhum usuario esteja logado no sistema."
cApresenta += _CRLF
cApresenta += " " + STR0152 +_CRLF //"Atenção: A finalidade do Wizard e ajustar a configuração do modulo/rotina selecionada, porem existem casos que e necessaria a avaliação de um DBA (Administrador de Banco de Dados) para otimizar os processos diretamente no banco de dados (tunning). Caso necessite de um DBA para verificar a performance de uma rotina voce pode entrar em contato com a equipe da TOTVS INFRA-SERVICES."

cTerAceite := OemToAnsi(STR0010) +_CRLF
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0011) +_CRLF
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0012) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0013) +_CRLF
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0014) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0015) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0016) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0017)
cTerAceite += OemToAnsi(STR0018) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0019) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0020) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0021)		
cTerAceite += OemToAnsi(STR0022) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0023)
cTerAceite += OemToAnsi(STR0024) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0025) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0026) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0027) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0028) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0029) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0030) +_CRLF	
cTerAceite += _CRLF
cTerAceite += OemToAnsi(STR0031) +_CRLF

SET DELETED ON

 SM0->(DbGotop())
If FWSizeFilial() > 2
	//-- Carrega filiais da empresa corrente
	SM0->(dbSetOrder(1))
	SM0->(dbSeek(cEmpAnt))
	Do While !SM0->(Eof()) .And. SM0->M0_CODIGO == cEmpAnt
		If !Empty(SM0->M0_CODIGO) .And. aScan(aEmpresas, {|x| x[2]+x[4] == SM0->M0_CODIGO+SubStr(SM0->M0_CODFIL,1,SM0->M0_SIZEFIL)}) == 0
			aAdd(aEmpresas, {.F.,SM0->M0_CODIGO,SM0->M0_NOME,SubStr(SM0->M0_CODFIL,1,SM0->M0_SIZEFIL),SM0->M0_FILIAL})
		EndIf
		SM0->(DbSkip())
	EndDo     
Else 
	//-- Carrega filiais da empresa corrente
	SM0->(dbSetOrder(1))
	SM0->(dbSeek(cEmpAnt))
	Do While !SM0->(Eof()) .And. SM0->M0_CODIGO == cEmpAnt
		If !Empty(SM0->M0_CODIGO) .And. aScan(aEmpresas, {|x| x[2]+x[4] == SM0->M0_CODIGO+SM0->M0_CODFIL}) == 0
			aAdd(aEmpresas, {.F.,SM0->M0_CODIGO,SM0->M0_NOME,SM0->M0_CODFIL,SM0->M0_FILIAL})
		EndIf
		SM0->(DbSkip())
	EndDo     
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ DIALOG PRINCIPAL                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE DIALOG oDlg TITLE STR0074 FROM 0, 0 TO 22, 75 SIZE 820, 550 PIXEL //"WIZPERF - Configuração de Performance"
@ 200,000 BITMAP oBmp RESNAME OemToAnsi(STR0033) OF oDlg SIZE 120, oDlg:nBottom NOBORDER WHEN .F. PIXEL
@ 000,000 BITMAP oBmp RESNAME OemToAnsi(STR0033) OF oDlg SIZE 120, oDlg:nBottom NOBORDER WHEN .F. PIXEL
@ 005,000 SAY oTitulo     VAR cTitulo OF oDlg PIXEL FONT (TFont():New('Arial',0,-13,.T.,.T.))
@ 015,000 SAY oAcao       VAR cAcao   OF oDlg PIXEL
@ 255,345 BUTTON oBtnAvanca   PROMPT OemToAnsi(STR0035) SIZE 60, 14 ACTION If(oBtnAvanca:cCaption==STR0056 , (GravaLog(cLogUpdate), oDlg:End()), IIf(oBtnAvanca:cCaption=='&Executar',(ProcUpdate(aListUpd, aEmpresas), SelePanel(@nTela,oTree,oTFolder,oLBCabec,oLBDetalhe, aSX6)),SelePanel(@nTela,oTree,oTFolder,oLBCabec,oLBDetalhe, aSX6))) OF oDlg PIXEL //'&Finalizar'

oDlg:nStyle := nOR( DS_MODALFRAME, WS_POPUP, WS_CAPTION, WS_VISIBLE )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PAINEL 1 - BEM VINDO                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oPanel1 := TPanel():New( 025, 060, ,oDlg, , , , , , 345, 225, .F.,.T. )

@ 002,005 SAY oApresenta VAR OemToAnsi(STR0036) 	OF oPanel1  	        FONT (TFont():New('Arial',0,-13,.T.,.T.))	PIXEL	//"Bem-Vindo!"
@ 015,005 GET oMemo1     VAR cApresenta OF oPanel1 MEMO PIXEL SIZE 330,200	FONT (TFont():New('Verdana',,-12,.T.)) NO	BORDER
oMemo1:lReadOnly := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PAINEL 2 - TERMO DE ACEITE                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oPanel2 := TPanel():New( 025, 060, ,oDlg, , , , , , 345, 225, .F.,.T. )
@ 002,005 SAY oTerAceite 		VAR STR0075 OF oPanel2 FONT (TFont():New('Arial',0,-13,.T.,.T.)) PIXEL //"Termo de Aceite - Leia com atenção!"
@ 015,005 GET oMemo2     		VAR cTerAceite  OF oPanel2 MEMO PIXEL 	SIZE 320,190 FONT (TFont():New('Verdana',,-12,.T.)) NO BORDER
@ 212,235 CheckBox oChkAceite 	VAR lConcordo PROMPT OemToAnsi(STR0038)	SIZE 80,10 Of oPanel2 PIXEL
oMemo2:lReadOnly   := .T.
oChkAceite:bChange := {|| Concordo(lConcordo)}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PAINEL 3 - SELECAO EMPRESA/FILIAL                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oPanel3 := TPanel():New( 020, 060, ,oDlg, , , , , , 345, 225, .F.,.T. )
@ 000,000 SAY oSelEmp VAR STR0048 OF oPanel3 FONT (TFont():New('Arial',0,-13,.T.,.T.)) PIXEL //"Selecione a Empresa"
oList := TWBrowse():New( 010, 000, 330, 200,,{ "", IIf(FWSizeFilial()>2,STR0164,STR0039), IIf(FWSizeFilial()>2,STR0165,STR0040),STR0086,STR0087 },,oPanel3,,,,,,,,,,,,.F.,,.T.,,.F.,,,)//"Código"##"Empresa"
oList:SetArray(aEmpresas)
oList:bLine      := { || { If( aEmpresas[oList:nAT,1], oOk, oNOK ), aEmpresas[oList:nAt,2], aEmpresas[oList:nAT,3],aEmpresas[oList:nAT,4],aEmpresas[oList:nAT,5] } }
oList:bLDblClick := { || aEmpresas[oList:nAt,1] := !aEmpresas[oList:nAt,1] } 

@ 212,000 GET oMemo3 VAR cEscEmp OF oPanel3 MEMO PIXEL SIZE 320,12 FONT (TFont():New('Verdana',,-10,.T.)) NO BORDER
oMemo3:lReadOnly := .T.
                              
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PAINEL 4 - SELECAO DA ROTINA/PROCESSO             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oPanel4 := TPanel():New( 020, 060, ,oDlg, , , , , , 345, 225, .F.,.T. )
@ 000,000 SAY oSelProc VAR STR0076 OF oPanel4 FONT (TFont():New('Arial',0,-13,.T.,.T.)) PIXEL //"Selecione o Modulo/Rotina"

// Informacoes para montagem do Tree
oTree:= dbTree():New(010, 000,220,335,oPanel4,,,.T.)
oTree:lShowHint:= .F.

// Modulos disponiveis para o Wizard
Do Case
	Case AllTrim(OAPP:CMODNAME) == 'SIGAATF'
		aAdd(aModulos,{"#SIGAATF","ATF_WIZPER","ATFIMG32","#ATF",STR0081})
	Case AllTrim(OAPP:CMODNAME) == 'SIGACOM'
		aAdd(aModulos,{"#SIGACOM","COM_WIZPER","COMIMG32","#COM",STR0083})
	Case AllTrim(OAPP:CMODNAME) == 'SIGACTB'
		aAdd(aModulos,{"#SIGACTB","CTB_WIZPER","CTBIMG32","#CTB",STR0079})
	Case AllTrim(OAPP:CMODNAME) == 'SIGAEST'
		aAdd(aModulos,{"#SIGAEST","EST_WIZPER","ESTIMG32","#EST",STR0078})
	Case AllTrim(OAPP:CMODNAME) == 'SIGAFAT'
		aAdd(aModulos,{"#SIGAFAT","FAT_WIZPER","FATIMG32","#FAT",STR0084})
	Case AllTrim(OAPP:CMODNAME) == 'SIGAFIN'
		aAdd(aModulos,{"#SIGAFIN","FIN_WIZPER","FINIMG32","#FIN",STR0085})
	Case AllTrim(OAPP:CMODNAME) == 'SIGAPMS'
		aAdd(aModulos,{"#SIGAPMS","PMS_WIZPER","PMSIMG32","#PMS",STR0080})
	Case AllTrim(OAPP:CMODNAME) == 'SIGAPCO'
		aAdd(aModulos,{"#SIGAPCO","PCO_WIZPER","PCOIMG32","#PCO",STR0082})
	Case AllTrim(OAPP:CMODNAME) == 'SIGAGPE'
		aAdd(aModulos,{"#SIGAGPE","GPE_WIZPER","GPEIMG32","#GPE",STR0116})
	Case AllTrim(OAPP:CMODNAME) == 'SIGAPON'
		aAdd(aModulos,{"#SIGAPON","PON_WIZPER","PONIMG32","#PON",STR0151})
EndCase

//Montagem do Menu de selecao do Modulo/Rotina
For nX := 1 to Len(aModulos)
	If FindFunction(aModulos[nX,2])
		oTree:AddTree(PADR(aModulos[nX,5],100),.F.,,,aModulos[nX,3],aModulos[nX,3],aModulos[nX,1])
		aItens := &(aModulos[nX,2]+"()")
		aAdd(aItGeral,aItens)
		If Len(aItens)>0
			For nY:= 1 to Len(aItens)
				oTree:AddTreeItem(PADR(aItens[nY,2]+' -> '+aItens[nY,3],100),,,aItens[nY,5],"BMPTABLE","BMPTABLE")
			Next nY
		EndIf
		oTree:EndTree()
	Else
		Aviso(STR0060,STR0118,{STR0065}) //"O Template com a configuração de performance deste modulo não foi localizada."
		FINAL("WIZPERF - Template")
	EndIf	
Next nX
oTree:Refresh()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PAINEL 5 - OTIMIZACAO                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oPanel5 := TPanel():New( 005, 057, ,oDlg, , , , , , 345, 245, .F.,.T. )
// Cria a Folder    

oTFolder := TFolder():New( 0 , 0 ,aTFolder,,oPanel5,0,,,.T.,,345,245 )

// Cria a TwBrowse do Log de Execucoes (Cabecalho)
@ 014,005 SAY STR0102 OF oTFolder:aDialogs[4] FONT (TFont():New('Arial',0,-10,.T.,.T.)) PIXEL //"Selecione o ID"
oLBCabec := TWBrowse():New( 020,005,335,050,,{STR0103,STR0104,STR0105,STR0106,STR0107,STR0113,STR0114},{TamSX3("CV8_IDMOV")[1],TamSX3("CV8_PROC")[1],TamSX3("CV8_USER")[1],TamSX3("CV8_DATA")[1],TamSX3("CV8_HORA")[1] },oTFolder:aDialogs[4],,,,,,,,,,,,.F.,,.T.,,.F.) //"ID Processo"#"Rotina"#"Usuario"#"Data"#"Hora"
oLBCabec:SetArray(aArrayCV8)
oLBCabec:bChange := { || WizLogDetail(aArrayCV8[oLbCabec:nAt,1],oLBDetalhe,oLBCabec) }
oLBCabec:bLine := { || { aArrayCV8[oLBCabec:nAT][1], aArrayCV8[oLBCabec:nAT][2], aArrayCV8[oLBCabec:nAT][3], aArrayCV8[oLBCabec:nAT][4], aArrayCV8[oLBCabec:nAT][5], aArrayCV8[oLBCabec:nAT][6], aArrayCV8[oLBCabec:nAT][7]} }
oLBCabec:Refresh()

// Cria a TwBrowse do Log de Execucoes (Detalhes)
@ 074,005 SAY STR0108 OF oTFolder:aDialogs[4] FONT (TFont():New('Arial',0,-10,.T.,.T.)) PIXEL //"Detalhes do Processamento"
oLBDetalhe := TWBrowse():New( 080,005,335,150,,{STR0103,STR0109,STR0106,STR0107,STR0110,STR0112,STR0111},{TamSX3("CV8_IDMOV")[1],TamSX3("CV8_FILIAL")[1],8,10,1,TamSX3("CV8_MSG")[1],TamSX3("CV8_SBPROC")[1]},oTFolder:aDialogs[4],,,,,,,,,,,,.F.,,.T.,,.F.)
oLBDetalhe:SetArray(aArrayDET)                                                                                        
oLBDetalhe:bLDblClick := { || WizLogMemo(oLBDetalhe:nAt,oLBDetalhe) }
oLBDetalhe:bLine := { || { aArrayDET[oLBDetalhe:nAT][1],aArrayDET[oLBDetalhe:nAT][2],aArrayDET[oLBDetalhe:nAT][3],aArrayDET[oLBDetalhe:nAT][4],aArrayDET[oLBDetalhe:nAT][5],aArrayDET[oLBDetalhe:nAT][6],aArrayDET[oLBDetalhe:nAT][7] } }
oLBDetalhe:Refresh()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PAINEL 6 - LOG DE MODIFICACOES                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ              
oPanel6 := TPanel():New( 005, 057, ,oDlg, , , , , , 345, 245, .F.,.T. )

@ 002,005 SAY oLogUpdate VAR OemToAnsi(STR0042)	OF oPanel6 FONT (TFont():New('Arial',0,-13,.T.,.T.))PIXEL
@ 015,005 GET oMemo4     VAR cLogUpdate	OF oPanel6 MEMO PIXEL SIZE 320,200 FONT (TFont():New('Courrer New',,-12,.T.)) NO BORDER
oMemo4:lReadOnly   := .T.

ACTIVATE DIALOG oDlg CENTER ON INIT SelePanel(@nTela,oTree,oTFolder,oLBCabec,oLBDetalhe, aSX6)

// Restaura a Empresa/Filial corrente
cFilAnt := cFilBkp
cEmpAnt := cEmpBkp

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³SelePanel ³ Autor ³Microsiga S/A          ³ Data ³ 28/03/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Funcao utilizada para selecao atraves de painel            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P10                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATERIAIS                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SelePanel(nTela,oTree,oTFolder,oLBCabec,oLBDetalhe, aSX6)
Local nX 		:= 0
Local nY 		:= 0
Local nSelecao  := 0
Local lRet 		:= .T.
Local lAchou    := .F.
Local lTemCV8   := .F.
Local aTDN      := {}
Local cDtTempl  := ''
Local cProcSP   := ''

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza variaveis da janela principal ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oTitulo:nLeft           := 120; oTitulo:Refresh()
oAcao:nLeft             := 120; oAcao:Refresh()
oBmp:lVisibleControl    := .T.
oPanel1:lVisibleControl := .F.
oPanel2:lVisibleControl := .F.
oPanel3:lVisibleControl := .F.
oPanel4:lVisibleControl := .F.
oPanel5:lVisibleControl := .F.
oPanel6:lVisibleControl := .F.

Do Case
	Case nTela == 1 //-- Apresentacao
		oPanel1:lVisibleControl := .T.
	Case nTela == 2 //-- Termo de aceite
		cAcao := OemToAnsi(STR0043)
		oPanel2:lVisibleControl := .T.
		oBtnAvanca:lActive      := .F.
	Case nTela == 3 //-- Selecao da empresa
		cAcao := OemToAnsi(STR0044)
		oPanel3:lVisibleControl := .T.
	Case nTela == 4 //-- Selecao Modulo/Rotina
		nSelecao := 0
		For nX:=1 to Len(aEmpresas)
			If aEmpresas[nX,1]
				nSelecao++
			EndIf
		Next nX
		If nSelecao <= 1
		   nPos:=aScan(aEmpresas,{|x| x[1] })
		   If (nPos > 0)
				cAcao := OemToAnsi(STR0047)
				oPanel4:lVisibleControl := .T.
				oBtnAvanca:lActive      := .T.
				// Abre a empresa/Ambiente
				AbreEmpre(aEmpresas[nPos,2], aEmpresas[nPos,4])
			Else
				lRet := .F.
				cAcao := OemToAnsi(STR0048)
				oPanel3:lVisibleControl := .T.
				Aviso(STR0060,STR0048,{"Ok"})
			EndIf
		Else
			lRet := .F.
			cAcao := OemToAnsi(STR0048)
			oPanel3:lVisibleControl := .T.
			Aviso(STR0060,STR0166,{"Ok"})	//"Selecione apenas uma Empresa/Filial!"
		EndIf
	Case nTela == 5 //-- Configuracao de Performance
		cAcao := OemToAnsi(STR0049);oAcao:Refresh()
		oPanel5:lVisibleControl := .T. 
		oBtnAvanca:lActive      := .T.
		// Recupera o codigo do modulo/rotina
		cCargo  := AllTrim(oTree:GetCargo())
	    For nX:=1 to Len(aItGeral)
	    	For nY:=1 to Len(aItGeral[nX])
		    	If cCargo==aItGeral[nX,nY,5]
			        cWModulo:= aItGeral[nX,nY,1]
			        cWRotina:= aItGeral[nX,nY,2]
					cDtTempl:= Iif(Len(aItGeral[nx,nY])>3,aItGeral[nx,nY,4],'')
					aTDN 	:= Iif(Len(aItGeral[nx,nY])>5,aItGeral[nX,nY,6],{})
					aSX6 	:= Iif(Len(aItGeral[nx,nY])>6,aItGeral[nX,nY,7],{})
					cProcSP := Iif(Len(aItGeral[nx,nY])>7,aItGeral[nX,nY,8],'')
					lTemCV8 := Iif(Len(aItGeral[nx,nY])>8,aItGeral[nX,nY,9],.F.)
			        lAchou  := .T.
			        Exit
				EndIf
			Next nY
			If lAchou
				Exit
			EndIf
	    Next nX
		// Caso o usuario nao selecione o modulo/rotina nao avancar
	    If Empty(cWRotina) .Or. Empty(cWModulo)
			lRet  := .F.
			cAcao := OemToAnsi(STR0047)
			oPanel4:lVisibleControl := .T.
			oPanel5:lVisibleControl := .F.
			Aviso(STR0060,STR0047,{"Ok"}) //"Seleccione o Modulo/Rotina"
		Else
	        // Carrega as informacoes da Folder
			WIZFolder(oTFolder,aTDN,aSX6,oLBCabec,oLBDetalhe,cProcSP,lTemCV8)
		EndIf
	Case nTela == 6 //-- Finalizacao
		cAcao := OemToAnsi(STR0054)
		cAcao                   := OemToAnsi(STR0052) ; oAcao:Refresh()
		oPanel6:lVisibleControl := .T.
		oBtnAvanca:cCaption     := OemToAnsi(STR0056)
		// Recupera a data de publicacao do template
		cCargo  := AllTrim(oTree:GetCargo())
	    For nX:=1 to Len(aItGeral)
	    	For nY:=1 to Len(aItGeral[nX])
		    	If cCargo==aItGeral[nX,nY,5]
			        cWModulo:= aItGeral[nX,nY,1]
			        cWRotina:= aItGeral[nX,nY,2]
					cDtTempl:= Iif(Len(aItGeral[nx,nY])>3,aItGeral[nx,nY,4],'')
			        lAchou  := .T.
			        Exit
				EndIf
			Next nY
			If lAchou
				Exit
			EndIf
	    Next nX
		If Empty(cLogUpdate)
			cLogUpdate :=	STR0074 + _CRLF + _CRLF +;
							STR0001 + _CRLF + _CRLF +; 				//"Microsiga Protheus 11 | Versao do Wizard: 20140102.P11.80"
							STR0141 + cDtTempl	+ _CRLF	+ _CRLF +;	//"Data de Publicação do Template selecionado: "
							STR0142 + cWModulo	+ _CRLF	+ _CRLF	+;	//"Modulo Selecionado: #####"
							STR0143 + cWRotina	+ _CRLF + _CRLF	+;	//"Rotina Selecionada: #####"
							STR0120 + _CRLF 	+ _CRLF	+ _CRLF		//"ATENÇÃO: NÃO HOUVE MODIFICAÇÕES NO AMBIENTE "
		Else 
			cLogUpdate :=	STR0074 + _CRLF + _CRLF +;				//"Microsiga Protheus 11 | Versao do Wizard: 20140102.P11.80"
							STR0001 + _CRLF + _CRLF + ;				//"WIZPERF - Configuração de Performance"
							STR0141 + cDtTempl 	+ _CRLF	+ _CRLF +;	//"Data de Publicação do Template selecionado: "
							STR0142 + cWModulo	+ _CRLF	+ _CRLF	+;	//"Modulo Selecionado: #####"
				            STR0143 + cWRotina	+ _CRLF + _CRLF	+;	//"Rotina Selecionada: #####"
							cLogUpdate
		EndIf
EndCase

If lRet
	nTela ++
EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³Concordo   ³ Autor ³TOTVS S.A.            ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Termo de Aceite                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Concordo(lConcordo)

If lConcordo
	oBtnAvanca:lActive    := .T.
Else
	oBtnAvanca:lActive    := .F.
EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AbreEmpre  ³ Autor ³TOTVS S.A.            ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a abertura da Empresa                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function AbreEmpre(cCodEmp, cCodFil)

//-- Muda para empresa selecionada
cEmpAnt := cCodEmp
//-- Muda para Filial selecionada
cFilant := cCodFil 
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WizFolder  ³ Autor ³TOTVS S.A.            ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Painel 5 - Definicoes do TFolder                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function WIZFolder(oTFolder,aTDN,aSX6,oLBCabec,oLBDetalhe,cProcSP,lTemCV8)
Local nX       := 0
Local nY       := 0
Local nLin     := 0
Local cConteudo:= ''
Local cSobre   := '' 
Local cAliasCV8:= ''
Local cQuery   := ''
Local aBrowse  := {}
Local aAreaAnt := GetArea()
Local oScrollBox1, oScrollBox2
Local oScrollBox3, oScrollBox4
Local oBrowse, oDlg1, oPanel8, oTexto
Local oOK      := LoadBitmap(GetResources(),'br_verde')
Local oNO      := LoadBitmap(GetResources(),'br_vermelho')  
Local aObj1SX6 := Array(50)
Local aObj2SX6 := Array(50)

Default cProcSP := ''
Default lTemCV8 := .F.
Default aTDN    := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem do AHeader.                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aRotina := {{"","",0,4}}
PRIVATE aHeader := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ FOLDER 1 - Documentacao TDN                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aTDN) > 0
	oScrollBox1 := TScrollBox():New( oTFolder:aDialogs[1], 0, 0, 345, 245)
	oScrollBox1:Align := CONTROL_ALIGN_ALLCLIENT
	
	nLin := 5
	For nX := 1 to Len(aTDN)
		TSay():New(nLin,005,&("{|| ' >> "  + aTDN[nX,1] + "'  }"),oScrollBox1,,TFont():New('Arial',0,-11,,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,280,100)
		nLin := nLin + 006
	    THButton():New(nLin,005,aTDN[nX,3],oScrollBox1,&("{|| WinExec('cmd /c start" + Space(1) + aTDN[nX,3] + "' ) }"),200,10)
		nLin := nLin + 015
	Next nX
Else
	oTFolder:HidePage(1)
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ FOLDER 2 - Parametros SX6                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aSX6) > 0
	oScrollBox2 := TScrollBox():New( oTFolder:aDialogs[2], 0, 0, 345, 245)
	oScrollBox2:Align := CONTROL_ALIGN_ALLCLIENT
			
	nLin := 5
	For nX := 1 to Len(aSX6)
		TSay():New(nLin,005,&("{|| ' " + STR0097 + " " + aSX6[nX,1] + "'  }"),oScrollBox2,,TFont():New('Arial',0,-12,.T.,.T.,,,,,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,280,100) // Parametro:
		nLin := nLin + 015
		TSay():New(nLin,010,&("{|| ' " + STR0098 + "'  }"),oScrollBox2,,TFont():New('Arial',0,-11,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100) //Sobre o Parametro:
		nLin := nLin + 010
	
		For nY := 1 to Len(aSX6[nX,5])
			cSobre :=  StrTran( aSX6[nX,5,nY], "'", "" ) + " "
			TSay():New(nLin,015,&("{|| ' "  + cSobre + "'  }"),oScrollBox2,,TFont():New('Courier New',0,-11,.T.,.F.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)
			nLin := nLin + 007
		Next nY
	
		nLin := nLin + 15
	
		// Posiciona no Parametro SX6
		GETMV(aSX6[nX,1],.F.)
		If AllTrim(SX6->X6_VAR)==AllTrim(aSX6[nX,1])
			cConteudo := AllTrim(SX6->X6_CONTEUD)
		Else
			cConteudo := STR0099 //'Não Cadastrado'
		EndIf
	
		//Conteudo Atual:
						TSay():New(nLin,010,&("{|| ' " + STR0100 	+ "'  }")	,oScrollBox2,,TFont():New('Arial',0,-11,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)
		aObj1SX6[nX] :=	TSay():New(nLin,080,&("{|| ' " + cConteudo + "'  }")	,oScrollBox2,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,CLR_RED  ,CLR_WHITE,250,100)
		
		nLin := nLin + 015
		
		//Conteudo Sugerido:
						TSay():New(nLin,010,&("{|| ' " + STR0101 		+ "'  }"),oScrollBox2,,TFont():New('Arial',0,-11,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100) 
		aObj2SX6[nX] :=	TSay():New(nLin,080,&("{|| ' " + aSX6[nX,3]	+ "'  }"),oScrollBox2,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,CLR_RED  ,CLR_WHITE,250,100)
		nLin := nLin + 020
		// Carrega o conteudo Atual no array aSX6    
		aSX6[nX,4] := cConteudo
		
	Next nX
	
	TButton():Create( oScrollBox2,nLin+020,105,STR0002,{||WizEditPar(aSX6,oScrollBox2,aObj2SX6)},70,12,,,,.T.,,,,,,)	//"Editar Parametros"
	TButton():Create( oScrollBox2,nLin+020,180,STR0006,{|| Processa({ || WizAtuPar(aSX6,oScrollBox2,aObj1SX6), STR0153 }) },70,12,,,,.T.,,,,,,)	//"Atualizando Parametros ..."
Else
	oTFolder:HidePage(2)
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ FOLDER 3 - Stored Procedures                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cProcSP) .And. FindFunction("LoadProcs") //Verificar a existencia da funcao LoadProcs (CFGX051)
	oScrollBox3 := TScrollBox():New( oTFolder:aDialogs[3], 0, 0, 345, 245)
	oScrollBox3:Align := CONTROL_ALIGN_ALLCLIENT
	// Consulta o status do processo (Stored Procedures)	
	WizConSP(oScrollBox3,cProcSP)
Else
	oTFolder:HidePage(3)
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ FOLDER 4 - Logs de Execucoes                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !CV8->(FieldPos("CV8_IDMOV"))>0
	Aviso(STR0060,STR0119,{STR0065}) //"Para visualizar o Log da Tabela CV8 aplique o update UPDATF01 para criação do campo CV8_IDMOV"
EndIf

If lTemCV8 .And. CV8->(FieldPos("CV8_IDMOV"))>0
	nLin := 5
	TSay():New(nLin,005,&("{|| ' Modulo: " + cWModulo + " | " + STR0154 + cWRotina + "'  }"),oTFolder:aDialogs[4],,TFont():New('Arial',0,-11,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)
	
	cAliasCV8 := GetNextAlias()
	cQuery := "SELECT"
	
	cQuery += " CV8_FILIAL,"
	cQuery += " CV8_IDMOV,"
	cQuery += " CV8_PROC,"
	cQuery += " CV8_USER,"
	cQuery += " ( SELECT IsNull(Min(CV8_DATA),'') "
	cQuery +=     " FROM "+RetSqlName("CV8")+" "
	cQuery += " WHERE  CV8_IDMOV = CV8.CV8_IDMOV "
	cQuery +=    " AND CV8_PROC = '" + Padr(cWRotina,TamSX3("CV8_PROC")[1]) + "' "
	cQuery +=     " AND CV8_INFO = '1' "
	cQuery +=     " AND CV8_SBPROC = '' "
	cQuery +=     " AND D_E_L_E_T_ = ' ' ) DATAINI, "
	
	cQuery += " ( SELECT IsNull(Min(CV8_HORA),'') "
	cQuery +=     " FROM "+RetSqlName("CV8")+" "
	cQuery += " WHERE  CV8_IDMOV = CV8.CV8_IDMOV "
	cQuery +=    " AND CV8_PROC = '" + Padr(cWRotina,TamSX3("CV8_PROC")[1]) + "' "
	cQuery +=     " AND CV8_INFO = '1' "
	cQuery +=     " AND CV8_SBPROC = '' "
	cQuery +=     " AND D_E_L_E_T_ = ' ' ) HORAINI, "
	
	cQuery += " ( SELECT IsNull(MAX(CV8_DATA),'') "
	cQuery +=     " FROM "+RetSqlName("CV8")+" "
	cQuery += " WHERE  CV8_IDMOV = CV8.CV8_IDMOV "
	cQuery +=    " AND CV8_PROC = '" + Padr(cWRotina,TamSX3("CV8_PROC")[1]) + "' "
	cQuery +=     " AND CV8_INFO = '2' "
	cQuery +=     " AND CV8_SBPROC = '' "
	cQuery +=     " AND D_E_L_E_T_ = ' ' ) DATAFIM, "
	
	cQuery += " ( SELECT IsNull(MAX(CV8_HORA),'') "
	cQuery +=     " FROM "+RetSqlName("CV8")+" "
	cQuery += " WHERE  CV8_IDMOV = CV8.CV8_IDMOV "
	cQuery +=    " AND CV8_PROC = '" + Padr(cWRotina,TamSX3("CV8_PROC")[1]) + "' "
	cQuery +=     " AND CV8_INFO = '2' "
	cQuery +=     " AND CV8_SBPROC = '' "
	cQuery +=     " AND D_E_L_E_T_ = ' ' ) HORAFIM
	
	cQuery +=  " FROM "+RetSqlName("CV8")+" CV8 "
	cQuery += " WHERE CV8_PROC = '" + Padr(cWRotina,TamSX3("CV8_PROC")[1]) + "' "
	cQuery +=   " AND D_E_L_E_T_ = ' ' "
	
	cQuery +=   " GROUP BY  CV8_FILIAL,CV8_IDMOV,CV8_PROC,CV8_USER"
	cQuery +=   " ORDER BY  CV8_FILIAL,CV8_IDMOV,CV8_PROC,CV8_USER"
	cQuery := ChangeQuery(cQuery, .F.)
	
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasCV8,.T.,.T.)
	
	TcSetField(cAliasCV8,"DATAINI","D",TamSX3("CV8_DATA")[1],TamSX3("CV8_DATA")[2])
	TcSetField(cAliasCV8,"DATAFIM","D",TamSX3("CV8_DATA")[1],TamSX3("CV8_DATA")[2])
	// Carrega as informacoes para o Log de processamento
	Do While !Eof()
		aAdd(aArrayCV8,{(cAliasCV8)->CV8_IDMOV,(cAliasCV8)->CV8_PROC, (cAliasCV8)->CV8_USER, (cAliasCV8)->DATAINI, (cAliasCV8)->HORAINI,(cAliasCV8)->DATAFIM, (cAliasCV8)->HORAFIM })
		(cAliasCV8)->(dbSkip())
	EndDo
	
	// Remove a linha em branco
	If Len(aArrayCV8) >= 2
		If Empty(aArrayCV8[1,1])
			Adel(aArrayCV8,1)
			ASize(aArrayCV8,Len(aArrayCV8)-1)
		EndIf
	EndIf	
	
	// Atualiza a TWBrowse com os dados da rotina selecionada
	oLBCabec:SetArray(aArrayCV8)
	oLBCabec:bLine := { || {aArrayCV8[oLBCabec:nAT][1],aArrayCV8[oLBCabec:nAT][2],aArrayCV8[oLBCabec:nAT][3],aArrayCV8[oLBCabec:nAT][4],aArrayCV8[oLBCabec:nAT][5],aArrayCV8[oLBCabec:nAT][6],aArrayCV8[oLBCabec:nAT][7] } }
	oLBCabec:Refresh() 
	
	//Atualiza a tabela de Logs pela primeira vez
	WizLogDetail(aArrayCV8[oLbCabec:nAt,1],oLBDetalhe)
	
	If Select(cAliasCV8) > 0 
		(cAliasCV8)->(dbCloseArea())
	EndIf
Else
	oTFolder:HidePage(4)
EndIf

RestArea(aAreaAnt)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WizLogDetail ³ Autor ³TOTVS S.A.          ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualiza o Log de Processamento ao selecionar o ID         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function WizLogDetail(cID,oLBDetalhe)
Local cAliasCV8 := ''
Local cQuery    := ''
Local aAreaAnt  := GetArea()
Local cQRotina  := Iif(Type('cWRotina')=="C",cWRotina,'')

// Carrega o Log de processamento
cAliasCV8 := GetNextAlias()
cQuery := "SELECT CV8_IDMOV, CV8_FILIAL, CV8_DATA, CV8_HORA, CV8_MSG, CV8_INFO, CV8_SBPROC, R_E_C_N_O_ RECNOCV8 "
cQuery +=  " FROM "+RetSqlName("CV8")+" CV8 "
cQuery += " WHERE CV8_IDMOV = '" + cID + "' "
cQuery +=   " AND CV8_PROC = '" + Padr(cQRotina,TamSX3("CV8_PROC")[1]) + "' "
cQuery +=   " AND D_E_L_E_T_ = ' ' "
cQuery +=   " ORDER BY  CV8_IDMOV,R_E_C_N_O_,CV8_PROC,CV8_USER,CV8_DATA,CV8_HORA "
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasCV8,.T.,.T.)
aArrayDET   := {{'','','','','','',''}}
aEval(CV8->(dbStruct()), {|x| If(x[2] <> "C", TcSetField(cAliasCV8,x[1],x[2],x[3],x[4]),Nil)})
Do While !Eof()
	aAdd(aArrayDet,{(cAliasCV8)->CV8_IDMOV,(cAliasCV8)->CV8_FILIAL,(cAliasCV8)->CV8_DATA,(cAliasCV8)->CV8_HORA,(cAliasCV8)->CV8_MSG,(cAliasCV8)->CV8_SBPROC,(cAliasCV8)->CV8_INFO,STRZERO((cAliasCV8)->RECNOCV8,15)})
	(cAliasCV8)->(dbSkip())
EndDo

// Remove a linha em branco
If Len(aArrayDET) >= 2
	If Empty(aArrayDET[1,1])
		Adel(aArrayDET,1)
		ASize(aArrayDET,Len(aArrayDET)-1)
	EndIf
EndIf	

// Montagem do detalhe do Log 
If !Empty(cID) .And. Len(aArrayDET) > 0
	oLBDetalhe:SetArray(aArrayDET)
	oLBDetalhe:bLine := { || {aArrayDET[oLBDetalhe:nAT][1],aArrayDET[oLBDetalhe:nAT][2],aArrayDET[oLBDetalhe:nAT][3],aArrayDET[oLBDetalhe:nAT][4],aArrayDET[oLBDetalhe:nAT][5],aArrayDET[oLBDetalhe:nAT][6],aArrayDET[oLBDetalhe:nAT][7]} }
	oLBDetalhe:aColSizes := {TamSX3("CV8_IDMOV")[1],TamSX3("CV8_FILIAL")[1]+20,8,10,1,TamSX3("CV8_MSG")[1],TamSX3("CV8_SBPROC")[1],15}
	oLBDetalhe:Refresh() 
EndIf

// Encerra o alias temporario
If Select(cAliasCV8) > 0 
	(cAliasCV8)->(dbCloseArea())
EndIf	
RestArea(aAreaAnt)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    | WizLogMemo  ³ Autor ³TOTVS S.A.          ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Leitura do campo MEMO (Tabela CV8)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function WizLogMemo(nLinha)
Local oDlgMemo	:= Nil
Local oBotOk	:= Nil
Local nrecnoCV8 := 0
Local oMemo, mMemo

// Carrega os dados do campo MEMO (CV8_DET)
If Type('aArrayDet')=="A" .And. Len(aArrayDet)>=nLinha
	nRecnoCV8 := Val(aArrayDet[nLinha,8])
	dbSelectArea("CV8")
	dbGoto(nRecnoCV8)
	mMemo := CV8->CV8_DET
EndIf	

DEFINE DIALOG oDlgMemo FROM  005,005 TO 300,370 OF oTFolder:aDialogs[4] TITLE STR0108 PIXEL	//"Detalhes do Processamento"

//Campo memo
@ 00,00 GET oMemo VAR mMemo OF oDlgMemo MEMO SIZE 185,148 FONT oDlgMemo:oFont

oMemo:bRClicked := {||AllwaysTrue()}

ACTIVATE DIALOG oDlgMemo CENTER

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    | WizEditPar  ³ Autor ³TOTVS S.A.          ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Edita os parametros SX6                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function WizEditPar(aSX6,oScrollBox2,aObj2SX6)
Local nX       := 0
Local aNewSX6  := aClone(aSX6)
Local oDlgEdit
Local oLBSX6 

Local oBrowse1

// Cria a TWBrowse do Log de Execucoes (Detalhes)
DEFINE DIALOG oDlgEdit FROM  010,005 TO 400,700 TITLE STR0041 OF oScrollBox2 PIXEL	//"Editar Conteudo Sugerido"

TSay():New(005,005,&("{|| ' " + STR0045 + "'  }"),oDlgEdit,,TFont():New('Arial',0,-11,,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,280,100)	//"Modifique os parametros conforme desejado:"

// Define o Browse
DEFINE FWBROWSE oBrowse1 DATA ARRAY ARRAY aNewSX6 NO SEEK NO CONFIG NO LOCATE DOUBLECLICK { || WizDbClick(oBrowse1,@aNewSX6,@aSX6) } OF oDlgEdit

// Adiciona as colunas do Browse
ADD COLUMN 	oColumn DATA { || aNewSX6[oBrowse1:At(),1] } TITLE STR0046	  SIZE 10 HEADERCLICK {||.F.} ALIGN 0 OF oBrowse1	//"Parametro"
ADD COLUMN 	oColumn DATA { || aNewSX6[oBrowse1:At(),2] } TITLE STR0050	  SIZE 10 HEADERCLICK {||.F.} ALIGN 0 OF oBrowse1	//"Tipo"
ADD COLUMN 	oColumn DATA { || aNewSX6[oBrowse1:At(),3] } TITLE STR0051	  SIZE 04 HEADERCLICK {||.F.} ALIGN 0 OF oBrowse1	//"Conteudo Sugerido"
ADD COLUMN 	oColumn DATA { || aNewSX6[oBrowse1:At(),4] } TITLE STR0053	  SIZE 04 HEADERCLICK {||.F.} ALIGN 0 OF oBrowse1	//"Conteudo Atual"

// Ativacao do Browse
ACTIVATE FWBROWSE oBrowse1

ACTIVATE DIALOG oDlgEdit CENTER

// Atualiza a visualizacao dos conteudos alterados pelo usuario
For nX:=1 to Len(aSX6)
	aObj2SX6[nX]:SetText(aSX6[nX,3])
Next nX

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    | WizAtuPar   ³ Autor ³TOTVS S.A.          ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualizar os parametros SX6                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function WizAtuPar(aSX6,oScrollBox2,aObj1SX6)
Local nOpcao     := 0
Local nX         := 0
Local nI 		 := 0
Local nJ 		 := 0
Local cNoProcess := ''
Local cConteudo  := ''
Local aArea      := GetArea()
Local aAreaSX6   := SX6->(GetArea())
Local aFieldSX6  := {}
Local aEstrut    := {	"X6_FIL"	,"X6_VAR"		,"X6_TIPO"		,"X6_DESCRIC"	,"X6_DSCSPA"	,"X6_DSCENG",;
				 		"X6_DESC1"	,"X6_DSCSPA1"	,"X6_DSCENG1"	,"X6_DESC2"		,"X6_DSCSPA2"	,;
						"X6_DSCENG2","X6_CONTEUD"	,"X6_CONTSPA"	,"X6_CONTENG"	,"X6_PROPRI"}

If Aviso(STR0060,STR0061,{STR0066,STR0067}) == 1 //##ATENCAO##"Confirma a atualização de parametros?"
	SX6->(dbSetOrder(1))
	For nX := 1 to Len(aSX6)
		If !Empty(aSX6[nX,1])
			If SX6->(MsSeek(cFilAnt+aSX6[nX,1])) .And. AllTrim(aSX6[nX,1]) == AllTrim(SX6->X6_VAR) 
				If (AllTrim(aSX6[nX,3])==AllTrim(SX6->X6_CONTEUD)) 
					cLogUpdate += _CRLF
					cLogUpdate += STR0121 + aSX6[nX,1] 				+ STR0122 + _CRLF	//"PARAMETRO: " ###### " -> NÃO ALTERADO!"
					cLogUpdate += STR0124 + AllTrim(aSX6[nX,3])		+ _CRLF				//"Conteudo atual :"
					cLogUpdate += STR0125 + _CRLF   				+ _CRLF 			//"O conteudo sugerido já esta informado no parametro."
				Else
					cConteudo := AllTrim(SX6->X6_CONTEUD)
					RecLock("SX6",.F.)
					SX6->X6_CONTEUD	:= AllTrim(aSX6[nX,3])
					SX6->X6_CONTSPA	:= AllTrim(aSX6[nX,3])
					SX6->X6_CONTENG	:= AllTrim(aSX6[nX,3])
					SX6->(MsUnlock())				
					cLogUpdate += _CRLF
					cLogUpdate += STR0121 + aSX6[nX,1]				+ STR0123	+ _CRLF 	//"PARAMETRO: " ###### " -> ALTERADO!"
					cLogUpdate += STR0109 + ":" + cFilAnt			+ _CRLF 				//"Filial:"
					cLogUpdate += STR0126 + AllTrim(cConteudo)		+ _CRLF 				//"Conteudo Anterior: "
					cLogUpdate += STR0127 + AllTrim(aSX6[nX,3])		+ _CRLF		+ _CRLF		//"Novo Conteudo: "
				EndIf	
			ElseIf SX6->(MsSeek(Space(Len(cFilAnt))+aSX6[nX,1])) .And. AllTrim(aSX6[nX,1]) == AllTrim(SX6->X6_VAR)
				If (AllTrim(aSX6[nX,3])==AllTrim(SX6->X6_CONTEUD)) 
					cLogUpdate += _CRLF
					cLogUpdate += STR0121 + aSX6[nX,1] + STR0122	+ _CRLF	//"PARAMETRO: " ###### " -> NÃO ALTERADO!"
					cLogUpdate += STR0124 + AllTrim(aSX6[nX,3])		+ _CRLF	//"Conteudo atual :"
					cLogUpdate += STR0125 + _CRLF  					+ _CRLF	//"O conteudo sugerido já esta informado no parametro."
				Else
					cConteudo := AllTrim(SX6->X6_CONTEUD)
					RecLock("SX6",.F.)
					SX6->X6_CONTEUD	:= AllTrim(aSX6[nX,3])
					SX6->X6_CONTSPA	:= AllTrim(aSX6[nX,3])
					SX6->X6_CONTENG	:= AllTrim(aSX6[nX,3])
					SX6->(MsUnlock())				
					cLogUpdate += _CRLF
					cLogUpdate += STR0121 + aSX6[nX,1]				+ STR0123				+ _CRLF	//"PARAMETRO: " ###### " -> ALTERADO!"
					cLogUpdate += STR0109 + ":"						+ Space(Len(cFilAnt))	+ _CRLF	//"Filial:"
					cLogUpdate += STR0126 + AllTrim(cConteudo)		+ _CRLF 						//"Conteudo Anterior: "
					cLogUpdate += STR0127 + AllTrim(aSX6[nX,3])		+ _CRLF  				+ _CRLF	//"Novo Conteudo: "
				EndIf	
			Else	
	    	    nOpcao := Aviso(STR0060,STR0062 + aSX6[nX,1] + " " + STR0063,{STR0128,STR0129, STR0067}) //##ATENCAO##"O parametro "###" não foi encontrado no seu ambiente, deseja cria-lo?"
	    	    If nOpcao==1 .Or. nOpcao==2
					aFieldSX6 := {}
					AAdd( aFieldSX6,	{Iif(nOpcao==1,Space(Len(cFilAnt)),cFilAnt),;	//--X6_FIL
										aSX6[nX,1],;										//--X6_VAR
										aSX6[nX,2],;										//--X6_TIPO
										"Parametro incluido pelo WIZPERF"	,;			//--X6_DESCRIC
										"Parámetro introducida por WIZPERF"	,;		//--X6_DSCSPA
										"Parameters enclosed by WIZPERF"	,;			//--X6_DSCENG
										"",;												//--X6_DESC1
										"",;												//--X6_DSCSPA1
										"",;												//--X6_DSCENG1
										"",;												//--X6_DESC2
										"",;												//--X6_DSCSPA2
										"",;												//--X6_DSCENG2
										aSX6[nX,3],;										//--X6_CONTEUD
										aSX6[nX,3],;										//--X6_CONTSPA
										aSX6[nX,3],;										//--X6_CONTENG
										"S"} )												//--X6_PROPRI
					
					SX6->(dbSetOrder(1))
					For nI := 1 To Len(aFieldSX6)
						If !(SX6->( dbSeek(aFieldSX6[nI,1]+aFieldSX6[nI,2])) .And. AllTrim(aSX6[nX,1]) == AllTrim(SX6->X6_VAR))
							RecLock("SX6",.T.)
							For nJ:=1 To Len(aFieldSX6[nI])
								SX6->( FieldPut(FieldPos(aEstrut[nJ]),aFieldSX6[nI,nJ]))
							Next nJ
							SX6->( MsUnLock())
						EndIf
					Next nI
					cLogUpdate += STR0131 + aFieldSX6[1,2]	+ _CRLF										//"INCLUSÃO DO PARAMETRO: "
					cLogUpdate += STR0109 + ":"				+ aFieldSX6[1,1]		+ _CRLF				//"Filial:	"
					cLogUpdate += STR0130			 		+ AllTrim(aSX6[nX,3])	+ _CRLF  + _CRLF	//"Conteudo: "
	    	    Else
					cNoProcess += "|"+aSX6[nX,1]    	    
					cLogUpdate += STR0134 + aSX6[nX,1]				+ _CRLF				//"PARAMETRO NÃO INCLUIDO: "
					cLogUpdate += STR0109 + ":" + cFilAnt	 		+ _CRLF				//Filial:
					cLogUpdate += STR0132 + AllTrim(aSX6[nX,3])		+ _CRLF 			//"Conteudo Sugerido: "
					cLogUpdate += STR0133 + _CRLF	  				+ _CRLF				//"Usuario optou em não criar o parametro"
	    	    EndIf
			EndIf
		EndIf	
	Next nX
	
	// Atualiza a visualizacao dos conteudos alterados pelo usuario
	For nX:=1 to Len(aSX6)
		If !(AllTrim(aSX6[nX,1]) $ cNoProcess)
			aObj1SX6[nX]:SetText(aSX6[nX,3])
		EndIf	
	Next nX

	Aviso(STR0060,STR0064,{STR0065}) //##"ATENÇÃO"##"Atualização Concluida!"
EndIf	
RestArea(aAreaSX6)
RestArea(aArea)
Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    | WizDbClick  ³ Autor ³TOTVS S.A.          ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Edita o conteudo dos parametros SX6                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function WizDbClick(oBrowse1,aNewSX6,aSX6)
Local nLinha:= oBrowse1:At()
Local cConteudo:= PADR(aNewSX6[nLinha,3],60)
Local oDlgGet

DEFINE DIALOG oDlgGet FROM  010,005 TO 160,430 TITLE &(" ' " + AllTrim(aNewSX6[nLinha,1]) + "' ") PIXEL

TSay():New(012,005,{||STR0057},oDlgGet,,TFont():New('Arial',0,-11,,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,280,100)	//"Informe o Novo Conteudo"
tGet():New(22,005,{|u| Iif( PCount() > 0, cConteudo := u, cConteudo)},oDlgGet,200,,"@!",,,,,,,.T.,,,,,,,,,,"cConteudo")
TButton():Create( TButton():Create( oDlgGet,45,70,"Ok"	,{||aNewSX6[nLinha,3]:=cConteudo,oDlgGet:End()},40,12,,,,.T.,,,,,,),180,270,"Ok",{||aNewSX6[nLinha,3]:=cConteudo,oDlgGet:End()},40,12,,,,.T.,,,,,,)

ACTIVATE DIALOG oDlgGet CENTER

// Atualiza os dados no Browse
oBrowse1:UpdateBrowse() 

// Atualiza o Array de Parametros
aSX6 := aClone(aNewSX6)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    | WizConSP    ³ Autor ³TOTVS S.A.          ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta a utilizacao de stored procedures                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function WizConSP(oScrollBox3,cProcSP)
Local cQuery	   := ''
Local cNome		   := ''
Local cTOP400Alias := ''
Local cMemo1       := ''
Local nPos		   := 0
Local nX		   := 0
Local nLin         := 20
Local aProcBk      := {}
Local aProcessos   := {}
Local cEmp		   := cEmpAnt
Local cDataName	   := Tcgetdb()
Local oMemo1

// Carrega os processos em um vetor para posterior exibicao
LoadProcs(aProcBk)
// Adiciona o codigo do processo ao vetor na ordem correta para exibicao na consulta
For nX := 1 to Len(aProcBk)
	If Alltrim(aProcBk[nX,7])==AllTrim(cProcSP)
		aAdd( aProcessos, {2,aProcBk[nX,2],aProcBk[nX,7],aProcBk[nX,3],aProcBk[nX,4],aProcBk[nX,5],aProcBk[nX,6],aProcBk[nX,7]} )
	EndIf	
Next nX

// Consulta se a procedure foi instalada/versao
If cDataName == "AS400" .or. cDataName == "DB2/400"
	// Identifica nome do Schema ( Alias )
	cTOP400Alias := GetSrvProfString('DBALIAS','')
	If Empty(cTOP400Alias)
		cTOP400Alias := GetSrvProfString('TOPALIAS','')
	EndIf
	If Empty(cTOP400Alias)
		cTOP400Alias := GetPvProfString('TOTVSDBACCESS','ALIAS','',GetAdv97())
	EndIf
	If Empty(cTOP400Alias)
		cTOP400Alias := GetPvProfString('TOPCONNECT','ALIAS','',GetAdv97())
	EndIf
	cQuery := "select SP_NOME,SP_ASSINAT from " + cTOP400Alias + ".TOP_SP where SP_VERSAO = '" + cVersao + "' and RTrim( SP_NOME ) like '%_" + cEmp + "'""
Else
	cQuery := "select SP_NOME,SP_ASSINAT from TOP_SP where SP_VERSAO = '" + cVersao + If(Upper(Trim(TcGetDb())) = "INFORMIX","' and Trim( SP_NOME ) like '%_","' and RTrim( SP_NOME ) like '%_") + cEmp + "'""
EndIf

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery), "TOPSP" )

Do While !TOPSP->( Eof() )
	cNome := Substr(TOPSP->SP_NOME,1,Len(AllTrim(TOPSP->SP_NOME))-3)
	nPos  := Ascan(aProcessos, {|x| AllTrim(x[7])==cNome } )

	If nPos > 0 .And. AllTrim(aProcessos[nPos,8])==AllTrim(cProcSP)
		For nX := 1 To Len(aProcessos)
			If aProcessos[nX,7] == aProcessos[nPos,7]
				aProcessos[nX,1] := iIf( TOPSP->SP_ASSINAT == aProcessos[nX,5], "Verde", "Amarelo" )
				aProcessos[nX,4] := TOPSP->SP_ASSINAT
				If aProcessos[nX,5] <> NIL
					If TOPSP->SP_ASSINAT <> aProcessos[nX,5]
						aProcessos[nX,6] := iIf(  TOPSP->SP_ASSINAT > aProcessos[nX,5], STR0155, STR0156) //'Rotina desatualizada'###'Processo desatualizado'
					Else
						aProcessos[nX,6] := 'Ok'
					EndIf
				Else
					aProcessos[nX,5] := STR0155	//'Rotina desatualizada.
				EndIf
			EndIf
		Next nX
		Exit	
	EndIf
	TOPSP->( dbSkip() )
EndDo
TOPSP->(dbCloseArea())

// Status da Stored Procedure
TSay():New(nLin,010,&("{|| ' " + STR0135 	+ aProcessos[1,2] 	+ "'  }"),oScrollBox3,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)	//"ROTINA: "
nLin += 10
TSay():New(nLin,010,&("{|| ' " + STR0136  	+ aProcessos[1,3] 	+ "'  }"),oScrollBox3,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)	//"Processo: "
nLin += 10
TSay():New(nLin,010,&("{|| ' " + STR0137	+ aProcessos[1,7] 	+ "'  }"),oScrollBox3,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)	//"Stored Procedure: "
nLin += 10
TSay():New(nLin,010,&("{|| ' " + STR0138	+ Iif(Empty(aProcessos[1,4]),STR0149,aProcessos[1,4]) 	+ "'  }"),oScrollBox3,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)	//"Assinatura Procedure: "
nLin += 10
TSay():New(nLin,010,&("{|| ' " + STR0139 	+ aProcessos[1,5] 	+ "'  }"),oScrollBox3,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)	//"Assinatura Rotina: "
nLin += 10
TSay():New(nLin,010,&("{|| ' " + STR0140	+ aProcessos[1,6] 	+ "'  }"),oScrollBox3,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,IiF(AllTrim(aProcessos[1,6])=='Ok',CLR_GREEN,CLR_RED),CLR_WHITE,250,100)	//"STATUS : "
	

If AllTrim(aProcessos[1,6]) == 'Ok'
	nLin += 30
	cMemo1 := STR0144 //" O pacote de stored procedures esta sincronizado com a assinatura da rotina corretamente, caso tenha queda de performance na execução da stored procedure e recomendada a analise de um DBA para verificar a execução da procedure dentro do database."
	TSay():New(nLin,010,&("{|| ' " + STR0145 + "'  }"),oScrollBox3,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)	//"Observação:"
	nLin+=10
	@ nLin,010 GET oMemo1  VAR cMemo1 OF oScrollBox3 MEMO PIXEL SIZE 320,080 FONT (TFont():New('Verdana',,-12,.T.)) NO BORDER	
Else
	nLin += 30
	cMemo1 := STR0146 + _CRLF + _CRLF	//" O pacote de stored procedures NÃO esta sincronizado com a assinatura da rotina, por este motivo a rotina não utilizara as procedures PREJUDICANDO SUA PERFORMANCE."
	cMemo1 += STR0147 + _CRLF			//" Verifique a instalação das procedures/rotina atraves do modulo Configurador (SIGACFG)."
	cMemo1 += STR0148 					//" Opção de Menu ->  Banco de Dados \ Dicionario \ Stored Procedures (CFGX051)"

	TSay():New(nLin,010,&("{|| ' " + STR0145 + "'  }"),oScrollBox3,,TFont():New('Arial',0,-12,.T.,.T.),,,,.T.,CLR_BLACK,CLR_WHITE,250,100)	//"Observação:"
	nLin+=10
	@ nLin,010 GET oMemo1  VAR cMemo1 OF oScrollBox3 MEMO PIXEL SIZE 320,080 FONT (TFont():New('Courier New',,-12,.T.)) NO BORDER	
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    | GravaLog    ³ Autor ³TOTVS S.A.          ³ Data ³28/03/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava o log de Alteracoes                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Versao    ³ P11                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WIZARD PERFORMANCE                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GravaLog(cLogUpdate)

Local cFile := ''
Local cMask      := STR0160 + " (*.LOG) |*.log|"
Local nOcorr     := 0
Local lRet       := .T.

cFile := cGetFile(cMask, '')

If Empty(cFile)
	cFile := 'WIZ'+Right(CriaTrab(, .F.), 4)+'.LOG'
	Do While File(cFile)
		cFile := 'WIZ'+Right(CriaTrab(, .F.), 4)+'.LOG'
	EndDo
	nOcorr := 1
ElseIf !(Upper(Right(cFile, 4))=='.LOG')	
	cFile += '.LOG'
	nOcorr := 2
EndIf

lRet := MemoWrite(cFile, cLogUpdate)

If nOcorr == 1
	Aviso(STR0060,STR0157+cFile+" "+STR0158,{"Ok"})	//"Este LOG foi salvo automaticamente como "##" no diretorio dos SXs."
ElseIf nOcorr == 2
	Aviso(STR0060,STR0159 +' ('+cFile+').',{"Ok"})		//"A extencao '.LOG' foi adicionada ao arquivo, que foi salvo do diretorio escolhido ("
EndIf

Return lRet