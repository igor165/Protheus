#Include "FINA375.CH"
#Include "PROTHEUS.CH"


// 17/08/2009 - Compilacao para o campo filial de 4 posicoes
// 18/08/2009 - Compilacao para o campo filial de 4 posicoes


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ FINA375	³ Autor ³ Mauricio Pequim		³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa para montagem dos titulos de IR - OffLine		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ SigaFin - FINA375										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FinA375()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Vari veis 											 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nOpcao		:= 0
Local cPergunta		:= "AFI375"
Local oDlg
Local aSays			:= {}
Local aButtons		:= {}
Local lEnd			:= .F.

Local aResultado	:= {}
Local cForUniao 	:= ""
Local cLojUniao 	:= ""
Local cNomUniao 	:= ""

Local cLojaImp		:= PadR( "00", TamSX3( "A2_LOJA" )[1], "0" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ aRotina e utilizada noutras rotinas do Siga                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro	:= OemToAnsi(STR0001)  //"Apuracao dos titulos de IR - OffLine"
PRIVATE aFlagCTB	:= {}
PRIVATE lUsaFlag	:= SuperGetMV( "MV_CTBFLAG" , .T. /*lHelp*/, .F. /*cPadrao*/)

PRIVATE aRotina 	:=	{	{ "aRotina Falso", "AxPesq"  , 0, 1 },;
							{ "aRotina Falso", "AxVisual", 0, 2 },;
							{ "aRotina Falso", "AxInclui", 0, 3 },;
							{ "aRotina Falso", "AxAltera", 0, 4 }}					

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa o log de processamento                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ProcLogIni( aButtons )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria as naturezas necessarias               				 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
F375NatNew()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao do Fornecedor Uniao                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SA2")
DbSetOrder(1)
If !DbSeek(xFilial("SA2")+GetMv("MV_UNIAO")+Space(Len(A2_COD)-Len(GetMv("MV_UNIAO")))+cLojaImp)
	Reclock("SA2",.T.)
	SA2->A2_FILIAL	:= xFilial("SA2")
	SA2->A2_COD		:= GetMv("MV_UNIAO")
	SA2->A2_LOJA	:= cLojaImp
	SA2->A2_NOME	:= "UNIAO"
	SA2->A2_NREDUZ	:= "UNIAO"
	SA2->A2_BAIRRO	:= "."
	SA2->A2_MUN		:= "."
	SA2->A2_EST 	:= SuperGetMv("MV_ESTADO")
	SA2->A2_END		:= "."
	SA2->A2_TIPO	:= "."
	MsUnlock()
	FkCommit()
EndIf

cForUniao	:= SA2->A2_COD
cLojUniao	:= SA2->A2_LOJA
cNomUniao	:= SA2->A2_NREDUZ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Estrutura da matriz aResultado                               ³
//³==============================================================³
//³ 1-Fornecedor                                                 ³
//³ 2-Loja                                                       ³
//³ 3-Data do vencimento                                         ³
//³ 4-Valor da base de calculo                                   ³
//³ 5-Valor do Ir ja retido                                      ³
//³ 6-Valor do Ir a recolher                                     ³
//³ 7-Origem dos titulos analisados SE2			                 ³
//³ 8-Registros dos titulos utilizados para a base de calculo    ³
//³ 9-Titulo ja gerado para complemento do IR                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey (VK_F12,{|a,b| AcessaPerg("AFI375",.T.)})

Pergunte(cPergunta,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                        ³
//³=============================================================³
//³ mv_par01 // Da data                                         ³
//³ mv_par02 // Ate a data                                      ³
//³ mv_par03 // Do fornecedor                                   ³
//³ mv_par04 // Ate o fornecedor                                ³
//³ mv_par05 // Da loja                                         ³
//³ mv_par06 // Ate a loja                                 		³
//³ mv_par07 // Simulacao Sim/Nao                               ³
//³ mv_par08 // Data a ser considerada                     		³
//³ mv_par09 // Considerar filiais                         		³
//³ mv_par10 // Da filial                                  		³
//³ mv_par11 // Ate a filial                               		³
//³ mv_par12 // Dt de emissao do titulo a ser gerado       		³
//³ mv_par13 // Vencimento do titulo a ser gerado          		³
//³ mv_par14 // Cria nota de debito                        		³
//³ mv_par15 // Mostra lancamentos contabeis               		³
//³ mv_par16 // Aglutina lancamentos contabeis             		³
//³ mv_par17 // Lancamentos contabeis on-line              		³
//³ mv_par18 // Tipos de Fornecedores:                     		³
//³             Fisica, juridica ou ambas                  		³
//³ mv_par19 // Considerar tabela progressiva de IRRF      		³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Aadd(aSays,OemToAnsi(STR0002)) //"Este programa tem como objetivo gerar titulos a pagar de IR, conforme a soma"
Aadd(aSays,OemToAnsi(STR0003)) //"dos titulos a vencer na mesma data do mesmo fornecedor."

Aadd(aButtons, { 05,.T.,{|| Pergunte(cPergunta,.T. )}})
Aadd(aButtons, { 11,.T.,{|| Fa375TblTmp(), Cfgx011()}})
Aadd(aButtons, { 01,.T.,{|o| nOpcao:=1, o:oWnd:End()}})
Aadd(aButtons, { 02,.T.,{|o| o:oWnd:End()}})

FormBatch(cCadastro,aSays,aButtons,,,465)

If nOpcao == 1

	If !CtbValiDt(,dDataBase,,,,{"FIN001"},)
		Return
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualiza o log de processamento   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ProcLogAtu("INICIO")
		
	If Select("NEWSE2") == 0
		ChkFile("SE2",.F.,"NEWSE2")
	Endif
	
	If MV_PAR01 > MV_PAR02 // Data de/Ate
		Help(" ",1,"F375DATA")
		Set Key VK_F12 To
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o log de processamento com o erro  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ProcLogAtu("ERRO","F375DATA",Ap5GetHelp("F375DATA"))
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o log de processamento   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ProcLogAtu("FIM")
		
		Return
	Endif
	If MV_PAR12 > MV_PAR13 // Emissao/Vencto
		Help(" ",1,"F375DTVENC")
		Set Key VK_F12 To
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o log de processamento com o erro  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ProcLogAtu("ERRO","F375DTVENC",Ap5GetHelp("F375DTVENC"))
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o log de processamento   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ProcLogAtu("FIM")
		
		Return
	Endif
	
	Processa({|lEnd| Fa375Pagar(aResultado)},STR0008)				//"Apuracao do IR Off-Line"
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ MV_PAR07=1 => Simulacao										 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If MV_PAR07 == 1
		If Len(aResultado) >= 1
			Fa375Result(aResultado,cForUniao,cLojUniao,cNomUniao)
		EndIf
	Else
		If Len(aResultado) >= 1
			Fa375Grava(aResultado,cForUniao,cLojUniao,cNomUniao)
		EndIf
	EndIf
	If Select("NEWSE2") > 0
		dbSelectArea("NEWSE2")
		dbCloseArea()
		dbSelectArea("SE2")
		dbSetOrder(1)
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualiza o log de processamento   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ProcLogAtu("FIM")
EndIf

Set Key VK_F12 To

Return(Nil)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Fa375Grava       ³ Mauricio Pequim   	  ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gravacao dos dados nas tabelas envolvidas                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Fa375Grava(ExpA1) 	           									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 - Array contendo dados a serem gravados  na criacao  ³±±
±±³			 ³          dos titulos de IRRF										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA375																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa375Grava(aDados,cForUniao,cLojUniao,cNomUniao)

Local nLaco := 0

For nLaco:=1 To Len(aDados)
	If aDados[nLaco,7] >= 0.01
		Fa375GrvE2(aDados,nLaco,cForUniao,cLojUniao,cNomUniao)
	Elseif aDados[nLaco,7]<= -0.01
		F375GrvE2N(aDados,nLaco,cForUniao,cLojUniao,cNomUniao)
	EndIf
Next

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Fa375Result      ³ Mauricio Pequim   	  ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Simulacao para analise do dados obtidos                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA375Result(ExpA1)				 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 => array que contem os dados dos titulos de IR a se- ³±±
±±³			 ³          rem gerados para simula‡Æo  							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA375																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa375Result(aDados,cForUniao,cLojUniao,cNomUniao)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Vari veis 											           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nUsado	:= 8
Local nConta	:= Len(aDados)
Local nLaco		:= 0
Local nBase		:= 0
Local nRetido	:= 0
Local nRecolher	:= 0
Local nOpcao	:= 1
Local oBmp
Local nA			:= 0
Local oGet
Local aTotais  := {}

Private aHeader	:= {}
Private aCols	:= Array(nConta,nUsado+1)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem do vetor aHeader                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Aadd(aHeader,{STR0032,		"E2_EMIS1",		" ",					08, 00, " ", "    ", "D", ,"V"}) //"Data"
Aadd(aHeader,{STR0017,		"A2_COD",		" ",					06, 00, " ", "    ", "C", ,"V"}) //"Fornecedor    "
Aadd(aHeader,{STR0018,		"A2_LOJA",		"@!",					02, 00, " ", "    ", "C", ,"V"}) //"Loja          "
Aadd(aHeader,{STR0034,		"A2_NUMCON",	"@!",					10, 00, " ", "    ", "C", ,"V"}) //"Imposto       "
Aadd(aHeader,{STR0019,		"E2_VALOR",		"@e 999,999,999.99",	15, 02, " ", "    ", "N", ,"V"}) //"Valor Base    "
Aadd(aHeader,{STR0020,		"E2_VALOR",		"@e 999,999,999.99",	15, 02, " ", "    ", "N", ,"V"}) //"Imposto Retido     "
Aadd(aHeader,{STR0021,		"E2_VALOR",		"@e 999,999,999.99",	15, 02, " ", "    ", "N", ,"V"}) //"Imposto a Recolher "
Aadd(aHeader,{"              ",		"E2_OK",		"!!",					02, 00, " ", "    ", "C", ,"V"})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem do aCols com os valores apurados                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nA:=1 to Len(aDados)
	aCols[nA,1] := aDados[nA,3]						// Data
	aCols[nA,2] := aDados[nA,1]						// Fornecedor
	aCols[nA,3] := aDados[nA,2]						// Loja
	aCols[nA,4] := aDados[nA,4]						// Imposto
	aCols[nA,5] := aDados[nA,5]						// Valor base para montagem do titulo de IR
	aCols[nA,6] := aDados[nA,6]						// Valor ja retido de IR
	aCols[nA,7] := aDados[nA,7]						// Diferenca de IR a recolher
	aCols[nA,8] := Space(02)
	aCols[nA,9] := .F.
	
	nPos := Ascan(aTotais, {|e| Alltrim(e[1]) == aDados[nA,4] } )
	If nPos > 0
		aTotais[nPos,2]	+= aDados[nA,5]
		aTotais[nPos,3]	+= aDados[nA,6]
		aTotais[nPos,4]	+= aDados[nA,7]
	Else
		AADD(aTotais ,{aDados[nA,4],aDados[nA,5],aDados[nA,6],aDados[nA,7]})
	Endif
	
Next nA

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona no aCols a linha de total geral                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nA := 1 to Len (aTotais)
	aadd(aCols,{Space(08)	,;
	STR0022 		,;			//"Total Geral"
	Space(02)	,;
	aTotais[nA,1]	,; 	//Imposto
	aTotais[nA,2]	,;		//Valor Base
	aTotais[nA,3]	,;		//Valor Retido
	aTotais[nA,4]	,;		//Valor a Gerar
	Space(02)		,;
	.F.				})
	
Next

DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0008) FROM 100,001 To 400,740 Of oMainWnd Pixel	//"Apuracao do IR Off-Line"
@ 000, 000 BITMAP oBmp RESNAME "Login" oF oDlg SIZE 50, 200 NOBORDER WHEN .F. PIXEL
oGet := MsGetDados():New(001,055,120,370,2,"Allways True","Allways True","",.F.,,,,200)
oGet:oBrowse:lHscroll := .F.

Define Sbutton From 130,240 Type 1 Action (nOpcao:=1,oDlg:End())		Enable Of oDlg
Define Sbutton From 130,270 Type 2 Action (nOpcao:=0,oDlg:End())		Enable Of oDlg
Define Sbutton From 130,300 Type 6 Action Fr375Rel(aDados)			 	Enable Of oDlg
ACTIVATE MSDIALOG oDlg Centered

If nOpcao==1  // Gravacao dos dados
	Fa375Grava(aDados,cForUniao,cLojUniao,cNomUniao)
EndIf

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Fa375Pagar        ³ Mauricio Pequim  	    ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processamento da analise do contas a pagar                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA375Pagar(ExpA1)					 			 		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array que conter  os dados dos titulos de IRRF	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA375													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa375Pagar(aResultado)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis 											 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nValIR		:= 0
Local nValBase		:= 0
Local nValRetido	:= 0
Local cFornece		:= ""
Local cLoja			:= ""
Local cIndex		:= CriaTrab(,.F.)
Local cChave		:= ""
Local nIndex		:= 0
Local cFiltro		:= ""
Local dData			:= Ctod("//")
Local lProcessar	:= .T.
Local nTitGerado	:= Space(21)
Local aAreaSE2		:= {}
Local nResultado	:= 0
Local nPercIR		:= 0
Local dDtCompara	:= Ctod("//")
Local aTitUsado		:= {}
Local aRegs			:= {}
Local lContinua		:= .T.
Local cTpPessoa		:= ""
Local lTabelaProg	:= .F.
Local nIRReter		:= 0
Local lF375IRPF		:= ExistBlock("F375IRPF")
Local lF375Base		:= ExistBlock("F375Base")
Local dDataIni		:= Ctod("//")
Local dDataFin		:= Ctod("//")
Local nPis5952A		:= 0		//Imposto a gerar
Local nCof5952A		:= 0
Local nCsl5952A		:= 0
Local nPis5979A		:= 0
Local nCof5960A		:= 0
Local nCsl5987A		:= 0
Local nPis5952R		:= 0     //Imposto ja Retido
Local nCof5952R		:= 0
Local nCsl5952R		:= 0
Local nPis5979R		:= 0
Local nCof5960R		:= 0
Local nCsl5987R		:= 0
Local nBase5952		:= 0		//Base dos impostos
Local nBase5979		:= 0
Local nBase5960		:= 0
Local nBase5987		:= 0
Local cCodPis 		:= "5979"			//Codigos de Retencao do imposto
Local cCodCof 		:= "5960"
Local cCodCsl 		:= "5987"
Local aRegs5952		:= {}					//Array para os titulos com retencao 5952
Local aRegs5979		:= {}					//Array para os titulos com retencao 5979
Local aRegs5960		:= {}					//Array para os titulos com retencao 5960
Local aRegs5987 	:= {}					//Array para os titulos com retencao 5987
Local nVlMinPis 	:= SuperGetMv("MV_VRETPIS",.f.,0)
Local nVlMinCof 	:= SuperGetMv("MV_VRETCOF",.f.,0)
Local nVlMinCsl 	:= SuperGetMv("MV_VRETCSL",.f.,0)
Local nTodosImp 	:= 0
Local nTodosMin 	:= (nVlMinPis + nVlMinCof + nVlMinCsl) / 3   //Media dos valores minimos de retencao
Local nValImp	 	:= 0
Local nPis5952T		:= 0     //Imposto ja Retido no titulo
Local nCof5952T		:= 0
Local nCsl5952T		:= 0
Local nPis5979T		:= 0
Local nCof5960T		:= 0
Local nCsl5987T		:= 0
Local nRetTitIr   	:= 0
Local cUniao 		:= PADR(GetMV("MV_UNIAO"),Len(SA2->A2_COD))
Local nA			:= 0
Local nBaseIrrf		:= 0
Local lContrRet 	:= .T.
Local lPCCBaixa 	:= SuperGetMv("MV_BX10925",.T.,"2") == "1" 
Local lRETIR375 	:= ExistBlock("RETIR375")
Local lRet			:= .F.
Local	lAchou 		:= .F.    
Local nRec
Local lAplMinIR 	:= .F. // Carrega variavel de verificacao de consideracao de valor minimo de retencao de IR.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Utiliza o campo: vencto, vencto real ou emissao              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MV_PAR08 == 1
	cChave := "E2_FILIAL+E2_FORNECE+E2_LOJA+Dtos(E2_VENCTO)+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO"
ElseIf MV_PAR08 = 2
	cChave := "E2_FILIAL+E2_FORNECE+E2_LOJA+Dtos(E2_VENCREA)+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO"
ElseIf MV_PAR08 = 3
	cChave := "E2_FILIAL+E2_FORNECE+E2_LOJA+Dtos(E2_EMIS1)+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO"
ElseIf MV_PAR08 = 4
	cChave := "E2_FILIAL+E2_FORNECE+E2_LOJA+Dtos(E2_EMISSAO)+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Selecao do SE2 conforme parametros                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SE2")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se deverar ser considerada filial de/ate            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MV_PAR09 == 2
	cFiltro := "E2_FILIAL == '" + xFilial("SE2") + "'.And."
Else
	cFiltro := "E2_FILIAL >= '" + xFilial("SE2", MV_PAR10) + "'.And. E2_FILIAL <= '" + xFilial("SE2", MV_PAR11 ) + "'.And."
EndIf

cFiltro += "((E2_FORNECE >= '"+MV_PAR03+"'.And. E2_FORNECE  <= '"+MV_PAR04+"').Or."
cFiltro += "(E2_FORNECE=='"+cUniao+"'.And. E2_TIPO == 'TX ')).And."
cFiltro += "E2_LOJA    >= '"+MV_PAR05+"'.And. E2_LOJA     <= '"+MV_PAR06+"'.And."

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Utiliza o campo: vencto, vencto real ou emissao              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MV_PAR08 == 1
	cFiltro += "DTOS(E2_VENCTO) >= '"+Dtos(MV_PAR01)+"'.And. DTOS(E2_VENCTO) <= '"+Dtos(MV_PAR02)+"'"
ElseIf MV_PAR08 == 2
	cFiltro += "DTOS(E2_VENCREA) >= '"+Dtos(MV_PAR01)+"'.And. DTOS(E2_VENCREA) <= '"+Dtos(MV_PAR02)+"'"
ElseIf MV_PAR08 == 3
	cFiltro += "DTOS(E2_EMIS1) >= '"+Dtos(MV_PAR01)+"'.And. DTOS(E2_EMIS1) <= '"+Dtos(MV_PAR02)+"'"
ElseIf MV_PAR08 == 4
	cFiltro += "DTOS(E2_EMISSAO) >= '"+Dtos(MV_PAR01)+"'.And. DTOS(E2_EMISSAO) <= '"+Dtos(MV_PAR02)+"'"
EndIf

IndRegua("SE2",cIndex,cChave,,cFiltro,OemToAnsi(STR0004))  //"Selecionando Registros..."
nIndex := RetIndex("SE2")
#IFNDEF TOP
	DbSetIndex(cIndex+OrdBagExt())
#ENDIF
DbSetOrder(nIndex+1)
DbGoTop()

ProcRegua(RecCount())

dbSelectArea("SA2")
dbSetOrder(1)
SA2->(dbSeek(xfilial("SA2") + mv_par03 + mv_par05,.T.))
While SA2->A2_COD <= mv_par04 .And. SA2->A2_LOJA <= mv_par06 .And. ! SA2->(Eof())
	
	lContinua	:= .T.
	lAchou 		:= .F.
	cTpPessoa	:= ""
	cFornece    := SA2->A2_COD
	cLoja       := SA2->A2_LOJA
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o usuario optou por selecionar o tipo do forne-  ³
	//³ cedor.                                                       ³
	//³ MV_PAR18 (1) - Pessoa fisica                                 ³
	//³ MV_PAR18 (2) - Pessoa juridica                               ³
	//³ MV_PAR18 (3) - Ambas                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	cTpPessoa := SA2->A2_TIPO
	lIRPFBaixa := IIf( cPaisLoc == "BRA", SA2->A2_CALCIRF == "2" .And. SA2->A2_TIPO == "F", .F.) 


	// Verifica se o fornecedor trata o valor minimo de retencao.
	// 1 - Não considera  2 - Considera o parâmetro MV_VLRETIR
	If SA2->A2_MINIRF == "2"
		lAplMinIR := .T.
	EndIf	
		
	If MV_PAR18 == 1 .and. SA2->A2_TIPO <> "F"
		lContinua := .F.
	Endif 
	If MV_PAR18 == 2 .and. SA2->A2_TIPO <> "J"
		lContinua := .F.
	Endif
	
	If !lContinua
		DbSelectArea("SA2")
		DbSkip()
		Loop
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Utiliza o campo: vencto, vencto real ou emissao              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If MV_PAR08 == 1
		dData := SE2->E2_VENCTO
	ElseIf MV_PAR08 == 2
		dData := SE2->E2_VENCREA
	ElseIf MV_PAR08 == 3
		dData := SE2->E2_EMIS1
	ElseIf MV_PAR08 == 4
		dData := SE2->E2_EMISSAO
	EndIf
	dDtCompara	:= Dtos(dData)
	nValIR		:= 0
	nValBase		:= 0
	aValRetido	:= {0,0,0,0,0,0,0}
	lProcessar	:= .T.
	aTitGerado	:= {}
	nResultado	:= 0
	nPercIR		:= 0
	aRegs			:= {}
	nPis5952A	:= 0		//Imposto a gerar
	nCof5952A	:= 0
	nCsl5952A	:= 0
	nPis5979A	:= 0
	nCof5960A	:= 0
	nCsl5987A	:= 0
	nPis5952R	:= 0     //Imposto ja Retido
	nCof5952R	:= 0
	nCsl5952R	:= 0
	nPis5979R	:= 0
	nCof5960R	:= 0
	nCsl5987R	:= 0
	nBase5952	:= 0		//Base dos impostos
	nBase5979	:= 0
	nBase5960	:= 0
	nBase5987	:= 0
	aRegs5952	:= {}    //Titulos p/ retencao de Pis, Confins e Csll - 5952
	aRegs5960	:= {}    //Titulos p/ Cofins
	aRegs5979	:= {}    //Titulos p/ Pis
	aRegs5987	:= {}		//Titulos p/ Csll
	nPis5952T	:= 0     //Imposto ja Retido
	nCof5952T	:= 0
	nCsl5952T	:= 0
	nPis5979T	:= 0
	nCof5960T	:= 0
	nCsl5987T	:= 0
	nRetTitIr   := 0
	nIrReter	:= 0
	
	If cTpPessoa != "F"
		cCondicao := {|| SE2->E2_FORNECE+SE2->E2_LOJA == cFornece+cLoja .and. dDtCompara >= DTOS(mv_par01) .and. dDtCompara <= DTOS(mv_par02)}
	Else
		cCondicao := {|| SE2->E2_FORNECE+SE2->E2_LOJA+Substr(dDtCompara,5,2) == cFornece+cLoja+Substr(Dtos(dData),5,2)}
	Endif
	
	If lIRPFBaixa
		
		aCalcIRPF := F241BsIRPF(0,"SE2",0,.F.,cFornece,cLoja,,FirstDay(mv_par02),.T.)
		
		If aCalcIRPF[1] <> 0 .Or. aCalcIRPF[2] <> 0
			
			//BEGINDOC
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Calcula IRPF e deduz os valores retidos anteriormente³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//ENDDOC*/
			If cTpPessoa == "F"
				nValoIRPF := Fa050TabIR(aCalcIRPF[1]) - aCalcIRPF[2]
			Else
				If lIRPFBaixa
					nValoIRPF := FClcIRPJ(SE2->E2_VALOR,.T.,.T.)
				Else
					nValoIRPF := FCalcIr(aCalcIRPF[1],"J",.T.,.T.,.F.) // - aCalcIRPF[2]
				EndIf
			EndIf
			Aadd(aResultado,{cFornece,cLoja,FirstDay(mv_par02),"IRPF",aCalcIRPF[1],aCalcIRPF[2],nValoIRPF,aCalcIRPF[3]})
			
		EndIf
		
	Else
		DbSelectArea("SE2")
		DbSetOrder(6)   
		nRec := SE2->( Recno () )
		DBSeek(xFilial("SE2")+cFornece+cLoja)
		While !Eof() .And. IIF(mv_par09 == 2 , SE2->E2_FILIAL == xFilial("SE2"),.T.) .AND. Eval(cCondicao)
			
			lAchou := .T.
			
			IncProc(STR0023) //"Selecionado titulos a pagar"
			
			nValRetido := 0	//Irrf
			nPis5952R  := 0	//Pis 5952
			nCof5952R  := 0	//Cofins 5952
			nCsl5952R  := 0	//Csll 5952
			nPis5979R  := 0	//Pis 5979
			nCof5960R  := 0	//Cofins 5960
			nCsl5987R  := 0	//Csll 5987
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se para este fornecedor e neste periodo ha mais que ³
			//³ um titulo gerado de IR, caso exista nao ira processar.       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aCampos := {"SE2->E2_NUMTIT","SE2->E2_TITPIS","SE2->E2_TITCOF","SE2->E2_TITCSL"}
			For nA := 1 to Len(aCampos)
				cTitulo := &(aCampos[nA])
				If !Empty(cTitulo) .And. !(AllTrim(cTitulo) $ ".FINA375.FINA040.")
					If Ascan(aTitGerado,cTitulo) == 0
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Verifica o status do titulo de IR gerado e se tudo estiver   ³
						//³ correto somamos o valor do titulo gerado ao valor de IR      ³
						//³ ja retido.                                                   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						Fa375SitTit( cTitulo, nA, @aValRetido, @aTitGerado )
					Endif
				EndIf
			Next
			//Valores já retidos
			nValRetido += aValRetido[1]	//Irrf
			nPis5952R  += aValRetido[2]	//Pis 5952
			nCof5952R  += aValRetido[3]	//Cofins 5952
			nCsl5952R  += aValRetido[4]	//Csll 5952
			nPis5979R  += aValRetido[5]	//Pis 5979
			nCof5960R  += aValRetido[6]	//Cofins 5960
			nCsl5987R  += aValRetido[7]	//Csll 5987
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Avalia se a tabela progressiva sera utilizada ou nao         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lTabelaProg := .F.
			If cTpPessoa == "F"
				If MV_PAR19 == 1
					lTabelaProg := .T.
				EndIf
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Desconsiderar titulos de abatimento							 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SE2->E2_TIPO $ "TX /"+MVABATIM
				DbSelectArea("SE2")
				DbSetOrder(6)
				DbSkip()
				Loop
			EndIf
			
			DbSelectArea("SED")
			DbSetOrder(1)
			If DbSeek(xFilial("SED")+SE2->E2_NATUREZ)
				If SED->ED_CALCIRF == "S"
					//Ponto de entrada para manipulacao do valor base de calculo do IRRF
					If lF375Base
						nValBase	+= ExecBlock("F375Base",.f.,.f.)
					Else
						nBaseIrrf	:= SE2->(E2_VALOR+E2_IRRF+E2_INSS+E2_ISS+E2_SEST)
						
						If !lPccBaixa
							If lContrRet
								If Empty(SE2->E2_PRETPIS)
									nBaseIrrf += IIF(Empty(SE2->E2_VRETPIS), SE2->E2_PIS , SE2->E2_VRETPIS )
								Endif
								If Empty(SE2->E2_PRETCOF)
									nBaseIrrf += IIF(Empty(SE2->E2_VRETCOF), SE2->E2_COFINS , SE2->E2_VRETCOF )
								Endif
								If Empty(SE2->E2_PRETCSL)
									nBaseIrrf += IIF(Empty(SE2->E2_VRETCSL), SE2->E2_CSLL , SE2->E2_VRETCSL )
								Endif
							Else
								nBaseIrrf += SE2->(E2_PIS+E2_COFINS+E2_CSLL)
							Endif
						Endif
						
						//Se existir redutor da base do IR, calcular nova base
						If cPaisLoc $ "ANG|ARG|AUS|BOL|BRA|CHI|COL|COS|DOM|EQU|EUA|HAI|PAD|PAN|PAR|PER|POR|PTG|SAL|TRI|URU|VEN" .and. SED->ED_BASEIRF > 0
							nBaseIrrf := nBaseIrrf * (SED->ED_BASEIRF/100)
						Endif
						nValBase += nBaseIrrf
					Endif
					nRetTitIr	+= SE2->E2_IRRF
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se o programa deve utilizar a tabela progressiva    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lTabelaProg
						nPercIR := 0
					Else
						nPercIR := IIF(SED->ED_PERCIRF>0,SED->ED_PERCIRF,SuperGetMv("MV_ALIQIRF"))/100
					EndIf
					If ! GetNewPar("MV_RNDIRRF",.F.)
						nIRReter	+=  NoRound(nBaseIrrf * nPercIr,2)
					Else
						nIRReter	+=  Round(nBaseIrrf * nPercIr,2)
					Endif
					Aadd(aRegs,SE2->(Recno()))
				EndIf
				
				//Verifico os campos de impostos e se for pessoa Juridica
				If cTpPessoa != "F" .and. dDataBase < SuperGetMv("MV_RF10925",.t.,CTOD("26/07/04"))
					
					//Fornecedor calcula todos os impostos e a Natureza Idem
					If SA2->A2_RECPIS == "2" .and. SA2->A2_RECCOFI == "2" .and.;
						SA2->A2_RECCSLL == "2" .and. SED->ED_CALCCSL == "S" .and. ;
						SED->ED_CALCPIS == "S" .and. SED->ED_CALCCOF == "S"
						cCodPis	:= "5952"
						cCodCof	:= "5952"
						cCodCsl	:= "5952"
						lRetImp	:= .T.
						nBase	:= SE2->(E2_VALOR+E2_IRRF+E2_INSS+E2_ISS+E2_SEST+E2_PIS+E2_COFINS+E2_CSLL)
						nBase5952 += nBase
					ElseIf SA2->A2_RECPIS == "2" .or. SA2->A2_RECCOFI == "2" .or.;
						SA2->A2_RECCSLL == "2"
						cCodPis	:= "5979"
						cCodCof	:= "5960"
						cCodCsl	:= "5987"
						lRetImp	:= .T.
						nBase	:= SE2->(E2_VALOR+E2_IRRF+E2_INSS+E2_ISS+E2_SEST+E2_PIS+E2_COFINS+E2_CSLL)
					Else
						lRetImp	:= .F.
					Endif
					
					If lRetImp
						If SED->ED_CALCPIS == "S" .and. SA2->A2_RECPIS == "2"
							nValImp := Round( nBase *(SED->ED_PERCPIS/100),2)
							
							If cCodPis == "5952"
								nPis5952T += SE2->E2_PIS
								nPis5952A += nValImp
								Aadd(aRegs5952,SE2->(Recno()))
							Else
								nBase5979 += nBase
								nPis5979T += SE2->E2_PIS
								nPis5979A += nValImp
								Aadd(aRegs5979,SE2->(Recno()))
							Endif
						Endif
						
						If SED->ED_CALCCOF == "S" .and. SA2->A2_RECCOFI == "2"
							nValImp := Round( nBase *(SED->ED_PERCCOF/100),2)
							
							If cCodCof == "5952"
								nCof5952T += SE2->E2_COFINS
								nCof5952A += nValImp
							Else
								nBase5960 += nBase
								nCof5960T += SE2->E2_COFINS
								nCof5960A += nValImp
								Aadd(aRegs5960,SE2->(Recno()))
							Endif
						Endif
						
						If SED->ED_CALCCSL == "S"  .and. SA2->A2_RECCSLL == "2"
							nValImp := Round( nBase *(SED->ED_PERCCSL/100),2)
							lCsll := .T.
							
							If cCodCsl == "5952"
								nCsl5952T += SE2->E2_CSLL
								nCsl5952A += nValImp
							Else
								nBase5987 += nBase
								nCsl5987T += SE2->E2_CSLL
								nCsl5987A += nValImp
								Aadd(aRegs5987,SE2->(Recno()))
							Endif
						Endif
					Endif
				EndIf
			EndIf
			
			DbSelectArea("SE2")
			DbSetOrder(6)
			DbSkip()
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Utiliza o campo: vencto, vencto real ou emissao              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If MV_PAR08 == 1
				dDtCompara := Dtos(SE2->E2_VENCTO)
			ElseIf MV_PAR08 == 2
				dDtCompara := Dtos(SE2->E2_VENCREA)
			ElseIf MV_PAR08 == 3
				dDtCompara := Dtos(SE2->E2_EMIS1)
			ElseIf MV_PAR08 == 4
				dDtCompara := Dtos(SE2->E2_EMISSAO)
			EndIf
			
		EndDo
		SE2->( dbGoto(nRec) )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o processamento podera continuar                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lProcessar
			Exit
		EndIf
		
		If !lAchou // nao achou registro na SE2 para este fornecedor
			DbSelectArea("SA2")
			dbSkip()
			Loop			
		Endif
		
		nValRetido += nRetTitIr //IRRF
		nPis5952R  += nPis5952T	//Pis 5952
		nCof5952R  += nCof5952T	//Cofins 5952
		nCsl5952R  += nCsl5952T	//Csll 5952
		nPis5979R  += nPis5979T	//Pis 5979
		nCof5960R  += nCof5960T	//Cofins 5960
		nCsl5987R  += nCsl5987T	//Csll 5987
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o valor retido ja e maior que a base de dados    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nValRetido < 0
			nValRetido := 0
		EndIf
		
		// Verifica se o fornecedor trata o valor minimo de retencao.- FINANCEIRO
		If (lAplMinIR .And. nIRReter <= GetMv("MV_VLRETIR") ) 
			nIRReter := 0
		EndIf		

				
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Variavel para armazenar o IR a ser retido                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lTabelaProg
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Analise da tab progressiva para verificacao do valor do IRRF ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cTpPessoa == "F"
				nIRReter := NoRound(Fa050TabIr(nValBase),TamSx3("E2_IRRF")[2])
			Else
				nIRReter := NoRound(FCalcIr(nValBase,"J",.T.,.T.,.F.),TamSx3("E2_IRRF")[2])
			EndIf
			If lF375IRPF
				nIRReter	:= ExecBlock("F375IRPF",.f.,.f.,nValBase)
			Endif
			nResultado	:= (nIRReter - nValRetido)
		Else
			nResultado	:= nIrReter - nValRetido
		EndIf
		If (lRETIR375)
			lRet := ExecBlock("RETIR375",.F.,.F.,{cFornece,cLoja,dData,"IRRF",nValBase,nValRetido,nResultado,aRegs,aTitUsado})
		EndIf
		If ((lAplMinIR .and. nIRReter > GetMv("MV_VLRETIR")) .Or. !lAplMinIR) .Or. lRet
			Aadd(aResultado,{cFornece,cLoja,dData,"IRRF",nValBase,nValRetido,nResultado,aRegs,aTitUsado})
		ElseIf lAplMinIR .and. nResultado < 0
			Aadd(aResultado,{cFornece,cLoja,dData,"IRRF",nValBase,nValRetido,nResultado,aRegs,aTitUsado})	
		Endif
	
		If nIRReter > GetMv("MV_VLRETIR") .Or. lRet
			Aadd(aResultado,{cFornece,cLoja,dData,"IRRF",nValBase,nValRetido,nResultado,aRegs,aTitUsado})
		Endif
			
		//Verifico os campos de impostos e se for pessoa Juridica
		If cTpPessoa != "F"  .and. dDataBase < SuperGetMv("MV_RF10925",.t.,CTOD("26/07/04"))
			
			//Pis, Cofins e Csll ao mesmo tempo - CodRet 5952
			If nBase5952 > 0
				nTodosImp += nPis5952A + nCof5952A + nCSL5952A
				nTodosImpL := nPis5952A + nCof5952A + nCSL5952A - nPis5952R - nCof5952R - nCSL5952R
				lVlrMin		:= nTodosImp > nTodosMin //Atingiu minimo de recolhimento
				If nTodosImp > nTodosMin
					nResultado := 0
					If lVlrMin
						nResultado := nPis5952A - nPis5952R
					Endif
					If nPis5952R > 0 .or. nResultado > 0
						Aadd(aResultado,{cFornece,cLoja,dData,"PIS 5952",nBase5952,nPis5952R,nResultado,aRegs5952})
					Endif
					If lVlrMin
						nResultado := nCof5952A - nCof5952R
					Endif
					If nCof5952R > 0 .or. nResultado > 0
						Aadd(aResultado,{cFornece,cLoja,dData,"COFINS 5952",nBase5952,nCof5952R,nResultado,aRegs5952})
					Endif
					If lVlrMin
						nResultado := nCSL5952A - nCSL5952R
					Endif
					If nCSL5952R > 0 .or. nResultado > 0
						Aadd(aResultado,{cFornece,cLoja,dData,"CSLL 5952",nBase5952,nCsl5952R,nResultado,aRegs5952})
					Endif
				Endif
			Endif
			//PIS
			If nBase5979 > 0
				nResultado := nPis5979A - nPis5979R
				If nResultado <= nVlMinPis
					nResultado := 0
				Endif
				If nPis5979R > 0 .or. nResultado > 0
					Aadd(aResultado,{cFornece,cLoja,dData,"PIS 5979",nBase5979,nPis5979R,nResultado,aRegs5979})
				Endif
			Endif
			//COFINS
			If nBase5960 > 0
				nResultado := nCof5960A - nCof5960R
				If nResultado <= nVlMinCof
					nResultado := 0
				Endif
				If nCof5960R > 0 .or. nResultado > 0
					Aadd(aResultado,{cFornece,cLoja,dData,"COFINS 5960",nBase5960,nCof5960R,nResultado,aRegs5960})
				Endif
			Endif
			//CSLL
			If nBase5987 > 0
				nResultado := nCSL5987A - nCSL5987R
				If nResultado <= nVlMinCsl
					nResultado := 0
				Endif
				IF nCsl5987R > 0 .or. nResultado > 0
					Aadd(aResultado,{cFornece,cLoja,dData,"CSLL 5987",nBase5987,nCsl5987R,nResultado,aRegs5987})
				Endif
			Endif
		Endif
		
		
	EndIf
	
	DbSelectArea("SA2")
	dbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da tabela de titulos a pagar          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SE2")
Set Filter To
RetIndex("SE2")
DbSetOrder(1)
DbSeek(xFilial())

Return(Nil)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Fa375SitTit      ³ Mauricio Pequim       ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica a situacao do titulo de IR gerado            	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA375SitTit(ExpC1,ExpN1,ExpA1)							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1  - Titulo a ser pesquisado:                    	      ³±±
±±³          ³			  SE2-Prefixo+Numero+Parcela+Tipo+Fornecedor+Loja ³±±
±±³          ³ExpN1  - Totalizador de IR ja retido.                       ³±±
±±³          ³ExpA1  - Vetor que contem os titulos ja utilizados na soma  ³±±
±±³          ³ 			do valor retido.                    		      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA375													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa375SitTit(cNumTit,nCol,aValRetido,aTitUsado,l5952)

Local aArea			:= {}
Local lProcessar	:= .T.
Local cFilAtu		:= cFilAnt
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o registro de IR gerado sofreu alguma alteracao  ³
//³ por parte do usuario.                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("NEWSE2")
aArea := GetArea()
If !Empty(xFilial("SE2"))
	cFilAnt := Substr(cNumTit,1,2)
Endif
DbSetOrder(1)
If DbSeek(cNumTit)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se os dados nao foram gravados por outro modulo.    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(NEWSE2->E2_ORIGEM) .And. !(Upper(AllTrim(NEWSE2->E2_ORIGEM)) $ ".FINA375.FINA040.")
		lProcessar := .F.
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o titulo nao esta em bordero.                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(NEWSE2->E2_NUMBOR)
		lProcessar := .F.
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se data do movimento n„o ‚ menor que data limite de ³
	//³ movimentacao no financeiro.                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If NEWSE2->E2_TIPO $ MVPAGANT
		If !DtMovFin(,,"1")
			lProcessar := .F.
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se nao ‚ um titulo de ISS, IR ou INSS               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If NEWSE2->E2_TIPO $ "ISS#TX #INS#TXA"
		If Fa050Pai()
			lProcessar := .F.
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se foi emitido cheque para este titulo.             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If NEWSE2->E2_IMPCHEQ == "S"
		lProcessar := .F.
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se foi emitido cheque para um dos titulos de        ³
	//³ impostos e verifica a delecao do titulo pai.                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Fa050VerImp()
		lProcessar := .F.
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desconsiderar titulos de abatimento								  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If NEWSE2->E2_TIPO $ MVABATIM
		lProcessar := .F.
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Soma o valor do titulo ao valor de IR retido                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aScan(aTitUsado,NEWSE2->(E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)) == 0
		Aadd(aTitUsado,NEWSE2->(E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA))
		If lProcessar
			//IRRF
			If nCol == 1
				If NEWSE2->E2_TIPO == "TXA"
					aValRetido[1] -= NEWSE2->E2_VALOR
				Else
					aValRetido[1] += NEWSE2->E2_VALOR
				Endif
			ElseIf nCol == 2 //PIS
				If NEWSE2->E2_CODRET == "5952"
					aValRetido[2] += NEWSE2->E2_VALOR
				Else
					aValRetido[5] += NEWSE2->E2_VALOR
				Endif
			ElseIf nCol == 3 //COFINS
				If NEWSE2->E2_CODRET == "5952"
					aValRetido[3] += NEWSE2->E2_VALOR
				Else
					aValRetido[6] += NEWSE2->E2_VALOR
				Endif
			ElseIf nCol == 4 //CSLL
				If NEWSE2->E2_CODRET == "5952"
					aValRetido[4] += NEWSE2->E2_VALOR
				Else
					aValRetido[7] += NEWSE2->E2_VALOR
				Endif
			Endif
		EndIf
	EndIf
EndIf
cFilAnt:= cFilAtu
RestArea(aArea)
Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Fa375GrvE2       ³ Mauricio Pequim       ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Gravacao dos titulos no SE2                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA375GrvE2(ExpA1,ExpN1,ExpC1) 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array contendo os dados do titulo a ser gravado    ³±±
±±³			 ³ ExpN1 = N£mero do registro no array								  ³±±
±±³			 ³ ExpC1 = Titulo ja gerado para complemento do IR				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FinA375 				                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa375GrvE2(aRegistro,nLinha,cForUniao,cLojUniao,cNomUniao)

Local cParcela		:= TamParcela("E2_PARCELA",Space(1),Space(2),Space(3))
Local cNumTit		:= ""
Local cTipoSE2		:= "TX "
Local cNatureza	:= &(GetMv("MV_IRF"))
Local cNatNDF		:= &(GetMv("MV_NATNDF",.F.,'"NDF"'))
Local cPrefixo		:= ""
Local cFornece		:= ""
Local cLoja			:= ""
Local cNomeReduz	:= ""
Local cArquivo
Local cPadrao		:= "510"
Local lPadrao		:= .T.
Local lDigita		:= .T.
Local lAglutina		:= .T.
Local nTotal		:= 0
Local nHdlPrv
Local nA	:= 0
Local aTamParc 	:= TamSx3("E2_PARCELA")
Local cNatDesc		:= " "
Local cTitGerado	:= ""
Local lAtuSldNat 	:= .T.

Private cLote		:= ""
LoteCont("FIN")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Analise dos codigos das naturezas envolvidas                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Substr(aRegistro[nLinha,4],1,3) $ "PIS"
	cNatureza	:= GetMv("MV_PISNAT",.F.,"PIS")
	cNatureza	:= Iif(Empty(cNatureza),,cNatureza)
ElseIf Substr(aRegistro[nLinha,4],1,3) $ "COF"
	cNatureza	:= GetMv("MV_COFINS",.F.,"COF")
	cNatureza	:= Iif(Empty(cNatureza),"COF",cNatureza)
ElseIf Substr(aRegistro[nLinha,4],1,3) $ "CSL"
	cNatureza	:= GetMv("MV_CSLL",.F.,"CSL")
	cNatureza	:= Iif(Empty(cNatureza),"CSL",cNatureza)
	cDescNat		:= "CSLL"
Else
	cNatureza	:= Iif(Empty(cNatureza),"IRF",cNatureza)
	cDescNat		:= "IRRF"
Endif
cNatNDF		:= Iif(Empty(cNatNDF),"NDF",cNatNDF)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena informacoes quanto aos titulos originais            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nA:= Len(aRegistro[nLinha,8])
DbSelectArea("SE2")
DbGoTo(aRegistro[nLinha,8,nA])
cNumTit	:= SE2->E2_NUM
cPrefixo	:= SE2->E2_PREFIXO
cParcela	:= SE2->E2_PARCELA
cFornece	:= SE2->E2_FORNECE
cLoja		:= SE2->E2_LOJA

DbSelectArea("SA2")
DbSetOrder(1)
If DbSeek(xFilial()+cFornece+cLoja)
	cNomeReduz := SA2->A2_NREDUZ
Else
	cNomeReduz := ""
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o numero do titulo ja existe                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SE2")
DbSetOrder(1)
While .T.
	If DbSeek(xFilial("SE2")+cPrefixo+cNumTit+cParcela+cTipoSE2+cForUniao+cLojUniao)
		While !Eof() .and. xFilial("SE2")+cPrefixo+cNumTit+cParcela+cTipoSE2+cForUniao+cLojUniao == ;
			SE2->(E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)
			cParcela := StrZero(Max(Val(SE2->E2_PARCELA),Val(cParcela)),aTamParc[1])
			dbSkip()
		Enddo
		cParcela := Soma1(cParcela)
	Else
		Exit
	Endif
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gravacao do titulo de IR apurado                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RecLock("SE2",.T.)
SE2->E2_FILIAL		:= xFilial("SE2")
SE2->E2_PREFIXO	:= cPrefixo
SE2->E2_NUM			:= cNumTit
SE2->E2_PARCELA	:= cParcela
SE2->E2_TIPO		:= cTipoSE2
SE2->E2_EMISSAO	:= MV_PAR12
SE2->E2_VALOR		:= aRegistro[nLinha,7]
SE2->E2_VENCREA	:= DataValida(MV_PAR13,.T.)
SE2->E2_SALDO		:= aRegistro[nLinha,7]
SE2->E2_VENCTO		:= MV_PAR13
SE2->E2_VENCORI	:= MV_PAR13
SE2->E2_MOEDA		:= 1
SE2->E2_EMIS1		:= dDataBase
SE2->E2_FORNECE	:= cForUniao
SE2->E2_LOJA		:= cLojUniao
SE2->E2_NOMFOR		:= cNomUniao
SE2->E2_VLCRUZ		:= aRegistro[nLinha,7]
SE2->E2_ORIGEM		:= " "
SE2->E2_NATUREZ	:= cNatureza
SE2->E2_NUMTIT		:= "FINA375"
SE2->E2_MULTNAT	:= "2"
SE2->E2_FILORIG := cFilAnt

If Alltrim(aRegistro[nLinha,4]) != "IRRF"
	SE2->E2_CODRET := Right(aRegistro[nLinha,4],4)
Else
	If ExistBlock("F375IRF")
		SE2->E2_CODRET := ExecBlock("F375IRF",.F.,.F.)
	Endif
Endif
SE2->E2_DIRF		:= If(Empty(SE2->E2_CODRET),"2","1")

If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil
	aAdd( aFlagCTB, {"E2_LA", "S", "SE2", SE2->( Recno() ), 0, 0, 0} )
EndIf

If ExistBlock("F375IRFC")
	ExecBlock("F375IRFC",.F.,.F.)
Endif

MsUnlock()

If lAtuSldNat
	//Atualizo o valor atual para o saldo da natureza
	AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA,"2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, If(SE2->E2_TIPO $ MVABATIM, "+", "-"),,FunName(),"SE2",SE2->(Recno()),0)
EndIf

FkCommit()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contabilizacao do titulo de IR                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lPadrao		:= VerPadrao(cPadrao)
lDigita		:= Iif(MV_PAR15==1,.T.,.F.)
lAglutina 	:= Iif(MV_PAR16==1,.T.,.F.)

If lPadrao .and. mv_par17 == 1   // Contabiliza On-Line
	nHdlPrv	:= HeadProva( 	cLote,;
							"FINA375",;
							Substr(cUsuario,7,6),;
							@cArquivo)
							
	nTotal += DetProva( nHdlPrv,;
	                    cPadrao,;
	                    "FINA375",;
	                    cLote,;
	                    /*nLinha*/,;
	                    /*lExecuta*/,;
	                    /*cCriterio*/,;
	                    /*lRateio*/,;
	                    /*cChaveBusca*/,;
	                    /*aCT5*/,;
	                    /*lPosiciona*/,;
	                    @aFlagCTB,;
	                    /*aTabRecOri*/,;
	                    /*aDadosProva*/ )

	RodaProva(  nHdlPrv,;
				nTotal)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia para Lancamento Contabil                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cA100Incl( cArquivo,;
	           nHdlPrv,;
	           3,;
	           cLote,;
	           lDigita,;
	           lAglutina,;
	           /*cOnLine*/,;
	           /*dData*/,;
	           /*dReproc*/,;
	           @aFlagCTB,;
	           /*aDadosProva*/,;
	           /*aDiario*/ )
	aFlagCTB := {}  // Limpa o coteudo apos a efetivacao do lancamento

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contabilizacao On-Line                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nTotal > 0 .AND. !lUsaFlag
		DbSelectArea("SE2")
		RecLock("SE2")
		SE2->E2_LA := "S "
		MsUnlock()
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza os titulos originais                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nA:=1 To Len(aRegistro[nLinha,8])
	DbSelectArea("SE2")
	DbGoTo(aRegistro[nLinha,8,nA])
	If "IRRF" $ aRegistro[nLinha,4]
		If Empty(SE2->E2_NUMTIT)
			RecLock("SE2",.F.)
			SE2->E2_NUMTIT := xFilial()+cPrefixo+cNumTit+cParcela+cTipoSE2+cForUniao+cLojUniao
			MsUnlock()
		Endif
	ElseIf "PIS" $ aRegistro[nLinha,4]
		If Empty(SE2->E2_TITPIS)
			RecLock("SE2",.F.)
			SE2->E2_TITPIS := xFilial()+cPrefixo+cNumTit+cParcela+cTipoSE2+cForUniao+cLojUniao
			MsUnlock()
		Endif
	ElseIf "COFINS" $ aRegistro[nLinha,4]
		If Empty(SE2->E2_TITCOF)
			RecLock("SE2",.F.)
			SE2->E2_TITCOF := xFilial()+cPrefixo+cNumTit+cParcela+cTipoSE2+cForUniao+cLojUniao
			MsUnlock()
		Endif
	ElseIf "CSLL" $ aRegistro[nLinha,4]
		If Empty(SE2->E2_TITCSL)
			RecLock("SE2",.F.)
			SE2->E2_TITCSL := xFilial()+cPrefixo+cNumTit+cParcela+cTipoSE2+cForUniao+cLojUniao
			MsUnlock()
		Endif
	Endif
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Implantacao do titulo NDF                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MV_PAR14 == 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o numero do titulo ja existe                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SE2")
	DbSetOrder(1)
	While .T.
		cTitGerado := xFilial()+cPrefixo+cNumTit+cParcela+"NDF"+SA2->A2_COD+SA2->A2_LOJA
		If DbSeek(xFilial("SE2")+cTitGerado)
			While !Eof() .and. xFilial("SE2")+cTitGerado == ;
				SE2->(E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)
				cParcela := StrZero(Max(Val(SE2->E2_PARCELA),Val(cParcela)),aTamParc[1])
				dbSkip()
			Enddo
			cParcela := Soma1(cParcela)
		Else
			Exit
		Endif
	EndDo
	RecLock("SE2",.T.)
	SE2->E2_FILIAL		:= xFilial("SE2")
	SE2->E2_PREFIXO	:= cPrefixo
	SE2->E2_NUM			:= cNumTit
	SE2->E2_PARCELA	:= cParcela
	SE2->E2_TIPO		:= "NDF"
	SE2->E2_EMISSAO	:= MV_PAR12
	SE2->E2_VALOR		:= aRegistro[nLinha,7]
	SE2->E2_VENCREA	:= DataValida(MV_PAR13,.T.)
	SE2->E2_SALDO		:= aRegistro[nLinha,7]
	SE2->E2_VENCTO		:= MV_PAR13
	SE2->E2_VENCORI	:= MV_PAR13
	SE2->E2_MOEDA		:= 1
	SE2->E2_EMIS1		:= dDataBase
	SE2->E2_FORNECE	:= cFornece
	SE2->E2_LOJA		:= cLoja
	SE2->E2_NOMFOR		:= cNomeReduz
	SE2->E2_VLCRUZ		:= aRegistro[nLinha,7]
	SE2->E2_ORIGEM		:= " "
	SE2->E2_NATUREZ	:= cNatNDF
	SE2->E2_NUMTIT		:= "FINA375"
	SE2->E2_MULTNAT	:= "2"
	SE2->E2_FILORIG := cFilAnt
	
	If ExistBlock("F375NDFC")
		ExecBlock("F375NDFC",.F.,.F.)
	Endif
	
	If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil
		aAdd( aFlagCTB, {"E2_LA", "S", "SE2", SE2->( Recno() ), 0, 0, 0} )
	EndIf

	MsUnlock()
	FKCommit()
	
	If lAtuSldNat
		//Atualizo o valor atual para o saldo da natureza
		AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA,"2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, If(SE2->E2_TIPO $ MVABATIM, "+", "-"),,FunName(),"SE2",SE2->(Recno()),0)
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contabilizacao do titulo de NDF                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lPadrao		:= VerPadrao(cPadrao)
	lDigita		:= Iif(MV_PAR15==1,.T.,.F.)
	lAglutina 	:= Iif(MV_PAR16==1,.T.,.F.)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contabilizacao On-Line                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lPadrao .and. mv_par17 == 1  // Contabiliza on-line
		nHdlPrv	:= HeadProva(	cLote,;
								"FINA375",;
								Substr(cUsuario,7,6),;
								@cArquivo)
								
		nTotal += DetProva( nHdlPrv,;
		                    cPadrao,;
		                    "FINA375",;
		                    cLote,;
		                    /*nLinha*/,;
		                    /*lExecuta*/,;
		                    /*cCriterio*/,;
		                    /*lRateio*/,;
		                    /*cChaveBusca*/,;
		                    /*aCT5*/,;
		                    /*lPosiciona*/,;
		                    @aFlagCTB,;
		                    /*aTabRecOri*/,;
		                    /*aDadosProva*/ )

		RodaProva(  nHdlPrv,;
					nTotal)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Envia para Lancamento Contabil                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cA100Incl( cArquivo,;
		           nHdlPrv,;
		           3,;
		           cLote,;
		           lDigita,;
		           lAglutina,;
		           /*cOnLine*/,;
		           /*dData*/,;
		           /*dReproc*/,;
		           @aFlagCTB,;
		           /*aDadosProva*/,;
		           /*aDiario*/ )
		aFlagCTB := {}  // Limpa o coteudo apos a efetivacao do lancamento

		If nTotal > 0 .AND. !lUsaFlag
			DbSelectArea("SE2")
			RecLock("SE2")
			SE2->E2_LA := "S "
			MsUnlock()
		EndIf
	EndIf
EndIf
Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ F375GRVE2N       ³ Mauricio Pequim       ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Gravacao dos titulos no SE2                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F375GRVE2N(ExpA1,ExpN1,ExpC1) 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array contendo os dados do titulo a ser gravado    ³±±
±±³			 ³ ExpN1 = N£mero do registro no array								  ³±±
±±³			 ³ ExpC1 = Titulo ja gerado para complemento do IR				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FinA375 				                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F375GrvE2N (aRegistro,nLinha,cForUniao,cLojUniao,cNomUniao)

Local cParcela		:= TamParcela("E2_PARCELA",Space(1),Space(2),Space(3))
Local cNumTit		:= ""
Local cTpSE2TXA	:= "TXA"
Local cNatureza	:= &(GetMv("MV_IRF"))
Local cNatNCF		:=  (GetNewPar("MV_NATNCF",.F.,"NCF"))
Local cPrefixo		:= ""
Local cFornece		:= ""
Local cLoja			:= ""
Local cNomeReduz	:= ""
Local cArquivo
Local cPadrao		:= "510"
Local lPadrao		:= .T.
Local lDigita		:= .T.
Local lAglutina		:= .T.
Local nTotal		:= 0
Local nHdlPrv
Local nA	:= 0
Local aTamParc 	:= TamSx3("E2_PARCELA")
Local cNatDesc		:= " "
Local cTitGerado	:= ""
Local lAtuSldNat 	:= .T.

Private cLote		:= ""
LoteCont("FIN")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Analise dos codigos das naturezas envolvidas                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Substr(aRegistro[nLinha,4],1,3) $ "PIS"
	cNatureza	:= GetMv("MV_PISNAT",.F.,"PIS")
	cNatureza	:= Iif(Empty(cNatureza),,cNatureza)
ElseIf Substr(aRegistro[nLinha,4],1,3) $ "COF"
	cNatureza	:= GetMv("MV_COFINS",.F.,"COF")
	cNatureza	:= Iif(Empty(cNatureza),"COF",cNatureza)
ElseIf Substr(aRegistro[nLinha,4],1,3) $ "CSL"
	cNatureza	:= GetMv("MV_CSLL",.F.,"CSL")
	cNatureza	:= Iif(Empty(cNatureza),"CSL",cNatureza)
	cDescNat		:= "CSLL"
Else
	cNatureza	:= Iif(Empty(cNatureza),"IRF",cNatureza)
	cDescNat		:= "IRRF"
Endif
cNatNCF		:= Iif(Empty(cNatNCF),"NCF",cNatNCF)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena informacoes quanto aos titulos originais            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nA:= Len(aRegistro[nLinha,8])
DbSelectArea("SE2")
DbGoTo(aRegistro[nLinha,8,nA])
cNumTit	:= SE2->E2_NUM
cPrefixo	:= SE2->E2_PREFIXO
cParcela	:= SE2->E2_PARCELA
cFornece	:= SE2->E2_FORNECE
cLoja		:= SE2->E2_LOJA

DbSelectArea("SA2")
DbSetOrder(1)
If DbSeek(xFilial()+cFornece+cLoja)
	cNomeReduz := SA2->A2_NREDUZ
Else
	cNomeReduz := ""
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o numero do titulo ja existe                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SE2")
DbSetOrder(1)
While .T.
	If DbSeek(xFilial("SE2")+cPrefixo+cNumTit+cParcela+cTpSE2TXA+cForUniao+cLojUniao)
		While !Eof() .and. xFilial("SE2")+cPrefixo+cNumTit+cParcela+cTpSE2TXA+cForUniao+cLojUniao == ;
			SE2->(E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)
			cParcela := StrZero(Max(Val(SE2->E2_PARCELA),Val(cParcela)),aTamParc[1])
			dbSkip()
		Enddo
		cParcela := Soma1(cParcela)
	Else
		Exit
	Endif
EndDo


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gravacao do titulo de IR apurado                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RecLock("SE2",.T.)
SE2->E2_FILIAL		:= xFilial("SE2")
SE2->E2_PREFIXO	:= cPrefixo
SE2->E2_NUM			:= cNumTit
SE2->E2_PARCELA	:= cParcela
SE2->E2_TIPO		:= cTpSE2TXA
SE2->E2_EMISSAO	:= MV_PAR12
SE2->E2_VALOR		:= abs(aRegistro[nLinha,7])
SE2->E2_VENCREA	:= DataValida(MV_PAR13,.T.)
SE2->E2_SALDO		:= abs(aRegistro[nLinha,7])
SE2->E2_VENCTO		:= MV_PAR13
SE2->E2_VENCORI	:= MV_PAR13
SE2->E2_MOEDA		:= 1
SE2->E2_EMIS1		:= dDataBase
SE2->E2_FORNECE	:= cForUniao
SE2->E2_LOJA		:= cLojUniao
SE2->E2_NOMFOR		:= cNomUniao
SE2->E2_VLCRUZ		:= abs(aRegistro[nLinha,7])
SE2->E2_ORIGEM		:= " "
SE2->E2_NATUREZ	:= cNatureza
SE2->E2_NUMTIT		:= "FINA375"
SE2->E2_MULTNAT	:= "2"
SE2->E2_FILORIG := cFilAnt

If Alltrim(aRegistro[nLinha,4]) != "IRRF"
	SE2->E2_CODRET := Right(aRegistro[nLinha,4],4)
Else
	If ExistBlock("F375IRF")
		SE2->E2_CODRET := ExecBlock("F375IRF",.F.,.F.)
	Endif
Endif
SE2->E2_DIRF		:= If(Empty(SE2->E2_CODRET),"2","1")

If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil
	aAdd( aFlagCTB, {"E2_LA", "S", "SE2", SE2->( Recno() ), 0, 0, 0} )
EndIf

If ExistBlock("F375CIRF")
	ExecBlock("F375CIRF",.F.,.F.)
Endif

MsUnlock()
FkCommit()

If lAtuSldNat
	//Atualizo o valor atual para o saldo da natureza
	AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA,"2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, If(SE2->E2_TIPO $ MVABATIM, "+", "-"),,FunName(),"SE2",SE2->(Recno()),0)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contabilizacao do titulo de IR                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lPadrao		:= VerPadrao(cPadrao)
lDigita		:= Iif(MV_PAR15==1,.T.,.F.)
lAglutina 	:= Iif(MV_PAR16==1,.T.,.F.)

If lPadrao .and. mv_par17 == 1   // Contabiliza On-Line
	nHdlPrv	:= HeadProva(	cLote,;
							"FINA375",;
							Substr(cUsuario,7,6),;
							@cArquivo)

	nTotal += DetProva( nHdlPrv,;
	                    cPadrao,;
	                    "FINA375",;
	                    cLote,;
	                    /*nLinha*/,;
	                    /*lExecuta*/,;
	                    /*cCriterio*/,;
	                    /*lRateio*/,;
	                    /*cChaveBusca*/,;
	                    /*aCT5*/,;
	                    /*lPosiciona*/,;
	                    @aFlagCTB,;
	                    /*aTabRecOri*/,;
	                    /*aDadosProva*/ )

	RodaProva(  nHdlPrv,;
				nTotal)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia para Lancamento Contabil                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cA100Incl( cArquivo,;
	           nHdlPrv,;
	           3,;
	           cLote,;
	           lDigita,;
	           lAglutina,;
	           /*cOnLine*/,;
	           /*dData*/,;
	           /*dReproc*/,;
	           @aFlagCTB,;
	           /*aDadosProva*/,;
	           /*aDiario*/ )
	aFlagCTB := {}  // Limpa o coteudo apos a efetivacao do lancamento
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contabilizacao On-Line                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nTotal > 0 .AND. !lUsaFlag
		DbSelectArea("SE2")
		RecLock("SE2")
		SE2->E2_LA := "S "
		MsUnlock()
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza os titulos originais                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nA:=1 To Len(aRegistro[nLinha,8])
	DbSelectArea("SE2")
	DbGoTo(aRegistro[nLinha,8,nA])
	If "IRRF" $ aRegistro[nLinha,4]
		If Empty(SE2->E2_NUMTIT)
			RecLock("SE2",.F.)
			SE2->E2_NUMTIT := xFilial()+cPrefixo+cNumTit+cParcela+cTpSE2TXA+cForUniao+cLojUniao
			MsUnlock()
		Endif
	ElseIf "PIS" $ aRegistro[nLinha,4]
		If Empty(SE2->E2_TITPIS)
			RecLock("SE2",.F.)
			SE2->E2_TITPIS := xFilial()+cPrefixo+cNumTit+cParcela+cTpSE2TXA+cForUniao+cLojUniao
			MsUnlock()
		Endif
	ElseIf "COFINS" $ aRegistro[nLinha,4]
		If Empty(SE2->E2_TITCOF)
			RecLock("SE2",.F.)
			SE2->E2_TITCOF := xFilial()+cPrefixo+cNumTit+cParcela+cTpSE2TXA+cForUniao+cLojUniao
			MsUnlock()
		Endif
	ElseIf "CSLL" $ aRegistro[nLinha,4]
		If Empty(SE2->E2_TITCSL)
			RecLock("SE2",.F.)
			SE2->E2_TITCSL := xFilial()+cPrefixo+cNumTit+cParcela+cTpSE2TXA+cForUniao+cLojUniao
			MsUnlock()
		Endif
	Endif
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Implantacao do titulo NDF                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MV_PAR14 == 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o numero do titulo ja existe                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SE2")
	DbSetOrder(1)
	While .T.
		cTitGerado := xFilial()+cPrefixo+cNumTit+cParcela+"NDF"+SA2->A2_COD+SA2->A2_LOJA
		If DbSeek(xFilial("SE2")+cTitGerado)
			While !Eof() .and. xFilial("SE2")+cTitGerado == ;
				SE2->(E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)
				cParcela := StrZero(Max(Val(SE2->E2_PARCELA),Val(cParcela)),aTamParc[1])
				dbSkip()
			Enddo
			cParcela := Soma1(cParcela)
		Else
			Exit
		Endif
	EndDo
	RecLock("SE2",.T.)
	SE2->E2_FILIAL		:= xFilial("SE2")
	SE2->E2_PREFIXO	:= cPrefixo
	SE2->E2_NUM			:= cNumTit
	SE2->E2_PARCELA	:= cParcela
	SE2->E2_TIPO		:= "NCF"
	SE2->E2_EMISSAO	:= MV_PAR12
	SE2->E2_VALOR		:= abs(aRegistro[nLinha,7])
	SE2->E2_VENCREA	:= DataValida(MV_PAR13,.T.)
	SE2->E2_SALDO		:= abs(aRegistro[nLinha,7])
	SE2->E2_VENCTO		:= MV_PAR13
	SE2->E2_VENCORI	:= MV_PAR13
	SE2->E2_MOEDA		:= 1
	SE2->E2_EMIS1		:= dDataBase
	SE2->E2_FORNECE	:= cFornece
	SE2->E2_LOJA		:= cLoja
	SE2->E2_NOMFOR		:= cNomeReduz
	SE2->E2_VLCRUZ		:= abs(aRegistro[nLinha,7])
	SE2->E2_ORIGEM		:= " "
	SE2->E2_NATUREZ	:= cNatNCF
	SE2->E2_NUMTIT		:= "FINA375"
	SE2->E2_MULTNAT	:= "2"
	SE2->E2_FILORIG := cFilAnt

	If ExistBlock("F375CNCF")
		ExecBlock("F375CNCF",.F.,.F.)
	Endif
	
	If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil
		aAdd( aFlagCTB, {"E2_LA", "S", "SE2", SE2->( Recno() ), 0, 0, 0} )
	EndIf

	MsUnlock()
	FKCommit()
	
	If lAtuSldNat
		//Atualizo o valor atual para o saldo da natureza
		AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA,"2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, If(SE2->E2_TIPO $ MVABATIM, "+", "-"),,FunName(),"SE2",SE2->(Recno()),0)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contabilizacao do titulo de NDF                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lPadrao		:= VerPadrao(cPadrao)
	lDigita		:= Iif(MV_PAR15==1,.T.,.F.)
	lAglutina 	:= Iif(MV_PAR16==1,.T.,.F.)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contabilizacao On-Line                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lPadrao .and. mv_par17 == 1  // Contabiliza on-line
		nHdlPrv	:= HeadProva(	cLote,;
								"FINA375",;
								Substr(cUsuario,7,6),;
								@cArquivo)

	nTotal += DetProva( nHdlPrv,;
	                    cPadrao,;
	                    "FINA375",;
	                    cLote,;
	                    /*nLinha*/,;
	                    /*lExecuta*/,;
	                    /*cCriterio*/,;
	                    /*lRateio*/,;
	                    /*cChaveBusca*/,;
	                    /*aCT5*/,;
	                    /*lPosiciona*/,;
	                    @aFlagCTB,;
	                    /*aTabRecOri*/,;
	                    /*aDadosProva*/ )

		RodaProva(  nHdlPrv,;
					nTotal)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Envia para Lancamento Contabil                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cA100Incl( cArquivo,;
		           nHdlPrv,;
		           3,;
		           cLote,;
		           lDigita,;
		           lAglutina,;
		           /*cOnLine*/,;
		           /*dData*/,;
		           /*dReproc*/,;
		           @aFlagCTB,;
		           /*aDadosProva*/,;
		           /*aDiario*/ )
		aFlagCTB := {}  // Limpa o coteudo apos a efetivacao do lancamento

		If nTotal > 0 .AND. !lUsaFlag
			DbSelectArea("SE2")
			RecLock("SE2")
			SE2->E2_LA := "S "
			MsUnlock()
		EndIf
	EndIf
EndIf
Return(Nil)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Fr375Rel         ³ Mauricio Pequim       ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Impressao do relatorio analitico para conferencia          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Fr375Rel(ExpA1) 														  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array contendo dados dos titulos apurados de IR 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA375				                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fr375Rel(aDados)

Local cDesc1	:= STR0024 //"Este relatorio ir  demonstrar os titulos que foram utilizados para "
Local cDesc2	:= STR0025 //"para a montagem da base de calculo do IR a recolher"
Local cDesc3	:= ""
Local wNrel
Local Tamanho	:= "G"
Local nA		:= 0
Local nB		:= 0
Local CbCont	:= 0
Local CbTxt		:= Space(10)
Local cString	:= "SE2"
Local nColPrefixo
Local nColNumero
Local nColParcela
Local nColTipo
Local nColEmissao
Local nColVencto
Local nColVencRea
Local nColValor
Local nColFornec
Local nColLoja
Local nColNome
Local nColRetido
Local nColRec
Local nColIrTit
Local nSubTit			:= 0
Local aTotais			:= {	{"IRRF     ",0,0,0} ,;
{"Ret. 5952",0,0,0} ,;
{"PIS      ",0,0,0} ,;
{"Cofins   ",0,0,0} ,;
{"CSLL     ",0,0,0} }

Local nBase 	:= 0
Local nTotGerTit := 0
Local nTotGerRet := 0
Local nTotGerRec := 0
Local lContrRet := .T.
Local lPCCBaixa := SuperGetMv("MV_BX10925",.T.,"2") == "1"  

Private Li			:= 80
Private M_pag		:= 1
Private Titulo		:= STR0026 //"Resultado Analitico para a conferencia - Carteira Pagar"
Private cabec1		:= ""
Private cabec2		:= ""
Private aReturn	:= {STR0027, 1, STR0028, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
Private nomeprog	:= "FINA375"
Private nLastKey	:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel := "FR375Rel"
wnrel := SetPrint(cString,wNrel,,titulo,cDesc1,cDesc2,cDesc3,.F.,"",.T.,Tamanho,"",.F.)
If nLastKey == 27
	Return(Nil)
EndIf

SetDefault(aReturn,cString)
If nLastKey == 27
	Return(Nil)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Considerar filiais                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MV_PAR09 == 1
	nColPrefixo	:= 9
	nColNumero	:= 18
	nColParcela	:= 26
	nColTipo		:= 32
	nColEmissao	:= 38
	nColVencto	:= 50
	nColVencRea	:= 62
	nColValor	:= 74
	nColFornec	:= 89
	nColLoja		:= 100
	nColNome		:= 105
	nColIrTit	:= 128
	nColRetido	:= 140
	nColRec		:= 152
	
	Cabec1 := STR0029	 //"Filial  Prefixo  Numero  Parc  Tipo  Emissao     Vencimento  Venc Real           Valor  Fornecedor               IR Titulo  IR Retido  IR Recolher"
	
	//" 			 Filial  Prefixo  Numero  Parc  Tipo  Emissao     Vencimento  Venc Real           Valor  Fornecedor                              IR Titulo   IR Retido IR Recolher"
	//           12      123      123456  1     123   99/99/9999  99/99/9999  99/99/9999  99,999,999.99  1234567890-1234 12345678901234567890   999,999.99  999,999.99  999,999.99
	//           1       9        18      26    32    38          50          62          74             89         100  105                    128         140         152
	
Else
	nColPrefixo	:= 1
	nColNumero	:= 10
	nColParcela	:= 18
	nColTipo		:= 24
	nColEmissao	:= 30
	nColVencto	:= 42
	nColVencRea	:= 54
	nColValor	:= 66
	nColFornec	:= 81
	nColLoja		:= 92
	nColNome		:= 97
	nColIrTit	:= 119
	nColRetido	:= 132
	nColRec		:= 145
	
	Cabec1 := STR0030 //" Prefixo  Numero  Parc  Tipo  Emissao     Vencimento  Venc Real           Valor  Fornecedor                    IR Titulo    IR Retido  IR Recolher"
	
	//" 			 Prefixo  Numero  Parc  Tipo  Emissao     Vencimento  Venc Real           Valor  Fornecedor                             IR Titulo    IR Retido  IR Recolher"
	//           123      123456  1     123   99/99/9999  99/99/9999  99/99/9999  99,999,999.99  1234567890-1234 12345678901234567890   999,999.99  999,999.99   999,999.99
	//           1        10      18    24    30          42          54          66             81         92   97                     119         132			 145
	
EndIf

Cabec2 := ""
If Len(aDados) > 0
	For nA:=1 To Len(aDados)
		If Li >= 58
			Cabec(Titulo,Cabec1,Cabec2,NomeProg,Tamanho,Iif(aReturn[4]==1,15,18))
			Li := Prow()+1
		EndIf
		nSubTit := 0
		For nB:=1 To len(aDados[nA,8])
			If Li >= 58
				Cabec(Titulo,Cabec1,Cabec2,NomeProg,Tamanho,Iif(aReturn[4]==1,15,18))
				Li := Prow()+1
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Posiciona na tabela de titulos a pagar                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("SE2")
			DbSetOrder(1)
			DbGoTo(aDados[nA,8,nB])
			
			If aDados[nA,4] == "IRPF" .And. ! SE2->E2_TIPO $ MVPAGANT
				If SE2->E2_SALDO > 0
					nBase	:= SE2->(E2_VALOR - E2_SALDO + E2_INSS + E2_ISS + E2_SEST)
				Else
					nBase	:= SE2->(E2_VALOR + E2_INSS + E2_ISS + E2_SEST)
				EndIf
			Else
				nBase	:= SE2->(E2_VALOR+E2_IRRF+E2_INSS+E2_ISS+E2_SEST)
			EndIf
			
			If !lPccBaixa
				If lContrRet
					If Empty(SE2->E2_PRETPIS)
						nBase += IIF(Empty(SE2->E2_VRETPIS), SE2->E2_PIS , SE2->E2_VRETPIS )
					Endif
					If Empty(SE2->E2_PRETCOF)
						nBase += IIF(Empty(SE2->E2_VRETCOF), SE2->E2_COFINS , SE2->E2_VRETCOF )
					Endif
					If Empty(SE2->E2_PRETCSL)
						nBase += IIF(Empty(SE2->E2_VRETCSL), SE2->E2_CSLL , SE2->E2_VRETCSL )
					Endif
				Else
					nBase += SE2->(E2_PIS+E2_COFINS+E2_CSLL)
				Endif
			Endif
			
			If MV_PAR09 == 1
				@Li,001 PSAY SE2->E2_FILIAL
			EndIf
			@Li,nColPrefixo	PSAY SE2->E2_PREFIXO
			@Li,nColNumero		PSAY SE2->E2_NUM
			@Li,nColParcela	PSAY SE2->E2_PARCELA
			@Li,nColTipo		PSAY SE2->E2_TIPO
			@Li,nColEmissao	PSAY SE2->E2_EMIS1						Picture "@d"
			@Li,nColVencto		PSAY SE2->E2_VENCTO						Picture "@d"
			@Li,nColVencRea	PSAY SE2->E2_VENCREA						Picture "@d"
			@Li,nColValor		PSAY nBase					Picture "@e 99,999,999.99"
			@Li,nColFornec		PSAY SE2->E2_FORNECE
			@Li,nColLoja		PSAY SE2->E2_LOJA
			@Li,nColNome		PSAY SubStr(SE2->E2_NOMFOR,1,20)
			
			
			If "IRRF,IRPF" $ aDados[nA,4]
				@Li,nColIrTit		PSAY SE2->E2_IRRF							Picture "@e 999,999.99"
				nSubTit		+= SE2->E2_IRRF
			Endif
			If "PIS" $ aDados[nA,4]
				@Li,nColIrTit		PSAY SE2->E2_PIS							Picture "@e 999,999.99"
				nSubTit		+= SE2->E2_PIS
			Endif
			If "COFINS" $ aDados[nA,4]
				@Li,nColIrTit		PSAY SE2->E2_COFINS						Picture "@e 999,999.99"
				nSubTit		+= SE2->E2_COFINS
			Endif
			If "CSLL" $ aDados[nA,4]
				@Li,nColIrTit		PSAY SE2->E2_CSLL							Picture "@e 999,999.99"
				nSubTit		+= SE2->E2_CSLL
			Endif
			Li++
		Next nB
		
		If Li >= 58
			Cabec(Titulo,Cabec1,Cabec2,NomeProg,Tamanho,Iif(aReturn[4]==1,15,18))
			Li := Prow()+1
		EndIf
		@Li,030					PSAY STR0033 +" -->> "+aDados[nA,4]	//"Sub-Total"
		@Li,nColValor			PSAY aDados[nA,5]		Picture "@e 99,999,999.99"
		
		@ Li,nColIrTit			PSAY nSubTit			Picture "@e 999,999.99"
		@ Li,nColRetido		PSAY aDados[nA,6]	- nSubTit	Picture "@e 999,999.99"
		@Li,nColRec				PSAY aDados[nA,7]		Picture "@e 999,999.99"
		
		If "IRRF" $ aDados[nA,4]
			aTotais[1,2] += nSubTit               		//Imposto no titulo
			aTotais[1,3] += aDados[nA,6]	- nSubTit	//Imposto ja Retido
			aTotais[1,4] += aDados[nA,7]				   //Imposto a recolher
			
		ElseIf "PIS 5952" $ aDados[nA,4] .or. "COFINS 5952" $ aDados[nA,4] .or.;
			"CSLL 5952" $ aDados[nA,4]
			aTotais[2,2] += nSubTit
			aTotais[2,3] += aDados[nA,6]	- nSubTit
			aTotais[2,4] += aDados[nA,7]
			
		ElseIf "PIS" $ aDados[nA,4]
			aTotais[3,2] += nSubTit
			aTotais[3,3] += aDados[nA,6]	- nSubTit
			aTotais[3,4] += aDados[nA,7]
			
		ElseIf "COFINS" $ aDados[nA,4]
			aTotais[4,2] += nSubTit
			aTotais[4,3] += aDados[nA,6]	- nSubTit
			aTotais[4,4] += aDados[nA,7]
			
		ElseIf "CSLL" $ aDados[nA,4]
			aTotais[5,2] += nSubTit
			aTotais[5,3] += aDados[nA,6]	- nSubTit
			aTotais[5,4] += aDados[nA,7]
		Endif
		Li += 2
	Next nA
	
	For nA := 1 to Len(aTotais)
		If Li >= 58
			Cabec(Titulo,Cabec1,Cabec2,NomeProg,Tamanho,Iif(aReturn[4]==1,15,18))
			Li := Prow()+1
		EndIf
		If aTotais[nA,2]+aTotais[nA,3]+aTotais[nA,4] > 0
			@Li,030					PSAY STR0031+ "-->> "+ aTotais[nA,1] //"TOTAL"
			@Li,nColIrTit			PSAY aTotais[nA,2]	Picture "@e 999,999.99"
			@Li,nColRetido			PSAY aTotais[nA,3]	Picture "@e 999,999.99"
			@Li,nColRec				PSAY aTotais[nA,4]	Picture "@e 999,999.99"
			
			nTotGerTit += aTotais[nA,2]
			nTotGerRet += aTotais[nA,3]
			nTotGerRec += aTotais[nA,4]
			Li++
		Endif
	Next
	
	If Li >= 58
		Cabec(Titulo,Cabec1,Cabec2,NomeProg,Tamanho,Iif(aReturn[4]==1,15,18))
		Li := Prow()+1
	EndIf
	
	Li++
	@Li,030					PSAY STR0022 //"TOTAL GERAL"
	@Li,nColIrTit			PSAY nTotGerTit	Picture "@e 999,999.99"
	@Li,nColRetido			PSAY nTotGerRet	Picture "@e 999,999.99"
	@Li,nColRec				PSAY nTotGerRec	Picture "@e 999,999.99"
	Li++
	
	
	Roda(CbCont,CbTxt,Tamanho)
	
Endif

If aReturn[5] = 1
	Set Printer To
	DbCommitAll()
	OurSpool(wnrel)
EndIf

MS_FLUSH()

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ F375NatNew       ³ Mauricio Pequim       ³ Data ³ 05/06/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Criacao nas naturezas necessarias no IR OffLine            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F375NatNew()						 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA375      			                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F375NatNew()

Local cDescNat := ""
Local cNatureza:= ""
Local nA := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria a natureza de IRRF                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nA := 1 to 5
	DO CASE
		CASE nA == 1	//IRRF
			cNatureza	:= Alltrim(&(GetMv("MV_IRF",.F.,'"IRF"')))
			cNatureza	:= If (Empty(cNatureza),"IRF",cNatureza)
			cDescNat		:= OemToAnsi(STR0011)  // "IMPOSTO DE RENDA RETIDO NA FONTE"
		CASE nA == 2	//NDF
			cNatureza	:= Alltrim(&(GetMv("MV_NATNDF",.F.,'"NDF"')))
			cNatureza	:= If (Empty(cNatureza),"NDF",cNatureza)
			cDescNat := OemToAnsi(STR0014)  // "NOTA DE DEBITO AO FORNECEDOR"
		CASE nA == 3	//PIS
			cNatureza	:= GetMv("MV_PISNAT",.F.,"PIS")
			cNatureza	:= If (Empty(cNatureza),"PIS",cNatureza)
			cDescNat		:= "PIS"
		CASE nA == 4	//Cofins
			cNatureza	:= GetMv("MV_COFINS",.F.,"COF")
			cNatureza	:= If (Empty(cNatureza),"COF",cNatureza)
			cDescNat		:= "COFINS"
		CASE nA == 5	//CSLL
			cNatureza	:= GetMv("MV_CSLL",.F.,"CSL")
			cNatureza	:= If (Empty(cNatureza),"CSL",cNatureza)
			cDescNat		:= "CSLL"
	ENDCASE
	
	DbSelectArea("SED")
	cNatureza := AllTrim(cNatureza)
	cNatureza := cNatureza+(Space(10-Len(cNatureza)))
	If ( ! DbSeek( cFilial + cNatureza ) )
		RecLock("SED",.T.)
		SED->ED_FILIAL  := cFilial
		SED->ED_CODIGO  := cNatureza
		SED->ED_CALCIRF := "N"
		SED->ED_CALCISS := "N"
		SED->ED_CALCINS := "N"
		SED->ED_CALCCOF := "N"
		SED->ED_CALCPIS := "N"
		SED->ED_CALCCSL := "N"
		SED->ED_DESCRIC := cDescNat 
		SED->ED_TIPO	:= "2"
		MsUnlock()
		FkCommit()
	EndIf
Next

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Fa375TblTmp      ³ Mauricio Pequim       ³ Data ³ 23.08.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Verifica se o arquivo temporario da tabela progressiva de  ³±±
±±³          ³ IR ja esta aberta.                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA375TblTmp() 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA375   				                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa375TblTmp()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Esta tabela e aberta na rotina Cfgx011()                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Select("cArqTmp") > 0
	DbSelectArea("cArqTmp")
	DbCloseArea()
EndIf

Return(Nil)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ F375Week			  ³ Mauricio Pequim       ³ Data ³ 06.11.03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Identifica data incial e final da semana para apuracao do  ³±±
±±³          ³ IR de pessoa juridica.                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F375Week()   															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA375   				                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function f375Week(dData,dDataIni,dDataFin)
Local nDiaSemana := Dow(dData)

//Acho o dia inicio da semana
dDataIni := DTOS(dData-(nDiaSemana-1))
//Acho o dia final da semana
dDataFin := DTOS(dData+(7-nDiaSemana))

Return
Return
