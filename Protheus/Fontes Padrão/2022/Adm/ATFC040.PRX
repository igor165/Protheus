#INCLUDE "ATFC040.CH"
#include "Protheus.ch"

STATIC lIsRussia	:= If(cPaisLoc$"RUS",.T.,.F.) // CAZARINI - Flag to indicate if is Russia location

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFC040    ³ Autor ³ Cesar C S Prado       ³ Data ³ 13/05/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Consulta Ficha de Ativo                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ATFC040
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aPos:= {  8,  4, 11, 74 }
lCab     := .T.
Private aRotina := MenuDef()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas                                         ³
//³ MV_PAR01 - Moeda 2                                                        ³
//³ MV_PAR02 - Moeda 3                                                        ³
//³ MV_PAR03 - Moeda 4                                                        ³
//³ MV_PAR04 - Moeda 5                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("AFC040",.F.)

cMoedas:=""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro := OemToAnsi(STR0003) // "Consulta de Ficha de Ativos"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse( 6, 1,22,75,"SN1")

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AFC040FICH ³ Autor ³ Cesar C S Prado       ³ Data ³ 13.05.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina para exibir a ficha do Ativo selecionado              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AFC040FICH(ExpC1,ExpN1)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                     ³±±
±±³          ³ ExpN1 = Numero do registro                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AFC040FICH(cAlias,nReg,nOpc)

LOCAL oDlg ,cTitulo := OemToAnsi(STR0006) // "Ficha do Ativo"
LOCAL oFont
LOCAL lWHEN :=.T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Salva a integridade dos dados                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cChapa   := Subs(N1_CHAPA + Space(12),1,12)
Local cGrupo   := Subs(N1_GRUPO + Space(02),1, 6)
Local cLocal   := Subs(N1_LOCAL + Space(12),1,12)
Local cDtAquis := DtoC(N1_AQUISIC)
Local cDtBx    := DtoC(N1_BAIXA)
Local cProjeto := Subs(N1_PROJETO+Space(15),1,15)
Local cFornec  := N1_FORNEC+"-"+N1_LOJA
Local cNF      := N1_NFISCAL+"/"+SerieNfId(cAlias,2,'N1_NSERIE')
Local cQuant   := Transform(N1_QUANTD, "@E 9999,999.9999")
Local cSeguro  := Subs(N1_CSEGURO +Space(30),1,30)
Local cApolice := Subs(N1_APOLICE+Space(15),1,20)
Local cVencSeg := DtoC(N1_DTVENC)
Local cTipoSeg := Subs(N1_TIPOSEG+Space(10), 1,10)
Local cTipo    := ""
Local lPrimeiro := .T.
Local cCr       := Chr(13)+Chr(10)
Local cChave   := ""
Local cIndex   := ""
Local nIndex   := ""
Local nValorResi:= 0
Local nMoeda
Local nTamanho  := TamSX3("N3_CCONTAB")[1]  

Local cTypes10	:= IIF(lIsRussia,"*" + AtfNValMod({1}, "*"),"") // CAZARINI - 29/03/2017 - If is Russia, add new valuations models - main models
Local cTypesNM	:= IIF(lIsRussia,"*" + AtfNValMod({3,4}, "*"),"") // CAZARINI - 29/03/2017 - If is Russia, add new valuations models - 17 and 16 models
Private cPicture1 := PesqPict("SN3","N3_VORIG1", 20)
Private cPicture2 := PesqPict("SN3","N3_VORIG2", 20)
Private cPicture3 := PesqPict("SN3","N3_VORIG3", 20)
Private cPicture4 := PesqPict("SN3","N3_VORIG4", 20)
Private cPicture5 := PesqPict("SN3","N3_VORIG5", 20)

cMoedas := "1"
cMoedas += IIf(MV_PAR01 == 1, "2", "")
cMoedas += IIf(MV_PAR02 == 1, "3", "")
cMoedas += IIf(MV_PAR03 == 1, "4", "")
cMoedas += IIf(MV_PAR04 == 1, "5", "")

mCampo := OemToAnsi(STR0005)  + cCr //"Codigo      Item  Descricao                       Aquisicao  Baixa    " 
mCampo += OemToAnsi("------      ----  ------------------------------  ---------  --------")   +cCr
mCampo += 				N1_CBASE+"  "+N1_ITEM+"  "+Subs(N1_DESCRIC,1,30)+"  "+cDtAquis+"  "+cDtBx +cCr

dbSelectArea("SN3")
DbSetOrder(1)
dbSeek(xFilial()+SN1->N1_CBASE+SN1->N1_ITEM)

dbSelectArea("SN2")
dbSetOrder(1)
If dbSeek(xFilial()+SN1->N1_CBASE+SN1->N1_ITEM)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta express„o da Indregua                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cIndex := CriaTrab(nil,.f.)
	cChave  := "N2_FILIAL+N2_CBASE+N2_ITEM+N2_TIPO+N2_SEQ"
	IndRegua("SN2",cIndex,cChave,,,"Selecionando Registros...")
	nIndex := RetIndex("SN2")
	dbSelectArea("SN2")
	#IFNDEF TOP
		dbSetIndex(cIndex+OrdBagExt())
	#ENDIF
	dbSetOrder(nIndex+1)
	DbGoTop()

	dbSeek(xFilial()+SN3->N3_CBASE+SN3->N3_ITEM)
	While xFilial() == SN3->N3_FILIAL .and. ;
		SN2->N2_CBASE  == SN3->N3_CBASE  .and. ;
		SN2->N2_ITEM   == SN3->N3_ITEM .And. !Eof()
		If lPrimeiro
			lPrimeiro := .F.
			mCampo += cCr
			mCampo += cCr
			mCampo += OemtoAnsi(STR0007) // "Descricao:  "
		Endif
		mCampo += Space(12)+SN2->N2_HISTOR + cCr
		SN2->( dbSkip() )
	EndDo
	RetIndex("SN2")
	dbSetOrder(1)
	Set Filter to
	FErase (cIndex+OrdBagExt())
Endif
dbSelectArea("SA2")
dbSetOrder(1)
If dbSeek(xFilial()+SN1->N1_FORNEC)
	cFornec := cFornec + "   "+ SA2->A2_NOME
Else
	cFornec := cFornec + Space(43)
Endif
dbSelectArea("SN1")
dbSetOrder(1)
mCampo += cCr                       // xxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxx  xxxxxx
mCampo += OemToAnsi(STR0008) + cCr //"Fornecedor   Nome Fornecedor                           Nota Fisc.  Quantd.        Placa     "
mCampo +=                   OemToAnsi("----------  ----------------------------------------  ----------  -------------  --------  ") + cCr
mCampo += 				                   cFornec+                                         "  " +cNF+"  "+cQuant+"  "+N1_PLACA+"  " + cCr
mCampo += cCr
mCampo += OemToAnsi(STR0009) + cCr // "No. Chapa    Grupo  Local.Fisica  Projeto          "
mCampo += OemToAnsi("------------ ------ ------------  ---------------  ") + cCr
mCampo +=				cChapa+" "+cGrupo+" "+cLocal+"  "+cProjeto           + cCr
mCampo += cCr
mCampo += OemToAnsi(STR0010) + cCr // "Companhia Seguradora           Numero da apolice    Vencto.  Tipo Seg."
mCampo += OemToAnsi("------------------------------ -------------------- -------- ---------") + cCr
mCampo += 				cSeguro+" "+cApolice+" "+cVencSeg+" "+cTipoSeg  +cCr

dbSelectArea("SN3")
DBSetOrder(1)
dbSeek(xFilial("SN3")+SN1->N1_CBASE+SN1->N1_ITEM)

While !Eof() .And. N3_CBASE+N3_ITEM = SN1->N1_CBASE+SN1->N1_ITEM
	cTipo:=N3_TIPO+" - " +	IIF(N3_TIPO $ ("01*10*16*17" + cTypes10 + cTypesNM), 	OemToAnsi(STR0011),; 	// "AQUISICAO    " 
									IIF(N3_TIPO = "02", 	OemToAnsi(STR0012),; 	// "REAVALIACAO  "
									IIF(N3_TIPO = "03", 	OemToAnsi(STR0013),; 	// "ADIANTAMENTO "
									IIF(N3_TIPO = "50", 	OemToAnsi(STR0029),; 	// "REAVALIACAO  "
									IIF(N3_TIPO = "51", 	OemToAnsi(STR0030),; 	// "ADIANTAMENTO "
									IIF(N3_TIPO = "52", 	OemToAnsi(STR0031),; 	// "ADIANTAMENTO "
									IIF(N3_TIPO = "53", 	OemToAnsi(STR0032),; 	// "REAVALIACAO  "
									IIF(N3_TIPO = "54", 	OemToAnsi(STR0033),; 	// "ADIANTAMENTO "
																OemToAnsi(STR0014))))))))) 	// "LEI 8200     "

	mCampo += cCr
	mCampo += OemToAnsi(STR0015) + cCr // "Tipo                Historico")
	mCampo += OemToAnsi("------------------  ----------------------------------------") + cCr
	mCampo += cTipo+"  "+N3_HISTOR + cCr
	mCampo += cCr
	mCampo += Space(20) + OemToAnsi(STR0016)+Mascara(N3_CCONTAB,,,nTamanho+5) + cCr // "Conta do Bem ...........: "
	mCampo += Space(20) + OemToAnsi(STR0017)+Mascara(N3_CCORREC,,,nTamanho+5) + cCr // "Conta Correcao Monet. ..: "
	mCampo += Space(20) + OemToAnsi(STR0018)+Mascara(N3_CDEPREC,,,nTamanho+5) + cCr // "Conta Desp. Deprec. ....: "
	mCampo += Space(20) + OemToAnsi(STR0019)+Mascara(N3_CCDEPR,,,nTamanho+5) + cCr // "Conta Deprec. Acumulada : "
	mCampo += Space(20) + OemToAnsi(STR0020)+Mascara(N3_CDESP,,,nTamanho+5) + cCr // "Conta Corr. Depreciacao : "
	For nMoeda:=1 To 5
		cMoeda:=Str(nMoeda,1)
		If cMoeda == "1"
			nValorResi:= Abs(SN3->N3_VORIG1+SN3->N3_AMPLIA1+SN3->N3_VRCACM1) -;
							(SN3->N3_VRDACM1+SN3->N3_VRCDA1)
			If SN3->N3_VORIG1+SN3->N3_AMPLIA1+SN3->N3_VRCACM1 < 0
				nValorResi *= -1
			Endif
		Else
			nValorResi := (&('SN3->N3_VORIG'+cMoeda)+&('SN3->N3_AMPLIA'+cMoeda))-;
							&('SN3->N3_VRDACM'+cMoeda)
			If Abs(&('SN3->N3_VORIG'+cMoeda)+&('SN3->N3_AMPLIA'+cMoeda)) < 0
				nValorResi *= -1
			Endif
		Endif
		IF cMoeda $ cMoedas
			mCampo += cCr
			mCampo += PadC(OemToAnsi(STR0021)+ALLTRIM(GetMV("MV_SIMB"+cMoeda))+" ***",80)+ cCr // "*** Valores em "
			mCampo += cCr
			mCampo += 	OemToAnsi(STR0022)+; // "Vlr.Original "
							Transform(N3_VORIG&cMoeda, cPicture&cMoeda)+;
							OemToAnsi(STR0023) + Transform(N3_TXDEPR&cMoeda, "@E 999.9999")+; 	// "  Tx.Depr. "
							OemToAnsi(STR0024) + Transform(N3_INDICE&cMoeda, "@E 999.9999") + cCr // " Indice "
			mCampo += OemToAnsi(STR0028) + Transform(nValorResi, cPicture&cMoeda)
			mCampo += cCr
			mCampo += OemToAnsi(STR0027) + Transform(N3_AMPLIA&cMoeda, cPicture&cMoeda) + cCr     //"Vlr Ampliacao  "
			mCampo += cCr
			mCampo += OemToAnsi(STR0025) + cCr // "                     Correcao           Depreciacao  Corr. da Depreciacao"
			mCampo += OemToAnsi("         --------------------  --------------------  --------------------") + cCr
			mCampo += OemToAnsi(STR0026)+; //"NO MES   "
									Transform(IIf(nMoeda = 1, N3_VRCMES1,0),	cPicture&cMoeda)+"  "+;
									Transform(N3_VRDMES&cMoeda,					cPicture&cMoeda)+"  "+;
									Transform(IIf(nMoeda = 1, N3_VRCDM1,0) ,	cPicture&cMoeda) + cCr
			mCampo += OemToAnsi("EXERC.   ")+;
									Transform(IIf(nMoeda = 1, N3_VRCBAL1,0),	cPicture&cMoeda)+"  "+;
									Transform(N3_VRDBAL&cMoeda,					cPicture&cMoeda)+"  "+;
									Transform(IIf(nMoeda = 1, N3_VRCDB1,0) ,	cPicture&cMoeda) + cCr
			mCampo += OemToAnsi("ACUMUL.  ")+;
									Transform(IIf(nMoeda = 1, N3_VRCACM1,0),	cPicture&cMoeda)+"  "+;
									Transform(N3_VRDACM&cMoeda,					cPicture&cMoeda)+"  "+;
									Transform(IIf(nMoeda = 1, N3_VRCDA1,0) ,	cPicture&cMoeda) + cCr
		End
	Next 
	dbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Direciona a impressao para arquivo temporario                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SN1")

	DEFINE FONT oFont NAME "Courier New" SIZE 6, 15
	DEFINE MSDIALOG oDlg FROM  120,15 TO 421,696 TITLE cTitulo PIXEL FONT oFont
  	DEFINE SBUTTON FROM 128,240 ENABLE OF oDlg TYPE 1 ACTION (oDlg:End())
	@ 18,20 GET oHelp VAR mCampo size 300,100 of oDlg MEMO PIXEL When lWhen READONLY
	oHelp:bRClicked:={||AlwaysTrue()}			
	oHelp:oFont:=oFont
	ACTIVATE MSDIALOG oDlg
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ana Paula N. Silva     ³ Data ³30/11/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina := { { OemToAnsi(STR0001),"AxPesqui"  , 0 , 1},; // "Pesquisar"
					 { OemToAnsi(STR0035),"CTBC662"   , 0 , 7},; // "Tracker Contábil"     
                     { OemToAnsi(STR0002),"AFC040FICH", 0 , 4} } // "Ficha Ativo"
Return(aRotina)
