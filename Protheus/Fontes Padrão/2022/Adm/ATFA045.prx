#include "atfa045.ch"
#include "Protheus.ch"
#INCLUDE 'FWMVCDEF.CH'

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.

//********************************
// Controle de multiplas moedas  *
//********************************
Static lMultMoed := .t.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFA045    ³ Autor ³ Claudio D. de Souza   ³ Data ³ 16/02/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Baixa de adiantamentos sem calculo da depreciacao da baixa   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Ativo Fixo                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFA045( )
Local dDateDescon := CtoD("04/04/2022") 
Local dDateAtual := GetRmtDate() //Pega data do windows.
Local alink	   := {{"Saiba mais!","https://tdn.totvs.com/pages/viewpage.action?pageId=612017168"}}

If !Isblind()
	DESHELP045("ATFA045DES", chr(10) + STR0027 + CHR(10)+CHR(13)+CHR(10)+CHR(13), alink )  //"A rotina Baixa de Adiantamentos(ATFA045), esta em processo de descontinuacao e nao estara disponivel apos 04/04/2022."
Endif

If dDateAtual >= dDateDescon
	Return .F.
Endif

Private aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa tecla F12 para ativar parametros de lan‡amentos contab.             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F12, {|| Pergunte("AFA040",.T.)})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas:                          
//³ mv_par01 - 1 Mostra lan‡amentos cont beis                              ³
//³            2 NAO Mostra lan‡amentos cont beis                          ³
//³            3 NAO Contabiliza                                           ³
//³ mv_par02 - 1 Aglutina                                                  ³
//³            2 Nao Aglutina                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("AFA040",.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ lClassifica usada p/ classificacao. Aqui p/ NÆo criar Automatico  ³
//³ qdo entrar na tela de inclusao no ATFA010 (lClassifica := .T.)    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lClassifica := .T.
Private cMoedaAtf := GetMV("MV_ATFMOEDA")
Private cMarca    := GetMark()
Private dBaixa030 := dDataBase
//********************************
// Controle de multiplas moedas  *
//********************************
Private aValDepr	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
Private aVlrAtual	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
Private aVlResid 	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
Private aValBaixa	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
Private aVRDACM 	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
/*Private nValDepr1 := 0
Private nValDepr2 := 0
Private nValDepr3 := 0
Private nValDepr4 := 0
Private nValDepr5 := 0*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  lContabiliza - Ser  sinalizada de acordo com a pergunta. No programa    ³
//³  atfa010-Inclus„o, ser  usada para decidir se contabiliza ou n„o.        ³
//³  lHeader e lTrailler  - Informa se o programa atfa010 far  ou n„o a      ³
//³  abertura e o fechamento do cprova.                                      ³
//³  No atfa010 ser„o sinalizados sempre com .T., pois no caso de inclus„o   ³
//³  manual o atfa010 far  sempre a abertura e o fechamento do cprova.       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lContabiliza := .F.
Private lHeader := .F.
Private lTrailler := .F.
Private Inclui := .T.
Private cLoteAtf, cPadrao
Private nHdlPrv
Private nOriginal 	:= 0
Private lCprova 	:= .F., nTotal := 0.00
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Esta vari vel serve como sem foro para copia ou repeti‡„o de bens. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lCopia := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabe‡alho da tela de atualiza‡”es                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro := STR0004 // "Baixa de Adiantamentos de Ativos Imobilizados" 

Set Decimals to 4

//Verifica se o Ativo está atualizado
ATFXKERNEL()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endere‡a a fun‡„o de BROWSE ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse( 6, 1,22,75,"SN1",,,,,, AtfLegenda("SN1"))
SET KEY VK_F12 TO

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ AF045Baix  ³ Autor ³ Claudio D. de Souza   ³ Data ³ 16/02/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Baixa adiantamentos de Ativos Imobilizados - Sem Calc. Deprec³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ATFA045                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function af045Baix(cAlias,nReg,nOpc)

Local lInverte 	:= .F.
Local oDlg,oMark,oQtda,oValor,oPanel
Local lMostra 	:= Iif( mv_par01 == 1, .T., .F. )
Local lAdiant   := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis usadas no procedimento de baixa do adiantamento   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cIndex 	:= ""
Local nOpcA  	:= 0
// Vari veis-Ponte
//********************************
// Controle de multiplas moedas  *
//********************************
Local aTTAtual := If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
//Local nTTAtual1 := nTTAtual2 := nTTAtual3 := nTTAtual4 := nTTAtual5 := 0
Local nTTCorDep := 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa taxa da UFIR no 1.dia do pr¢ximo mˆs.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nUfirDia 		:= RecMoeda(dDataBase, cMoedaAtf)
Local aRecno		:= {}	// Recnos que iram compor a baixa
Local aRecPon		:= {}	// Recnos que compoem a baixa para ser passado para
							// Ponto de entrada, Estrutura { { "Alias", Recno } }
Local aQtdBaixa 	:= {}	// Array com quantidades parcias a serem baixadas
Local nQtdBai, cN1_NFISCAL, cN1_NSERIE, lParcial
Local nSaveSx8		:= GetSx8Len()
Local nMoeda		:= 1
Local nRecno
Local cFiltro
Local cMyFilial 	:= ""
Local cBaseAdt 		:= Spac( 10 )
Local nQtdOri 		:= SN1->N1_QUANTD
Local cOrdem		:= ""
Local lAF010AIN		:= EXISTBLOCK("AF010AIN")
Local nX 			:= 0
Local aParam040		:= Array(2)
Local cIDMOV	:= "" 
Local cTpSaldo	:= ""
Local lSN3Saldo := .T.
Local cOcorr 	:= ""
Local aDadosComp :={}
Local nVenda	:= 0

//Lançamentos PCO
Local aPadPCO := {"000373","01","ATFA045"}

	// Model para gravação de ativo definitivo
Local oModel045     := FWLoadModel("ATFA012")
Local oModelSN1
Local oModelSN3
Local lCcdp 			:= GetMv("MV_ATFCCDP",.F.,.F.)
Local lSCDP 			:= GetMv("MV_ATFSCDP",.F.,.F.)
Local lCVDP 			:= GetMv("MV_ATFCVDP",.F.,.F.)
Local cMoedaFisc		:= AllTrim(GetMV("MV_ATFMOED"))
Local cNomeTxSN3    	:= Subs("N3_TXDEPRn",1,10-len(cMoedaFisc) ) +cMoedaFisc
Local cNomeTxSNG    	:= Subs("NG_TXDEPRn",1,10-len(cMoedaFisc) ) +cMoedaFisc
Local cCodAtv        := ''
Local cDescAtv       := ''
Local aClote		 := {}


Private cArquivo	:= " "
//********************************
// Controle de multiplas moedas  *
//********************************
Private aVlrAtual 	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
Private aTxMedia	:= If(lMultMoed,AtfMultMoe(,,{|x| If(x=1,1,0) }), {1,0,0,0,0} )
//Private nVlrAtual1 	:= nVlrAtual2 := nVlrAtual3 := nVlrAtual4 := nVlrAtual5 := 0
//Private aTxMedia	:= {1,0,0,0,0,0}
Private nPercAtiv 	:= 0, lUmaVez := .T.
Private nValCorDep 	:= 0, nValCorr := 0
Private _aAF040REC	:= {}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a data ‚ v lida                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If dDataBase <= GetMv("MV_ULTDEPR")
	HELP(" ",1,"ATF150AD")
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Observa‡„o: estas vari veis private s„o usadas pela       ³
//³  rotina de inclus„o do atfa010                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cBase, cItemAtivo, cDescr
Private cCContab, cCCorrec, cDeprec, cCDesp, cCCDepr, cCusto
Private dIniDepr, cTipo, dDataAqui

//********************************
// Controle de multiplas moedas  *
//********************************
//Private aVlrAtual	:= AtfMultMoe(,,{|x| 0})
Private aVlResid 	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
Private aValBaixa	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
Private aVlrTot 	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
Private aTTDBal 	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
Private nTTCorBal	:= 0
Private nValBaixa1:= 0
//Private nVlrAtual1	:= 0, nVlrAtual2:= 0, nVlrAtual3:= 0, nVlrAtual4 := 0, nVlrAtual5 := 0
//Private nVlResid1 	:= 0, nVlResid2 := 0, nVlResid3 := 0, nVlResid4  := 0, nVlResid5	:= 0
//Private nValBaixa1	:= 0, nValBaixa2:= 0, nValBaixa3:= 0, nValBaixa4 := 0, nValBaixa5 := 0

//Private nVlrTot1 := 0, nVlrTot2 := 0, nVlrTot3 := 0, nVlrTot4 := 0, nVlrTot5 := 0
//Private nTTDBal1 := 0; nTTDBal2 := 0; nTTDBal3 := 0; nTTDBal4 := 0; nTTDBal5 := 0; nTTCorBal:= 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o n£mero do Lote do m¢dulo Ativo Fixo     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aClote := FWGetSX5("09",'ATF')
If len(aClote)>0
	cLoteAtf := SubStr(aClote[1][4],1,4)
EndIf

Private cArqF3 := "SN1"


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se existem adiantamentos no bem selecionado. ³
//³ O arquivo SN3 foi posicionado com seek na inicializa- ³
//³ ‡„o das variaveis locais.                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cBase           := Space( Len( SN1->N1_CBASE ) )
cItemAtivo      := Space( Len( SN1->N1_ITEM  ) )
cDescr          := Space( Len( SN1->N1_DESCRIC ) )
cBaseAdt        := SN1->N1_CBASE
cTipo           := "01"
dDataAqui       := dDataBase
//********************************
// Controle de multiplas moedas  *
//********************************
//nVlrTot1 := nVlrTot2 := nVlrTot3 := nVlrTot4 := nVlrTot5 := 0

If !FreeForUse("SN1",xFilial("SN1")+SN1->(N1_CBASE+N1_ITEM))
	Return
Endif
//23/07/2010
PcoIniLan(aPadPCO[1])
While .t.
	nOpca:=0
	DEFINE MSDIALOG oDlg FROM  80,43 TO 255,470 TITLE STR0009 PIXEL // "Baixa por Adiantamento" 
	@ 05, 007 	TO 45, 211 LABEL STR0010 	OF oDlg  PIXEL 	// "Dados do Novo Bem"
	@ 51, 007 	TO 84, 127 LABEL STR0011 	OF oDlg  PIXEL // "Crit‚rio para Sele‡„o"
	@ 16, 011 	SAY STR0012 SIZE 025, 07 OF oDlg PIXEL	// "C¢digo"       
	@ 16, 162 	SAY STR0013 SIZE 013, 07 OF oDlg PIXEL 	// "Item"          
	@ 34, 011 	SAY STR0016 SIZE 032, 07 OF oDlg PIXEL 	// "Descri‡„o"
	@ 58, 011 	SAY STR0014 SIZE 053, 07 OF oDlg PIXEL 	// "C¢digo Base dos"
	@ 70, 011 	SAY STR0015 SIZE 053, 07 OF oDlg PIXEL 	// "Adiantamentos" 
	@ 14, 047 	MSGET cBase SIZE 036, 10 OF oDlg PIXEL Picture "@!" Valid ! Empty( cbase )
	@ 14, 180 	MSGET cItemAtivo      SIZE 023, 10 OF oDlg PIXEL Picture "@!" Valid ! Empty( cItemAtivo ) .And. Af045Bem(cBase,cItemAtivo)
	@ 30, 047 	MSGET cDescr          SIZE 160, 10 OF oDlg PIXEL Picture "@!" Valid ! Empty( cDescr )
	@ 62, 075 	MSGET cBaseAdt        SIZE 039, 10 OF oDlg PIXEL Picture "@!" Valid ! Empty( cBaseAdt) F3 "SN1"
	DEFINE SBUTTON FROM 62, 153 TYPE 1 ENABLE OF oDlg ACTION (nOpca:=1,oDlg:End())
	DEFINE SBUTTON FROM 62, 183 TYPE 2 ENABLE OF oDlg ACTION (nOpca:=0,oDlg:End())
	ACTIVATE MSDIALOG oDlg CENTERED
	If nOpca # 1
		Return
	Endif
    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtra o arquivo por tipo e vencimento                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SN3")
	cIndex := CriaTrab(nil,.f.)
	cChave  := IndexKey()
	cMyFilial := xFilial("SN3")
	cFiltro := AF045Check(cMyFilial,cBaseAdt)
	IndRegua("SN3",cIndex,cChave,,cFiltro,STR0017) // "Selecionando Registros..."
	nIndex := RetIndex("SN3")
	dbSelectArea("SN3")
	#IFNDEF TOP
		dbSetIndex( cIndex +OrdBagExt())
	#ENDIF
	dbSelectArea("SN3")
	dbSetOrder(nIndex+1)
	dbGoTop( )
    
	If SN3->( BOF() ) .and. SN3->( EOF() )
		Help(" ",1,"RECNO")
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Restaura os indices      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RetIndex("SN3")
		Set Filter To
		fErase (cIndex+OrdBagExt())
		Loop
	End
	lAdiant := .T.
    
	nValor  := 0    // valor total dos T¡tulos,mostrado no rodape do browse
	nQtdBem := 0    // quantidade de T¡tulos,mostrado no rodape do browse
	nOpca   := 0
	aSize := MSADVSIZE()    
	DEFINE MSDIALOG oDlg1 TITLE STR0019 From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL// "Baixa por Adiantamentos"
	oDlg1:lMaximized := .T.
	oPanel := TPanel():New(0,0,'',oDlg1,, .T., .T.,, ,30,30,.T.,.T. )
	oPanel:Align := CONTROL_ALIGN_TOP
	     
	@0.8,.8 Say STR0020 	Of oPanel	// "Valor Total:"
	@0.8, 7 Say oValor VAR nValor Picture "@E 999,999,999,999.99" Of oPanel
	@1.3,.8 Say STR0021 	Of oPanel	// "Quantidade:"	
	@1.3, 9 Say oQtda VAR nQtdBem Picture "@E 99999" SIZE 50,10 Of oPanel
	
	//@1.4,.8 Say STR0020 	// "Valor Total:"
	//@1.4, 7 Say oValor VAR nValor Picture "@E 999,999,999,999.99"
	//@1.9,.8 Say STR0021 	// "Quantidade:"
	//@1.9, 9 Say oQtda VAR nQtdBem Picture "@E 99999" SIZE 50,10
	oMark := MsSelect():New("SN3","N3_OK","!N3_BAIXA",,@lInverte,@cMarca,{35,1,143,315})
	oMark:bMark := {| | Af045Display(cMarca,lInverte,oValor,oQtda,.T.)}
	oMark:oBrowse:lhasMark := .T.
	oMark:oBrowse:lCanAllmark := .T.
	oMark:oBrowse:bAllMark := { || Af045MarqDes(cMarca,lInverte,oValor,oQtda, .F., .T.) }
	oMark:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
	nValor  := 0
	nQtdBem := 0
	Af045MarqDes(cMarca,lInverte,oValor,oQtda,.F.,.T.)
	ACTIVATE MSDIALOG oDlg1 ON INIT EnchoiceBar(oDlg1,	{|| nOpca := 1,oDlg1:End()}, {|| nOpca := 2,oDlg1:End()},,;
												    					{ { "NOTE", { || Af045BxParcial(aQtdBaixa) }, "Dados da baixa", "Baixa" } })
	IF nOpcA == 2     //Desmarca se Abandonar
		Af045MarqDes(cMarca,lInverte,oValor,oQtda,.T.,.T.)
		Exit
	ElseIF nOpcA == 3 .or. nOpca == 0 .or. nQtdBem == 0
		Exit
	End
	lContabiliza := Iif(mv_par01 <= 2 , .T. , .F. )
	lHeader := .T.
	lTrailler := .F.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ BOPS 00000123127 - PROTECAO DA NUMERACAO SEQUENCIAL DA BAIXA ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !AF250VLDNM(@cOrdem)
		Exit
	Endif
	cBaixa := GetSx8Num("SN3","N3_CODBAIX",,Val(cOrdem))
    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Primeira Parte: baixam-se os bens de adiantamento. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SN3")
	dbGoTop()
    
	While !Eof( ).and. SN3->N3_FILIAL == cMyFilial
        
		dbSelectArea( "SN1" )
		dbSeek( cFilial + SN3->N3_CBASE + SN3->N3_ITEM )
		dbSelectArea( "SN3" )
        
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  N„o considera adiantamentos j   baixados.  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Val( SN3->N3_BAIXA ) # 0
			dbSkip( )
			Loop
		End
        
		If SN3->N3_OK == cMarca
			Aadd(aRecno, Recno())

			nQtdBai := Ascan(aQtdBaixa, { |x| X[4] = SN3->(Recno()) } )
			
			If nQtdBai > 0
				nQtdBai := aQtdBaixa[nQtdBai][3]
			Else
				nQtdBai := nQtdOri
			Endif
			
			//********************************
			// Controle de multiplas moedas  *
			//********************************
			If lMultMoed
				AtfMultMoe(,,{|x| aVlrAtual[x] := SN3->&( "N3_VORIG"+Alltrim(Str(x)) )+SN3->&( If(x>9,"N3_AMPLI","N3_AMPLIA")+Alltrim(str(x)) ) })
			Else
				aVlrAtual[2] := SN3->N3_VORIG2+SN3->N3_AMPLIA2
				aVlrAtual[3] := SN3->N3_VORIG3+SN3->N3_AMPLIA3
				aVlrAtual[4] := SN3->N3_VORIG4+SN3->N3_AMPLIA4
				aVlrAtual[5] := SN3->N3_VORIG5+SN3->N3_AMPLIA5
			EndIf
			aVlrAtual[1] := Iif(SN1->N1_PATRIM # "C", SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1, SN3->N3_VORIG1+SN3->N3_AMPLIA1)

			AF035Resid(dDataBase,nQtdBai,nQtdOri)
			AF030Valor(nValBaixa1,1,nQtdOri,nQtdBai)
			
			//********************************
			// Controle de multiplas moedas  *
			//********************************
			For nMoeda := 1 To __nQuantas
				cMoeda  := Alltrim(Str(nMoeda))
				IF cMoeda == "1"
					aVlrTot[1] += aValBaixa[1]
				Else
				   aVlrTot[nMoeda] += aValBaixa[nMoeda]
				Endif
			Next

			//********************************
			// Controle de multiplas moedas  *
			//********************************
			If lMultMoed
				AtfMultMoe(,,{|x| aTTAtual[x] += aValBaixa[x] })
			Else
				aTTAtual[1] += aValBaixa[1]		// Acumulo valores originais para gerar
				aTTAtual[2] += aValBaixa[2]		// o novo bem
				aTTAtual[3] += aValBaixa[3]
				aTTAtual[4] += aValBaixa[4]
				aTTAtual[5] += aValBaixa[5]
			EndIf

		End
		dbSelectArea( "SN3" )
		dbSetOrder( nIndex + 1 )
		dbSkip()
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Etapa Final:   Inclue-se um bem novo com o valor original   ³
	//³ igual  … soma dos valores  originais dos adiantamentos      ³
	//³ Observa‡Æo: Caso a inclusÆo do novo bem seja cancelada,     ³
	//³ os adiantamentos precisam ter suas baixas canceladas manu-, ³
	//³ almente, pois o volume de trabalho torna o estorno invi vel.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//********************************
	// Controle de multiplas moedas  *
	//********************************
	If lMultMoed
		AtfMultMoe(,,{|x| aVlrAtual[x] := aTTAtual[x] })
	Else
		aVlrAtual[1] := aTTAtual[1]
		aVlrAtual[2] := aTTAtual[2]
		aVlrAtual[3] := aTTAtual[3]
		aVlrAtual[4] := aTTAtual[4]
		aVlrAtual[5] := aTTAtual[5]
	EndIf

// Faco a gravacao somente se os bens forem confirmados

	__CODBAIX := cBaixa
	__IDBAIXA := "2"

	// Verifica a existencia do ponto de entrada antes da exibicao dos dados da baixa
	// para disponibilizar os recnos das fichas que irao compor o novo bem
	IF lAF010AIN
		_aAF040REC := AClone(aRecno)
	ENDIF

	// Salva as perguntas padroes antes da chamada da ParamBox
	For nX := 1 to Len(aParam040)
		aParam040[nX]	:= &("Mv_Par"+StrZero(nX,2))
	Next nX

	// Prepara as peguntas para execucao da ATFA010 - Inclusao da ficha do bem
	SetKey(VK_F12, {|| Pergunte("AFA012",.T.)})
	Pergunte("AFA012",.F.)

	oModel045:SetOperation(MODEL_OPERATION_INSERT)
	oModel045:Activate(.F.)
	oModelSN1  := oModel045:GetModel('SN1MASTER')
	oModelSN3  := oModel045:GetModel('SN3DETAIL')

	If oModelSN3:IsEmpty()
		oModelSN3:AddLine()
	Endif
	oModelSN3:GoLine(1)
		// Verifica se estes campos criados por outros programas existem
	If Type("cBase") == "C"
		oModelSN1:SetValue("N1_CBASE" , cBase)
		oModelSN3:SetValue("N3_CBASE" , cBase)
	Endif
	If Type("cItemAtivo") == "C"
		oModelSN1:SetValue("N1_ITEM" , cItemAtivo)
		oModelSN3:SetValue("N3_ITEM" , cItemAtivo)
	Endif
	If Type("cDescr") == "C"
		oModelSN1:SetValue("N1_DESCRIC" , cDescr)
		oModelSN3:SetValue("N3_HISTOR", Alltrim("IMPLANTACAO " + cDescr))
	endif

	if Type("cTipo") == "C"
		oModelSN3:SetValue("N3_TIPO" , cTipo)
	endif
	if Type("dDataAqui") == "D"
		oModelSN3:SetValue("N3_AQUISIC" , dDataAqui)
	endif
	if Type("dIniDepr") == "D"
		oModelSN3:SetValue("N3_DINDEPR" , dIniDepr)
	endif
	if Type("cCContab") == "C"
		oModelSN3:SetValue("N3_CCONTAB" , cCContab)
	endif
	if Type("cCCorrec") == "C"
		oModelSN3:SetValue("N3_CCORREC" , cCCorrec)
	endif
	if Type("cDeprec") == "C"
		oModelSN3:SetValue("N3_CDEPREC" , cDeprec)
	endif
	if Type("cCdesp") == "C"
		oModelSN3:SetValue("N3_CDESP" , cCdesp)
	endif
	if Type("cCCDepr") == "C"
		oModelSN3:SetValue("N3_CCDEPR" , cCCDepr)
	endif
	if Type("cCusto") == "C"
		oModelSN3:SetValue("N3_CCUSTO" ,  cCusto)
	endif
		// *******************************
		// Controle de multiplas moedas  *
		// *******************************

	if Type("aVlrTot") == "A"
		for nX := 1 to Len(aVlrTot)
			oModelSN3:SetValue("N3_VORIG"+Str(nX,1,0) , aVlrTot[nX])
		next nX
	endif

	if Type("aValDepr") == "A"
		for nX := 1 to Len(aValDepr)
			oModelSN3:SetValue("N3_VRDACM"+Str(nX,1,0) , aValDepr[nX])
		next nX
	endif

	if !Empty(oModelSN1:GetValue("N1_GRUPO"))
		oModelSN3:SetValue(cNomeTxSN3 , GetAdvFval("SNG",cNomeTxSNG,xFilial("SNG") + M->N1_GRUPO ,1,""))
	endif



	If  FWExecView('Ativo'  , 'ATFA012', MODEL_OPERATION_INSERT, /*oDlg*/, {|| .T. } ,/*bOk*/ , /*nPercReducao*/, /*aEnableButtons*/, /*bCancel*/ , /*cOperatId*/, /*cToolBar*/, oModel045 ) == 0
		
		// Restaura as perguntas padroes apos a chamada da ParamBox
		SetKey(VK_F12, {|| Pergunte("AFA040",.T.)})
		For nX := 1 to Len(aParam040)
			&("Mv_Par"+StrZero(nX,2)) := aParam040[nX]
		Next nX

		Aadd(aRecPon, { "SN1", SN1->(Recno()) })		// Registro gerado
		For nRecno := 1 To Len(aRecno)
			DbSelectArea("SN3")
			DbGoto(aRecno[nRecno])
	
			dbSelectArea( "SN1" )
			MsSeek( xFilial("SN1") + SN3->N3_CBASE + SN3->N3_ITEM )
			dbSelectArea( "SN3" )
         If nRecno = 1
				Aadd(aRecPon, { "SN1", SN1->(Recno()) })
			Endif
			Aadd(aRecPon, { "SN3", SN3->(Recno()) })

			nQtdBai 	:= Ascan(aQtdBaixa, { |x| X[4] = SN3->(Recno()) } )
			cN1_NFISCAL	:= ""
			cN1_NSERIE	:= ""
			cSeqReav 	:= SN3->N3_SEQREAV
			
			If nQtdBai > 0
				cN1_NFISCAL	:= aQtdBaixa[nQtdBai][1]
				cN1_NSERIE	:= aQtdBaixa[nQtdBai][2]
				nQtdBai 	:= aQtdBaixa[nQtdBai][3]
			Else
				nQtdBai 	:= nQtdOri
			Endif

			//********************************
			// Controle de multiplas moedas  *
			//********************************
			If lMultMoed
				AtfMultMoe(,,{|x| aVlrAtual[x] := SN3->&( "N3_VORIG"+Alltrim(Str(x)) )+SN3->&( If(x>9,"N3_AMPLI","N3_AMPLIA")+Alltrim(Str(x)) ) })
			Else
				aVlrAtual[2] := SN3->N3_VORIG2+SN3->N3_AMPLIA2
				aVlrAtual[3] := SN3->N3_VORIG3+SN3->N3_AMPLIA3
				aVlrAtual[4] := SN3->N3_VORIG4+SN3->N3_AMPLIA4
				aVlrAtual[5] := SN3->N3_VORIG5+SN3->N3_AMPLIA5
			EndIf
			aVlrAtual[1] := Iif(SN1->N1_PATRIM # "C", SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1, SN3->N3_VORIG1+SN3->N3_AMPLIA1)			

			AF035Resid(dDataBase,nQtdBai,nQtdOri)			
			AF030Valor(aValBaixa[1],1,nQtdOri,nQtdBai)
		
			If SN1->N1_PATRIM $ " N"
				cTipoImob := "5"
			Elseif SN1->N1_PATRIM $ "ACS"
				cTipoImob := "C"
			Else
				cTipoImob := "D"
			End
			
			BEGIN TRANSACTION
			
			ATFSaldo(	SN3->N3_CCONTAB,dDataBase,cTipoImob,;
					aValBaixa[1],aValBaixa[2],aValBaixa[3],aValBaixa[4],aValBaixa[5],"+",;
					nUfirDia,SN3->N3_SUBCCON,, SN3->N3_CLVLCON,SN3->N3_CUSTBEM,"1",aValBaixa)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza SN3         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lParcial
				Af030Parcial(SN3->N3_CBASE, SN3->N3_ITEM, SN3->N3_SEQ, dDataBase,;
				{ || ( 	AfAtuComple("SN3"), SN3->N3_IDBAIXA	:= "2") }, "2")
			Else
				RecLock("SN3")
				SN3->N3_BAIXA   := "2"
				SN3->N3_DTBAIXA := dDataBase
				SN3->N3_CODBAIX := cBaixa
				SN3->N3_IDBAIXA := "2"
				SN3->N3_PERCBAI := (nQtdBai / nQtdOri)
				SN3->N3_QUANTD  := nQtdOri
				msUnlock()
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza SN1         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SN1")
			RecLock("SN1")
			If nQtdBai = nQtdOri
				SN1->N1_BAIXA := dDataBase
				lParcial := .F.
			Else
				lParcial := .T.
			Endif
			
			SN1->N1_QUANTD 	-= nQtdBai
			
			If ! Empty(cN1_NFISCAL)
				N1_NFISCAL	:= cN1_NFISCAL
				N1_NSERIE	:= cN1_NSERIE
			Endif

			MsUnlock()
            
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Gera registro de movimenta‡„o         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cOcorr 	   := '01' 
			nVenda 		:= Round( aValBaixa[1] , X3Decimal("N4_VENDA")  )
			aDadosComp := ATFXCompl( 0 ,0 ,'08',cBaixa,/*cFilOrig*/,/*cSerie*/,/*cNota*/,nVenda,/*cLocal*/, SN3->N3_PRODMES )
			If lSN3Saldo
				cTpSaldo := SN3->N3_TPSALDO
			EndIf
			ATFXMOV(cFilAnt,@cIDMOV,dDataBase,cOcorr,SN3->N3_CBASE,SN3->N3_ITEM,SN3->N3_TIPO,SN3->N3_BAIXA,SN3->N3_SEQ,SN3->N3_SEQREAV,"1",nQtdBai,cTpSaldo,,aValBaixa,aDadosComp)
            
			//23/06/2010
			PcoDetLan(aPadPCO[1],aPadPCO[2],aPadPCO[3]) 
            Aadd(aRecPon, { "SN4", SN4->(Recno()) })

			END TRANSACTION

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existe lan‡amento padr„o.     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cPadrao := "812"
			lPadrao := VerPadrao(cPadrao)
			IF lPadrao .and. lContabiliza
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Envia para lan‡amento cont bil, desde que exista lan‡amento padro-  ³
				//³ nizado para o ativo cadastrado.                                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !lCprova .And. lHeader
					nHdlPrv := HeadProva(cLoteAtf,"ATFA045",Substr(cUsername,1,6),@cArquivo,.T.)
					lCprova := .T.
				Endif
				nTotal  += DetProva(nHdlPrv,cPadrao,"ATFA045",cLoteAtf)
			End
		Next
		If ExistBlock("AF045BAI")
			ExecBlock("AF045BAI",.F.,.F., aRecPon)
		Endif
	Endif

	oModel045:DeActivate()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura os ¡ndices, independente de estar ou n„o no AS-400  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SN3")
	Af045MarqDes(cMarca,lInverte,oValor,oQtda,.F.,.T.)		// Desmarco selecao N3_OK
	Set Filter To
	fErase (cIndex+OrdBagExt())
	RetIndex("SN3")
	lAdiant := .F.

	While (GetSx8Len() > nSaveSx8)
		ConfirmSX8()
	End

	Exit
EndDo

PcoFinLan(aPadPCO[1])  

If nOpca == 1 .and. nQtdBem > 0
	If !lTrailler .and. lCprova .and. lContabiliza
		RodaProva(nHdlPrv,nTotal)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Envia para Lan‡amento Cont bil           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cA100Incl(cArquivo,nHdlPrv,3,cLoteAtf,lMostra,Iif(mv_par02 == 1,.T.,.F.))
	Endif
Endif

If lAdiant
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Restaura os indices, independente de estar ou n„o no AS-400  ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    dbSelectArea("SN3")
    Set Filter To
    fErase (cIndex+OrdBagExt())
End
RetIndex("SN3")

nQtdBem := 0
nValor  := 0
Return Nil

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ AF045Bem   ³ Autor ³ Claudio D. de Souza   ³ Data ³ 16/02/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se o novo bem ja' existe                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ ATFA045                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Af045Bem( cBase , cItemAtivo )
Local lExist := .F.
Local nOrdem := 0 , nPosicao := 0

dbSelectarea( "SN1" )
nOrdem := IndexOrd()
nPosicao := Recno()

dbSetOrder( 1 )
lExist := Iif( MsSeek( xFilial("SN1") + cBase + cItemAtivo ) ,.T. , .F. )

If lExist
    HELP(" ",1,"AF40JAEXIS")
Endif

dbSelectArea( "SN1" )
dbSetOrder( nOrdem )
dbGoto( nPosicao )

Return IIf( lExist , .F. , .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AF045Check³ Autor ³ Claudio D. de Souza   ³ Data ³ 16/02/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna expressao para Indice Condicional                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AF045Check()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ATFA045                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AF045Check(cMyFilial,cBaseAdt)
Local cFiltro := ""

cFiltro := 'N3_FILIAL = "' + cMyFilial + '" .AND. '
cFiltro += 'N3_CBASE = "' + cBaseAdt + '" .AND. '
cFiltro += 'N3_BAIXA != "1" .AND. N3_BAIXA != "2" .AND. '
cFiltro += 'N3_TIPO = "03" '

Return cFiltro

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Af045Display³ Autor ³ Claudio D. de Souza   ³ Data ³ 16/02/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Atualiza tela de sele‡ao de registros da baixa autom tica     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   |Af045Display( cMarca,lInverte,oValor,oQtda )                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ATFA045								                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Af045Display(cMarca,lInverte,oValor,oQtda,lHelp)
Default lHelp := .F.

If IsMark("N3_OK",cMarca,lInverte)
  	SN1->(MsSeek(xFilial("SN1") + SN3->(N3_CBASE + N3_ITEM)))
	If SN1->N1_STATUS == "2" 
   		If lHelp
			Help(" ",1,"A030BLOQ")   //Este bem esta bloqueado, nao poder sofrer baixas.
		Endif	
   		RecLock("SN3",.f.)
   		SN3->N3_OK := "  "
   		MsUnlock()
	Else
	    nValor += (SN3->N3_VORIG1 + SN3->N3_VRCACM1)
	    nQtdBem++
	EndIf
Else
    nValor -= (SN3->N3_VORIG1 + SN3->N3_VRCACM1)
    nQtdBem--
    nQtdBem:= Iif(nQtdBem<0,0,nQtdBem)
End	
If ValType(oValor) == "O" .And.;
	ValType(oQtda) == "O"
	oValor:Refresh()
	oQtda:Refresh()
Endif
Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Af045MarqDes| Autor ³ Claudio D. de Souza   ³ Data ³ 17/02/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Marc/Desmarca os bens se NAO confirmar a baixa                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Af045MarqDes()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ATFA045                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Af045MarqDes(cMarca,lInverte,oValor,oQtda,lZera,lTodos)
Local aArea := GetArea()

Default lZera := .T.

dbSetOrder(nIndex+1)
dbGoTop()
While !EOF()
	RecLock("SN3",.f.)
  	SN3->N3_OK := If(Empty(SN3->N3_OK), cMarca, "  ")
  	If lTodos
	  	Af045Display(cMarca,lInverte,oValor,oQtda)
	Endif
	MsUnlock()
	dbSkip()
End

If lZera
	nQtdBem := 0
	nValor  := 0
Endif	

RestArea(aArea)

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Af045BxParcial³ Autor ³ Claudio D. de Souza ³ Data ³ 16/02/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Prepara dados para chamada da tela de baixa da rotina ATFA030 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³aQtdBaixa = Array de n elementos contendo em cada elemento	³±±
±±³          ³1 = Numero da nota fiscal										³±±
±±³          ³2 = Serie                     								³±±
±±³          ³3 = Quantidade parcial										³±±
±±³          ³4 = Recno do SN3 para gravacao   								³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Af045BxParcial(aQtdBaixa)                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ATFA045                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Af045BxParcial(aQtdBaixa)

Local nBaixa
Local oNvlVend
Local lBxFilho  	:= .F.
Local lVlVend 		:= .T.
Local aRetSX5		:= {}
Local nX			:= 0
Local aClote		:= {}

Private lSN7      	:= .F.
Private nVlVend		:= Iif(ExistBlock( "AF045VAL" ),ExecBlock("AF045VAL",.F.,.F.), 0)
Private nQtdeSn		:= 0
Private nPercBaixa	:= 100, nValCorDep := 0, nValCorr := 0
Private dBaixa030 	:= dDataBase

//********************************
// Controle de multiplas moedas  *
//********************************
Private aPicture := {}, cPictQtd
//Private cPicture1, cPicture2, cPicture3, cPicture4, cPicture5, cPictQtd
Private cMotivo		:= "01-Venda"
Private aMotivos	:= {}
Private cArquivo	:= ""
Private nTotal 		:= 0
Private nHdlPrv		:= 0
Private cPadrao
Private lQuant    	:= .T.
Private nPercAtiv 	:= 0, lUmaVez := .T.

//********************************
// Controle de multiplas moedas  *
//********************************
nVlrAtual1	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
nVlResid1 	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
nValBaixa1	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
nValDepr1 	:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
nDepr1 		:= If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
//nVlrAtual1	:= 0; nVlrAtual2:= 0; nVlrAtual3:= 0; nVlrAtual4 := 0; nVlrAtual5 := 0
//nVlResid1 	:= 0; nVlResid2 := 0; nVlResid3 := 0; nVlResid4  := 0; nVlResid5	:= 0
//nValBaixa1	:= 0; nValBaixa2:= 0; nValBaixa3:= 0; nValBaixa4 := 0; nValBaixa5 := 0
//nValDepr1 	:= 0; nValDepr2 := 0; nValDepr3 := 0; nValDepr4  := 0; nValDepr5	:= 0
//nDepr1 		:= 0; nDepr2 := 0; nDepr3 := 0; nDepr4 := 0; nDepr5 := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o numero do Lote do modulo Ativo Fixo 						³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aClote := FWGetSX5("09",'ATF')
If len(aClote)>0
	cLoteAtf := SubStr(aClote[1][4],1,4)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica tabela de motivos para baixa 							 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aRetSX5 := FWGetSX5("16")
for nX:= 1 to len(aRetSX5) // Filial, Tabela, Chave, Descricao
	cCapital := Capital(PadR(aRetSX5[nX][4],12))
	AAdd( aMotivos, Left(aRetSX5[nX][3], 2) + "-" + cCapital )
	If SubStr(aRetSX5[nX][3],1,2 ) = "08"
		cMotivo := Left(aRetSX5[nX][3], 2) + "-" + cCapital
	Endif
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa picture para valores do ativo										³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictQtd  := PesqPict("SN1","N1_QUANTD", 09)
//********************************
// Controle de multiplas moedas  *
//********************************
If lMultMoed
	AtfMultMoe(,,{|x| aAdd(aPicture,PesqPict("SN3","N3_VORIG"+Alltrim(Str(x)), 20))   })
Else
	aPicture[1] := PesqPict("SN3","N3_VORIG1", 20)
	aPicture[2] := PesqPict("SN3","N3_VORIG2", 20)
	aPicture[3] := PesqPict("SN3","N3_VORIG3", 20)
	aPicture[4] := PesqPict("SN3","N3_VORIG4", 20)
	aPicture[5] := PesqPict("SN3","N3_VORIG5", 20)
EndIf

SN1->(MsSeek(xFilial("SN1") + SN3->N3_CBASE + SN3->N3_ITEM))

If (nBaixa := Ascan(aQtdBaixa, { |x| X[4] = SN3->(Recno()) })) = 0
	Aadd(aQtdBaixa, { 	If(Empty(SN1->N1_NFISCAL), CriaVar("N1_NFISCAL"),;
						SN1->N1_NFISCAL), If(Empty(SN1->N1_NSERIE),;
						CriaVar("N1_NSERIE"), SN1->N1_NSERIE),;
						SN1->N1_QUANTD, SN3->(Recno()) })
	nBaixa := Len(aQtdBaixa)
Endif

//********************************
// Controle de multiplas moedas  *
//********************************
If lMultMoed
	AtfMultMoe(,,{|x| aVlrAtual[x] 	:= SN3->&("N3_VORIG"+Alltrim(Str(x)))+SN3->&(If(x>9,"N3_AMPLI","N3_AMPLIA")+Alltrim(Str(x)))  })
Else
	aVlrAtual[2] 	:= SN3->N3_VORIG2+SN3->N3_AMPLIA2
	aVlrAtual[3] 	:= SN3->N3_VORIG3+SN3->N3_AMPLIA3
	aVlrAtual[4] 	:= SN3->N3_VORIG4+SN3->N3_AMPLIA4
	aVlrAtual[5] 	:= SN3->N3_VORIG5+SN3->N3_AMPLIA5
EndIf
aVlrAtual[1] 	:= Iif(SN1->N1_PATRIM # "C", SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1, SN3->N3_VORIG1+SN3->N3_AMPLIA1)

dDindepr 	:= SN3->N3_DINDEPR
	
dbSelectArea("SN7")
dbSetOrder(1)
If dbSeek(xFilial("SN7")+SN3->N3_CBASE + SN3->N3_ITEM)
	If Empty(SN7->N7_DTBAIXA)
		nVlVend    := SN7->N7_VLSIMU1
		dBaixa030  := SN7->N7_DTSIMUL
		cMotivo    := SN7->N7_MOTIVO
		cNota      := SN7->N7_NOTA
		cSerie     := SN7->N7_SERIE
		lSN7       := .T.
	EndIf
Endif
	
AF035Resid(dDataBase,@aQtdBaixa[nBaixa][3],SN1->N1_QUANTD)
AF030Valor(aValBaixa[1],1,SN1->N1_QUANTD,aQtdBaixa[nBaixa][3])

//********************************
// Controle de multiplas moedas  *
//********************************
If lMultMoed
	AtfMultMoe(,,{|x| aVlrAtual[x] 	:= SN3->&("N3_VORIG"+Alltrim(Str(x)))+SN3->&(If(x>9,"N3_AMPLI","N3_AMPLIA")+Alltrim(Str(x)))  })
Else
	aVlrAtual[2] 	:= SN3->N3_VORIG2+SN3->N3_AMPLIA2
	aVlrAtual[3] 	:= SN3->N3_VORIG3+SN3->N3_AMPLIA3
	aVlrAtual[4] 	:= SN3->N3_VORIG4+SN3->N3_AMPLIA4
	aVlrAtual[5] 	:= SN3->N3_VORIG5+SN3->N3_AMPLIA5
EndIf
aVlrAtual[1] 	:= Iif(SN1->N1_PATRIM # "C", SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1, SN3->N3_VORIG1+SN3->N3_AMPLIA1)

dDindepr 	:= SN3->N3_DINDEPR

Af030DadosBx(	SN3->N3_CBASE, SN3->N3_ITEM, SN3->N3_TIPO, @aQtdBaixa[nBaixa][3],;
					SN1->N1_QUANTD, 0.00, onVlVend,lBxFilho,lVlVend,"N",;
					@aQtdBaixa[nBaixa][1], @aQtdBaixa[nBaixa][2],, .F.)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ana Paula N. Silva     ³ Data ³08/12/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina := {	{ STR0001,"AxPesqui"   , 0 , 1},; // "Pesquisar"  
							{ STR0002,'VIEWDEF.ATFA012' , 0 , 2},; // "Visualizar" 
							{ STR0003,"AF045Baix"  , 0 , 3},; // "Baixa Adiant"
							{ STR0022,"Af250Canc(,,,,,,'000373','ATFA045')"  , 0 , 6},; // "Canc. Adiant"
							{ STR0023, "AtfLegenda", 0 , 3 , ,.F. } }	// "Legenda"						
Return(aRotina)


/*/{Protheus.doc} DESHELP045
Rotina para exibir HELP com direcionamento para o TDN/KCS
@type  Function
@author Totvs
@since 04/10/2019
@version 12.1.25
@return
/*/
Function DESHELP045(cTitulo AS Character, cMsg AS Character, aBtLinks AS Array)

Local oDlg AS Object
Local oPanel AS Object
Local oTextDesc AS Object
Local oBtnLink1 AS Object
Local oBtnLink2 AS Object
Local oBtnLink3 AS Object
Local oFont AS Object
Local oSay AS Object

Default cTitulo := ""
Default cMsg := ""
Default aBtLinks := {}

oFont := TFont():New('Arial',,-16,.T.)

// Limite de 3 links externos para nao estourar a tela
DEFINE MSDIALOG oDlg FROM 0,0 TO 140,360 TITLE OemToAnsi(cTitulo) PIXEL // 150 altura da tela x 352 largura da tela
oDlg:lMaximized := .F.

oPanel := TPanelCss():New(0,0,"",oDlg,,.F.,.F.,,,oDlg:nWidth/2,oDlg:nHeight/2,.T.,.F.)

oPanelScroll := TScrollBox():New(oPanel,0,0,70,180,.T.,.T.,.F.) //80 altura da borda interior x  180 largura da borda interior   

oTextDesc := tSimpleEditor():New(2,2, oPanelScroll,(oPanelScroll:nWidth/2)-7,(oPanelScroll:nHeight/2)-7,,.T.,,,.T. )
oTextDesc:Setcss("background-color : transparent; border: 1px solid #DCDCDC;  border-radius: 4px; ")
oTextDesc:Load(cMsg)

If Len(aBtLinks) >= 1
	oSay := TSay():New((oPanel:nHeight/1)-72,005,{|| "Testes acesse:"},oPanel,,oFont,,,,.T.)

	oBtnLink1 := TButton():New( (oPanel:nHeight/2)-60, 5, aBtLinks[1,1]   ,oPanel,, 40 ,12,,,.F.,.T.,.F.,,.F.,,,.F. ) // 40 area largura do link x 12 area altura do link 
	oBtnLink1:SetCSS("QPushButton {text-decoration: underline; color: blue; border: 0px solid #DCDCDC; border-radius: 0px;Text-align:left}")
	oBtnLink1:bLClicked := {|| ShellExecute("open", aBtLinks[1,2] ,"","",3) }
EndIf

If Len(aBtLinks) >= 2
	oBtnLink2 := TButton():New( (oPanel:nHeight/2)-48, 5, aBtLinks[2,1]   ,oPanel,, 40 ,12,,,.F.,.T.,.F.,,.F.,,,.F. )
	oBtnLink2:SetCSS("QPushButton {text-decoration: underline; color: blue; border: 0px solid #DCDCDC; border-radius: 0px;Text-align:left}")
	oBtnLink2:bLClicked := {|| ShellExecute("open", aBtLinks[2,2] ,"","",3) }
EndIf

If Len(aBtLinks) >= 3
	oBtnLink3 := TButton():New( (oPanel:nHeight/2)-36, 5, aBtLinks[3,1]   ,oPanel,, 40 ,12,,,.F.,.T.,.F.,,.F.,,,.F. )
	oBtnLink3:SetCSS("QPushButton {text-decoration: underline; color: blue; border: 0px solid #DCDCDC; border-radius: 0px;Text-align:left}")
	oBtnLink3:bLClicked := {|| ShellExecute("open", aBtLinks[3,2] ,"","",3) }
EndIf

ACTIVATE MSDIALOG oDlg CENTERED

Return
