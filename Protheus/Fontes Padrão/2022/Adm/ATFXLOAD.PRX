#Include "PROTHEUS.CH"
#Include "ATFXLOAD.CH"

/*


ͻ
Programa   ATFLOAD  Autor  ARNALDO RAYMUNDO JR  Data   15/01/10   
͹
Desc.      Funo de carregamento das configuracoes mdulo ATF        
           executada na primeira vez que o usurio entra no mdulo    
͹
Uso        AP                                                        
ͼ


*/
Function ATFLoadDef()
Local lRet	:= .T.

MsgRun(OemToAnsi(STR0001),OemToAnsi(STR0002),{|| ATFXTabela() }) // "Carregando as configuraes do Mdulo Ativo Fixo" ## "Aguarde"

Return lRet

/*


ͻ
Programa  ATFXTabela Autor  Alvaro Camillo Neto  Data   01/12/09   
͹
Desc.     Realiza o carregamento das tabelas auxiliares CTB           
                                                                      
͹
Uso        AP                                                        
ͼ


*/
Function ATFXTabela()
Local aArea := GetArea()

// Ajuste pontual do SX6 em funo de problemas na ferramenta ATUSX
// Dever ser removido deste ponto aps normalizacao da ferramenta
ATFAtuSX6()

//---------------------------------------------------------------------------------------------
// A carga da SN0 foi migrada para o RUP_ATF por questao de performance na abertura do mdulo.
// Caso a tabela nao tenha todos os dados, a funcao  chamada diretamente.
//---------------------------------------------------------------------------------------------
If ChkFile("SN0")
	SN0->(DBSetOrder(1)) //N0_FILIAL+N0_TABELA+N0_CHAVE
	//  verificado a tabela 16, porque  o ultimo elemento do array e por conta de performance.
	// Se acrescentado algo nessa funo, necessrio remover o 16 
	// ou mudar a condio para executa-lo.
	If FindFunction("ATFLOADSN0") .and. !SN0->(MSSeek(XFilial("SN0")+"16"))  
		ATFLOADSN0()
	EndIf
EndIf

RestArea(aArea)
Return .T.

/*


Ŀ
Funo    ATFAtuSX6  Autor  ---------------------  Data  23/05/07 
Ĵ
Descrio  Funcao de processamento da gravacao do SX6                 
Ĵ
 Uso       ATUALIZACAO SIGAATF                                        
ٱ


*/
Static Function ATFAtuSX6()
//  X6_FIL   X6_VAR     X6_TIPO    X6_DESCRIC X6_DSCSPA  X6_DSCENG  X6_DESC1 X6_DSCSPA1 X6_DSCENG1
//  X6_DESC2 X6_DSCSPA2 X6_DSCENG2 X6_CONTEUD X6_CONTSPA X6_CONTENG X6_PROPRI

Local aSX6   	:= {}
Local aEstrut	:= {}
Local i      	:= 0
Local j      	:= 0
Local lSX6	 	:= .F.
Local cAlias 	:= ''

If (cPaisLoc == "BRA")
	aEstrut:= { "X6_FIL","X6_VAR","X6_TIPO",;
	"X6_DESCRIC","X6_DSCSPA","X6_DSCENG",;
	"X6_DESC1","X6_DSCSPA1","X6_DSCENG1",;
	"X6_DESC2","X6_DSCSPA2","X6_DSCENG2",;
	"X6_CONTEUD","X6_CONTSPA","X6_CONTENG","X6_PROPRI"}
Else
	aEstrut:= { "X6_FIL","X6_VAR","X6_TIPO",;
	"X6_DESCRIC","X6_DSCSPA","X6_DSCENG",;
	"X6_DESC1","X6_DSCSPA1","X6_DSCENG1",;
	"X6_DESC2","X6_DSCSPA2","X6_DSCENG2",;
	"X6_CONTEUD","X6_CONTSPA","X6_CONTENG","X6_PROPRI"}
EndIf

// Realiza o tratamento dos parametros conforme configuracao do ambiente
ATFAtuParam( @aSX6 )

dbSelectArea("SX6")
dbSetOrder(1)
For i:= 1 To Len(aSX6)
	If !Empty(aSX6[i][2])
		If !MsSeek(aSX6[i,1]+aSX6[i,2])
			lSX6	:= .T.
			If !(aSX6[i,2]$cAlias)
				cAlias += aSX6[i,2]+"/"
			EndIf
			RecLock("SX6",.T.)
			For j:=1 To Len(aSX6[i])
				If !Empty(FieldName(FieldPos(aEstrut[j])))
					FieldPut(FieldPos(aEstrut[j]),aSX6[i,j])
				EndIf
			Next j
			
			dbCommit()
			MsUnLock()
		EndIf
	EndIf
Next i   

Return

/*


Ŀ
Funo    ATFAtuParam Autor  Totvs                 Data  16/07/10 
Ĵ
Descrio  Funcao para a criacao dos parametros de calculo da deprec. 
Ĵ
 Uso       SIGAATF                                                    
ٱ


*/
Static Function ATFAtuParam( aSX6 )
Local aAreaSX6	:= SX6->( GetArea() )
Local aSM0Atv	:= {}
Local aDataBkp	:= {}
Local cData		:= "19800101"
Local nInc		:= 0
Local nX		:= 0

//---------------------------------------------------------------------------------------
// Caso nao encontre o MV_ULTDEPR com o compartilhamento da filial, recria os parmetros
//---------------------------------------------------------------------------------------
DBSelectArea("SX6")
SX6->(DBSetOrder(1)) //X6_FIL+X6_VAR
If !SX6->(MSSeek(XFilial("SN1") + "MV_ULTDEPR"))

	//--------------------------------------------------------------
	// Exclui o(s) MV_ULTDEPR com compartilhamento diferente da SN1
	//--------------------------------------------------------------
	DbSelectArea("SX6")
	SX6->(DbSetOrder(1))
	SX6->(DbGoTop())

	While SX6->(!Eof())

		If AllTrim( SX6->X6_VAR ) == "MV_ULTDEPR"
		

		   if ( FwRetIdiom()=="es" )
			  cData := AllTrim( SX6->X6_CONTSPA )
		   else
			  if ( FwRetIdiom()=="en" )
				  cData := AllTrim( SX6->X6_CONTENG )
			  else
				  cData := AllTrim( SX6->X6_CONTEUD )
			  endif
		   endif 

			AAdd(aDataBkp,{SX6->X6_FIL,cData})

			RecLock("SX6")
			SX6->(DBDelete())
			SX6->(MSUnLock())

		EndIf

	SX6->(DbSkip())
	EndDo

	//-----------------------------------------------------
	// Obtem as filiais conforme o compartilhamento da SN1
	//-----------------------------------------------------
	aSM0Atv := AdmAbreSM0()

	//---------------------------------------------------------
	// Cria o(s) MV_ULTDEPR conforme o compartilhamento da SN1
	//---------------------------------------------------------
	For nInc := 1 To Len( aSM0Atv )

		//--------------------------------------------------------------------------------------------------------------
		// Mantem a data do parametro anterior caso alterado de Exclusivo para Parcialmente Compartilhado ou vice-versa
		// Ex: D MG 01 -> D MG ou D MG -> D MG 01
		//--------------------------------------------------------------------------------------------------------------
		For nX := 1 To Len(aDataBkp)

			If AllTrim(aSM0Atv[nInc]) $ aDataBkp[nX,1] .Or. AllTrim(aDataBkp[nX,1]) $ aSM0Atv[nInc]
				cData := aDataBkp[nX,2]
				Exit
			EndIf

		Next nX

		//---------------------------------------
		// Alimenta array utilizado para geracao
		//---------------------------------------
		AAdd( aSX6, {	aSM0Atv[nInc], "MV_ULTDEPR", "D",;
						"Data da ultimo calculo da depreciacao de ativos ",;
						"Fecha del ultimo calculo de la depreciacion de ",;
						"Fixed assets depreciation last calculation ",;
						"imobilizados. ",;
						"activos fijos. ",;
						"date. ",;
						"",;
						"",;
						"",;
						cData, cData, cData, "S" } )
	Next nInc

EndIf

RestArea(aAreaSX6)

//--------------------
// Limpeza dos arrays
//--------------------
ASize(aAreaSX6,0)
aAreaSX6 := Nil

ASize(aSM0Atv,0)
aSM0Atv := Nil

ASize(aDataBkp,0)
aDataBkp := Nil

Return

/*/


Ŀ
Funo    AdmAbreSM0 Autor  Orizio                 Data  22/01/10 
Ĵ
Descrio Retorna um array com as informacoes das filias das empresas 
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Static Function AdmAbreSM0()
Local aRetSM0	:= FWLoadSM0()
Local aFilAtv	:= {}
Local nX		:= 0
Local cFilAux	:= ""
Local cFilConv	:= ""

For nX := 1 To Len(aRetSM0)

	If aRetSM0[nX][1] == cEmpAnt

		cFilConv := XFilial("SN1",aRetSM0[nX][2])

		If cFilConv <> cFilAux

			Aadd(aFilAtv, cFilConv)

			cFilAux := cFilConv

		EndIf

	EndIf

Next nX

Return aFilAtv

/*

Ŀ
Funo     ATFLoad     Autor  Marcelo Akama          Data  28/07/09 
Ĵ
Descrio  Validacao dos parametros na entrada do modulo                
Ĵ
 Uso       Genrico                                                     
ٱ


*/
Function ATFLoad()

Local cCalcDep	:= GetNewPar("MV_CALCDEP",'0')
Local cTipDepr	:= GetMV("MV_TIPDEPR",.F.,"0")
Local lOk		:= .F.
Local cHelp

//Ŀ
//Disponibilizado tipo 9 para exercicio completo e proximo trimestre
//Localizacao Argentina                                             
//
lOk:=(cCalcDep=='0' .And. cTipDepr$"0|1|2") .Or. (cCalcDep=='1' .And. cTipDepr$"3|4|5")  .or. cTipDepr == "9"

If !lOk
	Help("  ",1,"ATFWRTDEPR")
	cHelp := Ap5GetHelp("ATFWRTDEPR")
	cHelp := replace(cHelp,CRLF,' ')
	Final(cHelp)
EndIf

// Verifica se as rotinas do novo kernel do ativo esto presentes
If lOk
	//Verifica se o Ativo est atualizado
	lOk := ATFXKERNEL()
EndIf

If lOk
	//"Carregando as configuraes do Mdulo Ativo Fixo"
	ATFLoadDef()
EndIf

Return lOk
