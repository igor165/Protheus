#INCLUDE "PROTHEUS.CH"

//-------------------------------------------------------------------
/*{Protheus.doc} RBE_ATF
Função de compatibilização antes da execução de Base do UPDDISTR.
Esta função é relativa ao módulo ATFdico.

@param  cVersion   - Versão do Protheus
@param  cMode      - Modo de execução. 1=Por grupo de empresas / 2=Por grupo de empresas + filial (filial completa)
@param  cRelStart  - Release de partida  Ex: 002
@param  cRelFinish - Release del chegada Ex: 005
@param  cLocaliz   - Localização (país). Ex: BRA

@since 19/07/2022
@version P12
*/
//-------------------------------------------------------------------
Function RBE_ATF(cVersion, cMode, cRelStart, cRelFinish, cLocaliz)

	If cMode == '1' .and. cRelStart < "2210"
		conout("[RBE_ATF] Versao: " + cVersion + " | " + "Modo: " + cMode + " | " + "Release Inicial:" + cRelStart + " | " + "Release Final: " + cRelFinish + " | " + "Pais : " + cLocaliz)
		conout("[RBE_ATF] Inicio: " + Time())

		ATF2210()

		conout("[RBE_ATF] Final : " + Time())
	EndIf


Return NIL


//-------------------------------------------------------------------
/*{Protheus.doc}

Tratamentos especificos para atualização do Release 12.1.2210

@since 19/07/2022
@version P12
*/
//-------------------------------------------------------------------
Static Function ATF2210()
Local aArea    := GetArea()
Local aAreaSIX := SIX->( GetArea() )

// Os indices 2 e 3 da tabela SNS do ATF foram deletados no R33
// Foi recriado no R2210 o indice 2 com a chave que era usada no indice 3,
// Esta alteração saiu na Exp Continua para R33 e R27
// Como não há deleção de indices. Com isso as chaves dos indices 2 e 3 ficaram iguais em Clientes
// Isso gera duplicidade de indices.
// Aqui é deletado o indice 3 para que esta duplicidade não exista

SIX->( dbSetOrder( 1 ) )

If SIX->( dbSeek( 'SNS3' ) )

	RecLock( 'SIX', .F. )
	dbDelete()
	MsUnlock()

EndIf

RestArea( aAreaSIX )
RestArea( aArea )

Return NIL

