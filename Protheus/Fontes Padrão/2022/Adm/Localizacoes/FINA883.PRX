#INCLUDE "PROTHEUS.CH"
#INCLUDE "fileio.ch"
#INCLUDE "FINA883.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FINA883    ³ Autor ³ Jesus Peñaloza           ³ Data ³ 11.04.14   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Integracion de SPEI y TEF                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador   ³ Data   ³ BOPS/FNC  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³           ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function FINA883()
Private cPerg := "FINA883"
Private aHeader := {}
Private aDetail := {}
Private aTraill := {}
Private aOrden  := {}
Private cEOL    := CHR(13)+CHR(10)
Private nLinea  := 500+Len(cEOL)
Private nDetail := 0
Private nCount  := 0
Private cNomBan := ''
Private cLayout := ''
Private aObjects:= {}
Private cAliasSEK:=CriaTrab(Nil,.F.)

Private cBanco  := ''
Private cSucurs := ''
Private cCuenta := ''
Private cProveei:= ''
Private cTiendai:= ''
Private cProveef:= ''
Private cTiendaf:= ''
Private dFechai := ''
Private dFechaf := ''
Private cOrdeni := ''
Private cOrdenf := ''
Private cUbicac := ''
Private cNombre := ''
Private cTipoOr := ''

If Pergunte(cPerg,.T.)
	If TodoOk(cPerg)
		dbSelectArea("SA6")
		SA6->(dbSetOrder(1))
		SA6->(dbSeek(xFilial("SA6")+cBanco))
		cLayout := SA6->A6_CNAB2
		cNomBan := SA6->A6_NOME
		GeraHeader()
		MsAguarde({|| GeraDetail()}, STR0001, , .T.)//"Seleccionando Información"
		If nCount > 0
			GeraTraill()
			FN883DOC()()
		EndIf
	Else
		FINA883()
	EndIf
EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³TodoOK    ³ Autor ³ Microsiga             ³ Data ³23/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica los valores de los parametros                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ TodoOK(cExp1)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cExp1.- Nombre del grupo de preguntas                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER022                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function TodoOK(cPerg)
Local lRet := .F.

Pergunte(cPerg,.F.)
cBanco  := MV_PAR01
cSucurs := MV_PAR02
cCuenta := MV_PAR03
cProveei:= MV_PAR04
cTiendai:= MV_PAR05
cProveef:= MV_PAR06
cTiendaf:= MV_PAR07
dFechai := MV_PAR08
dFechaf := MV_PAR09
cOrdeni := MV_PAR10
cOrdenf := MV_PAR11
cUbicac := MV_PAR12
cNombre := MV_PAR13
cTipoOr := MV_PAR14

If Len(Alltrim(cBanco)) == 0
	Aviso(STR0002, STR0004, {STR0003})//"Ingresa el código de banco"
ElseIf Len(Alltrim(cSucurs)) == 0
	Aviso(STR0002, STR0005, {STR0003})//"Ingresa el código de sucursal"
ElseIf Len(Alltrim(cCuenta)) == 0
	Aviso(STR0002, STR0006, {STR0003})//"Ingresa el número de cuenta"
ElseIf Len(Alltrim(cProveef)) == 0
	Aviso(STR0002, STR0007, {STR0003})//"Ingresa el código de proveedor final"
ElseIf Len(Alltrim(cTiendaf)) == 0
	Aviso(STR0002, STR0008, {STR0003})//"Ingresa el código de tienda final"
ElseIf Len(Alltrim(DTOS(dFechaf))) == 0
	Aviso(STR0002, STR0009, {STR0003})//"Ingresa la fecha final"
ElseIf Len(Alltrim(cOrdenf)) == 0
	Aviso(STR0002, STR0010, {STR0003})//"Ingresa orden de pago final"
ElseIf Len(Alltrim(cUbicac)) == 0
	Aviso(STR0002, STR0011, {STR0003})//"Ingresa la ruta donde se guardará el archivo"
ElseIf !ExistDir(Alltrim(cUbicac))
	Aviso(STR0002, STR0012, {STR0003})//"El directorio seleccionado no existe"
ElseIf Len(Alltrim(cNombre)) == 0
	Aviso(STR0002, STR0013, {STR0003})//"Ingresa el nombre del archivo"
Else
	lRet := .T.
EndIf
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GeraHeader  ³ Autor ³ Jesus Peñaloza      ³ Data ³15/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Genera el encabezado del archivo con las ordenes de pago   ³±±
±±³          ³ de transferencia bancaria                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GeraHeader()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GeraHeader()
Local nHdl    := 0
Local nPosIni := 0
Local nPosFin := 0
Local nDecima := 0
Local nTamano := 0
Local cLinea  := ''
Local nLineas := 0
Local cConten := ''
Local nConten := 0
Local aLinea  := {}
Local nRenglon:= 0
Local aAreaSM0:= SM0->(GetArea()) //guarder area de SM0
nHdl := fOpen(cLayout)
If nHdl == -1
	Aviso(STR0002, STR0014+cLayout, {STR0003})//"No fue posible abrir el archivo "
Else
	SM0->(MsSeek(cEmpAnt+cFilAnt))
	fSeek(nHdl,0,FS_SET)
	nLineas := fRead(nHdl, @cLinea, nLinea)
	While nLineas >= nLinea
		If (SubStr(cLinea,1,1)!="1")
			If Substr(cLinea,3,1) == "H"
				If alltrim(str(nRenglon)) == substr(cLinea,2,1)
					nPosIni := val(substr(cLinea,20,3))
					nPosFin := val(substr(cLinea,23,3))
					nDecima := val(substr(cLinea,26,1))
					nTamano := nPosFin-nPosIni+1
					cConten := substr(cLinea,27,255)
					cConten := &cConten
					If valtype(cConten) == 'N'
						nConten := Round(cConten,nDecima)
						aAdd(aLinea,padl(substr(alltrim(str(nConten)),1,nTamano),nTamano,'0'))
					Else
						aAdd(aLinea,padr(substr(cConten,1,nTamano),nTamano))
					EndIf
				Else
					aAdd(aHeader, aLinea)
					aLinea := {}
					nRenglon := val(substr(cLinea,2,1))
					nPosIni := val(substr(cLinea,20,3))
					nPosFin := val(substr(cLinea,23,3))
					nDecima := val(substr(cLinea,26,1))
					nTamano := nPosFin-nPosIni+1
					cConten := substr(cLinea,27,255)
					cConten := &cConten
					If valtype(cConten) == 'N'
						nConten := Round(cConten,nDecima)
						aAdd(aLinea,padl(substr(alltrim(str(nConten)),1,nTamano),nTamano,'0'))
					Else
						aAdd(aLinea,padr(substr(cConten,1,nTamano),nTamano))
					EndIf
				EndIf
			EndIf
		ElseIf substr(cLinea,3,1) == "D"
			nDetail++
		EndIf
		nLineas := fRead(nHdl, @cLinea, nLinea)
	EndDo
	aAdd(aHeader, aLinea)
	fClose(nHdl)
EndIf
RestArea(aAreaSM0)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GeraDetail  ³ Autor ³ Jesus Peñaloza      ³ Data ³15/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Genera el detalle del archivo con las ordenes de pago      ³±±
±±³          ³ de transferencia bancaria                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GeraDetail()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GeraDetail()
Local cQuery := ''
Local nHdl    := 0
Local nPosIni := 0
Local nPosFin := 0
Local nDecima := 0
Local nTamano := 0
Local cLinea  := ''
Local nLineas := 0
Local cConten := ''
Local nConten := 0
Local aLinea  := {}
Local nRenglon:= 0
Local nConta  := 0
Local nReg    := 0
Local nApunta := 0

nHdl := fOpen(cLayout)
If nHdl == -1
	Aviso(STR0002, STR0014+cLayout, {STR0003})//No fue posible abrir el archivo
Else
	cQuery := "SELECT SEK.*, SA2.*, SA6.* FROM "+RetSQLName("SEK")+" SEK, "+RetSqlName("SA2")+" SA2, "+RetSqlName("SA6")+" SA6 "
	cQuery += "WHERE SEK.EK_FILIAL = '"+xFilial("SEK")+"' "
	cQuery += "AND SA2.A2_FILIAL = '"+xFilial("SA2")+"' "
	cQuery += "AND SA6.A6_FILIAL = '"+xFilial("SA6")+"' "
	cQuery += "AND SEK.EK_FORNECE = SA2.A2_COD AND SEK.EK_LOJA = SA2.A2_LOJA "
	cQuery += "AND SEK.EK_BANCO = SA6.A6_COD AND SEK.EK_AGENCIA = SA6.A6_AGENCIA AND SEK.EK_CONTA = SA6.A6_NUMCON "
	cQuery += "AND EK_TIPO IN ('TF', 'CH') "
	cQuery += "AND EK_BANCO = '"+cBanco+"' AND EK_AGENCIA = '"+cSucurs+"' AND EK_CONTA = '"+cCuenta+"' "
	cQuery += "AND EK_FORNECE BETWEEN '"+cProveei+"' AND '"+cProveef+"' "
	cQuery += "AND EK_LOJA BETWEEN '"+cTiendai+"' AND '"+cTiendaf+"' "
	cQuery += "AND EK_EMISSAO BETWEEN '"+DTOS(dFechai)+"' AND '"+DTOS(dFechaf)+"' "
	cQuery += "AND EK_ORDPAGO BETWEEN '"+ALLTRIM(cOrdeni)+"' AND '"+ALLTRIM(cOrdenf)+"' "
	cQuery += "AND SEK.D_E_L_E_T_ = '' "
	cQuery += "AND SA2.D_E_L_E_T_ = '' "
	cQuery += "AND SA6.D_E_L_E_T_ = '' "
	If cTipoOr == 1
		cQuery += "AND EK_ENVIO = '"+Alltrim(STR(cTipoOr))+"'"
	ElseIf cTipoOr == 2
		cQuery += "AND EK_ENVIO <> 1"
	EndIf
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSEK,.T.,.T.)
	count to nCount
	If nCount > 0
		(cAliasSEK)->(dbGoTop())
		While (!(cAliasSEK)->(EOF()))
			aAdd(aOrden, {.T., (cAliasSEK)->EK_ORDPAGO, (cAliasSEK)->EK_FORNECE, (cAliasSEK)->EK_LOJA, (cAliasSEK)->A2_NOME, (cAliasSEK)->EK_ENVIO})
			(cAliasSEK)->(dbSkip())
		EndDo
		(cAliasSEK)->(dbGoTop())
		fSeek(nHdl,0,FS_SET)
		nLineas := fRead(nHdl, @cLinea, nLinea)
		While (!(cAliasSEK)->(EOF()))
			nReg++
			fSeek(nHdl,0,FS_SET)
			nLineas := fRead(nHdl, @cLinea, nLinea)
			While nLineas >= nLinea
				If (SubStr(cLinea,1,1)!="1")
					If Substr(cLinea,3,1) == "D"
						If alltrim(str(nRenglon)) == substr(cLinea,2,1)
							nPosIni := val(substr(cLinea,20,3))
							nPosFin := val(substr(cLinea,23,3))
							nDecima := val(substr(cLinea,26,1))
							nTamano := nPosFin-nPosIni+1
							cConten := substr(cLinea,27,255)
							//cConten := &cConten
							nApunta := At("->", cConten)
							If nApunta != 0
								cConten := alltrim("(cAliasSEK)"+substr(cConten,nApunta,255))
								cConten := &cConten
								If valtype(cConten) == 'C'
									aAdd(aLinea,padr(substr(cConten,1,nTamano),nTamano))
								ElseIf valtype(cConten) == 'N'
									aAdd(aLinea,padr(substr(str(cConten),1,nTamano),nTamano))
								EndIf
							ElseIf valtype(&cConten) == 'N'
								nConten := Round(&cConten,nDecima)
								aAdd(aLinea,padl(substr(alltrim(str(nConten)),1,nTamano),nTamano,'0'))
							ElseIf valtype(&cConten) == 'C'
								aAdd(aLinea,padr(substr(&cConten,1,nTamano),nTamano))
							EndIf
						EndIf
					EndIf
				EndIf
				nLineas := fRead(nHdl, @cLinea, nLinea)
			EndDo
			aAdd(aLinea, alltrim(STR(nReg)))
			aAdd(aDetail, aLinea)
			aLinea := {}
			(cAliasSEK)->(dbSkip())
			MsProcTxt(STR0016)//"Procesando..."
			nConta++
			If nConta == nCount
				nConta := 0
				nRenglon++
				If nRenglon < nDetail
					nReg := 0
					(cAliasSEK)->(dbGoTop())
				EndIf
			EndIf
		EndDo
		(cAliasSEK)->(dbCloseArea())
		fClose(nHdl)
	Else
		Aviso(STR0002, STR0015, {STR0003})//"No existen órdenes de pago dentro de los rangos seleccionados"
	EndIf
EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GeraTraill  ³ Autor ³ Jesus Peñaloza      ³ Data ³21/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Genera el pie de pagina del archivo con las ordenes de pago³±±
±±³          ³ de transferencia bancaria                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GeraTraill()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GeraTraill()
Local nHdl    := 0
Local nPosIni := 0
Local nPosFin := 0
Local nDecima := 0
Local nTamano := 0
Local cLinea  := ''
Local nLineas := 0
Local cConten := ''
Local nConten := 0
Local aLinea  := {}
Local nRenglon:= 0

nHdl := fOpen(cLayout)
If nHdl == -1
	Aviso(STR0002, STR0014+cLayout, {STR0003})//No fue posible abrir el archivo
Else
	fSeek(nHdl,0,FS_SET)
	nLineas := fRead(nHdl, @cLinea, nLinea)
	While nLineas >= nLinea
		If (SubStr(cLinea,1,1)!="1")
			If Substr(cLinea,3,1) == "T"
				If alltrim(str(nRenglon)) == substr(cLinea,2,1)
					nPosIni := val(substr(cLinea,20,3))
					nPosFin := val(substr(cLinea,23,3))
					nDecima := val(substr(cLinea,26,1))
					nTamano := nPosFin-nPosIni+1
					cConten := substr(cLinea,27,255)
					cConten := &cConten
					If valtype(cConten) == 'N'
						nConten := Round(cConten,nDecima)
						aAdd(aLinea,padl(substr(alltrim(str(nConten)),1,nTamano),nTamano,'0'))
					Else
						aAdd(aLinea,padr(substr(cConten,1,nTamano),nTamano))
					EndIf
				Else
					aAdd(aTraill, aLinea)
					aLinea := {}
					nRenglon := val(substr(cLinea,2,1))
					nPosIni := val(substr(cLinea,20,3))
					nPosFin := val(substr(cLinea,23,3))
					nDecima := val(substr(cLinea,26,1))
					nTamano := nPosFin-nPosIni+1
					cConten := substr(cLinea,27,255)
					cConten := &cConten
					If valtype(cConten) == 'N'
						nConten := Round(cConten,nDecima)
						aAdd(aLinea,padl(substr(alltrim(str(nConten)),1,nTamano),nTamano,'0'))
					Else
						aAdd(aLinea,padr(substr(cConten,1,nTamano),nTamano))
					EndIf
				EndIf
			EndIf
		EndIf
		nLineas := fRead(nHdl, @cLinea, nLinea)
	EndDo
	aAdd(aTraill, aLinea)
	fClose(nHdl)
EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GeraArchivo ³ Autor ³ Jesus Peñaloza      ³ Data ³21/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Genera el archivo de las ordenes de pago de transferencias ³±±
±±³          ³ bancarias                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GeraArchivo()                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FN883DOC()                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GeraArchivo()
Local nHdl    := 0
Local nX      := 0
Local nY      := 0
Local nZ      := 0
Local cFilSek := xFilial("SEK")
Local lHayMarc:= .F.

For nX := 1 to len(oListBox:aArray)
	If oListBox:aArray[nX][1]
		lHayMarc := .T.
	EndIf
Next nX
If lHayMarc
	nHdl := FCreate(alltrim(cUbicac)+"\"+alltrim(cNombre)+".txt")
	If nHdl == -1
		Aviso(STR0002, STR0017, {STR0003})//"Error al crear archivo"
	Else
		dbSelectArea("SEK")
		SEK->(dbSetOrder(1))
		SEK->(dbGoTop())
		ProcRegua(len(aHeader)+len(aDetail)+len(aTraill))
		For nX := 1 to len(aHeader)
			IncProc()
			For nY := 1 to len(aHeader[nX])
				FWrite(nHdl, aHeader[nX][nY])
			Next nY
			fWrite(nHdl,cEOL)
		Next nX
		For nX := 1 to len(oListBox:aArray)
			If SEK->(dbSeek(cFilSek+oListBox:aArray[nX][2])) .AND. oListBox:aArray[nX][1]
				RecLock("SEK", .F.)
				SEK->EK_ENVIO := '1'
				MsUnlock()
			EndIf
			For nY := 1 to len(aDetail)
				IncProc()
				If oListBox:aArray[nX][1] .and. nX == val(aDetail[nY][len(aDetail[nY])])
					For nZ := 1 to len(aDetail[nY])-1
						fWrite(nHdl, aDetail[nY][nZ])
					Next nZ
					fWrite(nHdl,cEOL)
				EndIf
			Next nY
		Next nX
		For nX := 1 to len(aTraill)
			IncProc()
			For nY := 1 to len(aTraill[nX])
				FWrite(nHdl, aTraill[nX][nY])
			Next nY
			fWrite(nHdl,cEOL)
		Next nX
		fClose(nHdl)
		Aviso(STR0018, STR0019+Alltrim(cNombre)+STR0020+cEOL+cUbicac, {STR0003})//Archivo generado en la ruta
	EndIf
Else
	Aviso(STR0002, STR0021, {STR0003})//"No hay Ordenes de Pago Seleccionadas"
EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³FN883DOC ³ Autor ³ Alfredo Medrano       ³ Data ³20/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Muestra preguntas y ejecuta pantalla para                  ³±±
±±³          ³ selecciona de Pre-factura.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FN883DOC()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MT459CON                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum						                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function FN883DOC()   
 
Local cNom     := ""  
Local oBtnMarcTod  //Marcar, desmarcar, invertir
Local oBtnDesmTod
Local oBtnInverte
Local bOk1
Local bCan1
Local lEnd 		:= .T.
Local bActiva  :={||lActiva:=(if 	((len(oListBox:aarray )>0 .and. !empty(oListBox:aarray[1,2]) ), .t.,.f.))} 
Local bMarcTod 	:={||(MarcProd( oListBox , @aOrden , @oDlg , "M" ),oListBox:nColPos := 1,oListBox:Refresh())}
Local bDesmTod 	:={||(MarcProd( oListBox , @aOrden , @oDlg , "D" ),oListBox:nColPos := 1,oListBox:Refresh())}
Local bInverte 	:={||(MarcProd( oListBox , @aOrden , @oDlg , "I" ),oListBox:nColPos := 1,oListBox:Refresh())}
Local bOrdenLst	:={||if 	((len(oListBox:aarray )>0 .and. !empty(oListBox:aarray[1,2]) ),FN883Ordena(),OemToAnsi(STR0022))}//"Para usar esta opcion debe haber datos en la lista"
Local bBuscar	:={||FN883Busca()}
Local aOrdenBuscar:={OemToAnsi(STR0024),OemToAnsi(STR0025),OemToAnsi(STR0026),OemToAnsi(STR0027)} 
//BOTONES

Local bAsigna	:={||Processa( {|lEnd| GeraArchivo()}, OemToAnsi(STR0023),OemToAnsi(STR0016), .T. ), IIF(!lEnd,,oDlg:End())}
Local bCancela	:={|| oDlg:End()} 

Private aHeaderCPP :=aClone(aOrdenBuscar)
Private cDatBus	:=space(15)
Private oEnv   := LoadBitmap(GetResources(),'br_verde')//cargar imagenes del repositiorio
Private oNoEnv := LoadBitmap(GetResources(),'br_vermelho')//cargar imagenes del repositiorio
Private oOk    	:= LoadBitmap( GetResources(), "LBOK" ) //cargar imagenes del repositiorio
Private oNo		:= LoadBitmap( GetResources(), "LBNO" ) //cargar imagenes del repositiorio
Private oDlg2  
Private aButtons	:={} 
Private oLbx  
Private cOrden		:=''  
//posiciÓn en el LISTBOX
Private nPosForn	:=5  //PROVEEDOR
Private nPosLoja	:=6  //TIENDA
Private nPosDoc	:=2  //DOCTO
Private nPosSeri	:=3  //SERIE
Private nPosFolF	:=10 //FOLIO FISCAL  

CURSORWAIT()
/*
  ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
  ³Prepara botones de la barra de herramientas      ³
  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ /*/
aAdd(aButtons, { ,{|| MuesLeyenda()}, ,STR0045})//Leyenda
aAdd(aButtons, {'PMSRRFSH' , bOrdenLst,OemToAnsi(STR0028),OemToAnsi(STR0029)}) //"Ordenar los datos","Ordenar"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Hace  calculo automatico de dimenciones de objetos     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
aSize :=MsAdvSize()
		aSize := MsAdvSize()
		AAdd( aObjects, { 20, 20, .T., .T. } )      
		AAdd( aObjects, { 70, 70, .T., .T. } )//VENTANA DEL LISTBOX
		AAdd( aObjects, { 10,10, .T., .T. } )
aInfo	:= { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 5, 5 }
aPosObj	:= MsObjSize( aInfo, aObjects,.T.)  

                        
CURSORARROW()	 
                
DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0030+cNomBan) From aSize[7],0 To aSize[6],aSize[5] of oMainWnd PIXEL //"Generacion de Archivos de Transferencia Bancaria. Banco: "

@ aPosObj[1,1],aPosObj[1,2] LISTBOX oListBox FIELDS HEADER "",STR0031,aHeaderCPP[1],aHeaderCPP[2],aHeaderCPP[3],aHeaderCPP[4];
  SIZE aPosObj[2][4], aPosObj[2][3]-20 PIXEL ON DBLCLICK (MarcProd(oListBox,@aOrden,@oDlg),oListBox:nColPos := 1,oListBox:Refresh())  //NOSCROLL 

oListBox:SetArray( aOrden )
oListBox:bLine := { || {IF(	aOrden[oListBox:nAt,6]=='1',oEnv,oNoEnv),;
							IF(	aOrden[oListBox:nAt,1],oOk,oNo),;
							aOrden[oListBox:nAt,2],;	
							aOrden[oListBox:nAt,3],;
							aOrden[oListBox:nAt,4],;
							aOrden[oListBox:nAt,5]}}  

oGroup2:= tGroup():New(aPosObj[3,1],aPosObj[3,2],aPosObj[3,3],aPosObj[3,4],,oDlg,,CLR_WHITE,.T.)        		
aEval:= bActiva		
oBtnMarcTod:=tButton():New( aPosObj[3][1]+6,318, OemToAnsi(STR0032) ,oGroup2,bMarcTod,58,13.50  ,,,.F.,.T.,.F.,,.F.,bActiva,,.F. )	 //"Marca todo - <F4>"
oBtnDesmTod:=tButton():New( aPosObj[3][1]+6,379, OemToAnsi(STR0033) ,oGroup2,bDesmTod,58,13.50  ,,,.F.,.T.,.F.,,.F.,bActiva,,.F. )	 //"Desmarca todo - <F5>"
oBtnInverte:=tButton():New( aPosObj[3][1]+6,440 ,OemToAnsi(STR0034) ,oGroup2,bInverte,58,13.50  ,,,.F.,.T.,.F.,,.F.,bActiva,,.F. ) //"Inv. seleccion - <F6>"
oComboBus:= tComboBox():New(aPosObj[3,1]+8,10,{|u|if(PCount()>0,cOrden:=u,cOrden)},;
			          aOrdenBuscar,50,24,oGroup2,,nil,,,,.T.,,,,bActiva,,,,,'cOrden')  
@ aPosObj[3,1]+8,60  msGET cDatBus 	  when lActiva	SIZE  60,09  OF oGroup2 PIXEL 		
oSButton2 := tButton():New(aPosObj[3,1]+6,130,OemToAnsi(STR0035),oGroup2,bBuscar,54.50,13.50 ,,,.F.,.T.,.F.,,.F.,bActiva,,.F. )	//"Buscar"

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,bAsigna,bCancela,,aButtons)

Return

/*                                                                               
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao     ³ MarcProd ³ Autor ³ Laura Medina         ³ Data ³ 26/09/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descripción³ Función para marcar productos.                            ³±± 
±±³           ³ 			                                              ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±                
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³  /  /  ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MarcProd( oListBox , aOrden , oDlg , cMarckTip )

DEFAULT cMarckTip := ""
IF Empty( cMarckTip )  
	aOrden[ oListBox:nAt , 1 ] := !aOrden[ oListBox:nAt , 1 ]
ElseIF cMarckTip              == "M"
	aEval( aOrden , { |x,y| aOrden[y,1] := .T. } )
ElseIF cMarckTip == "D"
	aEval( aOrden , { |x,y| aOrden[y,1] := .F. } )
ElseIF cMarckTip == "I"
	aEval( aOrden , { |x,y| aOrden[y,1] := !aOrden[y,1] } )
EndIF

Return( NIL )
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FN883Busca ³ Autor ³ Alfredo Medrano      ³ Data ³21/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Busca en el ListBox                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FN883Busca		      			                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Ninguno					                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FN883DOC                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static function FN883Busca()                   
Local nPosBus:=0     
Local nPos:=oComboBus:nat+1

If valtype(oListBox:aarray[1,npos])=="C"
	nPosBus:=aScan(oListBox:aarray,{|x| upper(ALLTRIM(x[nPos])) == upper(ALLTRIM(cDatBus))} )
Else
	If !Empty(ctod(cDatBus))
		nPosBus:=aScan(oListBox:aarray,{|x| x[nPos] == ctod(cDatBus)} )
	Else
		nPosBus:=aScan(oListBox:aarray,{|x| x[nPos] == Val(cDatBus) })
	EndIf
EndIf   

If nPosBus >0
	oListBox:nat:=nPosBus
Else 
    msgInfo(OemToAnsi(STR0036)) //"No encontro!"
EndIf	

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FN883Ordena³ Autor ³ Gpe Santacruz         ³ Data ³09/10/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Selecciona las columnas a ordenar                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FN883Ordena(ExpC1)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Numero da opcion selecionada                        ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FN883DOC                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static function FN883Ordena(nOpc)  
Local cLinOrdOk:="AllwaysTrue()"  
Local cTodOrdOk:="AllwaysTrue()" 
Local odlg3  :="AllwaysTrue()" 
Local oGetOrdena
Local cFielOrdOk:="AllwaysTrue()"    
Local nOpcOrdena:=0     
Local aColsOrdena:={}
Local aHeaderOrdena:={} 
Local oCombo:=Nil   
Local cCombo:=''
Local aItems:={}
Local nx:=0
Local ni:=0                  
Local nUsado:=3
Local aSelAlt:={"COLUM"}  //Columna que permitirá alteraciones

//aHeader del getdados de ORDENAR
Aadd(aHeaderOrdena, { OemToAnsi(STR0037),"ITEM","99",2,0,"AllwaysTrue()" ,CHR(251),"N",'','','' } ) //"Item"
Aadd(aHeaderOrdena, { OemToAnsi(STR0038),"COLUM","999",3,0,"AllwaysTrue()" ,CHR(251),"N",'','','' } )  //"Columna"
Aadd(aHeaderOrdena, { OemToAnsi(STR0039),"CAMPOS","",11,0,"AllwaysTrue()",CHR(251),"C",'','',''} )   //"Campos"

//aCols del getdados ORDENAR
for ni:=1 to len(oListBox:aheaderS)
		Aadd(aColsOrdena,Array(nUsado+1))
		aColsOrdena[Len(aColsOrdena)][1] := ni
		aColsOrdena[Len(aColsOrdena)][2] := 0			
		aColsOrdena[Len(aColsOrdena)][3] :=oListbox:aheaderS[NI]
		aColsOrdena[Len(aColsOrdena)][nUsado+1] := .F.
next

//Items del combobox
aItems:= {OemToAnsi(STR0040),OemToAnsi(STR0041)} //'Descendente','Ascendente'
cCombo:= aItems[1] //Opción def<ault del  combobox

DEFINE MSDIALOG oDlg3 TITLE OemToAnsi(STR0042) From c(40),c(10) To c(235),c(300) PIXEL //"Ordenar OP's" 
 	
         oGetOrdena:= MsNewGetDados():New(c(13),c(05),c(85),c(145), 2,cLinOrdOk,cTodOrdOk,nil,aSelAlt, 0, 999,cFielOrdOk,;
	                                     "",nil,  oDlg3, aHeaderOrdena, aColsOrdena)   
		oCombo:= tComboBox():New(c(88),c(05),{|u|if(PCount()>0,cCombo:=u,cCombo)},;
		                          aItems,50,20,oDlg3,,nil,,,,.T.,,,,,,,,,'cCombo')     
		                          
ACTIVATE MSDIALOG oDlg3 centered ON INIT EnchoiceBar(oDlg3,{||OrdenaArray(oGetOrdena,oCombo:nat),oDlg3:End()},{||oDlg3:End()},,)
Return
 /*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³OrdenaArray       ³ Gpe Santacruz         ³ Data ³14/07/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Ordena las columnas del getdados principal                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ OrdenaArray(ExpO1,ExpN1)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 = Objeto, del getdados que indica el orden           ³±±
±±³          ³ ExpN1 = Numerico, indica si el orden es 2-ascen o 1-desc   ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FN883Ordena                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±/*/

static function OrdenaArray(oGetOrdena,nSelOrd)  

Local ni:=0
Local ctipo:=''
Local cStrX:=''
Local cStrY:=''    
Local cOper:=''

Cursorwait()                
	oGetOrdena:acols :=aSort(oGetOrdena:acols,,,{|x,y| x[2] <= y[2]})
	if nSelOrd==1 //descendente
	   cOper:=' >= '
	else          
	   cOper:=' <= '
	endif
	
	for ni:= 1 to len(oGetOrdena:acols)
	    if oGetOrdena:acols[ni,2]<>0   
	           cTipo:=valtype(oListBox:aarray[1,oGetOrdena:acols[ni,1]])
	           if ctipo=='N'
	              cStrX+="str(x["+alltrim(str(oGetOrdena:acols[ni,1]))+"])+"
	              cStrY+="str(y["+alltrim(str(oGetOrdena:acols[ni,1]))+"])+"
	            else        
	               if cTipo=='D'
					  cStrX+="dtos(x["+alltrim(str(oGetOrdena:acols[ni,1]))+"])+"
	                  cStrY+="dtos(y["+alltrim(str(oGetOrdena:acols[ni,1]))+"])+"		                  
	               else       
						  cStrX+="x["+alltrim(str(oGetOrdena:acols[ni,1]))+"]+"
				          cStrY+="y["+alltrim(str(oGetOrdena:acols[ni,1]))+"]+"		                  
	                endif   
	             endif    
	     endif    
	next               
	cStrX:=substr(cStrX,1,len(cStrX)-1)  
	cStrY:=substr(cStry,1,len(cStrY)-1)                                            
	if !empty(cStrX)
	      &("oListBox:aarray := aSort(oListBox:aarray,,,{|x,y| "+cStrX+cOper+cStrY+"})")      
	endif
CursorArrow()
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MuesLeyenda       ³ Jesus Peñaloza        ³ Data ³28/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Crea una ventana para mostrar la leyenda                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MuesLeyenda()                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FN883DOC                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±/*/
Static Function MuesLeyenda()
BrwLegenda(OemToAnsi(STR0024),OemToAnsi(STR0045),{;//"Orden de Pago"
            {"BR_VERDE",OemToAnsi(STR0043)},;//"Enviados"
            {"BR_VERMELHO",OemToAnsi(STR0044)};//"Sin Enviar"
            })
Return
