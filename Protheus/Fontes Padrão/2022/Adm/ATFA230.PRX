#INCLUDE "ATFA230.CH"
#Include "Protheus.ch"

Static lFWCodFil := .T.

//********************************
// Controle de multiplas moedas  *
//********************************
Static lMultMoed := .T.

/*/


Ŀ
Funo     ATFA230   Autor  Alice Y Yamamoto       Data  17/11/98 
Ĵ
Descrio  Alterao da taxa de depreciao automtica                
Ĵ
Sintaxe    ATFA230()                                                  
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function ATFA230
//Ŀ
// Define Variaveis                                               
//

Local aSays:={}, aButtons:={}
Local nOpca := 0

Private cCadastro := STR0001     //"Alterao da Taxa de Depreciao "

dbSelectArea("SN1")
dbSetOrder(1 )

Pergunte("AFA230",.F.)
AADD(aSays,STR0004) //"Este programa tem o objetivo de alterar as taxas de de- "
AADD(aSays,STR0005) //"preciao automaticamente de acordo com  os parmetros  "
AADD(aSays,STR0006) //"escolhidos"

//Ŀ
// Inicializa o log de processamento                            
//
ProcLogIni( aButtons )


AADD(aButtons, { 5,.T.,{|| Pergunte("AFA230",.T. ) } } )
AADD(aButtons, { 1,.T.,{|| nOpca := 1,	IF( AtfOK(),FechaBatch(),nOpca := 0 ) }} )
AADD(aButtons, { 2,.T.,{|| FechaBatch() }} )

FormBatch( cCadastro, aSays, aButtons,,,375 )

If nOpca == 1
	//Ŀ
	// Atualiza o log de processamento   
	//
	ProcLogAtu("INICIO")

	If MV_PAR18 == 1 .And. !Empty(xFilial("SN3"))// Seleciona filiais
		Processa({ |lEnd| Afa230Fil(MV_PAR19,MV_PAR20) })
	Else
		Processa({ |lEnd| AFA230Proc() })
	EndIf

	//Ŀ
	// Atualiza o log de processamento   
	//
	ProcLogAtu("FIM")
Endif

Return

/*


ͻ
Programa  Ctb230Fil Autor  Alvaro Camillo Neto  Data   21/09/09   
͹
Desc.     Executa o processamento para cada filial                    
                                                                      
͹
Uso        ATFA230                                                  
ͼ


*/
Static Function Afa230Fil(cFilDe,cFilAte)
Local cFilIni 	:= cFIlAnt
Local aArea		:= GetArea()
Local nInc		:= 0
Local aSM0		:= {}

aSM0 := AdmAbreSM0()
For nInc := 1 To Len( aSM0 )
	If aSM0[nInc][1] == cEmpAnt .AND. aSM0[nInc][2] >= cFilDe .AND. aSM0[nInc][2] <= cFilAte
		cFilAnt := aSM0[nInc][2]
		ProcLogAtu( "MENSAGEM", "EXECUTANDO O PROCESSO DA FILIAL "  + cFilAnt )
		Afa230Proc()
	EndIf
Next

cFIlAnt := cFilIni
RestArea(aArea)
Return

/*/


Ŀ
Funo	 AFA230Proc Autor  Alice Yamamoto	     Data 17/11/98  
Ĵ
Descrio  Efetua a alterao das taxas de depreciao                
Ĵ
Sintaxe e  AFA230Proc            									  
Ĵ
 Uso		  															  
ٱ


*/

Function AFA230Proc()
Local cMoeda
Local lUpdDepPer 	:= .F.
Local cCalcDep		:= GetMV("MV_CALCDEP") // 0 = Monthly or 1 = Annually
lOCAL nNewPerDep	:= 0

//Ŀ
// Carrega as perguntas selecionadas:                      
// mv_par01 - Do Cdigo                                    
// mv_par02 - At o Cdigo                                 
// mv_par03 - Grupo de                                     
// mv_par04 - Grupo Ate                                    
// mv_par05 - Nova Taxa                                    
// mv_par06 - Alterar Taxa na Moeda                        
// mv_par07 - Do CCusto                                    
// mv_par08 - At oCCusto                                  
// mv_par09 - Da Conta                                     
// mv_par10 - At a Conta                                  
// mv_par11 - Moedas? Todas / Especifica                   
// mv_par12 - Do Item Contabil       						
// mv_par13 - Ate o item Contabil    						
// mv_par14 - Da Classe de Valor     						
// mv_par15 - Ate a Classe de Valor  						
// mv_par16 - Do Item                                      
// mv_par17 - At o Item                                   
// mv_par18 - Seleciona Filiais?                           
// mv_par19 - Filial De                                    
// mv_par20 - Filial At?                                  
// mv_par21 - Do tipo do Ativo                             
// mv_par22 - Ate o tipo do Ativo                          
//

If MV_PAR18 == 1 .And. Empty(xFilial("SN1"))
	ProcLogAtu("MENSAGEM","TRATAMENTO MULTI FILIAL DESABILITADO: SN1 COMPARTILHADO")
EndIf

cMoeda :=StrZero(mv_par06,1)

cAliasQry := SelDados(	"SN3",;
						"N3_FILIAL = '" + xFilial("SN3") + "' .And. " +;
						"N3_CBASE >= '" + mv_par01 + "' .And. " +;
						"N3_CBASE <= '" + mv_par02 + "' .And. " +;
						"N3_ITEM >= '" + mv_par16 + "' .And. " +;
						"N3_ITEM <= '" + mv_par17 + "' .And. " +;
						"N3_TIPO >= '" + mv_par21 + "' .And. " +;
						"N3_TIPO <= '" + mv_par22 + "' .And. " +;
						"N3_CCUSTO >= '" + mv_par07 + "' .And. " +;
						"N3_CCUSTO <= '" + mv_par08 + "' .And. " +;
						"N3_CCONTAB >= '" + mv_par09 + "' .And. " +;
						"N3_CCONTAB <= '" + mv_par10 + "' .And. " +;
						"N3_SUBCCON >= '" + mv_par12 + "' .And. " +;
						"N3_SUBCCON <= '" + mv_par13 + "' .And. " +;
						"N3_CLVLCON >= '" + mv_par14 + "' .And. " +;
						"N3_CLVLCON <= '" + mv_par15 + "' .And. " +;
						"N3_BAIXA < '1'", .F., "SN1",, "N1_CBASE = N3_CBASE AND " +;
						"N1_ITEM = N3_ITEM AND N1_GRUPO >= '" + mv_par03 + "' AND " +;
						"N1_GRUPO <= '" + mv_par04 + "' AND " +;
						"N1_BAIXA = ' ' " ;
						+ " AND N1_TAXAPAD = '"+space(TamSX3("N1_TAXAPAD")[1])+"'" )


//Ŀ
// Inicia a leitura do SN3                                                
//

While !Eof()
    //Ŀ
    // Movimenta a regua                                                   
    //
	If ! CarregaSel("SN3", { || 	SN1->(DbSeek(xFilial()+SN3->N3_CBASE+SN3->N3_ITEM)) .And.;
									SN1->N1_GRUPO >= mv_par03 .And.;
									SN1->N1_GRUPO <= mv_par04 .And.;
									Empty(SN1->N1_BAIXA) .And.;
									Empty(SN1->N1_TAXAPAD)  } )
		Loop
	Endif

	If mv_par05 >= 0
		lUpdDepPer := cPaisLoc $ "RUS"
		Reclock("SN3",.F.)
		If mv_par11 = 1
			//********************************
			// Controle de multiplas moedas  *
			//********************************
			If lMultMoed
				AtfMultMoe("SN3","N3_TXDEPR",{|x| mv_par05 },,lUpdDepPer)
			Else
				If lUpdDepPer // update the field Depreciation period
					// Calculate the depreciation period according to the new depreciation tax
					If mv_par05 > 0
						nNewPerDep := (100 * 12)  / mv_par05
						If cCalcDep == "1" // 1 = Annually
							If nNewPerDep / 12 == Int(nNewPerDep / 12)
								nNewPerDep := nNewPerDep / 12
							Else
								nNewPerDep := Int(nNewPerDep / 12) + 1					
							Endif	
						Endif
						SN3->N3_PERDEPR := NoRound( nNewPerDep, TAMSX3('N3_PERDEPR')[2])
					Endif
				Endif
				SN3->N3_TXDEPR1 := mv_par05
				SN3->N3_TXDEPR2 := mv_par05
				SN3->N3_TXDEPR3 := mv_par05
				SN3->N3_TXDEPR4 := mv_par05
				SN3->N3_TXDEPR5 := mv_par05
			EndIf
		Else
			If lUpdDepPer .and. cMoeda == "1" // update the field Depreciation period
				// Calculate the depreciation period according to the new depreciation tax
				If mv_par05 > 0
					nNewPerDep := (100 * 12)  / mv_par05
					If cCalcDep == "1" // 1 = Annually
						If nNewPerDep / 12 == Int(nNewPerDep / 12)
							nNewPerDep := nNewPerDep / 12
						Else
							nNewPerDep := Int(nNewPerDep / 12) + 1					
						Endif	
					Endif
					SN3->N3_PERDEPR := NoRound( nNewPerDep, TAMSX3('N3_PERDEPR')[2])
				Endif
			Endif
			SN3->&(If(Val(cMoeda)>9,'N3_TXDEP','N3_TXDEPR')+cMoeda) := mv_par05
		Endif
		msUnlock()
	EndIf

	DbSelectArea(cAliasQry)
	dbSkip()
EndDo

RemoveSel("SN3")

Return

/*/


Ŀ
Funo     AtfOk     Autor  Alice Y. Yamamoto      Data  19.01.99 
Ĵ
Descrio  Mensagem e confirmacao do processamento                    
Ĵ
Sintaxe    AtfOk(void)                                                
ٱ


/*/
Static function AtfOk()
Return(.T.)

/*/


Ŀ
Funo    Af230VldTx Autor  Alice Y. Yamamoto      Data  19.01.99 
Ĵ
Descrio  Mensagem e confirmacao do processamento                    
Ĵ
Sintaxe    Af230VldTx ()                                              
ٱ


/*/
Function Af230VldTx()
Local lRet := .T.

If mv_par05 < 0
	Help(" ",1,"AF230TXDEP")  //Taxa de depreciao no pode ser menor que zero
	lRet := .F.
EndIf

Return(lRet)


/*/


Ŀ
Funo    AdmAbreSM0 Autor  Orizio                 Data  22/01/10 
Ĵ
Descrio Retorna um array com as informacoes das filias das empresas 
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Static Function AdmAbreSM0()
	Local aArea			:= SM0->( GetArea() )
	Local aAux			:= {}
	Local aRetSM0		:= {}
	Local lFWLoadSM0	:= .T.
	Local lFWCodFilSM0 	:= .T.

	If lFWLoadSM0
		aRetSM0	:= FWLoadSM0()
	Else
		DbSelectArea( "SM0" )
		SM0->( DbGoTop() )
		While SM0->( !Eof() )
			aAux := { 	SM0->M0_CODIGO,;
						IIf( lFWCodFilSM0, FWGETCODFILIAL, SM0->M0_CODFIL ),;
						"",;
						"",;
						"",;
						SM0->M0_NOME,;
						SM0->M0_FILIAL }

			aAdd( aRetSM0, aClone( aAux ) )
			SM0->( DbSkip() )
		End
	EndIf

	RestArea( aArea )
Return aRetSM0
