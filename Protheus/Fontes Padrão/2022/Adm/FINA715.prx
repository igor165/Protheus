#INCLUDE "TOTVS.ch"

/*/{Protheus.doc} FINA715
	Função para busca na SA6 dos bancos que estão configurados como retorno de baixa. Executado como JOB
	@type function JOB
	@version 12.1.33
	@author francisco.oliveira
	@since 29/03/2022
	@return logical
/*/

Function FINA715() As Logical
	Local lRet       As Logical
	Local cBancos    As Character
	Local cSelect    As Character
	Local cFrom      As Character
	Local cWhere     As Character
	Local cAlsTMP    As Character
	Local cLayoutBco As Character
	Local cParametr  As Character
	Local cParamAux  As Character
	Local jLayout    As Json
	Local cLockName	 As Character

	cLockName := cEmpAnt + " - FINA715"

	lRet	:= .F.

	If cPaisloc == 'BRA' .And. GetRPORelease() >= '12.1.033' .And. (AliasInDic("F79") .and. AliasInDic("F7A"))
		If FWModeAccess("F79",1) == FWModeAccess("F7A",1) .AND. FWModeAccess("F79",2) == FWModeAccess("F7A",2) .AND. FWModeAccess("F79",3) == FWModeAccess("F7A",3)
			If LockByName( cLockName, .T./*lEmpresa*/, .F./*lFilial*/ )
				cBancos    := '001' // separar bancos por |  exemplo 001|237
				cAlsTMP    := GetNextAlias()

				cParametr := SUPERGETMV( 'MV_FNGDTBX', .F., 'NEX' )

				Iif(cParametr == 'NEX' .Or. Empty(cParametr), cParamAux := Dtos(Date()), cParamAux := cParametr )

				cSelect := "% SA6.A6_FILIAL, SA6.A6_COD,    SA6.A6_AGENCIA, SA6.A6_DVAGE,
				cSelect += "  SA6.A6_NUMCON, SA6.A6_CFGAPI, SA6.A6_DVCTA,   SA6.A6_NOME, SA6.A6_CGC,  "
				cSelect += "  SA6.A6_BCOOFI, SA6.A6_CFGBOL, SA6. A6_BLOCKED, "
				cSelect += "  SEE.EE_AGEOFI, SEE.EE_CTAOFI, SEE.EE_SUBCTA, "
				cSelect += "  SEE.R_E_C_N_O_ AS RECNOEE, SA6.R_E_C_N_O_ AS RECNOA6 %"

				cFrom := "%" + RetSqlName("SA6") + " SA6  "
				cFrom += " INNER JOIN  " + RetSqlName("SEE") + " SEE ON "
				cFrom += " SA6.A6_COD = SEE.EE_CODIGO AND "
				cFrom += " SA6.A6_AGENCIA = SEE.EE_AGENCIA AND "
				cFrom += " SA6.A6_NUMCON = SEE.EE_CONTA %"

				cWhere := "% SA6.D_E_L_E_T_  = ' '  AND "
				cWhere += "  SEE.D_E_L_E_T_  = ' '  AND "
				cWhere += " A6_BCOOFI IN " + FormatIn(cBancos, '|') + " AND "
				cWhere += " A6_CFGAPI = '3' AND "
				cWhere += " A6_BLOCKED = '2' %"

				BeginSQL Alias cAlsTMP

				SELECT
					%Exp:cSelect%
				FROM
					%Exp:cFrom%
				WHERE
					%Exp:cWhere%
				EndSQL

				jLayout	:= JsonObject():new()

				While !(cAlsTMP)->(EOF())
					SA6->(DbGoTo((cAlsTMP)->RECNOA6))
					SEE->(DbGoTo((cAlsTMP)->RECNOEE))
					cLayoutBco := SA6->A6_CFGBOL
					jLayout:FromJson(cLayoutBco)
					lRet := .T.
					// Verifica se o Banco esta configurado para Retorno de Baixa
					If SA6->A6_BCOOFI == '001'
						lRet := &('totvs.protheus.backoffice.ngf.bills.recurrence.bb.LoadBx001(jLayout, cParamAux)')
					Endif
					(cAlsTMP)->(DBSkip())
				Enddo

				If lRet .And. (!Empty(cParametr) .And. cParametr != 'NEX' )
					PUTMV('MV_FNGDTBX', Dtos(Date()))
				Endif

				UnLockByName(cLockName, .T./*lEmpresa*/, .F./*lFilial*/ )
				
			Else
				FwLogMsg('INFO',, "FINA715", "FINA715", "", 'cLockName', "["+ cLockName + "] Running on another thread" )
			EndIf
		Else
			FwLogMsg('ERROR',, "FINA715", "FINA715", "", 'Share', "F79 and F7A tables must have the same share. Check the fields X2_MODO, X2_MODOUN and X2_MODOEMP." )
		EndIf			
	EndIf

Return lRet

/*/{Protheus.doc} SchedDef
	Execucao da rotina via Schedule.
	@return  aParam
/*/
Static Function SchedDef()
	Local aParam := {}

	aParam := {"P",; //Tipo R para relatorio P para processo
			"",;     //Nome do grupo de perguntas (SX1)
			Nil,;    //cAlias (para Relatorio)
			Nil,;    //aArray (para Relatorio)
			Nil}     //Titulo (para Relatorio)
Return aParam
