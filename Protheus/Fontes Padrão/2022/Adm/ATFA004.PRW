#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "ATFA004.CH"

//FNM_FILIAL+FNM_IDMOV
//FNM_FILIAL+FNM_ROTINA+FNM_REVIS+FNM_OPER+DTOS(FNM_DATA)+FNM_IDMOV
//FNM_FILIAL+DTOS(FNM_DATA)+FNM_ROTINA+FNM_OPER+FNM_REVIS+FNM_IDMOV
//FNM_FILIAL+FNM_CODSOL+FNM_ROTINA+FNM_OPER+FNM_REVIS+DTOS(FNM_DATA)+FNM_IDMOV
//FNM_FILIAL+FNM_CODAPR+FNM_ROTINA+FNM_OPER+FNM_REVIS+DTOS(FNM_DATA)+FNM_IDMOV

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ATFA004     ºAutor  ³Renan Guedes      º Data ³  12/09/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cadastro de operações com controle de aprovação             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFA004()

Local oBrowse
Local lRet			:= .T.
Local aArea			:= GetArea()
Local aAreaFNM		:= {}
Local cCadastro		:= STR0001		//"Movimentos de aprovação e históricos de alterações"
Local cCodUsr		:= RetCodUsr()		//Código do usuário ativo

If lRet
	dbSelectArea("FNM")
	aAreaFNM := FNM->(GetArea())
	FNM->(dbSetOrder(1))		//FNM_FILIAL+FNM_IDMOV

	oBrowse := FWMBrowse():New() 
	oBrowse:SetAlias("FNM")
	oBrowse:SetDescription(cCadastro) 			//Descrição do browse
	//Adicona legenda ao browse
	oBrowse:AddLegend( "FNM_STATUS == '1'", "BLUE"		, STR0007	)		//"Pendente"
	oBrowse:AddLegend( "FNM_STATUS == '2'", "GREEN"		, STR0008	)		//"Aprovado"
	oBrowse:AddLegend( "FNM_STATUS == '3'", "RED"		, STR0009	)		//"Reprovado"
	//Adiciona um filtro ao browse
	oBrowse:SetFilterDefault( "FNM_CODSOL == '" + cCodUsr + "' .Or. FNM_CODAPR == '" + cCodUsr + "'"	) 
	//Desliga a exibição dos detalhes
	//oBrowse:DisableDetails()
	oBrowse:Activate() 
	
	RestArea(aAreaFNM)
EndIf

RestArea(aArea)
	
Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MENUDEF   ºAutor  ³Renan Guedes        º Data ³  11/07/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MenuDef() 

Local aRotina := {} 
 
ADD OPTION aRotina Title STR0002	Action 'VIEWDEF.ATFA004' 	OPERATION 2	ACCESS 0 		//"Visualizar"
ADD OPTION aRotina Title STR0003	Action 'VIEWDEF.ATFA004'	OPERATION 4	ACCESS 0 		//"Aprovar/Rejeitar"

Return aRotina 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ATFA002   ºAutor  ³Renan Guedes        º Data ³  12/06/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Modelo de dados do cadastro de operações com controle de    º±±
±±º          ³aprovação                                                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ModelDef() 

Local oStruFNM		:= FWFormStruct( 1, 'FNM' ) 
Local oModel		// Modelo de dados que será construído 

oModel := MPFormModel():New( 'ATFA004',/*bPreValidacao*/, { |oModel| AF004PosVld(oModel) } )
//Antiga enchoice
oModel:AddFields( 'FNMMASTER', /*cOwner*/, oStruFNM )
//Descrição do modelo
oModel:SetDescription( STR0004 ) 		//"Modelo de dados dos movimentos de aprovação e históricos de alterações"
//Validação de ativação do modelo
oModel:SetVldActivate( { |oModel| AF004VldAct(oModel) } )

Return oModel


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AF004VldAct   ºAutor  ³Renan Guedes    º Data ³  12/19/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Validação da ativação do modelo - valida antes de qualquer  º±±
±±º          ³ação que ative o modelo de dados                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AF004VldAct(oModel)

Local lRet			:= .T.
Local nOperation 	:= oModel:GetOperation()
Local cCodUsr		:= RetCodUsr()		//Código do usuário ativo

If lRet .And. (nOperation == MODEL_OPERATION_UPDATE) .And. (FNM->FNM_CODAPR != cCodUsr)
	lRet := .F.
	Help(" ",1,"AFA004NAPR")		//"Usuário sem permissão para aprovar/rejeitar o movimento de aprovação."##"Somente o usuário aprovador poderá aprovar/rejeitar o movimento de aprovação."
EndIf

If lRet .And. (nOperation == MODEL_OPERATION_UPDATE) .And. (FNM->FNM_STATUS != "1")
	lRet := .F.
	Help(" ",1,"AFA004NOK")		//"Movimento de aprovação já aprovado/rejeitado."##"Somente movimentos de aprovação pendentes podem ser aprovados/rejeitados."
EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AF004PosVld   ºAutor  ³Renan Guedes    º Data ³  12/19/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Pós-validação do modelo de dados (antigo TudOk)             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AF004PosVld(oModel)

Local lRet			:= .T.
Local nOperation 	:= oModel:GetOperation()
Local cStatus		:= oModel:GetValue('FNMMASTER', 'FNM_STATUS' )
Local aArea			:= GetArea()

If lRet .And. (nOperation == MODEL_OPERATION_UPDATE) .And. !(cStatus $ "2|3|")
	lRet := .F.
	Help("",1,"AFA004STAT")		//"O movimento de aprovação somente pode ser aprovado ou rejeitado."##"O movimento de aprovação deve ser aprovado ou rejeitado. Para mantê-lo pendente, cancele a operação."
EndIf

//Valida a aprovação da revisão do AVP
If lRet .And. (nOperation == MODEL_OPERATION_UPDATE) .And. (cStatus == "2") .And. (Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA440") .And. (Upper(AllTrim(FNM->FNM_OPER)) == "06")
	lRet := AF440TudOk(Nil,cStatus,.T.,FNM->FNM_RECORI)
EndIf

If lRet
	Do Case         
	
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA440" .And. Upper(AllTrim(FNM->FNM_OPER)) == "06" .And. (cStatus == "2")		//Aprovação da revisão de AVP
			MsgRun( STR0014 ,, {||	AF440Grava(Nil,FNM->FNM_RECORI,.T.) } ) //"Processando revisão de AVP ..."
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA440" .And. Upper(AllTrim(FNM->FNM_OPER)) == "06" .And. (cStatus == "3")		//Rejeição da revisão de AVP
			AF440RepRev(FNM->FNM_RECORI)    
			
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA006" .And. Upper(AllTrim(FNM->FNM_OPER)) == "06" .And. (cStatus == "2")		//Aprovação da revisão de taxa de índice de depreciação
			AF006Aprv(1,FNM->FNM_RECORI)
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA006" .And. Upper(AllTrim(FNM->FNM_OPER)) == "06" .And. (cStatus == "3")		//Rejeição da revisão de taxa de índice de depreciação
			AF006Aprv(2,FNM->FNM_RECORI)
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA006" .And. Upper(AllTrim(FNM->FNM_OPER)) == "07" .And. (cStatus == "2")		//Aprovação do bloqueio/desbloqueio de taxa de índice de depreciação
			AF006Aprv(3,FNM->FNM_RECORI)
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA006" .And. Upper(AllTrim(FNM->FNM_OPER)) == "08" .And. (cStatus == "2")		//Aprovação da importação de taxa de índice de depreciação
			AF006Aprv(4,FNM->FNM_RECORI)
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA006" .And. Upper(AllTrim(FNM->FNM_OPER)) == "08" .And. (cStatus == "3")		//Rejeição da importação de taxa de índice de depreciação
			AF006Aprv(5,FNM->FNM_RECORI)   
			
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA430" .And. Upper(AllTrim(FNM->FNM_OPER)) == "10" .And. (cStatus == "2")		//Aprovação do Encerramento de Projeto
			dbSelectArea("FNB")
			FNB->(dbGoTo(FNM->FNM_RECORI))
			lRet := AFA430ENC("FNB",FNM->FNM_RECORI,3,.T.)
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA430" .And. Upper(AllTrim(FNM->FNM_OPER)) == "10" .And. (cStatus == "3")		//Rejeição do Encerramento de Projeto
			dbSelectArea("FNB")
			FNB->(dbGoTo(FNM->FNM_RECORI))
			AF430FLAG(FNB->FNB_CODPRJ,FNB->FNB_REVIS,"1")
			
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA430" .And. Upper(AllTrim(FNM->FNM_OPER)) == "06" .And. (cStatus == "2")		//Aprovação do Encerramento de Projeto
			dbSelectArea("FNB")
			FNB->(dbGoTo(FNM->FNM_RECORI))
	   		MsgRun( STR0011 ,, {||	lRet := AF430REVGR(FNB->FNB_CODPRJ,FNB->FNB_REVIS,.T.) } ) //"Processando revisão do Projeto..."
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA430" .And. Upper(AllTrim(FNM->FNM_OPER)) == "06" .And. (cStatus == "3")		//Aprovação do Revisão de Projeto
			dbSelectArea("FNB")
			FNB->(dbGoTo(FNM->FNM_RECORI))
			cRevis := AF430GetRev(FNB->FNB_CODPRJ,'8')
			AF430FLAG(FNB->FNB_CODPRJ,cRevis,"3")  
			
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA430" .And. Upper(AllTrim(FNM->FNM_OPER)) == "11" .And. (cStatus == "2")		//Aprovação da Atualização de Projeto
			dbSelectArea("FNB")
			FNB->(dbGoTo(FNM->FNM_RECORI))
			MsgRun( STR0016 ,, {|| lRet := AFA430ATU("FNB",FNM->FNM_RECORI,3,.T.) } )//"Realizando Atualização de Projeto"
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA430" .And. Upper(AllTrim(FNM->FNM_OPER)) == "11" .And. (cStatus == "3")		//Rejeição da Atualização de Projeto
			dbSelectArea("FNB")
			FNB->(dbGoTo(FNM->FNM_RECORI))
			AF430FLAG(FNB->FNB_CODPRJ,FNB->FNB_REVIS,"0")
			
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA430" .And. Upper(AllTrim(FNM->FNM_OPER)) == "12" .And. (cStatus == "2")		//Aprovação da Revisão da Taxa de Projeto
			dbSelectArea("FNB")
			FNB->(dbGoTo(FNM->FNM_RECORI))
			MsgRun( STR0017 ,, {|| lRet := AF430REVTX("FNB",FNM->FNM_RECORI,3,.T.,FNM->FNM_DATA) } )//"Processando revisão de taxas de AVP..."
		Case Upper(AllTrim(FNM->FNM_ROTINA)) == "ATFA430" .And. Upper(AllTrim(FNM->FNM_OPER)) == "12" .And. (cStatus == "3")		//Rejeição da Revisão da Taxa  de Projeto
			dbSelectArea("FNB")
			FNB->(dbGoTo(FNM->FNM_RECORI))
			AF430CancTx(FNB->FNB_CODPRJ,FNB->FNB_REVIS)
			AF430FLAG(FNB->FNB_CODPRJ,FNB->FNB_REVIS,"1")
			
	EndCase
EndIf

RestArea(aArea)
Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ATFA002   ºAutor  ³Renan Guedes        º Data ³  12/06/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ViewDef() 

Local oModel 	:= FWLoadModel( 'ATFA004' ) 		// Cria um objeto de Modelo de dados baseado no ModelDef() do fonte informado 
Local oStruFNM 	:= FWFormStruct( 2, 'FNM' ) 		// Cria a estrutura a ser usada na View 
Local oView  									// Interface de visualização construída 
Local bBlock	:= {|| .T.}

//tratamento para Dados Protegidos quando o usuario não tiver acesso a dados pessoais não ativar F3.
If FindFunction("CTPROTDADO") .AND. !CTPROTDADO()
	oStruFNM:SetProperty( 'FNM_CODAPR', MVC_VIEW_LOOKUP, "" )
	oStruFNM:SetProperty( 'FNM_CODSOL', MVC_VIEW_LOOKUP, "" )
Endif

// Cria o objeto de View 
oView := FWFormView():New() 
// Define qual o Modelo de dados será utilizado na View 
oView:SetModel( oModel ) 
// Adiciona no nosso View um controle do tipo formulário  
oView:AddField( 'VIEWFNM', oStruFNM, 'FNMMASTER' )
// Criar um "box" horizontal para receber algum elemento da view 
oView:CreateHorizontalBox( 'TELA' , 100 ) 
// Relaciona o identificador (ID) da View com o "box" para exibição 
oView:SetOwnerView( 'VIEWFNM', 'TELA' ) 
//Botão origem do registro de aprovação
oView:AddUserButton( STR0010, 'SDUSEEK', {|oView| AFA004Origem()} )		//"Origem"
//Força o fechamento da janela na confirmação
oView:SetCloseOnOk( bBlock )

Return oView


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AF004GrvMov  ºAutor  ³Renan Guedes     º Data ³  12/14/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AF004GrvMov(cRotina,cOper,dData,cCodSol,cMoeda,nValor,cOrigem,cAlias,nRec,lATF006AUT)

Local lRet			:= .T.
Local mMemSol		:= ""
Local cIdProc		:= ""
Local aAreaFNL		:= {}
Local aAreaFNM		:= {}
Local nTamRot, nTamOper 
Local cRev			:= ""

Default cRotina		:= ""
Default cOper		:= ""
Default dData		:= dDataBase
Default cCodSol		:= RetCodUsr()
Default cMoeda		:= "01"
Default nValor		:= 0
Default cOrigem		:= FunName()
Default cAlias		:= ""
Default nRec		:= 0
Default lATF006AUT	:= .F.   //UTILIZADO PARA ROTINA AUTOMATICA IMPORTAÇÃO CSV ATFA006IMP DA ROTINA ATFA006

If Empty(cRotina) .Or. Empty(cOper) .Or. Empty(cAlias) .Or. (nRec <= 0)
	lRet := .F.
Else
	If !lATF006AUT  //apenas rotinas não automáticas chamam a tela para o preenchimento da justificativa
	//Chama a janela para o preenchimento da justificativa
	AF004Jus(@mMemSol)
	Else
		mMemSol := STR0013  //"Importação CSV"		
	EndIf	
	
	nTamRot		:= TamSX3("FNH_ROTINA")[1]
	nTamOper	:= TamSX3("FNH_OPER")[1]
	
	//Padroniza os strings da rotina e operação
	cRotina		:= PADR(AllTrim(cRotina), nTamRot)
	cOper		:= PADR(AllTrim(cOper), nTamOper)
	
	//Posiciona no aprovador habilitado
	cRev := AFXAprRev(cRotina)
	dbSelectArea("FNL")
	aAreaFNL := FNL->(GetArea())
	FNL->(dbSetOrder(1))		//FNL_FILIAL+FNL_ROTINA+FNL_REVIS+FNL_OPER+FNL_CODAPR
	If FNL->(MsSeek(xFilial("FNL")+cRotina+cRev+cOper))
	
		//Gera um novo código de processo
		cIdProc	:= GetSxeNum('FNM','FNM_IDMOV','FNM_IDMOV'+cEmpAnt,1)
	
		//Grava o movimento de aprovação
		dbSelectArea("FNM")
		aAreaFNM := FNM->(GetArea())
		Reclock("FNM",.T.)
		FNM->FNM_FILIAL	:= xFilial("FNM")
		FNM->FNM_IDMOV	:= cIdProc
		FNM->FNM_ROTINA	:= cRotina
		FNM->FNM_REVIS	:= FNL->FNL_REVIS		//Revisão da alçada
		FNM->FNM_OPER	:= cOper
		FNM->FNM_DATA	:= dData
		FNM->FNM_CODSOL	:= cCodSol
		FNM->FNM_CODAPR	:= FNL->FNL_CODAPR
		FNM->FNM_MOEDA	:= cMoeda
		FNM->FNM_VALOR	:= nValor
		FNM->FNM_STATUS	:= "1"		//Pendente
		FNM->FNM_MEMSOL	:= mMemSol
		FNM->FNM_MEMAPR	:= ""
		FNM->FNM_ORIGEM	:= cOrigem
		FNM->FNM_TABORI	:= cAlias
		FNM->FNM_RECORI	:= nRec
		FNM->(MsUnlock())
		
		ConfirmSX8()
	Else
		lRet := .F.
	EndIf
EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AF004Jus   ºAutor  ³Renan Guedes       º Data ³  12/14/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Janela de preenchimento da justificativa do movimento       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AF004Jus(mMemSol)

Local oDlgJus		:= Nil
Local oBotOk		:= Nil

DEFINE MSDIALOG oDlgJus FROM 1,1 TO 10,48.5 OF oMainWnd STYLE DS_MODALFRAME TITLE STR0005		//"Justificativa"
@ 00,000 BITMAP oBmp RESNAME "PROJETOAP" oF oDlgJus SIZE 35,oDlgJus:nBottom / 2.4 NOBORDER WHEN .F. PIXEL
//Campo memo
@ 0.5,5.7 GET oMemo VAR mMemSol OF oDlgJus MEMO SIZE 135,40 FONT oDlgJus:oFont COLOR CLR_BLACK,CLR_HGRAY
oMemo:bRClicked := {||AllwaysTrue()}
//Botão Confirmar
DEFINE SBUTTON FROM 52,155	TYPE 1 ACTION ( IIF(AF004TOk(mMemSol),oDlgJus:End(),.F.) ) ENABLE OF oDlgJus  
oDlgJus:Activate( ,,, .T.,,, {|| oDlgJus:lEscClose := .F. } )

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AF004TOk  ºAutor  ³Alvaro Camillo Neto º Data ³  31/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Rotina que valida a tela de justificativa de solicitacao   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AF004TOk(mMemSol)
Local lRet := .T.

If Empty(mMemSol)
	Help("",1,"AFA004JUS",, STR0015, 1, 0 ) //"Por favor, preencha o campo de justificativa."
	lRet := .F.
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AFA004Origem   ºAutor  ³Renan Guedes   º Data ³  12/19/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Exibe a janela de consulta do cadastro de origem do 		  º±±
±±º          ³movimento de aprovação                                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AFA004Origem()

Local aArea			:= GetArea()
Local aAreaSN0		:= {}
Local lRet			:= .T.
Local cAlias		:= ""
Local nRec			:= 0

Private cCadastro	:= ""

dbSelectArea("SN0")
aAreaSN0 := SN0->(GetArea())
SN0->(dbSetOrder(1))		//N0_FILIAL+N0_TABELA+N0_CHAVE
If SN0->(MsSeek(xFilial("SN0")+"20"+AllTrim(FNM->FNM_ORIGEM)))
	cCadastro := AllTrim(SN0->N0_DESC01)
	cAlias := FNM->FNM_TABORI
	nRec := FNM->FNM_RECORI
	dbSelectArea(cAlias)
	(cAlias)->(dbGoTo(nRec))
	AxVisual( cAlias, nRec, 2)
EndIf

RestArea(aAreaSN0)
RestArea(aArea)

Return lRet
