#INCLUDE "rwmake.ch" 


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³CBSF2520E º Autor ³ Anderson Rodrigues º Data ³  23/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Acerto do CB0 na Exclusao da NF de devolucao via Protheus,  º±±
±±º          ³Somente se a etiqueta estiver com NF de Devolucao gravada   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAACD                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function CBSF2520E()
Local nX			:= 0
Local cCBCQEND	:= SuperGetMv("MV_CBCQEND",.F.,Space(TamSx3("CB0_LOCALI")[1]))

Private aItens := {}

If !SuperGetMV("MV_CBPE001",.F.,.F.)
	Return
EndIf

If Type("l520AUTO") =="L" .and. l520AUTO
	Return 
EndIf

If SF2->F2_TIPO # "D"
   Return
Endif

SD2->(DbSetOrder(3))
If ! SD2->(DbSeek(xFilial("SD2")+SF2->F2_DOC+SF2->F2_SERIE+SF2->F2_CLIENTE+SF2->F2_LOJA))
   Return
Endif

While ! SD2->(EOF()) .and. xFilial("SD2")  == SD2->D2_FILIAL;
   .and. SD2->D2_DOC     == SF2->F2_DOC;
   .and. SD2->D2_SERIE   == SF2->F2_SERIE;
	.and. SD2->D2_CLIENTE == SF2->F2_CLIENTE ;
	.and. SD2->D2_LOJA    == SF2->F2_LOJA
	aadd(aItens,SD2->({D2_DOC,D2_SERIE,D2_NFORI,D2_SERIORI,D2_CLIENTE,D2_LOJA,D2_COD}))
	SD2->(DbSkip())
Enddo

If Empty(aItens)
   Return
Endif
	
For nX := 1 to Len(aItens)
   CB0->(DbSetOrder(6)) 
   If CB0->(DbSeek(xFilial("CB0")+aItens[nX,03]+aItens[nX,04]+aItens[nX,05]+aItens[nX,06]+aItens[nX,07]))
      If CB0->CB0_LOCAL == AlmoxCQ() .and. CB0->CB0_LOCALI # cCBCQEND .and. CB0->(CB0_NFSAI+CB0_SERIES) == aItens[nX,01]+aItens[nX,02]			   
         DigitaEti(nX)			  
      Endif   
   Endif
Next

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³DigitaEti º Autor ³ Anderson Rodrigues º Data ³  23/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Informacao da etiqueta a ser estornada                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAACD                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function DigitaEti(nX)
Local cEtiqueta := Space(20)

@ 01,001 TO 170,326 DIALOG oDlg TITLE OemToAnsi("Ajuste da Etiqueta - Produto "+aItens[nX,07])
@ 06,008 TO 076,156

@ 15,018 SAY "Etiqueta"
@ 15,047 GET cEtiqueta Valid VldEtiq(cEtiqueta,nX) SIZE 70,150

@ 050,047 BMPBUTTON TYPE 01 ACTION OK(nX)
@ 050,087 BMPBUTTON TYPE 02 ACTION Close(oDlg)
Activate Dialog oDlg Centered
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³ VldEtiq  º Autor ³ Anderson Rodrigues º Data ³  23/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Validacao da etiqueta                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function VldEtiq(cEtiqueta,nX)
Local aEtiqueta := {}
Local cCbEndCQ	:= GetMV("MV_CBENDCQ")

CB0->(DbSetOrder(1))
If ! CB0->(DbSeek(xFilial("CB0")+cEtiqueta))
	MsgBox("Etiqueta nao encontrada","Atencao","Stop")
	Return .f.
Endif
If ascan(aEtiqueta,{|x|x[1] == CB0->CB0_CODETI}) > 0
	MsgBox("Etiqueta ja informada","Atencao","Stop")
	Return .f.
Endif
If Empty(CB0->CB0_NFSAI) 
	MsgBox("Etiqueta nao possui NF de devolucao","Atencao","Stop")
	Return .f.
Endif
If CB0->(CB0_NFENT+CB0_SERIEE+CB0_FORNEC+CB0_LOJAFO) # aItens[nX,03]+aItens[nX,04]+aItens[nX,05]+aItens[nX,06]
	MsgBox("Etiqueta Invalida","Atencao","Stop")
	Return .f.
Endif
If CB0->CB0_CODPRO # aItens[nX,07]
   MsgBox("Etiqueta Invalida","Atencao","Stop")
	Return .f.
Endif
//Se o local da etiqueta não for Arm.CQ OU (MV_CBENDCQ não vazio e o endereço for de inspeção)
If CB0->CB0_LOCAL != AlmoxCQ() .Or. (!Empty(cCbEndCQ) .And. CB0_LOCAL+Alltrim(CB0->CB0_LOCALI)+";" $ cCbEndCQ)
	MsgBox("Etiqueta Invalida","Atencao","Stop")
	Return .f.
Endif
aadd(aEtiqueta,{cEtiqueta})
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³   OK     º Autor ³ Anderson Rodrigues º Data ³  23/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Confirmacao do estorno da etiqueta                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function OK(nX)
RecLock("CB0",.f.)
CB0->CB0_NFSAI := " "
CB0->CB0_SERIES:= " "   
CB0->(CbLog("10",{CB0_CODPRO,CB0_QTDE,CB0_LOTE,CB0_SLOTE,CB0_LOCAL,CB0_LOCALI,CB0_OP,CB0_CC,NIl,CB0_CODETI,"Estorno da NF de Devolucao "+SF2->F2_DOC+SF2->F2_SERIE}))	
CB0->(MsUnlock())
Close(oDlg)
Return

