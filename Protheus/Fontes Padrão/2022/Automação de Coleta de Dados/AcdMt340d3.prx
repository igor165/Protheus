#include "rwmake.ch" 
#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ CBMT340D3³ Autor ³ Henrique Gomes Oikawa       		  	 ³ Data ³ 27/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualiza Status do SB7 e CBA (MATA340)	 	 	 				 		   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAACD																	   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function CBMT340D3(dDtEmiss, cProduto, cLocal, cLocaliz, cNumSeri, cLoteCtl, cNumLote,aMv_Par,lAltCBA,cCodUnit)
Local lUsaCB001	:= UsaCB0("01")
Local aArea		:= GetArea()
Local aSB7 		:= SB7->(GetArea())
Local aCBA 		:= CBA->(GetArea())
Local lWmsNew	:= SuperGetMv("MV_WMSNEW",.F.,.F.)
Local lUniCPO	:= SB7->(ColumnPos("B7_IDUNIT")) > 0
Local bSeek     := Nil

Default aMv_Par 	:= {}
Default lAltCBA		:= .T.
Default cCodUnit    := IIf( lUniCPO, CriaVar('D14_IDUNIT', .F.) , Space( 6 ) )

If Empty(aMv_Par)
	AADD(aMv_par,mv_par01)
	AADD(aMv_par,mv_par02)
	AADD(aMv_par,mv_par03)
	AADD(aMv_par,mv_par04)
	AADD(aMv_par,mv_par05)
	AADD(aMv_par,mv_par06)
	AADD(aMv_par,mv_par07)
	AADD(aMv_par,mv_par08)
	AADD(aMv_par,mv_par09)
	AADD(aMv_par,mv_par10)
	AADD(aMv_par,mv_par11)
	AADD(aMv_par,mv_par12)
	AADD(aMv_par,mv_par13)
	AADD(aMv_par,mv_par14)
EndIf

If ! SuperGetMV("MV_CBPE012",.F.,.F.)
	Return
EndIf           

SBE->(DbSetOrder(1))
CBA->(DbSetOrder(1))
If lUniCPO .And. lWmsNew
	SB7->(DbSetOrder(2))//B7_FILIAL+B7_NUMLOTE+B7_LOTECTL+B7_COD+B7_LOCAL+B7_LOCALIZ+B7_NUMSERI+B7_IDUNIT
	SB7->(DbSeek(xFilial("SB7")+cNumLote+cLoteCtl+cProduto+cLocal+cLocaliz+cNumSeri+cCodUnit+Dtos(dDtEmiss)))
	bSeek := {||SB7->(xFilial('SB7')+B7_NUMLOTE+B7_LOTECTL+B7_COD+B7_LOCAL+B7_LOCALIZ+B7_NUMSERI+B7_IDUNIT+Dtos(B7_DATA)) == xFilial('SB7')+cNumLote+cLoteCtl+cProduto+cLocal+cLocaliz+cNumSeri+cCodUnit+Dtos(dDtEmiss)}
Else
	SB7->(DbSetOrder(1))
	SB7->(DbSeek(xFilial("SB7")+Dtos(dDtEmiss)+cProduto+cLocal+cLocaliz+cNumSeri+cLoteCtl+cNumLote))
	bSeek := {||SB7->(xFilial('SB7')+Dtos(B7_DATA)+B7_COD+B7_LOCAL+B7_LOCALIZ+B7_NUMSERI+B7_LOTECTL+B7_NUMLOTE) == xFilial('SB7')+Dtos(dDtEmiss)+cProduto+cLocal+cLocaliz+cNumSeri+cLoteCtl+cNumLote}
EndIf

While Eval(bSeek)
	If ! (SB7->B7_DOC >= aMv_par[11] .AND. SB7->B7_DOC <= aMv_par[12])
	   SB7->(DbSkip())
	   Loop
	EndIf  
	
	//Gravar o Status no SB7 
	RecLock('SB7',.f.)
	SB7->B7_STATUS := '2'
	SB7->(MsUnlock())
	
	//Gravar o Status no Mestre 
	If lAltCBA .and. CBA->(DbSeek(xFilial("CBA")+SB7->B7_DOC)) .and. CBA->CBA_STATUS # '5'
		RecLock('CBA',.f.)
		CBA->CBA_STATUS := '5'
		CBA->(MsUnlock())     
	EndIf		                 
	
	If SB5->(DbSeek(xFilial("SB5")+SB7->B7_COD))
		RecLock('SB5',.f.)
		SB5->B5_DTINV := dDataBase
		SB5->(MsUnLock())
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se as etiquetas devem ser atualizadas ou nao.       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lUsaCB001 .and. CBA->(Found())
		CBAtuCB0()
	EndIf
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Esta funcao ira desbloquer o produto ou endereco   		     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	CBUnBlqInv(SB7->B7_DOC,SB7->B7_COD)  
	
	SB7->(DbSkip())
EndDo         
RestArea(aCBA)
RestArea(aSB7)
RestArea(aArea)
Return
