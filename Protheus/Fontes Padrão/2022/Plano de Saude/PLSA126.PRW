#INCLUDE "plsa126.ch"

#include "PLSMGER.CH"
#include "PROTHEUS.CH"

#define K_ExcLote 4
/*/
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????Ŀ??
??? Programa ? PLSA126    ? Autor ? Tulio Cesar       ? Data ? 28.12.2003 ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???Descri??o ? Processa o valor de pagamentos divididos entre o corpo     ???
???          ? clinico de uma clinica.                                    ???
??????????????????????????????????????????????????????????????????????????ٱ?
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
/*/
Function PLSA126
//?????????????????????????????????????????????????????????????????????Ŀ
//? Define variaveis da rotina...                                       ?
//???????????????????????????????????????????????????????????????????????
PRIVATE aRotina :=	MenuDef()
PRIVATE cCadastro := STR0005 //"Gerar Pagamentos Divididos"

BDP->(mBrowse(06,01,22,75,"BDP",,"BDP->BDP_STATUS='1'"))

Return
/*/
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????Ŀ??
??? Funcao   ? PLSA126INC ? Autor ? Tulio Cesar       ? Data ? 28.12.2003 ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???Descri??o ? Processa o valor de pagamentos divididos entre o corpo     ???
???          ? clinico de uma clinica.                                    ???
??????????????????????????????????????????????????????????????????????????ٱ?
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
/*/
Function PLSA126INC(cAlias,nReg,nOpc)
LOCAL nOpca      := 0
Local nFor1      := 0
Local nFor2      := 0
Local nFor3      := 0
Local nFor4      := 0
Local nNiveis    := 0
LOCAL cCodRDA    := ''
LOCAL cCalcImp   := ''  
LOCAL cCodsPags  := getNewPar("MV_PLSCDPM","101")
Local cCodAux    := ''
Local lDivParc   := .f.
Local lAchou     := .f.
LOCAL aCompPag   := {}
LOCAL aRetorno   := {}                               
LOCAL aLancPagto := {}
Local aCorpoCli  := {}
Local aNiveis    := {}

//?????????????????????????????????????????????????????????????????????Ŀ
//? Executa funcao padrao para incluir....                              ?
//???????????????????????????????????????????????????????????????????????
nOpca := AxInclui(cAlias,0,K_Incluir) 

If nOpca == K_OK
   //?????????????????????????????????????????????????????????????????????Ŀ
   //? Inicio do processamento...                                          ?
   //???????????????????????????????????????????????????????????????????????
   BAU->(DbSetOrder(1))
   BAU->(DbSeek(xFilial("BAU")))
   While ! BAU->(Eof()) .And. BAU->BAU_FILIAL == xFilial("BAU")
         
         If BAU->BAU_MODPAG $ "2,3"
            If BAU->BAU_CODIGO >= BDP->BDP_RDADE .And. BAU->BAU_CODIGO <= BDP->BDP_RDAATE
            
               cCodRDA  := BAU->BAU_CODIGO           
               aCompPag := {}
               cCalcImp := IF(BAU->(FieldPos("BAU_CALIMP"))>0,BAU->BAU_CALIMP,"2")
               aRetorno := PLSLDCRE(cCodRDA,BDP->BDP_ANO,BDP->BDP_MES,nil,nil,"","ZZZZ","","ZZZZZZZZ","","ZZZZZZZZ",;
                                    BDP->BDP_OPERDA,BAU->BAU_CODSA2,BAU->BAU_LOJSA2,aLancPagto,cCalcImp,"","ZZZZ","","","",.F.,"",.F.,.T.)

               If aRetorno[1]
                  lDivParc := .F.
                  aCorpoCli := {}
			      //?????????????????????????????????????????????????????????????????????Ŀ
			      //? Monta array com o Corpo Clinico e Procedimentos a dividir pagamento ?
				  //???????????????????????????????????????????????????????????????????????
                  P126Proced(@aCorpoCli, BAU->BAU_CODIGO, BDP->BDP_OPERDA)
                  For nFor1 := 1 To Len(aCorpoCli)
			      	  //?????????????????????????????????????????????????????????????????????Ŀ
			     	  //? Se nao ha procedimentos/grupos de procedimentos cadastrados para o  ?
			     	  //? medico, divide o valor de todos os procedimentos da guia.           ?
			     	  //? Desta forma, mantem-se a funcionalidade anterior do programa.       ?
				  	  //???????????????????????????????????????????????????????????????????????
                      If Len(aCorpoCli[nFor1, 4]) == 0
	                  	 aCompPag := aRetorno[2]
	                  	 If Len(aCompPag) > 0
	                    	For nFor2 := 1 To Len(aCompPag)
	                       	    If aCompPag[nFor2,3] $ cCodsPags
	                       	       aCorpoCli[nFor1, 3] += aCompPag[nFor2,2]
	                       	    Endif
	                     	Next nFor2
	                  	 EndIf
	                  Else          
			      	     //?????????????????????????????????????????????????????????????????Ŀ
			     	     //? Se ha procedimentos/grupos de procedimentos cadastrados para o  ?
			     	     //? medico, divide apenas o valor dos procedimentos da guia que se  ?
			     	     //? enquadrem nos procedimentos/grupos de procedimentos cadastrados ?
				  	     //???????????????????????????????????????????????????????????????????
	                     lDivParc := .T.
	                     For nFor2 := 1 To Len(aCorpoCli[nFor1, 4])
						     aNiveis  := PLSESPNIV(aCorpoCli[nFor1, 4][nFor2, 2])
						     nNiveis  := (aNiveis[1]+1)     
		                     aCompPag := aRetorno[7]
		                     For nFor3 := 1 To Len(aCompPag)
	                       	   If aCompPag[nFor3,5] $ cCodsPags
			                      lAchou := .F.
							      For nFor4 := 1 To nNiveis
							   	   	   If  nFor4 == 1 // Procedimento
							       		   cCodAux := aCorpoCli[nFor1, 4][nFor2, 1]+aCorpoCli[nFor1, 4][nFor2, 2]+aCorpoCli[nFor1, 4][nFor2, 3]
								       Else           // Grupo de Procedimentos
								           cCodAux := aCorpoCli[nFor1, 4][nFor2, 1]+aCorpoCli[nFor1, 4][nFor2, 2]+Subs(aCorpoCli[nFor1, 4][nFor2, 3],aNiveis[2,(nFor4-1),1],aNiveis[2,(nFor4-1),2])
								       Endif
								       If SubStr(aCompPag[nFor3,1]+aCompPag[nFor3,2]+aCompPag[nFor3,3], 1, Len(AllTrim(cCodAux))) == cCodAux
								          lAchou := .T.
								          Exit
								       EndIf
						  	      Next nFor4
					      	      //????????????????????????????????????????????????????????????????????Ŀ
				     	    	  //? Se o procedimento da guia se enquadrou no procedimento ou no grupo ?
				     		      //? de procedimentos cadastrado, acumular valor para ser dividido.     ?
						  	      //??????????????????????????????????????????????????????????????????????
						  	      If lAchou
						  	         aCorpoCli[nFor1, 3] += aCompPag[nFor3, 4]
						  	      EndIf
						  	   EndIf
	                  	     Next nFor3
	                     Next nFor2
                      EndIf
                  Next nFor1
                  
		          MsAguarde( { || ProcDiv(BAU->BAU_MODPAG,BDP->BDP_OPERDA,aCorpoCli,lDivParc) }  , 'Aguarde. Processando...' , "" , .T. ) 

               Endif   
            Endif   
         Endif   
   
   BAU->(DbSkip())
   Enddo
Endif
//?????????????????????????????????????????????????????????????????????Ŀ
//? Fim da rotina...                                                    ?
//???????????????????????????????????????????????????????????????????????
Return
/*/
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????Ŀ??
??? Funcao   ? ProcDiv    ? Autor ? Tulio Cesar       ? Data ? 28.12.2003 ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???Descri??o ? Processa a rotina.                                         ???
??????????????????????????????????????????????????????????????????????????ٱ?
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
/*/
Static Function ProcDiv(cModPag,cOpeRda,aCorpoCli,lDivParc)
LOCAL nAux
Local nTotRat
Local nDif    
Local nTotPerc
LOCAL cCodLanDeb := GetNewPar("MV_PLSLNDB","900")
LOCAL cCodLanCre := GetNewPar("MV_PLSLNCR","901")
Local nInd
                                                                   
BBB->(DbSetOrder(1))
If ! BBB->(DbSeek(xFilial("BBB")+cCodLanDeb))
   CriaBBBPad("1",cCodLanDeb)
Endif   

If ! BBB->(DbSeek(xFilial("BBB")+cCodLanCre))
   CriaBBBPad("2",cCodLanCre)
Endif   

MsProcTxt(STR0006+BAU->BAU_CODIGO+" - "+BAU->BAU_NOME) //"Processando Prestador "

If  Len(aCorpoCli) > 0
	nTotRat  := 0                        
	nTotPerc := 0                                             
	//????????????????????????????????????????????????????????????????????????????????Ŀ
	//? Se nao ha procedimentos/grupos de procedimentos cadastrados para nenhum dos    ?
	//? membros do corpo clinico, soma percentuais de divisao. Caso a soma seja 100%,  ?
	//? devera fazer ajuste de centavos no ultimo membro do corpo clinico              ?
	//??????????????????????????????????????????????????????????????????????????????????
	If ! lDivParc
		aEval(aCorpoCli, { |x| nTotPerc += x[2] })
	EndIf
	//?????????????????????????????????????????????????????????????????????Ŀ
	//? Calcula o valor a creditar para cada membro do corpo clinico.       ?
	//???????????????????????????????????????????????????????????????????????
	For nInd := 1 To Len(aCorpoCli)
	    If aCorpoCli[nInd, 3] > 0
			nAux    := Round((aCorpoCli[nInd, 3]*aCorpoCli[nInd, 2])/100,2)
			nTotRat += nAux                                
			//?????????????????????????????????????????????????????????????????????Ŀ
			//? Se eh o ultimo item do array, nao ha procedimentos/grupos de proce- ?
			//? dimentos cadastrados para nenhum dos membros do corpo clinico e a   ?
			//? soma dos percentuais de divisao somam 100%, faz ajuste de centavos  ?
			//? no ultimo membro do corpo clinico.                                  ?
			//???????????????????????????????????????????????????????????????????????
			If nInd == Len(aCorpocli)
				If ! lDivParc .And. nTotPerc == 100
				   nDif    := aCorpoCli[nInd, 3] - nTotRat
				   nAux    += nDif
				   nTotRat += nDif
				EndIf
			EndIf
			//?????????????????????????????????????????????????????????????????????Ŀ
			//? Grava o Credito para o membro do corpo clinico.                     ?
			//???????????????????????????????????????????????????????????????????????
			GravaDebCre(BDP->BDP_OPERDA,aCorpoCli[nInd, 1],BDP->BDP_ANO,BDP->BDP_MES,nAux,"C",cCodLanCre,BDP->BDP_NUMSEQ)
	    EndIf
	Next nInd

	//?????????????????????????????????????????????????????????????????????Ŀ
	//? Grava o Debito para a RDA da Guia com o valor total dividido entre  ?
	//? os membros do corpo clinico.                                        ?
	//???????????????????????????????????????????????????????????????????????
	GravaDebCre(BDP->BDP_OPERDA,BAU->BAU_CODIGO,BDP->BDP_ANO,BDP->BDP_MES,nTotRat,"D",cCodLanDeb,BDP->BDP_NUMSEQ)
   
    BEQ->(RecLock("BEQ",.T.))
    BEQ->BEQ_FILIAL := xFilial("BEQ")
    BEQ->BEQ_CODRDA := BAU->BAU_CODIGO
    BEQ->BEQ_ANO    := BDP->BDP_ANO
    BEQ->BEQ_MES    := BDP->BDP_MES
    BEQ->BEQ_NUMSEQ := BDP->BDP_NUMSEQ
    BEQ->(MsUnLock())
Endif

Return
/*/
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????Ŀ??
??? Funcao   ?GravaDebCre ? Autor ? Tulio Cesar       ? Data ? 28.12.2003 ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???Descri??o ? Grava o debito ou credito                                  ???
??????????????????????????????????????????????????????????????????????????ٱ?
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
/*/
Static Function GravaDebCre(cOpeRda,cCodRda,cAno,cMes,nValor,cTipo,cCodLan,cNumSeq)
//cTipo  "D" Debito
//       "C" Credito
LOCAL cObs
LOCAL cNomRda
LOCAL nOrdBAU := BAU->(IndexOrd())
LOCAL nRecBAU := BAU->(Recno())

cNomRda := BAU->(Posicione("BAU",1,xFilial("BAU")+cCodRda,"BAU_NOME"))

BAU->(DbSetOrder(nOrdBAU))
BAU->(DbGoTo(nRecBAU))

If cTipo == "C"
   cObs := GetNewPar("MV_PLSMSCR",STR0007) //"LANCAMENTO AUTOMATICO CREDITO CORPO CLINICO EM FUNCAO DA DIVISAO DA PRODUCAO"
Else
   cObs := GetNewPar("MV_PLSMSDB",STR0008) //"LANCAMENTO AUTOMATICO DEBITO RDA EM FUNCAO DA DIVISAO DA PRODUCAO"
Endif   

BGQ->(DbSetOrder(4))
If ! BGQ->(DbSeek(xFilial("BGQ")+cOpeRda+cCodRda+cAno+cMes+cCodLan))
   BBB->(DbSetOrder(1))
   BBB->(DbSeek(xFilial("BBB")+cCodLan))

   BGQ->(RecLock("BGQ",.T.))
   BGQ->BGQ_FILIAL := xFilial("BGQ")
   BGQ->BGQ_CODSEQ := GetSX8Num("BGQ","BGQ_CODSEQ")
   BGQ->BGQ_CODIGO := cCodRda
   BGQ->BGQ_NOME   := cNomRda
   BGQ->BGQ_ANO    := cAno
   BGQ->BGQ_MES    := cMes
   BGQ->BGQ_CODLAN := cCodLan
   BGQ->BGQ_VALOR  := nValor
   BGQ->BGQ_TIPO   := BBB->BBB_TIPSER
   BGQ->BGQ_INCIR  := BBB->BBB_INCIR
   BGQ->BGQ_INCINS := BBB->BBB_INCINS
   BGQ->BGQ_INCPIS := BBB->BBB_INCPIS
   BGQ->BGQ_INCCOF := BBB->BBB_INCCOF
   BGQ->BGQ_INCCSL := BBB->BBB_INCCSL
   BGQ->BGQ_CONMFT := BBB->BBB_CONMFT
   BGQ->BGQ_TIPOCT := BBB->BBB_TIPOCT
   BGQ->BGQ_INTERC := "0"
   BGQ->BGQ_CODOPE := cOpeRda
   BGQ->BGQ_OBS    := cObs
   BGQ->BGQ_NUMLOT := ""
   BGQ->BGQ_OPELOT := ""
   BGQ->BGQ_LANAUT := "1"          
   BGQ->BGQ_NUMLAU := ""
   BGQ->BGQ_OPELAU := ""
   BGQ->BGQ_NMSQLA := cNumSeq
   BGQ->(MsUnLock())                               
   ConfirmSX8()
 Else
   BGQ->(RecLock("BGQ",.F.))
   BGQ->BGQ_VALOR  += nValor
   BGQ->(MsUnLock())                               
Endif

Return
/*/
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????Ŀ??
??? Funcao   ? PLSA126EXC ? Autor ? Tulio Cesar       ? Data ? 28.12.2003 ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???Descri??o ? Exclui um lote de pagamentos divididos                     ???
??????????????????????????????????????????????????????????????????????????ٱ?
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
/*/
Function PLSA126EXC(cAlias,nReg,nOpc)
LOCAL nOpca 
LOCAL cNumSeq
//?????????????????????????????????????????????????????????????????????Ŀ
//? Define variaveis da rotina...                                       ?
//???????????????????????????????????????????????????????????????????????
If BDP->BDP_STATUS == "2"
   Help("",1,"PLSA126EXC")
   Return
Endif

cNumSeq := BDP->BDP_NUMSEQ
nOpca   := AxDeleta(cAlias,nReg,K_Visualizar) 
//?????????????????????????????????????????????????????????????????????Ŀ
//? Leitura de todos os debitos...                                      ?
//???????????????????????????????????????????????????????????????????????
BGQ->(DbSetOrder(5))
While BGQ->(DbSeek(xFilial("BGQ")+cNumSeq))
      BGQ->(RecLock("BGQ",.F.))
      BGQ->(DbDelete())
      BGQ->(MsUnLock())
Enddo
//?????????????????????????????????????????????????????????????????????Ŀ
//? Leitura de todos os lancamento de divisoes mensais                  ?
//???????????????????????????????????????????????????????????????????????
BEQ->(DbSetOrder(2))
While BEQ->(DbSeek(xFilial("BEQ")+cNumSeq))
      BEQ->(RecLock("BEQ",.F.))
      BEQ->(DbDelete())
      BEQ->(MsUnLock())
Enddo
//?????????????????????????????????????????????????????????????????????Ŀ
//? Fim da rotina...                                                    ?
//???????????????????????????????????????????????????????????????????????
Return

Static Function CriaBBBPad(cTipo,cCodigo)
//1 debito
//2 credito                   
LOCAL cMsg

If cTipo == "1"
   cMsg := STR0009 //"LANCAMENTO DE DEBITO PARA PAGAMENTOS DIVIDIDOS"
Else
   cMsg := STR0010 //"LANCAMENTO DE CREDITO PARA PAGAMENTOS DIVIDIDOS"
Endif   

BBB->(RecLock("BBB",.T.))
BBB->BBB_FILIAL := xFilial("BBB")
BBB->BBB_CODSER := cCodigo
BBB->BBB_DESCRI := cMsg
BBB->BBB_VLRSER := 0
BBB->BBB_QTDCH  := 0
BBB->BBB_TIPSER := cTipo
BBB->BBB_INCIR  := "1"
BBB->BBB_INCINS := "1"
BBB->BBB_CODANT := ""
BBB->BBB_CONTA  := ""
BBB->BBB_HISTC  := ""
BBB->(MsUnLock())


Return

/*/
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????Ŀ??
??? Funcao   ? P126PROCED ? Autor ? Sandro Hoffman    ? Data ? 05.06.2006 ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???Descri??o ? Monta array com o Corpo Clinico. Para cada membro do corpo ???
???          ? clinico, monta array com os procedimentos/grupos de proce- ???
???          ? dimentos que deverao ter o pagamento dividido.             ???
??????????????????????????????????????????????????????????????????????????ٱ?
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
/*/
Static Function P126Proced(aCorpoCli, cCodRDA, cOpeRDA)

    Local nInd
    
	BC1->(DbSetOrder(2))
	BC1->(MsSeek(xFilial("BC1")+cCodRDA+cOpeRDA))
    Do While ! BC1->(Eof()) .And. ;
                 BC1->(BC1_FILIAL+BC1_CODIGO+BC1_CODINT) == xFilial("BC1")+cCodRDA+cOpeRDA
		aAdd(aCorpoCli, { BC1->BC1_CODRDA, BC1->BC1_PERSOC, 0, {} })
	    If BE6->(FieldPos("BE6_PGTDIV")) > 0
			nInd := Len(aCorpoCli)
			BE6->(DbSetOrder(1))
			BE6->(MsSeek(xFilial("BE6")+cCodRDA+BC1->(BC1_CODPRF+BC1_CODLOC+BC1_CODESP)))
			Do While ! BE6->(Eof()) .And. ;
			             BE6->(BE6_FILIAL+BE6_CODIGO+BE6_CODPRF+BE6_CODLOC+BE6_CODESP) == xFilial("BE6")+cCodRDA+BC1->(BC1_CODPRF+BC1_CODLOC+BC1_CODESP)
				//???????????????????????????????????????????????????????????????????Ŀ
				//? Somente grava array dos procedimentos/grupos de procedimentos se  ?
				//? no BE6 estiver informado que o mesmo devera ser dividido.         ?
				//?????????????????????????????????????????????????????????????????????
			    If BE6->BE6_PGTDIV == "1" // 0-Nao / 1-Sim
			       aAdd(aCorpoCli[nInd, 4], { BE6->BE6_CODTAB, BE6->BE6_CODPAD, BE6->BE6_CODPRO })
			    EndIf
			 	BE6->(DbSkip())
			EndDo
		EndIf
    	BC1->(DbSkip())
	EndDo

Return

/*/
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????Ŀ??
???Programa  ?MenuDef   ? Autor ? Darcio R. Sporl       ? Data ?27/12/2006???
?????????????????????????????????????????????????????????????????????????Ĵ??
???Descri??o ? Utilizacao de menu Funcional                               ???
???          ?                                                            ???
???          ?                                                            ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???Retorno   ?Array com opcoes da rotina.                                 ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???Parametros?Parametros do array a Rotina:                               ???
???          ?1. Nome a aparecer no cabecalho                             ???
???          ?2. Nome da Rotina associada                                 ???
???          ?3. Reservado                                                ???
???          ?4. Tipo de Transa??o a ser efetuada:                        ???
???          ?		1 - Pesquisa e Posiciona em um Banco de Dados           ???
???          ?    2 - Simplesmente Mostra os Campos                       ???
???          ?    3 - Inclui registros no Bancos de Dados                 ???
???          ?    4 - Altera o registro corrente                          ???
???          ?    5 - Remove o registro corrente do Banco de Dados        ???
???          ?5. Nivel de acesso                                          ???
???          ?6. Habilita Menu Funcional                                  ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???   DATA   ? Programador   ?Manutencao efetuada                         ???
?????????????????????????????????????????????????????????????????????????Ĵ??
???          ?               ?                                            ???
??????????????????????????????????????????????????????????????????????????ٱ?
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
/*/
Static Function MenuDef()
Private aRotina := {	{ STR0001	,'AxPesqui'		,0 ,K_Pesquisar		,0 ,.F.},; //"Pesquisar"
											{ STR0002	,'AxVisual  '	,0 ,K_Visualizar	,0 ,Nil},; //"Visualizar"
											{ STR0003	,'PLSA126Inc'	,0 ,K_Incluir			,0 ,Nil},; //"Incluir"
											{ STR0004	,'plsa126Exc'	,0 ,K_ExcLote			,0 ,Nil}}  //"Excluir"
Return(aRotina)