#include "PROTHEUS.CH"
#include "PLSMGER.CH"
/*/
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддбдддддддбддддддддддддддддддддддддбддддддбдддддддддд©╠╠╠
╠╠ЁFuncao    Ё PLSA182 Ё Autor Ё Paulo Carnelossi       Ё Data Ё 13.06.03 Ё╠╠╠
╠╠цддддддддддедддддддддадддддддаддддддддддддддддддддддддаддддддадддддддддд╢╠╠╠
╠╠ЁDescricao Ё Internacao                                                 Ё╠╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠ЁSintaxe   Ё PLSA182()                                                  Ё╠╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠Ё Uso      Ё Advanced Protheus                                          Ё╠╠╠
╠╠Ё Uso      Ё Advanced Protheus                                          Ё╠╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠Ё Alteracoes desde sua construcao inicial.                              Ё╠╠╠
╠╠цддддддддддбддддддбдддддддддддддбддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠Ё Data     Ё BOPS Ё Programador Ё Breve Descricao                       Ё╠╠╠
╠╠цддддддддддеддддддедддддддддддддеддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠Ё 01.07.03 Ё      Ё Tulio Cesar Ё Apos informar a data de inclusao      Ё╠╠╠
╠╠Ё          Ё      Ё             Ё gerar a senha aleatoria...            Ё╠╠╠
╠╠юддддддддддаддддддадддддддддддддаддддддддддддддддддддддддддддддддддддддды╠╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function PLSA182()
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define variaveis PRIVATE...                                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aRotina   := MenuDef()
PRIVATE cCadastro := "Data de Internacao"

PRIVATE aCdCores  := { { 'BR_VERDE'    ,'Autorizada'              },;
                        { 'BR_VERMELHO' ,'Autorizada Parcial'          },;
                        { 'BR_AMARELO'  ,'Nao Autorizada'  },;
                        { 'BR_MARRON'   ,'Aguardando finalizacao do atendimento' }}

PRIVATE aCores    := { { 'BE4_STATUS = "1"',aCdCores[1,1] },;
                        { 'BE4_STATUS = "2"',aCdCores[2,1] },;
                        { 'BE4_STATUS = "3"',aCdCores[3,1] },;
                        { 'BE4_STATUS = "4"',aCdCores[4,1] }}
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Endereca a funcao de BROWSE...                                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
BE4->(DbSetOrder(1))
BE4->(DbSeek(xFilial("BE4")))
BE4->(mBrowse(006,001,022,075,"BE4" , , , , , Nil    , aCores, , , ,nil))
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Fim da Rotina Principal...                                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Return
/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё PLSA182Mov Ё Autor Ё Paulo Carnelossi    Ё Data Ё 13.06.03 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Programa de Alimentacao da data de internacao              Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function PLSA182Mov(cAlias,nReg,nOpc)
Local I__f := 0
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define variaveis de uso generico...                                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
LOCAL nOpcA      := 0                                                           
LOCAL oDlg                   
LOCAL aCampos    := {}
LOCAL nFor          
LOCAL nPos                    
LOCAL oSay
Local oGet
Local oEncInt  

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Uso na enchoiceBar...                                                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
LOCAL aButtons   := {}                             
LOCAL bOK        := {|| nOpca := 1,If(.T.,oDlg:End(),nOpca:=2),If(nOpca==1,oDlg:End(),.F.) }
LOCAL bCancel    := {|| oDlg:End() }

LOCAL dDatInt	:= IF(nOpc==K_Incluir,dDataBase,BE4->BE4_DATPRO)
LOCAL cHorInt  := IF(nOpc==K_Incluir,StrTran(Time(),":",""),BE4->BE4_HORPRO)
Local lDataInt := .T.   
Local cSenhaPLS

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define fonte...                                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local oFont
LOCAL oFontAutor

If nOpc == K_Incluir .And. ! Empty(BE4->BE4_DATPRO)
   Aviso( "JA GRAVADO", "JА estА alimentada a data de internacao para esta GIH. Utilize a rotina de alteraГЦo!", { "Ok" }, 2 ) 
   Return
ElseIf nOpc == K_Incluir .And. BE4->BE4_STATUS == "3"
   Aviso( "JA GRAVADO", "A guia nao foi autorizada.", { "Ok" }, 2 ) 
   Return
ElseIf nOpc == K_Alterar .And. Empty(BE4->BE4_DATPRO)
   Aviso( "NAO EXISTENTE", "NЦo foi alimentada data de internacao para esta GIH. Utilize a rotina de inclusЦo!", { "Ok" }, 2 ) 
   Return

ElseIf (nOpc == K_Alterar .OR. nOpc == K_Excluir) .And. A182BQV()
   Aviso( "NAO ALTERAR", "NЦo pode ser alterada a data de internacao para esta GIH, por existirem apontamentos de evoluГЦo de diАrias. Exclua primeiro as evoluГУes cadastradas!", { "Ok" }, 2 ) 
   Return

ElseIf nOpc == K_Visualizar .OR. nOpc == K_Excluir
   lDataInt := .F.   
   
EndIf   
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define fontes utilizadas somente nesta funcao...                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD

DEFINE FONT oFontAutor NAME "Arial" SIZE 000,-010 BOLD
                                            
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta dados da enchoice...                                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Copy "BE4" To Memory

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta os campos que serao exibidos para o caso de aut. internacao...     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
STORE FIELDS "BE4" TO aCampos

For nFor := 1 To Len(__aNotCampos)
    nPos := Ascan(aCampos,{|x| x = __aNotCampos[nFor]})
    If nPos > 0
       aDel(aCampos,nPos)
    Endif   
Next

// Cria Dialog...
DEFINE MSDIALOG oDlg TITLE cCadastro FROM ndLinIni,000 TO ndLinFin,ndColFin OF GetWndDefault()

@ 032, 003 GROUP oGrupo TO 073, 393 PIXEL OF oDlg LABEL " "+"Incluir Data de InternaГЦo" +" "  COLOR CLR_HBLUE, CLR_HRED
@ 045, 070  Say oSay PROMPT "Data da InternaГЦo" SIZE 250,010 OF oDlg PIXEL FONT oFont COLOR CLR_HBLUE
@ 045, 125  Say oSay PROMPT ": " SIZE 250,010 OF oDlg PIXEL FONT oFont COLOR CLR_HBLUE
@ 042, 130  MSGET oGet VAR dDatInt SIZE 0,0 OF oDlg FONT oFont PICTURE "@D" WHEN lDataInt VALID A360CHEBLO(M->BE4_CODRDA,dDatInt) PIXEL 
@ 045, 220  Say oSay PROMPT "Hora da InternaГЦo" SIZE 250,010 OF oDlg PIXEL FONT oFont COLOR CLR_HBLUE
@ 045, 275  Say oSay PROMPT ": " SIZE 250,010 OF oDlg PIXEL FONT oFont COLOR CLR_HBLUE
@ 042, 280  MSGET oGet VAR cHorInt SIZE 0,0 OF oDlg FONT oFont PICTURE "@R 99:99" WHEN lDataInt PIXEL 

// Exibe say...
@ 065,287 SAY oSay PROMPT "Internacao Numero   "  SIZE 220,010 OF oDlg PIXEL FONT oFontAutor
@ 065,335 SAY oSay PROMPT BE4->BE4_CODOPE+"."+BE4->BE4_ANOINT+"."+BE4->BE4_MESINT+"."+BE4->BE4_NUMINT SIZE 220,010 OF oDlg PIXEL COLOR CLR_HRED FONT oFontAutor
If ! Empty(BE4->BE4_SENHA)
   @ 065,240 SAY oSay PROMPT "Senha "  SIZE 220,010 OF oDlg PIXEL FONT oFontAutor
   @ 065,257 SAY oSay PROMPT BE4->BE4_SENHA SIZE 220,010 OF oDlg PIXEL COLOR CLR_HRED FONT oFontAutor
Endif

// Cria Enchoice...
oEncInt := BE4->(MSMGET():New("BE4",BE4->(Recno()),K_Visualizar,,,,aCampos,{075,001,210,393},aCampos,,,,,oDlg,,,.F.))
M->BE4_USUARI := BE4->(BE4_OPEUSR+BE4_CODEMP+BE4_MATRIC+BE4_TIPREG+BE4_DIGITO)

// Ativa Dialog...
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, bOK, bCancel, nOpc == K_Excluir, aButtons)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Busca retorno contido no aRetorno...                                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If nOpca == K_OK                       	                  
   If nOpc <> K_Visualizar
   
   	If nOpc == K_Excluir
   		dDatInt := Ctod(Space(8))
   		cHorInt := ""
   	EndIf	
   	
      //здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      //Ё Inicia transacao...                                                      Ё
      //юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
      Begin Transaction
      //здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      //Ё Gravacao dos dados da internacao...                                      Ё
      //юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
      A182BE4(dDatInt, cHorInt, nReg) // Grava Data e Hora Internacao (senha qdo for o caso)
      
      If nOpc == K_Incluir
			A182Final(BE4->BE4_SENHA)
      ElseIf nOpc == K_Excluir
      
      	If BQV->(dbSeek(xFilial("BQV")+BE4->BE4_CODOPE+BE4->BE4_ANOINT+BE4->BE4_MESINT+BE4->BE4_NUMINT))
	      	RecLock("BQV", .F.)
				BQV->(dbDelete())
				MsUnLock()
	   	EndIf
      
      EndIf
      //здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      //Ё Finaliza transacao...                                                    Ё
      //юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
      End Transaction

   Endif   
Endif
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Fim da Rotina Principal...                                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Return	

/*/
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠╠
╠╠ЁFuncao    Ё PLSA182LEG Ё Autor Ё Paulo Carnelossi    Ё Data Ё 13.06.03 Ё╠╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠╠
╠╠ЁDescricao Ё Exibe a legenda...                                         Ё╠╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function PLSA182Leg()
Local aLegenda := { { aCdCores[1,1],aCdCores[1,2] },;
                     { aCdCores[2,1],aCdCores[2,2] },;
                     { aCdCores[3,1],aCdCores[3,2] },;
                     { aCdCores[4,1],aCdCores[4,2] }}
BrwLegenda(cCadastro,"Status" ,aLegenda)
Return


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁPLSA182   ╨Autor  ЁMicrosiga           ╨ Data Ё  06/17/03   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё                                                            ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё AP                                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A182BQV()
Local aArea  := GetArea(), nCount := 0

BQV->(dbSeek(xFilial("BQV")+BE4->BE4_CODOPE+BE4->BE4_ANOINT+BE4->BE4_MESINT+BE4->BE4_NUMINT))

BQV->(DBEval({|| nCount++},,{||BQV_FILIAL+BQV_CODOPE+BQV_ANOINT+BQV_MESINT+BQV_NUMINT ==;
						xFilial("BQV")+BE4->BE4_CODOPE+BE4->BE4_ANOINT+BE4->BE4_MESINT+BE4->BE4_NUMINT},,,.F.))
RestArea(aArea)

Return(nCount>1)



/*/
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддбдддддддбддддддддддддддддддддддддбддддддбдддддддддд©╠╠╠
╠╠ЁFuncao    ЁPLSSENAUTЁ Autor Ё Tulio Cesar            Ё Data Ё 01.07.03 Ё╠╠╠
╠╠цддддддддддедддддддддадддддддаддддддддддддддддддддддддаддддддадддддддддд╢╠╠╠
╠╠ЁDescricao Ё Gera uma senha aleatoria de autorizacao de internacao      Ё╠╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function PLSSenAut(dDataGer)
LOCAL cSenhaPLS	:= ''
LOCAL cLet1		:= ''
LOCAL cLet2		:= ''
LOCAL cLet3		:= ''        
Local nTam		:= TamSx3('BEA_SENHA')[1]   
DEFAULT dDataGer := Date()

If GetNewPar("MV_PLSMSEN","1") == "2"

   cLet1	:= CHR(65+Val(PLSRAND('25')))
   cLet2	:= CHR(65+Val(PLSRAND('25')))
   cLet3	:= CHR(65+Val(PLSRAND('25')))
   
   If VALTYPE(dDataGer) == "C"
   		cStr := cLet1+cLet2+cLet3+Subs(dDataGer,1,2)
   Else
   		cStr := cLet1+cLet2+cLet3+Subs(dtoc(dDataGer),1,2)
   EndIf
   
   cSenhaPLS := IIf( nTam > 5, PLSRAND( Replicate('9',nTam-5), cStr ), '99999')
   
Else
   cSenhaPLS := PLSRAND( Replicate('9',IIf(nTam < 9, 9, nTam ) ) )
Endif

Return(cSenhaPLS)    
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё A182Final Ё Autor Ё Tulio Cesar          Ё Data Ё 01.07.03 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Exibir a senha de autorizacao de internacao                Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
/*/
Static Function A182Final(cSenhaPLS)
LOCAL oDlg

LOCAL oFontAutor                                    
LOCAL oFontTit
LOCAL oSay                   
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define fontes utilizadas somente nesta funcao...                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
DEFINE FONT oFontNum NAME "Arial" SIZE 000,-016 BOLD
DEFINE FONT oFontAutor NAME "Arial" SIZE 000,-019 BOLD

DEFINE MSDIALOG oDlg TITLE "Senha de Autorizacao" FROM 009,000 TO 015,070 OF GetWndDefault()

@ 007,005 SAY oSay PROMPT "Senha   "  SIZE 220,010 OF oDlg PIXEL FONT oFontNum
@ 006,095 SAY oSay PROMPT cSenhaPLS SIZE 220,010 OF oDlg PIXEL FONT oFontAutor COLOR CLR_HRED

ACTIVATE MSDIALOG oDlg CENTERED

Return                                            

Function A182BE4(dDatInt, cHorInt, nReg)
LOCAL nH
   //здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
   //Ё Gravacao dos dados da internacao...                                      Ё
   //юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
   BEJ->(DbSetOrder(1))
   BEJ->(DbSeek(xFilial("BEJ")+BE4->(BE4_CODOPE+BE4_ANOINT+BE4_MESINT+BE4_NUMINT)))
      
   BE4->(dbGoTo(nReg))
   
   nH := PLSAbreSem("P090SEMDEF.SMF")             
   BE4->(RecLock("BE4", .F.))
   BE4->BE4_DATPRO := dDatInt
   BE4->BE4_HORPRO := cHorInt
   If Empty(BE4->BE4_SENHA)          
     	 BE4->BE4_SENHA  := PLSSenAut(dDatInt)
   Endif   
   BE4->(MsUnLock())                          
   PLSFechaSem(nH,"P090SEMDEF.SMF")               

Return

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁVerSis     Ё Autor Ё Eduardo Motta        | Data Ё 08.12.05 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao ЁFuncao intermediaria a PLSGETVERSAO para que nao de mensagemЁ╠╠
╠╠Ё          Ёde funcao nao existe caso a PLSGETVERSAO nao esteja compiladЁ╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
/*/

Static Function VerSis()
Local nVersao := 7
If FindFunction("PLSGETVERSAO")
   nVersao := PlsGetVersao()
EndIf
Return nVersao

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁMenuDef   Ё Autor Ё Darcio R. Sporl       Ё Data Ё29/12/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Utilizacao de menu Funcional                               Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁArray com opcoes da rotina.                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁParametros do array a Rotina:                               Ё╠╠
╠╠Ё          Ё1. Nome a aparecer no cabecalho                             Ё╠╠
╠╠Ё          Ё2. Nome da Rotina associada                                 Ё╠╠
╠╠Ё          Ё3. Reservado                                                Ё╠╠
╠╠Ё          Ё4. Tipo de Transa┤└o a ser efetuada:                        Ё╠╠
╠╠Ё          Ё		1 - Pesquisa e Posiciona em um Banco de Dados           Ё╠╠
╠╠Ё          Ё    2 - Simplesmente Mostra os Campos                       Ё╠╠
╠╠Ё          Ё    3 - Inclui registros no Bancos de Dados                 Ё╠╠
╠╠Ё          Ё    4 - Altera o registro corrente                          Ё╠╠
╠╠Ё          Ё    5 - Remove o registro corrente do Banco de Dados        Ё╠╠
╠╠Ё          Ё5. Nivel de acesso                                          Ё╠╠
╠╠Ё          Ё6. Habilita Menu Funcional                                  Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function MenuDef()
Private aRotina := {	{ STRPL01  ,'AxPesqui'  ,  0 , K_Pesquisar  , 0, .F.},;
											{ STRPL02 ,'PLSA182Mov',  0 , K_Visualizar , 0, Nil},;
											{ STRPL03    ,'PLSA182Mov',  0 , K_Incluir    , 0, Nil},;
											{ STRPL04    ,'PLSA182Mov',  0 , K_Alterar    , 0, Nil},;
											{ STRPL05    ,'PLSA182Mov',  0 , K_Excluir    , 0, Nil},;
											{ "Legenda"    ,"PLSA182LEG",  0 , 0            , 0, .F.} } 
Return(aRotina)
