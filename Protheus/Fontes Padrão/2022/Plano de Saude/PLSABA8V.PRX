#INCLUDE "TOTVS.CH"
#INCLUDE 'FWMBROWSE.CH'
#INCLUDE 'FWMVCDEF.CH'  
                            
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Define
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
#DEFINE PLS_CORLIN 	"#D6E4EA"

/*/{Protheus.doc} PLSABA8V
View da rotina de Tabela DinБmica de Eventos

@author Alexander Santos
@since 10/02/2011
@version P11
/*/

function PLSABA8V() 
local aArea     	:= GetArea()
local oFWLayer 	:= nil 
local oPLUpTop 	:= nil
local oDlg		:= nil 
local oTFolder	:= nil
local oBA8		:= nil
local oBDY		:= nil 
local oBD4		:= nil 
local oBA8C		:= PLSABA8C():new()
local aFolder		:= oBA8C:getFolder()
local aSize     := MsAdvSize(.F.)
local aInfo		:= {}
local aObjects	:= {}
local aPosObj		:= {}
local lBDY		:= oBA8C:getlBDY()

private lTemDig   	:= .f.
private cCadastro 	:= oBA8C:getTitulo()
private _nTamCod		:= PLSRETCT()
private _cCodPad  	:= oBA8C:getCodPad()
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Dividindo a tela ao meio																 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
aObjects 	:= {	{ 100, 50, .T., .T. },;
					{ 100, 50, .T., .T. } }

aInfo		:= { aSize[1], aSize[2], aSize[3], aSize[4], 0, 0 }
aPosObj	:= MsObjSize( aInfo, aObjects, .T. )
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё MsDialog																 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
oDlg := msDialog():New(aSize[7],0,aSize[6],aSize[5],cCadastro,,,,,,,,,.T.)
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Cria o conteiner onde serЦo colocados browse's							 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
oPLUp := tPanel():New(aPosObj[1,1],aPosObj[1,2],,oDlg,,,,,,aPosObj[1,4],aPosObj[1,3]-5)
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Nao entra no modelo correto															 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
aRotina := {}                               
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Adiciona mais um filtro somente se a glosa ainda nao foi filtrada
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
oBA8 := FWmBrowse():New()
oBA8:setOwner(oPLUp)
oBA8:setProfileID('0')
oBA8:setMainProc("PLSABA8M")
oBA8:setAlias(oBA8C:getAlias(0)) 
oBA8:setDescription(oBA8C:getTitulo(0))
oBA8:setMenuDef(oBA8C:getModel(0))
oBA8:disableDetails()  
oBA8:disableReport()                 
oBA8:setFilterDefault(oBA8C:getFiltro())
oBA8:setBlkBackColor( {||PLS_CORLIN} )    
oBA8:activate()
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Folder																	 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
oTFolder := tFolder():new(aPosObj[2,1],aPosObj[2,2],aFolder,,oDlg,,,,.t.,,aPosObj[2,4],(aPosObj[2,3]-(aPosObj[1,3]-aPosObj[1,1])))
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Nao entra no modelo correto															 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
aRotina := {}                               
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Browse BD4															 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
oBD4 := FWmBrowse():New()
oBD4:setOwner(oTFolder:aDialogs[1])
oBD4:setProfileID('1')
oBD4:setMainProc("PLSABD4M")
oBD4:setAlias(oBA8C:getAlias(1))
oBD4:setDescription(oBA8C:getTitulo(1))
oBD4:setMenuDef(oBA8C:getModel(1))
oBD4:disableDetails()  
oBD4:forceQuitButton()
oBD4:disableReport()                  	
oBD4:setBlkBackColor({||PLS_CORLIN})
oBD4:activate()
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Relacao do Browse Mestre com os Detail									 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
oRBA8BD4 := FWBrwRelation():new()       
oRBA8BD4:addRelation( oBA8, oBD4, {	{'BD4_FILIAL', 'xFilial("BA8")' },;
											{'BD4_CODTAB', 'BA8_CODTAB'},;
											{'BD4_CDPADP', 'BA8_CDPADP'},;
											{'BD4_CODPRO', 'BA8_CODPRO'} } )
oRBA8BD4:Activate()
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Verifica se monta o BDY															 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
if lBDY
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//Ё Browse BDY															 
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	oBDY := FWmBrowse():New()
	oBDY:setOwner(oTFolder:aDialogs[2])
	oBDY:setProfileID('2')
	oBDY:setAlias(oBA8C:getAlias(2))
	oBDY:setDescription(oBA8C:getTitulo(2))
	oBDY:setMenuDef(oBA8C:getModel(2))
	oBDY:disableDetails()  
	oBDY:forceQuitButton()
	oBDY:disableReport()                  	
	oBDY:setBlkBackColor({||PLS_CORLIN})
	oBDY:activate()
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//Ё Relacao do Browse Mestre com os Detail									 
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	oRBA8BDY := FWBrwRelation():new()       
	oRBA8BDY:addRelation( oBA8, oBDY, {	{'BDY_FILIAL', 'xFilial("BA8")' },;
												{'BDY_CODTAB', 'BA8_CODTAB'},;
												{'BDY_CDPADP', 'BA8_CDPADP'},;
												{'BDY_CODPRO', 'BA8_CODPRO'} } )
	oRBA8BDY:Activate()
endIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Ativando componentes de tela											 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
oDlg:lCentered := .T.
oDlg:bRClicked := {||}
oDlg:Activate()       
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Rest nas linhas do browse e na area										 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
restArea(aArea)                   
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//Ё Fim da Rotina															 
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
return NIL