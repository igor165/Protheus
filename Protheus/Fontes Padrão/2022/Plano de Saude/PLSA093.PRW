#include "PLSMGER.CH"
#include "PROTHEUS.CH"
/*/
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддбдддддддбддддддддддддддддддддддддбддддддбдддддддддд©╠╠╠
╠╠ЁFuncao    Ё PLSA093 Ё Autor Ё Tulio Cesar            Ё Data Ё 27.05.03 Ё╠╠╠
╠╠цддддддддддедддддддддадддддддаддддддддддддддддддддддддаддддддадддддддддд╢╠╠╠
╠╠ЁDescricao Ё Prorrogacao de Internacoes                                 Ё╠╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠ЁSintaxe   Ё PLSA093()                                                  Ё╠╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠Ё Uso      Ё Advanced Protheus                                          Ё╠╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠Ё Alteracoes desde sua construcao inicial.                              Ё╠╠╠
╠╠цддддддддддбддддддбдддддддддддддбддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠Ё Data     Ё BOPS Ё Programador Ё Breve Descricao                       Ё╠╠╠
╠╠цддддддддддеддддддедддддддддддддеддддддддддддддддддддддддддддддддддддддд╢╠╠╠
╠╠Ё          Ё      Ё             Ё                                       Ё╠╠╠
╠╠юддддддддддаддддддадддддддддддддаддддддддддддддддддддддддддддддддддддддды╠╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function PLSA093()
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define variaveis LOCAL...                                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
LOCAL cFil
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define variaveis PRIVATE...                                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aRotina   := MenuDef()
                        
PRIVATE aCdCores  := { { 'BR_VERDE'		,'Sem prorrogacoes'	},;
                        { 'BR_VERMELHO'	,'Ja prorrogada'	}}
                        
PRIVATE cCadastro := "Prorrogacao de Internacao"
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta filtro...                                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
#IFDEF TOP
        cFil := "@BE4_FILIAL = '"+xFilial("BE4")+"' AND BE4_STATUS = '1' AND D_E_L_E_T_ = ' '"
#ELSE                
        cFil := "BE4_FILIAL = '"+xFilial("BE4")+"' .And. BE4_STATUS = '1' "
#ENDIF

BE4->(DbSetOrder(1))   
DbSelectArea("BE4")
SET FILTER TO &cFil
BE4->(DbSetOrder(1))
BE4->(MsSeek(xFilial("BE4")))

BE4->(mBrowse(06,01,22,75,"BE4",,,,,,{ 	{ "Pls093Pro()", "DISABLE" },;
						            		{ ".T.", "ENABLE" } },,,,, .T. ))
DbSelectArea("BE4")
SET FILTER TO
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Fim da Rotina Principal...                                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Return
/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё PLSA093Mov Ё Autor Ё Tulio Cesar         Ё Data Ё 27.05.03 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Programa de Movimentacao das Prorrogacoes de Internacoes   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function PLSA093Mov(cAlias,nReg,nOpc)
Local I__f := 0
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define variaveis de uso generico...                                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
LOCAL nOpcA      := 0                                                           
LOCAL oDlg                   
LOCAL oSay                   
LOCAL oEncPro
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Uso na enchoiceBar...                                                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
LOCAL aButtons   := {}                             
LOCAL bOK        := {|| nOpca := 1,If( Obrigatorio(aGets,aTela) , oDlg:End() ,(nOpca:=2,.F.) ) ,If( nOpca==1 , oDlg:End() , .F. ) }
LOCAL bCancel    := {|| oDlg:End() }                                 
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define fonte...                                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local oFont
LOCAL oFontAutor                                    
Local lDataInt := .T.
LOCAL dDatInt  := IF(Empty(BE4->BE4_DATPRO),(lDataInt := .T., dDataBase),(lDataInt := .F., BE4->BE4_DATPRO))
LOCAL cHorInt  := IF(Empty(BE4->BE4_HORPRO),StrTran(Time(),":",""),BE4->BE4_HORPRO)
Local cSenhaPLS
Local nOpcOld


PRIVATE aGets[0]
PRIVATE aTela[0][0]
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define fontes utilizadas somente nesta funcao...                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
DEFINE FONT oFontAutor NAME "Arial" SIZE 000,-017 BOLD
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Checa se foi informado a data de internacao...                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/* // Retirado em 09/01/2006 - Sandro - Passou a permitir a digitacao da data/hora de internacao
   // para guias que ainda nao tiverem essa informacao gravada
If Empty(BE4->BE4_DATPRO)
   Aviso( "NAO INTERNADO", "Nao alimentado a data de internaГЦo deste usuario. NЦo permitido lanГar evolucao de diАrias!", { "Ok" }, 2 ) 	
   Return
Endif
*/
DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta dados da enchoice...                                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Copy "BET" To Memory Blank
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Dialog...                                                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
DEFINE MSDIALOG oDlg TITLE cCadastro FROM ndLinIni,000 TO ndLinFin,ndColFin OF GetWndDefault()

@ 017, 003 GROUP oGrupo TO 050, 393 PIXEL OF oDlg LABEL " "+"Alimentar Data de InternaГЦo" +" "  COLOR CLR_HBLUE, CLR_HRED
@ 032, 070  Say oSay PROMPT "Data da InternaГЦo" SIZE 250,010 OF oDlg PIXEL FONT oFont COLOR CLR_HBLUE
@ 032, 125  Say oSay PROMPT ": " SIZE 250,010 OF oDlg PIXEL FONT oFont COLOR CLR_HBLUE
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se a RDA esta bloqueada na data da internacao                   Ё
//Ё Alexander 09/06/2005													 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
@ 030, 130  MSGET oGet VAR dDatInt SIZE 0,0 OF oDlg FONT oFont PICTURE "@D" WHEN lDataInt VALID A360CHEBLO(BE4->BE4_CODRDA,dDatInt) PIXEL 
@ 032, 220  Say oSay PROMPT "Hora da InternaГЦo" SIZE 250,010 OF oDlg PIXEL FONT oFont COLOR CLR_HBLUE
@ 032, 275  Say oSay PROMPT ": " SIZE 250,010 OF oDlg PIXEL FONT oFont COLOR CLR_HBLUE
@ 030, 280  MSGET oGet VAR cHorInt SIZE 0,0 OF oDlg FONT oFont PICTURE "@R 99:99" WHEN lDataInt PIXEL 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Enchoice...                                                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oEncInt := BET->(MsMGet():New("BET",0,K_Incluir,,,,,{055,001,196,393},,,,,,oDlg,,,.F.))
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Exibe say...                                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
//@ 001,217 SAY oSay PROMPT "Internacao Numero   "  SIZE 220,010 OF oEncInt:oBox PIXEL FONT oFontAutor
//@ 001,300 SAY oSay PROMPT BE4->BE4_CODOPE+"."+BE4->BE4_ANOINT+"."+BE4->BE4_MESINT+"."+BE4->BE4_NUMINT SIZE 220,010 OF oEncInt:oBox PIXEL COLOR CLR_HRED FONT oFontAutor
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ativa Dialog...                                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,bOK,bCancel,.F.,aButtons)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Busca retorno contido no aRetorno...                                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If nOpca == K_OK                       	                  

   If lDataInt
      //здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      //Ё Inicia transacao...                                                      Ё
      //юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
      Begin Transaction                

         nOpcOld := nOpc
         nOpc    := K_Incluir
         A182BE4(dDatInt, cHorInt, nReg) // Grava Data e Hora Internacao (senha qdo for o caso)
         nOpc    := nOpcOld

      //здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      //Ё Finaliza transacao...                                                    Ё
      //юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
      End Transaction
			
   EndIf			

   //здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
   //Ё Grava a prorrogacao...                                                   Ё
   //юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
   M->BET_NUMPRO := PLSA093Num()
   M->BET_CODOPE := BE4->BE4_CODOPE
   M->BET_ANOINT := BE4->BE4_ANOINT
   M->BET_MESINT := BE4->BE4_MESINT
   M->BET_NUMINT := BE4->BE4_NUMINT
      
   BET->(PLUPTENC("BET",nOpc))
   //здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
   //Ё Exibe mensagem informativa...                                            Ё
   //юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
   A093Final(M->BET_CODOPE,M->BET_ANOINT,M->BET_MESINT,M->BET_NUMINT,M->BET_NUMPRO,BE4->BE4_SENHA,lDataInt)
Endif
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Fim da Rotina Principal...                                               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Return	
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё A093Final Ё Autor Ё Tulio Cesar          Ё Data Ё 25.07.03 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Exibir dados da Prorrogacao da Internacao                  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
/*/
Static Function A093Final(cCodOpe,cAnoInt,cMesInt,cNumInt,cNumPro,cSenhaPLS,lDataINt)
LOCAL cNomUser
LOCAL cSIM
LOCAL nNAO
LOCAL cNomCre
LOCAL oDlg

LOCAL oFontNum
LOCAL oFontAutor                                    
LOCAL oFontTit
LOCAL oSay                   
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define fontes utilizadas somente nesta funcao...                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
DEFINE FONT oFontNum NAME "Arial" SIZE 000,-016 BOLD
DEFINE FONT oFontAutor NAME "Arial" SIZE 000,-019 BOLD
DEFINE FONT oFontTit NAME "Arial" SIZE 000,-011 BOLD
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Mostra Resumo da Internacao...                                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
DEFINE MSDIALOG oDlg TITLE "Dados da Internacao" FROM 009,000 TO 018,070 OF GetWndDefault()

SButton():New(097, 005, 1, {|| oDlg:End() },,.T.)
                                               
If lDataInt         
   @ 007,005 SAY oSay PROMPT "Senha de Autorizacao  "  SIZE 220,010 OF oDlg PIXEL FONT oFontNum
   @ 006,095 SAY oSay PROMPT cSenhaPLS SIZE 220,010 OF oDlg PIXEL FONT oFontAutor COLOR CLR_HRED
EndIf
@ 027,005 SAY oSay PROMPT "Prorrogacao de Internacao Numero   "  SIZE 220,010 OF oDlg PIXEL FONT oFontNum
@ 045,005 SAY oSay PROMPT cCodOpe+"."+cAnoInt+"."+cMesInt+"."+cNumInt+" - "+cNumPro SIZE 220,010 OF oDlg PIXEL FONT oFontAutor COLOR CLR_HRED

ACTIVATE MSDIALOG oDlg CENTERED

Return
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё PLSA093NumЁ Autor Ё Tulio Cesar          Ё Data Ё 25.07.03 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Busca o proximo numero de prorrogacao de internacao...     Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
/*/
Function PLSA093Num()
LOCAL cNewCod := PLSA625Cd("BET_NUMPRO","BET",1,"BET->(BET_CODOPE+BET_ANOINT+BET_MESINT+BET_NUMINT)",BE4->(BE4_CODOPE+BE4_ANOINT+BE4_MESINT+BE4_NUMINT))
Return(cNewCod)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё PLSA093VisЁ Autor Ё Tulio Cesar          Ё Data Ё 25.07.03 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Visualiza as prorrogacoes em aberto...                     Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
/*/
Function PLSA093Vis(cAlias,nReg,nOpc,cNil,nOp)

LOCAL cTitulo
LOCAL aRet	    := {}
LOCAL aDados    := {}
LOCAL aAreaBE4  := nil
LOCAL cGuiInt   := BE4->(BE4_GUIINT)
LOCAL cNumInt   := BE4->( BE4_CODOPE + BE4_ANOINT + BE4_MESINT + BE4_NUMINT )
LOCAL cSQL    
DEFAULT nOp     := 1

if BE4->BE4_TIPGUI == "05" 
  //A rotina previa ser chamada somente da guia de solicitaГЦo, mas tambИm И preciso prever quando estА sendo chamado de um resumo.
  //Se for resumo eu busco a solicitaГЦo com base no guiint para depois buscar as prorrogaГУes
  aAreaBE4  := BE4->(GetArea())

  BE4->(DbSetOrder(1))
  if BE4->( MsSeek( xFilial("BE4") + cGuiInt ) )
    cNumInt := BE4->( BE4_CODOPE + BE4_ANOINT + BE4_MESINT + BE4_NUMINT )
  EndIf
  
  RestArea( aAreaBE4 )

endIf

cSQL    := " SELECT  '1' TIPO, R_E_C_N_O_ RECNO, BET_CODOPE CODOPE,BET_NUMPRO NUMEROPRO, "
cSQL    += " BET_DATPRO DATPRO,BET_NDIAS DIAS ,BET_MOTPRO MOTIVO FROM "+ RetSQLName("BET") +" WHERE "
cSQL    += " BET_FILIAL = '" + xFilial("BET")+ "' AND "
cSQL    += " BET_CODOPE = '" + SubStr(cNumInt, 01 , 4 ) + "' AND "
cSQL    += " BET_ANOINT = '" + SubStr(cNumInt, 05 , 4 ) + "' AND "
cSQL    += " BET_MESINT = '" + SubStr(cNumInt, 09 , 2 ) + "' AND "
cSQL    += " BET_NUMINT = '" + SubStr(cNumInt, 11 , 8 ) + "' AND "
cSQL    += " D_E_L_E_T_ = ' ' "    
cSQL    += " UNION ALL "
CSQL    += " SELECT '2' TIPO, R_E_C_N_O_ RECNO, B4Q_OPEMOV CODOPE, "
cSQL    += " B4Q_OPEMOV||B4Q_ANOAUT||B4Q_MESAUT||B4Q_NUMAUT NUMEROPRO, "
cSQL    += " B4Q_DATPRO DATPRO,B4Q_QTDADA DIAS,'' MOTIVO FROM " + RetSQLName("B4Q") + " WHERE "
cSQL    += " B4Q_FILIAL = '" + xFilial("B4Q") + "' AND "
cSQL    += " B4Q_GUIREF = '" + cNumInt + "' AND "
cSQL    += " D_E_L_E_T_ = ' ' "

      
PLSQuery(cSQL,"TrbBET")

While ! TrbBET->(Eof())    
	If TrbBET->(TIPO) == '1'	
    	BET->(DBGoTo(TrbBET->(RECNO))) 
	    TrbBET->(aadd(aDados,{NUMEROPRO,DATPRO,DIAS,MOTIVO,BEU->(Posicione("BEU",1,xFilial("BEU")+TrbBET->(CODOPE+MOTIVO),"BEU_DESCRI")),BET->BET_MSG01,BET->BET_MSG02,BET->BET_MSG03}))
 	Else
 		B4Q->(DBGoTo(TrbBET->(RECNO)))
	    TrbBET->(aadd(aDados,{NUMEROPRO,DATPRO,DIAS,"","",ALLTRIM(B4Q->B4Q_INDCLI),ALLTRIM(B4Q->B4Q_JUSOPE),ALLTRIM(B4Q->B4Q_JUSOBS)}))
 	EndIf	
   TrbBET->(DbSkip())
Enddo
 
TrbBET->(DbCloseArea())

If Len(aDados) > 0   

   If nOp == 1
      cTitulo := Transform(cNumInt,"@R 9999.9999.99.99999999") + ' VisualizaГЦo'
   Else
      cTitulo := Transform(cNumInt,"@R 9999.9999.99.99999999") + ' ExclusЦo'
   EndIf                        
   
   aRet := PLSCRIGEN(aDados,{ 	{"Numero","@C",025}			,{"Data","@D",030 }			,{"Nr Dias","999",30 },;
   								{"Motivo","@C",030 }		,{"Descricao","@C",100 }	,{"Observacao 1","@C",050},;
   								{"Observacao 2","@C",050}	,{"Observacao 3","@C",050} } ,;
   								"  Prorrogacoes da Internacao "+cTitulo)
   								
   If nOp == 2 .and. aRet[1]

      If MsgYesNo("Confirma a EXCLUSцO da ProrrogaГЦo de InternaГЦo N╨ " + cNumInt + " Item " + aDados[aRet[2],1] + " ?")
      
         BET->( DbSetOrder(1) ) //BET_FILIAL + BET_CODOPE + BET_ANOINT + BET_MESINT + BET_NUMINT + BET_NUMPRO + DTOS(BET_DATPRO)
         If BET->( MsSeek( xFilial("BET")+BE4->BE4_CODOPE+BE4->BE4_ANOINT+BE4->BE4_MESINT+BE4->BE4_NUMINT+Alltrim(aDados[aRet[2],1] ) ) )
	         BET->(RecLock("BET",.F.))
		         BET->(DbDelete())
	         BET->(MsUnLock())
         EndIf
         
	  EndIf 	

   EndIf
Else
   Help("",1,"PLSA093Vis")
Endif   

Return
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё PLSA093NDIЁ Autor Ё Tulio Cesar          Ё Data Ё 25.07.03 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Sugere numero de dias de prorrogacao.                      Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
/*/
Function PLSA093NDI()
LOCAL nRet := 0

BEJ->(DbSetOrder(1))
BEJ->(MsSeek(xFilial("BEJ")+BE4->(BE4_CODOPE+BE4_ANOINT+BE4_MESINT+BE4_NUMINT)))

Return(nRet)   
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё PLSA093INIЁ Autor Ё Tulio Cesar          Ё Data Ё 30.05.03 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Inicializa dados.                                          Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
/*/
Function PLSA093INI()


oBrwBD6:bChange    := { || Eval(oBrwBD6:bLostFocus),oBrwBD7:ForceRefresh(oBrwBD6) }
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё GETDADOS -> Parametros Pagamento X Procedimento...                  Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oBrwBD7:oPai       := oBrwBD6
oBrwBD7:aOrigem    := {"BD6_SEQUEN"}
oBrwBD7:aRelac     := {"BD7_SEQUEN"}

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё PLSA093Pro Ё Autor Ё Wagner Mobile Costa Ё Data Ё 16.06.03 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Retorna se a internacao ja foi ou nao prorrogada           Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function PLS093Pro

Return 	BET->(MsSeek(xFilial("BET") + BE4->BE4_CODOPE + BE4->BE4_ANOINT +;
		BE4->BE4_MESINT + BE4->BE4_NUMINT))

/*/
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠╠
╠╠ЁFuncao    Ё PLSA093LEG Ё Autor Ё Geraldo Felix JuniorЁ Data Ё 08.01.02 Ё╠╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠╠
╠╠ЁDescricao Ё Exibe a legenda...                                         Ё╠╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function PLSA093Leg()
Local aLegenda := { { aCdCores[1,1],aCdCores[1,2] },;
                     { aCdCores[2,1],aCdCores[2,2] }}
                     
BrwLegenda(cCadastro,"Status" ,aLegenda)

Return


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё PLSR093  Ё Autor Ё  BATATINHA            Ё Data Ё 05.04.06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁGuia de Autorizacao de Prorrogacao de Internacao            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё PLSR093(void)                                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё SIGAPLS                                                    Ё╠╠ 
╠╠юддддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Function PLSR093()

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Variaveis                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
LOCAL CbCont,cabec1,cabec2,cabec3,nPos,wnrel
LOCAL tamanho	:= "M"
LOCAL titulo	:= "AUTORIZACAO DE PRORROGACAO DE INTERNACAO HOSPITALAR"
LOCAL cDesc1	:= "Impressao da Guia de Prorrogacao de Internacao Hospitalar"
LOCAL cDesc2	:= "de acordo com a configuracao do usuario."
LOCAL cDesc3	:= ""               			
LOCAL lPrinter	:= .T.
LOCAL cNumProrr := ""
LOCAL cMsg1		:= ""
LOCAL cMsg2     := ""
LOCAL cMsg3     := ""
LOCAL cMotPro   := ""
LOCAL cOpeInt   := ""
LOCAL cAnoInt   := ""
LOCAL cMesInt   := ""
LOCAL cNumInt   := ""
LOCAL nNDias    := 0
LOCAL dDatProrr := ""

PRIVATE nSvRecno	:= BE4->( Recno() )	//Salva posicao do BE4 para Restaurar apos SetPrint()
PRIVATE aReturn 	:= { "Zebrado", 1,"Administracao", 2, 2, 1, "", 1 }  //"Zebrado"###"Administracao"
PRIVATE aLinha		:= { }
PRIVATE nomeprog	:="PLSR093",nLastKey := 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Objetos utilizados na impressao grafica                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Private oFont07,oFont08n, oFont08 ,oFont10, oFont10n, oFont12,oFont12n
Private oFont15,oFont15n, oFont21n,oFont16n, oFont09, oFont09n
Private oPrint

oFont07		:= TFont():New("Tahoma",07,07,,.F.,,,,.T.,.F.)
oFont08n	:= TFont():New("Tahoma",08,08,,.T.,,,,.T.,.F.)		//negrito
oFont08 	:= TFont():New("Tahoma",08,08,,.F.,,,,.T.,.F.)	
oFont09		:= TFont():New("Tahoma",09,09,,.F.,,,,.T.,.F.)                
oFont09n	:= TFont():New("Tahoma",09,09,,.T.,,,,.T.,.F.)                
oFont10 	:= TFont():New("Tahoma",10,10,,.F.,,,,.T.,.F.)		//Font padrao utilizado para impressao de detalhe
oFont10n	:= TFont():New("Tahoma",10,10,,.T.,,,,.T.,.F.)		//negrito 
oFont12		:= TFont():New("Tahoma",12,12,,.F.,,,,.T.,.F.)		//Normal s/negrito
oFont12n	:= TFont():New("Tahoma",12,12,,.T.,,,,.T.,.F.)		//Negrito
oFont15 	:= TFont():New("Tahoma",15,15,,.F.,,,,.T.,.F.)		
oFont15n	:= TFont():New("Tahoma",15,15,,.T.,,,,.T.,.F.)		//Negrito
oFont21n	:= TFont():New("Tahoma",21,21,,.T.,,,,.T.,.T.)      	//Negrito
oFont16n	:= TFont():New("Arial",16,16,,.T.,,,,.T.,.F.)        //Negrito

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas para Impressao do Cabecalho e Rodape    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
cbcont   := 0
cabec1   := "AUTORIZACAO DE PRORROGACAO DE INTERNACAO HOSPITALAR"
cabec2   := ""
cabec3   := ""
cString  := "BE4"
aOrd     := {}
wnrel    :="PLSR093"           											//--Nome Default do relatorio em Disco

//-- Objeto para impressao grafica
oPrint 	:=TMSPrinter():New("GUIA DE AUTORIZACAO DE PRORROGACAO DE INTERNACAO HOSPITALAR ")
oPrint  :SetPortrait()										//--Modo retrato
oPrint	:StartPage() 										//--Inicia uma nova pagina
                                                        
//-- Verifica se existe alguma impressora  configurada para Impres.Grafica ...
lPrinter	:= oPrint:IsPrinterActive()

If !lPrinter
	oPrint:Setup()
Endif

wnrel:=SetPrint(cString,wnrel,,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.F.,tamanho)
If nLastKey = 27
	Set Filter To
	Return
Endif

RptStatus({|lEnd| R093Imp(@lEnd,wnRel,cString)},Titulo)

oPrint:Preview()  		// Visualiza impressao grafica antes de imprimir

Return

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё R093IMP  Ё Autor Ё BATATINHA             Ё Data Ё 05/03/06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Chamada do Relatorio                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё PLSR093                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function R093Imp(lEnd,wnRel,cString)

Local cFileLogo		:= ""
Local nLinMax		:= 2050									//-- Numero maximo de Linhas
Local nColMax		:= 2350									//-- Numero maximo de Colunas
Local nColIni		:= 50                                   //-- Coluna Lateral (inicial) Esquerda
Local li 			:= 30 									//-- Contador de Linhas
Local cDet 			:= ""
Local nTotLin
Local nInd

DbSelectArea("BE4")
//-- Posiciona o ponteiro
BE4->( dbGoto( nSvRecno ) )	

//Posiciono no BET e carrego as variaveis utilizadas na impressao.
BET->(DbSetOrder(1))
If !BET->(DbSeek(xFilial("BET")+ BE4->(BE4_CODOPE + BE4_ANOINT + BE4_MESINT + BE4_NUMINT)))
	MsgAlert("Nao Existem Prorrogacoes para a GIH:"+Space(1)+"("+BE4->(BE4_CODOPE+"."+BE4_ANOINT+"."+BE4_MESINT+"."+BE4_NUMINT)+")"+Space(1)+"selecionada para impressao") 
	Return
Else
	While ! BET->(Eof()) .And. BET->(BET_FILIAL+BET_CODOPE+BET_ANOINT+BET_MESINT+BET_NUMINT) == xFilial("BET")+BE4->(BE4_CODOPE+BE4_ANOINT+BE4_MESINT+BE4_NUMINT)
			cOpeInt   := BET->BET_CODOPE
			cAnoInt   := BET->BET_ANOINT
			cMesInt   := BET->BET_MESINT
			cNumInt   := BET->BET_NUMINT
	 		cNumProrr := BET->BET_NUMPRO
			dDatProrr := BET->BET_DATPRO
	 		nNDias    := BET->BET_NDIAS                    					
	 	  	cMotPro   := BET->BET_MOTPRO                    					
	 	  	cMsg1 	  := BET->BET_MSG01
			cMsg2 	  := BET->BET_MSG02 		                    					
			cMsg3 	  := BET->BET_MSG03 
 	  	BET->(DbSkip())
	Enddo  
EndIf

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁBox Principal                                                 Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:Box( 030,030,nLinMax, nColMax )

//  -- CABECALHO DA GUIA  -- //
/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁCarrega e Imprime Logotipo da Empresa                         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
fLogoEmp(@cFileLogo)
oPrint:Line(30,50,30,nColMax)
If File(cFilelogo)
	oPrint:SayBitmap(080,50, cFileLogo,400,090) 		//-- Tem que estar abaixo do RootPath
Endif 

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁNome da Operadora 										     Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
cDet	:= ""
BA0->(DbSetOrder(1))
BA0->(DbSeek(xFilial("BE4")+ BE4->(BE4_OPEUSR)))
oPrint:say(150 ,500, BA0->BA0_NOMINT , oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁEndereco                										 Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(200 , 500 , BA0->BA0_END + space(02) + BA0->BA0_BAIRRO                         , oFont08)
oPrint:say(250 , 500 , BA0->BA0_CEP + space(2)+  BA0->BA0_CIDADE + space(2)+ BA0->BA0_EST , oFont08)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁAUTORIZACAO DE PRORROGACAO DE INTERNACAO HOSPITALAR No :      Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(040, 0500,"AUTORIZACAO DE PRORROGACAO DE INTERNACAO HOSPITALAR ", oFont15n)
cDet	:= cOpeInt+"."+cAnoInt+"."+cMesInt+"."+cNumInt+" - "+cNumProrr
oPrint:say(090 , 0500 ," No : "+Space(1)+cDet, oFont15n)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁSenha                                                         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
If ! Empty(BE4->BE4_SENHA)
   oPrint:say(150 , ( nColMax-700) ,"Senha : "+ BE4->BE4_SENHA , oFont09n)
Endif   

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁCodigo ANS                                                    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(200 , nColMax-700    ,"ANS :"                    , oFont09n)		//-- Codigo ANS
oPrint:say(200 , nColMax-500    ,BA0->BA0_SUSEP                       , oFont10n)	
oPrint:say(260 , nColMax-700    ,"Qtde Prorrogacao Autorizada:"       , oFont09n)		//-- Qtde Dias Prorrogados
oPrint:say(260 , nColMax-190    ,Transform(nNDias,"@R 99")   		   , oFont12n)		//-- Nr. Dias Prorrogados
oPrint:line(310,nColIni,310, nColMax-050)

//  -- FIM CABECALHO DA GUIA  -- //

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁTipo de Internacao      										 Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Li 	:= 330
oPrint:say(li , nColIni , "Tipo de Internacao   : "      , oFont10n)
oPrint:say(li , nColIni + 400 , BE4->BE4_GRPINT+' - '+X3COMBO("BE4_GRPINT",BE4->BE4_GRPINT)    , oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁData da Prorrogacao     										 Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li , nColIni + 0900 , "Data da Prorrogacao:"   , oFont10n )
cDet	:= dtoc(dDatProrr) 
oPrint:say(li , nColIni + 1300 , dtoc(dDatProrr), oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁTipo : Implementacao posterior                				 Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li , nColIni + 1600 , "Tipo: "+"Prorrogacao" , oFont10n)
li += 050

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁPadrao de Acomoda┤└o                                          Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
BI4->(DbSetOrder(1))
BI4->(DbSeek(xFilial("BE4")+BE4->BE4_PADINT))
oPrint:say(li, nColIni       , "Padrao de Acomodacao : "                    , oFont10n )
oPrint:say(li, ncolIni + 450 , BI4->BI4_CODACO+" - "+BI4->BI4_DESCRI, oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁProduto                                                       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
BA1->(DbSetOrder(2))
BA1->(DbSeek(xFilial("BA1")+BE4->(BE4_OPEUSR+BE4_CODEMP+BE4_MATRIC+BE4_TIPREG)))

BA3->(DbSetOrder(1))
BA3->(DbSeek(xFilial("BA3")+BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC)))
oPrint:say(li, nColIni + 1000  , "            Produto : "  , oFont10n)
oPrint:say(li, ncolIni + 1300  ,If(BA3->BA3_APLEI=="1","Regulamentado","Nao Regulamentado"), oFont10)
li +=050
oPrint:line(li,nColIni,li, nColMax-050)
li += 20

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁUsuario                                                       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li, nColIni      , "Usuario    : " , oFont10n)
//Caso seja Recen nascido imprime na frente do nome a mensagem "Atendimento Recem Nascido"
If BE4->BE4_ATERNA <> '1'
	oPrint:say(li, ncolIni + 250 , BA1->BA1_NOMUSR    , oFont10)
Else
	oPrint:say(li, ncolIni + 250 , AllTrim(BA1->BA1_NOMUSR) + " (Atendimento Recem Nascido)", oFont10)
Endif

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁCodigo                                                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li, nColIni + 1600 , "Codigo  : " , oFont10n )
If  BA1->BA1_CODINT == BA1->BA1_OPEORI .or. empty(BA1->BA1_MATANT)
    oPrint:say(li, ncolIni + 1800 , BA1->(BA1_CODINT+"."+BA1_CODEMP+"."+BA1_MATRIC+"."+BA1_TIPREG+"-"+BA1_DIGITO)  , oFont10)
Else
    oPrint:say(li, ncolIni + 1800 , BA1->BA1_MATANT , oFont10)
Endif
Li+= 050

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁIdentidade                                                    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/                                                     
oPrint:say(li, nColIni       , "Identidade : " , oFont10n )
oPrint:say(li, ncolIni + 250 , BA1->BA1_DRGUSR , oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁSexo                                                          Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/                                                     
oPrint:say(li, nColIni + 900 , "Sexo :" , oFont10n )
oPrint:say(li, ncolIni +1050 , X3COMBO("BA1_SEXO",BA1->BA1_SEXO) , oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁData de Nascimento                                            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li, nColIni + 1600 , "Data de Nascimento:" , oFont10n )
oPrint:say(li, ncolIni + 2000 , dtoc(BA1->BA1_DATNAS) , oFont10)
Li+= 050

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁEmpresa                                                       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
BG9->(DbSetOrder(1))
BG9->(DbSeek( xFilial("BG9")+BA1->(BA1_CODINT+BA1_CODEMP) )  )
oPrint:say(li, nColIni       , "Empresa    : " , oFont10n )
oPrint:say(li, ncolIni + 250 ,BG9->BG9_DESCRI , oFont10)
li += 050

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁPlano                                                         Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
BI3->(DbSetOrder(1))
BI3->(DbSeek(xFilial("BI3")+BA3->(BA3_CODINT+BA3_CODPLA+BA3_VERSAO)))
oPrint:say(li, nColIni        , "Plano      : " , oFont10n )
oPrint:say(li, ncolIni + 250  , BI3->(BI3_CODIGO + "-"+BI3_DESCRI )      , oFont10)
li +=050
oPrint:line(li,nColIni,li, nColMax-050)
li += 20

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁMedico Solicitante                                            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
BB0->( DbSetOrder(4) )
BB0->(DbSeek(xFilial("BB0")+BE4->(BE4_ESTSOL+BE4_REGSOL+BE4_SIGLA)))
oPrint:say(li, nColIni        , "Medico Solicitante : ", oFont10n )
oPrint:say(li, ncolIni + 400  , BB0->BB0_NOME      , oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁCRM                                                           Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li, nColIni + 1600 , "CRM     : " , oFont10n )
oPrint:say(li, ncolIni + 1800 , BB0->BB0_NUMCR     , oFont10)
Li +=050

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁHospital / Recurso :                                          Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
BAU->(DbSetOrder(1))
BAU->(DbSeek(xFilial("BAU")+BE4->BE4_CODRDA))
oPrint:say(li, nColIni        , "Hospital / Recurso : ", oFont10n )
oPrint:say(li, ncolIni + 400  , BAU->BAU_NOME      , oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁCodigo                                                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li, nColIni + 1600  , "Codigo  : ", oFont10n )
oPrint:say(li, ncolIni + 1800  , BAU->BAU_CODIGO    , oFont10)
li +=050
oPrint:line(li,nColIni,li, nColMax-050)
li += 20

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁCID PRINCIPAL                                                 Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li, nColIni, "CID   Principal :", oFont10n  )
oPrint:say(li, ncolIni +  400  , BE4->BE4_CID        , oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁData da Internacao                                            Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li, nColIni + 1600  , "Data da Internacao : ", oFont10n )
oPrint:say(li, ncolIni + 2000  , dtoc(BE4->BE4_DATPRO) , oFont10)
li +=050

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁCID Secundario                                                Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li, nColIni         , "CID Secundario : " , oFont10n )
oPrint:say(li, ncolIni + 0400  , BE4->BE4_CIDSEC   , oFont10)

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁHora da Interna┤└o :                                          Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li, nColIni + 1600  , "Hora da Internacao : ", oFont10n)
oPrint:say(li, ncolIni + 2000  , substr(BE4->BE4_HORPRO,1,2)+":" +substr(BE4->BE4_HORPRO,3,2), oFont10)
Li +=050
oPrint:line(li,nColIni,li, nColMax-050)
Li += 20

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁImpressao do Cabecalho da Linha de Detalhe                    Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:Box(li,nColIni,Li+450, nColMax-50 )										//-- Box Detalhe AMB
LI +=20
//-- Cabecalho do Detalhe
oPrint:say(li,nColIni,"AMB/CBHPM",oFont10n )
oPrint:say(li,nColIni+ 350 ,"Descricao",oFont10n )
oPrint:say(li,nColIni+1600 ,"Qtde",oFont10n )
oPrint:say(li,nColIni+1800 ,"Dias Prorrogados" ,oFont10n )
li += 050
oPrint:line(li,nColIni,li, nColMax-050)
li += 20

//-- Detalhe
//-- Imprime somente um lancamento ( implementacao posterior)
BEJ->(DbSetOrder(1))
If BEJ->(DbSeek(xFilial("BEJ")+BE4->(BE4_CODOPE+BE4_ANOINT+BE4_MESINT+BE4_NUMINT)))
   While ! BEJ->(Eof()) .And. BEJ->(BEJ_FILIAL+BEJ_CODOPE+BEJ_ANOINT+BEJ_MESINT+BEJ_NUMINT) == ;
                               xFilial("BEJ")+BE4->(BE4_CODOPE+BE4_ANOINT+BE4_MESINT+BE4_NUMINT)

         cDet	:= PLSPICPRO(BEJ->BEJ_CODPAD,BEJ->BEJ_CODPRO)
         oPrint:say(li,nColIni+10,cDet,oFont10n)		//-- AMB
         BR8->(DbSetOrder(1)) 
         BR8->(DbSeek(xFilial("BR8")+BEJ->(BEJ_CODPAD+BEJ_CODPRO)))
         nTotLin := MlCount(AllTrim(BR8->BR8_DESCRI),60) // Verifica quantas linhas a descricao vai ocupar
         oPrint:say(li,nColIni+ 350,MemoLine(BR8->BR8_DESCRI,60,1) ,oFont10) // -- Imprime 1a. Linha de Descricao
         oPrint:say(li,nColIni+1610,Transform(BEJ->BEJ_QTDPRO,"@R 99") ,oFont10)	//-- Qtde         
         oPrint:say(li,nColIni+1900,Transform(nNDias,"@R 99"),oFont10)	//-- Dias     
     
         Li += 035               
         If nTotLin > 1 // Se ha mais de 1 linha de descricao, imprime as demais
            For nInd := 2 To nTotLin
                oPrint:say(li,nColIni+ 350,MemoLine(BR8->BR8_DESCRI,60,nInd),oFont10)	//-- Descricao             
                Li += 035               
            Next nInd
         EndIf                                                              
   BEJ->(DbSkip())
   Enddo
Endif                               

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁImpressao Motivo :                                          Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Li := 1380
BEU->(DbSetOrder(1))
BEU->(DbSeek(xFilial("BET")+ BET->BET_CODOPE + cMotPro))
oPrint:say(li,nColIni,"Motivo da Prorrogacao:",oFont10n)
li += 050
oPrint:say(li,ncolIni,cMotPro+" - "+BEU->BEU_DESCRI,oFont10)
li += 100
oPrint:line(li,nColIni,li,nColMax-050)
li += 010

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁImpressao Observacoes :                                       Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
oPrint:say(li,nColIni,"Observacoes:",oFont10n)
li += 050
oPrint:say(li,ncolIni,cMsg1,oFont10)
li+= 050
oPrint:say(li,ncolIni,cMsg2,oFont10)
li+= 050
oPrint:say(li,ncolIni,cMsg3,oFont10)		
li+= 120
oPrint:line(li,nColIni,li,nColMax-050)
Li += 20

/*здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
  ЁCarimbos e Assinaturas                                        Ё
  юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Li += 130
oPrint:Line(li,nColIni+50,li,0800)
oPrint:Line(li,1150,li,1850)
Li += 10
oPrint:Say(li,nColIni+50,"Carimbo e Assinatura do Medico Respons═vel",oFont08)
oPrint:Say(li,1150,"Carimbo da Operadora",oFont08)				

oPrint:EndPage() 		// Finaliza a pagina
Return .t.

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁfLogoEmp  ╨Autor  ЁBATATINHA           ╨ Data Ё  05/03/06   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     Ё                                                            ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё PLSR093                                                    ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function fLogoEmp( cLogo,cTipo)
Local  cStartPath	:= GetSrvProfString("Startpath","")
Default cTipo 	:= "1"

//-- Logotipo da Empresa
If cTipo =="1"
	cLogo := cStartPath + "LGRL"+FWCompany()+FWCodFil()+".BMP" 	// Empresa+Filial
		If !File( cLogo )
	cLogo := cStartPath + "LGRL"+FWCompany()+".BMP" 				// Empresa
		endif
Else
	cLogo := cStartPath + "LogoSiga.bmp"
Endif


Return(NIL)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁMenuDef   Ё Autor Ё Darcio R. Sporl       Ё Data Ё26/12/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Utilizacao de menu Funcional                               Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁArray com opcoes da rotina.                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁParametros do array a Rotina:                               Ё╠╠
╠╠Ё          Ё1. Nome a aparecer no cabecalho                             Ё╠╠
╠╠Ё          Ё2. Nome da Rotina associada                                 Ё╠╠
╠╠Ё          Ё3. Reservado                                                Ё╠╠
╠╠Ё          Ё4. Tipo de Transa┤└o a ser efetuada:                        Ё╠╠
╠╠Ё          Ё		1 - Pesquisa e Posiciona em um Banco de Dados           Ё╠╠
╠╠Ё          Ё    2 - Simplesmente Mostra os Campos                       Ё╠╠
╠╠Ё          Ё    3 - Inclui registros no Bancos de Dados                 Ё╠╠
╠╠Ё          Ё    4 - Altera o registro corrente                          Ё╠╠
╠╠Ё          Ё    5 - Remove o registro corrente do Banco de Dados        Ё╠╠
╠╠Ё          Ё5. Nivel de acesso                                          Ё╠╠
╠╠Ё          Ё6. Habilita Menu Funcional                                  Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function MenuDef()
Private aRotina := {	{ STRPL01					, 'AxPesqui'  				,0 ,K_Pesquisar		,0 ,.F.} ,;
											{ "Vis.r Internacao"	, 'PLSA092Mov' 				,0 ,K_Visualizar	,0 ,Nil} ,;
											{ "Nova Prorrog."			, 'PLSA093Mov' 				,0 ,K_Incluir			,0 ,Nil} ,;
											{ "Vis. Prorrog."			, 'PLSA093Vis(,,,,1)'	,0 ,K_Alterar			,0 ,Nil} ,;
											{ "Excluir Prorrog."	, 'PLSA093Vis(,,,,2)'	,0 ,K_Excluir			,0 ,Nil} ,;
											{ "Imprimir"   		 		, 'PLSR093'						,0 ,K_Visualizar	,0 ,Nil} ,;
											{ "Legenda"    		 		, 'PLSA093LEG'				,0 ,K_Alterar			,0 ,.F.} }
Return(aRotina)