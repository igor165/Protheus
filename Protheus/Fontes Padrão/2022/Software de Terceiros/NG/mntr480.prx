#INCLUDE "MNTR480.CH"
#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR480  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³27/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Comsumo de Produto Por Tipo de  Equipamento                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/ 
Function MNTR480()
	
	Local oReport
	Local aArea	 := GetArea()
	
	Private cTRB := GetNextAlias()
	Private vVETHORAS := {}

	Private nTamPro := TamSX3("B1_COD")[1]-15

	SetKey( VK_F9, { | | NGVersao( "MNTR480" , 2 ) } )

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetPortrait() //Default Retrato 
		oReport:PrintDialog()
	Else
		MNTR480R3()
	EndIf
	RestArea(aArea)  

Return .T. 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³  Elisangela Costa     ³ Data ³ 06/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR480                                                    ³±±
±±|__________|____________________________________________________________|±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef() 
	Local oReport   
	Local oSection1, oSection2
	Local oCell
	Local oTotaliz   
	Local oBreak1, oBreak2

	//LAYOUT 
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
	___________________________________________________________________________________________________________________________________________ 
	Consumo de Produto Por Tipo de Equipamento 
	___________________________________________________________________________________________________________________________________________ 

	Familia  Descricao 
	___________________________________________________________________________________________________________________________________________  
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Bem              Descricao                               Produto         Descricao                       Quantidade  Un.           Custo
	___________________________________________________________________________________________________________________________________________
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999.999,99  xxx  999.999.999,99
	xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999.999,99  xxx  999.999.999,99
	xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999.999,99  xxx  999.999.999,99
	xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999.999,99  xxx  999.999.999,99

	Total do Bem
	___________________________________________________________________________________________________________________________________________
	999.999.999,99

	Total da Familia
	___________________________________________________________________________________________________________________________________________
	999.999.999,99

	Total Geral
	___________________________________________________________________________________________________________________________________________
	999.999.999,99

	*/    

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR480",OemToAnsi(STR0006),"MNT480",{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002+" "+STR0003)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ mv_par01     // De Grupo                                     ³
	//³ mv_par02     // Ate Grupo                                    ³
	//³ mv_par03     // De Produto                                   ³
	//³ mv_par04     // Ate Produto                                  ³
	//³ mv_par05     // De Data Inicio                               ³
	//³ mv_par06     // Ate Data Fim                                 ³
	//³ mv_par07     // De Familia                                   ³
	//³ mv_par08     // Ate Familia                                  ³
	//³ mv_par09     // De Bem                                       ³
	//³ mv_par10     // Ate Bem                                      ³  
	//³ mv_par11     // Tipo de Custo (Medio/Standard)               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte(oReport:uParam,.F.) 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:SetTotalInLine(.F.)
	oSection1 := TRSection():New(oReport,STR0026,{(cTRB),"ST6"}) //Familia
	TRCell():New(oSection1,"(cTRB)->CODFAMI",(cTRB),STR0026,"@!" ,6,/*lPixel*/,/*{|| code-block de impressao }*/) //"Familia" 
	TRCell():New(oSection1,"T6_NOME"     ,"ST6",STR0027,"@!" ,40,/*lPixel*/)  //"Descrição"
	TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6")+(cTRB)->CODFAMI})   

	oSection2 := TRSection():New(oReport,STR0033,{(cTRB),"ST9","SB1"}) //Bens
	oCell1 := TRCell():New(oSection2,"(cTRB)->BEM"     ,(cTRB) ,STR0028,"@!" ,16,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem"  
	oCell1 := TRCell():New(oSection2,"T9_NOME"      ,"ST9" ,STR0027,"@!" ,40,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrição"
	oCell1 := TRCell():New(oSection2,"(cTRB)->PRODUTO" ,(cTRB) ,STR0029,"@!" ,nTamPro+15,/*lPixel*/,/*{|| code-block de impressao }*/) //"Produto"  
	oCell1 := TRCell():New(oSection2,"B1_DESC"      ,"SB1" ,STR0027,"@!" ,30,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrição"
	oCell1 := TRCell():New(oSection2,"(cTRB)->QUANTID" ,(cTRB) ,STR0030,"@E 999,999.99" ,10,/*lPixel*/,/*{|| code-block de impressao }*/) //"Quantidade"
	oCell1 := TRCell():New(oSection2,"B1_UM"        ,"SB1" ,STR0031,"@!" ,2,/*lPixel*/,/*{|| code-block de impressao }*/) //"Un." 
	oCell1 := TRCell():New(oSection2,"(cTRB)->CUSTO"   ,(cTRB) ,STR0032,"@E 999,999,999.99" ,14,/*lPixel*/,/*{|| code-block de impressao }*/) //"Custo"
	//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a
	//direita
	oSection2:Cell("(cTRB)->QUANTID"):SetHeaderAlign("RIGHT") 
	oSection2:Cell("(cTRB)->CUSTO"):SetHeaderAlign("RIGHT")   
	TRPosition():New(oSection2,"ST9",1,{|| xFilial("ST9")+(cTRB)->BEM}) 
	TRPosition():New(oSection2,"SB1",1,{|| xFilial("SB1")+(cTRB)->PRODUTO}) 

	oBreak1 := TRBreak():New(oSection1,".T.",STR0023,.F.)//Total da Familia 
	TRFunction():New(oSection2:Cell("(cTRB)->CUSTO"),/*cId*/,"SUM",oBreak1,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)

	oBreak2 := TRBreak():New(oSection2,".T.",STR0022,.F.) //Total do Bem
	TRFunction():New(oSection2:Cell("(cTRB)->CUSTO"),/*cId*/,"SUM",oBreak2,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)

	oTotaliz := TRFunction():New(oSection2:Cell("(cTRB)->CUSTO"),"TOTGCUST","SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)

Return oReport 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR480R3³ Autor ³ Inacio Luiz Kolling   ³ Data ³27/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Comsumo de Produto Por Tipo de  Equipamento                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/ 
Function MNTR480R3()

	Local cSTRING    := "SI3"
	Local cDESC1     := STR0001 //"Relatorio de Consumo de Produto Por Tipo de Equipamento."
	Local cDESC2     := STR0002 //"E permitido selecionar tipo de informacao a ser"
	Local cDESC3     := STR0003 //"apresentada, atraves dos parametros do relatorio."
	Local WNREL      := "MNTR480"

	Private aRETURN  := {STR0004,1,STR0005,2,2,1,"",1} //"Zebrado"###"Administracao"
	Private nLASTKEY := 0
	Private TITULO   := STR0006 //"Consumo de Produto Por Tipo de Equipamento"
	Private TAMANHO  := "M"
	Private NOMEPROG := "MNTR480"
	Private nTIPO    := 0
	Private cPERG    := "MNT480"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ mv_par01     // De Grupo                                     ³
	//³ mv_par02     // Ate Grupo                                    ³
	//³ mv_par03     // De Produto                                   ³
	//³ mv_par04     // Ate Produto                                  ³
	//³ mv_par05     // De Data Inicio                               ³
	//³ mv_par06     // Ate Data Fim                                 ³
	//³ mv_par07     // De Familia                                   ³
	//³ mv_par08     // Ate Familia                                  ³
	//³ mv_par09     // De Bem                                       ³
	//³ mv_par10     // Ate Bem                                      ³  
	//³ mv_par11     // Tipo de Custo (Medio/Standard)               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte(cPERG,.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		Return
	EndIf
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND| R480IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R480IMP  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³27/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³DESCri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR480                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R480IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT := ""
	Local nCNTIMPR := 0
	Local oARQTR480

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis para controle do cursor de progressao do relatorio ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local nTOTREGS := 0,nMULT := 1,nPOSANT := 4,nPOSATU := 4,nPOSCNT := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contadores de linha e pagina                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private li := 80,m_pag := 1

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se deve comprimir ou nao                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTIPO := IIF(aRETURN[4]==1,15,18)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta os Cabecalhos                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private CABEC1 :=  STR0017+Space(nTamPro)+STR0034 //"         Produto          Nome Do Produto                             Quantidade"
	Private CABEC2 := " "
	Private aVETINR := {} 

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
	************************************************************************************************************************************
	Produto         Descricao                                Quantidade  Un.           Custo
	************************************************************************************************************************************
	Familia.: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	Bem.....: xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          999.999,99  xxx  999.999.999,99
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          999.999,99  xxx  999.999.999,99

	Total do Bem                                                              999.999.999,99

	Total da Familia                                                          999.999.999,99

	Total Geral                                                               999.999.999,99 
	*/
	
	aDBFR480 := {{"CODFAMI" ,"C",06,0},;
				 {"BEM"     ,"C",16,0},;
				 {"PRODUTO" ,"C",nTamPro+15,0},;
				 {"QUANTID" ,"N",09,2},;
				 {"CUSTO"   ,"N",12,2}}
	
	//Cria Tabela Temporária 
	oARQTR480 := NGFwTmpTbl(cTRB,aDBFR480,{{"CODFAMI","BEM","PRODUTO"}}) 

	Processa({|lEND| MNTR480STL()},STR0018) //"Processando Itens das O.S. Normais..."
	Processa({|lEND| MNTR480STT()},STR0019) //"Processando Itens das O.S. Historico..."

	nTotalG := 0.00
	DbSelectArea(cTRB)
	DbGotop()
	SetRegua(LastRec())    
	While !Eof()

		cFami := (cTRB)->CODFAMI
		nCustoFam := 0.00

		If Li > 57 
			NgSomaLi(58)
		Else
			NgSomaLi(58)                            
			NgSomaLi(58)                               
		EndIf   
		DbSelectArea("ST6")
		DbSetOrder(1)
		DbSeek(xFILIAL("ST6")+cFAMI)
		@ Li,000 Psay STR0020+cFAMI+" "+ST6->T6_NOME //"Familia.: "

		DbSelectArea(cTRB)
		While !Eof() .And. (cTRB)->CODFAMI == cFAMI

			nCustoBem := 0.00
			cCodBem := (cTRB)->BEM

			DbSelectArea("ST9")
			DbSetOrder(01)
			DbSeek(xFilial("ST9")+(cTRB)->BEM)
			If Li > 57 
				NgSomaLi(58)
			Else
				NgSomaLi(58)                            
				NgSomaLi(58)                               
			EndIf 
			@ Li,000 PSay STR0021 //"Bem.....: "
			@ Li,010 PSay ST9->T9_CODBEM
			@ Li,027 PSay ST9->T9_NOME

			DbSelectArea(cTRB)
			While !Eof() .And. (cTRB)->CODFAMI == cFAMI .And.;
			(cTRB)->BEM == cCodBem

				IncRegua()
				DbSelectArea("SB1")
				DbSetOrder(1)
				DbSeek(xFILIAL("SB1")+(cTRB)->PRODUTO)
				NgSomaLi(58)                            
				@ Li,010 Psay (cTRB)->PRODUTO Picture "@!"
				@ Li,026+nTamPro Psay SubStr(SB1->B1_DESC,1,40)
				@ Li,067+nTamPro Psay (cTRB)->QUANTID Picture "@E 999,999.99"
				@ Li,079+nTamPro Psay SB1->B1_UM     
				@ Li,084+nTamPro Psay (cTRB)->CUSTO   Picture "@E 999,999,999.99"
				nCustoBem := nCustoBem + (cTRB)->CUSTO
				DbSelectArea(cTRB) 
				DbSkip()
			End
			If Li > 57 
				NgSomaLi(58)
			Else
				NgSomaLi(58)                            
				NgSomaLi(58)                               
			EndIf  
			@ li,010 Psay STR0022  //"Total do Bem"
			@ li,084+nTamPro Psay nCustoBem Picture "@E 999,999,999.99"
			nCustoFam := nCustoFam + nCustoBem    
		End
		If Li > 57 
			NgSomaLi(58)
		Else
			NgSomaLi(58)                            
			NgSomaLi(58)                               
		EndIf 
		@ li,010 Psay STR0023  //"Total da Familia"
		@ li,084+nTamPro Psay nCustoFam  Picture "@E 999,999,999.99"
		nTotalG := nTotalG + nCustoFam
	End         
	If nTotalG > 0  
		If Li > 57 
			NgSomaLi(58)
		Else
			NgSomaLi(58)                            
			NgSomaLi(58)                               
		EndIf 
		@ li,010 Psay STR0024 //"Total Geral"
		@ Li,084+nTamPro Psay  nTotalG  PICTURE "@E 999,999,999.99"
	EndIf
	Roda(nCNTIMPR,cRODATXT,TAMANHO)

	//Deleta o arquivo temporario fisicamente   
	oARQTR480:Delete()      

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principam             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex("ST9")
	Set Filter To
	Set Device To Screen
	If aRETURN[5] = 1
		Set Printer To
		DbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³ Elisangela Costa      ³ Data ³ 22/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ReportDef                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local oARQTR480
	
	Private aVETINR := {} 

	aDBFR480 := {{"CODFAMI" ,"C",06,0},;
				 {"BEM"     ,"C",16,0},;
				 {"PRODUTO" ,"C",nTamPro+15,0},;
				 {"QUANTID" ,"N",09,2},;
				 {"CUSTO"   ,"N",12,2}}
	
	//Cria Tabela Temporária 
	oARQTR480 := NGFwTmpTbl(cTRB,aDBFR480,{{"CODFAMI","BEM","PRODUTO"}}) 
	 

	Processa({|lEND| MNTR480STL()},STR0018) //"Processando Itens das O.S. Normais..."
	Processa({|lEND| MNTR480STT()},STR0019) //"Processando Itens das O.S. Historico..."

	DbSelectArea(cTRB)
	DbGotop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()

		cFami := (cTRB)->CODFAMI
		oSection1:Init()
		oSection1:PrintLine() 
		While !Eof() .And. !oReport:Cancel() .And. (cTRB)->CODFAMI == cFAMI

			cCodBem := (cTRB)->BEM
			lPriBem := .T.

			oSection2:Init()

			DbSelectArea(cTRB)
			While !Eof() .And. !oReport:Cancel() .And. (cTRB)->CODFAMI == cFAMI .And.;
			(cTRB)->BEM == cCodBem 

				oReport:IncMeter()
				If lPriBem 
					lPriBem := .F.
					oSection2:Cell("(cTRB)->BEM"):Show()  
					oSection2:Cell("T9_NOME"):Show()
				Else
					oSection2:Cell("(cTRB)->BEM"):Hide()  
					oSection2:Cell("T9_NOME"):Hide()
				EndIf 
				oSection2:PrintLine()

				DbSelectArea(cTRB)
				DbSkip()
			End
			oSection2:Finish()
		End   
		oSection1:Finish()
	End  

	//Deleta o arquivo temporario fisicamente  
	oARQTR480:Delete()             

Return .T. 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR480STL³ Autor ³ Inacio Luiz Kolling   ³ Data ³27/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os itens das O.S. normais                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR480                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR480STL()

	DbSelectArea("STL")
	DbSetOrder(6)
	DbSeek(xFILIAL("STL")+DTOS(MV_PAR05),.T.)
	ProcRegua(LastRec())
	While !Eof() .And. STL->TL_FILIAL == xFILIAL("STL") .And.;
	STL->TL_DTINICI <= MV_PAR06

		IncProc()

		If Alltrim(STL->TL_SEQRELA) = "0"
			DbSelectArea("STL")
			DbSkip()
			Loop
		EndIf

		If STL->TL_TIPOREG <> "P"
			DbSelectArea("STL")
			DbSkip()
			Loop
		EndIf

		DbSelectArea("STJ")
		DbSetOrder(1)
		DbSeek(xFILIAL("STJ")+STL->TL_ORDEM+STL->TL_PLANO)
		If STJ->TJ_SITUACA <> "L" .Or. stj->tj_tipoos <> "B"
			DbSelectArea("STL")
			DbSkip()
			Loop
		EndIf

		DbSelectArea("SB1")
		DbSetOrder(1)
		DbSeek(xFILIAL("SB1")+STL->TL_CODIGO)
		If SB1->B1_GRUPO < MV_PAR01 .Or. SB1->B1_GRUPO > MV_PAR02
			DbSelectArea("STL")
			DbSkip()
			Loop
		EndIf

		If STL->TL_CODIGO < MV_PAR03 .Or. STL->TL_CODIGO > MV_PAR04
			DbSelectArea("STL")
			DbSkip()
			Loop
		EndIf

		DbSelectArea("ST9")
		DbSetOrder(1)
		DbSeek(xFILIAL("ST9")+STJ->TJ_CODBEM)
		If ST9->T9_CODFAMI < MV_PAR07 .Or. ST9->T9_CODFAMI > MV_PAR08
			DbSelectArea("STL")
			DbSkip()
			Loop
		EndIf   

		If STJ->TJ_CODBEM < MV_PAR09 .Or. STJ->TJ_CODBEM > MV_PAR10
			DbSelectArea("STL")
			DbSkip()
			Loop
		EndIf

		vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
		STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
		STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
		nQTDHORAS := vVETHORAS[1]

		R480GRTRAB(ST9->T9_CODFAMI,STJ->TJ_CODBEM,STL->TL_CODIGO,nQTDHORAS,STL->TL_CUSTO,STJ->TJ_TERMINO)

		DbSelectArea("STL")
		DbSkip()
	End
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR480STT³ Autor ³ Inacio Luiz Kolling   ³ Data ³27/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os itens das O.S. Historicos                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR480                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR480STT()

	DbSelectArea("STT")
	DbSetOrder(5)
	DbSeek(xFILIAL("STT")+DTOS(MV_PAR05),.T.)
	ProcRegua(LastRec())
	While !Eof() .And. STT->TT_FILIAL == xFILIAL("STT") .And.;
	STT->TT_DTINICI <= MV_PAR06

		IncProc()

		If Alltrim(STT->TT_SEQRELA) = "0"
			DbSelectArea("STT")
			DbSkip()
			Loop
		EndIf

		If STT->TT_TIPOREG <> "P"
			DbSelectArea("STT")
			DbSkip()
			Loop
		EndIf

		DbSelectArea("STS")
		DbSetOrder(1)
		DbSeek(xFILIAL("STS")+STT->TT_ORDEM+STT->TT_PLANO)
		If STS->TS_SITUACA <> "L" .Or. sts->ts_tipoos <> "B"
			DbSelectArea("STT")
			DbSkip()
			Loop
		EndIf

		DbSelectArea("SB1")
		DbSetOrder(1)
		DbSeek(xFILIAL("SB1")+STT->TT_CODIGO)
		If SB1->B1_GRUPO < MV_PAR01 .Or. SB1->B1_GRUPO > MV_PAR02
			DbSelectArea("STT")
			DbSkip()
			Loop
		EndIf

		If STT->TT_CODIGO < MV_PAR03 .Or. STT->TT_CODIGO > MV_PAR04
			DbSelectArea("STT")
			DbSkip()
			Loop
		EndIf

		DbSelectArea("ST9")
		DbSetOrder(1)
		DbSeek(xFILIAL("ST9")+STS->TS_CODBEM)
		If ST9->T9_CODFAMI < MV_PAR07 .Or. ST9->T9_CODFAMI > MV_PAR08
			DbSelectArea("STT")
			DbSkip()
			Loop
		EndIf

		If STS->TS_CODBEM < MV_PAR09 .Or. STS->TS_CODBEM > MV_PAR10
			DbSelectArea("STT")
			DbSkip()
			Loop
		EndIf

		vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
		STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
		STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
		nQTDHORAS := vVETHORAS[1]

		R480GRTRAB(ST9->T9_CODFAMI,STS->TS_CODBEM,STT->TT_CODIGO,nQTDHORAS,STT->TT_CUSTO,STS->TS_TERMINO)

		DbSelectArea("STT")
		DbSkip()
	End
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³R480GRATRB³ Autor ³ Inacio Luiz Kolling   ³ Data ³21/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Grava no arquivo temporario                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR480        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function R480GRTRAB(cCODFAMI,cBEM,cPROD,nQUANT,nCUSTO,cTERMINO)

	DbSelectArea(cTRB)
	If !DbSeek(cCODFAMI+cBEM+cPROD)
		DbAppend()
		(cTRB)->CODFAMI := cCODFAMI
		(cTRB)->BEM     := cBEM
		(cTRB)->PRODUTO := cPROD
	EndIf                                                            
	(cTRB)->QUANTID := (cTRB)->QUANTID+nQUANT                            

	If MV_PAR11 = 1  //Medio
		(cTRB)->CUSTO := (cTRB)->CUSTO + nCUSTO
	Else             //Standard
		aVETCUST := NGCUSTSTAN(cPROD,"P")
		nVCUSTO  := aVETCUST[1] //Custo standard 
		nVCUSTO  := nVCUSTO * nQUANT

		(cTRB)->CUSTO += nVCUSTO
	EndIf	   

Return .T.                               