#INCLUDE "Mntr870.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 1 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR870  ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 02/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ordem de Servico Abertas Classificadas Por Centro de Custo ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR870()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Local cString := "STJ"
Local cdesc1  := STR0001 //"Emissao de Ordem de Servico Abertas. O Usuario pode selecionar"
Local cdesc2  := STR0002 //"parametros de selecao para a impressao."
Local cdesc3  := " "
Local wnrel   := "MNTR870"

Private aReturn  := { STR0003, 1,STR0004, 2, 2, 1, "",1 }  //"Zebrado"###"Administracao"
Private nLastKey := 0
Private cPerg    := "MNT870"
Private Titulo   := STR0005 //"Ordem De Servico Por Centro de Custo"
Private Tamanho  := "M"

/*
  ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
  ³ Variaveis utilizadas para parametros                         ³
  ³ mv_par01     // De  Centro de Custo                          ³
  ³ mv_par02     // Ate Centro de Custo                          ³
  ³ mv_par03     // De  Familia de Bem                           ³
  ³ mv_par04     // Ate Familia de Bem                           ³
  ³ mv_par05     // De  Data                                     ³
  ³ mv_par06     // Ate Data                                     ³
  ³ mv_par07     // De Localizacao                               ³
  ³ mv_par08     // Ate Localizacao                              ³
  | mv_par09     // Imprimir Localizacao ?  Sim/Nao              |
  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/


pergunte(cPerg,.F.)
wnrel:=SetPrint(cString,wnrel,cperg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

If nLastKey = 27
   Set Filter To
   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)
   Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
   Set Filter To
   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)
   Return
Endif

RptStatus({|lEnd| R870Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R870Imp  ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 02/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR870                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R870Imp(lEnd,wnRel,titulo,tamanho)
Local cRodaTxt  := ""
Local nCntImpr  := 0,i
Local cEstrut   := ""
Local aEstrut   := {}
Local nCont     := 0
Private li := 80 ,m_pag := 1, lStj := .f.
Private nomeprog := "MNTR870"
Private ntipo    := IIF(aReturn[4]==1,15,18)
Store " " To Cabec1,Cabec2

cCONDST9 := 'ST9->T9_CODFAMI >= MV_PAR03 .And. ST9->T9_CODFAMI <= MV_PAR04'
cCONDSTJ := 'STJ->TJ_SITUACA = "L" .And. STJ->TJ_TERMINO = "N" .And. '
//cCONDSTJ := cCONDSTJ+'STJ->TJ_TIPOOS = "B" .And. '
cCONDSTJ := cCONDSTJ+'(STJ->TJ_DTORIGI >= MV_PAR05 .And. STJ->TJ_DTORIGI <= MV_PAR06)'

/*
         1         2         3         4         5         6         7         8         9         0         1         2         3    
123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
____________________________________________________________________________________________________________________________________

	Planilha de Ordens de Serviço referente período de __/__/__    a   __/__/__  
____________________________________________________________________________________________________________________________________

Centro de custo.:   XXXXXXXXXXXXXXXX  - XXXXXXXXXXXXXXXXXXXXXXXXXXXX

Ordem   Bem               Nome do Bem                     Local   Dt Manut.   Dt Ult.Man.  Dt Realiz. Obs

999999  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx  99/99/9999  99/99/9999   __/__/__   _____________________________
999999  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx  99/99/9999  99/99/9999   __/__/__   _____________________________

Centro de custo.:   XXXXXXXXXXXXXXXX  - XXXXXXXXXXXXXXXXXXXXXXXXXXXX

999999  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxx  99/99/9999  __/__/__  ________
999999  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     xxxxxx  99/99/9999  __/__/__  ________
*/

DbSelectArea("STJ")
DbSetOrder(10)
DbSeek(xFilial('STJ')+MV_PAR01,.t.)
SetRegua(LastRec())

While !Eof() .And. STJ->TJ_FILIAL == xFilial('STJ') .And.;
	STJ->TJ_CCUSTO <= MV_PAR02
	
	IncRegua()
	
	aCusto := { }
	
	cCusto := STJ->TJ_CCUSTO
	
	While !Eof() .And. STJ->TJ_FILIAL == xFilial('STJ') .And.;
		cCusto = STJ->TJ_CCUSTO .And. STJ->TJ_CCUSTO <= MV_PAR02
		
		If STJ->TJ_TIPOOS == "B"
			DbSelectArea("ST9")
			DbSetOrder(01)
			DbSeek(xFilial('ST9')+STJ->TJ_CODBEM)
			DbSelectArea("STF")
			DbSetOrder(1)
			Dbseek(xFilial("STF")+STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA)
			
			If &(cCONDST9)
				If &(cCONDSTJ)
					aAdd(aCusto,{STJ->TJ_ORDEM,STJ->TJ_CODBEM,SubStr(ST9->T9_NOME,1,40),ST9->T9_LOCAL,STJ->TJ_DTORIGI,STF->TF_DTULTMA,STJ->TJ_TIPOOS})
				Endif
			Endif
		Else
			If &(cCONDSTJ)
				dbSelectArea("TAF")
				dbSetOrder(7)
				If dbSeek(xFilial("TAF")+"X"+"2"+STJ->TJ_CODBEM)
					If mv_par07 <= TAF->TAF_CODNIV .And. TAF->TAF_CODNIV <= mv_par08
						aAdd(aCusto,{STJ->TJ_ORDEM,STJ->TJ_CODBEM,TAF->TAF_NOMNIV,TAF->TAF_NIVSUP,STJ->TJ_DTORIGI,TAF->TAF_CCUSTO,STJ->TJ_TIPOOS})
					EndIf
				EndIf
			EndIf
		EndIf
		
		dbSelectArea("STJ")
		dbSkip()
	End
	
	lPrint := .t.
	For i:= 1 to len(aCusto)
		If lPrint
			Somalinha()
			Somalinha()
			@ Li,000 Psay STR0006 //"Centro de Custo:"
			Dbselectarea("SI3")
			Dbsetorder(1)
			Dbseek(xFilial("SI3")+cCusto)
			cTit_CC := Trim(cCusto) + ' - ' + trim(SI3->I3_DESC)
			@ Li,017 Psay cTit_CC
			Somalinha()
			@ Li,000 Psay STR0007 //"Ordem   Local    Dt Manut.      Dt Ult.Man.      Dt Realiz.             Obs"
			Somalinha()
			lPrint := .f.
		Endif
		
		lStj := .t.
		//Somalinha()
		lStj := .f.
		
		If aCusto[i][7] == "B"
			@ Li,000 Psay aCusto[i][1] //Ordem
			@ Li,008 Psay aCusto[i][4] //Local
			@ Li,017 Psay aCusto[i][5] Picture "99/99/9999" //Data Original
			@ Li,032 Psay aCusto[i][6] Picture "99/99/9999" //Data da Ultima Manutencao
			@ Li,050 Psay "__/__/__"
			@ Li,060 Psay replicate("_",30)
			Somalinha()
			
			@ Li,008 Psay STR0014 + AllTrim(aCusto[i][2]) + " - " + AllTrim(aCusto[i][3]) //"Bem.: "
			Somalinha()
			
			If MV_PAR09 == 1
				cEstrut := NGLocComp(aCusto[i][2],"1")
				aEstrut := {}
				If Len(cEstrut) > 80
					aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,80)))
					cEstrut := AllTrim(SubStr(cEstrut,81))
					While Len(cEstrut) > 80
						aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,80)))
						cEstrut := AllTrim(SubStr(cEstrut,81))
					End
					aAdd(aEstrut,AllTrim(cEstrut))
				EndIf
				
				If Len(aEstrut) > 0
					@ Li,008 Psay STR0015 //"Localização.: "
					For nCont := 1 To Len(aEstrut)
						@ Li,023 Psay aEstrut[nCont]
						If nCont <> Len(aEstrut)
							Somalinha()
						Endif
					Next nCont
				ElseIf AllTrim(cEstrut) <> AllTrim(aCusto[i][2])
					@ Li,008 Psay STR0015 + cEstrut //"Localização: "
					Somalinha()
				EndIf
			Endif
		Else
			@ Li,000 Psay aCusto[i][1] //Ordem
//			@ Li,008 Psay aCusto[i][4] //Nivel Superior
			@ Li,017 Psay aCusto[i][5] Picture "99/99/9999" //Data Original
//			@ Li,032 Psay aCusto[i][6] //Centro de Custo
			@ Li,050 Psay "__/__/__"
			@ Li,060 Psay replicate("_",30)
			Somalinha()
			
			@ Li,008 Psay STR0014 + AllTrim(aCusto[i][2]) + " - " + AllTrim(aCusto[i][3]) //"Bem.: "
			Somalinha()
			 
			If MV_PAR09 == 1
				cEstrut := NGLocComp(aCusto[i][2],"2")
				aEstrut := {}
				If Len(cEstrut) > 80
					aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,80)))
					cEstrut := AllTrim(SubStr(cEstrut,80))
					While Len(cEstrut) > 80
						aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,80)))
						cEstrut := AllTrim(SubStr(cEstrut,80))
					End
					aAdd(aEstrut,AllTrim(cEstrut))
				EndIf
				
				If Len(aEstrut) > 0
					@ Li,008 Psay STR0015 //"Localização.: "
					For nCont := 1 To Len(aEstrut)
						@ Li,023 Psay aEstrut[nCont]
						If nCont <> Len(aEstrut)
							Somalinha()
						Endif
					Next nCont
				ElseIf AllTrim(cEstrut) <> AllTrim(aCusto[i][2])
					@ Li,008 Psay STR0015 + cEstrut //"Localização.: "
					Somalinha()
				EndIf
			Endif
		EndIf
		
	Next i
	Somalinha()
	dbSelectArea("STJ")
End

Roda(nCntImpr,cRodaTxt,Tamanho)

RetIndex("STJ")
Set Filter To
Set device to Screen

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif
MS_FLUSH()


Return NIL

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³Denis Hyroshi de Souza ³ Data ³ 02/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR870                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Somalinha()
   Li++
   If Li > 58  
      Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
      Cabecalho()
   EndIf
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ Cabecalho³ Autor ³Denis Hyroshi de Souza ³ Data ³ 02/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Cabecalho da Ordem de Servico                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR870                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Cabecalho()     
@ Li,020 Psay STR0008 //"Planilha de Ordens de Servico Referente ao Periodo de "
@ Li,074 Psay Mv_par05 Picture '99/99/9999'
@ Li,085 Psay STR0009 //"ate"
@ Li,089 Psay Mv_Par06 Picture '99/99/9999'
Somalinha()
@ Li,000 Psay replicate("_",132) 
Somalinha()    
If lStj
   Somalinha()
   lStj := .f.
Endif 
Return