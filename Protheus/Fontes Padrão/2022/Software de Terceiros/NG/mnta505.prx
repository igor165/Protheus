#INCLUDE "MNTA505.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

#DEFINE _nVERSAO 2 //Versao do fonte

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA505
Cadastro de Unidade de Negocio

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Function MNTA505()
	
	Local aNGBeginPrm := NGBeginPrm( _nVERSAO )
	Local oBrowse
	
	Private cCadastro := NgSX2Nome( "TSW" ) // "Unidade de Negócio"
	Private aRotina	:= MenuDef()
	
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias( "TSW" )
	oBrowse:SetDescription( cCadastro )
	oBrowse:Activate()
	
	NGReturnPrm( aNGBeginPrm )
	
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Interface da rotina

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function MenuDef()
// Inicializa MenuDef com todas as opções
Return FWMVCMenu( "MNTA505" )

//------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ModelDef()

	Local oModel
	Local oStructTSW := FWFormStruct(1,"TSW")

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTA505")

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTA505_TSW", Nil, oStructTSW)

	oModel:SetDescription( NgSX2Nome( "TSW" ) )

Return oModel

//---------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel("MNTA505")
	Local oView  := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA505_TSW" , FWFormStruct(2,"TSW"))

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100 )

	// Associa um View a um box
	oView:SetOwnerView( "MNTA505_TSW" , "MASTER" )
	
	// Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn( oView )

Return oView