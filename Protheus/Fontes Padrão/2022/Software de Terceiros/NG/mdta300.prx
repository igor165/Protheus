#INCLUDE "Mdta300.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 02 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTA300  ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 18/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Cadastro das Necessidades legais da Legislacao ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/        
Function MDTA300
   
//--------------------------------------------------------
//  Armazena variaveis p/ devolucao (NGRIGHTCLICK)
//--------------------------------------------------------
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO,"MDTA300",{"TAO"})
   
Private aRotina	:= MenuDef()   
Private cCadastro	:= OemtoAnsi(STR0004) //"Requisitos"   
Private aTROCAF3	:= {}   
  
SetBrwCHGAll( .F. ) // nao apresentar a tela para informar a filial
     
Dbselectarea("TA0")   
Dbsetorder(1)
mBrowse(6,1,22,75,"TA0")

//--------------------------------------------------------
//  Devolve variaveis armazenadas (NGRIGHTCLICK)
//--------------------------------------------------------
NGRETURNPRM(aNGBEGINPRM) // Devolve variaveis armazenadas [NGRIGHTCLICK]

Return                              
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTA300  ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 18/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Cadastro das Necessidades legais da Legislacao ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDT300REQ( cAls, nRec , nOpcx )
Local oldROTINA	:= aCLONE(aROTINA)
Local aArea			:= (cAls)->( GetArea() ) 
Local cMDT300Fil
   
Dbselectarea("TA0")
If Empty(TA0->TA0_CODLEG)
   Return
EndIf
Private cCODREQ := TA0->TA0_CODLEG
Private cNOMREQ := TA0->TA0_EMENTA
Private lCODREQUIS := .f.

Private aRotina := MenuDef( 2 )

SetBrwCHGAll( .F. ) // nao apresentar a tela para informar a filial

cMDT300Fil	:= "TOA_FILIAL = '" + xFilial("TOA") + "' AND TOA_CODREQ = '" + cCODREQ + "'"

mBrowse(6, 1, 22, 75, "TOA",,,,,,,,,,,,,, cMDT300Fil )

Dbselectarea("TOA")
Set Filter to  
Dbsetorder(1)
Dbseek(xFilial("TOA"))

RestArea( aArea )  
aRotina := aCLONE(oldROTINA)

Return                      
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A300DESTIN³ Autor ³Denis Hyroshi de Souza ³ Data ³ 18/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistencia para o campo destino da necessidade legal     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A300DESTIN()
aTROCAF3 := {}

If M->TOA_TIPREQ == "1"
   AADD(aTROCAF3,{"TOA_CODIGO","TMA"})
ElseIf M->TOA_TIPREQ == "2"
   AADD(aTROCAF3,{"TOA_CODIGO","SRJ"})
ElseIf M->TOA_TIPREQ == "3"
   AADD(aTROCAF3,{"TOA_CODIGO","TN7"})
ElseIf M->TOA_TIPREQ == "4"
   AADD(aTROCAF3,{"TOA_CODIGO","TN0"})
ElseIf M->TOA_TIPREQ == "5"
   AADD(aTROCAF3,{"TOA_CODIGO","TN5"})
EndIf
Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A300CHKCOD³ Autor ³Denis Hyroshi de Souza ³ Data ³ 18/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Consiste o codigo do campo necessidade legal                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A300CHKCOD()
Local aAreaTOA := TOA->(GetArea())

M->TOA_NOME  := Space(20)

If Empty(M->TOA_CODIGO)
	Return .T.
EndIf

If M->TOA_TIPREQ = "1"
   cCODLEI := Substr(M->TOA_CODIGO,1,Len(TMA->TMA_AGENTE))
   If !TMA->(Dbseek(xFilial("TMA")+cCODLEI))
      MsgStop(STR0007,STR0008) //"Agente não cadastrado."###"ATENCAO"
      Return .f.
   EndIf
   M->TOA_NOME := TMA->TMA_NOMAGE
ElseIf M->TOA_TIPREQ = "2"
   cCODLEI := Substr(M->TOA_CODIGO,1,Len(SRJ->RJ_FUNCAO))
   If !SRJ->(Dbseek(xFilial("SRJ")+cCODLEI))
      MsgStop(STR0009,STR0008) //"Função não cadastrada."###"ATENCAO"
      Return .f.
   EndIf
   M->TOA_NOME := SRJ->RJ_DESC
ElseIf M->TOA_TIPREQ = "3"
   cCODLEI := Substr(M->TOA_CODIGO,1,Len(TN7->TN7_FONTE))
   If !TN7->(Dbseek(xFilial("TN7")+cCODLEI))
      MsgStop(STR0010,STR0008) //"Fonte Geradora não cadastrada."###"ATENCAO"
      Return .f.
   EndIf
   M->TOA_NOME := TN7->TN7_NOMFON
ElseIf M->TOA_TIPREQ = "4"
   cCODLEI := Substr(M->TOA_CODIGO,1,Len(TN0->TN0_NUMRIS))
   If !TN0->(Dbseek(xFilial("TN0")+cCODLEI))
      MsgStop(STR0011,STR0008) //"Risco não cadastrado."###"ATENCAO"
      Return .f.
   EndIf
   TMA->(Dbseek(xFilial("TMA")+TN0->TN0_AGENTE))
   M->TOA_NOME := TMA->TMA_NOMAGE
ElseIf M->TOA_TIPREQ = "5"
   cCODLEI := Substr(M->TOA_CODIGO,1,Len(TN5->TN5_CODTAR))
   If !TN5->(Dbseek(xFilial("TN5")+cCODLEI))
      MsgStop(STR0012,STR0008) //"Tarefa não cadastrada."###"ATENCAO"
      Return .f.
   EndIf
   M->TOA_NOME := TN5->TN5_NOMTAR
EndIf

If INCLUI
	Dbselectarea("TOA")
	Dbsetorder(01)
	If Dbseek(xFilial("TOA")+cCODREQ+M->TOA_TIPREQ+M->TOA_CODIGO)
		RestArea(aAreaTOA)
		Help(" ",1,"JAEXISTINF")
		Return .f.
	EndIf
EndIf

xx := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TOA_NOME"})
If xx > 0
   aCOLS[n][xx] := M->TOA_NOME
EndIf

RestArea(aAreaTOA)
Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A300DELREQ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 18/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Deleta registros                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A300DELREQ
Local aOLD := aCLONE(aROTINA)

PRIVATE aRotina := Menudef( 3 )

PRIVATE aTELA[0][0],aGETS[0],aHeader[0],nUsado:=0
nOpc := 5

DbSelectArea("TOA")
DbSetOrder(1)

M->TOA_CODREQ := TOA->TOA_CODREQ
lRET  := NGCAD01("TOA",recno(),5)

aROTINA := aCLONE(aOLD)

lRefresh := .T.

DbSelectArea("TOA")
DbGoTop()

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A300INCREQ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 18/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inclui registros                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A300INCREQ(cAlias,nREG,nOPCX)
Local LVar01:=1,nLinhas:=0,cSaveMenuh, bCampo
Local GetList:={}, nOpcao:=3
Local lGravaOK := .T.
Local oDlg, oGet, i,oPnlPai

//Variaveis de Tela
Private oMenu, aObjects, aInfo    
Private aTELA[0][0],aGETS[0],aHeader[0],nUsado:=0

aTROCAF3 	:= {}
aObjects	:= {}
aInfo 		:= {}      

aSize := MsAdvSize(,.f.,430)
Aadd(aObjects,{0.01,0.01,.t.,.t.})
aInfo := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
aPosObj := MsObjSize(aInfo, aObjects,.t.)

Dbselectarea("TOA")

bCampo := { |nCPO| Field(nCPO) }

FOR i := 1 TO FCount()
    M->&(Eval(bCampo,i)) := FieldGet(i)

	If Inclui
		IF ValType(M->&(Eval(bCampo,i))) == "C"  
			If Eval(bCampo,i) == "TOA_FILIAL"
				M->&(Eval(bCampo,i)) := xFilial("TOA")
			Else 
				M->&(Eval(bCampo,i)) := Space(Len(M->&(Eval(bCampo,i))))
			Endif
        ELSEIF ValType(M->&(Eval(bCampo,i))) == "N"
               M->&(Eval(bCampo,i)) := 0               
        ELSEIF ValType(M->&(Eval(bCampo,i))) == "D"
                  M->&(Eval(bCampo,i)) := cTod("  /  /  ")
        ELSEIF ValType(M->&(Eval(bCampo,i))) == "L"
               M->&(Eval(bCampo,i)) := .F.
        ENDIF
	Else
		M->&(Eval(bCampo,i)) := TOA->&(Eval(bCampo,i))
	Endif
Next i

M->TOA_CODREQ := cCODREQ
 

lRefresh := .t.

Dbselectarea("TOA")
	aPos1 := {  1,  1, 130,318}
	Continua := .F.
	Dbselectarea("TOA")
	
	nOpca := 0

DEFINE MSDIALOG oDlg TITLE OemToAnsi(cCadastro) From aSize[7],0 To aSize[6],aSize[5]-5 Of oMainWnd Pixel
    
    oPnlPai := TPanel():New(0,0,,oDlg,,,,,,100,100,.F.,.F.)
	oPnlPai:Align := CONTROL_ALIGN_ALLCLIENT
	
	Enchoice("TOA",nReg,nOpcx,,,,,aPosObj[1],,3,,,,oPnlPai)
	If Inclui
		M->TOA_NOMREQ := Substr(cNOMREQ,1,40)
		M->TOA_NOME   := Space(20)
	Endif
	
	NgPopUp(asMenu,@oMenu)
	oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
	
	
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=1,If(!CHKMDT300(),nOpca := 0,oDlg:End())},{||oDlg:End()})

If nOpcA == 1
	MDT300SAVE(cAlias,nOpcao)
ElseIf __lSX8
	RollBackSX8()
Endif
    
Set Filter to TOA_FILIAL == xFilial("TOA") .and. TOA_CODREQ == cCODREQ

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A300NECBRW³ Autor ³Denis Hyroshi de Souza ³ Data ³ 18/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna o nome da necessidade legal                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A300NECBRW(nTipo)
Local cRet := Space(20)
Local aArea := GetArea()
Local cTab := Space(3)
Local cCpo := Space(10)

If TOA->TOA_TIPREQ == "1"
	cTab := "TMA"
	cCpo := "TMA->TMA_NOMAGE"
Elseif TOA->TOA_TIPREQ == "2"
	cTab := "SRJ"
	cCpo := "SRJ->RJ_DESC"
Elseif TOA->TOA_TIPREQ == "3"
	cTab := "TN7"        
	cCpo := "TN7->TN7_NOMFON"
Elseif TOA->TOA_TIPREQ == "4"
	cTab := "TN0"
	cCpo := "TMA->TMA_NOMAGE"
Elseif TOA->TOA_TIPREQ == "5"
	cTab := "TN5"
	cCpo := "TN5->TN5_NOMTAR"
Else	
	Return cRet
Endif

Dbselectarea(cTab)
Dbsetorder(1)
Dbseek(xFilial(cTab)+TOA->TOA_CODIGO)
If TOA->TOA_TIPREQ == "4"
	Dbselectarea("TMA")
	Dbsetorder(1)
	Dbseek(xFilial("TMA")+TN0->TN0_AGENTE)
Endif	

cRet := Substr(&cCpo,1,20)

RestArea(aArea)
Return cRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CHKMDT300 ³ Autor ³Denis Hyroshi de Souza ³ Data ³20/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistencia antes de gravar                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CHKMDT300()
Local aArea := GetArea()

If !A300CHKCOD()
	Return .f.
Endif

If !obrigatorio(aGets,aTela)
	Return .f.
Endif
Dbselectarea("TOA")
Dbsetorder(1)
If Dbseek(xFilial("TOA")+M->TOA_CODREQ+M->TOA_TIPREQ+M->TOA_CODIGO)
	Help(" ",1,"JAEXISTINF")
	Restarea(aArea)
	Return .f.
Endif

Restarea(aArea)
Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDT300SAVE³ Autor ³Denis Hyroshi de Souza ³ Data ³20/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava o registro                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function MDT300SAVE(cAlias,nOpcao)
Local aArea := GetArea()
Local nx,ny

Dbselectarea("TOA")
RecLock("TOA",.t.)
For ny := 1 To TOA->(FCOUNT())
	nx := "M->" + FieldName(ny)
	FieldPut(ny, &nx.)
Next ny
TOA->TOA_FILIAL := xFilial("TOA")
Msunlock("TOA")

Restarea(aArea)
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMDT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef( nOpcao )
Local aRotina
Default nOpcao := 1

If nOpcao == 1
	aRotina :=	{	{STR0001 , "AxPesqui"  , 0 , 1 }	,; 	//"Pesquisar"
			 	 		{STR0002 , "NGCAD01"   , 0 , 2 }	,; 	//"Visualizar"
						{STR0003 , "MDT300REQ" , 0 , 3 }	} 		//"Necessidades"
ElseIf nOpcao == 2
	aRotina	:= {	{STR0001 , "AxPesqui"  , 0 , 1 } ,; 		//"Pesquisar"
						{STR0002 , "NGCAD01"   , 0 , 2 } ,; 		//"Visualizar"
						{STR0005 , "A300INCREQ", 0 , 3 } ,; 		//"Incluir"
						{STR0006 , "A300DELREQ", 0 , 5 , 3 } } //"Excluir"
ElseIf nOpcao == 3
	aRotina	:= {	{STR0001 , "AxPesqui" , 0 , 1 }	,; 		//"Pesquisar"
						{STR0002 , "AxPesqui" , 0 , 1 }	,; 		//"Visualizar"
						{STR0005 , "AxPesqui" , 0 , 1 } ,; 		//"Incluir"
						{STR0013 , "AxPesqui" , 0 , 1 } ,; 		//"Alterar"
						{STR0006 , "AxPesqui" , 0 , 1 } } 			//"Excluir"
EndIf

Return aRotina