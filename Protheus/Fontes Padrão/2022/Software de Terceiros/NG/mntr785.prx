#INCLUDE "mntr785.ch"
#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR785  ³ Autor ³ Elisangela Costa      ³ Data ³ 30/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Bens da Manutenao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR785()

    Local aNGBEGINPRM := NGBEGINPRM()
    Local aArea := GetArea()
    Local oReport

    Private cPerg    := "MNT785"
	Private cTRBZ785 := GetNextAlias()
	Private oTmpTbl1 := Nil

    /*---------------------------------------------------------------
    Vetor utilizado para armazenar retorno da função MNT045TRB,
    criada de acordo com o item 18 (RoadMap 2013/14)
    ---------------------------------------------------------------*/
    Private vFilTRB := MNT045TRB()

    SetKey(VK_F4, {|| MNT045FIL( vFilTRB[2] )})

    If FindFunction("TRepInUse") .And. TRepInUse()
        //-- Interface de impressao
        oReport := ReportDef()
        oReport:SetPortrait() //Default Retrato
        oReport:PrintDialog()
    Else
        MNTR785R3()
    EndIf

    MNT045TRB( .T., vFilTRB[1], vFilTRB[2])

    NGRETURNPRM(aNGBEGINPRM)

    RestArea(aArea)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³Elisangela Costa       ³ Data ³  11/07/06³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR705                                                    ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef()
Local oReport
Local oSection1
Local oSection2
Local oCell
Private cImpEstrut

//LAYOUT
/*
          1         2         3         4         5         6         7         8         9        10       110       120
012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567
________________________________________________________________________________________________________________________________________________________________________
                                                                  Bens da Manutencao
________________________________________________________________________________________________________________________________________________________________________
Servico  Descricao
________________________________________________________________________________________________________________________________________________________________________
xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Bem               Nome                       Pai               C.Custos              C.Trab.    O.s     Data        Contado 1     Contador 2
________________________________________________________________________________________________________________________________________________________________________
xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxx  xxxxxx  xx/xx/xxxx  999.999.999  999.999.999
xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxx  xxxxxx  xx/xx/xxxx  999.999.999  999.999.999
xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxx  xxxxxx  xx/xx/xxxx  999.999.999  999.999.999
*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MNTR785",OemToAnsi(STR0006),"MNT785",{|oReport| ReportPrint(oReport)},STR0001+". "+STR0002+" "+STR0003)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para qarametros!                        ³
//³ mv_par01     // De Servico                                   ³
//³ mv_par02     // Ate Servico                                  ³
//³ mv_par03     // De Sequencia                                 ³
//³ mv_par04     // Ate Sequencia                                ³
//³ mv_par05     // De Data                                      ³
//³ mv_par06     // Ate Data                                     ³
//| mv_par07     // Imprimir Localizacao ?  Sim/Nao              |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New(oReport,STR0024,{cTRBZ785,"ST4"}) //"Manutencao"
TRCell():New(oSection1,"(cTRBZ785)->SERVICO",cTRBZ785,STR0012,"@!" ,6 ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Serviço"
TRCell():New(oSection1,"T4_NOME"     ,"ST4",STR0013,"@!" ,40,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrição"
TRCell():New(oSection1,"(cTRBZ785)->SEQRELA",cTRBZ785,STR0014,"@!" ,6 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Sequência"
TRPosition():New(oSection1,"ST4",1,{|| xFilial("ST4") + (cTRBZ785)->SERVICO})

oSection2 := TRSection():New(oReport,STR0025,{cTRBZ785,"STC","ST9"}) //"Bens"
oCell := TRCell():New(oSection2,"(cTRBZ785)->CODBEM"  ,cTRBZ785 ,STR0015,"@!" ,16 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Bem"
oCell := TRCell():New(oSection2,"T9_NOME"      ,"ST9" ,STR0016,"@!" ,40 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Nome"
oCell := TRCell():New(oSection2,"TC_CODBEM"    ,"STC" ,STR0017,"@!" ,16 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Pai"
oCell := TRCell():New(oSection2,"(cTRBZ785)->CCUSTO"  ,cTRBZ785 ,STR0018,"@!" ,20 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"C.Custos"
oCell := TRCell():New(oSection2,"(cTRBZ785)->CENTRAB" ,cTRBZ785 ,STR0019,"@!" ,6  ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"C.Trab."
oCell := TRCell():New(oSection2,"(cTRBZ785)->ORDEM"   ,cTRBZ785 ,STR0020,"@!" ,6  ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"O.S."
oCell := TRCell():New(oSection2,"(cTRBZ785)->ORIGI"   ,cTRBZ785 ,STR0021,"99/99/9999" ,10,/*lPixel*/,/*{|| code-block de impressao }*/) //"Data"
oCell := TRCell():New(oSection2,"(cTRBZ785)->CONTAD1" ,cTRBZ785 ,STR0022,"@E 999,999,999"  ,11,/*lPixel*/,/*{|| code-block de impressao }*/) //"Contador 1"
oCell := TRCell():New(oSection2,"(cTRBZ785)->CONTAD2" ,cTRBZ785 ,STR0023,"@E 999,999,999"  ,11,/*lPixel*/,/*{|| code-block de impressao }*/) //"Contador 2"
//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a
//direita
oSection2:Cell("(cTRBZ785)->CONTAD1"):SetHeaderAlign("RIGHT")
oSection2:Cell("(cTRBZ785)->CONTAD2"):SetHeaderAlign("RIGHT")
TRPosition():New(oSection2,"ST9",1,{|| xFilial("ST9") + (cTRBZ785)->CODBEM})
TRPosition():New(oSection2,"STC",3,{|| xFilial("STC") + (cTRBZ785)->CODBEM})

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR785  ³ Autor ³ Elisangela Costa      ³ Data ³ 30/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Bens da Manutenao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR785R3()

    Local cString    := "STJ"
    Local cdesc1     := STR0001 //"Relatorio de Bens da Manutencao"
    Local cdesc2     := STR0002 //"E permitido selecionar tipo de informacao a ser"
    Local cdesc3     := STR0003 //"apresentada, atraves dos parametros do relatorio."
    Local wnrel      := "MNTR785"

    Private aReturn  := { STR0004, 1,STR0005, 1, 2, 1, "",1 }  //"Zebrado"###"Administracao"
    Private nLastKey := 0
    Private Titulo   := STR0006 //"Bens da Manutencao"
    Private Tamanho  := "M"
    Private nomeprog := "MNTR785"
    Private lMaqui   := .F.

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Verifica as perguntas selecionadas                           ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Variaveis utilizadas para qarametros!                        ³
    //³ mv_par01     // De Servico                                   ³
    //³ mv_par02     // Ate Servico                                  ³
    //³ mv_par03     // De Sequencia                                 ³
    //³ mv_par04     // Ate Sequencia                                ³
    //³ mv_par05     // De Data                                      ³
    //³ mv_par06     // Ate Data                                     ³
    //| mv_par07     // Imprimir Localizacao ?  Sim/Nao              |
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Pergunte(cPerg,.F.)

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Envia controle para a funcao SETPRINT                        ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    wnrel := SetPrint(cString,wnrel,cperg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

    SetKey(VK_F4, {|| })

    If nLastKey = 27
        Set Filter To
        Return
    EndIf

    SetDefault(aReturn,cString)
    RptStatus({|lEnd| R785Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R785Imp  ³ Autor ³ Elisangela Costa      ³ Data ³ 30/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR785                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function R785IMP(lEND,WNREL,TITULO,TAMANHO)

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Define Variaveis                                             ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Local cRodaTxt := ""
    Local nCntImpr := 0
    Local cEstrut  := ""
    Local aEstrut  := ""
    Local nCont    := 0

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Variaveis para controle do cursor de progressao do relatorio ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Local nTotRegs := 0 ,nMult := 1 ,nPosAnt := 4 ,nPosAtu := 4 ,nPosCnt := 0

    cPARSEQJ := '(STJ->TJ_SEQRELA >= MV_PAR03 .And. STJ->TJ_SEQRELA <= MV_PAR04)'
    cPARSEQS := '(STS->TS_SEQRELA >= MV_PAR03 .And. STS->TS_SEQRELA <= MV_PAR04)'

    cCONDSTJ := 'STJ->TJ_SITUACA = "L" .And. STJ->TJ_TERMINO = "S" .And. '
    cCONDSTJ := cCONDSTJ + cPARSEQJ+' .And.'
    cCONDSTJ := cCONDSTJ + '(STJ->TJ_DTORIGI >= MV_PAR05 .And. STJ->TJ_DTORIGI <= MV_PAR06) .And.'
    cCONDSTJ := cCONDSTJ + 'STJ->TJ_TIPOOS = "B"'
    cCONDSTJ := cCONDSTJ + ' .And. !MNT045STB( STJ->TJ_CODBEM, vFilTRB[2] )'

    cCONDSTS := 'STS->TS_SITUACA = "L" .And. STS->TS_TERMINO = "S" .And. '
    cCONDSTS := cCONDSTS + cPARSEQS+' .And.'
    cCONDSTS := cCONDSTS + '(STS->TS_DTORIGI >= MV_PAR05 .And. STS->TS_DTORIGI <= MV_PAR06) .And.'
    cCONDSTS := cCONDSTS + 'STS->TS_TIPOOS = "B"'
    cCONDSTS := cCONDSTS + ' .And. !MNT045STB( STS->TS_CODBEM, vFilTRB[2] )'

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Contadores de linha e pagina                                 ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Private li := 80 ,m_pag := 1

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Variaveis locais exclusivas deste programa                   ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Private CABEC1  := ""
    Private CABEC2  := ""
    Private aVETINR := {}

    /*/                                                                                                    1         1         1         1
    1         2         3         4         5         6         7         8         9         0         1         2         3
    012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
    ______________________________________________________________________________________________________________________________________

    Servico - xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  Sequencia - xxx

    Bem              Nome                      Pai              C.Custos             C.Trab.   O.S.   Data        Contador 1  Contador 2
    xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxx xxxxxx xx/xx/xxxx 999.999.999 999.999.999
    xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxx xxxxxx xx/xx/xxxx 999.999.999 999.999.999
    xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxx xxxxxx xx/xx/xxxx 999.999.999 999.999.999
    xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxx xxxxxx xx/xx/xxxx 999.999.999 999.999.999

    /*/

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Verifica se deve comprimir ou nao                            ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    nTipo  := IIF(aReturn[4]==1,15,18)

    aDBFR785 := {{"SERVICO"  ,"C",06,0},;
                 {"SEQRELA"  ,"C",03,0},;
                 {"CODBEM"   ,"C",16,0},;
                 {"CCUSTO"   ,"C",Len(STJ->TJ_CCUSTO),0},;
                 {"CENTRAB"  ,"C",06,0},;
                 {"ORDEM"    ,"C",06,0},;
                 {"ORIGI"    ,"D",08,0},;
                 {"CONTAD1"  ,"N",09,0},;
                 {"CONTAD2"  ,"N",09,0}}

    vINDR785  := {{"SERVICO","SEQRELA","CODBEM","ORDEM","ORIGI"}}

	oTmpTbl1  := NGFwTmpTbl(cTRBZ785,aDBFR785,vINDR785)

    Processa({|lEND| MNTR785STJ()},STR0011) //"Selecionando Registros..."
    Processa({|lEND| MNTR785STS()},STR0011) //"Selecionando Registros..."

    // Inicio da Impressao
    dbSelectArea(cTRBZ785)
    dbGoTop()
    SetRegua(LastRec())
    While !EoF()

        IncRegua()
        cServico := (cTRBZ785)->SERVICO
        While !Eof() .And. (cTRBZ785)->SERVICO = cSERVICO

            nSeq := (cTRBZ785)->SEQRELA

            NgSomali(58)
            NgSomali(58)
            dbSelectArea("ST4")
            dbSeek(xFilial("ST4")+cServico)
            @ Li,000 Psay STR0008 + cServico //"Servico - "
            @ Li,019 Psay ST4->T4_NOME
            @ Li,062 Psay STR0009  //"Sequencia - "
            @ Li,074 Psay nSeq
            NgSomali(58)
            NgSomali(58)
            @ Li,000 Psay STR0010 //"Bem              Nome                      Pai              C.Custos             C.Trab.   O.S.   Data        Contador 1  Contador 2"

            dbSelectArea(cTRBZ785)
            While !Eof() .And. (cTRBZ785)->SERVICO = cSERVICO .And. (cTRBZ785)->SEQRELA  = nSeq

                cCODBEM := (cTRBZ785)->CODBEM

                NgSomali(58)
                @ Li,000 Psay (cTRBZ785)->CODBEM
                @ Li,017 Psay NGSEEK('ST9',cCODBEM,1,'Substr(T9_NOME,1,25)')

                lPRIMEI := .T.
                dbSelectArea(cTRBZ785)
                While !Eof() .And. (cTRBZ785)->SERVICO = cSERVICO .And. (cTRBZ785)->SEQRELA  = nSeq .And. (cTRBZ785)->CODBEM == cCODBEM

                    If !lPRIMEI
                        NgSomali(58)
                    EndIf
                    dbSelectArea("STC")
                    dbSetOrder(03)
                    dbSeek(xFilial("STC")+(cTRBZ785)->CODBEM)
                    @ LI,043 Psay STC->TC_CODBEM
                    @ Li,060 Psay (cTRBZ785)->CCUSTO
                    @ Li,081 Psay (cTRBZ785)->CENTRAB
                    @ Li,091 Psay (cTRBZ785)->ORDEM   Picture "!@"
                    @ Li,098 Psay (cTRBZ785)->ORIGI   Picture "99/99/9999"
                    @ Li,109 Psay (cTRBZ785)->CONTAD1 Picture "@E 999,999,999"
                    @ Li,121 Psay (cTRBZ785)->CONTAD2 Picture "@E 999,999,999"
                    lPRIMEI := .F.

                    dbSelectArea(cTRBZ785)
                    dbSkip()
                End

                If MV_PAR07 == 1
                    cEstrut := NGLocComp(cCODBEM,"1")
                    aEstrut := {}
                    If Len(cEstrut) > 100
                        aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
                        cEstrut := AllTrim(SubStr(cEstrut,101))
                        While Len(cEstrut) > 100
                            aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
                            cEstrut := AllTrim(SubStr(cEstrut,101))
                        EndDo
                        aAdd(aEstrut,AllTrim(cEstrut))
                    EndIf

                    If Len(aEstrut) > 0
                        For nCont := 1 To Len(aEstrut)
                            NgSomali(58)
                            If nCont == 1
                                @ LI,017 Psay STR0026 + aEstrut[nCont] //"Localização.: "
                            Else
                                @ LI,017 Psay Space(Len(STR0026)) + aEstrut[nCont] //"Localização.: "
                            EndIf
                        Next nCont
                    Else
                        NgSomali(58)
                        If AllTrim(cEstrut) <> AllTrim(cCODBEM)
                            NgSomali(58)
                            @ LI,017 Psay STR0026 + cEstrut //"Localização.: "
                        Else
                            NgSomali(58)
                            @ LI,017 Psay STR0026 + STR0027 //"Localização.: "###"Não está relacionado a uma Estrutura."
                            NgSomali(58)
                        EndIf
                    EndIf
                EndIf
            EndDo
        EndDo
    EndDo

    Roda(nCntImpr,cRodaTxt,Tamanho)

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Devolve a condicao original do arquivo principam             ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    RetIndex("STJ")
    RetIndex("STS")
    RetIndex("STC")
    RetIndex("ST4")
    RetIndex("ST9")

    Set Filter To
    Set Device To Screen

    If aReturn[5] = 1
        Set Printer To
        dbCommitAll()
        OurSpool(wnrel)
    EndIf

    //Deleta o arquivo temporario fisicamente
    oTmpTbl1:Delete()

    MS_FLUSH()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³  Elisangela Costa     ³ Data ³ 21/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR270                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)

    Local oSection1 := oReport:Section(1)
    Local oSection2 := oReport:Section(2)
    Local cEstrut   := ""
    Local aEstrut   := {}
    Local nCont     := 0
    Local nImpLin   := 0
    Local nImpCol   := 0

    Private aVETINR := {}

    cPARSEQJ := '(stj->tj_seqrela >= mv_par03 .And. stj->tj_seqrela <= mv_par04)'
    cPARSEQS := '(sts->ts_seqrela >= mv_par03 .And. sts->ts_seqrela <= mv_par04)'

    cCONDSTJ := 'stj->tj_situaca = "L" .And. stj->tj_termino = "S" .And. '
    cCONDSTJ := cCONDSTJ + cPARSEQJ+' .And.'
    cCONDSTJ := cCONDSTJ + '(stj->tj_dtorigi >= mv_par05 .And. stj->tj_dtorigi <= mv_par06) .And.'
    cCONDSTJ := cCONDSTJ + 'stj->tj_tipoos = "B"'
    cCONDSTJ := cCONDSTJ + ' .And. !MNT045STB( STJ->TJ_CODBEM, vFilTRB[2] )'
    cCONDSTS := 'sts->ts_situaca = "L" .And. sts->ts_termino = "S" .And. '
    cCONDSTS := cCONDSTS + cPARSEQS+' .And.'
    cCONDSTS := cCONDSTS + '(sts->ts_dtorigi >= mv_par05 .And. sts->ts_dtorigi <= mv_par06) .And.'
    cCONDSTS := cCONDSTS + 'sts->ts_tipoos = "B"'
    cCONDSTS := cCONDSTS + ' .And. !MNT045STB( STS->TS_CODBEM, vFilTRB[2] )'

    aDBFR785 := {{"SERVICO"  ,"C",06,0},;
			     {"SEQRELA"  ,"C",03,0},;
			     {"CODBEM"   ,"C",16,0},;
			     {"CCUSTO"   ,"C",Len(STJ->TJ_CCUSTO),0},;
			     {"CENTRAB"  ,"C",06,0},;
			     {"ORDEM"    ,"C",06,0},;
			     {"ORIGI"    ,"D",08,0},;
			     {"CONTAD1"  ,"N",09,0},;
			     {"CONTAD2"  ,"N",09,0}}

    vINDR785  := {{"SERVICO","SEQRELA","CODBEM","ORDEM","ORIGI"}}
	oTmpTbl1  := NGFwTmpTbl(cTRBZ785,aDBFR785,vINDR785)

    Processa({|lEND| MNTR785STJ()},STR0011) //"Selecionando Registros..."
    Processa({|lEND| MNTR785STS()},STR0011) //"Selecionando Registros..."

    dbSelectArea(cTRBZ785)
    dbGotop()

    oReport:SetMeter(RecCount())

    While !Eof() .And. !oReport:Cancel()

        cSERVICO  := (cTRBZ785)->SERVICO
        nSeq      := (cTRBZ785)->SEQRELA

        oSection1:Init()
        oSection1:PrintLine()
        oSection2:Init()

        While !Eof() .And. !oReport:Cancel() .And. (cTRBZ785)->SERVICO = cSERVICO .And.;
                (cTRBZ785)->SEQRELA = nSeq

            cCODBEM  := (cTRBZ785)->CODBEM
            cCODBEM2 := " "

            While !Eof() .And. !oReport:Cancel() .And. (cTRBZ785)->SERVICO = cSERVICO .And.;
                    (cTRBZ785)->SEQRELA = nSeq .And. (cTRBZ785)->CODBEM == cCODBEM

                If cCODBEM2 == cCODBEM
                    oSection2:Cell("(cTRBZ785)->CODBEM"):Hide()
                    oSection2:Cell("T9_NOME"):Hide()
                Else
                    oSection2:Cell("(cTRBZ785)->CODBEM"):Show()
                    oSection2:Cell("T9_NOME"):Show()
                EndIf

                cCODBEM2 := (cTRBZ785)->CODBEM

                oReport:IncMeter()
                oSection2:PrintLine()

                dbSelectArea(cTRBZ785)
                dbSkip()
            EndDo

            If MV_PAR07 == 1
                cEstrut := NGLocComp(cCODBEM,"1")
                aEstrut := {}

                If Len(cEstrut) > 100
                    aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))

                    cEstrut := AllTrim(SubStr(cEstrut,101))

                    While Len(cEstrut) > 100
                        aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
                        cEstrut := AllTrim(SubStr(cEstrut,101))
                    End

                    aAdd(aEstrut,AllTrim(cEstrut))
                EndIf

                nImpCol := oSection2:Cell("T9_NOME"):Col()

                If Len(aEstrut) > 0
                    For nCont := 1 To Len(aEstrut)
                        If nCont == 1
                            cImpEstrut := STR0026 + aEstrut[nCont] //"Localização.: "
                        Else
                            cImpEstrut := Space(Len(STR0026)) + aEstrut[nCont] //"Localização.: "
                        EndIf

                        nImpLin    := oReport:Row()

                        oReport:PrintText(cImpEstrut,nImpLin,nImpCol)
                        oReport:SkipLine()
                    Next nCont
                Else
                    oReport:SkipLine()

                    If AllTrim(cEstrut) <> AllTrim(cCODBEM)
                        cImpEstrut := STR0026 + cEstrut
                    Else
                        cImpEstrut := STR0026 + STR0027 //"Localização.: "###"Não está relacionado a uma Estrutura."
                    EndIf

                    nImpLin := oReport:Row()

                    oReport:PrintText(cImpEstrut,nImpLin,nImpCol)
                    oReport:SkipLine()
                    oReport:SkipLine()
                EndIf
            EndIf

        EndDo

        oSection1:Finish()
        oSection2:Finish()

    EndDo

    //Deleta o arquivo temporario fisicamente
    oTmpTbl1:Delete()

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR785STJ³ Autor ³ Elisangela Costa      ³ Data ³ 11/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Leitura do STJ                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR785                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR785STJ()

    dbSelectArea("STJ")
    dbSetOrder(04)
    dbSeek(xFilial("STJ")+MV_PAR01,.t.)
    ProcRegua(LastRec())
    While !Eof() .And. STJ->TJ_FILIAL == xFilial("STJ") .And.;
            STJ->TJ_SERVICO <= MV_PAR02

        IncProc()
        If &(cCONDSTJ)
            (cTRBZ785)->(DbAppend())
            (cTRBZ785)->SERVICO := STJ->TJ_SERVICO
            (cTRBZ785)->SEQRELA := STJ->TJ_SEQRELA
            (cTRBZ785)->CODBEM  := STJ->TJ_CODBEM
            (cTRBZ785)->CCUSTO  := STJ->TJ_CCUSTO
            (cTRBZ785)->CENTRAB := STJ->TJ_CENTRAB
            (cTRBZ785)->ORDEM   := STJ->TJ_ORDEM
            (cTRBZ785)->ORIGI   := STJ->TJ_DTORIGI
            (cTRBZ785)->CONTAD1 := STJ->TJ_POSCONT
            (cTRBZ785)->CONTAD2 := STJ->TJ_POSCON2
        EndIf
        dbSelectArea("STJ")
        dbSkip()
    EndDo
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR785STS³ Autor ³ Elisangela Costa      ³ Data ³ 11/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Leitura do STS                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR785                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR785STS()

    dbSelectArea("STS")
    dbSetOrder(05)
    dbSeek(xFilial("STS")+MV_PAR01,.t.)
    ProcRegua(LastRec())
    While !Eof() .And. STS->TS_FILIAL == xFilial("STS") .And. ;
            STS->TS_SERVICO <= MV_PAR02

        IncProc()
        If &(cCONDSTS)
            (cTRBZ785)->(DbAppend())
            (cTRBZ785)->SERVICO := STS->TS_SERVICO
            (cTRBZ785)->SEQRELA := STS->TS_SEQRELA
            (cTRBZ785)->CODBEM  := STS->TS_CODBEM
            (cTRBZ785)->CCUSTO  := STS->TS_CCUSTO
            (cTRBZ785)->CENTRAB := STS->TS_CENTRAB
            (cTRBZ785)->ORDEM   := STS->TS_ORDEM
            (cTRBZ785)->ORIGI   := STS->TS_DTORIGI
            (cTRBZ785)->CONTAD1 := STS->TS_POSCONT
            (cTRBZ785)->CONTAD2 := STS->TS_POSCON2
        EndIf
        dbSelectArea("STS")
        dbSkip()
    EndDo

Return .T.