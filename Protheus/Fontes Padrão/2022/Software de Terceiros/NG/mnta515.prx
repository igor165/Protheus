#INCLUDE 'MNTA515.CH'
#INCLUDE 'TOTVS.CH'
#INCLUDE 'FWMVCDEF.CH'

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA515
Rotina de Cadastro de Taxas por ano veículo

@type function

@author karina.felicetti
@since 29/05/2020

@return Nil
/*/
//---------------------------------------------------------------------
Function MNTA515()

    Local oBrowse

    oBrowse := FWMBrowse():New()
    oBrowse:SetAlias( 'TS5' )
    oBrowse:SetMenuDef( 'MNTA515' )
    oBrowse:SetDescription( STR0006 ) // 'Cadastro de Taxas por ano veículo'
    oBrowse:Activate()

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Definição do Menu (padrão MVC).

@author karina.felicetti
@since 04/06/2020

@return array, retorna Menu padrão MVC
/*/
//---------------------------------------------------------------------
Static Function MenuDef()
//Inicializa MenuDef com todas as opções
Return FWMVCMenu( 'MNTA515' )

//---------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Modelo de dados da rotina

@author karina.felicetti

@since 29/05/2020

@return oModel, objeto do modelo MVC
/*/
//---------------------------------------------------------------------
Static Function ModelDef()

	Local oStructTS5 := FWFormStruct( 1, 'TS5' )
	Local oStructTS7 := FWFormStruct( 1, 'TS7', { |x| !(ALLTRIM(x) $ 'TS7_CODFAM, TS7_TIPMOD, TS7_DOCTO') }, /*lViewUsado*/ )

	Local oModel

    // Cria o objeto do Modelo de Dados
    oModel := MPFormModel():New( 'MNTA515', /*bPre*/, /*bPost*/, /*bCommit*/, /*bCancel*/ )

    // Adiciona ao modelo uma estrutura de formulário de edição por campo
    oModel:AddFields( 'MNTA515_TS5', Nil, oStructTS5, /*bPre*/, /*bPost*/, /*bLoad*/ )

	// Adiciona ao modelo uma estrutura de formulário de edição por grid
	oModel:AddGrid( 'MNTA515_TS7', 'MNTA515_TS5', oStructTS7, /*bLinePre*/, { |oGrid| A515LINOK( oGrid ) }, /*bPreVal*/, /*bPost*/, /*BLoad*/ )

	// Faz relaciomaneto entre os compomentes do model
	oModel:SetRelation( 'MNTA515_TS7', { { 'TS7_FILIAL', 'xFilial( "TS7" )' }, { 'TS7_CODFAM', 'TS5_CODFAM' }, { 'TS7_TIPMOD', 'TS5_TIPMOD' }, { 'TS7_DOCTO', 'TS5_DOCTO' } }, TS7->( IndexKey( 1 ) ) )

	// Liga o controle de nao repeticao de linha
	oModel:GetModel( 'MNTA515_TS7' ):SetUniqueLine( { 'TS7_ANO' } )

    oModel:SetDescription( STR0006 ) // 'Cadastro de Taxas por ano veículo'

	// Adiciona a descricao do Componente do Modelo de Dados
	oModel:GetModel('MNTA515_TS5' ):SetDescription( STR0014 ) // 'Documento por Ano do Veículo'
	oModel:GetModel('MNTA515_TS7' ):SetDescription( STR0015 ) // 'Valor do Documento por Ano do Veículo'

Return oModel

//---------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Interface da rotina

@author karina.felicetti

@since 29/05/2020

@return oView, objeto da View MVC
/*/
//---------------------------------------------------------------------
Static Function ViewDef()

	Local oModel     := FWLoadModel( 'MNTA515' )
	Local oStructTS5 := FWFormStruct( 2, 'TS5' )
	Local oStructTS7 := FWFormStruct( 2, 'TS7', { |x| !(ALLTRIM(x) $ 'TS7_CODFAM, TS7_TIPMOD, TS7_DOCTO') }, /*lViewUsado*/ )
	Local oView      := FWFormView():New()

    // Objeto do model a se associar a view.
    oView:SetModel( oModel )

    // Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
    oView:AddField( 'VIEW_TS5', oStructTS5, 'MNTA515_TS5' )

	// Adiciona no nosso View um controle do tipo FormGrid(antiga newgetdados)
	oView:AddGrid( 'VIEW_TS7', oStructTS7, 'MNTA515_TS7' )

	// Criar um 'box' horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( 'SUPERIOR', 60 )
	oView:CreateHorizontalBox( 'INFERIOR', 40 )

	// Relaciona o ID da View com o 'box' para exibicao
	oView:SetOwnerView( 'VIEW_TS5', 'SUPERIOR' )
	oView:SetOwnerView( 'VIEW_TS7', 'INFERIOR' )

    // Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
    NGMVCUserBtn( oView )

Return oView

//----------------------------------------------------------------------
/*/{Protheus.doc} MNTA515ANO
Função utilizada no valid do campo TS7_ANO, responsável pelas validações de ano

@type function

@author karina.felicetti
@since 01/06/2020

@return Lógico, .T. se o ano inserido for válido, .F. se inválido
/*/
//---------------------------------------------------------------------
Function MNTA515ANO()

	Local oModel := FWModelActive() // Copia o Model utilizado.
	Local oGrid  := oModel:GetModel( 'MNTA515_TS7' ) // Posiciona no Model da Grid
	Local lRet   := .T.

	If Len( AllTrim( oGrid:GetValue( 'TS7_ANO' ) ) ) != 4
		Help( NIL, NIL, NIL, '', STR0009, 1, 0 ) // 'Ano deverá conter 4 dígitos!'
		lRet := .F.
	Endif

	If lRet .And. oGrid:GetValue( 'TS7_ANO' ) == '0000'
		Help( NIL, NIL, NIL, '', STR0010, 1, 0 ) // 'Ano não poderá ser igual a 0000!'
		lRet := .F.
	Endif

	If lRet .And. Val( oGrid:GetValue( 'TS7_ANO' ) ) > ( Year( dDATABASE ) + 1 )
		Help( NIL, NIL, NIL, '', STR0011 + AllTrim( Str( ( Year( dDATABASE ) + 1 ) ) ) + '!', 1, 0 ) // 'Ano informado não poderá ser maior que '###
		lRet := .F.
	Endif

Return lRet

//-----------------------------------------------------------------
/*/{Protheus.doc} A515LINOK
Consiste a existência do código de ano na Grid

@type function

@author karina.felicetti
@since 01/06/2020

@return lógico, define se o campo Ano foi informado corretamente
/*/
//-----------------------------------------------------------------
Static Function A515LINOK( oGrid )

	Local lRet    := .T.
	Local cModelo := oGrid:GetValue( 'TS7_ANO' ) // Busca o valor do campo

	If !oGrid:IsDeleted() .And. Empty( cModelo )
		Help( ' ', 1 , 'OBRIGAT' )
		lRet := .F.
	Endif

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA515VAL
Realiza validação do campo. ( X3_VALID )

@author  Eduardo Mussi
@since   15/07/2020

@param   cField, Caracter, Campo a ser validado

@return  lRet, Define se a informação inserida está correta
/*/
//-------------------------------------------------------------------
Function MNTA515VAL( cField )

	Local lRet   := .T.
	Local oModel := FWModelActive()

	If cField == 'TS5_CODFAM'

		If !Empty( oModel:GetValue( 'MNTA515_TS5', 'TS5_CODFAM' ) )

			// -------------------------------------------------------
			// Verifica se a familia existe na ST6( FAMILIA )
			// -------------------------------------------------------
			If !ExistCpo( 'ST6', oModel:GetValue( 'MNTA515_TS5', 'TS5_CODFAM' ) )

				lRet := .F.

			// ---------------------------------------------
			// Verifica chave única da tabela TS5
			// TS5_FILIAL + TS5_CODFAM + TS5_TIPMOD + TS5_DOCTO
			// ---------------------------------------------
			ElseIf  !Empty( oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) ) .And. ;
					!Empty( oModel:GetValue( 'MNTA515_TS5', 'TS5_DOCTO' ) ) .And. ;
					!ExistChav( 'TS5', oModel:GetValue( 'MNTA515_TS5', 'TS5_CODFAM' ) + ;
								oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) + ;
								oModel:GetValue( 'MNTA515_TS5', 'TS5_DOCTO'  ), 1 )

				lRet := .F.

			EndIf

		EndIf

	ElseIf cField == 'TS5_TIPMOD'

		If !Empty( oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) )

			// -------------------------------------------------------
			// Verifica se tipo modelo existe na TQR ou é '*' ( TODOS )
			// -------------------------------------------------------
			If Trim( oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) ) != '*' .And.;
				!ExistCpo( 'TQR', oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) )

					lRet := .F.

			// ---------------------------------------------
			// Verifica chave única da tabela TS5
			// TS5_FILIAL + TS5_CODFAM + TS5_TIPMOD + TS5_DOCTO
			// ---------------------------------------------
			ElseIf  !Empty( oModel:GetValue( 'MNTA515_TS5', 'TS5_CODFAM' ) ) .And. ;
					!Empty( oModel:GetValue( 'MNTA515_TS5', 'TS5_DOCTO' ) ) .And. ;
					!ExistChav( 'TS5', oModel:GetValue( 'MNTA515_TS5', 'TS5_CODFAM' ) + ;
								oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) + ;
								oModel:GetValue( 'MNTA515_TS5', 'TS5_DOCTO'  ), 1 )

				lRet := .F.

			EndIf


		EndIf

	ElseIf cField == 'TS5_DOCTO'

		If !Empty( oModel:GetValue( 'MNTA515_TS5', 'TS5_DOCTO' ) )

			// -------------------------------------------------------------
			// Verifica se o documento existe na TS0( Documento )
			// -------------------------------------------------------------
			If !ExistCpo( 'TS0', oModel:GetValue( 'MNTA515_TS5', 'TS5_DOCTO' ) )

				lRet := .F.

			// -------------------------------------------------------------
			// Verifica chave única da tabela TS5
			// TS5_FILIAL + TS5_CODFAM + TS5_TIPMOD + TS5_DOCTO
			// -------------------------------------------------------------
			ElseIf  !Empty( oModel:GetValue( 'MNTA515_TS5', 'TS5_CODFAM' ) ) .And. ;
					!Empty( oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) ) .And. ;
					!ExistChav( 'TS5', oModel:GetValue( 'MNTA515_TS5', 'TS5_CODFAM' ) + ;
									oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) + ;
									oModel:GetValue( 'MNTA515_TS5', 'TS5_DOCTO'  ), 1 )

				lRet := .F.

			EndIf


		EndIf

	EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA515GAT
Busca informação a ser gatilhada no campo. ( X7_REGRA )

@author  Eduardo Mussi
@since   15/07/2020

@param   cDominio  , Caracter, Campo que está chamando o gatilho
@param   cContraDom, Caracter, Campo de contra dominio que receberá o valor de retorno do processo.

@return  Indefinido, Retorna valor a ser carregado no campo passado pelo cDominio
/*/
//-------------------------------------------------------------------
Function MNTA515GAT( cDominio, cContraDom )

	Local oModel  := FWModelActive()
	Local xReturn

	If cDominio == 'TS5_CODFAM'

		If cContraDom == 'TS5_NOMFAM'

			//--------------------------------------------------------------------
			// Busca código informado no campo TS5_CODFAM na tabela ST6( Familia )
			// retornando sua descrição
			//--------------------------------------------------------------------
			xReturn := SubStr( Posicione( 'ST6', 1, xFilial( 'ST6' ) + oModel:GetValue( 'MNTA515_TS5', 'TS5_CODFAM' ), 'T6_NOME' ), 1, GetSx3Cache( 'TS5_NOMFAM', 'X3_TAMANHO' ) )

		EndIf

	ElseIf cDominio == 'TS5_TIPMOD'

		If cContraDom == 'TS5_NOMMOD'

			//--------------------------------------------------------------------
			// Busca na TQR pelo código informado no campo TS5_TIPMOD considerando
			// também o tipo modelo '*' ( TODOS )
			//--------------------------------------------------------------------
			xReturn := SubStr( MNTDesTpMd( oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) ), 1, GetSx3Cache( 'TS5_NOMMOD', 'X3_TAMANHO' ) )

		EndIf

	ElseIf cDominio == 'TS5_DOCTO'

		If cContraDom == 'TS5_NOMDOC'

			//--------------------------------------------------------------------
			// Busca código informado no campo TS5_DOCTO na tabela TS0( Documentos )
			// retornando sua descrição
			//--------------------------------------------------------------------
			xReturn := SubStr( Posicione( 'TS0', 1, xFilial( 'TS0' ) + oModel:GetValue( 'MNTA515_TS5', 'TS5_DOCTO' ), 'TS0_NOMDOC' ), 1, GetSx3Cache( 'TS5_NOMDOC', 'X3_TAMANHO' ) )

		EndIf

	EndIf

Return xReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA515REL
Busca informação a ser apresentada no campo. ( X3_RELACAO )

@author  Eduardo Mussi
@since   15/07/2020

@param   cField, Caracter, Campo a ser carregado informações

@return  cDesc, Retorna valor a ser carregado no campo passado pelo cField
/*/
//-------------------------------------------------------------------
Function MNTA515REL( cField )

	Local cDesc  := ''
	Local oModel := FWModelActive()

	If cField == 'TS5_NOMFAM'

		//--------------------------------------------------------------------
		// Em processo de alteração busca código informado no campo TS5_CODFAM
		// na tabela  ST6( Familia ) retornando sua descrição.
		//--------------------------------------------------------------------
		cDesc := SubStr( Posicione( 'ST6', 1, xFilial( 'ST6' ) + oModel:GetValue( 'MNTA515_TS5', 'TS5_CODFAM' ), 'T6_NOME' ), 1, GetSx3Cache( 'TS5_NOMFAM', 'X3_TAMANHO' ) )

	ElseIf cField == 'TS5_NOMMOD'

		//--------------------------------------------------------------------
		// Em processo de alteração busca na TQR pelo código informado no campo
		// TS5_TIPMOD considerando também o tipo modelo '*' ( TODOS )
		//--------------------------------------------------------------------
		cDesc := SubStr( MNTDesTpMd( oModel:GetValue( 'MNTA515_TS5', 'TS5_TIPMOD' ) ), 1, GetSx3Cache( 'TS5_NOMMOD', 'X3_TAMANHO' ) )

	ElseIf cField == 'TS5_NOMDOC'

		//--------------------------------------------------------------------
		// Em processo de alteração busca código informado no campo TS5_DOCTO
		// na tabela TS0( Documentos ) retornando sua descrição.
		//--------------------------------------------------------------------
		cDesc := SubStr( Posicione( 'TS0', 1, xFilial( 'TS0' ) + oModel:GetValue( 'MNTA515_TS5', 'TS5_DOCTO' ), 'TS0_NOMDOC' ), 1, GetSx3Cache( 'TS5_NOMDOC', 'X3_TAMANHO' ) )

	EndIf

Return cDesc

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA515WHE
Define se o campo será aberto para edição ( X3_WHEN )

@obs Essa função só é utilizada nos campos TS5_CODFAM/TS5_TIPMOD/TS5_DOCTO

@type   Function

@author Eduardo Mussi
@since  26/08/2020

@Param  cField, Caracter, Campo da TS5 a ser verificado.

@return Lógico, Define se o campo estará aberto para edição.
/*/
//-------------------------------------------------------------------
Function MNTA515WHE( cField )

	Local oModel := FWModelActive()
	//---------------------------------------------------------------------------
	// Os campos só devem ser abertos no processo de inclusão.
	//---------------------------------------------------------------------------

Return oModel:GetOperation() == MODEL_OPERATION_INSERT

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA515REL
Busca informação a ser apresentada no campo . ( X3_INIBRW )

@author  Eduardo Mussi
@since   15/07/2020

@param   cField, Caracter, Campo a ser carregado informações

@return  cDesc, Retorna valor a ser carregado no campo passado pelo cField
/*/
//-------------------------------------------------------------------
Function MNTA515BRW( cField )

	Local cDesc := ''

	If cField == 'TS5_NOMFAM'

		//--------------------------------------------------------------------
		// Busca código informado no campo TS5_CODFAM na tabela ST6( Familia )
		// retornando sua descrição
		//--------------------------------------------------------------------
		cDesc := SubStr( Posicione( 'ST6', 1, xFilial( 'ST6' ) + TS5->TS5_CODFAM, 'T6_NOME' ), 1, GetSx3Cache( 'TS5_NOMFAM', 'X3_TAMANHO' ) )

	ElseIf cField == 'TS5_NOMMOD'

		//--------------------------------------------------------------------
		// Busca na TQR pelo código informado no campo TS5_TIPMOD considerando
		// também o tipo modelo '*' ( TODOS )
		//--------------------------------------------------------------------
		cDesc := SubStr( MNTDesTpMd( TS5->TS5_TIPMOD ), 1, GetSx3Cache( 'TS5_NOMMOD', 'X3_TAMANHO' ) )

	ElseIf cField == 'TS5_NOMDOC'

		//--------------------------------------------------------------------
		// Busca código informado no campo TS5_DOCTO na tabela TS0( Documentos )
		// retornando sua descrição
		//--------------------------------------------------------------------
		cDesc := SubStr( Posicione( 'TS0', 1, xFilial( 'TS0' ) + TS5->TS5_DOCTO, 'TS0_NOMDOC' ), 1, GetSx3Cache( 'TS5_NOMDOC', 'X3_TAMANHO' ) )

	EndIf

Return cDesc
