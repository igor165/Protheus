#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV030    ³ Autor ³ Marcos Wagner Junior  ³ Data ³01/03/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Quantidade total motoristas proprios/nao demitidos no periodo ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data    - Data inicio                                     ³±±
±±³          ³ Ate_Data   - Ate data                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nQtdLt  - Litros total                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV030(De_Data,Ate_Data)
Local aAreaOLD   := GetArea(),nQtdMot := 0

// Variáveis de Histórico de Indicadores
Local lMV_HIST := NGI6MVHIST()
Local aParams := {}
Local cCodIndic := "MNTV030"
Local nResult := 0

// Armazena os Parâmetros
If lMV_HIST
	aParams := {}
	aAdd(aParams, {"DE_DATA" , De_Data})
	aAdd(aParams, {"ATE_DATA", Ate_Data})
	NGI6PREPPA(aParams, cCodIndic)
EndIf

If ValType(De_Data) != "D" .or. ValType(Ate_Data) != "D"
	NGI6PREPVA(cCodIndic, nResult)
	Return nResult
Endif

cAliasQry := GetNextAlias()
// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT COUNT(*) AS QTDMOT "
EndIf
cQuery += " FROM " + RetSqlName("DA4") + " DA4"
If GetMV("MV_NGMNTRH") = "S"
	cQuery += ","+RetSqlName("SRA") + " SRA "
EndIf
cQuery += " WHERE DA4.DA4_TIPMOT =  '1'"
If GetMV("MV_NGMNTRH") = "S"
	cQuery += " AND DA4.DA4_MAT    =  SRA.RA_MAT "
	If ValType(De_Data) == "D" .AND. ValType(Ate_Data) == "D"
		cQuery += " AND ((SRA.RA_DEMISSA >= '"+Dtos(De_Data)+"' AND SRA.RA_DEMISSA <= '"+Dtos(Ate_Data)+"')"
		cQuery += " OR  (SRA.RA_DEMISSA  >= '"+Dtos(Ate_Data)+"' AND SRA.RA_ADMISSA <= '"+Dtos(Ate_Data)+"')"
		cQuery += " OR  (SRA.RA_ADMISSA <= '"+Dtos(Ate_Data)+"' AND SRA.RA_DEMISSA  = ' ' )) "
	ElseIf ValType(De_Data) == "D"
		cQuery += " AND ((SRA.RA_DEMISSA >= '"+Dtos(De_Data)+"')  "
		cQuery += " OR   (SRA.RA_DEMISSA = ' ' AND SRA.RA_ADMISSA >= '"+Dtos(De_Data)+"')) "
	ElseIf ValType(Ate_Data) == "D"
		cQuery += " AND ((SRA.RA_DEMISSA  >= '"+Dtos(Ate_Data)+"' AND SRA.RA_ADMISSA <= '"+Dtos(Ate_Data)+"')"
		cQuery += " OR  (SRA.RA_ADMISSA <= '"+Dtos(Ate_Data)+"' AND SRA.RA_DEMISSA  = ' ' )) "
	Endif
	If NGSX2MODO("SRA") == NGSX2MODO("DA4")
		cQuery += " AND SRA.RA_FILIAL = DA4.DA4_FILIAL "
		cQuery += " AND DA4.DA4_FILIAL = '"+xFilial("DA4")+"' "
	Else
		cQuery += " AND SRA.RA_FILIAL  = '"+xFilial("SRA")+"'"
		cQuery += " AND DA4.DA4_FILIAL = '"+xFilial("DA4")+"'"
	Endif
EndIf
cQuery += " AND   DA4.DA4_BLQMOT =  '2' "
cQuery += " AND   DA4.D_E_L_E_T_ <> '*' "
If GetMV("MV_NGMNTRH") = "S"
	cQuery += " AND   SRA.D_E_L_E_T_ <> '*' "
EndIf
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

dbSelectArea(cAliasQry)
dbGoTop()
If lMV_HIST
	While !Eof()
		nQtdMot++
		dbSkip()
	End
Else
	If !Eof()
	   nQtdMot := (cAliasQry)->QTDMOT
	End
EndIf
(cAliasQry)->(dbCloseArea())

// RESULTADO
nResult := nQtdMot
NGI6PREPVA(cCodIndic, nResult)

RestArea(aAreaOLD)
Return nResult