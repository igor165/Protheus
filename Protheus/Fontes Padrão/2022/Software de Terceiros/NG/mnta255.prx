#INCLUDE "mnta255.ch"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA255  ³ Autor ³ Elisangela Costa      ³ Data ³ 20/12/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Reabre uma O.S. de acompanhamento                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA255() 

Local cORDEMTQA, cPLANOTQA
Local lOk := .T.

If "A" $ Alltrim(SuperGetMv("MV_NGLANEX",.F.,"-1"))
	MsgStop(STR0004,STR0003)//"Parâmetro MV_NGLANEX restringe lançamentos do tipo Contador."
	lOk := .F.
EndIf

If lOk .And. Pergunte("MNT255",.T.)
   cORDEMTQA := MV_PAR01
   
   dbSelectArea("TQA")
   dbsetorder(01)
   If dbSeek(xFilial("TQA")+cORDEMTQA) 
      
      cPLANOTQA := TQA->TQA_PLANO
      
      //Exclui os lancamentos de contador 1 e 2
      If !Empty(TQA->TQA_HORAC1) .And. TQA->TQA_POSCON > 0 
         MNT470EXCO(TQA->TQA_CODBEM,TQA->TQA_DTLEI1,TQA->TQA_HORAC1,1)
      EndIf 
        
      If !Empty(TQA->TQA_HORAC2) .And. TQA->TQA_POSCO2 > 0 
         MNT470EXCO(TQA->TQA_CODBEM,TQA->TQA_DTLEI2,TQA->TQA_HORAC2,2)
      EndIf 
         
      //Reabre o plano
      dbSelectArea("STO")
      dbSetOrder(01)  
      If dbSeek(xFilial("STO")+cPLANOTQA) .And. STO->TO_TERMINO == "S"
         RecLock("STO",.F.)
         STO->TO_TERMINO := "N"
         STO->TO_USUARIO := " "
         STO->(MsUnlock())
      EndIf 
                  
      dbSelectArea("TQA")
      RecLock("TQA",.F.)
      TQA->TQA_SITUAC := "L"  
      TQA->TQA_TERMIN := "N"
      TQA->TQA_POSCON := 0
      TQA->TQA_DTLEI1 := CTOD("  /  /  ")
      TQA->TQA_HORAC1 := "  :  "
      TQA->TQA_POSCO2 := 0
      TQA->TQA_DTLEI2 := CTOD("  /  /  ")
      TQA->TQA_HORAC2 := "  :  "
      TQA->TQA_RETORN := "N"
      TQA->(MsUnlock())
   EndIf    
EndIf

Return Nil  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT255CHKP³ Autor ³ Elisangela Costa      ³ Data ³ 20/12/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida o MV_PAR01                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA255                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT255CHKP() 

If !ExistCpo("TQA",MV_PAR01)
   Return .F.
EndIf 

dbSelectArea("TQA")
dbSetOrder(01)
If dbSeek(xFilial("TQA")+MV_PAR01)
   If TQA->TQA_RETORN <> "S"
      MsgStop(STR0002) //"Informe uma ordem de acompanhamento finalizada."
      Return .F. 
   EndIf    
EndIf  

Return .T.