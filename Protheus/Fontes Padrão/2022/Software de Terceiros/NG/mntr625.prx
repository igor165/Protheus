#INCLUDE "mntr625.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 2 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR625  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio Onde-Se-Usa Especialidade                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR625()

	//---------------------------------------------------------------------
	// Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	//---------------------------------------------------------------------
	Local aNGBEGINPRM	:= NGBEGINPRM(_nVERSAO)

	Local cString		:= "STG"
	Local wnrel			:= "MNTR625"
	Local cDesc1		:= STR0001 //"Relatorio das manutencoes em que a especialidade selecionada e planeja-"
	Local cDesc2		:= STR0002 //"da para utilizacao. Atraves da opcao parametros o usuario podera fazer "
	Local cDesc3		:= STR0003 //"selecao por Centro de Custo e Familia de Bens"

	Private Tamanho		:= "M"
	Private aReturn		:= { STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey	:= 0
	Private cPerg		:= "MNT625"
	Private titulo		:= STR0006 //"Onde-Se-Usa Especialidade"
	Private nTipo		:= 0
	Private NomeProg 	:= "MNTR625"
	Private cabec1,cabec2

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     // Especialidade a procurar                     ³
	//³ mv_par02     // De  Centro de Custos                         ³
	//³ mv_par03     // Ate Centro de Custos                         ³
	//³ mv_par04     // De  Familia de Bens                          ³
	//³ mv_par05     // Ate Familia de Bens                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte(cPerg,.F.)

	//---------------------------------------------------------------------
	// Envia controle para a funcao SETPRINT
	//---------------------------------------------------------------------
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		Return
	Endif
	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R625Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

	//---------------------------------------------------------------------
	// Devolve variaveis armazenadas (NGRIGHTCLICK)
	//---------------------------------------------------------------------
	NGRETURNPRM(aNGBEGINPRM)
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R625Imp  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR625                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R625Imp(lEnd,wnRel,titulo,tamanho)

	Local aINDR625		:= {}
	Local cRodaTxt		:= ""	
	Local cChave		:= Space(TAMSX3("T9_CODBEM")[1])
	Local cTRB625		:= GetNextAlias()
	Local nCntImpr		:= 0
	Local oARQTR625

	//---------------------------------------------------------------------
	// Contadores de linha e pagina
	//---------------------------------------------------------------------
	Private li := 80 ,m_pag := 1

	nTipo  := IIF(aReturn[4]==1,15,18) // Verifica se deve comprimir ou nao

	//---------------------------------------------------------------------
	// Monta os Cabecalhos
	//---------------------------------------------------------------------
	cabec1 := STR0007 //"Bem              Descrição                         Serviço Nome                          Sequencia   Qtd    Consumo Unidade Reserva"
	cabec2 := STR0008 //"Bem Pai          Descrição                      Tarefa Descricao                            C. Custo             C.Trab     Familia"

	/*/
	1         1         1         1
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	______________________________________________________________________________________________________________________________________
	Bem              Descrição                         Serviço Nome                          Sequencia   Qtd    Consumo Unidade Reserva
	Bem Pai          Descrição                      Tarefa Descricao                            C. Custo             C.Trab     Familia
	______________________________________________________________________________________________________________________________________
	Especialidade.:  xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	**************************************************************************************************************************************

	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999         xxx    xx:xx   xxx     xxx SUBST
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxx     xxxxxx

	*/

	aDBFR625 := {{"CODBEM" , "C", 16, 0},;
				 {"SERVICO", "C", 06, 0},;
				 {"SEQRELA", "C", 03, 0},;
				 {"SEQUEN" , "C", 03, 0},;
				 {"TAREFA" , "C", 06, 0},;
				 {"TIPOREG", "C", 01, 0},;
				 {"CODIGO" , "C", 15, 0},;
				 {"QUANTID", "N", 09, 0},;
				 {"QUANREC", "N", 03, 0},;
				 {"RESERVA", "C", 01, 0},; 
				 {"UNIDADE", "C", 02, 0}}   

	//Indice Tabela Temporária
	aINDR625  := {{"CODIGO","CODBEM","SERVICO","SEQRELA","SEQUEN","TAREFA"}}
		
	//Criação Tabela Temporária
	oARQTR625 := NGFwTmpTbl(cTRB625, aDBFR625, aINDR625)                                                                                      

	DbSelectArea("STG")
	DbSetOrder(02)
	DbSeek(xFilial("STG")+"E"+MV_PAR01)
	SetRegua(LastRec())
	While !Eof() .And. STG->TG_FILIAL == xFilial("STG") .And.;
	STG->TG_TIPOREG == "E" .And. SUBSTR(STG->TG_CODIGO,1,3) == MV_PAR01

		dbSelectArea(cTRB625)
		RecLock(cTRB625,.T.) 
		(cTRB625)->CODBEM		:= STG->TG_CODBEM
		(cTRB625)->SERVICO	:= STG->TG_SERVICO
		(cTRB625)->SEQRELA	:= STG->TG_SEQRELA
		(cTRB625)->TAREFA		:= STG->TG_TAREFA
		(cTRB625)->TIPOREG	:= STG->TG_TIPOREG
		(cTRB625)->CODIGO		:= STG->TG_CODIGO
		(cTRB625)->QUANTID	:= STG->TG_QUANTID
		(cTRB625)->QUANREC	:= STG->TG_QUANREC
		(cTRB625)->UNIDADE	:= STG->TG_UNIDADE
		(cTRB625)->RESERVA	:= STG->TG_RESERVA

		//Verifica a existe a sequencia
		dbSelectArea("ST5")
		dbSetorder(1)
		If dbSeek( xFilial("ST5") + STG->TG_SERVICO+STG->TG_SEQRELA+STG->TG_TAREFA)
			(cTRB625)->SEQUEN := STR(ST5->T5_SEQUENC,TAMSX3("T5_SEQUENC")[1])
		Else
			(cTRB625)->SEQUEN := STR(0,TAMSX3("T5_SEQUENC")[1])
		EndIf

		(cTRB625)->(MsUnlock())
		dbSelectarea("STG")
		dbSkip()

	End While

	DbSelectArea("ST0")
	DbSetOrder(01)
	DbSeek(xFilial("ST0")+MV_PAR01)

	DbSelectArea(CTRB625)
	DbSetOrder(01)
	DbGoTop()
	SetRegua(LastRec())
	If !EoF()
		While !EoF()

			IncRegua()
			If Li > 58
				Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
				@ Li,000 Psay STR0010 //"Especialidade.: "
				@ Li,017 Psay (cTRB625)->CODIGO
				@ Li,034 Psay ST0->T0_NOME
				SomaLinha()
				@ Li,000 Psay "************************************************************************************************************************************"
				SomaLinha()
			EndIf

			//---------------------------------------------------------------------
			// Selecao dos registros conforme os parametros
			//---------------------------------------------------------------------
			DbSelectArea("ST9")
			DbSetOrder(01)
			DbSeek(xFilial("ST9")+(cTRB625)->CODBEM)
			DbSelectArea("STG")

			If ST9->T9_CCUSTO < MV_PAR02 .Or. ST9->T9_CCUSTO > MV_PAR03
				Dbskip()
				Loop
			EndIf

			If ST9->T9_CODFAMI < MV_PAR04 .Or. ST9->T9_CODFAMI > MV_PAR05
				Dbskip()
				Loop
			EndIf

			//---------------------------------------------------------------------
			// Impressao da Primeira Linha da O.S.
			//---------------------------------------------------------------------
			DbSelectArea("ST4")
			DbSetOrder(01)
			DbSeek(xFilial("ST4")+(cTRB625)->SERVICO)

			@ Li,000 Psay (cTRB625)->CODBEM
			@ Li,017 Psay Substr(ST9->T9_NOME,1,33)
			@ Li,051 Psay (cTRB625)->SERVICO
			@ Li,059 Psay subst(ST4->T4_NOME,1,29)
			@ Li,089 Psay (cTRB625)->SEQRELA Picture "@!"
			@ Li,099 Psay (cTRB625)->QUANREC Picture "@E 999"
			@ Li,103+(10-LEN(NtoH((cTRB625)->QUANTID))) Psay NtoH((cTRB625)->QUANTID)
			@ Li,116 Psay (cTRB625)->UNIDADE
			If (cTRB625)->RESERVA = "S"
				@ Li,124 Psay STR0011 //"Sim"
			Else
				@ Li,124 Psay STR0012 //"Nao"
			EndIf
			SomaLinha()

			//---------------------------------------------------------------------
			// Verifica se o Bem tem Bem Pai
			//---------------------------------------------------------------------
			DbSelectArea("STC")
			DbSetOrder(03)
			cChave := (cTRB625)->CODBEM
			While .T.
				DbSeek(xFilial('STC')+cChave)
				If Found()
					cChave := STC->TC_CODBEM
					Dbskip()
					Loop
				EndIf
				Exit
			End

			//---------------------------------------------------------------------
			// Impressao da Segunda  Linha
			//---------------------------------------------------------------------
			If (cTRB625)->CODBEM != cChave
				DbSelectArea("ST9")
				DbSetOrder(01)
				DbSeek(xFilial("ST9") + cChave)
				@ Li,000 Psay cChave
				@ Li,017 Psay Substr(ST9->T9_NOME,1,30)
			EndIf

			@ Li,048 Psay (cTRB625)->TAREFA
			If (cTRB625)->TAREFA = "0     "
				@ Li,055 Psay STR0013 //"Sem Especificacao De Tarefa"
			Else
				@ Li,055 Psay Substr(NGSEEK("ST5",(CTRB625)->CODBEM+(CTRB625)->SERVICO+(CTRB625)->SEQRELA+(CTRB625)->TAREFA,1,"T5_DESCRIC"),1,35)
			EndIf

			DbSelectArea("ST9")
			DbSeek(xFilial('ST9')+(cTRB625)->CODBEM)
			@ Li,092 Psay ST9->T9_CCUSTO
			@ Li,113 Psay ST9->T9_CENTRAB
			@ Li,124 Psay ST9->T9_CODFAMI
			SomaLinha()
			SomaLinha()
			DbSelectArea(CTRB625)
			DbSkip()
		End While
	Else
		MsgInfo(STR0014,STR0015) //"Não existem dados para montar o relatório."###"ATENÇÃO"
		oARQTR625:Delete()
		Return .F.
	Endif

	Roda(nCntImpr,cRodaTxt,Tamanho)

	//Deleta tabela temporária.
	oARQTR625:Delete()

	//---------------------------------------------------------------------
	// Devolve a condicao original do arquivo principal
	//---------------------------------------------------------------------
	RetIndex("ST0")
	RetIndex("STG")
	RetIndex("ST9")
	RetIndex("ST4")
	RetIndex("STC")
	RetIndex("ST5")

	Set Filter To
	Set Device To Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf

	MS_FLUSH()

Return Nil

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaLinha()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR625                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Somalinha()

	Li++
	If Li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		@ Li,000 Psay STR0010 //"Especialidade.: "
		@ Li,017 Psay STG->TG_CODIGO
		@ Li,034 Psay ST0->T0_NOME
		SomaLinha()
		@ Li,000 Psay "************************************************************************************************************************************"
		SomaLinha()
	EndIf

Return .T.