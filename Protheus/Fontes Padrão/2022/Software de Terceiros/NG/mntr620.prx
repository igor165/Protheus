#INCLUDE "mntr620.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 2 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR620  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio Onde-Se-Usa Produto                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR620()

	Local aNGBEGINPRM	:= NGBEGINPRM(_nVERSAO)
	Local cString		:= "STG"
	Local wnrel			:= "MNTR620"
	Local cDesc1		:= STR0001 //"Relatorio das manutencoes em que o produto selecionado e planejado para"
	Local cDesc2		:= STR0002 //"utilizacao. Atraves da opcao parametros o usuario podera fazer selecao "
	Local cDesc3		:= STR0003 //"Centro de Custo e Familia de Bens"

	Private Tamanho		:= "M"
	Private aReturn		:= { STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey	:= 0
	Private cPerg		:= "MNT620"
	Private titulo		:= STR0006 //"Onde-Se-Usa Produto"
	Private ntipo		:= 0
	Private nomeprog	:= "MNTR620"
	Private cabec1,cabec2

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     // Produto a procurar                           ³
	//³ mv_par02     // De  Centro de Custos                         ³
	//³ mv_par03     // Ate Centro de Custos                         ³
	//³ mv_par04     // De  Familia de Bens                          ³
	//³ mv_par05     // Ate Familia de Bens                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte(cPerg,.F.)

	//---------------------------------------------------------------------
	// Envia controle para a funcao SETPRINT
	//---------------------------------------------------------------------
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

	If nLastKey = 27
		Set Filter To
		NGRETURNPRM(aNGBEGINPRM)
		Return
	Endif

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R620Imp(@lEnd,wnRel,titulo,tamanho)},titulo)
	NGRETURNPRM(aNGBEGINPRM)
Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R620Imp  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR620                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R620Imp(lEnd,wnRel,titulo,tamanho)

	Local aINDR620 	 := {}
	Local cRodaTxt	 := ""
	Local nCntImpr	 := 0
	Local cChave	 := Space(TAMSX3("T9_CODBEM")[1])
	Local cTRB620	 := GetNextAlias()
	Local oARQTR620
	
	//---------------------------------------------------------------------
	// Contadores de linha e pagina
	//---------------------------------------------------------------------
	Private li := 80 ,m_pag := 1

	//---------------------------------------------------------------------
	// Verifica se deve comprimir ou nao
	//---------------------------------------------------------------------
	nTipo  := If(aReturn[4]==1,15,18)

	//---------------------------------------------------------------------
	// Monta os Cabecalhos
	//---------------------------------------------------------------------

	cabec1 := STR0007 //"Bem              Nome do Bem                             Servic Nome                                Seq. Qtd    Consumo Uni Res Dest"
	cabec2 := STR0008 //"Pai              Nome do Bem Pai                     Tarefa Descricao                           C. Custo             C.Trab Famil"

	/*/                                                                                                  1         1         1         1
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	______________________________________________________________________________________________________________________________________
	Bem              Nome Do Bem                            Servic Nome                                 Seq. Qtd    Consumo Uni Res Dest
	Pai              Nome do Bem Pai                     Tarefa Descricao                           C. Custo             C.Trab Famil
	______________________________________________________________________________________________________________________________________
	Produto.: xxxxxxxxxxxxxxxiiiiiiiiiiiiiii xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	**************************************************************************************************************************************

	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  999 xxx xxx.xxx,xx xxx xxx SUBST
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxx

	*/                                                                                                                                                                          

	aDBFR620 := {{"CODBEM" , "C",16,0},;
				 {"SERVICO", "C",06,0},;
				 {"SEQRELA", "C",03,0},;
				 {"SEQUEN" , "C",03,0},;
				 {"TAREFA" , "C",06,0},;
				 {"TIPOREG", "C",01,0},;
				 {"CODIGO" , "C",15,0},;
				 {"QUANTID", "N",09,0},;
				 {"UNIDADE", "C",02,0},;
				 {"RESERVA", "C",01,0},; 
				 {"DESTINO", "C",01,0}}   

	//Indice Tabela Temporária
	aINDR620  := {{"CODBEM","SERVICO","SEQRELA","SEQUEN","TAREFA","TIPOREG","CODIGO"}}
	
	//Criação Tabela Temporária
	oARQTR620  := NGFwTmpTbl(cTRB620, aDBFR620, aINDR620) 

	DbSelectArea("STG")
	DbSetOrder(02)
	DbSeek(xFilial("STG")+"P"+MV_PAR01)
	While !Eof() .And. STG->TG_FILIAL == xFilial("STG") .And.;
	STG->TG_TIPOREG == "P" .And. STG->TG_CODIGO == MV_PAR01

		dbSelectArea(cTRB620)
		RecLock(cTRB620,.T.) 
		(cTRB620)->CODBEM	:= STG->TG_CODBEM
		(cTRB620)->SERVICO	:= STG->TG_SERVICO
		(cTRB620)->SEQRELA	:= STG->TG_SEQRELA
		(cTRB620)->TAREFA		:= STG->TG_TAREFA
		(cTRB620)->TIPOREG	:= STG->TG_TIPOREG
		(cTRB620)->CODIGO		:= STG->TG_CODIGO
		(cTRB620)->QUANTID	:= STG->TG_QUANTID
		(cTRB620)->UNIDADE	:= STG->TG_UNIDADE
		(cTRB620)->RESERVA	:= STG->TG_RESERVA
		(cTRB620)->DESTINO	:= STG->TG_DESTINO
		(cTRB620)->SEQUEN	:= STR(NGSEEK("ST5",STG->TG_CODBEM+STG->TG_SERVICO+STG->TG_SEQRELA+STG->TG_TAREFA,1,"T5_SEQUENC"), TAMSX3("T5_SEQUENC")[1])
		(cTRB620)->(MsUnlock())
		dbSelectarea("STG")
		dbSkip()

	End While

	DbSelectArea("SB1")
	DbSetOrder(01)
	DbSeek(xFilial("SB1")+MV_PAR01)

	DbSelectArea(cTRB620)
	DbSetOrder(01)
	DbGoTop()
	SetRegua(LastRec())
	If !EoF()
		While !EoF()

			IncRegua()
			If Li > 58
				Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
				@ Li,000 Psay STR0010 //"Produto.: "
				@ Li,010 Psay AllTrim((cTRB620)->CODIGO)+" "+SB1->B1_DESC
				SomaLinha()
				@ Li,000 Psay "************************************************************************************************************************************"
				SomaLinha()
			EndIf

			//---------------------------------------------------------------------
			// Selecao dos registros conforme os parametros
			//---------------------------------------------------------------------
			DbSelectArea("ST9")
			DbSetOrder(01)
			DbSeek(xFilial("ST9")+(cTRB620)->CODBEM)
			DbSelectArea(cTRB620)
			If ST9->T9_CCUSTO < MV_PAR02 .Or. ST9->T9_CCUSTO > MV_PAR03
				Dbskip()
				Loop
			EndIf

			If ST9->T9_CODFAMI < MV_PAR04 .Or. ST9->T9_CODFAMI > MV_PAR05
				Dbskip()
				Loop
			EndIf

			//---------------------------------------------------------------------
			// Impressao da Primeira Linha da O.S.
			//---------------------------------------------------------------------
			DbSelectArea("ST4")
			DbSetOrder(01)
			DbSeek(xFilial("ST4")+(cTRB620)->SERVICO)
			@ Li,000 Psay (cTRB620)->CODBEM
			@ Li,017 Psay Substr(ST9->T9_NOME,1,25)
			@ Li,052 Psay (cTRB620)->SERVICO
			@ Li,059 Psay Substr(ST4->T4_NOME,1,36)
			@ Li,096 Psay (cTRB620)->SEQRELA Picture "@!"
			@ Li,104 Psay (cTRB620)->QUANTID Picture "@E 999,999.99"
			@ Li,116 Psay (cTRB620)->UNIDADE
			If (cTRB620)->RESERVA = "S"
				@ Li,120 Psay STR0011 //"Sim"
			Else
				@ Li,120 Psay STR0012 //"Nao"
			EndIf

			Do Case
				Case (cTRB620)->DESTINO = "A"
				@ Li,125 Psay STR0013 //"Apoio"
				Case (cTRB620)->DESTINO = "T"
				@ Li,125 Psay STR0014 //"Troca"
				Case (cTRB620)->DESTINO = "S"
				@ Li,125 Psay STR0015 //"Subst"
			EndCase

			SomaLinha()

			//---------------------------------------------------------------------
			// Verifica se o Bem tem Bem Pai
			//---------------------------------------------------------------------
			DbSelectArea("STC")
			DbSetOrder(03)
			cChave := (cTRB620)->CODBEM

			While .T.
				DbSeek(xFilial("STC")+cChave)
				If Found()
					cChave := STC->TC_CODBEM
					Dbskip()
					Loop
				EndIf
				Exit
			End While

			//---------------------------------------------------------------------
			// Impressao da Segunda  Linha
			//---------------------------------------------------------------------

			If (cTRB620)->CODBEM != cChave
				DbSelectArea("ST9")
				DbSetOrder(01)
				DbSeek(xFilial("ST9") + cChave)
				@ Li,000 Psay cChave
				@ Li,017 Psay Substr(ST9->T9_NOME,1,25)
			EndIf

			@ Li,048 Psay (cTRB620)->TAREFA
			If (cTRB620)->TAREFA = "0     "
				@ Li,055 Psay STR0016 //"Sem Especificacao De Tarefa"
			Else
				@ Li,055 Psay Substr(NGSEEK("ST5",(cTRB620)->CODBEM+(cTRB620)->SERVICO+(cTRB620)->SEQRELA+(cTRB620)->TAREFA,1,"T5_DESCRIC"),1,35)
			EndIf

			DbSelectArea("ST9")
			DbSeek(xFilial("ST9")+STG->TG_CODBEM)
			@ Li,092 Psay ST9->T9_CCUSTO
			@ Li,112 Psay ST9->T9_CENTRAB
			@ Li,120 Psay ST9->T9_CODFAMI
			SomaLinha()
			SomaLinha()
			DbSelectArea(cTRB620)
			DbSkip()
		End While
	Else
		MsgInfo(STR0017,STR0018) //"Não existem dados para montar o relatório."###"ATENÇÃO"
		oARQTR620:Delete()
		Return .F.
	Endif
	Roda(nCntImpr,cRodaTxt,Tamanho)

	//Deleta tabela temporária.
	oARQTR620:Delete()

	//---------------------------------------------------------------------
	// Devolve a condicao original do arquivo principal
	//---------------------------------------------------------------------
	RetIndex("SB1")
	RetIndex("STG")
	RetIndex("ST9")
	RetIndex("ST4")
	RetIndex("STC")
	RetIndex("ST5")

	Set Filter To
	Set Device To Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf

	MS_FLUSH()

Return Nil
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaLinha()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR620                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Somalinha()

	Li++
	If Li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		@ Li,000 Psay STR0010 //"Produto.: "
		@ Li,010 Psay STG->TG_CODIGO
		@ Li,027 Psay SB1->B1_DESC
		SomaLinha()
		@ Li,000 Psay "************************************************************************************************************************************"
		SomaLinha()
	EndIf

Return .T.