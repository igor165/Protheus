#INCLUDE "mnta610.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA610  ³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³02/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Postos de Combustiveis                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA610()

	Local aNGBEGINPRM

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn(95)

		If GetRPORelease() >= '12.1.2210'
			MNTA613() // Cadastro de Postos em MVC
		Else
			aNGBEGINPRM := NGBEGINPRM()

			asMenu := NGRIGHTCLICK("MNTA610")

			//Define Array contendo as Rotinas a executar do programa
			//----------- Elementos contidos por dimensao ------------
			//1. Nome a aparecer no cabecalho
			//2. Nome da Rotina associada
			//3. Usado pela rotina
			//4. Tipo de Transa‡„o a ser efetuada
			//	1 - Pesquisa e Posiciona em um Banco de Dados
			//	2 - Simplesmente Mostra os Campos
			//	3 - Inclui registros no Bancos de Dados
			//	4 - Altera o registro corrente
			//	5 - Remove o registro corrente do Banco de Dados
			Private aRotina := MenuDef()

			//Define o cabecalho da tela de atualizacoes
			Private cCadastro  := OemtoAnsi(STR0036)   //"Cadastro de Postos"
			Private bNGGRAVA   := {|| }
			Private lCONVEN    := .T.
			Private lATIVO     := .F.
			Private lREATIV    := .F.
			Private aCHKDEL    := {}
			Private lUPDTransf := AliasInDic("TUI")

			//aCHKDEL array que verifica a INTEGRIDADE REFERENCIAL na exclusão do registro.
			//1 - Chave de pesquisa
			//2 - Alias de pesquisa
			//3 - Ordem de pesquisa
			aCHKDEL := {{'TQF->TQF_CODIGO+TQF->TQF_LOJA',"TQG",1},;
						{'TQF->TQF_CODIGO+TQF->TQF_LOJA',"TQH",1},;
						{'TQF->TQF_CODIGO+TQF->TQF_LOJA',"TQN",3}}

			//Endereca a funcao de BROWSE
			dbSelectArea("TQF")
			dbSetOrder(1)
			mBrowse( 6, 1,22,75,"TQF")

			//Devolve variaveis armazenadas (NGRIGHTCLICK)
			NGRETURNPRM(aNGBEGINPRM)

		EndIf

	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT610ATIV³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³03/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao campo Desativacao e Recadastramento              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610ATIV()

	If M->TQF_ATIVO == "2"
		lATIVO  := .T.
		lREATIV := .F.
	Else
		lATIVO  := .F.
		lREATIV := .T.
	EndIf

	If Altera
		If lREATIV
			If !MsgYesNo(STR0008,STR0010) // //"Deseja reativar o posto ?"###"ATENCAO" //"ATENÇÃO"
				M->TQF_ATIVO := "2"
				lATIVO       := .T.
				lREATIV      := .F.
			EndIf
		EndIf
	EndIf

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT610HIST³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³03/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao campo Desativacao e Recadastramento              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610HIST()

	dbSelectArea("TQO")
	dbSetOrder(1)
	If !dbSeek(xFILIAL("TQO")+M->TQF_CODIGO+M->TQF_LOJA+DTOS(M->TQF_DTDESA))
		RecLock("TQO",.T.)
		TQO->TQO_FILIAL	:= xFILIAL("TQO")
		TQO->TQO_CODPOS	:= M->TQF_CODIGO
		TQO->TQO_NREDUZ	:= M->TQF_NREDUZ
		TQO->TQO_LOJA		:= M->TQF_LOJA
		TQO->TQO_ATIVO	:= M->TQF_ATIVO
		TQO->TQO_DTCAD	:= M->TQF_DTCAD
		TQO->TQO_DTDESA	:= M->TQF_DTDESA
		TQO->TQO_DTUMOD	:= DDATABASE
		TQO->TQO_USUARI	:= SUBSTR(cUSUARIO,7,15)
		MsUnLock("TQO")
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc}  MNT610CAD
Funcao para montar uma tela para cadastro.

@author Evaldo Cevinski Jr.
@since 31/01/2006
@source MNTA610
@return
/*/
//---------------------------------------------------------------------
Function MNT610CAD( cAlias, nReg, nOpcX )

	Local OLDIND, cError, cKEY, lFIELD, bCampo, zy, x, i, lCLICK, lGRAVA
	Local LVar01     := 1
	Local nLinhas    := 0
	Local nInd       := 0
	Local nTamTot    := 0
	Local aNgHeader	 := {}
	Local GetList    := {}
	Local aArea      := GetArea()
	Local lDEL       := .T.
	Local lOk        := .F.
	Local dPROXPRE   := " "
	Local dPROXNEG   := " "
	Local cCampo     := ""
	Local cRelacao   := ""
	Local oSize      := FwDefSize():New(.T.)
	Local nLinIniTla := oSize:aWindSize[1] // Linha  inicial da tela
	Local nColIniTla := oSize:aWindSize[2] // Coluna inicial da tela
	Local nLinFimTla := oSize:aWindSize[3] // Linha  final   da tela
	Local nColFimTla := oSize:aWindSize[4] // Coluna final   da tela

	Private oDlg, oGet, oMenu
	Private aAlt       := {}
	Private aAlt2      := {}
	Private lAlterou   := .F.
	Private lChkDel    := .F.
	Private lChkDelTQG := .F.

	//Monta a entrada de dados do arquivo
	Private aTELA[0][0],aGETS[0],aHeader[0],Continua
	Private nUsado   := 0
	Private aSize    := MsAdvSize(,.F.,430)
	Private aObjects := {}

	Aadd(aObjects,{050,050,.T.,.T.})
	aInfo   := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.T.)
	bCampo  := {|nCPO| Field(nCPO) }

	lCLICK   := (TYPE("aSMENU") == "A")
	lCONVEN  := .T.
	If cAlias == "TQH"
		If lUPDTransf
			// Apenas Administradores podem incluir Negociação e Preço Negociado manualmente para Comboios
			If TQF->TQF_COMBOI == "1" .And. TQF->TQF_CONVEN = "7" .And. If(FindFunction("FWIsAdmin"), !FWIsAdmin(), (PswAdmin(, , RetCodUsr()) <> 0))
				If nOpcX == 3
					ShowHelpDlg(STR0010,{STR0104,STR0103},2,;  //"ATENÇÃO"##"Não é permitido incluir manualmente um Preço Negociado para um Comboio."##"Apenas Administradores possuem permissão para esta operação."
										{STR0112,""},1) //"Solicite a inclusão para um administrador."
					Return .F.
				ElseIf nOpcX == 4
					ShowHelpDlg(STR0010,{STR0116,STR0103},2,; //"ATENÇÃO"##""Não é permitido alterar manualmente um Preço Negociado para um Comboio."##"Apenas Administradores possuem permissão para esta operação."
										{STR0117,""},1) //"Solicite a alteração para um administrador."
					Return .F.
				ElseIf nOpcX == 5
					ShowHelpDlg(STR0010,{STR0118,STR0103},2,; //"ATENÇÃO"##"Não é permitido excluir manualmente um Preço Negociado para um Comboio."##"Apenas Administradores possuem permissão para esta operação."
										{STR0119,""},1) //"Solicite a exclusão para um administrador."
					Return .F.
				EndIf
			EndIf
		EndIf

		If nOPCX == 4 .Or. nOpcx == 5
			cPOSTO := TQH->TQH_CODPOS
			cLOJA  := TQH->TQH_LOJA
			cCOMB  := TQH->TQH_CODCOM
			dDATA  := TQH->TQH_DTNEG
			dbSetOrder(1)
			dbSeek(xFilial("TQH")+cPOSTO+cLOJA+cCOMB)
			While !Eof() .And. TQH->TQH_FILIAL == xFILIAL("TQH") .And. TQH->TQH_CODPOS == cPOSTO ;
						 .And. TQH->TQH_LOJA   == cLOJA 		 .And. TQH->TQH_CODCOM == cCOMB

				If dDATA < TQH->TQH_DTNEG
					dPROXPRE := TQH->TQH_DTNEG
					Exit
				Else
					dPROXPRE := dDataBase
				EndIf
				dbSelectArea("TQH")
				dbSkip()
			End
			RestArea(aArea)
			dbSelectArea("TQN")
			dbSetOrder(3)
			dbSeek(xFilial("TQN")+TQH->TQH_CODPOS+TQH->TQH_LOJA,.T.)
			While !Eof() .And. TQN->TQN_FILIAL == xFILIAL("TQN") .And. TQN->TQN_POSTO  == TQH->TQH_CODPOS;
						 .And. TQN->TQN_LOJA   == TQH->TQH_LOJA  .And. TQN->TQN_CODCOM == TQH->TQH_CODCOM

				If TQN->TQN_DTABAS >= TQH->TQH_DTNEG .And. TQN->TQN_DTABAS <= dPROXPRE .And. TQN->TQN_VALUNI == TQH->TQH_PRENEG

					dbSelectArea("TQM")
					dbSetOrder(1)
					dbSeek(xFilial("TQM")+TQH->TQH_CODCOM)
					lChkDel := .T.
					If nOpcx == 4 .Or. nOpcx == 5
						HELP(" ",1,STR0010,,STR0087+chr(10)+Chr(13)+STR0050 +; //"ATENÇÃO"### //"Não é possivel alterar ou excluir informaçoes da nego-"###"ciação de preço do combustível "
							 AllTrim(TQM->TQM_NOMCOM)+","+chr(10)+Chr(13)+STR0027,3,1) //"pois já existe abastecimento com esse preço."
						Return .F.
					EndIf
				EndIf
				dbSelectArea("TQN")
				dbSkip()
			End
		EndIf
	EndIf

	If cAlias == "TQG"
		If lUPDTransf
			// Apenas Administradores podem incluir Negociação e Preço Negociado manualmente para Comboios
			If TQF->TQF_COMBOI == "1" .And. TQF->TQF_CONVEN = "7" .And. If(FindFunction("FWIsAdmin"), !FWIsAdmin(), (PswAdmin(, , RetCodUsr()) <> 0))
				If nOpcX == 3
					ShowHelpDlg(STR0010,{STR0105,STR0103},2,;  //"ATENÇÃO"##"Não é permitido incluir manualmente uma Negociação para um Comboio." ### "Apenas Administradores possuem permissão para esta operação."
										{STR0112,""},1) //"Solicite a inclusão para um administrador."
					Return .F.
				ElseIf nOpcx == 4
					ShowHelpDlg(STR0010,{STR0120,STR0103},2,;  //"ATENÇÃO"##"Não é permitido alterar manualmente uma Negociação para um Comboio."##"Apenas Administradores possuem permissão para esta operação."
										{STR0117,""},1) //"Solicite a alteração para um administrador."
					Return .F.
				Elseif nOpcx == 5
					ShowHelpDlg(STR0010,{STR0121,STR0103},2,;//"ATENÇÃO"##"Não é permitido excluir manualmente uma Negociação para um Comboio."##"Apenas Administradores possuem permissão para esta operação."
										{STR0119,""},1) //"Solicite a exclusão para um administrador."
					Return .F.
				EndIf

			EndIf
		EndIf

		If nOpcx == 5 .Or. nOPCX == 4
			cPOSTO := TQG->TQG_CODPOS
			cLOJA  := TQG->TQG_LOJA
			dDATA  := TQG->TQG_DTNEG
			dbSetOrder(1)
			dbSeek(xFilial("TQG")+cPOSTO+cLOJA)
			While !Eof() .And. TQG->TQG_FILIAL == xFILIAL("TQG") .And. TQG->TQG_CODPOS == cPOSTO .And. TQG->TQG_LOJA == cLOJA
				If dDATA < TQG->TQG_DTNEG
					dPROXNEG := TQG->TQG_DTNEG
					Exit
				Else
					dPROXNEG := dDataBase
				EndIf
				dbSelectArea("TQG")
				dbSkip()
			End
			RestArea(aArea)
			dbSelectArea("TQN")
			dbSetOrder(3)
			dbSeek(xFilial("TQN")+TQG->TQG_CODPOS+TQG->TQG_LOJA,.T.)
			While !Eof() .And. TQN->TQN_FILIAL == xFILIAL("TQN") .And. TQN->TQN_POSTO  == TQG->TQG_CODPOS .And. TQN->TQN_LOJA   == TQG->TQG_LOJA

				If TQN->TQN_DTABAS >= TQG->TQG_DTNEG
					lChkDel := .T.
					If nOpcx == 4 .Or. nOpcx == 5
						HELP(" ",1,STR0010,,STR0084,3,1)  //"ATENÇÃO"###"Não é possível alterar ou excluir negociação com abastecimento existente."
						Return .F.
					EndIf
				EndIf
				dbSelectArea("TQN")
				dbSkip()
			End
		EndIf
	EndIf

	If TYPE("aCHKDEL") != "A"
		aCHKDEL := {}
	Endif

	lFIELD := (TYPE("aCHOICE") == "A")
	If lFIELD
		lFIELD := !empty(aCHOICE)
	Endif

	dbSelectArea(cALIAS)
	dbSetOrder(1)
	For i := 1 TO FCount()
		M->&(EVAL(bCampo,i)) := &(EVAL(bCampo,i))
		If nOPCX == 3
			IF     ValType(M->&(EVAL(bCampo,i))) == "C"
				M->&(EVAL(bCampo,i)) := SPACE(LEN(M->&(EVAL(bCampo,i))))
			ELSEIF ValType(M->&(EVAL(bCampo,i))) == "N"
				M->&(EVAL(bCampo,i)) := 0
			ELSEIF ValType(M->&(EVAL(bCampo,i))) == "D"
				M->&(EVAL(bCampo,i)) := cTod("  /  /  ")
			ELSEIF ValType(M->&(EVAL(bCampo,i))) == "L"
				M->&(EVAL(bCampo,i)) := .F.
			ENDIF
		Endif
	Next i

	If nOPCX == 3

		lATIVO := .F.
		aNgHeader := NGHeader(cAlias,,.F.)
		nTamTot := Len(aNgHeader)

		For nInd := 1 To nTamTot

			cCampo   := aNgHeader[nInd,2]
			cRelacao := Posicione("SX3",2,cCampo,"X3_RELACAO")

			If !Empty(cRelacao)
				xx  := "M->" + Trim(cCampo)
				yy  := Trim(cRelacao)
				&xx.:= &yy.
			Endif

		Next nInd

	Endif

	If cAlias == "TQF"
		dbSelectArea("TQF")
		For zy := 1 To FCount()
			nx := "m->" + FieldName(zy)
			If "_ATIVO"$Upper(nx)
				zx := "m->" + FieldName(zy)
				If &zx == "2"
					aAdd( aAlt, &zx.  )
				Else
					aAdd( aAlt2, &zx.  )
				EndIf
			EndIf
		Next zy
	EndIf

	nOpca := 0
	DEFINE MSDIALOG oDlg TITLE Ccadastro From nLinIniTla,nColIniTla TO nLinFimTla,nColFimTla Of oMainWnd PIXEL STYLE nOR(WS_VISIBLE,WS_POPUP)

		oEnchoice := If(lFIELD,Msmget():New(cALIAS, nReg, nopcx,,,,aCHOICE,aPosObj[1],,3),Msmget():New(cALIAS, nReg, nopcx,,,,,aPosObj[1],,3))

		If lCLICK
			NGPOPUP(aSMenu,@oMenu)
			oDlg:bRClicked           := { |o,x,y| oMenu:Activate(x,y,oDlg)}
			oEnchoice:oBox:bRClicked := { |o,x,y| oMenu:Activate(x,y,oDlg)}
		Endif

	Activate Dialog oDlg On Init (EnchoiceBar(oDlg,{|| lOk:=.T.,If(MNT610GRAV(nOpcx,cAlias),oDlg:End(),lOk := .F.)},{|| nOk:= 0,oDlg:End()})) Centered

	If ExistBlock("MNTA6101")
		ExecBlock("MNTA6101",.F.,.F.,{lOk,nOpcX})
	EndIf

	RestArea(aArea)

Return

//---------------------------------------------------------------------
/*/{Protheus.doc}  MNT610GRAV
Gravacao dos dados

@author Evaldo Cevinski Jr.
@since 03/01/2006

@return
/*/
//---------------------------------------------------------------------
Function MNT610GRAV(nOPCx,cAlias)

	Local ny, zy, zx, i, xx
	Local lInsert   := .F.
	Local aAreaSX3  := SX3->( GetArea() )
	Local cTitCNPJ  := ""
	Local cVarChave := ""

	If !Obrigatorio(aGets,aTela)
		Return .F.
	EndIf

	If nOPCx == 3 .Or. nOPCx == 4
		If cAlias = "TQG"
			If !Mnt610Obrig()
				Return .F.
			EndIf
		EndIf
	EndIf

	//Se Confirmou e a opcao incluir ou alterar
	If nOPCx == 3 .Or. nOPCx == 4
		If cAlias = "TQF"
			If nOPCx == 3
				If !ExistChav("TQF",M->TQF_CODIGO+M->TQF_LOJA)
					Return .F.
				EndIf
			EndIf

			// Valida o posto como Comboio
			If lUPDTransf
				If M->TQF_COMBOI <> "1" .And. Empty(M->TQF_CNPJ) .And. M->TQF_CONVEN <> "4"
					dbSelectArea("SX3")
					dbSetOrder(2)
					If dbSeek("TQF_CNPJ")
						cTitCNPJ := AllTrim( X3Titulo() )
					EndIf
					RestArea(aAreaSX3)
					Help(" ", 1, STR0010, Nil,; // "ATENÇÃO"
						 STR0106 + " '" + cTitCNPJ + "'.",; // "Para Postos, é obrigatório informar o campo"
						 1, 0)
					Return .F.
				EndIf

				If !MNTA610POS() .Or. !MNTA610LOJ()
					Return .F.
				EndIf

				If !MNTA610CMB() .Or. !MNTA610FRT(.T.)
					Return .F.
				EndIf
			EndIf

			If ExistBlock("MNTA6102")
				If !ExecBlock("MNTA6102",.F.,.F.)
					Return .F.
				EndIf
			EndIf

			If nOpcx == 4

				For zy := 1 To Len( aAlt )
					If M->TQF_ATIVO <> aAlt[zy]
						lAlterou := .T.
					EndIf
				Next zy

				For xx:= 1 To Len( aAlt2 )
					If M->TQF_ATIVO <> aAlt2[xx]
						If Empty(M->TQF_DTDESA)
							HELP(" ",1,STR0010,,STR0088+Chr(10)+Chr(13)+STR0054+Chr(10)+Chr(13)+STR0052,3,1) //"ATENÇÃO" //"Posto estava Ativo(Sim) e passou para Ativo"###"(Não), então é obrigatório informar a data de De-"###"sativação."
							Return .F.
						EndIf
					EndIf
				Next xx

				If lAlterou
					If Empty(M->TQF_DTREAT)
						Help(" ",1,STR0010,,STR0089+Chr(10)+Chr(13)+STR0090+Chr(10)+Chr(13)+STR0069,3,1) //"ATENÇÃO"### //"Posto estava Ativo(Não) e passou para Ativo(Sim)"###"novamente, então é obrigatório informar a data de" //"Reativação."
						Return .F.
					EndIf
					MsgRun(OemToAnsi(STR0013),OemToAnsi(STR0014),{|| MNT610HIST()}) //"Gravando Historico"###"Aguarde..."
					M->TQF_DTDESA := CtoD("  /  /  ")
				EndIf
				lInsert := .F.

			Endif
		EndIf

		If nOpcx == 3
			lInsert := .T.
		EndIf

		M->TQF_DTUMOD := DDATABASE
		M->TQF_USUARI := SUBSTR(cUSUARIO,7,15)

		dbSelectArea(cAlias)
		RecLock(cAlias,lInsert)

		For ny := 1 To FCOUNT()
			nx := "m->" + FieldName(ny)
			If "_FILIAL"$Upper(nx)
				&nx. := If(NGSX2MODO(cAlias) == NGSX2MODO("TQF") .AND. cAlias <> "TQF",TQF->TQF_FILIAL,xFilial(cAlias))
			Endif
			FieldPut(ny, &nx.)
		Next ny

		If cAlias == "TQG"
			TQG->TQG_CODPOS := TQF->TQF_CODIGO
			TQG->TQG_LOJA   := TQF->TQF_LOJA
			TQG->TQG_ORDENA := INVERTE(TQG->TQG_DTNEG)
		EndIf

		If cAlias == "TQH"
			TQH->TQH_CODPOS := TQF->TQF_CODIGO
			TQH->TQH_LOJA   := TQF->TQF_LOJA
			TQH->TQH_DTATUA := dDataBase
			TQH->TQH_USUARI := SUBSTR(cUSUARIO,7,15)
			TQH->TQH_ORDENA := INVERTE(TQH->TQH_DTNEG)
		EndIf
		MsUnLock(cAlias)

	Endif

	If nOPCx == 5

		//Verifica a Integridade Referencial
		lDEL := .T.
		For i := 1 to Len(aCHKDEL)
			dbSelectArea(aCHKDEL[i][2])
			OldInd := INDEXORD()
			dbSetOrder(aCHKDEL[i][3])
			cKEY := aCHKDEL[i][1]
			cVarChave := &(cKEY)

			If aCHKDEL[i][2] == "TQH"
				lDEL := fIntegPrec(cVarChave) //Verifica integridade referencial com o preço
			Else
				lDEL := !(dbSeek(xFilial(aCHKDEL[i][2]) + cVarChave))
			EndIf

			dbSetOrder(OldInd)
			If !lDEL
				If aCHKDEL[i][2] == "TQG"
					HELP(" ",1,STR0010,,STR0058+chr(10)+; //"ATENÇÃO"###"Exclusão não permitida. Ja existe "
					STR0057,3,1) //"negociação relacionada a este posto."
				ElseIf aCHKDEL[i][2] == "TQH"
					HELP(" ",1,STR0010,,STR0058+chr(10)+; //"ATENÇÃO"###"Exclusão não permitida. Ja existe "
					STR0056,3,1) //"preço relacionado a este posto."
				ElseIf aCHKDEL[i][2] == "TQN"
					HELP(" ",1,STR0010,,STR0058+chr(10)+; //"ATENÇÃO"###"Exclusão não permitida. Ja existe "
					STR0041,3,1) //"abastecimento relacionado a este posto."
				Else
					SX2->(dbSeek(aCHKDEL[i][2]))
					cError := AllTrim(FWX2Nome(aCHKDEL[i][2])) + " (" + FWX2CHAVE() + ")"
					HELP(" ",1,"NGINTMOD",,cError,5,1)
				Endif

				Exit
			Endif
		Next

		If lDEL
			dbSelectArea(cAlias)
			RecLock(cAlias,.F.)
			dbDelete()
			MsUnlock(cAlias)
			EvalTrigger()  // Processa Gatilhos
		EndIf
	Endif

	lRefresh := .T.
	If nOPCx == 3 .Or. nOPCx == 4
		If cAlias == "TQF" .And. M->TQF_ATIVO == "1" .And. M->TQF_TIPPOS <> "3"
			If !lUPDTransf .Or.  (lUPDTransf .And. M->TQF_COMBOI <> "1"  ) .Or. ;
			   (lUPDTransf .And. M->TQF_COMBOI == "1" .And. If(FindFunction("FWIsAdmin"), FWIsAdmin(), (PswAdmin(, , RetCodUsr()) == 0)) )
			    // Apenas Administradores podem incluir Negociação e Preço Negociado manualmente para Comboios
				If MsgYesNo(STR0015,STR0016) //"Deseja incluir a Negociação ?"###"ATENCÃO"
					MNT610CAD("TQG",,3)
				EndIf
			EndIf
		EndIf

		If cAlias == "TQG"
			If MsgYesNo(STR0017,STR0016) //"Deseja incluir Preço ?"###"ATENCÃO"
				MNT610CAD("TQH",,3)
			EndIf
		EndIf
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MNT610NEG  ³ Autor ³Evaldo Cevinski Jr.    ³ Data ³04/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro da Negociacao                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610NEG

	Local aOldRot  := aClone(aRotina)
	Local aOldArea := GetArea()

	aRotina := { { STR0001, "MNT610PESQ('TQG')",0,1},;  //"Pesquisar"
				 { STR0002, "MNT610CAD"        ,0,2},;  //"Visualizar"
				 { STR0003, "MNT610CAD"        ,0,3},;  //"Incluir"
				 { STR0004, "MNT610CAD"        ,0,4},;  //"Alterar"
				 { STR0005, "MNT610CAD"        ,0,5,3}} //"Excluir"

	//Define o cabecalho da tela de atualizacoes
	Private cCadastro  := OemtoAnsi(STR0006) //"Negociacao"
	Private aCHKDEL    := {}
	Private aIndTQG    := {}
	Private bFiltraBrw := {|| Nil}
	Private bNGGRAVA   := {||}
	aCHKDEL := { {'TQG->TQG_CODPOS+TQG->TQG_LOJA+DTOS(TQG_DTNEG)+TQG->TQG_HRNEG', "TQH", 3} }

	If TQF->TQF_TIPPOS == "3"
		aRotina := aClone(aOldRot)
		Help(" ",1,STR0010,,STR0040,3,1)  //"ATENÇÃO"###"Posto não conveniado!"
		Return .T.
	EndIf

	dbSelectArea("TQG")
	dbSetOrder(2)

	cCondTQG   := 'TQG_FILIAL == "'+xFILIAL("TQF")+'" .And. TQG_CODPOS == TQF->TQF_CODIGO .And. TQG_LOJA == TQF->TQF_LOJA'
	bFiltraBrw := {|| FilBrowse("TQG",@aIndTQG,@cCondTQG)}
	Eval(bFiltraBrw)

	mBrowse( 6, 1,22,75,"TQG")

	aEval(aIndTQG,{|x| Ferase(x[1]+OrdBagExt())})
	ENDFILBRW("TQG",aIndTQG)

	aRotina := aClone(aOldRot)
	RestArea(aOldArea)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MNT610PRE  ³ Autor ³Evaldo Cevinski Jr.    ³ Data ³04/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Preco do Combustivel                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610PRE

	Local aOldRot  := aClone(aRotina)
	Local aOldArea := GetArea()

	//VERIFICA SE TEM NEGOCIACAO PARA PODER INCLUIR PRECO
	dbSelectArea("TQG")
	dbSetOrder(1)
	If !dbSeek(xFilial("TQG")+TQF->TQF_CODIGO+TQF->TQF_LOJA)
		HELP(" ",1,STR0010,,STR0037,3,1) //"ATENÇÃO"###"Não existe negociação cadastrada."
		Return .F.
	EndIF

	aRotina := {{STR0001,"MNT610PESQ('TQH')",0,1},;  //"Pesquisar"
				{STR0002,"MNT610CAD"        ,0,2},;  //"Visualizar"
				{STR0003,"MNT610CAD"        ,0,3},;  //"Incluir"
				{STR0004,"MNT610CAD"        ,0,4},;  //"Alterar"
				{STR0005,"MNT610CAD"        ,0,5,3}} //"Excluir"

	//Define o cabecalho da tela de atualizacoes
	Private cCadastro := OemtoAnsi(STR0019) //"Preços"
	Private aCHKDEL   := {}
	Private bNGGRAVA  := {|| }

	If TQF->TQF_TIPPOS == "3"
		aRotina := aClone(aOldRot)
		Help(" ",1,STR0010,,STR0040,3,1)  //"ATENÇÃO"###"Posto não conveniado!"
		Return .T.
	EndIf

	dbSelectArea("TQH")
	dbSetOrder(4)
	Set Filter To TQH_FILIAL == xFILIAL("TQF") .And. TQH_CODPOS == TQF->TQF_CODIGO .And. TQH_LOJA == TQF->TQF_LOJA

	mBrowse( 6, 1,22,75,"TQH")

	Set Filter To
	aRotina := aClone(aOldRot)
	RestArea(aOldArea)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT610VALDT³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³05/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida a data Negociacao,nao pode ser menor que a ultima     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA610                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610VALDT(nTabela)

	Local aArea := GetArea()
	Local lErro := .F.

	If nTabela == "TQG"

		dbSelectArea("TQG")
		dbSetOrder(1)
		dbSeek(xFilial("TQG")+TQF->TQF_CODIGO+TQF->TQF_LOJA)
		While !Eof() .And. TQG->TQG_FILIAL = xFILIAL("TQG") .And. TQG->TQG_CODPOS == TQF->TQF_CODIGO .And. TQG->TQG_LOJA == TQF->TQF_LOJA

			If M->TQG_DTNEG < TQG->TQG_DTNEG
				lErro := .T.
			ElseIf M->TQG_DTNEG = TQG->TQG_DTNEG .And. M->TQG_HRNEG < TQG->TQG_HRNEG
				lErro := .T.
			EndIf

			If lErro
				Help("",1,STR0016,,STR0091+Chr(10)+Chr(13)+STR0060,3,1) //### //"ATENCÃO"### //"Data e Hora da Negociação não pode ser menor que a"###"última Negociação cadastrada"
				Return .F.
			EndIf

			dbSelectArea("TQG")
			dbSkip()
		End
	EndIf

	If nTabela == "TQH"
		dbSelectArea("TQH")
		dbSetOrder(1)
		dbSeek(xFilial("TQH")+TQF->TQF_CODIGO+TQF->TQF_LOJA+M->TQH_CODCOM)
		While !Eof() .And. TQH->TQH_FILIAL == xFilial("TQH") .And. TQH->TQH_CODPOS == TQF->TQF_CODIGO;
					 .And. TQH->TQH_LOJA   == TQF->TQF_LOJA  .And. TQH->TQH_CODCOM == M->TQH_CODCOM

			If M->TQH_DTNEG < TQH->TQH_DTNEG
				lErro := .T.
			ElseIf M->TQH_DTNEG = TQH->TQH_DTNEG .And. M->TQH_HRNEG < TQH->TQH_HRNEG
				lErro := .T.
			EndIf

			If lErro
				Help("",1,STR0016,,STR0092+Chr(10)+Chr(13)+STR0061,3,1) //### //"ATENCÃO"###  //"Data e hora da Negociação do Preço nao pode"###"ser menor ou igual a última cadastrada."
				Return .F.
			EndIf

			dbSelectArea("TQH")
			dbSkip()
		End
	EndIf

	RestArea(aArea)

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Mnt610Obrig³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³05/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida campos referente ao faturamento da Negociacao         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA610                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mnt610Obrig()

	If TQF->TQF_TIPPOS <> '2'

		If Empty(M->TQG_PRAZO)
			Help("",1,STR0016,,STR0094+Chr(10)+Chr(13)+STR0065,3,1) //### //"ATENCÃO" //"Para tipos de postos conveniados ou nâo conve-"###"niados deve-se informar o Prazo de Pagamento."
			Return .F.
		EndIf

		If Empty(M->TQG_DIAFAT)
			Help("",1,STR0016,,STR0094+Chr(10)+Chr(13)+STR0067,3,1) //### //"ATENCÃO" //"Para tipos de postos conveniados ou nâo conve-"###"niados deve-se informar o Dia de Faturamento."
			Return .F.
		EndIf

		If Empty(M->TQG_DIALIM) .AND. M->TQG_DIALIM <> 0
			Help("",1,STR0016,,STR0094+Chr(10)+Chr(13)+STR0095+Chr(10)+Chr(13)+STR0068 ,3,1) //### //"ATENCÃO"### //"Para tipos de postos conveniados ou nâo conve-"###"niados deve-se informar o prazo limite de Emis-"###"sâo da Nota Fiscal."
			Return .F.
		EndIf

	EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT610EXCHV³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³05/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida chave na tabela de Negociacao de Preco                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA610                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610EXCHV(nTAB)

	If nTAB == "TQG"
		If !EXISTCHAV("TQG",TQF->TQF_CODIGO+TQF->TQF_LOJA+DTOS(M->TQG_DTNEG)+M->TQG_HRNEG)
			Return .F.
		EndIf
	EndIf

	If nTAB == "TQH"
		If !EXISTCHAV("TQH",TQF->TQF_CODIGO+TQF->TQF_LOJA+M->TQH_CODCOM+DTOS(M->TQH_DTNEG)+M->TQH_HRNEG)
			Return .F.
		EndIf
	EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT610DTCAD³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³05/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida data negociacao nao pode ser menor que a data do ca-  ³±±
±±³          ³dastramento do posto e cadastros nao menor que Dt. corrente. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA610                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610DTCAD(cTAB)

	Local dUltNeg := " "
	Local aRet    := {}

	If cTab == "TQF"
		If M->TQF_DTCAD > dDataBase
			Help("",1,STR0016,,STR0096+Chr(10)+Chr(13)+STR0071,3,1)  //"ATENCÃO"###  //"Data do cadastramento do Posto não pode ser"###"maior que a Data corrente."
			Return .F.
		EndIf
	EndIf

	If cTab == "TQG"

		If M->TQG_DTNEG < TQF->TQF_DTCAD
			Help("",1,STR0016,,STR0097+Chr(10)+Chr(13)+STR0072,3,1)  //"ATENCÃO"### //"Data da Negociação não pode ser menor que a"###"Data de Cadastramento do Posto."
			Return .F.
		EndIf

		If M->TQG_DTNEG > dDataBase
			Help("",1,STR0016,,STR0098+Chr(10)+Chr(13)+STR0074,3,1)  //"ATENCÃO"###   //"Data da Negociação não pode ser maior que a"###"Data corrente."
			Return .F.
		EndIf
	EndIf

	If cTab == "TQH"

		dbSelectArea("TQG")
		dbSetOrder(1)
		dbSeek(xFilial("TQG")+TQF->TQF_CODIGO+TQF->TQF_LOJA)
		While !Eof() .And. TQG->TQG_FILIAL = xFILIAL("TQG") .And. TQG->TQG_CODPOS == TQF->TQF_CODIGO .And. TQG->TQG_LOJA == TQF->TQF_LOJA

			dUltNeg := TQG->TQG_DTNEG
			dbSelectArea("TQG")
			dbSkip()
		End

		cAliasQry := GetNextAlias()
		cQuery := " SELECT * "
		cQuery += " FROM " + RetSqlName("TQN")
		cQuery += " WHERE TQN_POSTO =  '" + TQF->TQF_CODIGO    +"'"
		cQuery += " and TQN_LOJA    =  '" + TQF->TQF_LOJA      +"'"
		cQuery += " and TQN_DTABAS  >  '" + Dtos(M->TQH_DTNEG) +"'"
		cQuery += " and TQN_CODCOM  =  '" + M->TQH_CODCOM +"' and D_E_L_E_T_<>'*'"
		cQuery += " and TQN_FILIAL  =  '" + xFilial("TQN")+"' "
		cQuery := ChangeQuery(cQuery)
		dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

		While !Eof()
			Aadd( aRet, { '1' } )
			Exit
		EndDo

		(cAliasQry)->(DbCloseArea())

		If !Empty(aRet)
			HELP(" ",1,STR0010,,STR0039+Chr(10)+Chr(13)+STR0038,3,1) //"ATENÇÃO"###"Já existem abastecimentos para esse posto "###"com data maior que a Data da negociação!"
			Return .F.
		EndIf
	EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT610DTDES³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³14/02/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida data de desativacao nao pode ser menor que a data de  ³±±
±±³          ³cadastramento do posto,nem menor que ultimo abast. e negoc.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA610                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610DTDES()

	Local dDtUltAbas := " "
	Local dDtUltPrec := " "

	If !Empty(M->TQF_DTDESA).And. M->TQF_DTDESA < TQF->TQF_DTCAD
		Help("",1,STR0016,,STR0079+Chr(10)+Chr(13)+STR0080,3,1)   //"ATENCÃO"### //"Data de Desativação do Posto não pode ser"###"menor que a Data do Cadastramento."
		Return .F.
	EndIf

	If M->TQF_DTREAT < TQF->TQF_DTDESA
		Help("",1,STR0016,,STR0101+Chr(10)+Chr(13)+STR0102,3,1)   //"ATENCÃO"###  //"Data de Reativação do Posto não pode ser"###"menor que a data de Desativação."
		Return .F.
	EndIf

	//data da reativacao nao pode ser menor que a data do cadastramento
	If !Empty(M->TQF_DTREAT).And. M->TQF_DTREAT < TQF->TQF_DTCAD
		Help("",1,STR0016,,STR0101+Chr(10)+Chr(13)+STR0080,3,1)   //"ATENCÃO"###"Data de Reativação do Posto não pode ser"###"menor que a Data do Cadastramento."
		Return .F.
	EndIf

	//menor que ultimo abastecimento
	dbSelectArea("TQN")
	dbSetOrder(3)
	If dbSeek(xFilial("TQN")+M->TQF_CODIGO+M->TQF_LOJA)
		While !Eof() .And. TQN->TQN_FILIAL = xFILIAL("TQN") .And. TQN->TQN_POSTO == TQF->TQF_CODIGO .And. TQN->TQN_LOJA == TQF->TQF_LOJA

			If dDtUltAbas < DTOS(TQN->TQN_DTABAS)
				dDtUltAbas := DTOS(TQN->TQN_DTABAS)
			EndIf
			dbSelectArea("TQN")
			dbSkip()
		End
	EndIf

	If DTOS(M->TQF_DTDESA) < dDtUltAbas
		Help("",1,STR0016,,STR0079+Chr(10)+Chr(13)+STR0082,3,1)   //"Data de Desativação do Posto não pode ser"###"menor que a Data do último Abastecimento."
		Return .F.
	EndIf

	//menor que ultima negociacao de preco
	dbSelectArea("TQH")
	dbSetOrder(1)
	If dbSeek(xFilial("TQH")+M->TQF_CODIGO+M->TQF_LOJA)
		While !Eof() .And. TQH->TQH_FILIAL = xFILIAL("TQH") .And. TQH->TQH_CODPOS == TQF->TQF_CODIGO .And. TQH->TQH_LOJA == TQF->TQF_LOJA

			If dDtUltPrec < DTOS(TQH->TQH_DTNEG)
				dDtUltPrec := DTOS(TQH->TQH_DTNEG)
			EndIf
			dbSelectArea("TQH")
			dbSkip()
		End
	EndIf

	If !Empty(M->TQF_DTDESA)
		If DTOS(M->TQF_DTDESA) < dDtUltPrec
			Help(" ",1,STR0016,,STR0079+Chr(10)+Chr(13)+STR0083,3,1)   //"ATENCÃO"### //"Data de Desativação do Posto não pode ser"###"menor que a Data do último Preço Negociado."
			Return .F.
		EndIf
	EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT610PREC ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³15/02/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida preco negociado nao pode ser maior que o preco da     ³±±
±±³          ³Bomba.                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA610                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610PREC()

	If M->TQH_PRENEG > M->TQH_PREBOM
		Help("",1,STR0016,,STR0077+Chr(10)+Chr(13)+STR0086,3,1)   //"ATENCÃO"  //"Preço Negociado não pode ser maior que o"###"Preço da Bomba."
		Return .F.
	EndIf
	M->TQH_DESCON := (1-(M->TQH_PRENEG/M->TQH_PREBOM))*100

Return .T.

Function MNT610CONV()

	If M->TQF_TIPPOS == "3"
		M->TQF_CONVEN := "4"
		lCONVEN := .F.
	ELSE
		lCONVEN := .T.
	EndIf

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNT610PESQ³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³ 17.03.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Pesquisa Postos - Tanques/Bombas                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA615                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT610PESQ(cTAB)

	Local nINDOLD := 0

	If cTAB == "TQG"
		dbSelectArea('TQG')
		nINDOLD := Indexord()
		AxPESQUI('TQG',Recno(),1)
		dbSelectArea('TQG')
		dbSetOrder(nINDOLD)
	Else
		dbSelectArea('TQH')
		nINDOLD := Indexord()
		AxPESQUI('TQH',Recno(),1)
		dbSelectArea('TQH')
		dbSetOrder(nINDOLD)
	EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA610dt  | Autor ³Soraia de Carvalho     ³ Data ³ 27/04/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao da hora de Reativacao                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA610                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MNA610DT()

	Local dData

	dData := dDataBase

	If  M->TQF_DTREAT > dData
		HELP(" ",1,STR0010,,STR0031,3,1) //"ATENÇÃO"###"Data da Reativação não pode ser superior à data atual."
		Return .F.
	EndIf

	If  M->TQF_DTDESA > dData
		HELP(" ",1,STR0010,,STR0026,3,1) //"ATENÇÃO"###"Data da Desativação do posto não pode ser superior à data atual."
		Return .F.
	EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT610FN  | Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 22/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Gatilho para campo telefone c/DDD e Fone do SA2             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA610                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT610FN()

	cTEL := AllTrim(SA2->A2_DDD+SA2->A2_TEL)

	n := AT("-",cTEL)
	If n > 0
		cTEL := SUBSTR(cTEL,1,n-1)+SUBSTR(cTEL,n+1,Len(cTel))
	EndIf

	nTAM := LEN(ALLTRIM(cTEL))
	If SubStr(cTEL,1,1) == "0"
		cTEL := SubStr(cTEL,2,nTAM)
	EndIf

	If nTAM <= 8
		cTEL := "xx"+cTEL
	EndIf

Return cTEL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³02/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
	Local aRotina := {{STR0001, "AxPesqui" , 0, 1, 0, Nil},;   //"Pesquisar"
					  {STR0002, "MNT610CAD", 0, 2, 0, Nil},;   //"Visualizar"
					  {STR0003, "MNT610CAD", 0, 3, 0, Nil},;   //"Incluir"
					  {STR0004, "MNT610CAD", 0, 4, 0, Nil},;   //"Alterar"
					  {STR0005, "MNT610CAD", 0, 5, 3, Nil},; //"Excluir"
					  {STR0006, "MNT610NEG", 0, 3, 0, .F.},;   //"Negociacao"
					  {STR0066, "MNT610PRE", 0, 3, 0, .F.},;   //"P&reco"
					  {STR0051, "MNTR945"  , 0, 8, 0, Nil}}    //"I&mprimir"

Return aRotina

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³MNTA610POSºAutor  ³Wagner S. de Lacerdaº Data ³  28/05/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validação do Posto.                                        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ .T./.F.                                                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SigaMNT (dicionário SX3)                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºProgramador ³   Data     ³ Descricao                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º            ³ xx/xx/xxxx ³                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA610POS()

	If M->TQF_COMBOI == "1"
		Return .T.
	EndIf

	If !ExistCpo("SA2", M->TQF_CODIGO, 1)
		Return .F.
	EndIf

	If !ALTERA .And. NGIFDBSEEK("TQF", M->TQF_CODIGO + M->TQF_LOJA, 1)
		Help(" ", 1, STR0010, Nil,; // "ATENÇÃO"
			 STR0107,; // "Já existe um Posto cadastrado com este código. Favor alterar o Código do Posto ou cancelar a operação."
			 1, 0)
		Return .F.
	EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³MNTA610LOJºAutor  ³Wagner S. de Lacerdaº Data ³  28/05/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validação da Loja do Posto.                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ .T./.F.                                                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SigaMNT (dicionário SX3)                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºProgramador ³   Data     ³ Descricao                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º            ³ xx/xx/xxxx ³                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA610LOJ()

	//Se For comboio não valida o fornecedor e Loja
	If M->TQF_COMBOI == "1"
		Return .T.
	EndIf

	If !Empty(M->TQF_LOJA) .And. ( !ExistCpo("SA2", M->TQF_CODIGO + M->TQF_LOJA, 1) .Or. !ExistChav("TQF", M->TQF_CODIGO + M->TQF_LOJA, 1) )
		Return .F.
	EndIf

	If !ALTERA .And. NGIFDBSEEK("TQF", M->TQF_CODIGO + M->TQF_LOJA, 1)
		ShowHelpDlg(STR0010,{STR0108},1,;  //"ATENÇÃO"##"Já existe um Posto/Loja cadastrado com estes códigos."
		{STR0114,""},1) //Favor alterar o Código do Posto/Loja, ou cancelar a operação."
		Return .F.
	EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³MNTA610CMBºAutor  ³Wagner S. de Lacerdaº Data ³  21/02/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Realiza as devidas alterações no cadastro de acordo com a  º±±
±±º          ³ flag do COMBOIO.                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ .T.                                                        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SigaMNT (dicionário SX3)                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºProgramador ³   Data     ³ Descricao                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º            ³ xx/xx/xxxx ³                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA610CMB()

	Local lComboio  := If(lUPDTransf, (M->TQF_COMBOI == "1"), .F.)
	Local nTQFFROTA := TAMSX3("TQF_FROTA")[1]

	// Se for Comboio, define o Posto como Posto Interno; caso contrário, define como Posto Conveniado
	If lComboio
		M->TQF_TIPPOS := "2"
	EndIf

	// Limpa o conteúdo do campo da Frota caso o posto não seja um Comboio
	If !lComboio
		M->TQF_FROTA := Space(nTQFFROTA)
	EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³MNTA610FRTºAutor  ³Wagner S. de Lacerdaº Data ³  21/02/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validação da Frota (Código do Bem) quando o Posto é um     º±±
±±º          ³ Comboio.                                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ .T.                                                        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ lBloqueia -> Opcional;                                     º±±
±±º          ³              Indica se deve impedir a continuidade da      º±±
±±º          ³              rotina:                                       º±±
±±º          ³                 .T. - Realiza o bloqueio                   º±±
±±º          ³                 .F. - Permite a continuidade               º±±
±±º          ³              Default: .F.                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SigaMNT (dicionário SX3)                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºProgramador ³   Data     ³ Descricao                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º            ³ xx/xx/xxxx ³                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTA610FRT(lBloqueia)

	Local cComboio  := If(lUPDTransf, M->TQF_COMBOI, "")
	Local cFrota    := If(lUPDTransf, M->TQF_FROTA, "")
	Local lComboio  := (cComboio == "1")
	Local aAreaTQF  := TQF->( GetArea() )
	Local nTQFRecNo := TQF->( RecNo() )

	Default lBloqueia := .T.

	// Update foi aplicado?
	If !lUPDTransf
		Return .F.
	EndIf

	// Valida o preenchimento do campo
	If Empty(cFrota)
		If !lComboio
			Return .T.
		Else
			ShowHelpDlg(STR0010,{STR0111,""},2,;  //"ATENÇÃO"##"Não foi informada a frota relacionada ao comboio."
								{STR0109,""},1)  //"Quando o Posto for um Comboio, é obrigatório informar a Frota relacionada a ele."
			Return !lBloqueia
		EndIf
	ElseIf !ExistCpo("ST9", cFrota, 1)
		Return .F.
	EndIf

	// Valida se a Frota não está relacionada à outro posto
	dbSelectArea("TQF")
	dbSetOrder(5)
	dbSeek(xFilial("TQF") + cComboio + cFrota, .T.)
	While !Eof() .And. TQF->TQF_FILIAL == xFilial("TQF") .And. TQF->TQF_COMBOI == cComboio .And. TQF->TQF_FROTA == cFrota

		If INCLUI .And. RecNo() <> nTQFRecNo
			ShowHelpDlg(STR0010,{STR0110},1,;  //"ATENÇÃO"##"Esta Frota já está relacionada à outro posto."
								{STR0113,""},1) //"Selecione outra frota para relacionamento."
			RestArea(aAreaTQF)
			Return !lBloqueia
		EndIf
		dbSelectArea("TQF")
		dbSkip()
	End

	RestArea(aAreaTQF)

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT610VLFOR
Função para validar o Fornecedor e Loja

@author Taina Alberto Cardoso
@since 08/08/2013
@version MP10
/*/
//---------------------------------------------------------------------
Function MNT610VLFOR()

	Local lRet  := .T.
	Local cLoja := TAMSX3("TQF_LOJA")[1]

	If M->TQF_COMBOI == "1"
		lRet := .T.
	ElseIf Empty(M->TQF_CODIGO)
		M->TQF_LOJA :=Space(TAMSX3("TQF_LOJA")[1])
	Else
		If M->TQF_CODIGO != SA2->A2_COD
			SA2->(dbSetOrder(1))
			SA2->(dbSeek(xFilial("SA2")+M->TQF_CODIGO))
		Endif
		cLoja       := SA2->A2_LOJA
		lRet        := ExistCpo("SA2",M->TQF_CODIGO+cLoja)
		M->TQF_LOJA := cLoja
	Endif

Return(lRet)

//---------------------------------------------------------------------
/*/{Protheus.doc} fIntegPrec
Verifica integridade referencial com o preco negociado

@author Lucas Guszak
@since 10/12/2013
@version MP10
@return lRet Indica se o registro pode ser excluido
/*/
//---------------------------------------------------------------------
Static Function fIntegPrec(cChave)

	Local lRet       := .T.
	Local cPostoLoja := ""
	Local dDataFim   := CTOD( "  /  /    " )
	Local cHoraFim   := "  :  "
	Local lProxima   := .F.
	Local aAreaTQG   := ("TQG")->(GetArea())
	Local aAreaTQH   := ("TQH")->(GetArea())

	dbSelectArea("TQG") // TQG - Negociacão
	dbSetOrder(1) //TQG_FILIAL+TQG_CODPOS+TQG_LOJA+DTOS(TQG_DTNEG)+TQG_HRNEG
	If dbSeek(xFilial("TQG")+cChave)

		cPostoLoja := TQG->TQG_CODPOS+TQG->TQG_LOJA

		dbSkip()

		//Proxima Negociacao
		If !Eof() .And. TQG->TQG_FILIAL+TQG->TQG_CODPOS+TQG->TQG_LOJA == xFilial("TQG")+cPostoLoja
			dDataFim := TQG->TQG_DTNEG
			cHoraFim := TQG->TQG_HRNEG
			lProxima := .T.
		EndIf

		// Procura por preços iguais a data e hora da negociação ou entre as duas negociações
		dbSelectArea("TQH") // TQH - Preco
		dbSetOrder(3) //TQH_FILIAL+TQH_CODPOS+TQH_LOJA+DTOS(TQH_DTNEG)+TQH_HRNEG
		lRet := !dbSeek( xFilial("TQH")+cChave, .T.)
		If TQH->TQH_FILIAL+TQH->TQH_CODPOS+TQH->TQH_LOJA == xFilial("TQH")+cPostoLoja
			//Verifica se existe outra negociacao para a loja do posto
			If lProxima
				If DTOS(TQH_DTNEG)+TQH_HRNEG < DTOS( dDataFim ) + cHoraFim
					lRet := .F.
				EndIf
			Else
				lRet := .F.
			EndIf
		EndIf

	EndIf

	RestArea(aAreaTQG)
	RestArea(aAreaTQH)

Return lRet
