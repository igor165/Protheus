#INCLUDE "mntr795.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 3 //Versao do fonte

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR795  ³ Autor ³ Elisangela Costa      ³ Data ³ 30/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rastrear Componentes                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR795()

    Local oReport
    Local aArea := GetArea()
    
    Private aNGBEGINPRM := NGBEGINPRM(_nVERSAO)
    
    /*---------------------------------------------------------------
    Vetor utilizado para armazenar retorno da função MNT045TRB,
    criada de acordo com o item 18 (RoadMap 2013/14)
    ---------------------------------------------------------------*/
    Private vFilTRB := MNT045TRB()
        
    SetKey(VK_F4, {|| MNT045FIL( vFilTRB[2] )})
    
    If FindFunction("TRepInUse") .And. TRepInUse()
        //-- Interface de impressao
        oReport := ReportDef()
        oReport:SetPortrait() //Default Retrato
        oReport:PrintDialog()
    Else
        MNTR795R3()
    EndIf

    MNT045TRB( .T., vFilTRB[1], vFilTRB[2])
    
    NGRETURNPRM(aNGBEGINPRM)
        
    RestArea(aArea)
    
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ Elisangela Costa      ³ Data ³ 30/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR795                                                    ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef()
Local oReport
Local oSection1
Local oSection2
Local oCell

SetKey(VK_F4, {|| }) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros!                        ³
//³ mv_par01     // De Familia                                   ³
//³ mv_par02     // Ate Familia                                  ³
//³ mv_par03     // De Bem                                       ³
//³ mv_par04     // Ate Bem                                      ³
//³ mv_par05     // De Data                                      ³
//³ mv_par06     // Ate Data                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//LAYOUT
/*/                                                                                                    1         1         1         1
          1         2         3         4         5         6         7         8         9         0         1         2         3
012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
______________________________________________________________________________________________________________________________________
                                                            Rastrear Componentes
______________________________________________________________________________________________________________________________________
Bem              Descrição
______________________________________________________________________________________________________________________________________
xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Dt.Entrada  Dt.Saida  Cont.Acum.1 Ent.  Cont.Acum.1 Saida  Cont.Acum.2 Ent.  Cont.Acum.2 Saida  Loc.   Bem Pai          Nome

---------------------------------------------------------------------------------------------------------------------------------------
99/99/9999  99/99/9999     999.999.999        999.999.999       999.999.999        999.999.999  xxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
99/99/9999  99/99/9999     999.999.999        999.999.999       999.999.999        999.999.999  xxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
99/99/9999  99/99/9999     999.999.999        999.999.999       999.999.999        999.999.999  xxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
/*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MNTR795",OemToAnsi(STR0006),"MNT795",{|oReport| ReportPrint(oReport)},STR0001+". "+STR0002+" "+STR0003)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01 // Bem                                              ³
//³ mv_par02 // Posicao                                          ³
//³ mv_par03 // Data Inicial                                     ³
//³ mv_par04 // Data Fim                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

oSection1 := TRSection():New(oReport,STR0010,{"ST9","STZ"})
TRCell():New(oSection1,"T9_CODBEM","ST9",STR0010,/*Picture*/,/*Tamanho*/) //"Bem"
TRCell():New(oSection1,"T9_NOME"  ,"ST9",STR0011,/*Picture*/,/*Tamanho*/) //"Descrição"

oSection2 := TRSection():New(oSection1,STR0021,{"STZ","ST9"})  //"Movimentacoes"
oCell := TRCell():New(oSection2,"TZ_DATAMOV","STZ",STR0012,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Dt.Entrada"
oCell := TRCell():New(oSection2,"TZ_DATASAI","STZ",STR0013,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Dt.Saida"
oCell := TRCell():New(oSection2,"TZ_POSCONT","STZ",STR0014,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Cont.Acum.1 Ent."
oCell := TRCell():New(oSection2,"TZ_CONTSAI","STZ",STR0015,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Cont.Acum.1 Saida"
oCell := TRCell():New(oSection2,"TZ_POSCON2","STZ",STR0016,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Cont.Acum.2 Ent."
oCell := TRCell():New(oSection2,"TZ_CONTSA2","STZ",STR0017,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Cont.Acum.2 Saida"
oCell := TRCell():New(oSection2,"TZ_LOCALIZ","STZ",STR0018,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Loc."
oCell := TRCell():New(oSection2,"TZ_BEMPAI" ,"STZ",STR0019,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem Pai"
oCell := TRCell():New(oSection2,"T9_NOME"   ,"ST9",STR0020,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) //"Nome"
//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a
//direita
oSection2:Cell("TZ_POSCONT"):SetHeaderAlign("RIGHT")
oSection2:Cell("TZ_CONTSAI"):SetHeaderAlign("RIGHT")
oSection2:Cell("TZ_POSCON2"):SetHeaderAlign("RIGHT")
oSection2:Cell("TZ_CONTSA2"):SetHeaderAlign("RIGHT")
TRPosition():New(oSection2,"ST9",1,{|| xFilial("ST9") + STZ->TZ_BEMPAI})

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR795R3³ Autor ³ Elisangela Costa      ³ Data ³ 30/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rastrear Componentes                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR795                                                    ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR795R3()

    Local cString    := "ST9"
    Local cdesc1     := STR0001 //"Relatorio de Rastreamento de Componentes"
    Local cdesc2     := STR0002 //"E permitido selecionar tipo de informacao a ser"
    Local cdesc3     := STR0003 //"apresentada, atraves dos parametros do relatorio."
    Local wnrel      := "MNTR795"
    
    Private aReturn  := { STR0004, 1,STR0005, 1, 2, 1, "",1 }  //"Zebrado"###"Administracao"
    Private nLastKey := 0
    Private Titulo   := STR0006 //"Rastrear Componentes"
    Private Tamanho  := "M"
    Private nomeprog := "MNTR795"
    Private lmaqui   := .f.
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Verifica as perguntas selecionadas                           ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Variaveis utilizadas para qarametros!                        ³
    //³ mv_par01     // De Familia                                   ³
    //³ mv_par02     // Ate Familia                                  ³
    //³ mv_par03     // De Bem                                       ³
    //³ mv_par04     // Ate Bem                                      ³
    //³ mv_par05     // De Data                                      ³
    //³ mv_par06     // Ate Data                                     ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Private cperg := "MNT795"
    
    Pergunte(cperg,.F.)
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Envia controle para a funcao SETPRINT                        ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    wnrel := SetPrint(cString,wnrel,cperg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
    
    SetKey(VK_F4, {|| })
    
    If nLastKey = 27
        Set Filter To
        Return
    Endif
    
    SetDefault(aReturn,cString)
    RptStatus({|lEnd| R795Imp(@lEnd,wnRel,titulo,tamanho)},titulo)
    
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R795Imp  ³ Autor ³ Elisangela Costa      ³ Data ³ 30/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR795                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function R795IMP(lEND,WNREL,TITULO,TAMANHO)
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Define Variaveis                                             ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Local cRodaTxt := ""
    Local nCntImpr := 0
    Local lImpRel  := .F.
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Contadores de linha e pagina                                 ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Private li := 80 ,m_pag := 1
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Variaveis locais exclusivas deste programa                   ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Private CABEC1 := STR0022 //"       Data                       Cont.Acum.1              Cont.Acum.2      "
    Private CABEC2 := STR0007 //"Entrada     Saida           Entrada        Saida      Entrada        Saida  Loc.  Bem Pai          Nome"
    /*/                                                                                                    1         1         1         1
    1         2         3         4         5         6         7         8         9         0         1         2         3
    012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
    ______________________________________________________________________________________________________________________________________
    Data                       Cont.Acum.1              Cont.Acum.2
    Entrada     Saida           Entrada        Saida      Entrada        Saida  Loc.    Bem Pai          Nome"
    
    Bem - xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    
    99/99/9999  99/99/9999  999.999.999  999.999.999  999.999.999  999.999.999  xxxxxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    99/99/9999  99/99/9999  999.999.999  999.999.999  999.999.999  999.999.999  xxxxxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    99/99/9999  99/99/9999  999.999.999  999.999.999  999.999.999  999.999.999  xxxxxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    
    Bem - xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    
    99/99/9999  99/99/9999  999.999.999  999.999.999  999.999.999  999.999.999  xxxxxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    99/99/9999  99/99/9999  999.999.999  999.999.999  999.999.999  999.999.999  xxxxxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    99/99/9999  99/99/9999  999.999.999  999.999.999  999.999.999  999.999.999  xxxxxx  xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    
    /*/
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Verifica se deve comprimir ou nao                            ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    nTipo  := IIF(aReturn[4]==1,15,18)
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Possiciona os arquivos para processamento                                ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    DbSelectArea("ST9")
    DbSetOrder(01)
    DbSeek(xFilial("ST9")+MV_PAR03,.t.)
    SetRegua(LastRec())
    While !Eof() .And. ST9->T9_FILIAL == xFilial("ST9") .And. ;
            ST9->T9_CODBEM <= MV_PAR04
        
        If lEnd
            @ PROW()+1,001 PSay STR0008 //"CANCELADO PELO OPERADOR"
            Exit
        EndIf
        
        IncRegua()
        
        If ST9->T9_CODFAMI < MV_PAR01 .Or. ST9->T9_CODFAMI > MV_PAR02
            DbSkip()
            Loop
        EndIf
        
        If MNT045STB( ST9->T9_CODBEM, vFilTRB[2] )
            dbSelectArea("ST9")
            dbSkip()
            Loop
        EndIf
    
        lImpRel := .T.
        lPRI    := .T.
        cBemST9 := Recno()
        DbSelectArea("STZ")
        DbSetOrder(02)
        DbSeek(xFilial("STZ")+ST9->T9_CODBEM)
        While !Eof() .And. STZ->TZ_FILIAL == xFilial("STZ") .And. ;
                STZ->TZ_CODBEM == ST9->T9_CODBEM
            
            If STZ->TZ_DATAMOV >= MV_PAR05 .And. STZ->TZ_DATASAI <= MV_PAR06
                If lPRI
                    NgSomali(58)
                    NgSomali(58)
                    @ Li,000 Psay STR0009 //"Bem - "
                    @ Li,006 Psay ST9->T9_CODBEM
                    @ Li,025 Psay ST9->T9_NOME
                    lPRI := .F.
                EndIf
                NgSomali(58)
                @ Li,000 Psay STZ->TZ_DATAMOV Picture "99/99/99
                @ Li,012 Psay STZ->TZ_DATASAI Picture "99/99/99
                @ Li,024 Psay STZ->TZ_POSCONT Picture "@E 999,999,999"
                @ Li,037 Psay STZ->TZ_CONTSAI Picture "@E 999,999,999"
                @ Li,050 Psay STZ->TZ_POSCON2 Picture "@E 999,999,999"
                @ Li,063 Psay STZ->TZ_CONTSA2 Picture "@E 999,999,999"
                @ Li,076 Psay STZ->TZ_LOCALIZ Picture "@!"
                @ Li,084 Psay STZ->TZ_BEMPAI  Picture "@!"
                
                DbSelectArea("ST9")
                DbSeek(xFilial("ST9")+STZ->TZ_BEMPAI)
                @ Li,102 Psay substr(ST9->T9_NOME,1,25)
                dbGoto(cBemST9)
            EndIf
            DbSelectArea("STZ")
            Dbskip()
        End
        DbSelectArea("ST9")
        Dbskip()
    EndDo
    
    If lImpRel
        Roda(nCntImpr,cRodaTxt,Tamanho)
    Else
        MsgInfo(STR0024, STR0023)
        Return Nil
    EndIf
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Devolve a condicao original do arquivo principam             ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    RetIndex("ST9")
    RetIndex("STZ")
    Set Filter To
    Set device to Screen
    
    If aReturn[5] = 1
        Set Printer To
        dbCommitAll()
        OurSpool(wnrel)
    Endif
    
    MS_FLUSH()
    
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint| Autor ³ Elisangela Costa      ³ Data ³ 30/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ReportDef                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)
    
    Local oSection1 := oReport:Section(1)
    Local oSection2 := oReport:Section(1):Section(1)

    DbSelectArea("ST9")
    DbSetOrder(01)
    DbSeek(xFilial("ST9")+MV_PAR03,.T.)
    oReport:SetMeter(RecCount())
    
    While !Eof() .And. !oReport:Cancel() .And. ST9->T9_FILIAL == xFilial("ST9") .And.;
            ST9->T9_CODBEM <= MV_PAR04
        
        If ST9->T9_CODFAMI < MV_PAR01 .Or. ST9->T9_CODFAMI > MV_PAR02
            DbSkip()
            Loop
        EndIf
        
        If MNT045STB( ST9->T9_CODBEM, vFilTRB[2] )
            dbSelectArea("ST9")
            dbSkip()
            Loop
        EndIf
        
        lPRI := .T.
        cBemST9 := Recno()
        DbSelectArea("STZ")
        DbSetOrder(02)
        DbSeek(xFilial("STZ")+ST9->T9_CODBEM)
        While !Eof() .And. !oReport:Cancel() .And.  STZ->TZ_FILIAL == xFilial("STZ") .And. ;
                STZ->TZ_CODBEM == ST9->T9_CODBEM
            
            oReport:IncMeter()
            If STZ->TZ_DATAMOV >= MV_PAR05 .And. STZ->TZ_DATASAI <= MV_PAR06
                
                If lPRI
                    oSection1:Init()
                    oSection2:Init()
                    oSection1:PrintLine()
                    lPRI := .F.
                EndIf
                
                oSection2:PrintLine()
                
                DbSelectArea("ST9")
                DbGoto(cBemST9)
            EndIf
            DbSelectArea("STZ")
            Dbskip()
        EndDo
        
        If !lPRI
            oSection1:Finish()
            oSection2:Finish()
        EndIf
        DbSelectArea("ST9")
        Dbskip()
    EndDo
Return Nil