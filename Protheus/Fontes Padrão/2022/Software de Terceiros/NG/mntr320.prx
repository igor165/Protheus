#INCLUDE "Mntr320.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 2 //Versao do fonte

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR320   ³ Autor ³ Elisangela Costa      ³ Data ³ 29/09/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Roteiro de planos de manutecao                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR320()
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)
	Local oReport
	Local aArea := GetArea()
	
	Private cTRB := GetNextAlias()

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetLandscape()  //Default Paisagem
		oReport:PrintDialog()
	Else
		MNTR320R3()
	EndIf
	RestArea(aArea)
	NGRETURNPRM(aNGBEGINPRM)
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ Elisangela Costa      ³ Data ³  26/06/06³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR320                                                    ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/



Static Function ReportDef()
	Local oReport
	Local oSection1
	Local oSection2
	Local oSection3
	Local oCell
	Local oCell2

	//LAYOUT
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123012345678901230123
	______________________________________________________________________________________________________________________________________________
	Roteiro do Plano
	______________________________________________________________________________________________________________________________________________
	Roteiro  Descricao
	______________________________________________________________________________________________________________________________________________
	XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

	Bem               Nome                                     Servico  Descricao                       Seq.
	______________________________________________________________________________________________________________________________________________
	XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXX

	O.s     Plano   Dt.Pr.Ini.  C.Custo              Descricao
	______________________________________________________________________________________________________________________________________________

	xxxxxx  xxxxxx  99/99/9999  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxx  xxxxxx  99/99/9999  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx


	Bem               Nome                                     Servico  Descricao                       Seq.
	______________________________________________________________________________________________________________________________________________
	XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXX

	O.s     Plano   Dt.Pr.Ini.  C.Custo              Descricao
	______________________________________________________________________________________________________________________________________________

	xxxxxx  xxxxxx  99/99/9999  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxx  xxxxxx  99/99/9999  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	*/

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR320",OemToAnsi(STR0005),"MNT32R",{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis utilizadas para qarametros!                        ³
	³ mv_par01     // De Roteiro                                   ³
	³ mv_par02     // Ate Roteiro                                  ³
	³ mv_par03     // De Plano                                     ³
	³ mv_par04     // Ate Plano                                    ³
	³ mv_par05     // De Servico                                   ³
	³ mv_par06     // Ate Servico                                  ³
	³ mv_par07     // De Data                                      ³
	³ mv_par08     // Ate Data                                     ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	*/
	Pergunte(oReport:uParam,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSection1 := TRSection():New(oReport,STR0010 ,{(cTRB),"TPU"})
	TRCell():New(oSection1,"(cTRB)->ROTEIRO",(cTRB),STR0010,"@!" ,6 ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Roteiro"
	TRCell():New(oSection1,"TPU_DESCRI"  ,"TPU",STR0011,"@!" ,40,/*lPixel*/)  //"Descrição"
	TRPosition():New(oSection1,"TPU",1,{|| xFilial("TPU") + (cTRB)->ROTEIRO})

	oSection2 := TRSection():New(oReport,STR0020,{(cTRB),"ST9","ST4"}) //Bens
	oCell := TRCell():New(oSection2,"(cTRB)->CODBEM"  ,(cTRB) ,STR0012 ,"@!" ,16,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem"
	oCell := TRCell():New(oSection2,"T9_NOME"      ,"ST9" ,STR0013 ,"@!" ,40,/*lPixel*/)  //"Nome"
	oCell := TRCell():New(oSection2,"(cTRB)->SERVICO" ,(cTRB) ,STR0014 ,"@!" ,6 ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Serviço"
	oCell := TRCell():New(oSection2,"T4_NOME"      ,"ST4" ,STR0011 ,"@!" ,40,/*lPixel*/)  //"Descrição"
	oCell := TRCell():New(oSection2,"(cTRB)->SEQRELA" ,(cTRB) ,STR0015 ,"@!" ,3 ,/*lPixel*/)  //"Seq."
	oCell := TRCell():New(oSection2,"(cTRB)->ORDEMSE" ,(cTRB) ,STR0016 ,"@!"        ,6,/*lPixel*/,/*{|| code-block de impressao }*/)  //"O.S"
	oCell := TRCell():New(oSection2,"(cTRB)->PLANO"   ,(cTRB) ,STR0017 ,"@!"        ,6,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Plano"
	oCell := TRCell():New(oSection2,"(cTRB)->DATAPRI" ,(cTRB) ,STR0018 ,"99/99/9999",8,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Dt.Pr.Ini."
	oCell := TRCell():New(oSection2,"(cTRB)->CCUSTO"  ,(cTRB) ,STR0019 ,"@!"        ,20,/*lPixel*/,/*{|| code-block de impressao }*/) //"C.Custo"
	oCell := TRCell():New(oSection2,"I3_DESC"      ,"SI3" ,STR0011 ,"@!"        ,40,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrição"
	oCell := TRPosition():New(oSection2,"ST9",1,{|| xFilial("ST9")+(cTRB)->CODBEM})
	oCell := TRPosition():New(oSection2,"ST4",1,{|| xFilial("ST4")+(cTRB)->SERVICO})
	oCell := TRPosition():New(oSection2,"SI3",1,{|| xFilial("SI3")+(cTRB)->CCUSTO})

Return oReport

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR320R3 ³ Autor ³ Elisangela Costa      ³ Data ³ 29/09/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Roteiro de planos de manutecao                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR320R3()

	Local cString    := "TPV"
	Local cDesc1     := STR0001 //"Relatorio de Roteiro de Plano. Roteiro a ser seguido com os bens e"
	Local cDesc2     := STR0002 //"sua sequencia de manutencao no roteiro."
	Local cDesc3     := ""
	Local wnrel      := "MNTR320"

	Private aReturn  := {STR0003,1,STR0004, 2, 2, 1, "",1} //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private cPerg    := "MNT32R"
	Private Titulo   := STR0005 //"Roteiro do Plano"
	Private Tamanho  := "G"

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis utilizadas para qarametros!                        ³
	³ mv_par01     // De Roteiro                                   ³
	³ mv_par02     // Ate Roteiro                                  ³
	³ mv_par03     // De Plano                                     ³
	³ mv_par04     // Ate Plano                                    ³
	³ mv_par05     // De Servico                                   ³
	³ mv_par06     // Ate Servico                                  ³
	³ mv_par07     // De Data                                      ³
	³ mv_par08     // Ate Data                                     ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	*/
	Pergunte(cPerg,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		DbSelectArea("TPV")
		Return
	EndIf
	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R320Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

	DbSelectArea("TPV")
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R320Imp  ³ Autor ³ Elisangela Costa      ³ Data ³ 29/09/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR320                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R320Imp(lEnd,wnRel,titulo,tamanho)

	Local cRodaTxt := ""
	Local nCntImpr := 0
	Local nMult    := 1
	Local oARQTR320

	Private li := 80 ,m_pag := 1
	Private Cabec1   := STR0006 //"Bem              Nome                           Servico Descricao                      Seq.  O.s     Plano   Dt.Pr.Ini.  C.Custo              Descricao"
	Private Cabec2   := " "
	Private nomeprog := "MNTR320"
	Private ntipo    := IIF(aReturn[4]==1,15,18)
	Private aVETINR  := {}

	/*
	*********************************************************************************************************************************************************************************************************
	*<empresa>                                                                                                                                                                             Folha..: xxxxx   *
	*SIGA /<nome .04                                 <     titulo do relatorio    >                                                                                                        DT.Ref.: dd/mm/aa*
	*Hora...: xx:xx:xx                                                                                                                                                                     Emissao: dd/mm/aa*
	*********************************************************************************************************************************************************************************************************
	Bem              Nome                           Servico Descricao                      Seq.  O.s     Plano   Dt.Pr.Ini.  C.Custo              Descricao
	*********************************************************************************************************************************************************************************************************
	1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

	Roteiro..: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxx  xxxxxx  xxxxxx  99/99/9999  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxx  xxxxxx  xxxxxx  99/99/9999  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	*/

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define o indice de leitura do arquivo de Bens            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	aDBFR320 :={{"ROTEIRO" ,"C",06,0},;
				{"SEQROT"  ,"C",05,0},;
				{"CODBEM"  ,"C",16,0},;
				{"SERVICO" ,"C",06,0},;
				{"SEQRELA" ,"C",03,0},;
				{"ORDEMSE" ,"C",06,0},;
				{"PLANO"   ,"C",06,0},;
				{"DATAPRI" ,"D",08,0},;
				{"CCUSTO"  ,"C",Len(stj->tj_ccusto),0}}

	//Cria Tabela Temporária 
	oARQTR320 := NGFwTmpTbl(cTRB,aDBFR320,{{"ROTEIRO","SEQROT","CODBEM","SERVICO","SEQRELA","DATAPRI"}})

	Processa({|lEND| MNTR320TPV()},STR0007) //"Processando Arquivo..."

	DbSelectArea(cTRB)
	DbSetOrder(01)
	DbGotop()
	SetRegua(LastRec())
	While !Eof()

		cROT320:= (cTRB)->ROTEIRO
		NGSOMALI(58)
		@ Li,000 Psay STR0008+(cTRB)->ROTEIRO  //"Roteiro......: "
		@ Li,022 Psay NGSEEK("TPU",(cTRB)->ROTEIRO,1,"TPU_DESCRI")
		NGSOMALI(58)

		cBEM320  := " "
		cSERV320 := " "
		cSEQU320 := "0"

		DbSelectArea(cTRB)
		While !Eof() .And. (cTRB)->ROTEIRO = cROT320
			IncRegua()
			NGSOMALI(58)
			If (cTRB)->CODBEM  <> cBEM320 .Or. (cTRB)->SERVICO <> cSERV320 .Or.;
			(cTRB)->SEQRELA <> cSEQU320

				If (cTRB)->CODBEM <> cBEM320
					@ Li,000 Psay (cTRB)->CODBEM
					@ Li,017 Psay NGSEEK("ST9",(cTRB)->CODBEM,1,"SubStr(ST9->T9_NOME,1,30)")
					@ Li,048 Psay (cTRB)->SERVICO
					@ Li,056 Psay NGSEEK("ST4",(cTRB)->SERVICO,1,"SubStr(ST4->T4_NOME,1,30)")
					@ Li,090 Psay (cTRB)->SEQRELA
					cBEM320  := (cTRB)->CODBEM
					cSERV320 := (cTRB)->SERVICO
					cSEQU320 := (cTRB)->SEQRELA
				ElseIf (cTRB)->SERVICO <> cSERV320
					@ Li,048 Psay (cTRB)->SERVICO
					@ Li,056 Psay NGSEEK("ST4",(cTRB)->SERVICO,1,"SubStr(ST4->T4_NOME,1,30)")
					@ Li,090 Psay (cTRB)->SEQRELA
					cSERV320 := (cTRB)->SERVICO
					cSEQU320 := (cTRB)->SEQRELA
				Else
					@ Li,090 Psay (cTRB)->SEQRELA
					cSEQU320 := (cTRB)->SEQRELA
				EndIf
			EndIf
			@ Li,093 Psay (cTRB)->ORDEMSE
			@ Li,101 Psay (cTRB)->PLANO
			@ Li,109 Psay (cTRB)->DATAPRI Picture "99/99/9999"
			@ Li,121 Psay (cTRB)->CCUSTO
			@ Li,142 Psay NGSEEK("SI3",(cTRB)->CCUSTO,1,"Substr(I3_DESC,1,40)")
			DbSelectArea(cTRB)
			DbSkip()
		End
		NGSOMALI(58)
	End

	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	Set Device To Screen
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		Ourspool(WNREL)
	EndIf
	MS_FLUSH()
	
	//Deleta o arquivo temporario fisicamente
	oARQTR320:Delete()

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³ Elisangela Costa      ³ Data ³  26/06/06³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ReportDef                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local oSection3 := oReport:Section(3)
	Local oARQTR320
	
	Private aVETINR := {}

	aDBFR320 :={{"ROTEIRO" ,"C",06,0},;
				{"SEQROT"  ,"C",05,0},;
				{"CODBEM"  ,"C",16,0},;
				{"SERVICO" ,"C",06,0},;
				{"SEQRELA" ,"C",03,0},;
				{"ORDEMSE" ,"C",06,0},;
				{"PLANO"   ,"C",06,0},;
				{"DATAPRI" ,"D",08,0},;
				{"CCUSTO" ,"C",Len(stj->tj_ccusto),0}}

	//Cria Tabela Temporária 
	oARQTR320 := NGFwTmpTbl(cTRB,aDBFR320,{{"ROTEIRO","SEQROT","CODBEM","SERVICO","SEQRELA","DATAPRI"}})

	Processa({|lEND| MNTR320TPV()},STR0007) //"Processando Arquivo..."

	DbSelectArea(cTRB)
	DbGotop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()

		cROT320  := (cTRB)->ROTEIRO
		cBEM320  := " "
		cSERV320 := " "
		cSEQU320 := "0"
		lPRIR320 := .T.

		oSection1:Init()
		oSection1:PrintLine()
		oSection2:Init()

		DbSelectArea(cTRB)
		While !Eof() .And. !oReport:Cancel() .And. (cTRB)->ROTEIRO = cROT320
			oReport:IncMeter()

			If (cTRB)->CODBEM  <> cBEM320 .Or. (cTRB)->SERVICO <> cSERV320 .Or.;
			(cTRB)->SEQRELA <> cSEQU320

				If (cTRB)->CODBEM <> cBEM320
					oSection2:Cell("(cTRB)->CODBEM"):Show()
					oSection2:Cell("T9_NOME"):Show()
					oSection2:Cell("(cTRB)->SERVICO"):Show()
					oSection2:Cell("T4_NOME"):Show()
					oSection2:Cell("(cTRB)->SEQRELA"):Show()

					cBEM320  := (cTRB)->CODBEM
					cSERV320 := (cTRB)->SERVICO
					cSEQU320 := (cTRB)->SEQRELA
				ElseIf (cTRB)->SERVICO <> cSERV320
					oSection2:Cell("(cTRB)->SERVICO"):Show()
					oSection2:Cell("T4_NOME"):Show()
					oSection2:Cell("(cTRB)->SEQRELA"):Show()

					cSERV320 := (cTRB)->SERVICO
					cSEQU320 := (cTRB)->SEQRELA
				Else
					oSection2:Cell("(cTRB)->SEQRELA"):Show()
					cSEQU320 := (cTRB)->SEQRELA
				EndIf
			EndIf
			oSection2:PrintLine()
			oSection2:Cell("(cTRB)->CODBEM"):Hide()
			oSection2:Cell("T9_NOME"):Hide()
			oSection2:Cell("(cTRB)->SERVICO"):Hide()
			oSection2:Cell("T4_NOME"):Hide()
			oSection2:Cell("(cTRB)->SEQRELA"):Hide()

			DbSelectArea(cTRB)
			DbSkip()
		End
		oSection2:Finish()
		oSection1:Finish()
	End
	//Deleta o arquivo temporario fisicamente
	oARQTR320:Delete()

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR320TPV³ Autor ³Elisangela Costa       ³ Data ³29/09/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa o arquivo de roteiro...                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³R320Imp                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNTR320TPV()

	cCondi320 := "STJ->TJ_PLANO >= MV_PAR03 .And. STJ->TJ_PLANO <= MV_PAR04 .And."
	cCondi320 := cCondi320 + "STJ->TJ_SERVICO >= MV_PAR05 .And. STJ->TJ_SERVICO <= MV_PAR06 .And."
	cCondi320 := cCondi320 + "STJ->TJ_DTMPINI >= MV_PAR07 .And. STJ->TJ_DTMPINI <= MV_PAR08"

	DbSelectArea("TPV")
	DbSetOrder(01)
	DbSeek(xFilial("TPV")+MV_PAR01,.T.)
	ProcRegua(LastRec())
	While !Eof() .And. TPV->TPV_FILIAL = xFilial("TPV") .And. TPV->TPV_CODROT <= MV_PAR02

		Incproc()
		DbSelectArea("STJ")
		DbSetOrder(02)
		DbSeek(xFilial("STJ")+"B"+TPV->TPV_CODBEM)
		While !Eof() .And. STJ->TJ_FILIAL == xFilial("STJ") .And.;
		STJ->TJ_TIPOOS == "B" .And. STJ->TJ_CODBEM == TPV->TPV_CODBEM

			If STJ->TJ_TERMINO = "N" .And. STJ->TJ_SITUACA = "L"
				If &(cCondi320)
					(cTRB)->(DbAppend())
					(cTRB)->ROTEIRO := TPV->TPV_CODROT
					(cTRB)->SEQROT  := TPV->TPV_SEQREL
					(cTRB)->CODBEM  := STJ->TJ_CODBEM
					(cTRB)->SERVICO := STJ->TJ_SERVICO
					(cTRB)->SEQRELA := STJ->TJ_SEQRELA
					(cTRB)->ORDEMSE := STJ->TJ_ORDEM
					(cTRB)->PLANO   := STJ->TJ_PLANO
					(cTRB)->DATAPRI := STJ->TJ_DTMPINI
					(cTRB)->CCUSTO  := STJ->TJ_CCUSTO
				EndIf
			EndIf
			DbSelectArea("STJ")
			DbSkip()
		End
		DbSelectArea("TPV")
		DbSkip()
	End
Return .T.