#INCLUDE "Mntr110.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR110  ³ Autor ³ Elisangela Costa      ³ Data ³ 04/06/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ordens de Servico por Localizacao                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR110()
	
	Local oReport
	Local aArea := GetArea()
	
	Local aNGBEGINPRM := NGBEGINPRM()
	
	Private cTRB110 := GetNextAlias()

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetPortrait() //Default Retrato
		oReport:PrintDialog()
	Else
		MNTR110R4()
	EndIf
	RestArea(aArea)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ReportDef³ Autor ³ Elisangela Costa      ³ Data ³ 05/06/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR110                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()
	Local oReport
	Local oSection1
	Local oSection2
	Local oCell

	//LAYOUT
	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123
	______________________________________________________________________________________________________________________________________________________________________________
	Ordens de Servico por Localizacao
	______________________________________________________________________________________________________________________________________________________________________________

	Localizacao      Descricao
	______________________________________________________________________________________________________________________________________________________________________________
	XXX              XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

	Ordem   Plano   Serviço Descrição                                 Seq.  Dt.Original  Dt.M.Prev.In.  Status
	_____________________________________________________________________________________________________________________________________________________________________________
	xxxxxx  xxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxx   99/99/9999   99/99/9999     Liberada
	xxxxxx  xxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxx   99/99/9999   99/99/9999     Liberada
	xxxxxx  xxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxx   99/99/9999   99/99/9999     Liberada


	*/

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR110",OemToAnsi(STR0001),"MNT110",{|oReport| ReportPrint(oReport)},STR0001+".") //"Ordens de Serviço por Localização"###"Ordens de Serviço por Localização"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     // De Localizacao                               ³
	//³ mv_par02     // Ate Localizacao    			   				  ³
	//³ mv_par03     // De Servico                                   ³
	//³ mv_par04     // Ate Servico                                  ³
	//³ mv_par05     // De data                                      ³
	//³ mv_par06     // Ate Data                                     ³
	//³ mv_par07     // OS Liberada (Sim/Nao)                        |
	//³ mv_par08     // OS Terminada (Sim/Nao)                       |
	//³ mv_par09     // OS Pendente (Sim/Nao                         |
	//³ mv_par10     // OS Cancelada (Sim/Nao)                       |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	Pergunte(oReport:uParam,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	oSection1 := TRSection():New(oReport,STR0014,{(cTRB110),"TAF","STS","STJ","ST4"})  //"Localização"
	TRCell():New(oSection1,"(cTRB110)->LOCALIZ ",(cTRB110),STR0014,/*Picture*/,3)  //"Localização"
	TRCell():New(oSection1,"TAF_NOMNIV"      ,"TAF"   ,STR0015,/*Picture*/,/*Tamanho*/)  //"Descrição"
	TRPosition():New(oSection1,"TAF",7,{|| xFilial("TAF") + "X2"+(cTRB110)->LOCALIZ})

	oSection2 := TRSection():New(oReport,STR0016,{(cTRB110),"ST4"}) //"Ordens de Servico"
	oCell := TRCell():New(oSection2,"(cTRB110)->ORDEM"  ,(cTRB110),STR0017,/*Picture*/,6,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Ordem"
	oCell := TRCell():New(oSection2,"(cTRB110)->PLANO"  ,(cTRB110),STR0018,/*Picture*/,6,/*lPixel*/,/*{|| code-block de impressao }*/) //"Plano"
	oCell := TRCell():New(oSection2,"(cTRB110)->SERVICO",(cTRB110),STR0019,/*Picture*/,6,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Serviço"
	oCell := TRCell():New(oSection2,"T4_NOME"        ,"ST4"   ,STR0015,/*Picture*/,40,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Descrição"
	oCell := TRCell():New(oSection2,"(cTRB110)->SEQUENC",(cTRB110),STR0020,/*Picture*/,3,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Seq."
	oCell := TRCell():New(oSection2,"(cTRB110)->DATORIG",(cTRB110),STR0021,"99/99/99",8,/*lPixel*/,/*{|| code-block de impressao }*/)   //"Dt.Original"
	oCell := TRCell():New(oSection2,"(cTRB110)->DATPRIN",(cTRB110),STR0022,"99/99/99",8,/*lPixel*/,/*{|| code-block de impressao }*/)   //"Dt.M.Prev.In."
	oCell := TRCell():New(oSection2,"STATUS"         ,"      ",STR0023,/*Picture*/,10,/*lPixel*/,{||MNT110RETS()})   //"Status"
	TRPosition():New(oSection2,"ST4",1,{|| xFilial("ST4") + (cTRB110)->SERVICO})

Return oReport


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR110R4³ Autor ³ Elisangela Costa      ³ Data ³ 05/06/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de O.s por localizacao                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR110                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR110R4()

	Local  WNREL     := "MNTR110"
	Local  LIMITE    := 132
	Local  cdesc1    := STR0024 //"Relatório de ordens de serviço por localização."
	Local  cdesc2    := STR0025 //"Permite listar OS terminadas, abertas ou todas de acordo"
	Local  cdesc3    := STR0026 //"com o parâmetro selecionado."
	Local  cSTRING   := "STJ"

	Private NOMEPROG  := "MNTR110"
	Private TAMANHO   := "M"
	Private aRETURN   := {STR0027,1,STR0028,1,2,1,"",1}   //"Zebrado"###"Administracao"
	Private nTIPO     := 0
	Private nLASTKEY  := 0
	Private Titulo    := STR0001 //"Ordens de Serviço por Localização"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as perguntas selecionadas                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     // De Localizacao                               ³
	//³ mv_par02     // Ate Localizacao    			   				  ³
	//³ mv_par03     // De Servico                                   ³
	//³ mv_par04     // Ate Servico                                  ³
	//³ mv_par05     // De data                                      ³
	//³ mv_par06     // Ate Data                                     ³
	//³ mv_par07     // OS Liberada (Sim/Nao)                        |
	//³ mv_par08     // OS Terminada (Sim/Nao)                       |
	//³ mv_par09     // OS Pendente (Sim/Nao                         |
	//³ mv_par10     // OS Cancelada (Sim/Nao)                       |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cPERG := "MNT110"
	
	Pergunte(cPERG,.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		dbSelectArea("STJ")
		Return
	EndIf
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND|MNTR110IM(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
	dbSelectArea("STJ")

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR110IM ³ Autor ³ Elisangela Costa      ³ Data ³ 04/06/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR110                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR110IM(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT := "",xx
	Local nCNTIMPR := 0
	Local oARQTR110

	Private lSEQSTJ := If(Ngverify("STJ"),.T.,.F.)
	Private aVETINR := {}
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis locais exclusivas deste programa                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	Private cUltimoReg, cCondicao, nAtual
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contadores de linha e pagina                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private li := 80 ,m_pag := 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se deve comprimir ou nao                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTIPO  := IIF(aRETURN[4]==1,15,18)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta os Cabecalhos                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	CABEC1 := STR0029 //"Localização Descrição"
	CABEC2 := STR0030       //"            Ordem   Plano   Serviço Descrição                                 Seq.  Dt.Original  Dt.M.Prev.In.  Status"

	/*/
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	************************************************************************************************************************************
	Localizacao Descricao
	Ordem   Plano   Servico Descricao                                 Seq.  Dt.Original  Dt.M.Prev.In.  Status
	************************************************************************************************************************************

	xxx         xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	xxxxxx  xxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxx   99/99/9999   99/99/9999     Liberada
	xxxxxx  xxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxx   99/99/9999   99/99/9999     Liberada
	xxxxxx  xxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxx   99/99/9999   99/99/9999     Pendente
	xxxxxx  xxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxx   99/99/9999   99/99/9999     Pendente


	*/
	aDBFR110 := {{"LOCALIZ" ,"C",03,0},;
				 {"ORDEM"   ,"C",06,0},;
				 {"PLANO"   ,"C",06,0},;
				 {"SERVICO" ,"C",06,0},;
				 {"SEQUENC" ,If(lSEQSTJ,"C","N"),3,0},;
				 {"DATORIG" ,"D",08,0},;
				 {"DATPRIN" ,"D",08,0},;
				 {"SITUACAO","C",01,0}}

	//Cria Tabela Temporária
	oARQTR110 := NGFwTmpTbl(cTRB110,aDBFR110,{{"LOCALIZ","DATORIG","DATPRIN","ORDEM"}})

	Processa({|lEND| MNTR110TRB()},STR0031) //"Processando as Ordens de Servico..."

	dbselectarea(cTRB110)
	dbGotop()
	SetRegua(LastRec())
	While !Eof()

		cLOCALIZ := (cTRB110)->LOCALIZ
		NGSOMALI(58)
		@ Li,000 Psay cLOCALIZ
		@ Li,012 Psay NGSEEK("TAF","X2"+cLOCALIZ,7,"TAF_NOMNIV")
		NGSOMALI(58)

		dbSelectArea(cTRB110)
		While !Eof() .And. (cTRB110)->LOCALIZ == cLOCALIZ

			IncRegua()
			NGSOMALI(58)
			@ Li,012 Psay (cTRB110)->ORDEM Picture "@!"
			@ Li,020 Psay (cTRB110)->PLANO Picture "@!"
			@ Li,028 Psay (cTRB110)->SERVICO Picture "@!"
			@ Li,036 Psay NGSEEK("ST4",(cTRB110)->SERVICO,1,"T4_NOME") Picture "@!"
			@ Li,078 Psay (cTRB110)->SEQUENC
			@ Li,084 Psay (cTRB110)->DATORIG Picture "99/99/99"
			@ Li,097 Psay (cTRB110)->DATPRIN Picture "99/99/99"
			If (cTRB110)->SITUACAO == "S"
				@ Li,112 Psay STR0032 //"Terminada"
			ElseIf (cTRB110)->SITUACAO == "L"
				@ Li,112 Psay STR0033  //"Liberada"
			ElseIf (cTRB110)->SITUACAO == "P"
				@ Li,112 Psay STR0034  //"Pendente"
			Else
				@ Li,112 Psay STR0035  //"Cancelada"
			EndIf
			dbSelectArea(cTRB110)
			dbSkip()
		End
		NGSOMALI(58)
	End

	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Apaga arquivo de Trabalho                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex("STS")
	RetIndex("STJ")
	RetIndex("TAF")
	RetIndex("ST4")
	Set Filter To
	Set Device To Screen
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

	//Deleta o arquivo temporario fisicamente
	oARQTR110:Delete()

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³ Elisangela Costa      ³ Data ³ 05/06/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ReportDef                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)
	
	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local oARQTR110 

	Private aVETINR := {}
	Private lSEQSTJ := If(Ngverify("STJ"),.T.,.F.)

	aDBFR110 := {{"LOCALIZ" ,"C",03,0},;
				 {"ORDEM"   ,"C",06,0},;
				 {"PLANO"   ,"C",06,0},;
				 {"SERVICO" ,"C",06,0},;
				 {"SEQUENC" ,If(lSEQSTJ,"C","N"),3,0},;
				 {"DATORIG" ,"D",08,0},;
				 {"DATPRIN" ,"D",08,0},;
				 {"SITUACAO","C",01,0}}

	//Cria Tabela Temporária
	oARQTR110 := NGFwTmpTbl(cTRB110,aDBFR110,{{"LOCALIZ","DATORIG","DATPRIN","ORDEM"}})

	Processa({|lEND| MNTR110TRB()},STR0031)  //"Processando as Ordens de Servico..."

	dbselectarea(cTRB110)
	dbGotop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()

		cLOCALIZ := (cTRB110)->LOCALIZ

		oSection1:Init()
		oSection1:PrintLine()
		oSection2:Init()
		While !Eof() .And. !oReport:Cancel() .And. (cTRB110)->LOCALIZ == cLOCALIZ

			oReport:IncMeter()
			oSection2:PrintLine()

			dbSelectArea(cTRB110)
			dbSkip()
		End
		oSection1:Finish()
		oSection2:Finish()
	End

	//Deleta o arquivo temporario fisicamente
	oARQTR110:Delete()

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR110TRB³ Autor ³ Elisangela Costa      ³ Data ³05/06/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os arquivos e carrega arquivo temporario           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR110                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR110TRB()

	//Ordena parametros De/Ate Localização
	If MV_PAR01 > MV_PAR02
		cTempPar  := MV_PAR01
		MV_PAR01 := MV_PAR02
		MV_PAR02 := cTempPar
	EndIf

	cCONDSTJ := 'STJ->TJ_SERVICO >= MV_PAR03 .And. STJ->TJ_SERVICO <= MV_PAR04 .And.'
	cCONDSTJ := cCONDSTJ + 'STJ->TJ_DTMPINI >= MV_PAR05 .And. STJ->TJ_DTMPINI <= MV_PAR06'

	cCONDSTS := 'STS->TS_SERVICO >= MV_PAR03 .And. STS->TS_SERVICO <= MV_PAR04 .And.'
	cCONDSTS := cCONDSTS + 'STS->TS_DTMPINI >= MV_PAR05 .And. STS->TS_DTMPINI <= MV_PAR06'

	cMV_PAR01 := MV_PAR01+Space(Len(STJ->TJ_CODBEM)-Len(MV_PAR01))
	cMV_PAR02 := MV_PAR02+Space(Len(STJ->TJ_CODBEM)-Len(MV_PAR02))
	dbselectarea("STJ")
	dbSetOrder(2)
	dbSeek(xFILIAL("STJ")+"L"+cMV_PAR01,.T.)
	ProcRegua(LastRec())
	While !Eof() .And. STJ->TJ_FILIAL = xFILIAL("STJ") .And.;
	STJ->TJ_TIPOOS == "L" .And. STJ->TJ_CODBEM <= cMV_PAR02

		IncProc()
		If &(cCONDSTJ)

			If STJ->TJ_SITUACA == "L" .And. STJ->TJ_TERMINO == "N"
				If MV_PAR07 == 2 // Liberadas
					dbSkip()
					Loop
				EndIf
			ElseIf STJ->TJ_SITUACA == "L" .And. STJ->TJ_TERMINO == "S"
				If MV_PAR08 == 2 // Terminadas
					dbSkip()
					Loop
				EndIf
			ElseIf STJ->TJ_SITUACA == "P" .And. STJ->TJ_TERMINO == "N"
				If MV_PAR09 == 2 // Pendentes
					dbSkip()
					Loop
				EndIf
			Else
				If MV_PAR10 == 2 // Canceladas
					dbSkip()
					Loop
				EndIf
			EndIf

			dbSelectArea(cTRB110)
			(cTRB110)->(DbAppend())
			(cTRB110)->LOCALIZ  := SubStr(STJ->TJ_CODBEM,1,3)
			(cTRB110)->ORDEM    := STJ->TJ_ORDEM
			(cTRB110)->PLANO    := STJ->TJ_PLANO
			(cTRB110)->SERVICO  := STJ->TJ_SERVICO
			(cTRB110)->SEQUENC  := If(lSEQSTJ,STJ->TJ_SEQRELA,STJ->TJ_SEQUENC)
			(cTRB110)->DATORIG  := STJ->TJ_DTORIGI
			(cTRB110)->DATPRIN  := STJ->TJ_DTMPINI
			(cTRB110)->SITUACAO := If(STJ->TJ_TERMINO == "S","S",STJ->TJ_SITUACA)
		EndIf
		dbSelectarea("STJ")
		dbSkip()
	End

	If MV_PAR08 == 1 .Or. MV_PAR10 == 1

		dbselectarea("STS")
		dbSetOrder(2)
		dbSeek(xFILIAL("STS")+"L"+cMV_PAR01,.T.)
		ProcRegua(LastRec())
		While !Eof() .And. STS->TS_FILIAL = xFILIAL("STS") .And.;
		STS->TS_TIPOOS == "L" .And. STS->TS_CODBEM <= cMV_PAR02

			IncProc()
			If &(cCONDSTS)

				If STS->TS_SITUACA == "L"
					If MV_PAR08 == 2 // Terminadas
						dbSkip()
						Loop
					EndIf
				Else
					If MV_PAR10 == 2 // Canceladas
						dbSkip()
						Loop
					EndIf
				EndIf

				dbSelectArea(cTRB110)
				(cTRB110)->(DbAppend())
				(cTRB110)->LOCALIZ  := SubStr(STS->TS_CODBEM,1,3)
				(cTRB110)->ORDEM    := STS->TS_ORDEM
				(cTRB110)->PLANO    := STS->TS_PLANO
				(cTRB110)->SERVICO  := STS->TS_SERVICO
				(cTRB110)->SEQUENC  := If(lSEQSTJ,STS->TS_SEQRELA,STS->TS_SEQUENC)
				(cTRB110)->DATORIG  := STS->TS_DTORIGI
				(cTRB110)->DATPRIN  := STS->TS_DTMPINI
				(cTRB110)->SITUACAO := If(STS->TS_TERMINO == "S","S",STS->TS_SITUACA)
			EndIf
			dbSelectarea("STS")
			dbSkip()
		End
	EndIf

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT110RETS³ Autor ³ Elisangela Costa      ³ Data ³ 05/06/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o status da OS                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³REPORTDEF                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNT110RETS()
	Local cStatus := ""

	If (cTRB110)->SITUACAO == "S"
		cStatus := STR0032 //"Terminada"
	ElseIf (cTRB110)->SITUACAO == "L"
		cStatus := STR0033  //"Liberada"
	ElseIf (cTRB110)->SITUACAO == "P"
		cStatus := STR0034  //"Pendente"
	Else
		cStatus := STR0035  //"Cancelada"
	EndIf

Return cStatus