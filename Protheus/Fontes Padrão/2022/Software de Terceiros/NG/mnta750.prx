#INCLUDE "MNTA750.ch"
#INCLUDE "PROTHEUS.CH"

//Posições para o Vetor aINSPE
Static __EIXO__   := 1
Static __CODBEM__ := 2
Static __CODLOC__ := 3
Static __SULCO1__ := 4
Static __SULCO2__ := 5
Static __SULCO3__ := 6

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA750  ³ Autor ³ Thiago Olis Machado   ³ Data ³11/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Registrar as inspecoes realizadas                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function MNTA750
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Guarda conteudo e declara variaveis padroes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM()

Local oDlg,oEnchoice,i, cChumba, oPainel01
Local cCadastro := STR0021 //"Registro de Inspeções"
Local aChoice   := {"TR9_ORDEM","TR9_CICLO","TR9_FROTA","TR9_NOME","TR9_PLACA",;
		              "TR9_CODFAM","TR9_DESFAM","TR9_TIPMOD","TR9_DESMOD"}
Local aSize := MsAdvSize(,.f.,430), aObjects := {}, aInfo := {}

Private aCols := {}
Private aRotina := MenuDef()
Private oMenu
Private aHeader := {}

aAdd(aObjects,{100,100,.t.,.t.})
aInfo   := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
aPosObj := MsObjSize(aInfo, aObjects,.t.)

DbSelectArea("TR9")
DbSetOrder(1)
RegToMemory("TR9",.T.)

DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL
oPainel01 := TPanel():New(00,00,,oDLG,,,,,,0,0,.F.,.F.)
oPainel01:Align := CONTROL_ALIGN_ALLCLIENT

oEnchoice := Msmget():New("TR9", 1, 3,,,,aChoice,aPosObj[1],,3,,,,oPainel01)
oEnchoice:oBox:Align := CONTROL_ALIGN_ALLCLIENT

//Utilizado para retirar o foco da enchoice - P.O.G.
@ -1,-1 MSGET cChumba SIZE 1,1 OF oPainel01 PIXEL Picture "@!"

NGPOPUP(asMenu,@oMenu)
oPainel01:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPainel01)}

ACTIVATE DIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| lOk := .t.,If(!Mnt750Chk(),lOk := .f. , oDlg:End()) },{|| lOk := .f.,oDlg:End()})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna conteudo de variaveis padroes       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT750CHK ³ Autor ³ Thiago Olis Machado   ³ Data ³11/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Checagem das inspecoes                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mnt750Chk()
Local cMsgCiclo   := AllTrim(M->TR9_CICLO)
Local cMsgVeiculo := AllTrim(M->TR9_FROTA)
Local lEnd := .f.

If Empty(M->TR9_ORDEM)
	Help(" ",1,"OBRIGAT")
	Return .f.
EndIf

If !MsgYesNo(STR0008+cMsgCiclo+STR0009; //"Deseja iniciar a inspeção do "###" ciclo para o veículo "
   				+cMsgVeiculo,STR0010)   //"ATENÇÃO"
	Return .f.
EndIf
If MNTOPEN232(M->TR9_FROTA) .And.  NGCADICBASE("TR9_NUMCAL","D","TR9",.F.)
    Processa({ |lEnd| MNTA995Gra(,,1,M->TR9_FROTA, M->TR9_CICLO, M->TR9_ORDEM)}, STR0011)//"Processando inspeção" (Modo Gráfico)
Else
    Processa({ |lEnd| Mnt750Pro()}, STR0011)//"Processando inspeção"
EndIf
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT750PRO ³ Autor ³ Thiago Olis Machado   ³ Data ³11/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria a tela de inspecao                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mnt750Pro()
Local oDlgPro
Local nTop := 0
Local cCadastro := STR0021 //"Registro de Inspeções"
Local oPnlTop, oPnlGet, oGet
Local aSize := MsAdvSize(,.f.,430)
Private cHora    := Time()
Private nPoscont := 0
Private dDtInsp := dDataBase,dDtDigit := dDataBase
Private nCnt := 0
Private cTCBe := NGSEEK("ST9",M->TR9_FROTA,1,"T9_TEMCONT")
Private oMenu

If cTCBe == "S"
	lCont := .t.
Else
	lCont := .f.
EndIf
If SetMdiChild()
	nTop := 0
	aSize[6] -= 30
Else
	nTop := aSize[7]
Endif

Define MsDialog oDlgPro Title cCadastro From nTop,0 To aSize[6],aSize[5] PIXEL
//------------
oPnlTop := TPanel():New(00,00,,oDlgPro,,,,,,00,00,.F.,.F.)
oPnlTop:Align := CONTROL_ALIGN_TOP
oPnlTop:nHeight := 180
If aSize[6] > 600
	oPnlTop:nHeight := 200
Endif

//------------
@ 010,010 Say OemToAnsi(RetTitSX3("TR9_ORDEM")) Of oPnlTop Pixel COLOR CLR_HBLUE
@ 008,049 Msget M->TR9_ORDEM  Picture "@!" SIZE 48,08 Of oPnlTop Pixel When .f.
@ 010,150 Say OemToAnsi(RetTitSX3("TR9_CICLO")) Of oPnlTop Pixel
@ 008,187 Msget M->TR9_CICLO  Picture "@!" SIZE 28,08 When .f. Of oPnlTop Pixel

@ 023,010 Say OemToAnsi(RetTitSX3("TR9_FROTA")) Of oPnlTop Pixel
@ 021,049 Msget M->TR9_FROTA  Picture "@!" SIZE 48,08 When .f. Of oPnlTop Pixel
@ 023,150 Say OemToAnsi(RetTitSX3("TR9_PLACA")) Of oPnlTop Pixel
@ 021,187 Msget M->TR9_PLACA  Picture "@!" SIZE 48,08 When .f. Of oPnlTop Pixel

@ 036,010 Say OemToAnsi(RetTitSX3("TR9_CODFAM")) Of oPnlTop Pixel
@ 034,049 Msget M->TR9_CODFAM  Picture "@!" SIZE 48,08 When .f. Of oPnlTop Pixel
@ 036,150 Say OemToAnsi(RetTitSX3("TR9_DESFAM")) Of oPnlTop Pixel
@ 034,187 Msget M->TR9_DESFAM  Picture "@!" SIZE 75,08 When .f. Of oPnlTop Pixel

@ 049,010 Say OemToAnsi(RetTitSX3("TR9_TIPMOD")) Of oPnlTop Pixel
@ 047,049 Msget M->TR9_TIPMOD  Picture "@!" SIZE 48,08 When .f. Of oPnlTop Pixel
@ 049,150 Say OemToAnsi(RetTitSX3("TR9_DESMOD")) Of oPnlTop Pixel
@ 047,187 Msget M->TR9_DESMOD  Picture "@!" SIZE 75,08 When .f. Of oPnlTop Pixel

@ 062,010 Say OemToAnsi(RetTitSX3("TR9_DTINSP")) Of oPnlTop Pixel COLOR CLR_HBLUE
@ 060,049 Msget dDtInsp  Picture "99/99/9999" SIZE 48,08  Of oPnlTop Pixel Valid If(!Empty(dDtInsp),Mnt750Vh(),.t.) HASBUTTON
@ 062,150 Say OemToAnsi(RetTitSX3("TR9_HRINSP")) Of oPnlTop Pixel COLOR CLR_HBLUE
@ 060,187 Msget cHora  Picture "99:99" SIZE 38,08 Of oPnlTop Pixel Valid If(!Empty(cHora),NgValHora(cHora,.t.),.t.) .And. MNTA750CCB(M->TR9_FROTA, dDtInsp, cHora)

@ 075,010 Say OemToAnsi(RetTitSX3("TR9_KMATU")) Of oPnlTop Pixel COLOR CLR_HBLUE
@ 073,049 Msget nPoscont  Picture "@E 999,999,999" SIZE 48,08 When lCont .And. (!FindFunction("NGBlCont") .Or. NGBlCont( M->TR9_FROTA )) Of oPnlTop Pixel
@ 075,150 Say OemToAnsi(RetTitSX3("TR9_DTDIGI")) Of oPnlTop Pixel
@ 073,187 Msget dDtDigit  Picture "99/99/9999" SIZE 48,08 Of oPnlTop Pixel When .f. HASBUTTON

Dbselectarea("TRC")
Dbsetorder(1)

aHeader := CabecGetD("TRC",{"TRC_ORDEM","TRC_PLANO","TRC_CICLO"})
ADHeadRec("TRC",aHeader)

nCnt := 0
DbSelectArea("TQ1")
DbSetOrder(1)
DbSeek(xFilial("TQ1")+M->TR9_CODFAM+M->TR9_TIPMOD)
While !Eof() .And. TQ1->TQ1_FILIAL == xFILIAL("TQ1");
	.And. TQ1->TQ1_DESENH == M->TR9_CODFAM;
	.And. TQ1->TQ1_TIPMOD == M->TR9_TIPMOD

	aAdd(aCols,{TQ1->TQ1_EIXO,;//Código do Eixo
				TQ1->TQ1_QTDPNE,;//Quantidade de Pneus
				Space(16),;//Número do Fogo 1
				TQ1->TQ1_LOCPN1,;//Localização 1
				0,;//Medida 1 - Pneu 1
				0,;//Medida 2 - Pneu 1
				0,;//Medida 3 - Pneu 1
				Space(16),;//Número do Fogo 2
				TQ1->TQ1_LOCPN2,;//Localização 2
				0,;//Medida 1 - Pneu 2
				0,;//Medida 2 - Pneu 2
				0,;//Medida 3 - Pneu 2
				Space(16),;//Número do Fogo 3
				TQ1->TQ1_LOCPN3,;//Localização 3
				0,;//Medida 1 - Pneu 3
				0,;//Medida 2 - Pneu 3
				0,;//Medida 3 - Pneu 3
				Space(16),;//Número do Fogo 4
				TQ1->TQ1_LOCPN4,;//Localização 4
				0,;//Medida 1 - Pneu 4
				0,;//Medida 2 - Pneu 4
				0,;//Medida 3 - Pneu 4
				Space(16),;//Número do Fogo 5
				TQ1->TQ1_LOCPN5,;//Localização 5
				0,;//Medida 1 - Pneu 5
				0,;//Medida 2 - Pneu 5
				0,;//Medida 3 - Pneu 5
				Space(16),;//Número do Fogo 6
				TQ1->TQ1_LOCPN6,;//Localização 6
				0,;//Medida 1 - Pneu 6
				0,;//Medida 2 - Pneu 6
				0,;//Medida 3 - Pneu 6
				Space(16),;//Número do Fogo 7
				TQ1->TQ1_LOCPN7,;//Localização 7
				0,;//Medida 1 - Pneu 7
				0,;//Medida 2 - Pneu 7
				0,;//Medida 3 - Pneu 7
				Space(16),;//Número do Fogo 8
				TQ1->TQ1_LOCPN8,;//Localização 8
				0,;//Medida 1 - Pneu 8
				0,;//Medida 2 - Pneu 8
				0,;//Medida 3 - Pneu 8
				Space(16),;//Número do Fogo 9
				TQ1->TQ1_LOCPN9,;//Localização 9
				0,;//Medida 1 - Pneu 9
				0,;//Medida 2 - Pneu 9
				0,;//Medida 3 - Pneu 9
				Space(16),;//Número do Fogo 10
				TQ1->TQ1_LOCPN0,;//Localização 10
				0,;//Medida 1 - Pneu 10
				0,;//Medida 2 - Pneu 10
				0,;//Medida 3 - Pneu 10
				"TRC",;//Alias
				TRC->(Recno()),;//Recno TRC
				.F.})//Não deletada
	nCnt++

	Dbselectarea("TQ1")
	DbSkip()
End

If Empty(aCOLS)//Caso aCols fique vazio, adiciona linha em branco
   aCols := BlankGetD(aHeader)
Endif

//Getdados
oPnlGet := TPanel():New(00,100,,oDlgPro,,,,,,600,24,.F.,.F.)
oPnlGet:Align := CONTROL_ALIGN_ALLCLIENT
oGet := MSGetDados():New(0,0,0,0,3,"AllwaysTrue","AllwaysTrue","",.F.,,,,nCnt,,,oPnlGet)
oGet:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

//Ativa click da direita nos dois panels pricipais
NGPOPUP(asMenu,@oMenu)
oPnlTop:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPnlTop)}
oPnlGet:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPnlGet)}

Activate Dialog oDlgPro On Init(EnchoiceBar(oDlgPro,{|| If(Mnt750OK(cHora, dDtInsp, dDtDigit, nPOSCONT),oDlgPro:End(),lOk := .f.)},{|| oDlgPro:End()}))

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT750OS  ³ Autor ³ Thiago Olis Machado   ³ Data ³11/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a OS e traz as informacoes adicionais               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mnt750Os()
	Local lRet 		:= .T.
	Local nRecInsp 	:= 0
	Local cNGSINSP	:= AllTrim(GETMV("MV_NGSINSP"))

	cAliasQry := GetNextAlias()
	cQuery := " SELECT TJ_SERVICO, R_E_C_N_O_ "
	cQuery += " FROM " + RetSQLName("STJ")
	cQuery += " WHERE TJ_ORDEM   = '"+M->TR9_ORDEM+"'"
	cQuery += "   AND TJ_FILIAL  = '"+xFilial("STJ")+"'"
	cQuery += "   AND D_E_L_E_T_ <> '*' "
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
	dbGoTop()
	If Eof()
		Help(" ",1,"REGNOIS")
		lRet := .f.
	Else
		While !Eof()
			If AllTrim((cAliasQry)->TJ_SERVICO) == cNGSINSP
				nRecInsp := (cAliasQry)->R_E_C_N_O_
				Exit
			EndIf
			dbSkip()
		End
		If nRecInsp == 0
			Help(" ",1,"NGATENCAO",,STR0023,3,1)  //"O.S. não é de Inspeção!"
			lRet := .f.
		Else
			dbSelectArea("STJ")
			dbGoTo(nRecInsp)
		Endif
	Endif
	(cAliasQry)->(dbCloseArea())

	If !lRet
		Return .f.
	Endif

	If STJ->TJ_TERMINO = "S"
		Help(" ",1,"NGATENCAO",,STR0019,3,1)
		Return .F.
	EndIf

	If STJ->TJ_SITUACA <> "L"
		Help(" ",1,"NGATENCAO",,STR0022,3,1)  //"O.S. não foi liberada!"
		Return .F.
	EndIf

	M->TR9_FROTA  := STJ->TJ_CODBEM
	M->TR9_PLANO  := STJ->TJ_PLANO

	Dbselectarea("ST9")
	Dbsetorder(16)
	IF !Dbseek(M->TR9_FROTA)
		MsgInfo(STR0012,STR0013) //"Este Bem nao esta cadastrado..."###"ATENCAO"
		Return .f.
	Else
		If ST9->T9_SITBEM != "A"
			MsgInfo(STR0014,STR0013)  //"Bem não está ativo!"###"ATENCAO"
			Return .f.
		Endif

		//---------------------------------------------------------------------------------
		// verifica se há pneus na estrutura com status aguardando marcação de fogo
		//---------------------------------------------------------------------------------
		If FindFunction( 'MNTAGFOGO' ) .And. !Empty( MNTAGFOGO( STJ->TJ_CODBEM ) )
			Return .F.
		EndIf

		Dbselectarea("TQ0")
		Dbsetorder(1)
		If !Dbseek(xFilial("TQ0")+ST9->T9_CODFAMI+ST9->T9_TIPMOD)
			MsgInfo(STR0020,STR0013) //"Esquema padrão não cadastrado"##"ATENCAO"
			Return .f.
		Else
			Dbselectarea("TQ1")
			Dbsetorder(1)
			If !Dbseek( xFilial("TQ1")+ST9->T9_CODFAMI+ST9->T9_TIPMOD)
				MsgInfo(STR0015,STR0013) //"Nao Existe Itens Para Esquema de Rodado "###"ATENCAO"
				Return .f.
			Endif
		Endif
		M->TR9_PLACA   := ST9->T9_PLACA
		M->TR9_CODFAM  := ST9->T9_CODFAMI
		M->TR9_NOME	   := ST9->T9_NOME
		DbSelectArea("ST6")
		DbSetOrder(1)
		If DbSeek(xFilial("ST6")+M->TR9_CODFAM)
			M->TR9_DESFAM := ST6->T6_NOME
		EndIf
		M->TR9_TIPMOD  := ST9->T9_TIPMOD

		DbSelectArea("TQR")
		DbSetOrder(1)
		If DbSeek(xFilial("TQR")+M->TR9_TIPMOD)
			M->TR9_DESMOD := STJ->TJ_CODBEM
		EndIf

	Endif

	M->TR9_CICLO  := Mnt750Cic()

	//VALIDA SE FROTA JA FOI INSPECIONADA
	If Val(M->TR9_CICLO) > 1
		cCiclo := AllTrim(Str(Val(M->TR9_CICLO) - 1))
		DbSelectArea("TR9")
		DbSetOrder(1)
		If TR9->(Dbseek(xFILIAL("TR9")+M->TR9_ORDEM+M->TR9_PLANO+cCiclo))
			If TR9->TR9_STATUS == "3"
				If !MsgYesNo(STR0024,STR0010) //"Veículo já inspecionado sem constar inconsistência! Deseja lançar mais um Ciclo?"###"ATENÇÃO"
					Return .F.
				EndIf
			EndIf
		Endif
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT750CIC ³ Autor ³ Thiago Olis Machado   ³ Data ³12/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Traz o proximo ciclo daquela OS.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Mnt750Cic()
nCiclo := 0
cAliasQry := GetNextAlias()

cQuery := " SELECT MAX(TR9_CICLO) CICLO "
cQuery += " FROM " + RetSQLName("TR9")
cQuery += " WHERE D_E_L_E_T_<>'*' and "
cQuery += " TR9_ORDEM = '"+M->TR9_ORDEM+"' and "
cQuery += " TR9_PLANO = '"+STJ->TJ_PLANO+"'"

cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

While !EOF()
	If Empty((cALIASQRY)->CICLO)
		nCiclo := 1
	Else
		nCiclo := Val((cALIASQRY)->CICLO)+1
	EndIf

	DbSelectArea(cALIASQRY)
	DbSkip()
End
(cALIASQRY)->( dbCloseArea() )
cCiclo := AllTrim(Str(nCiclo))
Return cCiclo

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³RetTitSX3 ³ Autor ³ Thiago Olis Machado   ³ Data ³12/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Traz o titulo do SX3                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function RetTitSX3( cCampo, lLanguage )

Local cTitCpo := ""

Default lLanguage := .T.

cTitCpo := Posicione("SX3",2, cCampo, "X3Titulo()")

Return cTitCpo

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT750VH  ³ Autor ³ Thiago Olis Machado   ³ Data ³12/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a data e a hora digitada.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mnt750Vh()
IF !Empty(dDtInsp) .And. !Empty(cHora)
	If dDtInsp < STJ->TJ_DTORIGI
 		Help(" ",1,"NGATENCAO",,STR0016,3,1) //"Data informada não pode ser menor que a data de abertura da O.S."
		Return .f.
	ElseIf dDtInsp > dDataBase
		Help(" ",1,"NGATENCAO",,STR0017,3,1) //"Data informada não pode ser maior que a data corrente."
		Return .f.
	ElseIf !Empty(STJ->TJ_DTMPINI)
		If dDtInsp < STJ->TJ_DTMPINI
		   	If !MsgYesNo(STR0031+" '"+DToC(STJ->TJ_DTMPINI)+"'."+STR0032,STR0010) // "Data de inicio informada e menor do que a data prevista para inicio da OS." ## "Deseja continuar?" ## "ATENÇÃO"
		      	Return .F.
		   	EndIf
		EndIf
	EndIf
EndIf

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT750OK  ³ Autor ³ Thiago Olis Machado   ³ Data ³12/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Traz o proximo ciclo daquela OS.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mnt750OK(cHora, dDtInsp, dDtDig, nPOSCONT, cNumCal, aINSPE)
Local i,j,x
Local nLocali
Local nPosSTP := 0
Local cHoraAux := cHora
Local dDtInsAu := dDtInsp
Local lTroca := .F.
Local lEntrou := .F.

If MNTOPEN232(M->TR9_FROTA) .And.  NGCADICBASE("TR9_NUMCAL","D","TR9",.F.)//Rotina nova
    Private cTCBe := NGSEEK("ST9",M->TR9_FROTA,1,"T9_TEMCONT")

    dbSelectArea("STP")
    dbSetOrder(9)
    While dbSeek(M->TR9_FROTA + DtoS(dDtInsAu) + cHoraAux)
        lEntrou := .T.
        nPosSTP := STP->TP_POSCONT

        If nPosSTP <= nPOSCONT
            cHoraAux := NGSOMAHCAR(cHoraAux, "00:01")//Incrementa 1 min no horário da inspeção, para não ocorrer inconsistência de contador
            If cHoraAux == "24:00" .Or. cHoraAux == "00:00"//Se a nova hora for meia noite
                dDtInsAu++
                //Formata o horário para gravação na base
                cHoraAux := "00:00"
            EndIf
            lTroca := .T.
         Else
            lTroca := .F.
            Exit
         EndIf

    EndDo

    cHoraAux := cHora
    dDtInsAu := dDtInsp

    If !lTroca .And. lEntrou
        dbSelectArea("STP")
        dbSetOrder(9)
        While dbSeek(M->TR9_FROTA + DtoS(dDtInsAu) + cHoraAux)

            nPosSTP := STP->TP_POSCONT

            If nPosSTP >= nPOSCONT
                cHoraAux := NGRETIRAHOR(cHoraAux, "00:01")[1]//Decrementa 1 min no horário da inspeção, para não ocorrer inconsistência de contador
                If cHoraAux == "23:59"//Se a nova hora for meia noite
                    dDtInsAu--
                EndIf
                lTroca := .T.
            Else
                Exit
                ShowHelpDlg(STR0013,{STR0033},2,{STR0034},2)
                Return .F.
            EndIf
        EndDo
    EndIf

    cHora := cHoraAux
    dDtIns := dDtInsAu

    PrepGraf()
    If Len(aINSPE) <> 0 //.And. MNTOPEN232(M->TR9_FROTA)
       //aCols := aINSPE
       For i := 1 to Len(aCols)
           For j := 4 to (Len(aCols[i]) - 2) Step 5
               //For k := 1 to Len(aINSPE)
                   If !Empty(aCols[i][j])
                       nLocali := aScan(aINSPE, {|x| x[3] == aCols[i][j]})
                       If nLocali > 0
                           aCols[i][j-1] := aINSPE[nLocali][__CODBEM__]
                           aCols[i][j+1] := aINSPE[nLocali][__SULCO1__]
                           aCols[i][j+2] := aINSPE[nLocali][__SULCO2__]
                           aCols[i][j+3] := aINSPE[nLocali][__SULCO3__]
                       EndIf
                   EndIf
               //Next k
           Next j
       Next i
    EndIf
Else
    If cTCBe == "S"
        If !NgChkHisto(M->TR9_FROTA,dDtInsp,nPoscont,cHora,1,,.T.)
            Return .F.
        Endif

        If !NgValiVard(M->TR9_FROTA,nPoscont,dDtInsp,cHora,1,.t.)
            Return .F.
        Endif

        NGTRETCON(M->TR9_FROTA,dDtInsp,nPoscont,cHora,1,,,"C",STJ->TJ_FILIAL)
    EndIf

    If Empty(dDtInsp) .or. Empty(cHora) .or. (Empty(nPoscont) .And. cTCBe == "S" )
        Help(" ",1,"OBRIGAT")
       Return .f.
    EndIf

    For i:= 1 To Len(aCols)
        nFogo := 3
        nSulco:= 5
        For x := 1 To aCols[i][2]
            If Empty(aCols[i][nFogo]) .and. AllTrim(aCols[i][1]) <> "RESERVA"
                Help(" ",1,"NGATENCAO",,STR0018,3,1) //"Número de fogo não informado."
                Return .f.
            EndIf
            If aCols[i][nSulco] <= 0 .Or. aCols[i][nSulco+1] <= 0 .Or. aCols[i][nSulco+2] <= 0
                If AllTrim(aCols[i][1]) <> "RESERVA"
                    Help(" ",1,"NGATENCAO",,STR0025,3,1)  //"Sulco informado deve ser maior que zero!"
                    Return .f.
                ElseIf AllTrim(aCols[i][1]) == "RESERVA" .And. !Empty(aCols[i][nFogo])
                    Help(" ",1,"NGATENCAO",,STR0025,3,1)  //"Sulco informado deve ser maior que zero!"
                    Return .f.
                EndIf
            EndIf
            nSulco += 5
            nFogo  += 5
        Next x
    Next i
EndIf

Mnt750Grv(cHora, dDtInsp, dDtDig, cNumCal, nPOSCONT)
MNT750INC()
Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT750lOK ³ Autor ³ Thiago Olis Machado   ³ Data ³15/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se o numero de fogo esta OK                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mnt750lOK(cLocaliz,nPneuX)
	Local xy,yy,xp,vp,yp
	Local i,ix
	Local lDirecional 	:= .F.
	Local cFam 			:= ""
	Local cNGSTARS		:= GETMV("MV_NGSTARS")

	aArraPn := {3,8,13,18,23,28,33,38,43,48}
	nColFa  := aArraPn[nPneuX]+1
	cLOCOLD := aCols[n][aArraPn[nPneuX]]
	lSairPl := .F.

	For xy := 1 to len(aCols)
		For yy := 1 to len(aArraPn)
			If yy = nPneuX .And. xy = n
			Else
				If aCols[xy][aArraPn[yy]] == cLocaliz
					Help(" ",1,"JAEXISTINF",,STR0026,3,1)    //"Esse pneu já foi informado em outra localização."
					&(READVAR()) := cLocOld
					aCols[n][aArraPn[nPneuX]] := cLocOld
					cCampo := cLocOld
					lRefresh := .t.
					Return .f.
				EndIf
			Endif
		Next yy
		If lSairPl
			Exit
		Endif
	Next xy

	//Valida se pneu informado nao esta com status removido de servico
	DbSelectArea("ST9")
	DbSetOrder(1)
	DbSeek(xFilial("ST9")+cLocaliz)
	cFam := ST9->T9_CODFAMI
	If ST9->T9_STATUS == cNGSTARS
		MsgStop(STR0027,STR0010) //"Pneu informado esta com Status = Removido de Serviço, informe outro."###"ATENÇÃO"
		Return .F.
	EndIf
	//Valida se posicao eh eixo direcional
	DbSelectArea("TQS")
	DbSetOrder(1)
	DbSeek(xFilial("TQS")+cLocaliz)

	DbSelectArea("ST9")
	DbSetOrder(1)
	DbSeek(xFilial("ST9")+M->TR9_FROTA)

	DbSelectArea("TQ0")
	DbSetOrder(1)
	DbSeek(xFilial("TQ0")+ST9->T9_CODFAMI+ST9->T9_TIPMOD)

	DbSelectArea("TQ1")
	DbSetOrder(1)
	DbSeek(xFilial("TQ1")+TQ0->TQ0_DESENH+TQ0->TQ0_TIPMOD)
	While !Eof() .And. TQ1->TQ1_FILIAL == xFilial("TQ1") .And. TQ1->TQ1_DESENH == TQ0->TQ0_DESENH;
		.And. TQ1->TQ1_TIPMOD == TQ0->TQ0_TIPMOD
		//If TQ1->TQ1_TIPEIX = '3'
		For yy := 1 to len(aArraPn)
			ix := If(yy = 10,"0",AllTrim(Str(yy)))
			cLoc := &("TQ1->TQ1_LOCPN"+ix)
			cFamP := &("TQ1->TQ1_FAMIL"+ix)
			If cLoc == aCols[n][aArraPn[nPneuX]+1]
				If TQ1->TQ1_TIPEIX = "3" .And. TQS->TQS_BANDAA > "1"
					lDirecional := .t.
				EndIf
				If cFam <> cFamP
					MsgStop(STR0028,STR0010) //"A medida do pneu informado não se adequa a estrutura da frota!"###"ATENÇÃO"
					Return .f.
				EndIf
			EndIf
		Next yy
		//EndIf
		DbSelectArea("TQ1")
		DbSkip()
	End
	If lDirecional
		If !MsgYesNo(STR0029,STR0010) //"Pneu informado na posição direcional é recapado. Confirma?"###"ATENÇÃO"
			Return .f.
		EndIf
	EndIf
	//EndIf

	Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT750GRV ³ Autor ³ Thiago Olis Machado   ³ Data ³12/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava as informacoes.                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mnt750Grv(cHoraS, dDataS, dDtDig, cNumCal, nPOSCONT)
Local nx,nz,ny,nQtdEixo := 0
Local i,j,k
Default cNumCal := ""

If Empty(cNumCal)
    cHoraS := cHora
    dDataS := dDtInsp
    dDtDig := dDtDigit
EndIf

Begin Transaction

	For nx = 1 to Len(aCols)

		DbSelectArea("TRC")
		DbSetOrder(1)
		If !DbSeek(xFilial("TRC")+M->TR9_ORDEM+STJ->TJ_PLANO+M->TR9_CICLO+aCOLS[nx][1])
			RecLock("TRC",.T.)
		Else
			RecLock("TRC",.F.)
		EndIf
		TRC->TRC_FILIAL := XFILIAL("TRC")
		TRC->TRC_ORDEM  := M->TR9_ORDEM
		TRC->TRC_PLANO  := STJ->TJ_PLANO
		TRC->TRC_CICLO  := M->TR9_CICLO

		nQtdEixo += aCols[nx][2]
		For nz := 1 To Fcount()
			xx := aScan(aHeader,{|x| AllTrim(x[2]) == AllTrim(Fieldname(nz))})
			If xx > 0
				vv   := "TRC->"+Fieldname(nz)
				&vv. := aCOLS[nx][xx]
			EndIf
		Next nz
		TRC->(MsUnLock())
	Next nx

	DbSelectArea("TR9")
	DbSetOrder(1)
	If !DbSeek(xFilial("TR9")+M->TR9_ORDEM+STJ->TJ_PLANO+M->TR9_CICLO)
		RecLock("TR9",.T.)
	Else
		RecLock("TR9",.F.)
	EndIf

	TR9->TR9_FILIAL := XFILIAL("TR9")
	TR9->TR9_ORDEM  := M->TR9_ORDEM
	TR9->TR9_PLANO  := STJ->TJ_PLANO
	TR9->TR9_CICLO  := M->TR9_CICLO
	TR9->TR9_DTINSP := dDataS
	TR9->TR9_HRINSP := cHoraS
	TR9->TR9_KMATU  := nPoscont
	TR9->TR9_DTDIGI := dDtDig
	TR9->TR9_HRDIGI := Time()
	TR9->TR9_USUDIG := SubStr(cUsuario,7,15)
	TR9->TR9_QTDATU := nQtdEixo
	TR9->TR9_FROTA  := M->TR9_FROTA
	TR9->TR9_STATUS := "1"
	TR9->TR9_PLACA  := M->TR9_PLACA
	If !Empty(cNumCal) .And. NGCADICBASE("TR9_NUMCAL","D","TR9",.F.)
	   TR9->TR9_NUMCAL := cNumCal
	EndIf
	TR9->(MsUnLock())

	EvalTrigger()
End Transaction

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Mnt750SUL ³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³19/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava as informacoes.                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mnt750SUL(nSulco,nFogo)

If nSulco < 1
	Help(" ",1,"NGATENCAO",,STR0025,3,1)  //"Sulco informado deve ser maior que zero!"
	Return .f.
Else
	DbSelectArea("TQS")
	DbSetOrder(1)
	DbSeek(xFilial("TQS")+aCols[n][nFogo])
	If TQS->TQS_SULCAT < nSulco
		//Help(" ",1,"NGATENCAO",,STR0030,3,1)  //"Sulco informado não pode ser maior que o sulco atual!"
		If !MsgYesNo(STR0030,STR0010) //"Sulco informado é maior que o sulco atual! Confirma?"
			Return .f.
		EndIf
	EndIf
EndIf

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT750INC ³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³20/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida se existem inconsistencias na inspecao              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT750INC()
Local nRecno := 0
Private nIncons := 0
Private aPneusQry, aPosicQry,aSulcoQry,nI
Private lCicIgual := .t.
cFil   := TR9->TR9_FILIAL
cOrdem := TR9->TR9_ORDEM
cPlano := TR9->TR9_PLANO
cCic   := TR9->TR9_CICLO

DbSelectArea("TRC")
DbSetOrder(1)
DbSeek(xFilial("TRC")+cOrdem+cPlano+cCic)
While !Eof() .And. TRC->TRC_FILIAL == xFilial("TRC") .And. TRC->TRC_ORDEM == cOrdem .And. TRC->TRC_PLANO == cPlano .And. TRC->TRC_CICLO == cCic

	nRecno := Recno()
	aPneusQry := {TRC->TRC_NFOGO1,TRC->TRC_NFOGO2,TRC->TRC_NFOGO3,TRC->TRC_NFOGO4,;
	TRC->TRC_NFOGO5,TRC->TRC_NFOGO6,TRC->TRC_NFOGO7,TRC->TRC_NFOGO8,;
	TRC->TRC_NFOGO9,TRC->TRC_NFOG10}

	aPosicQry := {TRC->TRC_LOCPN1,TRC->TRC_LOCPN2,TRC->TRC_LOCPN3,TRC->TRC_LOCPN4,;
	TRC->TRC_LOCPN5,TRC->TRC_LOCPN6,TRC->TRC_LOCPN7,TRC->TRC_LOCPN8,;
	TRC->TRC_LOCPN9,TRC->TRC_LOCP10}

	nI := 1

	While nI < 11
		If !Empty(aPneusQry[nI])
			DbSelectArea("TQS")
			DbSetOrder(01)
			If DbSeek( xFilial( 'TQS' ) + aPneusQry[nI] )
				nIncons := nIncons + MNA750EST(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu esta na estrutura do veiculo
              nIncons := nIncons + MNA750LOC(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu esta em local diferente do sistema
				nIncons := nIncons + MNA750SUL(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu esta com sulco maior que o registrado no sistema
				nIncons := nIncons + MNA750VEI(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu consta em outro veiculo
				nIncons := nIncons + MNA750FIL(aPneusQry[nI],aPosicQry[nI],nI)//Verifica se pneu esta em outra filial
				nIncons := nIncons + MNA750MED(TQS->TQS_MEDIDA,aPosicQry[nI],aPneusQry[nI])//Verifica se pneu tem a medida diferente dos demais do mesmo eixo
				nIncons := nIncons + MNA750FAM( xFilial( 'TQS' ),aPneusQry[nI],aPosicQry[nI],nI)//Verifica se o pneus eh de familia diferente da estrutura para esta posicao
			Endif
		Endif
		nI++
	End


	DbSelectArea("TRC")
	DbGoTo(nRecno)
	DbSkip()
End

dbSelectArea("TR9")
dbSetOrder(01)
dbSeek(cFIL+cORDEM+cPlano+cCic)
If TR9->TR9_CICLO = cCic
	RecLock("TR9",.F.)
	If nIncons = 0
		TR9->TR9_STATUS := "3"
	Else
		TR9->TR9_STATUS := "2"
	EndIf
	MsUnlock("TR9")
Endif

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNA750MED | Autor ³ Marcos Wagner Junior  ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se a medida do pneu eh igual aos outros pneus do   ³±±
±±³			 ³mesmo eixo                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA750MED(cMedida,cPosicao,cNumfogo)
Local aArea := GetArea()
Local nI := 1

While nI < 11
	dbSelectArea("TQS")
	dbSetOrder(01)
	If !Empty(aPneusQry[nI])
		If dbSeek( xFilial( 'TQS' ) + aPneusQry[nI] )
			If cMedida != TQS->TQS_MEDIDA
				RestArea(aArea)
				Return 1
			Endif
		Endif
	Endif
	nI++
End
RestArea(aArea)

Return 0
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNA750FIL | Autor ³ Marcos Wagner Junior  ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se o pneu	consta em outra filial                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA750FIL(cNumfogo,cPosicao,nI)
Local aArea   := GetArea()
Local cAlias  := GetNextAlias()
Local cQuery  := ""
Local cCodBem := ""

cQuery := "SELECT TQS_CODBEM FROM "+RetSqlName("TQS")
cQuery += " WHERE D_E_L_E_T_ <> '*' AND TQS_NUMFOG = "+ValToSql(cNumfogo)
cQuery += " AND TQS_FILIAL = "+ValToSql(xFilial("TQS"))

cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
dbSelectArea(cAlias)
dbGoTop()
If (cAlias)->(!Eof())
	cCodBem := (cAlias)->TQS_CODBEM
Endif

DbSelectArea("ST9")
DbSetOrder(16)
If DbSeek(cCodBem+"A")
	While !Eof() .AND. cCodBem == ST9->T9_CODBEM .AND. ST9->T9_SITBEM == "A"
		If xFilial( 'ST9', TRC->TRC_FILIAL ) != ST9->T9_FILIAL
			RestArea(aArea)
			Return 1
		Endif
		dbSkip()
	End
Endif
RestArea(aArea)
Return 0
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNA750VEI | Autor ³ Marcos Wagner Junior  ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se o pneu	consta em outro veiculo               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA750VEI(cPosicao,cNumfogo,nI)
Local aArea := GetArea()

DbSelectArea("TR9")
DbSetOrder(01)
If DbSeek(TRC->TRC_FILIAL+TRC->TRC_ORDEM)
	DbSelectArea("TQS")
	DbSetOrder(01)
	If DbSeek( xFilial( 'TQS' ) + cNumfogo )
		If TR9->TR9_PLACA != TQS->TQS_PLACA
			MNA750INS(TQS->TQS_FILIAL,TQS->TQS_PLACA) //VERIFICA SE BEM JA FOI INSPECIONADO
			RestArea(aArea)
			Return 1
		Endif
	Endif
Endif
RestArea(aArea)
Return 0
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNA750LOC | Autor ³ Marcos Wagner Junior  ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se o pneu esta posicionado em local diferente do	  ³±±
±±³          ³informado.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA750LOC(cPosicao,cNumfogo,nI)
Local i
Local aArea := GetArea()

If TQS->TQS_POSIC != cPosicao
	For i := 1 to Len(aCols)
		If AllTrim(aCols[i][1]) == "RESERVA" .AND. TQS->TQS_EIXO = "R"
			RestArea(aArea)
			Return 0
		EndIf
	Next i
	RestArea(aArea)
	Return 1
Endif
RestArea(aArea)

Return 0
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNA750EST | Autor ³ Marcos Wagner Junior  ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se o pneu esta com a placa em branco               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA750EST(cPosicao,cNumfogo,nI)
Local aArea := GetArea()

If Empty(TQS->TQS_PLACA)
	RestArea(aArea)
	Return 1
Endif
RestArea(aArea)

Return 0
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNA750FAM | Autor ³ Marcos Wagner Junior  ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se a familia do pneu eh diferente da definida na	  ³±±
±±³          ³estrutura para esta posicao.                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA750FAM(cFilNG,cPneu,cPosicao,nI)
Local aArea := GetArea()

dbSelectArea("ST9")
dbSetOrder(01)
If dbSeek(cFilNG+cPneu)
	dbSelectArea("TQ1")
	dbSetOrder(01)
	If dbSeek(ST9->T9_FILIAL+ST9->T9_CODFAMI+ST9->T9_TIPMOD)
		If ST9->T9_CODFAMI != TQ1->TQ1_DESENH
			RestArea(aArea)
			Return 1
		Endif
	Endif
Endif
RestArea(aArea)
Return 0
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNA750SUL | Autor ³Evaldo Cevinscki Jr.   ³ Data ³15/01/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se o pneu eh maior que o registrado no sistema	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA750SUL(cPosicao,cNumfogo,nI)

nMedida := MNTA750MME(nI)
If nMedida > TQS->TQS_SULCAT
	Return 1
Endif

Return 0
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNTA750MME| Autor ³ Marcos Wagner Junior  ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Das 3 medidas de sulco informadas, pega a menor delas    	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA750MME(nI)

	Local nMedida := 10000
	Local i
	Local aSulco  := {}

	cMed1 := "TRC->TRC_PN"+AllTrim(Str(nI))+"M1"
	cMed2 := "TRC->TRC_PN"+AllTrim(Str(nI))+"M2"
	cMed3 := "TRC->TRC_PN"+AllTrim(Str(nI))+"M3"

	For i:= 1 to 3
		cMedida := "cMed"+Str(i,1)
		nMedMin := &cMedida
		nMedida := MIN(nMedida,&nMedMin)
		aAdd(aSulco,nMedida)
	Next
	//Ordena os valores para descartar valor zero
	aSulco := aSort(aSulco)
	nMedida := If(aSulco[1] == 0, If(aSulco[2] == 0,aSulco[3],aSulco[2]),aSulco[1])

Return nMedida
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNA750INS | Autor ³ Evaldo Ceinscki Jr.   ³ Data ³22/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se frota ja foi inspecionada e estava sem inconsis-³±±
±±³          ³tencia,nesse caso muda o status para inconsistente.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA750INS(cFil,cPlaca)
cAliasQry := GetNextAlias()

cQuery := " SELECT TR9_FILIAL,TR9_ORDEM,TR9_PLANO,TR9_CICLO "
cQuery += " FROM " + RetSQLName("TR9")+" TR9,"+ RetSQLName("STJ")+ " STJ"
cQuery += " WHERE TR9.D_E_L_E_T_<>'*' and STJ.D_E_L_E_T_<>'*' and "
cQuery += " TR9.TR9_FILIAL = '"+cFil+"' and "
cQuery += " TR9.TR9_PLACA = '"+cPlaca+"' and TR9.TR9_STATUS <> '4' "
cQuery += " and TR9.TR9_FILIAL=STJ.TJ_FILIAL and TR9.TR9_ORDEM=STJ.TJ_ORDEM and "
cQuery += " TR9.TR9_PLANO=STJ.TJ_PLANO and STJ.TJ_TERMINO = 'N' "
cQuery += " and TR9.TR9_CICLO = "
cQuery += " (SELECT MAX(TR9_CICLO) FROM " + RetSQLName("TR9")+" A "
cQuery += " WHERE A.TR9_FILIAL='"+cFil+"' and A.TR9_ORDEM=TR9.TR9_ORDEM "
cQuery += " and A.TR9_PLANO=TR9.TR9_PLANO and A.D_E_L_E_T_<>'*' )"

cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

While !EOF()

	DbSelectArea("TR9")
	DbSetOrder(1)
	If DbSeek((cAliasQry)->TR9_FILIAL+(cAliasQry)->TR9_ORDEM+(cAliasQry)->TR9_PLANO+(cAliasQry)->TR9_CICLO)
		RecLock("TR9",.F.)
		TR9->TR9_STATUS := "2"
		MsUnlock("TR9")
	EndIf
	DbSelectArea(cALIASQRY)
	DbSkip()
End
(cALIASQRY)->( dbCloseArea() )

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³02/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina :=	{{STR0001 ,"NGCAD02" , 0 , 1 },;     //"Pesquisar"
                     {STR0002 ,"NGCAD02" , 0 , 2 },;     //"Visualizar"
                     {STR0003 ,"NGCAD02" , 0 , 3 },;     //"Incluir"
                     {STR0004 ,"NGCAD02" , 0 , 4 , 0 },; //"Alterar"
                     {STR0005 ,"NGCAD02" , 0 , 5 , 3 }}  //"Excluir"

Return aRotina


//---------------------------------------------------------------------
/*/{Protheus.doc} PrepGraf
Prepara a rotina para o modo gráfico inicializando variáveis do modelo antigo

@author William Rozin Gaspar
@since 15/06/2014
@return Nil
/*/
//---------------------------------------------------------------------

Static Function PrepGraf()

Local nCnt
//Private cHora    := Time()
//Private nPoscont := 0
//Private dDtInsp := dDataBase,dDtDigit := dDataBase


Dbselectarea("TRC")
Dbsetorder(1)

aHeader := CabecGetD("TRC",{"TRC_ORDEM","TRC_PLANO","TRC_CICLO"})
ADHeadRec("TRC",aHeader)

nCnt := 0
DbSelectArea("TQ1")
DbSetOrder(1)
DbSeek(xFilial("TQ1")+M->TR9_CODFAM+M->TR9_TIPMOD)
While !Eof() .And. TQ1->TQ1_FILIAL == xFILIAL("TQ1");
    .And. TQ1->TQ1_DESENH == M->TR9_CODFAM;
    .And. TQ1->TQ1_TIPMOD == M->TR9_TIPMOD

    aAdd(aCols,{TQ1->TQ1_EIXO,;//Código do Eixo
                TQ1->TQ1_QTDPNE,;//Quantidade de Pneus
                Space(16),;//Número do Fogo 1
                TQ1->TQ1_LOCPN1,;//Localização 1
                0,;//Medida 1 - Pneu 1
                0,;//Medida 2 - Pneu 1
                0,;//Medida 3 - Pneu 1
                Space(16),;//Número do Fogo 2
                TQ1->TQ1_LOCPN2,;//Localização 2
                0,;//Medida 1 - Pneu 2
                0,;//Medida 2 - Pneu 2
                0,;//Medida 3 - Pneu 2
                Space(16),;//Número do Fogo 3
                TQ1->TQ1_LOCPN3,;//Localização 3
                0,;//Medida 1 - Pneu 3
                0,;//Medida 2 - Pneu 3
                0,;//Medida 3 - Pneu 3
                Space(16),;//Número do Fogo 4
                TQ1->TQ1_LOCPN4,;//Localização 4
                0,;//Medida 1 - Pneu 4
                0,;//Medida 2 - Pneu 4
                0,;//Medida 3 - Pneu 4
                Space(16),;//Número do Fogo 5
                TQ1->TQ1_LOCPN5,;//Localização 5
                0,;//Medida 1 - Pneu 5
                0,;//Medida 2 - Pneu 5
                0,;//Medida 3 - Pneu 5
                Space(16),;//Número do Fogo 6
                TQ1->TQ1_LOCPN6,;//Localização 6
                0,;//Medida 1 - Pneu 6
                0,;//Medida 2 - Pneu 6
                0,;//Medida 3 - Pneu 6
                Space(16),;//Número do Fogo 7
                TQ1->TQ1_LOCPN7,;//Localização 7
                0,;//Medida 1 - Pneu 7
                0,;//Medida 2 - Pneu 7
                0,;//Medida 3 - Pneu 7
                Space(16),;//Número do Fogo 8
                TQ1->TQ1_LOCPN8,;//Localização 8
                0,;//Medida 1 - Pneu 8
                0,;//Medida 2 - Pneu 8
                0,;//Medida 3 - Pneu 8
                Space(16),;//Número do Fogo 9
                TQ1->TQ1_LOCPN9,;//Localização 9
                0,;//Medida 1 - Pneu 9
                0,;//Medida 2 - Pneu 9
                0,;//Medida 3 - Pneu 9
                Space(16),;//Número do Fogo 10
                TQ1->TQ1_LOCPN0,;//Localização 10
                0,;//Medida 1 - Pneu 10
                0,;//Medida 2 - Pneu 10
                0,;//Medida 3 - Pneu 10
                "TRC",;//Alias
                TRC->(Recno()),;//Recno TRC
                .F.})//Não deletada
    nCnt++

    Dbselectarea("TQ1")
    DbSkip()
End

If Empty(aCOLS)//Caso aCols fique vazio, adiciona linha em branco
   aCols := BlankGetD(aHeader)
Endif
Return
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA750CCB
Carrega o valor do contador do bem se o campo estiver bloqueado

@param cCobBem: Código do bem
@param dData: Data
@param cHora: Hora
@author Wexlei Silveira
@since 16/11/2017
@return True
/*/
//---------------------------------------------------------------------
Static Function MNTA750CCB(cCobBem, dData, cHora)

	If FindFunction("NGBlCont") .And. !NGBlCont( cCobBem )
		nPoscont := NGTpCont(cCobBem, dData, cHora)
	EndIf

Return .T.
