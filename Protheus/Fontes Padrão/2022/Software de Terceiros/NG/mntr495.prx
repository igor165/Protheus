#INCLUDE "MNTR495.CH"
#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR495  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³01/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Controle de Deslocamento dos Equipamentos Por Obra         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR495() 

	Local oReport
	Local aArea := GetArea()
	
	Private cTRB:= GetNextAlias() //Alias Tabela Temporária
	
	SetKey( VK_F9, { | | NGVersao( "MNTR495" , 2 ) } )
	
	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetPortrait() //Default Retrato 
		oReport:PrintDialog()
	Else
		MNTR495R3()
	EndIf
	RestArea(aArea)  

Return .T. 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³  Elisangela Costa     ³ Data ³ 07/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR495                                                    ³±±
±±|__________|____________________________________________________________|±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef() 
	Local oReport   
	Local oSection1, oSection2
	Local oCell

	//LAYOUT 
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
	___________________________________________________________________________________________________________________________________________ 
	Relatorio de Deslocamento de Equipamentos Por Obra
	___________________________________________________________________________________________________________________________________________ 

	Obra      C.Custo              Nome C. Custo          Situacao  Dt.Inic.   H.In.  Dt. Fim     H.Fim   Contador 1   Contador 2  
	___________________________________________________________________________________________________________________________________________  
	xxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxx xxxxxxxxx 99/99/9999 99:99  99/99/9999  99:99  999.999.999  999.999.999 
	xxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxx xxxxxxxxx 99/99/9999 99:99  99/99/9999  99:99  999.999.999  999.999.999 
	xxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxx xxxxxxxxx 99/99/9999 99:99  99/99/9999  99:99  999.999.999  999.999.999 

	*/    

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR495",OemToAnsi(STR0006),"MNT495",{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002+" "+STR0003)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ MV_PAR01     // De C.Custo (Obra)                            ³
	//³ MV_PAR02     // Ate C.Custo                                  ³
	//³ MV_PAR03     // De Data Inicio                               ³
	//³ MV_PAR04     // Ate Data Fim                                 ³
	//³ MV_PAR05     // De Familia                                   ³
	//³ MV_PAR06     // Ate Familia                                  ³
	//³ MV_PAR07     // De Bem                                       ³
	//³ MV_PAR08     // Ate Bem                                      ³
	//³ MV_PAR09     // QTD. Caracter Obra                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte(oReport:uParam,.F.) 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSection1 := TRSection():New(oReport,STR0028,{(cTRB),"ST9"})//Obras
	TRCell():New(oSection1,"(cTRB)->CODBEM",(cTRB),STR0028 ,"@!" ,6,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem" 
	TRCell():New(oSection1,"T9_NOME"    ,"ST9",STR0029 ,"@!" ,40,/*lPixel*/) //"Descrição" 
	TRPosition():New(oSection1,"ST9",1,{|| xFilial("ST9")+(cTRB)->CODBEM})   

	oSection2 := TRSection():New(oReport,STR0040,{(cTRB),"SI3"}) //Obras
	oCell1 := TRCell():New(oSection2,"(cTRB)->OBRA"    ,(cTRB) ,STR0030 ,"@!" ,20,/*lPixel*/,/*{|| code-block de impressao }*/) //"Obra"
	oCell1 := TRCell():New(oSection2,"(cTRB)->CCUSTO"  ,(cTRB) ,STR0031 ,"@!" ,20,/*lPixel*/,/*{|| code-block de impressao }*/) //"C.Custo"
	oCell1 := TRCell():New(oSection2,"I3_DESC"      ,"SI3" ,STR0032 ,"@!" ,40,/*lPixel*/,/*{|| code-block de impressao }*/) //"Nome C.Custo"
	oCell1 := TRCell():New(oSection2,"(cTRB)->SITUAC " ,(cTRB) ,STR0033 ,/*Picture*/,9 ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Situação"
	oCell1 := TRCell():New(oSection2,"(cTRB)->DTINIC"  ,(cTRB) ,STR0034 ,"99/99/9999" ,8,/*lPixel*/,/*{|| code-block de impressao }*/) //"Dt.Inic."
	oCell1 := TRCell():New(oSection2,"(cTRB)->HRINIC"  ,(cTRB) ,STR0035 ,"99:99" ,5,/*lPixel*/,/*{|| code-block de impressao }*/) //"H.In." 
	oCell1 := TRCell():New(oSection2,"     "        ,"   " ,STR0036 ,"99/99/9999" ,8,/*lPixel*/,/*{|| code-block de impressao }*/) //"Dt.Fim" 
	oCell1 := TRCell():New(oSection2,"     "        ,"   " ,STR0037 ,"99:99" ,5,/*lPixel*/,/*{|| code-block de impressao }*/)  //"H.Fim"
	oCell1 := TRCell():New(oSection2,"(cTRB)->POSCON"  ,(cTRB) ,STR0038 ,"@E 999,999,999" ,11,/*lPixel*/,/*{|| code-block de impressao }*/) //"Contador 1"
	oCell1 := TRCell():New(oSection2,"(cTRB)->POSCO2"  ,(cTRB) ,STR0039 ,"@E 999,999,999" ,11,/*lPixel*/,/*{|| code-block de impressao }*/) //"Contador 2" 
	//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a
	//direita
	oSection2:Cell("(cTRB)->POSCON"):SetHeaderAlign("RIGHT") 
	oSection2:Cell("(cTRB)->POSCO2"):SetHeaderAlign("RIGHT") 
	TRPosition():New(oSection2,"SI3",1,{|| xFilial("SI3")+(cTRB)->CCUSTO}) 

Return oReport 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR495R3³ Autor ³ Inacio Luiz Kolling   ³ Data ³01/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Controle de Deslocamento dos Equipamentos Por Obra         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR495R3() 

	Local cSTRING    := "SI3"
	Local cDESC1     := STR0001 //"Relatorio de Deslocamento de Equipamentos Por Obra."
	Local cDESC2     := STR0002 //"E permitido selecionar tipo de informacao a ser"
	Local cDESC3     := STR0003 //"apresentada, atraves dos parametros do relatorio."
	Local WNREL      := "MNTR495"

	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private nLASTKEY := 0
	Private TITULO   := STR0006 //"Relatorio de Deslocamento de Equipamentos Por Obra"
	Private TAMANHO  := "M"
	Private NOMEPROG := "MNTR495"
	Private nTIPO    := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ MV_PAR01     // De C.Custo (Obra)                            ³
	//³ MV_PAR02     // Ate C.Custo                                  ³
	//³ MV_PAR03     // De Data Inicio                               ³
	//³ MV_PAR04     // Ate Data Fim                                 ³
	//³ MV_PAR05     // De Familia                                   ³
	//³ MV_PAR06     // Ate Familia                                  ³
	//³ MV_PAR07     // De Bem                                       ³
	//³ MV_PAR08     // Ate Bem                                      ³
	//³ MV_PAR09     // QTD. Caracter Obra                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	Private cPERG := "MNT495"
	Pergunte(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		Return
	EndIf
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND| R495IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R495Imp  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³01/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR495                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R495IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT := ""
	Local nCNTIMPR := 0
	Local oARQTR495

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contadores de linha e pagina                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private li := 80 ,m_pag := 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se deve comprimir ou nao                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTIPO  := IIF(aRETURN[4]==1,15,18)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta os Cabecalhos                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private CABEC1 := STR0025 //" Obra      C.Custo              Nome C. Custo          Situacao  Dt.Inic.   H.In. Dt. Fim    H.Fim  Contador 1  Contador 2"
	Private CABEC2 := " "
	Private aVETINR := {} 

	/*
	1         2         3         4         5         6         7         8         9         100       110       120      130
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012346789012
	************************************************************************************************************************************
	Obra      C.Custo              Nome C. Custo          Situacao  Dt.Inic.   H.In. Dt. Fim    H.Fim  Contador 1  Contador 2
	***********************************************************************************************************************************
	Bem..: xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxx xxxxxxxxx 99/99/9999 99:99 99/99/9999 99:99 999.999.999 999.999.999 
	*/
	If Empty(MV_PAR09)
		Help(" ",1,"NGMVERPARA")
		Return .F.
	EndIf 

	aDBFR495 := {{"OBRA"   ,"C",20,0},;
				 {"CCUSTO" ,"C",Len(TPN->TPN_CCUSTO),0},;
				 {"CODBEM" ,"C",16,0},;
				 {"DTINIC" ,"D",08,0},;
				 {"HRINIC" ,"C",05,0},;
				 {"DTFIM"  ,"D",08,0},;
				 {"HRFIM"  ,"C",05,0},;
				 {"SITUAC" ,"C",09,0},;   
				 {"POSCON" ,"N",09,0},;
				 {"POSCO2" ,"N",09,0}}  
	
	//Cria Tabela Temporária 
	oARQTR495 := NGFwTmpTbl(cTRB,aDBFR495,{{"CODBEM","OBRA","CCUSTO","DTINIC"}}) 
	

	Processa({|lEND| MNTR495TRB()},STR0027) //"Processando Itens das O.S. Normais..."

	DbSelectArea(cTRB)
	DbGotop()
	SetRegua(LastRec())
	While !Eof()

		cCODBEM := (cTRB)->CODBEM
		DbSelectArea("ST9")
		DbSetOrder(01)
		DbSeek(xFILIAL("ST9")+cCODBEM)
		NGSOMALI(58)
		@ Li,000 Psay STR0022 + cCODBEM+" "+ST9->T9_NOME //"Bem..: "
		NGSOMALI(58)

		DbSelectArea(cTRB)
		lPRIOB := .T.
		While !Eof() .And. (cTRB)->CODBEM == cCODBEM

			cCODOBRA := (cTRB)->OBRA
			NGSOMALI(58)
			@ Li,001 Psay cCODOBRA Picture "@!"

			While !Eof() .And. (cTRB)->CODBEM == cCODBEM .And.;
			(cTRB)->OBRA   == cCODOBRA

				cCODCUST := (cTRB)->CCUSTO
				lPRIM := .T.
				DbSelectARea("SI3")
				DbSetOrder(01)
				DbSeek(xFILIAL("SI3")+cCODCUST)

				DbSelectArea(cTRB)
				While !Eof() .And. (cTRB)->CODBEM == cCODBEM  .And.;
				(cTRB)->OBRA == cCODOBRA .And. (cTRB)->CCUSTO == cCODCUST

					IncRegua()
					If !lPRIM
						NGSOMALI(58)
					Else
						@ Li,012 Psay cCODCUST Picture "@!"
						@ Li,033 Psay Substr(SI3->I3_DESC,1,22)
						lPRIM := .F.
					EndIf

					If (cTRB)->SITUAC = 'P'
						@ Li,056 Psay STR0024
					Else
						@ Li,056 Psay STR0023
					EndIf

					@ Li,066 Psay (cTRB)->DTINIC  Picture "99/99/99"
					@ Li,077 Psay (cTRB)->HRINIC  Picture "99:99"
					@ Li,100 Psay (cTRB)->POSCON  Picture "@E 999,999,999"
					@ Li,112 Psay (cTRB)->POSCO2  Picture "@E 999,999,999"

					DbSelectArea(cTRB)
					DbSkip()
				End
				NGSOMALI(58)
			End
		End
	End
	Roda(nCNTIMPR,cRODATXT,TAMANHO)
	
	//Deleta o arquivo temporario fisicamente 
	oARQTR495:Delete()        

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principam             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex("ST9")
	Set Filter To
	Set Device To Screen
	If aRETURN[5] = 1
		Set Printer To
		DbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()
Return Nil 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³ Elisangela Costa      ³ Data ³  28/06/06³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ReportDef                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local oARQTR495 
	
	Private aVETINR := {} 

	aDBFR495 := {{"OBRA"   ,"C",20,0},;
				 {"CCUSTO" ,"C",Len(TPN->TPN_CCUSTO),0},;
				 {"CODBEM" ,"C",16,0},;
				 {"DTINIC" ,"D",08,0},;
				 {"HRINIC" ,"C",05,0},;
				 {"DTFIM"  ,"D",08,0},;
				 {"HRFIM"  ,"C",05,0},;
				 {"SITUAC" ,"C",09,0},;   
				 {"POSCON" ,"N",09,0},;
				 {"POSCO2" ,"N",09,0}}	
	
	//Cria Tabela Temporária 
	oARQTR495 := NGFwTmpTbl(cTRB,aDBFR495,{{"CODBEM","OBRA","CCUSTO","DTINIC"}}) 

	Processa({|lEND| MNTR495TRB()},STR0027) //"Processando Itens das O.S. Normais..."

	DbSelectArea(cTRB)
	DbGotop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()

		cCODBEM := (cTRB)->CODBEM
		oSection1:Init()
		oSection1:PrintLine()
		oSection2:Init() 
		While !Eof() .And. !oReport:Cancel() .And. (cTRB)->CODBEM == cCODBEM

			cCODOB   := "  "
			cCODOBRA := (cTRB)->OBRA

			While !Eof() .And. !oReport:Cancel() .And. (cTRB)->CODBEM == cCODBEM .And.;
			(cTRB)->OBRA   == cCODOBRA

				cCODCUS  := "  "
				cCODCUST := (cTRB)->CCUSTO

				While !Eof() .And. !oReport:Cancel() .And. (cTRB)->CODBEM == cCODBEM  .And.;
				(cTRB)->OBRA == cCODOBRA .And. (cTRB)->CCUSTO == cCODCUST

					oReport:IncMeter()
					If cCODOB == cCODOBRA
						oSection2:Cell("(cTRB)->OBRA"):Hide()  
					Else
						oSection2:Cell("(cTRB)->OBRA"):Show() 
					EndIf 

					If cCODCUS == cCODCUST 
						oSection2:Cell("(cTRB)->CCUSTO"):Hide() 
						oSection2:Cell("I3_DESC"):Hide()   
					Else
						oSection2:Cell("(cTRB)->CCUSTO"):Show() 
						oSection2:Cell("I3_DESC"):Show() 
					EndIf 
					cCODOB  := (cTRB)->OBRA
					cCODCUS := (cTRB)->CCUSTO  
					oSection2:PrintLine()   

					DbSelectArea(cTRB)
					DbSkip()
				End
			End
		End
		oSection1:Finish() 
		oSection2:Finish()
	End
	//Deleta o arquivo temporario fisicamente  
	oARQTR495:Delete()         

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR495TRB³ Autor ³ Elisangela Costa      ³ Data ³07/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR495                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function  MNTR495TRB()

	DbSelectArea("TPN")
	DbSetOrder(01)
	DbSeek(xFILIAL("TPN")+MV_PAR07+DTOS(MV_PAR03),.T.)
	ProcRegua(LastRec())
	While !Eof() .And. TPN->TPN_FILIAL == xFILIAL("TPN") .And.;
	TPN->TPN_CODBEM <= MV_PAR08

		IncProc()
		If TPN->TPN_DTINIC < MV_PAR03 .Or. TPN->TPN_DTINIC > MV_PAR04
			DbSelectArea("TPN")
			DbSkip()
			Loop
		EndIf

		If TPN->TPN_CCUSTO < MV_PAR01 .Or. TPN->TPN_CCUSTO > MV_PAR02
			DbSelectArea("TPN")
			DbSkip()
			Loop
		EndIf

		DbSelectArea("ST9")
		DbSetOrder(01)
		DbSeek(xFILIAL("ST9")+TPN->TPN_CODBEM)
		If ST9->T9_CODFAMI < MV_PAR05 .Or. ST9->T9_CODFAMI > MV_PAR06
			DbSelectArea("TPN")
			DbSkip()
			Loop
		EndIf

		DbSelectArea(cTRB)
		DbAppend() 
		If MV_PAR09 > 20
			(cTRB)->OBRA   := SubStr(TPN->TPN_CCUSTO,1,20)
		Else
			(cTRB)->OBRA   := SubStr(TPN->TPN_CCUSTO,1,MV_PAR09)
		EndIf 
		(cTRB)->CCUSTO := TPN->TPN_CCUSTO
		(cTRB)->CODBEM := TPN->TPN_CODBEM
		(cTRB)->DTINIC := TPN->TPN_DTINIC
		(cTRB)->HRINIC := TPN->TPN_HRINIC
		(cTRB)->POSCON := TPN->TPN_POSCON
		(cTRB)->POSCO2 := TPN->TPN_POSCO2
		(cTRB)->SITUAC := If(TPN->TPN_UTILIZ = "P",STR0024,STR0023)    
		DbSelectArea("TPN")
		DbSkip()
	End

Return .T. 