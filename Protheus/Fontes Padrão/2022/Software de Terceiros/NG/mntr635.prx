#INCLUDE "mntr635.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 2 //Versao do fonte

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR635  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio Onde-Se-Usa Ferramenta                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR635()

	//---------------------------------------------------------------------
	// Guarda conteudo e declara variaveis padroes
	//---------------------------------------------------------------------
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

	Local cString		:= "STG"
	Local wnrel			:= "MNTR635"
	Local cDesc1		:= STR0001 //"Relatorio das manutencoes em que a ferramenta    selecionada e planeja-"
	Local cDesc2		:= STR0002 //"da para utilizacao. Atraves da opcao parametros o usuario podera fazer "
	Local cDesc3		:= STR0003 //"selecao por Centro de Custo e Familia de Bens"

	Private Tamanho		:= "M"
	Private aReturn		:= { STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey	:= 0
	Private cPerg		:= "MNT635"
	Private titulo		:= STR0006 //"Onde-Se-Usa Ferramenta"
	Private ntipo		:= 0
	Private nomeprog	:= "MNTR635"
	Private cabec1,cabec2

	//+--------------------------------------------------------------+
	//| Variaveis utilizadas para parametros                         |
	//| mv_par01     // Ferramenta    a procurar                     |
	//| mv_par02     // De  Centro de Custos                         |
	//| mv_par03     // Ate Centro de Custos                         |
	//| mv_par04     // De  Familia de Bens                          |
	//| mv_par05     // Ate Familia de Bens                          |
	//+--------------------------------------------------------------+
	Pergunte(cPerg,.F.)

	//---------------------------------------------------------------------
	// Envia controle para a funcao SETPRINT
	//---------------------------------------------------------------------
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		Return
	EndIf
	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R635Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

	//----------------------------------------+
	//| Retorna conteudo de variaveis padroes |
	//----------------------------------------+
	NGRETURNPRM(aNGBEGINPRM)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R635Imp  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR635                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R635Imp(lEnd,wnRel,titulo,tamanho) 
	
	Local aINDR635	 := {} //Array p/ Indice Tabela Temporária
	Local cRodaTxt	 := ""
	Local cChave	 := Space(TAMSX3("T9_CODBEM")[1])
	Local cTRB635	 := GetNextAlias() //Alias Tabela Temporária
	Local nCntImpr	 := 0
	Local oARQTR635 //Obj. Tabela Temporária
	
	//---------------------------------------------------------------------
	// Contadores de linha e pagina
	//---------------------------------------------------------------------
	Private li := 80 ,m_pag := 1

	//---------------------------------------------------------------------
	// Verifica se deve comprimir ou nao
	//---------------------------------------------------------------------
	nTipo := IIF(aReturn[4]==1,15,18)

	//---------------------------------------------------------------------
	// Monta os Cabecalhos
	//---------------------------------------------------------------------

	cabec1 := STR0007 //"Bem              Descrição                         Serviço Nome                          Sequencia   Qtd    Consumo Unidade Reserva"
	cabec2 := STR0008 //"Bem Pai          Descrição                      Tarefa Descricao                            C. Custo             C.Trab     Familia"

	/*/
	1         1         1         1
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	______________________________________________________________________________________________________________________________________
	Bem              Descrição                         Serviço Nome                          Sequencia   Qtd    Consumo Unidade Reserva
	Bem Pai          Descrição                      Tarefa Descricao                            C. Custo             C.Trab     Familia
	______________________________________________________________________________________________________________________________________
	Especialidade.:  xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	**************************************************************************************************************************************

	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999         xxx    xx:xx   xxx     xxx SUBST
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxx     xxxxxx

	*/

	aDBFR635 := {{"CODBEM" , "C", 16, 0},;
				 {"SERVICO", "C", 06, 0},;
				 {"SEQRELA", "C", 03, 0},;
				 {"SEQUEN" , "C", 03, 0},;
				 {"TAREFA" , "C", 06, 0},;
				 {"CODIGO" , "C", 15, 0},;
				 {"QUANTID", "N", 09, 0},;
				 {"QUANREC", "N", 03, 0},;
				 {"RESERVA", "C", 01, 0},; 
				 {"UNIDADE", "C", 02, 0}}   

	//Indice Tabela Temporária
	aINDR635  := {{"CODIGO","CODBEM","SERVICO","SEQRELA","SEQUEN","TAREFA"}}
	
	//Criação Tabela Temporária
	oARQTR635 := NGFwTmpTbl(cTRB635, aDBFR635, aINDR635)   

	DbSelectArea("STG")
	DbSetOrder(02)
	DbSeek(xFilial("STG")+"F"+MV_PAR01)
	SetRegua(LastRec())
	While !EoF() .And.  STG->TG_FILIAL == xFilial("STG") .And.;
	STG->TG_TIPOREG = "F" .And. SUBSTR(STG->TG_CODIGO,1,6) = MV_PAR01

		dbSelectArea(cTRB635)
		RecLock(cTRB635,.T.) 
		(cTRB635)->CODBEM	:= STG->TG_CODBEM
		(cTRB635)->SERVICO	:= STG->TG_SERVICO
		(cTRB635)->SEQRELA	:= STG->TG_SEQRELA
		(cTRB635)->TAREFA	:= STG->TG_TAREFA
		(cTRB635)->CODIGO	:= STG->TG_CODIGO
		(cTRB635)->QUANTID	:= STG->TG_QUANTID
		(cTRB635)->QUANREC	:= STG->TG_QUANREC
		(cTRB635)->UNIDADE	:= STG->TG_UNIDADE
		(cTRB635)->RESERVA	:= STG->TG_RESERVA
		(cTRB635)->SEQUEN	:= STR(NGSEEK("ST5",STG->TG_CODBEM+STG->TG_SERVICO+STG->TG_SEQRELA+STG->TG_TAREFA,1,"T5_SEQUENC"), TAMSX3("T5_SEQUENC")[1])
		(cTRB635)->(MsUnlock())
		dbSelectarea("STG")
		dbSkip()

	End While

	DbSelectArea("SH4")
	DbSetOrder(01)
	DbSeek(xFilial("SH4")+MV_PAR01)

	DbSelectArea(CTRB635)
	DbSetOrder(01)
	DbGoTop()
	SetRegua(LastRec())
	If !EoF()
		While !EoF()

			IncRegua()
			If Li > 58
				Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
				@ Li,000 Psay STR0010 //"Ferramenta.: "
				@ Li,017 Psay (cTRB635)->CODIGO
				@ Li,034 Psay SH4->H4_DESCRI
				SomaLinha()
				@ Li,000 Psay "************************************************************************************************************************************"
				SomaLinha()
			EndIf

			//---------------------------------------------------------------------
			// Selecao dos registros conforme os parametros
			//---------------------------------------------------------------------
			DbSelectArea("ST9")
			DbSetOrder(01)
			DbSeek(xFilial('ST9')+(cTRB635)->CODBEM)
			DbSelectArea("STG")
			If ST9->T9_CCUSTO < MV_PAR02 .Or. ST9->T9_CCUSTO > MV_PAR03
				DbSkip()
				Loop
			EndIf

			If ST9->T9_CODFAMI < MV_PAR04 .Or. ST9->T9_CODFAMI > MV_PAR05
				DbSkip()
				Loop
			EndIf

			//---------------------------------------------------------------------
			// Impressao da Primeira Linha da O.S.
			//---------------------------------------------------------------------
			DbSelectArea("ST4")
			DbSetOrder(01)
			DbSeek(xFilial('ST4')+(cTRB635)->SERVICO)
			@ Li,000 Psay (cTRB635)->CODBEM
			@ Li,017 Psay Substr(ST9->T9_NOME,1,33)
			@ Li,051 Psay (cTRB635)->SERVICO
			@ Li,059 Psay subst(ST4->T4_NOME,1,29)
			@ Li,089 Psay (cTRB635)->SEQRELA Picture "@!"
			@ Li,099 Psay (cTRB635)->QUANREC Picture "@E 999"
			@ Li,103 Psay (cTRB635)->QUANTID Picture "@E 999,999.99"
			@ Li,116 Psay (cTRB635)->UNIDADE
			If (cTRB635)->RESERVA = "S"
				@ Li,124 Psay STR0011 //"Sim"
			Else
				@ Li,124 Psay STR0012 //"Nao"
			EndIf

			SomaLinha()

			//---------------------------------------------------------------------
			// Verifica se o Bem tem Bem Pai
			//---------------------------------------------------------------------
			DbSelectArea("STC")
			DbSetOrder(03)
			cChave := (cTRB635)->CODBEM
			While .T.
				DbSeek(xFilial('STC')+cChave)
				If Found()
					cChave := STC->TC_CODBEM
					DbSkip()
					Loop
				EndIf
				Exit
			End

			//---------------------------------------------------------------------
			// Impressao da Segunda  Linha
			//---------------------------------------------------------------------

			If (cTRB635)->CODBEM != cChave
				DbSelectArea("ST9")
				DbSetOrder(01)
				DbSeek(xFilial("ST9")+cChave)
				@ Li,000 Psay cChave
				@ Li,017 Psay Substr(ST9->T9_NOME,1,35)
			EndIf

			@ Li,048 Psay (cTRB635)->TAREFA
			If (cTRB635)->TAREFA = "0     "
				@ Li,055 Psay STR0013 //"Sem Especificacao De Tarefa"
			Else
				@ Li,055 Psay Substr(NGSEEK("ST5",(CTRB635)->CODBEM+(CTRB635)->SERVICO+(CTRB635)->SEQRELA+(CTRB635)->TAREFA,1,"T5_DESCRIC"),1,35)
			EndIf

			DbSelectArea("ST9")
			DbSeek(xFilial('ST9')+(cTRB635)->CODBEM)
			@ Li,092 Psay ST9->T9_CCUSTO
			@ Li,113 Psay ST9->T9_CENTRAB
			@ Li,124 Psay ST9->T9_CODFAMI
			SomaLinha()
			SomaLinha()
			DbSelectArea(CTRB635)
			DbSkip()
		End While
	Else
		MsgInfo(STR0014,STR0015) //"Não existem dados para montar o relatório."###"ATENÇÃO"
		//Deleta Tabela Temporária.
		oARQTR635:Delete()
		Return .F.
	Endif

	Roda(nCntImpr,cRodaTxt,Tamanho)

	//Deleta Tabela Temporária.
	oARQTR635:Delete()

	//---------------------------------------------------------------------
	// Devolve a condicao original do arquivo principal
	//---------------------------------------------------------------------
	RetIndex("SH4")
	RetIndex("STG")
	RetIndex("ST9")
	RetIndex("ST4")
	RetIndex("STC")
	RetIndex("ST5")

	Set Filter To
	Set Device To Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf

	MS_FLUSH()

Return Nil

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaLinha()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR635                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Somalinha()

	Li++

	If Li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		@ Li,000 Psay STR0010 //"Ferramenta.: "
		@ Li,017 Psay STG->TG_CODIGO
		@ Li,034 Psay SH4->H4_DESCRI
		SomaLinha()
		@ Li,000 Psay "************************************************************************************************************************************"
		SomaLinha()
	EndIf

Return .T.