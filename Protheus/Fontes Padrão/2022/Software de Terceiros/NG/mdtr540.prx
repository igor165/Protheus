#INCLUDE "mdtr540.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 02 //Versao do fonte
/*/


Ŀ
Funo     MDTR540   Autor  Heverson Vitoreti      Data  31/07/06 
Ĵ
Descrio Relatorio demonstrativo dos custos dos exames realizados    
          Sera considerado para o relatorio todos os exames do tipo    
          ocupacional realizados no periodo solicitado. O Programa     
          le a tabela de Exames do Funcionario (TM5), e para cada exa- 
          realizado, o programa busca o valor na tabela precos (TMD), 
          com base no fornecedor e na data de realizacao do exame.    
          O relatorio saira classificado por Empresa, Centro de Custo, 
          Fornecedor,exame e acumulando os valores do exame.          
Ĵ
Sintaxe e  MDTR494void)                                               
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MDTR540()
//-----------------------------------------------------
//| Armazena variaveis p/ devolucao (NGRIGHTCLICK) 	  |
//-----------------------------------------------------
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Local oReport
Local aArea := GetArea()
Local nTamGrupExa := 0

lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

cAliasCC := "SI3"   
cF3CC    := "SI3" 
cCodCC2  := "I3_CUSTO"
cDescCC2 := "SI3->I3_DESC"
nSizeSI3 := If((TAMSX3("I3_CUSTO")[1]) < 1,9,(TAMSX3("I3_CUSTO")[1]))

If Alltrim(GETMV("MV_MCONTAB")) == "CTB"
	cAliasCC := "CTT"
	cF3CC    := "CTT"
	cCodCC2  := "CTT_CUSTO"
	cDescCC2 := "CTT->CTT_DESC01"		
	nSizeSI3 := If((TAMSX3("CTT_CUSTO")[1]) < 1,9,(TAMSX3("CTT_CUSTO")[1]))				
Endif

cPerg := If(!lSigaMdtPS,"MDT540    ","MDT540PS  ")

/*----------------------------------
//PERGUNTAS PADRO					|
| 01  De Centro de Custo ?  		|
| 02  Ate Centro de Custo ? 		|
| 03  De Fornecedor ?       		|
| 04  Ate Fornecedor ?      		|
| 05  De Exame ?            		|
| 06  Ate Exame ?           		|
| 07  De Data de Resul. ?   		|
| 08  Ate Data de Resul. ? 			|
|										|
//PERGUNTAS DO PRESTADOR DE SERVIO|
| 01  De Cliente ?          		|
| 02  Loja                  		|
| 03  At Cliente ?         		|
| 04  Loja                  		|
| 05  De Centro de Custo ?  		|
| 06  Ate Centro de Custo ? 		|
| 07  De Fornecedor ?       		|
| 08  Ate Fornecedor ?     			|
| 09  De Exame ?            		|
| 10  Ate Exame ?           		|
| 11  De Data de Resul. ?   		|
| 12  Ate Data de Resul. ?  		|
----------------------------------*/

If !MDTRESTRI(cPrograma)
	//Ŀ
	// Devolve variaveis armazenadas (NGRIGHTCLICK) 			 			  
	//
	NGRETURNPRM(aNGBEGINPRM)
	Return .F.
Endif

//Ŀ
// Adicionando as perguntas relacionadas a Exames ao grupo de   
// campos 048 - "Exame".                                        
//

If TRepInUse()
   //-- Interface de impressao
   oReport := ReportDef()
	oReport:SetPortrait()
   oReport:PrintDialog()
Else
   MDTR540R3()
EndIf
RestArea(aArea)  

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//
NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*


Ŀ
Funo     ReportDef Autor  Heverson Vitoreti      Data  18/07/06 
Ĵ
Descrio  Define as secoes impressas no relatorio                    
Ĵ
 Uso       MDTR540                                                    
|__________|____________________________________________________________| 

*/   
Static Function ReportDef() 

Local oReport 
Local oSection1 
Local oSection2 
Local oSection3 
Local oCell
Local oBreak1, oBreak2, oBreak3
Local cUsaPS := Alltrim(GETMV("MV_NGMDTPS")) //Usa Prestadora de Servico

Local nTamExa := If(TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1])

oReport := TReport():New("MDTR540",OemToAnsi(STR0006),cPerg,{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002+" "+STR0003)

Pergunte(oReport:uParam,.F.)

oReport:SetTotalInLine(.T.)

If lSigaMdtps

	oSection1 := TRSection():New(oReport,STR0034,{"TRB","SA1"}) //"Cliente"
	TRCell():New(oSection1,"TRB->CLIENT","TRB",STR0034,"@!",nTa1) //"Cliente"
	TRCell():New(oSection1,"TRB->LOJA"  ,"TRB",STR0018	,"@!",nTa1L) //"Loja"
	TRCell():New(oSection1,"A1_NOME"	,"SA1",STR0035   ,"@!",40) //"Nome"
	TRPosition():New(oSection1,"SA1",1,{|| xFilial("SA1")+TRB->CLIENT+TRB->LOJA})

	oSection2 := TRSection():New(oReport,STR0036,{"TRB","SI3","CTT"})            //"Centro de Custo"
	TRCell():New(oSection2,"TRB->CCUSTO"	 ,"TRB",STR0037 ,"@!",nSizeSI3) //"C.Custo"
	If cAliasCC == "CTT"
		TRCell():New(oSection2,"CTT_DESC01" ,"CTT",STR0038	,"@!",40) //"Descrio"
		TRPosition():New(oSection2,"CTT",1,{|| xFilial("CTT")+TRB->CCUSTO})
	Else
		TRCell():New(oSection2,"I3_DESC" 	,"SI3",STR0038	,"@!",40) //"Descrio"
		TRPosition():New(oSection2,"SI3",1,{|| xFilial("SI3")+TRB->CCUSTO})
	EndIf
	
	oSection3 := TRSection():New(oReport,STR0039,{"TRB","SA2"})  //"Fornecedor"
	TRCell():New(oSection3,"TRB->FORNEC","TRB",STR0039	,"@!",nTa2) //"Fornecedor"
	TRCell():New(oSection3,"TRB->LOJAF" ,"TRB",STR0018	    ,"@!",nTa2L) //"Loja"
	TRCell():New(oSection3,"A2_NOME"	,"SA2",STR0035   	,"@!",40) //"Nome"
	TRPosition():New(oSection3,"SA2",1,{|| xFilial("SA2")+TRB->FORNEC+TRB->LOJAF})
	
	oSection4 := TRSection():New(oReport,STR0040,{"TRB"})            //"Exames"
	oCell := TRCell():New(oSection4,"TRB->EXAME"	,"TRB",STR0041        ,"@!"             ,nTamExa,,) //"Exame"
	oCell := TRCell():New(oSection4,"TM4_NOMEXA"	,"TM4",STR0042,"@!"             ,40,,) //"Nome do Exame"
	oCell := TRCell():New(oSection4,"TRB->QTDE"	    ,"TRB",STR0043   ,"@E 999,999"     ,06,,) //"Quantidade"
	oCell := TRCell():New(oSection4,"TRB->VALOR"	,"TRB",STR0044        ,"@E 9,999,999.99",12,,) //"Custo"
	TRPosition():New(oSection4,"TM4",1,{|| xFilial("TM4")+TRB->EXAME})
	oSection4:Cell("TRB->QTDE"):SetHeaderAlign("RIGHT")
	oSection4:Cell("TRB->VALOR"):SetHeaderAlign("RIGHT")
	
	oBreak1 := TRBreak():New(oSection3,".T.",STR0010,.F.) //"Total do Fornecedor"
	TRFunction():New(oSection4:Cell("TRB->VALOR")	,/*cId*/,"SUM"	,oBreak1,STR0044,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/) //"Custo"
	
	oBreak2 := TRBreak():New(oSection2,".T.",STR0013,.F.) //"Total do Centro de Custo"
	TRFunction():New(oSection4:Cell("TRB->VALOR")	,/*cId*/,"SUM"	,oBreak2,STR0044,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/) //"Custo"

	oBreak3 := TRBreak():New(oSection1,".T.",STR0045,.F.) //"Total do Cliente"
	TRFunction():New(oSection4:Cell("TRB->VALOR")	,/*cId*/,"SUM"	,oBreak3,STR0044,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/) //"Custo"
	
	oTotaliz := TRFunction():New(oSection4:Cell("TRB->VALOR"),"TOTVAL"   ,"SUM",/*oBreak*/,STR0044,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/) //"Custo"

Else

	If cUsaPS == "N"
		oSection1 := TRSection():New(oReport,STR0036,{"TRB","SI3","CTT"})            //"Centro de Custo"
		TRCell():New(oSection1,"TRB->CCUSTO"	 ,"TRB",STR0037 ,"@!",nSizeSI3) //"C.Custo"
		If cAliasCC == "CTT"
			TRCell():New(oSection1,"CTT_DESC01" ,"CTT",STR0038	,"@!",40) //"Descrio"
			TRPosition():New(oSection1,"CTT",1,{|| xFilial("CTT")+TRB->CCUSTO})
		Else
			TRCell():New(oSection1,"I3_DESC" 	,"SI3",STR0038	,"@!",40) //"Descrio"
			TRPosition():New(oSection1,"SI3",1,{|| xFilial("SI3")+TRB->CCUSTO})
		EndIf
	
		oSection2 := TRSection():New(oReport,STR0039,{"TRB","SA2"})  //"Fornecedor"
		TRCell():New(oSection2,"TRB->FORNEC"	,"TRB",STR0039	,"@!",06) //"Fornecedor"
		TRCell():New(oSection2,"A2_NOME"		,"SA2",STR0035   	,"@!",40) //"Nome"
		TRPosition():New(oSection2,"SA2",1,{|| xFilial("SA2")+TRB->FORNEC})
		
		oSection3 := TRSection():New(oReport,STR0040,{"TRB"})            //"Exames"
		oCell := TRCell():New(oSection3,"TRB->EXAME"	,"TRB",STR0041        ,"@!"             ,nTamExa,,) //"Exame"
		oCell := TRCell():New(oSection3,"TM4_NOMEXA"	,"TM4",STR0042,"@!"             ,40,,) //"Nome do Exame"
		oCell := TRCell():New(oSection3,"TRB->QTDE"	,"TRB",STR0043   ,"@E 999,999"     ,06,,) //"Quantidade"
		oCell := TRCell():New(oSection3,"TRB->VALOR"	,"TRB",STR0044        ,"@E 9,999,999.99",12,,) //"Custo"
		TRPosition():New(oSection3,"TM4",1,{|| xFilial("TM4")+TRB->EXAME})
		oSection3:Cell("TRB->QTDE"):SetHeaderAlign("RIGHT")
		oSection3:Cell("TRB->VALOR"):SetHeaderAlign("RIGHT")
		
		oBreak1 := TRBreak():New(oSection2,".T.",STR0010,.F.) //"Total do Fornecedor"
		TRFunction():New(oSection3:Cell("TRB->VALOR")	,/*cId*/,"SUM"	,oBreak1,STR0044,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/) //"Custo"
		
		oBreak2 := TRBreak():New(oSection1,".T.",STR0013,.F.) //"Total do Centro de Custo"
		TRFunction():New(oSection3:Cell("TRB->VALOR")	,/*cId*/,"SUM"	,oBreak2,STR0044,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/) //"Custo"
		
		oTotaliz := TRFunction():New(oSection3:Cell("TRB->VALOR"),"TOTVAL"   ,"SUM",/*oBreak*/,STR0044,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/) //"Custo"
	Else
		oSection1 := TRSection():New(oReport,STR0034,{"TRB","SA1"}) //"Cliente"
		TRCell():New(oSection1,"TRB->CCUSTO"	,"TRB",STR0046	,"@!",06) //"Empresa"
		TRCell():New(oSection1,"A1_NOME"			,"SA1",STR0038,"@!",40) //"Descrio"
		TRPosition():New(oSection1,"SA1",1,{|| xFilial("SA1")+SubStr(TRB->CCUSTO,1,6)})
	
		oSection2 := TRSection():New(oReport,STR0036,{"TRB","SI3","CTT"})            //"Centro de Custo"
		TRCell():New(oSection2,"TRB->CCUSTO"	 ,"TRB",STR0037 ,"@!",9) //"C.Custo"
		If cAliasCC == "CTT"
			TRCell():New(oSection2,"CTT_DESC01" ,"CTT",STR0038	,"@!",40) //"Descrio"
			TRPosition():New(oSection2,"CTT",1,{|| xFilial("CTT")+TRB->CCUSTO})
		Else
			TRCell():New(oSection2,"I3_DESC" 	,"SI3",STR0038	,"@!",40) //"Descrio"
			TRPosition():New(oSection2,"SI3",1,{|| xFilial("SI3")+TRB->CCUSTO})
		EndIf
		
		oSection3 := TRSection():New(oReport,STR0039,{"TRB","SA2"})  //"Fornecedor"
		TRCell():New(oSection3,"TRB->FORNEC"	,"TRB",STR0039	,"@!",06) //"Fornecedor"
		TRCell():New(oSection3,"A2_NOME"		,"SA2",STR0035   	,"@!",40) //"Nome"
		TRPosition():New(oSection3,"SA2",1,{|| xFilial("SA2")+TRB->FORNEC})
		
		oSection4 := TRSection():New(oReport,STR0040,{"TRB"})            //"Exames"
		oCell := TRCell():New(oSection4,"TRB->EXAME"	,"TRB",STR0041        ,"@!"             ,nTamExa,,) //"Exame"
		oCell := TRCell():New(oSection4,"TM4_NOMEXA"	,"TM4",STR0042,"@!"             ,40,,) //"Nome do Exame"
		oCell := TRCell():New(oSection4,"TRB->QTDE"	,"TRB",STR0043   ,"@E 999,999"     ,06,,) //"Quantidade"
		oCell := TRCell():New(oSection4,"TRB->VALOR"	,"TRB",STR0044        ,"@E 9,999,999.99",12,,) //"Custo"
		TRPosition():New(oSection4,"TM4",1,{|| xFilial("TM4")+TRB->EXAME})
		oSection4:Cell("TRB->QTDE"):SetHeaderAlign("RIGHT")
		oSection4:Cell("TRB->VALOR"):SetHeaderAlign("RIGHT")
		
		oBreak1 := TRBreak():New(oSection3,".T.",STR0010,.F.) //"Total do Fornecedor"
		TRFunction():New(oSection4:Cell("TRB->VALOR")	,/*cId*/,"SUM"	,oBreak1,STR0044,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/) //"Custo"
		
		oBreak2 := TRBreak():New(oSection2,".T.",STR0013,.F.) //"Total do Centro de Custo"
		TRFunction():New(oSection4:Cell("TRB->VALOR")	,/*cId*/,"SUM"	,oBreak2,STR0044,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/) //"Custo"
	
		oBreak3 := TRBreak():New(oSection1,".T.",STR0014,.F.) //"Total da Empresa"
		TRFunction():New(oSection4:Cell("TRB->VALOR")	,/*cId*/,"SUM"	,oBreak3,STR0044,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/) //"Custo"
		
		oTotaliz := TRFunction():New(oSection4:Cell("TRB->VALOR"),"TOTVAL"   ,"SUM",/*oBreak*/,STR0044,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/) //"Custo"
	EndIf

Endif

Return oReport

/*/


Ŀ
Funo     MDTR540   Autor  Elisangela Costa       Data  13.01.03 
Ĵ
Descrio Relatorio demonstrativo dos custos dos exames realizados    
          Sera considerado para o relatorio todos os exames do tipo    
          ocupacional realizados no periodo solicitado. O Programa     
          le a tabela de Exames do Funcionario (TM5), e para cada exa- 
          realizado, o programa busca o valor na tabela precos (TMD), 
          com base no fornecedor e na data de realizacao do exame.    
          O relatorio saira classificado por Empresa, Centro de Custo, 
          Fornecedor,exame e acumulando os valores do exame.          
Ĵ
Sintaxe e  MDTR494void)                                               
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MDTR540R3()
//Ŀ
// Define Variaveis                                             
//
LOCAL wnrel   := "MDTR540"
LOCAL limite  := 132
LOCAL cDesc1  := STR0001  //"Relatorio de Custos dos Exames realizados. Atraves dos parametros"
LOCAL cDesc2  := STR0002 //"o usuario podera selecionar o centro de custo,fornecedor, exame,"
LOCAL cDesc3  := STR0003 //"e obterar os custos dos exames realizados no periodo."

LOCAL cString := "TM5"                       
PRIVATE nomeprog := "MDTR540"
PRIVATE tamanho  := "P"
PRIVATE aReturn  := { STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
PRIVATE titulo   := STR0006 //"Custos dos Exames realizados"
PRIVATE ntipo    := 0
PRIVATE nLastKey := 0
PRIVATE cabec1, cabec2
PRIVATE nValforn := 0.00
PRIVATE lContinua := .T.  
Private aVetinr := {}, oTempTRB

//Ŀ
// Verifica as perguntas selecionadas                           
//
pergunte(cPerg,.F.)
//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01             // De  Centro de Custo                  
// mv_par02             // Ate Centro de Custo                  
// mv_par03             // De  Fornecedor                       
// mv_par04             // Ate Fornecedor                       
// mv_par05             // De Exame                             
// mv_par06             // Ate Exame                            
// mv_par07             // Ate Dt. Realizacao                   
// mv_par08             // Ate Dt. Realizacao                   
//
//Ŀ
// Envia controle para a funcao SETPRINT                        
//
wnrel := "MDTR540"
wnrel := SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

If nLastKey == 27
	Set Filter to
    Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Set Filter to
	Return
Endif

RptStatus({|lEnd| R540Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return NIL
/*/


Ŀ
Funo     R540Imp   Autor Elisangela Costa        Data 14 /01/03 
Ĵ
Descrio  Chamada do Relatrio                                       
Ĵ
 Uso       MDTR540                                                    
ٱ


/*/
Static Function R540Imp(lEnd,wnRel,titulo,tamanho)

//Ŀ
// Define Variaveis                                             
//
LOCAL cRodaTxt := ""
LOCAL nCntImpr := 0
Local nTamCli  := TAMSX3("A1_COD")[1]
Local nTamLoj  := TAMSX3("A1_LOJA")[1]
Local nSpcCli  := If( nTamCli < 7 , 0 , nTamCli - 7 )

//Ŀ
// Variaveis para controle do cursor de progressao do relatorio 
//
LOCAL nTotRegs := 0 ,nMult := 1 ,nPosAnt := 4 ,nPosAtu := 4 ,nPosCnt := 0

//Ŀ
// Contadores de linha e pagina                                 
//
PRIVATE li := 80 ,m_pag := 1

//Ŀ
// Verifica se deve comprimir ou nao                            
//
nTipo  := IIF(aReturn[4]==1,15,18)

//Ŀ
// Monta os Cabecalhos                                          
//

cabec1 := STR0007 //"Codigo  Loja  Nome do Fornecedor"
cabec2 := STR0008 //"  Exame  Nome do Exame                               Quantidade            Custo"

//           1         2         3         4         5         6         7         8
// 012345678901234567890123456789012345678901234567890123456789012345678901234567890
//____________________________________________________________________________________
//Codigo  Nome do Fornecedor
//  Exame  Nome do Exame                               Quantidade            Custo
//____________________________________________________________________________________
//
//Empresa...: xxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//C.Custo...: xxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//
//xxxxxx   xx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999.999    99.999.999,99
//                                                                   -------------
//                                              Total do Fornecedor  99.999.999,99

//xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999.999    99.999.999,99
//                                                                   -------------
//                                         Total do Fornecedor       99.999.999,99
//                                         Total do Centro de Custo 999.999.999,99
//C.Custo...: xxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//
//xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999.999    99.999.999,99
//                                                                   -------------
//                                         Total do Fornecedor       99.999.999,99
//                                         Total do Centro de Custo 999.999.999,99
//                                         Total da Empresa       9.999.999.999,99
//                                         Total do Geral        99.999.999.999,99
// =========================================================

//           1         2         3         4         5         6         7         8
// 012345678901234567890123456789012345678901234567890123456789012345678901234567890
//____________________________________________________________________________________
//Codigo  Nome do Fornecedor
//  Exame  Nome do Exame                               Quantidade            Custo
//____________________________________________________________________________________
//
//C.Custo...: xxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//
//xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999.999    99.999.999,99
//                                                                   -------------
//                                              Total do Fornecedor  99.999.999,99

//xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999.999    99.999.999,99
//                                                                   -------------
//                                         Total do Fornecedor       99.999.999,99
//                                         Total do Centro de Custo 999.999.999,99
//C.Custo...: xxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//
//xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999.999    99.999.999,99
//                                                                   -------------
//                                         Total do Fornecedor       99.999.999,99
//                                         Total do Centro de Custo 999.999.999,99
//                                         Total do Geral         9.999.999.999,99

Processa({|lEND| MDTR540TM5(@lEnd)},STR0047) //"Processando Arquivo..."

If lSigaMdtps


	cabec1 := STR0034 + Space( nSpcCli ) + Space( 2 ) + STR0018 + Space( 2 ) + STR0035 //STR0048 //"Codigo  Loja  Nome do Fornecedor"

	dbSelectArea("TRB")
	dbGotop()
	If RecCount()==0
		MsgInfo(STR0049) //"No h nada para imprimir no relatrio."
		//Use 
		oTempTRB:Delete()
		RetIndex("TM5")		
		Set Filter To		
		Set device to Screen		        
		Return .F.
	Endif		
	
	nTotalG := 0.00  //Total Geral
	lPri := .T.
	
	dbSelectArea("TRB")
	dbGotop()
	While !EOF()
	   	         
		cCliente := TRB->CLIENT+TRB->LOJA		
		nTotEmp := 0.00 //Total da Empresa
						
		Somalinha()
		@Li,000 PSAY STR0023 + AllTrim(TRB->CLIENT) + "-" + AllTrim(TRB->LOJA) + " - " + NGSEEK("SA1",cCliente,1,"A1_NOME")  //"Cliente/Loja: "		
			
		While !Eof() .and. cCliente == TRB->CLIENT+TRB->LOJA	         
		 
			cCustoF := TRB->CCUSTO //Centro de Custo
			nTotCC  := 0.00 // Total do Centro de Custo
		 
			dbSelectArea(cAliasCC)
			dbSetOrder(01)
			dbSeek(xFilial(cAliasCC)+cCustoF)
			SomaLinha()   
			Somalinha()
			@ Li,000 PSay STR0012 + AllTrim(cCustoF)+" - "+&cDescCC2 PICTURE "@!"
	          
			DbSelectArea("TRB")   
			While !EOF() .And. TRB->CCUSTO = cCustoF
	         
				cFornec  := TRB->FORNEC+TRB->LOJAF
				nTotFornc := 0.00   //Total do Fornecedor

				dbSelectArea("SA2")
				dbSetOrder(01)
				dbSeek(xFilial("SA2")+cFornec)
				SomaLinha()   
				Somalinha()
				@ Li,000 PSay Alltrim(SA2->A2_COD) PICTURE "@!"
				@ Li,009+nSpcCli PSay AllTrim(SA2->A2_LOJA) PICTURE "@!"
				@ Li,015+nSpcCli PSay SA2->A2_NOME PICTURE "@!"
	
				DbSelectArea("TRB")   
				While !EOF() .And. TRB->CCUSTO = cCustoF .And. TRB->FORNEC+TRB->LOJAF == cFornec
                     
					IncProc()
					SomaLinha()
					@ Li,002 PSay TRB->EXAME
					dbSelectArea("TM4")
					dbSetOrder(01)
					dbSeek(xFilial("TM4")+TRB->EXAME)
					@ Li,014 PSay SUBSTR(TM4->TM4_NOMEXA,1,35)
					@ Li,056 PSay TRB->QTDE    PICTURE "999,999"
					@ Li,068 PSay TRB->VALOR   PICTURE "@E 9,999,999.99"

					nTotFornc := nTotFornc + TRB->VALOR
					dbSelectArea("TRB")
					dbSkip()

				End
				nTotCC  := nTotCC  + nTotFornc 
				SomaLinha()
				@ Li,067 PSay "-------------"
				SomaLinha()
				@ Li,040 PSay STR0010 //"Total do Fornecedor"
				@ Li,068 PSay nTotFornc  PICTURE "@E 9,999,999.99"
	
			End
			nTotEmp  := nTotEmp  + nTotCC 
			SomaLinha()
			SomaLinha()
			@ Li,040 PSay STR0013 //"Total do Centro de Custo"
			@ Li,068 PSay nTotCC PICTURE "@E 9,999,999.99"
	         
		End
		
		nTotalG := nTotalG + nTotEmp
		SomaLinha()
		SomaLinha()
		@ Li,040 PSay STR0045 //"Total do Cliente"
		@ Li,068 PSay nTotEmp PICTURE "@E 9,999,999.99"
		nTotEmp := 0.00 		
		
		If !Eof()			
			Somalinha()
			@ Li,000 PSAY __PrtThinLine()			
		Endif
	
	End	
	SomaLinha()
	SomaLinha()
	@ Li,067 PSay "-------------"
	SomaLinha()
	@ Li,040 PSay STR0015 //"Total Geral"
	@ Li,068 PSay nTotalG  PICTURE "@E 9,999,999.99" 

Else

	dbSelectArea("TRB")
	dbGotop()
	If RecCount()==0
		MsgInfo(STR0049) //"No h nada para imprimir no relatrio."
		//Use
		oTempTRB:Delete()
		RetIndex("TM5")		
		Set Filter To		
		Set device to Screen		        
		Return .F.
	Endif	
	
	cUsaPS := Alltrim(GETMV("MV_NGMDTPS")) //Usa Prestadora de Servico
	nTotalG := 0.00  //Total Geral
	lPri := .T.
	
	If cUsaPS = "S"
	   dbSelectArea("TRB")
	   dbGotop()
	   While !EOF()
	   
	         If lEnd
	            @ PROW()+1,001 PSay STR0009 //"CANCELADO PELO OPERADOR"
	            Exit
	         EndIf
	         
	         cCustoF := TRB->CCUSTO //Centro de Custo
	         nTotCC  := 0.00 // Total do Centro de Custo
	         
	         If lPri
	            cEmpresa := SubStr(TRB->CCUSTO,1,6)
	            dbSelectArea("SA1")
	            dbSetOrder(01)
	            dbSeek(xFilial("SA1")+cEmpresa)
	            SomaLinha()   
	            Somalinha()
	            @ Li,000 PSay STR0011 + cEmpresa PICTURE "@!" //"Empresa...: " 
	            @ Li,022 PSay SA1->A1_NOME PICTURE "@!"
	            nTotEmp := 0.00 //Total da Empresa
	         EndIf
	         cEmpresa1 := SubStr(TRB->CCUSTO,1,6)
	         If cEmpresa <> cEmpresa1
	            cEmpresa := SubStr(TRB->CCUSTO,1,6)
	            dbSelectArea("SA1")
	            dbSetOrder(01)
	            dbSeek(xFilial("SA1")+cEmpresa)
	            SomaLinha()   
	            Somalinha()
	            @ Li,000 PSay STR0011 + cEmpresa PICTURE "@!" //"Empresa...: "
	            @ Li,022 PSay SA1->A1_NOME PICTURE "@!"
	         EndIf   
	         dbSelectArea(cAliasCC)
	         dbSetOrder(01)
	         dbSeek(xFilial(cAliasCC)+cCustoF)
	         SomaLinha()   
	         Somalinha()
	         @ Li,000 PSay STR0012 + AllTrim(cCustoF)+" - "+&cDescCC2 PICTURE "@!"
	          
	         DbSelectArea("TRB")   
	         While !EOF()  .And. TRB->CCUSTO = cCustoF
	         
	               cFornec  := TRB->FORNEC
	               nTotFornc := 0.00 //Total do Fornecedor
	               
	               dbSelectArea("SA2")
	               dbSetOrder(01)
	               dbSeek(xFilial("SA2")+cFornec)
	               SomaLinha()   
	               Somalinha()
	               @ Li,000 PSay Alltrim(cFornec)+" - "+SA2->A2_NOME PICTURE "@!"
	
	               DbSelectArea("TRB")   
	               While !EOF() .And. TRB->CCUSTO = cCustoF .And. TRB->FORNEC == cFornec
	                     
	                     IncProc()
	                     SomaLinha()
	                     @ Li,002 PSay TRB->EXAME
	                     dbSelectArea("TM4")
	                     dbSetOrder(01)
	                     dbSeek(xFilial("TM4")+TRB->EXAME)
	                     @ Li,014 PSay SUBSTR(TM4->TM4_NOMEXA,1,35)
	                     @ Li,056 PSay TRB->QTDE    PICTURE "999,999"
	                     @ Li,068 PSay TRB->VALOR   PICTURE "@E 9,999,999.99"
	
	                     nTotFornc := nTotFornc + TRB->VALOR
	                     dbSelectArea("TRB")
	                     dbSkip()
	
	               EndDo 
	               nTotCC  := nTotCC  + nTotFornc 
	               SomaLinha()
	               @ Li,067 PSay "-------------"
	               SomaLinha()
	               @ Li,040 PSay STR0010 //"Total do Fornecedor"
	               @ Li,068 PSay nTotFornc  PICTURE "@E 9,999,999.99"
	
	         EndDo
	         nTotEmp  := nTotEmp  + nTotCC 
	         SomaLinha()
	         SomaLinha()
	         @ Li,040 PSay STR0013 //"Total do Centro de Custo"
	         @ Li,068 PSay nTotCC PICTURE "@E 9,999,999.99"
	         
	         cEmpresa1 := SubStr(TRB->CCUSTO,1,6)
	         If cEmpresa <> cEmpresa1
	            nTotalG := nTotalG + nTotEmp
	            SomaLinha()
	            SomaLinha()
	            @ Li,040 PSay STR0014 //"Total da Empresa"
	            @ Li,068 PSay nTotEmp PICTURE "@E 9,999,999.99"
	            nTotEmp := 0.00 
	          EndIf  
	   Enddo
	Else
	   dbSelectArea("TRB")
	   dbGotop()
	   While !EOF()
	
	         If lEnd
	            @ PROW()+1,001 PSay STR0009 //"CANCELADO PELO OPERADOR"
	            Exit
	         EndIf
	
	         cCustoF := TRB->CCUSTO //Centro de Custo
	         nTotCC  := 0.00 // Total do Centro de Custo
	
	         dbSelectArea(cAliasCC)
	         dbSetOrder(01)
	         dbSeek(xFilial(cAliasCC)+cCustoF)
	         SomaLinha()   
	         Somalinha()
	         @ Li,000 PSay STR0012 + Alltrim(cCustoF)+" - "+&cDescCC2 PICTURE "@!"
	          
	         DbSelectArea("TRB")   
	         While !EOF()  .AND. TRB->CCUSTO = cCustoF
	         
	               cFornec  := TRB->FORNEC
	               nTotFornc := 0.00 //Total do Fornecedor
	               
	               dbSelectArea("SA2")
	               dbSetOrder(01)
	               dbSeek(xFilial("SA2")+cFornec)
	               SomaLinha()   
	               Somalinha()
	               @ Li,000 PSay Alltrim(cFornec)+" - "+SA2->A2_NOME PICTURE "@!"
	
	               DbSelectArea("TRB")   
	               While !EOF() .And. TRB->CCUSTO = cCustoF .And. TRB->FORNEC == cFornec
	                     
	                     IncProc()
	                     SomaLinha()
	                     @ Li,002 PSay TRB->EXAME
	                     dbSelectArea("TM4")
	                     dbSetOrder(01)
	                     dbSeek(xFilial("TM4")+TRB->EXAME)
	                     @ Li,014 PSay SUBSTR(TM4->TM4_NOMEXA,1,35)
	                     @ Li,056 PSay TRB->QTDE    PICTURE "999,999"
	                     @ Li,068 PSay TRB->VALOR   PICTURE "@E 9,999,999.99"
	
	                     nTotFornc := nTotFornc + TRB->VALOR
	                     dbSelectArea("TRB")
	                     dbSkip()
	
	               EndDo 
	               nTotCC  := nTotCC  + nTotFornc 
	               SomaLinha()
	               @ Li,067 PSay "-------------"
	               SomaLinha()
	               @ Li,040 PSay STR0010 //"Total do Fornecedor"
	               @ Li,068 PSay nTotFornc  PICTURE "@E 9,999,999.99"
	
	         EndDo
	         nTotalG := nTotalG + nTotCC 
	         SomaLinha()
	         SomaLinha()
	         @ Li,040 PSay STR0013 //"Total do Centro de Custo"
	         @ Li,068 PSay nTotCC PICTURE "@E 9,999,999.99"
	   Enddo
	EndIf   
	SomaLinha()
	SomaLinha()
	@ Li,067 PSay "-------------"
	SomaLinha()
	@ Li,040 PSay STR0015 //"Total Geral"
	@ Li,068 PSay nTotalG  PICTURE "@E 9,999,999.99" 

Endif

Roda(nCntImpr,cRodaTxt,Tamanho)

//Ŀ
// Devolve a condicao original do arquivo principal             
//
RetIndex("TM5")

Set Filter To

Set device to Screen

If aReturn[5] = 1
    Set Printer To
    dbCommitAll()
    OurSpool(wnrel)
Endif
MS_FLUSH()

dbSelectArea("TRB")
//USE
oTempTRB:Delete()

Return NIL          

/*


Ŀ
Funo    ReportPrint Autor  Rafael Diogo Richter   Data  08/08/06 
Ĵ
Descrio  Chamada do Relatrio                                        
Ĵ
 Uso       ReportDef                                                   
ٱ


*/
Static Function ReportPrint(oReport)

Local oSection1 
Local oSection2 
Local oSection3 
Local oSection4
Local cFornec
Local cUsaPS := Alltrim(GETMV("MV_NGMDTPS")) //Usa Prestadora de Servico
Private aVetinr := {}, oTempTRB

Processa({|lEND| MDTR540TM5(@lEnd)},STR0047) //"Processando Arquivo..."

If lSigaMdtps

	oSection1 := oReport:Section(1)
	oSection2 := oReport:Section(2)
	oSection3 := oReport:Section(3)
	oSection4 := oReport:Section(4)

	lPri := .T.
	
	dbSelectArea("TRB")
	dbGotop()
	oReport:SetMeter(RecCount())
	
	While !EOF() .And. !oReport:Cancel()

		cCliente := TRB->CLIENT+TRB->LOJA        //Cliente
		oSection1:Init()
		oSection1:PrintLine()
		
		While !EOF() .And. !oReport:Cancel() .and. cCliente == TRB->CLIENT+TRB->LOJA
		
			cCustoF := TRB->CCUSTO //Centro de Custo
			nTotCC  := 0.00 // Total do Centro de Custo
	      
			oSection2:Init()
			oSection2:PrintLine()
	  	
			DbSelectArea("TRB")   
			While !EOF()  .And. TRB->CCUSTO = cCustoF .And. !oReport:Cancel()
	         
				cFornec  := TRB->FORNEC+TRB->LOJAF
				nTotFornc := 0.00 //Total do Fornecedor
	
				oSection3:Init()
				oSection3:PrintLine()
				oSection4:Init()
	
				DbSelectArea("TRB")   
				While !EOF() .And. TRB->CCUSTO = cCustoF .And. TRB->FORNEC+TRB->LOJAF == cFornec .And. !oReport:Cancel()
	                     
					oReport:IncMeter()				
					oSection4:PrintLine()
					
					dbSelectArea("TRB")
					dbSkip()
				EndDo
				oSection4:Finish()
				oSection3:Finish()				
			EndDo
			oSection2:Finish()
			
		End
		oSection1:Finish()
		
	Enddo

Else

	oSection1 := oReport:Section(1)
	oSection2 := oReport:Section(2)
	oSection3 := oReport:Section(3)
	oSection4 := oReport:Section(4)

	lPri := .T.
	
	If cUsaPS = "S"
		dbSelectArea("TRB")
		dbGotop()
		oReport:SetMeter(RecCount())
		While !EOF() .And. !oReport:Cancel()
	
			cCustoF := TRB->CCUSTO //Centro de Custo
			nTotCC  := 0.00 // Total do Centro de Custo
	      
			oSection1:Init()
			oSection1:PrintLine()
	  
			oSection2:Init()
			oSection2:PrintLine()
	
			DbSelectArea("TRB")   
			While !EOF()  .And. TRB->CCUSTO = cCustoF .And. !oReport:Cancel()
	         
				cFornec  := TRB->FORNEC
				nTotFornc := 0.00 //Total do Fornecedor
	
				oSection3:Init()
				oSection3:PrintLine()
				oSection4:Init()
	
				DbSelectArea("TRB")   
				While !EOF() .And. TRB->CCUSTO = cCustoF .And. TRB->FORNEC == cFornec .And. !oReport:Cancel()
	                     
					oReport:IncMeter()				
					oSection4:PrintLine()
					
					dbSelectArea("TRB")
					dbSkip()
				EndDo
				oSection3:Finish()
				oSection4:Finish()
			EndDo
			oSection1:Finish()
			oSection2:Finish()
		Enddo
	Else
		dbSelectArea("TRB")
		dbGotop()
		oReport:SetMeter(RecCount())
		While !EOF() .And. !oReport:Cancel()
	
			cCustoF := TRB->CCUSTO //Centro de Custo
			nTotCC  := 0.00 // Total do Centro de Custo
	
			oSection1:Init()
			oSection1:PrintLine()
	       
			DbSelectArea("TRB")   
			While !EOF()  .AND. TRB->CCUSTO = cCustoF .And. !oReport:Cancel()
	      	
				cFornec  := TRB->FORNEC
				nTotFornc := 0.00 //Total do Fornecedor
	               
				oSection2:Init()
				oSection2:PrintLine()
				oSection3:Init()
	
				DbSelectArea("TRB")   
				While !EOF() .And. TRB->CCUSTO = cCustoF .And. TRB->FORNEC == cFornec .And. !oReport:Cancel()
	   
					oReport:IncMeter()
					
					oSection3:PrintLine()
					
					DbSelectArea("TRB")
					DbSkip()
				End 
			oSection2:Finish()
			oSection3:Finish()
			End
			oSection1:Finish()
	   End
	EndIf       

Endif

dbSelectArea("TRB")
dbGotop()
If RecCount()==0
	MsgInfo(STR0049) //"No h nada para imprimir no relatrio."
	//Use
	oTempTRB:Delete()
	RetIndex("TM5")		
	Set Filter To		
	Set device to Screen		        
	Return .F.
Endif

dbSelectArea("TRB")
//USE
oTempTRB:Delete()

Return .T.

/*/

Ŀ
 Funo    SomaLinha Autor  Inacio Luiz Kolling    Data    /06/97 
Ĵ
 Descrio Incrementa Linha e Controla Salto de Pagina                
Ĵ
 Sintaxe   SomaLinha()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       MDTR405                                                    
ٱ

/*/
Static Function Somalinha()
    Li++
    If Li > 58
        Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
    EndIf
Return

/*/


Ŀ
Funo    MDTR540TM5 Autor  Rafael Diogo Richter   Data 08/08/2006
Ĵ
Descrio Processa o arquivo temporario.                              
Ĵ
 Uso       SigaMDT                                                    
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MDTR540TM5(lEnd)
//Local lContinua := .T.
Local nValForn
Local lAchoExa

Local nSizeSA2 := If((TAMSX3("A2_COD")[1])		< 1, 6, (TAMSX3("A2_COD")[1])	)
Local nSizeLo2 := If((TAMSX3("A2_LOJA")[1])		< 1, 6, (TAMSX3("A2_LOJA")[1])	)
Local nTamExa	:= If((TAMSX3("TM4_EXAME")[1])	< 1, 6, (TAMSX3("TM4_EXAME")[1]))
Private aVetinr := {}

If lSigaMdtps

	aDBF := {}
	AADD(aDBF,{"CLIENT","C",nTa1,0})
	AADD(aDBF,{"LOJA"  ,"C",nTa1L,0})	
	AADD(aDBF,{"FORNEC","C",nTa2,0})
	AADD(aDBF,{"LOJAF" ,"C",nTa2L,0})
	AADD(aDBF,{"EXAME" ,"C",nTamExa,0})
	AADD(aDBF,{"QTDE"  ,"N",06,0})
	AADD(aDBF,{"VALOR" ,"N",12,2})
	AADD(aDBF,{"CCUSTO","C",nSizeSI3,0})
	
	//Cria TRB
	oTempTRB := FWTemporaryTable():New( "TRB", aDBF )
	oTempTRB:AddIndex( "1", {"CCUSTO","FORNEC","LOJAF","EXAME"} )
	oTempTRB:Create()
	
	dbSelectArea("TM5")
	dbSetOrder(07)       //TM5_FILIAL+TM5_CC+TM5_EXAME
	dbSeek(xFilial("TM5")+MV_PAR05,.T.)
	
	ProcRegua(LastRec())
	
	//Ŀ
	// Correr TM5 para ler os  Exames                           
	//
	
	While !Eof()                              .AND.;
	      TM5->TM5_FILIAL == xFIlial('TM5')   .AND.;
	      TM5->TM5_CC <=  MV_PAR06
	
		IncProc()
		
		cCliente := substr(TM5->TM5_CC,1,nSizeTD)
		if cCliente < mv_par01+mv_par02 .or. cCliente > mv_par03+mv_par04
			DbSelectArea("TM5")
			DbSkip()
			Loop
		EndIf
		IF TM5->TM5_FORNEC+TM5->TM5_LOJA < MV_PAR07+MV_PAR08 .OR. TM5->TM5_FORNEC+TM5->TM5_LOJA > MV_PAR09+MV_PAR10
			DbSelectArea("TM5")
			DbSkip()
			Loop
		EndIf
		IF TM5->TM5_EXAME < MV_PAR11 .OR. TM5->TM5_EXAME > MV_PAR12
			DbSelectArea("TM5")
			DbSkip()
			Loop
		EndIf 
		IF TM5->TM5_DTRESU < MV_PAR13 .OR. TM5->TM5_DTRESU > MV_PAR14
			DbSelectArea("TM5")
			DbSkip()
			Loop
		EndIf
			
		If !Empty(TM5->TM5_DTRESU)
	
			nValforn := 0.00	        	
			lAchoExa := .F.
	
			Dbselectarea("TOL")
			Dbsetorder(3)
			Dbseek(xFilial("TOL")+TM5->TM5_FORNEC+TM5->TM5_LOJA)
			While !eof() .and. xFilial("TOL")+TM5->TM5_FORNEC+TM5->TM5_LOJA == TOL->(TOL_FILIAL+TOL_FORNEC+TOL_LOJAFO) .and. !lAchoExa
				If TOL->TOL_DTINIC <= TM5->TM5_DTRESU .and. TOL->TOL_DTTERM >= TM5->TM5_DTRESU .and. ;
					TOL->TOL_STATUS $ "1/3" .and. TOL->TOL_TIPCON == "2"
			
					Dbselectarea("TOM")
					Dbsetorder(1)
					Dbseek(xFilial("TOM")+TOL->TOL_NUMERO)
					While !eof() .and. xFilial("TOM")+TOL->TOL_NUMERO == TOM->TOM_FILIAL+TOM->TOM_NUMERO .and. !lAchoExa
						If Alltrim("MDTE"+TM5->TM5_EXAME) == Alltrim(TOM->TOM_SERVIC)
							lAchoExa := .t.
							nValforn := TOM->TOM_VALOR					
						Endif
						Dbselectarea("TOM")
						Dbskip()
					End
				Endif
				Dbselectarea("TOL")
				Dbskip()
			End	         
			 
			dbSelectArea("TRB")
			If !dbseek(TM5->TM5_CC+TM5->TM5_FORNEC+TM5->TM5_LOJA+TM5->TM5_EXAME)
				TRB->(DbAppend())
				TRB->CLIENT := SubStr(cCliente,1,nTa1)
				TRB->LOJA   := SubStr(cCliente,nTa1+1,nSizeTD)				
				TRB->CCUSTO := TM5->TM5_CC
				TRB->FORNEC := TM5->TM5_FORNEC
				TRB->LOJAF  := TM5->TM5_LOJA
				TRB->EXAME  := TM5->TM5_EXAME
			EndIf
			TRB->QTDE  := TRB->QTDE + 1
			TRB->VALOR := TRB->VALOR + nValforn 
	
		EndIf
		dbSelectArea("TM5")
		dbSKIP()
	End
	
Else

	aDBF := {}
	AADD(aDBF,{"FORNEC","C",nSizeSA2,0})
	AADD(aDBF,{"LOJA"  ,"C",nSizeLo2,0})
	AADD(aDBF,{"EXAME" ,"C",nTamExa,0})
	AADD(aDBF,{"QTDE"  ,"N",06,0})
	AADD(aDBF,{"VALOR" ,"N",12,2})
	AADD(aDBF,{"CCUSTO","C",nSizeSI3,0})

	oTempTRB := FWTemporaryTable():New( "TRB", aDBF )
	oTempTRB:AddIndex( "1", {"CCUSTO","FORNEC","LOJA","EXAME"} )
	oTempTRB:Create()
	
	dbSelectArea("TM5")
	dbSetOrder(07)
	dbSeek(xFilial("TM5")+MV_PAR01,.T.)
	
	ProcRegua(LastRec())
	
	//Ŀ
	// Correr TM5 para ler os  Exames                           
	//
	
	While !Eof()                              .AND.;
	      TM5->TM5_FILIAL == xFIlial('TM5')   .AND.;
	      TM5->TM5_CC <=  MV_PAR02
	
		IncProc()
		IF TM5->TM5_FORNEC < MV_PAR03 .OR. TM5->TM5_FORNEC > MV_PAR04
			DbSelectArea("TM5")
			DbSkip()
			Loop
		EndIf
		IF TM5->TM5_EXAME < MV_PAR05 .OR. TM5->TM5_EXAME > MV_PAR06
			DbSelectArea("TM5")
			DbSkip()
			Loop
		EndIf 
		IF TM5->TM5_DTRESU < MV_PAR07 .OR. TM5->TM5_DTRESU > MV_PAR08
			DbSelectArea("TM5")
			DbSkip()
			Loop
		EndIf
	
		If !Empty(TM5->TM5_DTRESU)
	
			nValforn := 0.00
	
			dbSelectArea("TMD")
			dbSetOrder(01)
			dbSeek(xFilial("TMD")+TM5->TM5_FORNEC+TM5->TM5_LOJA+TM5->TM5_EXAME)
	
			While !Eof() .AND.; 
				  TMD->TMD_FILIAL == xFIlial('TMD') .AND.; 
				  TMD->TMD_FORNEC == TM5->TM5_FORNEC .AND.; 
				  TMD->TMD_LOJA == TM5->TM5_LOJA .AND.; 
				  TMD->TMD_EXAME == TM5->TM5_EXAME
	
				If TMD->TMD_DTINIC <= TM5->TM5_DTRESU
					nValforn := TMD->TMD_VALEXA
				Else
					Exit
				Endif
	
				dbSelectArea("TMD")
				dbSkip()
	         End
			 
			dbSelectArea("TRB")
			If !dbseek(TM5->TM5_CC+TM5->TM5_FORNEC+TM5->TM5_LOJA+TM5->TM5_EXAME)
				TRB->(DbAppend())
				TRB->CCUSTO := TM5->TM5_CC
				TRB->FORNEC := TM5->TM5_FORNEC
				TRB->LOJA   := TM5->TM5_LOJA
				TRB->EXAME  := TM5->TM5_EXAME
			EndIf
			TRB->QTDE  := TRB->QTDE + 1
			TRB->VALOR := TRB->VALOR + nValforn 
	
		EndIf
		dbSelectArea("TM5")
		dbSKIP()
	End
	
Endif	

Return .T.