#INCLUDE "MNTR560.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR560   ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 11/10/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Indice de Pneus Recusados por Recapadora       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function MNTR560()  
Private cAliasQry  := GetNextAlias()
Private lnRegistro := .F.

Private NOMEPROG := "MNTR560"
Private TAMANHO  := "M"
Private aRETURN  := {STR0001,1,STR0002,1,2,1,"",1} //"Zebrado"###"Administracao"
Private TITULO   := STR0003 //"Relatório de Indice de Pneus Recusados por Recapadora"
Private nTIPO    := 0
Private nLASTKEY := 0
Private CABEC1,CABEC2 
Private aVETINR := {}    
Private cPERG := "MNR560"   
Private aPerg :={}
            
SetKey( VK_F9, { | | NGVersao( "MNTR560" , 1 ) } )

WNREL      := "MNTR560"
LIMITE     := 220
cDESC1     := STR0004 //"O relatório apresentará índices de pneus"
cDESC2     := STR0005 //"recusados pelas recapadoras."
cDESC3     := ''
cSTRING    := "TR7"       

Pergunte(cPERG,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
If nLASTKEY = 27
   DbSelectArea("TR7")  
   Return
EndIf     
SetDefault(aReturn,cSTRING)
RptStatus({|lEND| MNTR560IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0014,STR0015) //"Aguarde..."###"Processando Registros..."
Dbselectarea("TR7")  

Return .T.    

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNT560IMP | Autor ³ Marcos Wagner Junior  ³ Data ³ 11/10/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR560                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR560IMP(lEND,WNREL,TITULO,TAMANHO) 
Local nTotalPneu := 0, nRecusados := 0, nGerRecus := 0, nGerPneus := 0
Private cRODATXT := ""
Private nCNTIMPR := 0     
Private li := 80 ,m_pag := 1  
Private cFornece := '', cLoja := ''

nTIPO  := IIf(aReturn[4]==1,15,18)                                                                                                                                                                                               
            
CABEC1 := STR0016 //"Recapadora   Loja   Nome                                       Qtde Enviada   Qtde Recusada   % Recusado"
CABEC2 := ""   
/*
          1         2         3         4         5         6         7         8         9         0   
01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123
Recapadora   Loja   Nome                                       Qtde Enviada   Qtde Recusada   % Recusado
xxxxxxxx     xxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx          99999           99999       %99.99
*/

cAliasQry := GetNextAlias()
cQuery := " SELECT * "
cQuery += " FROM " + RetSqlName("TR7")+" TR7, " + RetSqlName("TR8")+" TR8 "
cQuery += " WHERE TR7.TR7_FILIAL >= '"+MV_PAR01+"'"
cQuery += " AND   TR7.TR7_FILIAL <= '"+MV_PAR02+"'"
cQuery += " AND   TR7.TR7_DTRECI >= '"+DTOS(MV_PAR03)+"'"
cQuery += " AND   TR7.TR7_DTRECI <= '"+DTOS(MV_PAR04)+"'"
cQuery += " AND   TR7.TR7_FORNEC >= '"+MV_PAR05+"'"
cQuery += " AND   TR7.TR7_LOJA   >= '"+MV_PAR06+"'"
cQuery += " AND   TR7.TR7_FORNEC <= '"+MV_PAR07+"'"
cQuery += " AND   TR7.TR7_LOJA   <= '"+MV_PAR08+"'"
cQuery += " AND   TR7.TR7_FILIAL = TR8.TR8_FILIAL "
cQuery += " AND   TR7.TR7_LOTE   = TR8.TR8_LOTE "
cQuery += " AND   TR7.D_E_L_E_T_ <> '*' "
cQuery += " AND   TR8.D_E_L_E_T_ <> '*' "
cQuery += " ORDER BY TR7.TR7_FORNEC, TR7.TR7_LOJA "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.) 
dbGoTop()

If !Eof()
	NgSomaLi(58)
	cFornece := (cAliasQry)->TR7_FORNEC
	cLoja    := (cAliasQry)->TR7_LOJA
	While !Eof()
		nTotalPneu++

		If (cAliasQry)->TR8_INDREL = '2'
			nRecusados++
		Endif
		cFornece := (cAliasQry)->TR7_FORNEC
		cLoja    := (cAliasQry)->TR7_LOJA
		dbSkip()
		If Eof() .OR. (cFornece != (cAliasQry)->TR7_FORNEC .OR. cLoja != (cAliasQry)->TR7_LOJA)
			If nRecusados > 0
			   @ Li,000   Psay cFornece
			   @ Li,013   Psay cLoja
	  		   @ Li,020   Psay NGSEEK('SA2',cFornece+cLoja,1,'SA2->A2_NOME')
			   @ Li,063   Psay AllTrim(Str(nTotalPneu))
			   @ Li,078   Psay AllTrim(Str(nRecusados))
			   nPerce := Round(((nRecusados/nTotalPneu) *100), 2)
			   If nRecusados == nTotalPneu
			   	@ Li,097   Psay Transform(Round(nPerce, 2),"@E 999.99")+"%"
			   Else
			   	If nRecusados == 0
			   		@ Li,099   Psay Transform(Round(nPerce, 2),"@E 9.99")+"%"
			   	Else
			   		@ Li,098   Psay Transform(Round(nPerce, 2),"@E 99.99")+"%"
			   	Endif
			   Endif
		   	NgSomaLi(58)
			   nGerPneus += nTotalPneu
			   nGerRecus += nRecusados
			Endif
		   nTotalPneu := 0
		   nRecusados := 0  
		Endif
		If Eof()
			NgSomaLi(58)
			@ Li,000   Psay STR0023 //"TOTAL"
		   @ Li,063   Psay AllTrim(Str(nGerPneus))
		   @ Li,078   Psay AllTrim(Str(nGerRecus))
		   nPerce := Round(((nGerRecus/nGerPneus) *100), 2)
		   If nRecusados == nTotalPneu
		   	@ Li,097   Psay Transform(Round(nPerce, 2),"@E 999.99")+"%"
		   Else
		   	If nRecusados == 0
		   		@ Li,099   Psay Transform(Round(nPerce, 2),"@E 9.99")+"%"
		   	Else
		   		@ Li,098   Psay Transform(Round(nPerce, 2),"@E 99.99")+"%"
		   	Endif
		   Endif
		Endif
	End	
Else
	MsgStop(STR0017,STR0018) //"Não existem dados para montar o relatório!"###"ATENÇÃO"
	Return .f.
Endif

RODA(nCNTIMPR,cRODATXT,TAMANHO)       

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex('TR7')
Set Filter To
Set Device To Screen
If aReturn[5] == 1
   Set Printer To
   dbCommitAll()
   OurSpool(WNREL)
EndIf
MS_FLUSH()

Return Nil  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT560DT  ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 11/10/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro De/Ate Data                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR560                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT560DT()

If  MV_PAR04 < MV_PAR03 .AND. !Empty(MV_PAR03) .AND. !Empty(MV_PAR04)
    MsgStop(STR0019) //"Até Data não pode ser inferior à De Data!"
    Return .F.  
EndIf  

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR560LOJ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 11/10/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro de Loja                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR560	                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR560LOJ(nForn,nLoja)

If ReadVar() = 'MV_PAR07'
	If MV_PAR07 = Replicate('Z',TAMSX3('A2_COD')[1])
		Return .t.
	Endif
Endif
If ReadVar() = 'MV_PAR08'
	If MV_PAR08 = Replicate('Z',TAMSX3('A2_LOJA')[1])
		Return .t.
	Endif
Endif

If ReadVar() = 'MV_PAR05' .OR. ReadVar() = 'MV_PAR06'
	cPosto := MV_PAR05
	cLoja  := MV_PAR06
Else
	cPosto := MV_PAR07
	cLoja  := MV_PAR08
Endif

If ReadVar() = 'MV_PAR05'
	If Empty(MV_PAR05)
		Return .t.
	Else
		If !NGSEEKCPO('SA2',MV_PAR05)
			Return .f.
		Endif
	Endif
Elseif ReadVar() = 'MV_PAR07'
	If Empty(MV_PAR07)
		Return .t.
	Else
		If !NGSEEKCPO('SA2',MV_PAR07)
			Return .f.
		Endif
	Endif
Endif

If !Empty(MV_PAR05) .AND. !Empty(MV_PAR07) .AND. (MV_PAR05 > MV_PAR07) .AND. (ReadVar() = 'MV_PAR05' .OR. ReadVar() = 'MV_PAR07')
   MsgStop(STR0020) //"De Fornecedor deverá ser menor que Até Fornecedor"
   Return .F.
Endif

If Empty(cPosto) .And. !Empty(cLoja)
   MsgStop(STR0021) //"Informe o Codigo do Fornecedor"
   cLoja := Space(Len(cLoja))
   Return .F. 
EndIf
If ReadVar() = 'MV_PAR06' .OR. ReadVar() = 'MV_PAR08'
	If !Empty(cPosto) .And. Empty(cLoja)
	   MsgStop(STR0022) //"Informe o Codigo da Loja"
	   Return .F. 
	EndIf
EndIf
If !Empty(cPosto) .And. !Empty(cLoja)
	IF !ExistCpo("SA2",cPosto+cLoja)
		Return .F.
	EndIf	
EndIf

   
Return .T.  