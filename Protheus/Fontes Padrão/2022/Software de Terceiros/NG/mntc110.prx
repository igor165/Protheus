#INCLUDE "MNTC110.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"  

#DEFINE _nVERSAO 2 //Versao do fonte

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTC110
Servicos do Tipo de Manutencao

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Function MNTC110( cTipoManut )
	
	Local aNGBeginPrm := NGBeginPrm( _nVERSAO )
	Local oBrowse, cFilterBrw
	
	Default cTipoManut := Space( TamSx3("T4_TIPOMAN")[1] )
	
	Private aRotina := MenuDef()
	
	cFilterBrw := " ST4->T4_FILIAL  == '" + xFilial("ST4") + "' .And. "
	cFilterBrw += " ST4->T4_TIPOMAN == '" + cTipoManut + "' "

	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias( "ST4" )
	oBrowse:SetFilterDefault( cFilterBrw )
	oBrowse:SetDescription( STR0001 ) // "Servicos"
	oBrowse:Activate()
	
	NGReturnPrm( aNGBeginPrm )
	
Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Interface da rotina

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function MenuDef()

	Local aRotina := {}
	
	ADD OPTION aRotina TITLE STR0003 ACTION 'VIEWDEF.MNTC110' OPERATION 2 ACCESS 0 //"Visualizar"

Return aRotina

//------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ModelDef()

	Local oModel

	Local oStructST4 := FWFormStruct(1,"ST4")

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTC110")

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTC110_ST4", Nil, oStructST4)

	oModel:SetDescription(STR0001)

Return oModel

//------------------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel("MNTC110")
	Local oView  := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTC110_ST4" , FWFormStruct(2,"ST4"))

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100 )

	// Associa um View a um box
	oView:SetOwnerView( "MNTC110_ST4" , "MASTER" )
	
	// Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn( oView )

Return oView