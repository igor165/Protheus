#INCLUDE "SGAG020.ch"
#include "Protheus.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SGAG020  ³ Autor ³ Thiago Olis Machado   ³ Data ³04/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grafico de Origem da Geracao do Residuo                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TB0 - Ocorrencia de Residuo.                        	     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Sgag020()
//---------------------------------------------------
// Armazena variaveis p/ devolucao (NGRIGHTCLICK)
//---------------------------------------------------
Local aNGBEGINPRM := NGBEGINPRM()

Local nOpca
Local nTamProd    	:= If( TAMSX3( "B1_COD" )[ 1 ] > 0 , TAMSX3( "B1_COD" )[ 1 ] , 15 )
Local cPerg       	:= PADR( "SGG020", 10 )
Local cAliTRB     	:= GetNextAlias()
Local aDBF        	:= {}
Local oDlg1, oPnl, oList,oPnlLis
Local aBtnG020		:= {{"",{|| SG020GRAFI(@cAliTRB)},STR0012,STR0012}} //"&Grafico"
Local oTempTRBA

Private cCadastro := OemToAnsi(STR0001)  //"Geracao de Residuos por Origem"
Private INCLUI    := .F.

//---------------------------------------------------
// Verifica as perguntas selecionadas
//---------------------------------------------------
//---------------------------------------------------
// Variaveis utilizadas para parametros
// mv_par01     // De Data
// mv_par02     // Ate Data
// mv_par03     // Gestao Ambiental   ?
// mv_par04     // Manutencao de Ativos?
// mv_par05     // PCP ?
//---------------------------------------------------

If !Pergunte(cPerg,.T.)
   Return
EndIf

aDBF := {{"CODRES" , "C", nTamProd,0} ,;
          {"DESCR"  , "C", 30,0} ,;
          {"QUANT" , "N", 14,2} ,;
          {"ORIGEM", "C", 20,0}}

oTempTRBA := FWTemporaryTable():New( cAliTRB, aDBF )
oTempTRBA:AddIndex( "1", {"ORIGEM"} )
oTempTRBA:Create()

//Processa as Opcoes do Criterio de Avaliacao
Processa({ || SGG020PROC(@cAliTRB) })

DbSelectArea(cAliTRB)
DbGoTop()
If Reccount() == 0
   MsgInfo(STR0008+CHR(13)+STR0009,STR0010) //"Segundo especificao dos parametros"###"nao existem dados para serem mostrados"###"ATENCAO"
   oTempTRBA:Delete()
   Return
Endif

Define MsDialog oDlg1 Title STR0011 From 0,0 to 250,600 Of oMainWnd Pixel  //"Geracao de Residuos por Origem "

	oPnl := TPanel():New(0,0,,oDlg1,,,,,,0,0,.F.,.F.)
	oPnl:Align := CONTROL_ALIGN_ALLCLIENT

		oPnlLis := TPanel():New(0,0,,oPnl,,,,,,0,20,.F.,.F.)
		oPnlLis:Align := CONTROL_ALIGN_ALLCLIENT

		@ 02.5,00 Listbox oList;
			  Fields ORIGEM,;
	                 PADL(Transform(QUANT,"@E 99,999,999,999.99"),14),;
	          FieldSizes 80,40;
	          Size 310,95;
	          Of oPnlLis;
	          HEADERS STR0013,;  //"Origem"
	                  STR0014; //"Quantidade"
	          Pixel
	 	oList:Align := CONTROL_ALIGN_ALLCLIENT

Activate MsDialog oDlg1 On Init EnchoiceBar(oDlg1,{||nopca:=1,oDlg1:End()},{||oDlg1:End()},,aBtnG020) Centered

oTempTRBA:Delete()

//---------------------------------------------------
// Devolve variaveis armazenadas (NGRIGHTCLICK)
//---------------------------------------------------
NGRETURNPRM(aNGBEGINPRM)
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGG020PROC³ Autor ³ Rafael Diogo Richter  ³ Data ³04/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa as Ocorrencias.                         		     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TB0 - Ocorrencias de Residuo.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGG020PROC(cAliTRB)
Local cOrigem

//Processa o Criterio de Avaliacao do Aspecto(TA6)
DbSelectArea("TB0")
DbSetOrder(03)
DbSeek(xFilial("TB0")+DtoS( Mv_Par01 ),.t. )

While !Eof() .And. TB0->TB0_FILIAL = xFILIAL("TB0") .And. TB0->TB0_DATA <= Mv_Par02

	If TB0->TB0_ORIGEM == "1" .and. Mv_Par03 == 2
		DbSelectArea("TB0")
		Dbskip()
		Loop
	EndIf

	If TB0->TB0_ORIGEM == "2" .and. Mv_Par04 == 2
		DbSelectArea("TB0")
		Dbskip()
		Loop
	EndIf

	If TB0->TB0_ORIGEM == "3" .and. Mv_Par05 == 2
		DbSelectArea("TB0")
		Dbskip()
		Loop
	EndIf

	If TB0->TB0_ORIGEM == "2"
		cOrigem := STR0016 //"Manutencao de Ativos"
	ElseIf TB0->TB0_ORIGEM == "3"
		cOrigem := STR0020 //"PCP"
	Else
		cOrigem := STR0015 //"Gestao Ambiental"
	EndIf

	DbSelectArea(cAliTRB)
	DbSetOrder(1)
	If !(DBSEEK(cOrigem))
		(cAliTRB)->(DbAppend())
		(cAliTRB)->CODRES := TB0->TB0_CODRES
		(cAliTRB)->QUANT  := TB0->TB0_QTDE
		(cAliTRB)->ORIGEM := cOrigem
	Else
		(cAliTRB)->QUANT += TB0->TB0_QTDE
	EndIf

	DbSelectArea("TB0")
	DbSkip()
End

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SG020GRAFI³ Autor ³ Thiago Olis Machado   ³ Data ³04/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para montar o Grafico dos Criterios de Avaliacao   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TB5 - Licenciamento do Fornecedor por Produto               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SG020GRAFI(cAliTRB)
Local cTilucen := STR0001 //"Geracao de Residuos por Origem"
Local cAliTRB2 := GetNextAlias()
Local aDBF3
Local oTempTRB3

//ARQUIVO DE DADOS
aDBF3 := {	{"CODIGO" , "C", 03,0} ,;
			{"NOMECOD", "C", 30,0} ,;
			{"VALOR1" , "N", 14,2} }

oTempTRB3 := FWTemporaryTable():New( cAliTRB2, aDBF3 )
oTempTRB3:AddIndex( "1", {"CODIGO"} )
oTempTRB3:Create()

DbselectArea(cAliTRB)
While !Eof()

	(cAliTRB2)->(DbAppend())
	If AllTrim((cAliTRB)->ORIGEM) == STR0015 //"Gestao Ambiental"
		(cAliTRB2)->CODIGO  := "1"
		(cAliTRB2)->NOMECOD := STR0015 //"Gestao Ambiental"
		(cAliTRB2)->VALOR1  := (cAliTRB)->QUANT
	ElseIf AllTrim((cAliTRB)->ORIGEM) == STR0016 //"Manutencao de Ativos"
		(cAliTRB2)->CODIGO  := "2"
		(cAliTRB2)->NOMECOD := STR0016 //"Manutencao de Ativos"
		(cAliTRB2)->VALOR1  := (cAliTRB)->QUANT
	Else
		(cAliTRB2)->CODIGO  := "3"
		(cAliTRB2)->NOMECOD := STR0020 //"PCP"
		(cAliTRB2)->VALOR1  := (cAliTRB)->QUANT
	EndIf
	DbselectArea(cAliTRB)
	Dbskip()
End

vCRIGTXT := NGGRAFICO(" "+cTilucen," ",cTilucen,STR0017 + DtoC( Mv_Par01 ) + STR0018 + DtoC( Mv_Par02 ) ,"",; //"Geracao de Residuos por Origem no periodo de "###" Ate "
								{STR0015},"A",cAliTRB2) //"Gestao Ambiental"

oTempTRB3:Delete()

DbselectArea(cAliTRB)
Dbgotop()

Return .t.
