#INCLUDE "MNTA970.ch"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA970  ³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³21/02/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Apropriacao de debitos do sinistro             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA970()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 							  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
Local aOldMenu
Local aNGCAD02 := {}
Private asMenu 

bKeyF9 := SetKey(VK_F9)          
bKeyF10 := SetKey(VK_F10)
bKeyF11 := SetKey(VK_F11)
bKeyF12 := SetKey(VK_F12)
SetKey( VK_F9, { | | NGVersao( "MNTA970" , 1 ) } )
SETKEY(VK_F10,Nil)
SETKEY(VK_F11,Nil)
SETKEY(VK_F12,Nil)

aOldMenu := ACLONE(asMenu)
asMenu := NGRIGHTCLICK("MNTA970")

aNGCAD02:={;
		If(Type("aCHOICE") == "A",ACLONE(aCHOICE),{}),;
		If(Type("aVARNAO") == "A",ACLONE(aVARNAO),{}),;
		If(Type("aGETNAO") == "A",ACLONE(aGETNAO),{}),;
		If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
		If(Type("cGETMAKE") == "C",cGETMAKE,NIL),;
		If(Type("cGETKEY") == "C",cGETKEY,NIL),;
		If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
		If(Type("cTUDOOK") == "C",cTUDOOK,NIL),;
		If(Type("cLINOK") == "C",cLINOK,NIL),;
		If(Type("aRELAC") == "A",ACLONE(aRELAC),{}),;
		If(Type("aCHKDEL") == "A",ACLONE(aCHKDEL),{}),;
		If(Type("bngGRAVA") == "A",ACLONE(bngGRAVA),{}),;
		If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}




Private aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro := OemtoAnsi(STR0007) //"Apropriação de Débitos do Sinistro"
Private aCHKDEL := {}, bNGGrava := {}
Private dDataSin

bNGGrava := {|| MNTA970GRA()}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("TRS")
DbSetOrder(1)
mBrowse( 6, 1,22,75,"TRS")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 							  	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SETKEY(VK_F9,bKeyF9)
SETKEY(VK_F10,bKeyF10)
SETKEY(VK_F11,bKeyF11)
SETKEY(VK_F12,bKeyF12)

asMenu := ACLONE(aOldMenu)

aCHOICE := ACLONE(aNGCAD02[1])
aVARNAO := ACLONE(aNGCAD02[2])
AGETNAO := ACLONE(aNGCAD02[3])
If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
If(aNGCAD02[5] != NIL,cGETMAKE := aNGCAD02[5],)
If(aNGCAD02[6] != NIL,cGETKEY := aNGCAD02[6],)
If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
If(aNGCAD02[8] != NIL,cTUDOOK := aNGCAD02[8],)
If(aNGCAD02[9] != NIL,cLINOK := aNGCAD02[9],)
aRELAC := ACLONE(aNGCAD02[10])
aCHKDEL := ACLONE(aNGCAD02[11])
bngGRAVA := ACLONE(aNGCAD02[12])
aNGBUTTON := ACLONE(aNGCAD02[13])

Return .T.         

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA970DAT³ Autor ³ Marcos Wagner Junior  ³ Data ³23/02/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Data de Abertura e Data de Conclusao          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA970                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA970DAT()
     
If !Empty(M->TRS_DTABER) .AND. !Empty(M->TRS_DTCONC)
	If M->TRS_DTABER > M->TRS_DTCONC
	   Msgstop(STR0008,STR0009) //"Data de Abertura não poderá ser maior que Data de Conclusão!"###"Atenção"
		Return .f.
	Endif	
Endif 

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA970GRA³ Autor ³ Marcos Wagner Junior  ³ Data ³16/04/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Se Inclusao, dispara workflow de APD de Sinistro           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA970                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA970GRA()
          
If Inclui
	MNTW070()
Endif

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA970REL³ Autor ³ Marcos Wagner Junior  ³ Data ³04/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de APD de Sinistro							           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA970                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA970REL()

Local aOldArea := GetArea()
Local nI := 0
Private NOMEPROG := "MNTR970"
Private TAMANHO  := "G"
Private aRETURN  := {STR0010,1,STR0011,1,2,1,"",1} //"Zebrado"###"Administracao"
Private TITULO   := "Relatório de Advertências Disciplinares"
Private nTIPO    := 0
Private nLASTKEY := 0
Private CABEC1,CABEC2 
Private aVETINR := {}    
Private oFont11, oFontCourier
Private oPrint := TMSPrinter():New( OemToAnsi(TITULO) )  

If FunName() = 'MNTA545'
	If !MsgYesNo(STR0050,STR0009) //"Deseja gerar uma notificação do APD?"###"Atenção"
		Return .t.
	Endif
Endif

WNREL      := "MNTR970"
LIMITE     := 182
cDESC1     := ""
cDESC2     := ""
cDESC3     := ""
cSTRING    := ""       

aLinha := {}
aMedida := {}
nLinha := 0
nMedida := 0 

oPrint:SetPortrait() //Default Retrato  

oFont10  := TFont():New("ARIAL",10,10,,.F.,,,,.F.,.F.)
oFont14  := TFont():New("ARIAL",14,14,,.T.,,,,.F.,.F.)
oFont15  := TFont():New("ARIAL",15,15,,.T.,,,,.F.,.F.)

lin := 70
nColFim := 2250
oPrint:StartPage()

oPrint:Say(lin,025,STR0012+TRS->TRS_NUMAPD,oFont15) //"Número APD: "
If TRS->TRS_STAPD = '1'
	cDescricao := STR0013 //"Nova"
ElseIf TRS->TRS_STAPD = '2'
	cDescricao := STR0014 //"Em Processo"
ElseIf TRS->TRS_STAPD = '3'
	cDescricao := STR0015 //"Rejeitada"
ElseIf TRS->TRS_STAPD = '4'
	cDescricao := STR0016 //"Cancelada"
ElseIf TRS->TRS_STAPD = '5'
	cDescricao := STR0017 //"Concluida"
Endif
oPrint:Say(lin,1700,STR0018+cDescricao,oFont15) //"Status: "
lin += 80
/*******************************************************/
oPrint:Box(lin,025,lin+265,nColFim)
lin += 10
oPrint:Say(lin,825,STR0019,oFont14) //"Informações do Motorista"
lin += 60
oPrint:Line(lin,025,lin,nColFim)
lin += 10
dbSelectArea("TRV")
dbSetOrder(01)
dbSeek(xFilial("TRV")+TRS->TRS_NUMSIN)
oPrint:Say(lin,035,STR0020+AllTrim(TRV->TRV_CODMOT)+' - '+NGSEEK("DA4",TRV->TRV_CODMOT,1,"SubStr(DA4_NOME,1,40)"),oFont10) //"Motorista: "
lin += 45
oPrint:Say(lin,035,STR0021+AllTrim(SRA->RA_CC)+' - '+NGSEEK("CTT",SRA->RA_CC,1,"CTT_DESC01"),oFont10) //"Centro Custo: "
lin += 45
oPrint:Say(lin,035,STR0022+NGSEEK("SQ3",SRA->RA_CARGO,1,"SubStr(Q3_DESCSUM,1,40)"),oFont10) //"Cargo: "
lin += 45
oPrint:Say(lin,035,STR0023+xFilial("TRH")+' - '+AllTrim(SM0->M0_FILIAL),oFont10) //"Filial: "
/*******************************************************/
lin += 60
oPrint:Say(lin,035,STR0024+TRS->TRS_TIPAPD,oFont10) //"Tipo de APD: "
/*******************************************************/
lin += 60
oPrint:Box(lin,025,lin+165,nColFim)
lin += 10
oPrint:Say(lin,795,STR0025,oFont14) //"Informações Sobre Valores"
lin += 60
oPrint:Line(lin,025,lin,nColFim)
lin += 10
oPrint:Say(lin,035,STR0026+AllTrim(Str(TRS->TRS_VALAPD)),oFont10) //"Valor Estimado: R$ "
lin += 45
oPrint:Say(lin,035,STR0027+Extenso(TRS->TRS_VALAPD,.F.,1),oFont10) //"Por Extenso: "
/*******************************************************/
lin += 60
oPrint:Box(lin,025,lin+70,nColFim)
lin += 10
oPrint:Say(lin,900,STR0028,oFont14) //"Descrição dos Fatos"
lin += 60
oPrint:Line(lin,025,lin+50,025)
oPrint:Line(lin,nColFim,lin+50,nColFim)
oPrint:Say(lin+5,035,STR0029,oFont10) //"Descrição dos fatos: "
dbSelectArea("TRH")
dbSetOrder(01)
dbSeek(xFilial("TRH")+TRS->TRS_NUMSIN)

cDescricao := If(NGCADICBASE('TRH_MMDAN2','A','TRH',.F.),;
                 NGMEMOSYP(TRH->TRH_MMSYP),TRH->TRH_DESCRI)
nLinhas := MlCount(cDescricao,79)
If nLinhas > 0
	For nI := 1 to nLinhas
	   oPrint:Say(lin+5,375,Memoline(cDescricao,79,nI),oFont10)
		oPrint:Line(lin,025,lin+50,025)
		oPrint:Line(lin,nColFim,lin+50,nColFim)
		lin += 45
	Next ix
Endif
oPrint:Line(lin,025,lin,nColFim)

oPrint:Say(lin+5,035,STR0030,oFont10) //"Parecer Jurídico: "
oPrint:Line(lin,025,lin+50,025)
oPrint:Line(lin,nColFim,lin+50,nColFim)
dbSelectArea("TRJ")
dbSetOrder(01)
dbSeek(xFilial("TRJ")+TRS->TRS_NUMSIN)
nLinhas := MlCount(TRJ->TRJ_PARECE,79)
If nLinhas > 0
	For nI := 1 to nLinhas
	   oPrint:Say(lin+5,375,Memoline(TRJ->TRJ_PARECE,79,nI),oFont10)
		oPrint:Line(lin,025,lin+50,025)
		oPrint:Line(lin,nColFim,lin+50,nColFim)
		lin += 45
	Next ix
Else
	lin += 45
Endif
oPrint:Line(lin,025,lin,nColFim)
oPrint:Line(lin,025,lin,nColFim)
/*******************************************************/
lin += 15
oPrint:Box(lin,025,lin+70,nColFim)
lin += 10
oPrint:Say(lin,968,STR0031,oFont14) //"Documentos"
lin += 60

dbSelectArea("TRI")
dbSetOrder(01)
If dbSeek(xFilial("TRI")+TRS->TRS_NUMSIN)
	While !Eof() .AND. xFilial("TRI") == TRI->TRI_FILIAL .AND. TRI->TRI_NUMSIN = TRS->TRS_NUMSIN
		oPrint:Say(lin+5,035,AllTrim(TRI->TRI_CODDOC)+' - '+AllTrim(NGSEEK("TRU",TRI->TRI_CODDOC,1,'TRU_NOMDOC')),oFont10)
		oPrint:Line(lin,025,lin+50,025)
		oPrint:Line(lin,nColFim,lin+50,nColFim)
		lin += 50
		oPrint:Line(lin,025,lin,nColFim)
		dbSkip()	
	End
Else
	oPrint:Say(lin+5,035,STR0032,oFont10) //"Não foram encontrados documentos cadastrados para o sinistro referente ao APD."
	oPrint:Line(lin,025,lin+45,025)
	oPrint:Line(lin,nColFim,lin+45,nColFim)
	lin += 45
	oPrint:Line(lin,025,lin,nColFim)	
Endif
/*******************************************************/
lin += 15
oPrint:Box(lin,025,lin+725,nColFim)
lin += 10
oPrint:Say(lin,970,STR0033,oFont14) //"Parecer do Responsável"
lin += 60
oPrint:Line(lin,025,lin,nColFim)
lin += 10
oPrint:Say(lin,035,STR0034,oFont10) //"-> Você concorda com os fatos da forma em que foram narrados?"
MNTA970BOX(1105,1255)
lin += 45
oPrint:Say(lin,035,STR0035,oFont10) //"-> Por que: "
lin += 45
oPrint:Line(lin,200,lin,nColFim-50)
lin += 45
oPrint:Line(lin,200,lin,nColFim-50)
lin += 45
oPrint:Line(lin,200,lin,nColFim-50)
lin += 10

oPrint:Say(lin,035,STR0055,oFont10) //"-> Se concorda, autoriza o desconto em sua folha de pagamento dos valores acima citados?"
MNTA970BOX(1530,1680)
lin += 45

oPrint:Say(lin,035,STR0036,oFont10) //"-> Diante dos fatos e das provas acima, você se declara responsável pelos danos ocorridos?"
MNTA970BOX(1535,1685)
lin += 45
oPrint:Say(lin,035,STR0035,oFont10) //"-> Por que: "
lin += 45
oPrint:Line(lin,200,lin,nColFim-50)
lin += 45
oPrint:Line(lin,200,lin,nColFim-50)
lin += 45
oPrint:Line(lin,200,lin,nColFim-50) 
lin += 10  

oPrint:Say(lin,035,STR0037,oFont10) //"-> Há algum procedimento que você queira sugerir ou alguma prova a apresentar?"
MNTA970BOX(1375,1525)
lin += 45
oPrint:Say(lin,035,STR0038,oFont10) //"-> Descrição: "
lin += 45
oPrint:Line(lin,230,lin,nColFim-50)
lin += 45
oPrint:Line(lin,230,lin,nColFim-50)
lin += 45
oPrint:Line(lin,230,lin,nColFim-50) 
lin += 60
/*******************************************************/
oPrint:Box(lin,025,lin+165,nColFim)
lin += 10
oPrint:Say(lin,910,STR0039,oFont14) //"Parecer do Gerente"
lin += 60
oPrint:Line(lin,025,lin,nColFim)
lin += 10
oPrint:Say(lin,035,STR0040,oFont10) //"Debitar para: "
lin += 45
oPrint:Say(lin,035,STR0041,oFont10) //"Forma: "
oPrint:Say(lin,1000,STR0042,oFont10) //"Parcelas: "
lin += 130
/*******************************************************/
oPrint:Line(lin,035,lin,(nColFim/2)-100)
oPrint:Line(lin,(nColFim/2)+100,lin,nColFim)
lin += 10
oPrint:Say(lin,350,STR0043,oFont10) //"Assinatura do colaborador"
oPrint:Say(lin,1590,STR0044,oFont10) //"Assinatura do gerente"
lin += 60
/*******************************************************/
oPrint:Box(lin,025,lin+220,nColFim)
lin += 10
oPrint:Say(lin,890,STR0045,oFont14) //"Recusa de Assinatura"
lin += 60
oPrint:Line(lin,025,lin,nColFim)
lin += 10
oPrint:Say(lin,035,STR0046,oFont10) //"Testemunha 1: "
oPrint:Say(lin,1070,STR0047,oFont10) //"Testemunha 2: "
lin += 40
oPrint:Line(lin,265,lin,(nColFim/2)-100)
oPrint:Line(lin,(nColFim/2)+185,lin,nColFim-50)
lin += 10
oPrint:Say(lin,035,STR0048,oFont10) //"RG: "
oPrint:Say(lin,1070,STR0048,oFont10) //"RG: "
lin += 45
oPrint:Say(lin,035,STR0049,oFont10) //"CPF: "
oPrint:Say(lin,1070,STR0049,oFont10) //"CPF: "

oPrint:EndPage()    
oPrint:Preview()
      
RestArea(aOldArea)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA970DT ³ Autor ³ Marcos Wagner Junior  ³ Data ³14/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Nao permite	que as Datas sejam inferiores a Data Sinistro  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA970                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA970DT()

If Altera
	MNTA970RET()
Endif

If dDataSin != Nil
	If ReadVar() == 'M->TRS_DTABER'
		If dDataSin > M->TRS_DTABER .AND. !Empty(M->TRS_DTABER)
		   Msgstop(OemToAnsi(RetTitSX3("TRS_DTABER"))+STR0051+DTOC(dDataSin)+"!'",STR0009)//" não poderá ser menor que "###"Atenção"
			Return .f.
		Endif
	Else
		If dDataSin > M->TRS_DTCONC .AND. !Empty(M->TRS_DTCONC)
		   Msgstop(OemToAnsi(RetTitSX3("TRS_DTCONC"))+STR0051+DTOC(dDataSin)+"!'",STR0009)//" não poderá ser menor que "###"Atenção"
			Return .f.
		Endif
	Endif
Endif

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNTA970RET³ Autor ³ Marcos Wagner Junior  ³ Data ³14/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Busca a Data do Sinistro  											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA970                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA970RET()

Local aOldArea := GetArea()
        
dbSelectArea("TRH")
dbSetOrder(01)
If dbSeek(xFilial("TRH")+M->TRS_NUMSIN)
	dDataSin := TRH->TRH_DTACID
Else
	dDataSin := Nil
Endif

RestArea(aOldArea)

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³02/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina :=	{ {STR0001   , "AxPesqui"  , 0 , 1},; //"Pesquisar"
                     {STR0002  , "NGCAD01"   , 0 , 2},; //"Visualizar"
                     {STR0003  , "NGCAD01"   , 0 , 3},; //"Incluir"
                     {STR0004  , "NGCAD01"   , 0 , 4},; //"Alterar"
                     {STR0005  , "MNTA970DEL()"   , 0 , 5, 3},; //"Excluir"
                     {STR0006  , "MNTA970REL()"   , 0 , 6}} //"Relatório"

Return aRotina

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNTA970DEL³ Autor ³ Marcos Wagner Junior  ³ Data ³26/05/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Delecao do APD de Sinistro											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA970                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA970DEL()
Local aOldArea := GetArea()

dbSelectArea("TRV")
dbSetOrder(01)
If dbSeek(xFilial("TRV")+TRS->TRS_NUMSIN)
	Msgstop(STR0052,STR0009) //"APD está relacionado a um Sinistro!"###"Atenção"
Else
	NGCAD01("TRS",Recno(),5)
Endif

RestArea(aOldArea)

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNTA970BOX³ Autor ³ Marcos Wagner Junior  ³ Data ³10/07/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao que monta o BOX (Sim/Não)									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA970                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA970BOX(nColunaS,nColunaN)

oPrint:Box(lin+10,nColunaS,lin+30,nColunaS+20)
oPrint:Say(lin,nColunaS+40,STR0053,oFont10) //"Sim"
oPrint:Box(lin+10,nColunaN,lin+30,nColunaN+20)
oPrint:Say(lin,nColunaN+40,STR0054,oFont10) //"Não"

Return .t.