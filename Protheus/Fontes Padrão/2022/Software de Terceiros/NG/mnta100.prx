#Include "MNTA100.CH"
#Include "Protheus.CH"
#Include "FWMVCDEF.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA100
Cadastro de Áreas de Manutenção

@author André Felipe Joriatti
@since 07/04/2014
@version P11
@return Nil
/*/
//---------------------------------------------------------------------

Function MNTA100()

	Local oBrowse
	Local aNGBEGINPRM := NGBEGINPRM(,"MNTA100",,,,,.F.)

	// Ajusta clique da direita da rotina
	fAjusteTQB()

	//Caso possua o ponto de entrada 'MNTA0832' deverá trocar para o modelo MVC.
	If FindFunction('U_MNTA1001') .And. !FindFunction('U_MNTA100')
		MsgInfo( STR0013, STR0012 ) // 'O ponto de entrada "MNTA1001" deve ser alterado conforme o novo modelo. Favor pesquisar a ISSUE MNG-6281 no TDN.'##'PONTO DE ENTRADA MNTA1001'
	EndIf

	oBrowse := FWMBrowse():New()
		oBrowse:SetAlias( "STD" )
		oBrowse:SetMenuDef( "MNTA100" )
		oBrowse:SetDescription( STR0009 ) // Áreas de Manutenção
		oBrowse:Activate()

	NGRETURNPRM( aNGBEGINPRM )

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Opções de menu

@author André Felipe Joriatti
@since 07/04/2014
@version P11
@return aRotina - Estrutura
	[n,1] Nome a aparecer no cabecalho
	[n,2] Nome da Rotina associada
	[n,3] Reservado
	[n,4] Tipo de Transação a ser efetuada:
		1 - Pesquisa e Posiciona em um Banco de Dados
		2 - Simplesmente Mostra os Campos
		3 - Inclui registros no Bancos de Dados
		4 - Altera o registro corrente
		5 - Remove o registro corrente do Banco de Dados
		6 - Alteração sem inclusão de registros
		7 - Cópia
		8 - Imprimir
	[n,5] Nivel de acesso
	[n,6] Habilita Menu Funcional
/*/
//---------------------------------------------------------------------
Static Function MenuDef()
// Inicializa MenuDef com todas as opções
Return FWMVCMenu( "MNTA100" )

//---------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author André Felipe Joriatti
@since 07/04/2014
@version P11
@return Nil
/*/
//---------------------------------------------------------------------

Static Function ModelDef()

	Local oModel

	Local oStructSTD

	oStructSTD := FWFormStruct( 1,"STD" )

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New( "MNTA100",/*bPre*/,,{ |oModel| CommitInfo( oModel ) },/*bCancel*/ )

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields( "MNTA100_STD", Nil, oStructSTD,/*bPre*/,/*bPost*/,/*bLoad*/ )

	// Descrição do Model
	oModel:SetDescription( STR0009 ) // Áreas de Manutenção

Return oModel

//---------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author André Felipe Joriatti
@since 07/04/2014
@version P11
@return Nil
/*/
//---------------------------------------------------------------------

Static Function ViewDef()

	Local oModel := FWLoadModel( "MNTA100" )
	Local oView  := Nil

	oStruSTD := FWFormStruct( 2,"STD" )

	oView := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel( oModel )

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA100_STD" , oStruSTD , /*cLinkID*/ )

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100,/*cIDOwner*/,/*lFixPixel*/,/*cIDFolder*/,/*cIDSheet*/ )

	// Associa um View a um box
	oView:SetOwnerView( "MNTA100_STD","MASTER" )

	// Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn( oView )

Return oView

//---------------------------------------------------------------------
/*/{Protheus.doc} CommitInfo
Grava todas as informações

@author André Felipe Joriatti
@since 07/04/2014
@version P11
@return .T.
/*/
//---------------------------------------------------------------------

Static Function CommitInfo( oModel )

	Local nOpc := oModel:GetOperation()

	//----------------------------
	// Grava informações do Model
	//----------------------------
	FwFormCommit( oModel )

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} fAjusteTQB
Ajusta tabela de clique da direita

@author André Felipe Joriatti
@since 08/04/2014
@version P11
@return Nil
/*/
//---------------------------------------------------------------------

Static Function fAjusteTQB()

	Local nLen_TQD_PROGRA := TAMSX3( "TQD_PROGRA" )[1]
	Local nLen_TQD_FUNCAO := TAMSX3( "TQD_FUNCAO" )[1]
	Local cDescricao      := STR0010

	DbSelectArea( "TQD" )
	DbSetOrder( 01 ) // TQD_FILIAL+TQD_PROGRA+TQD_FUNCAO
	If DbSeek( xFilial( "TQD" ) + PadR( "MNTA100",nLen_TQD_PROGRA ) +;
		PadR( "MNTC100",nLen_TQD_FUNCAO ) ) .And. AllTrim( TQD->TQD_PARAM  ) != "oView:GetValue( 'MNTA100_STD','TD_CODAREA' )"

		RecLock( "TQD",.F. )
		TQD->TQD_PARAM := "oView:GetValue( 'MNTA100_STD','TD_CODAREA' )"
		MsUnLock( "TQD" )

		If AllTrim( TQD->TQD_DESPOR ) != cDescricao .And. AllTrim( TQD->TQD_DESING ) != cDescricao .And. ;
			AllTrim( TQD->TQD_DESESP ) != cDescricao

			RecLock( "TQD",.F. )
			TQD->TQD_DESPOR := cDescricao // "Serviços"
			TQD->TQD_DESING := cDescricao // "Serviços"
			TQD->TQD_DESESP := cDescricao // "Serviços"
			MsUnLock( "TQD" )
		EndIf

	EndIf

	DbSelectArea( "TQD" )
	DbSetOrder( 01 ) // TQD_FILIAL+TQD_PROGRA+TQD_FUNCAO
	If DbSeek( xFilial( "TQD" ) + PadR( "MNTA100",nLen_TQD_PROGRA ) +;
		PadR( "MNTC105",nLen_TQD_FUNCAO ) ) .And. AllTrim( TQD->TQD_PARAM  ) != "oView:GetValue( 'MNTA100_STD','TD_CODAREA' )"

		RecLock( "TQD",.F. )
		TQD->TQD_PARAM := "oView:GetValue( 'MNTA100_STD','TD_CODAREA' )"
		MsUnLock( "TQD" )

	EndIf

Return Nil
