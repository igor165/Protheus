#INCLUDE "MNTR310.CH"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 1 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR310  ³ Autor ³ Deivys Joenck         ³ Data ³ 04/09/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Custo Resumido do Bem                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³OBSERVACAO³ Revisao t‚cnica 09/10/2002                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR310()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Guarda conteudo e declara variaveis padroes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Local oReport
Local aArea := GetArea()
Private vVETHORAS := {}
Private nSizeFil  := If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(TQN->TQN_FILIAL))
Private cPerg    := "MNT310    "
Private cTRBTMP   := GetNextAlias()
SetKey( VK_F9, { | | NGVersao( "MNTR310" , 1 ) } )

If FindFunction("TRepInUse") .And. TRepInUse()
	Private aTotalG:= {}
   //-- Interface de impressao
   oReport := ReportDef()
   oReport:SetLandscape()
   //oReport:SetPortrait() //Default Retrato
   oReport:PrintDialog()
Else
   MNTR310R3()
EndIf
RestArea(aArea)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna conteudo de variaveis padroes       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ Elisangela Costa      ³ Data ³ 23/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR310                                                    ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef()
Local oReport
Local oSection0
Local oSection1
Local oBreak

//LAYOUT
/*
          1         2         3         4         5         6         7         8         9        10       110       120
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123012345678901230123012301230123012301230123
______________________________________________________________________________________________________________________________________________________________________
                                                                                 Custo Resumido do Bem
______________________________________________________________________________________________________________________________________________________________________
Frota             Descricao                     Data        Num.Os  Hodometro  M.D.O Propria  M.D.O Terceiro  Mat. Proprio  Mat. Terceiro    Oleo Lub.     Valor Total
______________________________________________________________________________________________________________________________________________________________________
XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXX 99/99/9999  XXXXXX  9,999,999     999,999.99      999,999.99    999,999.99     999,999.99   999,999.99  999,999,999.99
                                                99/99/9999  XXXXXX  9,999,999     999,999.99      999,999.99    999,999.99     999,999.99   999,999.99  999,999,999.99
                                                99/99/9999  XXXXXX  9,999,999     999,999.99      999,999.99    999,999.99     999,999.99   999,999.99  999,999,999.99

XXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXX 99/99/9999  XXXXXX  9,999,999     999,999.99      999,999.99    999,999.99     999,999.99   999,999.99  999,999,999.99
                                                99/99/9999  XXXXXX  9,999,999     999,999.99      999,999.99    999,999.99     999,999.99   999,999.99  999,999,999.99


*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MNTR310",OemToAnsi(STR0001),"MNT310",{|oReport| ReportPrint(oReport)},STR0001+". "+STR0002+" "+STR0003)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis utilizadas para parametros!                        ³
³ mv_par01     // De  Data                                     ³
³ mv_par02     // Ate Data                                     ³
³ mv_par03     // De  Bem                                      ³
³ mv_par04     // Ate Bem                                      ³
³ mv_par05     // De  Centro de Custo                          ³
³ mv_par06     // Ate Centro de Custo                          ³
³ mv_par07     // De  Familia                                  ³
³ mv_par08     // Ate Familia                                  ³
³ mv_par09     // De  Grupo Lubrif.                            ³
³ mv_par10     // Ate Grupo Lubrif.                            ³
³ mv_par11     // Tipo de Custo (1-Medio, 2-Standard)          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection0 := TRSection():New(oReport,"Filiais",{"cTRBTMP"}) //Filiais
TRCell():New(oSection0,"(cTRBTMP)->FILIAL"  ,"cTRBTMP" ,"Filial" ,"@!",15,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Frota"
TRCell():New(oSection0,"NOMEFILIAL"         ,          ,"Nome"   ,"@!",40,.T./*lPixel*/, {||FwFilialName(,(cTRBTMP)->FILIAL)} )  //"Descrição"

oSection1 := TRSection():New(oReport,STR0025,{"cTRBTMP","ST9","STL","STJ"}) //Frotas
TRCell():New(oSection1,"(cTRBTMP)->CODBEM"  ,"cTRBTMP" ,STR0014 ,"@!"           ,30,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Frota"
TRCell():New(oSection1,"T9_NOME"            ,"ST9" ,STR0015 ,"@!"           ,40,.T./*lPixel*/,{|| SubStr(ST9->T9_NOME,1,20)})  //"Descrição"
TRCell():New(oSection1,"(cTRBTMP)->DATAOS"  ,"cTRBTMP" ,STR0016 ,"99/99/9999"   ,20,.T./*lPixel*/,/*{|| code-block de impressao }*/)  //"Data"
TRCell():New(oSection1,"(cTRBTMP)->NUMOS"   ,"cTRBTMP" ,STR0017 ,"@!"           ,15,.T./*lPixel*/,/*{|| code-block de impressao }*/)  //"Num.Os"
TRCell():New(oSection1,"(cTRBTMP)->POSCONT" ,"cTRBTMP" ,STR0018 ,"@E 9,999,999" ,20,.T./*lPixel*/,/*{|| code-block de impressao }*/)  //"Hodometro"
TRCell():New(oSection1,"(cTRBTMP)->MDOPRO"  ,"cTRBTMP" ,STR0019 ,"@E 999,999.99",30,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"M.D.O Propria"
TRCell():New(oSection1,"(cTRBTMP)->MDOTER"  ,"cTRBTMP" ,STR0020 ,"@E 999,999.99",30,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"M.D.O Terceiro"
TRCell():New(oSection1,"(cTRBTMP)->MATPRO"  ,"cTRBTMP" ,STR0021 ,"@E 999,999.99",30,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Mat. Proprio"
TRCell():New(oSection1,"(cTRBTMP)->MATTER"  ,"cTRBTMP" ,STR0022 ,"@E 999,999.99",30,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Mat. Terceiro"
TRCell():New(oSection1,"(cTRBTMP)->OLEO"    ,"cTRBTMP" ,STR0023 ,"@E 999,999.99",30,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Oleo Lub."
TRCell():New(oSection1,"VALR"         ,      ,STR0024 ,"@E 999,999,999.99",40,.T./*lPixel*/,{||NGRETVAL()}) //"Valor Total"
TRCell():New(oSection1,"(cTRBTMP)->POSCONT2","cTRBTMP" ,STR0026 ,"@E 9,999,999" ,20,.T./*lPixel*/,/*{|| code-block de impressao }*/)  //"2o.Cont."

oBreak := TRBreak():New(oSection1,".T.","Total do Bem",.F.) //"Total do Centro de Custo"
TRFunction():New(oSection1:Cell("(cTRBTMP)->MDOPRO"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("(cTRBTMP)->MDOTER"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("(cTRBTMP)->MATPRO"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("(cTRBTMP)->MATTER"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("(cTRBTMP)->OLEO"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("VALR"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 999,999,999.99",/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)

oBreak := TRBreak():New(oSection0,".T.","Total da Filial",.F.) //"Total do Centro de Custo"
TRFunction():New(oSection1:Cell("(cTRBTMP)->MDOPRO"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("(cTRBTMP)->MDOTER"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("(cTRBTMP)->MATPRO"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("(cTRBTMP)->MATTER"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("(cTRBTMP)->OLEO"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 9,999,999.99",/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)
TRFunction():New(oSection1:Cell("VALR"),/*cId*/,"SUM",oBreak,/*cTitle*/,"@E 999,999,999.99",/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/,/*oSection1*/)

//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a direita
oSection1:Cell("(cTRBTMP)->POSCONT"):SetHeaderAlign("RIGHT")
oSection1:Cell("(cTRBTMP)->MDOPRO"):SetHeaderAlign("RIGHT")
oSection1:Cell("(cTRBTMP)->MDOTER"):SetHeaderAlign("RIGHT")
oSection1:Cell("(cTRBTMP)->MATPRO"):SetHeaderAlign("RIGHT")
oSection1:Cell("(cTRBTMP)->MATTER"):SetHeaderAlign("RIGHT")
oSection1:Cell("(cTRBTMP)->OLEO"):SetHeaderAlign("RIGHT")
oSection1:Cell("VALR"):SetHeaderAlign("RIGHT")
oSection1:Cell("(cTRBTMP)->POSCONT2"):SetHeaderAlign("RIGHT")

TRPosition():New(oSection1,"ST9",1,{|| xFilial("ST9") + (cTRBTMP)->CODBEM})

oSection1:nLeftMargin := 3

Return oReport

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR310R3³ Autor ³ Deivys Joenck         ³ Data ³ 04/09/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Custo Resumido do Bem                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR310R3()

Local WNREL      := "MNTR310"
Local LIMITE     := 132
Local cDESC1     := STR0001 //"Custo Resumido do Bem."
Local cDESC2     := STR0002 //"O usuario pode selecionar quais os campos que deverao ser mostrados,"
Local cDESC3     := STR0003 //"bem como informar parametros de selecao para a impressao."
Local cSTRING    := "STJ"

Private NOMEPROG := "MNTR310"
Private TAMANHO  := "G"
Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1}    //"Zebrado"###"Administracao"
Private TITULO   := STR0001 //"Custo Resumido do Bem."
Private nLASTKEY := 0
Private cPERG    := "MNT310"
Private CABEC1 , CABEC2
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis utilizadas para parametros!                        ³
³ mv_par01     // De  Data                                     ³
³ mv_par02     // Ate Data                                     ³
³ mv_par03     // De  Bem                                      ³
³ mv_par04     // Ate Bem                                      ³
³ mv_par05     // De  Centro de Custo                          ³
³ mv_par06     // Ate Centro de Custo                          ³
³ mv_par07     // De  Familia                                  ³
³ mv_par08     // Ate Familia                                  ³
³ mv_par09     // De  Grupo Lubrif.                            ³
³ mv_par10     // Ate Grupo Lubrif.                            ³
³ mv_par11     // Tipo de Custo (1-Medio, 2-Standard)          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Pergunte(cPERG,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
If nLASTKEY = 27
   Set Filter To
   DbSelectArea("STJ")
   Return
EndIf
SetDefault(aRETURN,cSTRING)

RptStatus({|lEND| MNTR310IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
DbSelectArea("STJ")
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR310IMP³ Autor ³ Deivys Joenck         ³ Data ³ 04/09/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR310IMP(lEND,WNREL,TITULO,TAMANHO)

Local cRODATXT := ""
Local nCNTIMPR := 0
Local nMULT    := 1
Local nValTotB:= 0, nMDOPROB:= 0,nMDOTERB:=0, nMATPROB :=0,nMATTERB:=0,nOLEOB:=0
Local nValTotF:= 0, nMDOPROF:= 0,nMDOTERF:=0, nMATPROF :=0,nMATTERF:=0,nOLEOF:=0
Local nValTotG:= 0, nMDOPROG:= 0,nMDOTERG:=0, nMATPROG :=0,nMATTERG:=0,nOLEOG:=0
Local aFiliais := {}
Local cImpFilAtu := ""
Local cImpFilOld := ""
Local lFirstFil := .T.
Local aTotal := {}
Local lTemDados := .F.
Local oTmpTbl1  := Nil

Private li := 80,m_pag := 1
Private aVETINR := {}

nTIPO  := IIF(aRETURN[4]==1,15,18)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta os Cabecalhos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
CABEC1 := STR0008+"   "+STR0026 //"Bem               Data       Num.OS   Contador   M.D.O Propria   M.D.O Terceiro   Mat. Proprio   Mat. Terceiro   Oleo Lub.     Valor Total"+"2o.Cont."
CABEC2 := ""//STR0009 "                                                    PROPRIA    TERCEIRO     PROPRIO    TERCEIRO"

/*/
          1         2         3         4         5         6         7         8         9         0         1         2         3
012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
************************************************************************************************************************************
Bem               Data       Num.OS   Contador   M.D.O Propria   M.D.O Terceiro   Mat. Proprio   Mat. Terceiro   Oleo Lub.     Valor Total   2o.Cont.
************************************************************************************************************************************
xxxxxxxxxxxxxxxx  99/99/99  xxxxxx   9,999,999   999,999.99  999,999.99  999,999.99  999,999.99  999,999.99   999,999,999.99
/*/

aDBFR310 := {{"FILIAL" ,"C",nSizeFil,0},;
             {"NUMOS"  ,"C",06,0},;
             {"DATAOS" ,"D",08,0},;
             {"POSCONT","N",09,0},;
             {"POSCONT2","N",09,0},;
             {"CODBEM" ,"C",16,0},;
             {"MDOPRO" ,"N",12,2},;
             {"MDOTER" ,"N",12,2},;
             {"MATPRO" ,"N",12,2},;
             {"MATTER" ,"N",12,2},;
             {"OLEO"   ,"N",12,2}}

If mv_par14 = 1
   vINDR310  := {{"FILIAL","CODBEM","DATAOS","NUMOS"}}
Else
   vINDR310  := {{"FILIAL","CODBEM","DATAOS"}}
EndIf
aFiliais := MDTRETFIL("STJ", MV_PAR01, MV_PAR02)

oTmpTbl1  := NGFwTmpTbl(cTRBTMP,aDBFR310,vINDR310)

Processa({|lEND| MNTR310STJ()},STR0010+STR0011) //"Processando Arquivo..."..Normal
Processa({|lEND| MNTR310STS()},STR0010+STR0012) //"Processando Arquivo..." ..Historico

DbSelectArea(cTRBTMP)
DbGotop()
SetRegua(LastRec())
While !Eof()
   If !lTemDados
   	lTemDados := .T.
   EndIf
   cImpFilAtu := (cTRBTMP)->FILIAL
   cBem := (cTRBTMP)->CODBEM
   If cImpFilAtu <> cImpFilOld
		If !lFirstFil
			aTotal := {nMDOPROF,nMDOTERF,nMATPROF,nMATTERF,nOLEOF,nValTotF}
         ImpTotal(aTotal,2) // array com resultados, 1=Bem 2=Filial 3=Geral
	  EndIF
   	lFirstFil:= .F.
	   nValTotF :=0
	   nMDOPROF :=0
	   nMDOTERF :=0
	   nMATPROF :=0
	   nMATTERF :=0
	   nOLEOF   :=0
	   NGSOMALI(58)
     @ Li,000 Psay Replicate("*",50)
     NGSOMALI(58)
     @ Li,000 Psay "Filial: "+(cTRBTMP)->FILIAL+" - "+FwFilialName(,(cTRBTMP)->FILIAL)
     NGSOMALI(58)
     @ Li,000 Psay Replicate("*",50)
     NGSOMALI(58)
 	EndIf
   NGSOMALI(58)
   @ Li,000 Psay (cTRBTMP)->CODBEM
   nMDOPROB  := 0
   nMDOTERB  := 0
   nMATPROB  := 0
   nMATTERB  := 0
   nOLEOB    := 0
   nValTotB  := 0
   While !Eof() .And. (cTRBTMP)->CODBEM == cBem .And. (cTRBTMP)->FILIAL == cImpFilAtu
      IncRegua()
      nVALR := (cTRBTMP)->MDOPRO+(cTRBTMP)->MDOTER+(cTRBTMP)->MATPRO+(cTRBTMP)->MATTER+(cTRBTMP)->OLEO
      @ Li,018 Psay (cTRBTMP)->DATAOS  Picture "99/99/99"
      @ Li,029 Psay (cTRBTMP)->NUMOS   Picture "@!"
      @ Li,037 - Len(Alltrim((cTRBTMP)->POSCONT)) Psay (cTRBTMP)->POSCONT  Picture "@E 9,999,999"
      @ Li,052 - Len(Alltrim((cTRBTMP)->MDOPRO))  Psay (cTRBTMP)->MDOPRO   Picture "@E 999,999.99"
      @ Li,069 - Len(Alltrim((cTRBTMP)->MDOTER))  Psay (cTRBTMP)->MDOTER   Picture "@E 999,999.99"
      @ Li,084 - Len(Alltrim((cTRBTMP)->MATPRO))  Psay (cTRBTMP)->MATPRO   Picture "@E 999,999.99"
      @ Li,100 - Len(Alltrim((cTRBTMP)->MATTER))  Psay (cTRBTMP)->MATTER   Picture "@E 999,999.99"
      @ Li,112 - Len(Alltrim((cTRBTMP)->OLEO))    Psay (cTRBTMP)->OLEO     Picture "@E 999,999.99"
      @ Li,122 - Len(Alltrim(nVALR))        Psay nVALR         Picture "@E 999,999,999.99"
      @ Li,138 - Len(Alltrim((cTRBTMP)->POSCONT2))Psay (cTRBTMP)->POSCONT2 Picture "@E 9,999,999"
      nMDOPROB+= (cTRBTMP)->MDOPRO
      nMDOTERB+= (cTRBTMP)->MDOTER
      nMATPROB+= (cTRBTMP)->MATPRO
      nMATTERB+= (cTRBTMP)->MATTER
      nOLEOB+=	 (cTRBTMP)->OLEO
      nValTotB+= nVALR
      NGSOMALI(58)
      Dbselectarea(cTRBTMP)
      Dbskip()
   End
		aTotal := {nMDOPROB,nMDOTERB,nMATPROB,nMATTERB,nOLEOB,nValTotB}
  		ImpTotal(aTotal,1) // array com resultados, 1=Bem 2=Filial 3=Geral
  		//Total Filial
		nValTotF += nValTotB
	   nMDOPROF += nMDOPROB
	   nMDOTERF += nMDOTERB
	   nMATPROF += nMATPROB
	   nMATTERF += nMATTERB
	   nOLEOF   += nOLEOB
	   //Total Geral
	   nValTotG += nValTotB
	   nMDOPROG += nMDOPROB
	   nMDOTERG += nMDOTERB
	   nMATPROG += nMATPROB
	   nMATTERG += nMATTERB
	   nOLEOG   += nOLEOB



		cImpFilOld := cImpFilAtu
End
If lTemDados
	aTotal := {nMDOPROF,nMDOTERF,nMATPROF,nMATTERF,nOLEOF,nValTotF}
	ImpTotal(aTotal,2) // array com resultados, 1=Bem 2=Filial 3=Geral
	NGSOMALI(58)
	//
	aTotal := {nMDOPROG,nMDOTERG,nMATPROG,nMATTERG,nOLEOG,nValTotG}
	ImpTotal(aTotal,3)
Else
	NGSOMALI(58)
	@ Li,000 Psay "Não existem dados a serem impressos."
EndIf
//
RODA(nCNTIMPR,cRODATXT,TAMANHO)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principam             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("STJ")
Set Filter To
Set Device To Screen
If aRETURN[5] == 1
   Set Printer To
   dbCommitAll()
   OurSpool(WNREL)
EndIf
MS_FLUSH()

//Deleta o arquivo temporario fisicamente
oTmpTbl1:Delete()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³ Elisangela Costa      ³ Data ³ 23/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ReportDef                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)

	Local oSection1  := oReport:Section(1)
	Local oSection2  := oReport:Section(2)
	Local cImpFilAtu := ""
	Local cImpFilOld := ""
	Local lFirstFil  := .T.
	Local oTmpTbl1   := Nil

	aDBFR310 := {{"FILIAL" ,"C",nSizeFil,0},;
				{"NUMOS"  ,"C",06,0},;
				{"DATAOS" ,"D",08,0},;
				{"POSCONT","N",09,0},;
				{"POSCONT2","N",09,0},;
				{"CODBEM" ,"C",16,0},;
				{"MDOPRO" ,"N",12,2},;
				{"MDOTER" ,"N",12,2},;
				{"MATPRO" ,"N",12,2},;
				{"MATTER" ,"N",12,2},;
				{"OLEO"   ,"N",12,2}}

	If mv_par14 = 1
		vINDR310  := {{"FILIAL","CODBEM","DATAOS","NUMOS"}}
	Else
		vINDR310  := {{"FILIAL","CODBEM","DATAOS"}}
	EndIf

	oTmpTbl1 := NGFwTmpTbl(cTRBTMP,aDBFR310,vINDR310)

	Processa({|lEND| MNTR310STJ()},STR0010+STR0011) //"Processando Arquivo..."..Normal
	Processa({|lEND| MNTR310STS()},STR0010+STR0012) //"Processando Arquivo..." ..Historico

	aTotalG := {0,0,0,0,0,0}
	DbSelectArea(cTRBTMP)
	DbGotop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()

		cImpFilAtu:= (cTRBTMP)->FILIAL
		If !lFirstFil .And. cImpFilAtu <> cImpFilOld
			oSection1:Finish()
		EndIf

		If lFirstFil .Or. cImpFilAtu <> cImpFilOld
			oSection1:Init()
			oSection1:PrintLine()
			lFirstFil := .F.
		EndIf

		oSection2:Init()
		cBEM  := (cTRBTMP)->CODBEM
		cBEM2 := "  "
		DbSelectArea(cTRBTMP)
		While !Eof() .And. !oReport:Cancel() .And. (cTRBTMP)->CODBEM == cBEM  .And. (cTRBTMP)->FILIAL == cImpFilAtu

			oReport:IncMeter()
			nVALR := (cTRBTMP)->MDOPRO+(cTRBTMP)->MDOTER+(cTRBTMP)->MATPRO+(cTRBTMP)->MATTER+(cTRBTMP)->OLEO
			aTotalG[1]+=(cTRBTMP)->MDOPRO
			aTotalG[2]+=(cTRBTMP)->MDOTER
			aTotalG[3]+=(cTRBTMP)->MATPRO
			aTotalG[4]+=(cTRBTMP)->MATTER
			aTotalG[5]+=(cTRBTMP)->OLEO
			aTotalG[6]+=nVALR

			If cBEM == cBEM2
				oSection2:Cell("(cTRBTMP)->CODBEM"):Hide()
				oSection2:Cell("T9_NOME"):Hide()
			Else
				oSection2:Cell("(cTRBTMP)->CODBEM"):Show()
				oSection2:Cell("T9_NOME"):Show()
			EndIf
			cBEM2 := (cTRBTMP)->CODBEM
			oSection2:PrintLine()
			DbSelectArea(cTRBTMP)
			Dbskip()
		End

		oReport:SkipLine()
		oSection2:Finish()
		cImpFilOld := cImpFilAtu

	End

	oSection1:Finish()

	//Deleta o arquivo temporario fisicamente
	oTmpTbl1:Delete()

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR310STJ³ Autor ³ In cio Luiz kolling   ³ Data ³09/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa o.s. normais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR310STJ()
Local aFiliais := {}, i
cCONDSTJ := 'stj->tj_termino = "S" .And. stj->tj_situaca = "L" .And. '
cCONDSTJ := cCONDSTJ + '(stj->tj_dtmrfim >= MV_PAR03 .And. stj->tj_dtmrfim <= MV_PAR04)'
cCONDSTJ := cCONDSTJ + ' .And. (stj->tj_ccusto >= mv_par07 .And. stj->tj_ccusto <= mv_par08)'
cCONDST9 := 'st9->t9_codfami >= mv_par09 .And. st9->t9_codfami <= mv_par10'

aFiliais := MNTRETFIL("STJ", MV_PAR01, MV_PAR02)
For i:=1 to Len(aFiliais)
	Dbselectarea("STJ")
	Dbsetorder(2)
	Dbseek(xFilial("STJ",aFiliais[i,1])+"B"+MV_PAR05,.T.)
	Procregua(LastRec())
	While !Eof() .And. STJ->TJ_FILIAL == xFilial("STJ",aFiliais[i,1]) .And.;
	   STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM <= mv_par06
	   Incproc()
	   If &(cCONDSTJ)
	      Dbselectarea("ST9")
	      Dbsetorder(1)
	      If Dbseek(xFilial("ST9",aFiliais[i,1])+STJ->TJ_CODBEM)
	         If &(cCONDST9)
	            nMDOPRO := 0.00     //MAO DE OBRA PROPRIA (TIPOREG == "M")
	            nMDOTER := 0.00     //MAO DE OBRA DE TERCEIRO (DESC.PRODUTO == "SERVICO")
	            nMATPRO := 0.00     //MATERIAL PROPRIO (MOVIMENTA ESTOQUE)
	            nMATTER := 0.00     //MATERIAL TERCEITO (NAO MOVIMENTA ESTOQUE)
	            nOLEO   := 0.00     //PNEU (DESC.PROD == PNEU OU PROTETOR OU CAMARA)

	            Dbselectarea("STL")
	            Dbsetorder(1)
	            Dbseek(xFilial("STL",aFiliais[i,1])+STJ->TJ_ORDEM+STJ->TJ_PLANO)
	            While !Eof() .And. STL->TL_FILIAL == xFilial("STL",aFiliais[i,1]) .And.;
	               STL->TL_ORDEM == STJ->TJ_ORDEM .And. STL->TL_PLANO == STJ->TJ_PLANO

	               If Alltrim(STL->TL_SEQRELA) <> "0"

	                  If MV_PAR13 = 2 //STANDARD
	                     aVETCUST := NGCUSTSTAN(STL->TL_CODIGO,STL->TL_TIPOREG)
		                 nCUSTO   := aVETCUST[1] //Custo standard

	                    vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
	 						                          STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
	   													  STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
	                    nQTDHORAS := vVETHORAS[1]

		                 nCUSTO   := nCUSTO * nQTDHORAS
		              Else
		                 nCUSTO   := STL->TL_CUSTO
		              EndIf

	                  If STL->TL_TIPOREG == "M"
	                     nMDOPRO := nMDOPRO+nCUSTO
	                  Elseif STL->TL_TIPOREG == "T"
	                     nMDOTER := nMDOTER+nCUSTO
	                  Elseif STL->TL_TIPOREG == "P"
	                     Dbselectarea("SB1")
	                     Dbsetorder(1)
	                     Dbseek(xFilial("SB1",aFiliais[i,1])+Substr(STL->TL_CODIGO,1,15))
	                     If SB1->B1_GRUPO >= mv_par11 .And. SB1->B1_GRUPO <= MV_PAR12
	                        nOLEO := nOLEO+nCUSTO
	                     Else
	                        lPRO   := .T.
	                        lFOUND := .F.
	                        Dbselectarea("SD1")
	                        Dbsetorder(4)
	                        Dbseek(xFilial("SD1",aFiliais[i,1])+STL->TL_NUMSEQ)
	                        While !Eof() .And. SD1->D1_FILIAL == xFilial("SD1",aFiliais[i,1]);
	                           .And. SD1->D1_NUMSEQ == STL->TL_NUMSEQ
	                           If TRIM(STL->TL_CODIGO) == TRIM(SD1->D1_COD)
	                              lFOUND := .T.
	                              Exit
	                           Endif
	                           Dbskip()
	                        End
	                        If lFOUND
	                           Dbselectarea("SF4")
	                           Dbsetorder(1)
	                           If Dbseek(xFilial("SF4",aFiliais[i,1])+SD1->D1_TES)
	                              If SF4->F4_ESTOQUE == "N"
	                                 lPRO := .F.
	                              Endif
	                           Endif
	                        Endif
	                        nMATPRO := nMATPRO+If(lPRO,nCUSTO,0.00)
	                        nMATTER := nMATTER+If(lPRO,0.00,nCUSTO)
	                     Endif
	                  Endif
	               Endif
	               Dbselectarea("STL")
	               Dbskip()
	            End

	            MNTR310TR(xFilial("STJ",aFiliais[i,1]),STJ->TJ_ORDEM,STJ->TJ_CODBEM,STJ->TJ_DTMRFIM,STJ->TJ_POSCONT,STJ->TJ_POSCON2)

	         Endif
	      Endif
	   Endif
	   Dbselectarea("STJ")
	   Dbskip()
	End
Next i
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR310STS³ Autor ³ In cio Luiz kolling   ³ Data ³09/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa o.s. normais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR310STS()
Local aFiliais := {}, i
cCONDSTS := 'sts->ts_termino = "S" .And. sts->ts_situaca = "L" .And. '
cCONDSTS := cCONDSTS + '(sts->ts_dtmrfim >= MV_PAR03 .And. sts->ts_dtmrfim <= MV_PAR04)'
cCONDSTS := cCONDSTS + ' .And. (sts->ts_ccusto >= MV_PAR07 .And. sts->ts_ccusto <= MV_PAR08)'
cCONDST9 := 'st9->t9_codfami >= MV_PAR09 .And. st9->t9_codfami <= MV_PAR10'

aFiliais := MNTRETFIL("STS", MV_PAR01, MV_PAR02)
For i:=1 to Len(aFiliais)
	Dbselectarea("STS")
	Dbsetorder(2)
	Dbseek(xFilial("STS",aFiliais[i,1])+"B"+mv_par05,.T.)
	Procregua(LastRec())
	While !Eof() .And. STS->TS_FILIAL == xFilial("STS",aFiliais[i,1]) .And.;
	   STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM <= mv_par06
	   Incproc()
	   If &(cCONDSTS)
	      Dbselectarea("ST9")
	      Dbsetorder(1)
	      If Dbseek(xFilial("ST9",aFiliais[i,1])+STS->TS_CODBEM)
	         If &(cCONDST9)
	            nMDOPRO := 0.00     //MAO DE OBRA PROPRIA (TIPOREG == "M")
	            nMDOTER := 0.00     //MAO DE OBRA DE TERCEIRO (DESC.PRODUTO == "SERVICO")
	            nMATPRO := 0.00     //MATERIAL PROPRIO (MOVIMENTA ESTOQUE)
	            nMATTER := 0.00     //MATERIAL TERCEITO (NAO MOVIMENTA ESTOQUE)
	            nOLEO   := 0.00     //PNEU (DESC.PROD == PNEU OU PROTETOR OU CAMARA)

	            Dbselectarea("STT")
	            Dbsetorder(1)
	            Dbseek(xFilial("STT",aFiliais[i,1])+STS->TS_ORDEM+STS->TS_PLANO)
	            While !Eof() .And. STT->TT_FILIAL == xFilial("STT",aFiliais[i,1]) .And.;
	               STT->TT_ORDEM == STS->TS_ORDEM .And. STT->TT_PLANO == STS->TS_PLANO

	               If Alltrim(STT->TT_SEQRELA) <> "0"

	                  If MV_PAR13 = 2 //STANDARD
	                     aVETCUST := NGCUSTSTAN(STT->TT_CODIGO,STT->TT_TIPOREG)
		                 nCUSTO   := aVETCUST[1] //Custo standard

	                    vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
	 											           STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
	   											        STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
							  nQTDHORAS := vVETHORAS[1]

		                 nCUSTO   := nCUSTO * nQTDHORAS
		              Else
		                 nCUSTO   := STT->TT_CUSTO
		              EndIf

	                  If STT->TT_TIPOREG == "M"
	                     nMDOPRO := nMDOPRO+nCUSTO
	                  Elseif STT->TT_TIPOREG == "T"
	                     nMDOTER := nMDOTER+nCUSTO
	                  Elseif STT->TT_TIPOREG == "P"
	                     Dbselectarea("SB1")
	                     Dbsetorder(1)
	                     Dbseek(xFilial("SB1",aFiliais[i,1])+SUBSTR(STT->TT_CODIGO,1,15))
	                     If SB1->B1_GRUPO >= mv_par11 .And. SB1->B1_GRUPO <= MV_PAR12
	                        nOLEO := nOLEO+nCUSTO
	                     Else
	                        lPRO   := .T.
	                        lFOUND := .F.
	                        Dbselectarea("SD1")
	                        Dbsetorder(4)
	                        Dbseek(xFilial("SD1",aFiliais[i,1])+STT->TT_NUMSEQ)
	                        While !Eof() .And. SD1->D1_FILIAL == xFilial("SD1",aFiliais[i,1]);
	                           .And. SD1->D1_NUMSEQ == STT->TT_NUMSEQ
	                           If TRIM(STT->TT_CODIGO) == TRIM(SD1->D1_COD)
	                              lFOUND := .T.
	                              Exit
	                           Endif
	                           Dbskip()
	                        End
	                        If lFOUND
	                           Dbselectarea("SF4")
	                           Dbsetorder(1)
	                           If Dbseek(xFilial("SF4",aFiliais[i,1])+SD1->D1_TES)
	                              If SF4->F4_ESTOQUE == "N"
	                                 lPRO := .F.
	                              Endif
	                           Endif
	                        Endif
	                        nMATPRO := nMATPRO+If(lPRO,nCUSTO,0.00)
	                        nMATTER := nMATTER+If(lPRO,0.00,nCUSTO)
	                     Endif
	                  Endif
	               Endif
	               Dbselectarea("STT")
	               Dbskip()
	            End

	            MNTR310TR(xFilial("STS",aFiliais[i,1]),STS->TS_ORDEM,STS->TS_CODBEM,STS->TS_DTMRFIM,STS->TS_POSCONT,STS->TS_POSCON2)

	         Endif
	      Endif
	   Endif
	   Dbselectarea("STS")
	   Dbskip()
	End
Next i

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR310TR ³ Autor ³ In cio Luiz kolling   ³ Data ³09/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Grava arquivo temporario                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR310TR(cFil,cORDEM,cCODBEM,dDTFIM,nPOSCONT,nPOSCONT2)
Dbselectarea(cTRBTMP)

If mv_par14 = 1
   (cTRBTMP)->(DbAppend())
   (cTRBTMP)->FILIAL := cFil
   (cTRBTMP)->NUMOS  := cORDEM
   (cTRBTMP)->CODBEM := cCODBEM
   (cTRBTMP)->DATAOS := dDTFIM
   (cTRBTMP)->POSCONT:= nPOSCONT
   (cTRBTMP)->POSCONT2:= nPOSCONT2
   (cTRBTMP)->MDOPRO := nMDOPRO
   (cTRBTMP)->MDOTER := nMDOTER
   (cTRBTMP)->MATPRO := nMATPRO
   (cTRBTMP)->MATTER := nMATTER
   (cTRBTMP)->OLEO   := nOLEO
Else
   If !Dbseek(cCODBEM+DTOS(dDTFIM))
      RecLock(cTRBTMP,.T.)
      (cTRBTMP)->MDOPRO := 0
      (cTRBTMP)->MDOTER := 0
      (cTRBTMP)->MATPRO := 0
      (cTRBTMP)->MATTER := 0
      (cTRBTMP)->OLEO   := 0
   Else
      RecLock(cTRBTMP,.F.)
   Endif

   (cTRBTMP)->FILIAL := cFil
   (cTRBTMP)->NUMOS  := ""
   (cTRBTMP)->CODBEM := cCODBEM
   (cTRBTMP)->DATAOS := dDTFIM
   (cTRBTMP)->POSCONT:= nPOSCONT
   (cTRBTMP)->POSCONT2:= nPOSCONT2
   (cTRBTMP)->MDOPRO += nMDOPRO
   (cTRBTMP)->MDOTER += nMDOTER
   (cTRBTMP)->MATPRO += nMATPRO
   (cTRBTMP)->MATTER += nMATTER
   (cTRBTMP)->OLEO   += nOLEO

   STA->(MsUnLock())
Endif
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³NGRCODFAM ³Autor  ³ Elisangela Costa      ³ Data ³ 23/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o valor total                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³REPORTDEF                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function NGRETVAL()
Return nVALR

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MDTRETFIL ºAutor  ³Roger Rodrigues     º Data ³  03/09/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna filiais que nao sao prestador em um array           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAMDT                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNTRETFIL(cAlias, cFilDe, cFilAte)
Local aFiliais := {}
Local aFilsSM0 := FwAllFilial(,,cEmpAnt,.F.)
Local aAreaSM0 := SM0->(GetArea())
Local nX

If NGSX2MODO(cAlias) != "C"//Verifica se a tabela esta exclusiva
	For nX := 1 to len(aFilsSM0)
		If aFilsSM0[nX] >= cFilDe .and. aFilsSM0[nX] <= cFilAte
			aAdd( aFiliais , {aFilsSM0[nX], Upper(Substr(FwFilialName(,aFilsSM0[nX]), 1, 40)) })
			dbSelectArea("SM0")
			dbSkip()
		EndIf
	Next nX
	RestArea(aAreaSM0)
Else
	aAdd( aFiliais , {cFilAnt, Upper(Substr(FwFilialName(,cFilAnt), 1, 40)) })
Endif

Return aFiliais




/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNTR310   ºAutor  ³Microsiga           º Data ³  03/27/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
//
Static Function ImpTotal (aTotal,nTipo)
Local cTipo
//
If nTipo == 1
	cTipo := "Total do Bem"
ElseIf nTipo == 2
	cTipo := "Total da Filial"
ElseIf nTipo == 3
	cTipo := "Total Geral"
EndIf
//
@ Li,000 Psay __PrtFatLine()
NGSOMALI(58)
@ Li,000 Psay cTipo+":"
@ Li,052 - Len(Alltrim(aTotal[1])) Psay aTotal[1]   Picture "@E 999,999.99"
@ Li,069 - Len(Alltrim(aTotal[2])) Psay aTotal[2]   Picture "@E 999,999.99"
@ Li,084 - Len(Alltrim(aTotal[3])) Psay aTotal[3]   Picture "@E 999,999.99"
@ Li,100 - Len(Alltrim(aTotal[4])) Psay aTotal[4]   Picture "@E 999,999.99"
@ Li,112 - Len(Alltrim(aTotal[5])) Psay aTotal[5]   Picture "@E 999,999.99"
@ Li,122 - Len(Alltrim(aTotal[6])) Psay aTotal[6]   Picture "@E 999,999,999.99"
NGSOMALI(58)
NGSOMALI(58)
Return