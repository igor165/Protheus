#INCLUDE "MNTA094.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

Static lMntFrotas := IIf(FindFunction('MNTFrotas'), MNTFrotas(), SuperGetMV('MV_NGMNTFR') == 'S')

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA094
Cadastro de Modelos

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Function MNTA094()

	Local oBrowse

	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias( "TQR" )
	oBrowse:SetDescription( STR0006 ) // "Cadastro de Modelos"
	oBrowse:Activate()

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Interface da rotina

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function MenuDef()
// Inicializa MenuDef com todas as opções
Return FWMVCMenu( "MNTA094" )

//------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ModelDef()

	Local oModel
	Local oStructTQR := FWFormStruct(1,"TQR")

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTA094",, , { |oModel| CommitInfo( oModel ) })

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTA094_TQR", Nil, oStructTQR )

	oModel:SetDescription(STR0006)

Return oModel

//------------------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel("MNTA094")
	Local oView  := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA094_TQR" , FWFormStruct(2,"TQR") )

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100 )

	// Associa um View a um box
	oView:SetOwnerView( "MNTA094_TQR" , "MASTER" )

	// Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn( oView )

Return oView

//------------------------------------------------------------------------------
/*/{Protheus.doc} CommitInfo
Grava todas as informações

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function CommitInfo( oModel )

	//--------------------------------------------------------------------------
	// Grava informações do Model
	//--------------------------------------------------------------------------
	If	FwFormCommit( oModel )

		//--------------------------------------------------------------------------
		// Integração com PIMS
		//--------------------------------------------------------------------------
		If SuperGetMV("MV_PIMSINT",.F.,.F.)
			NGIntPIMS( "TQR" , ( "TQR" )->( RecNo() ) , oModel:GetOperation() )
		EndIf

	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA094VLD()
Validação dos campos.

@author Wexlei Silveira
@since	25/06/2020

@param cCampo, Caractere, Nome do campo a ser validado

@return Lógico, verdadeiro se informações corretas
/*/
//------------------------------------------------------------------------------
Function MNTA094VLD(cCampo)

	Local lRet := .T.

	If cCampo == 'TQR_TIPMOD'

		If !Empty( M->TQR_TIPMOD )

			// A partir do release 12.1.33, o valor '*' torna-se válido e indica que a regra aplica-se
			// à todos os tipos modelos existentes, por isso, este código não pode ser cadastrado na TQR
			If Trim( M->TQR_TIPMOD ) == '*'

				// ATENCAO: O código informado é de uso reservado e não pode ser cadastrado. Utilize outro valor para o código de tipo modelo.
				Help(Nil, Nil, Nil,/*cNome*/, STR0008, 4, 5, /*lPop*/, /*hWnd*/, /*nHeight*/, /*nWidth*/, /*lGravaLog*/, {STR0009})
				lRet := .F.

			ElseIf !ExistChav('TQR', M->TQR_TIPMOD )

				lRet := .F.

			EndIf

		EndIf

	ElseIf cCampo == 'TQR_CPPROD'

		If !Empty( M->TQR_CPPROD )

			lRet := Positivo()

		EndIf

	ElseIf cCampo == 'TQR_FABRIC'

		If !Empty( M->TQR_FABRIC )

			lRet := ExistCpo('ST7', M->TQR_FABRIC )

		EndIf

	ElseIf cCampo == 'TQR_UNPROD'

		If !Empty( M->TQR_UNPROD )

			lRet := ExistCpo('SAH', M->TQR_UNPROD )

		EndIf

	ElseIf cCampo == 'TQR_VALALU'

		If !Empty( M->TQR_VALALU )

			lRet := Positivo()

		EndIf

	EndIf

Return lRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA094INI()
Inicializador padrão para a descrição de campos.

@author Wexlei Silveira
@since	29/06/2020

@param cCampo, Caractere, Nome do campo

@return Caractere, Descrição do modelo
/*/
//------------------------------------------------------------------------------
Function MNTA094INI(cCampo)

	Local cDesc := ''

	If cCampo == 'TQR_NOMFAB'

		If !Inclui

			cDesc := Posicione("ST7", 1, xFilial("ST7") + TQR->TQR_FABRIC, "T7_NOME")

		EndIf

	EndIf

Return cDesc

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNT094WHEN
Função utilizada no When dos campos

@author Wexlei Silveira
@since 29/06/2020

@param cCampo, Caractere, Nome do campo

@return Lógico, Verdadeiro para liberar.
/*/
//------------------------------------------------------------------------------
Function MNT094WHEN(cCampo)

	Local lRet := .F.

	If cCampo == "TQR_CATBEM"

		lRet := lMntFrotas

	EndIf

Return lRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA094BRW(cCampo)
Inicializador do browse para a descrição de campos.

@author Wexlei Silveira
@since	29/06/2020

@param cCampo, Caractere, Nome do campo

@return Caractere, Descrição do campo
/*/
//------------------------------------------------------------------------------
Function MNTA094BRW(cCampo)

	Local cDesc := ''

	If cCampo == 'TQR_NOMFAB'

		cDesc := Posicione("ST7", 1, xFilial("ST7") + TQR->TQR_FABRIC, "T7_NOME")

	EndIf

Return cDesc
