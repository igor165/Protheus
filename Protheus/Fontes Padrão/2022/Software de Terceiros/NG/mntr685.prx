#INCLUDE "mntr685.ch"
#INCLUDE "PROTHEUS.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR685
description
@author  Rafael Diogo Richter
@since   30/01/2007
@version P12
@source  SIGAMNT
/*/
//-------------------------------------------------------------------
Function MNTR685()

	Local oReport
	Local aArea := GetArea()
	Local aNGBEGINPRM := NGBEGINPRM()//Armazena variaveis p/ devolucao (NGRIGHTCLICK)

	Private tiporeg  := ""
	Private cTRB     := GetNextAlias()
	Private cNomeBem := ""
	Private cNomeSer := ""
	Private lExist	  := .F.

	Private oTempTable	 //Obj. Tab. Temp.

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		Pergunte("MNT685",.T.)
		oReport := ReportDef()
		oReport:SetLandscape() //Default paisagem
		oReport:PrintDialog()
	Else
		MNTR685R3()
	EndIf

	RestArea(aArea)

	//Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	  ³
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef ³ Autor ³ Rafael Diogo Richter  ³ Data ³25/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Define as secoes impressas no relatorio                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()
	Local oReport
	Local oSection1, oSection2, oSection3, oSection4, oSection5
	Local oCell, oCell2, oCell3, oCell4
	Local nSizeCod := If((TAMSX3("B1_COD")[1]) < 1,15,(TAMSX3("B1_COD")[1]))

	Pergunte("MNT685",.F.)
	Private cPic := If(mv_par01 == 1,"@E 99,999.99", "99:99")
	Private nTamanho := If(mv_par01 == 1,9,5)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR685",OemToAnsi(STR0006),"MNT685",{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002+" "+STR0003)
	oReport:ParamReadOnly()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSection1 := TRSection():New(oReport,STR0021,{"STL","SB1","ST1","SH4","SH2","ST0"}) //"Insumo"
	TRCell():New(oSection1," "			," "	,STR0022		,"@!" ,nSizeCod,/*lPixel*/,{|| nProduto}) //"Código"
	TRCell():New(oSection1," "			," "	,STR0023		,"@!" ,40,/*lPixel*/,{|| nNome}) //"Descrição"
	//TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6") + TRB->FAMIBEM})

	#IFDEF TOP
	oSection2 := TRSection():New(oReport,STR0024,{(cTRB)}) //"Detalhes"
	oCell := TRCell():New(oSection2,"TL_TIPOREG" 	,(cTRB)	,STR0025	,"@S1" 				,01,/*lPixel*/) //"Prior."
	oCell := TRCell():New(oSection2,"TL_ORDEM" 		,(cTRB)	,STR0026 	,"@!" 				,06,/*lPixel*/) //"Ordem"
	oCell := TRCell():New(oSection2,"TJ_CODBEM" 	,(cTRB)	,STR0027	,"@!" 				,16,/*lPixel*/) //"Bem"
	oCell := TRCell():New(oSection2,"T9_NOME"		,(cTRB)	,STR0028	,"@S30"				,30,/*lPixel*/,{|| cNomeBem}) //"Nome"
	oCell := TRCell():New(oSection2,"TJ_SERVICO" 	,(cTRB)	,STR0029	,"@!"				,06,/*lPixel*/) //"Serv."
	oCell := TRCell():New(oSection2,"T4_NOME"		,(cTRB)	,STR0028	,"@S30" 			,30,/*lPixel*/,{|| cNomeSer}) //"Nome"
	oCell := TRCell():New(oSection2,"TL_TAREFA"		,(cTRB)	,STR0030	,"@!" 				,06,/*lPixel*/) //"Tarefa"
	oCell := TRCell():New(oSection2,"TL_DTINICI" 	,(cTRB)	,STR0031	,"99/99/9999" 		,10,/*lPixel*/) //"Data Inicio"
	oCell := TRCell():New(oSection2,"TL_HOINICI" 	,(cTRB)	,STR0032	,"99:99"		 	,05,/*lPixel*/) //"Hr.Ini."
	oCell := TRCell():New(oSection2,"TL_DTFIM" 		,(cTRB)	,STR0033	,"99/99/9999" 		,10,/*lPixel*/) //"Dt.Fim"
	oCell := TRCell():New(oSection2,"TL_HOFIM"	 	,(cTRB)	,STR0034	,"99:99" 			,05,/*lPixel*/) //"Hr.Fim"
	oCell := TRCell():New(oSection2,"TL_QUANREC" 	,(cTRB)	,STR0035	,"999"				,03,/*lPixel*/) //"Qtde"
	oCell := TRCell():New(oSection2,"TL_QUANTID"    ,(cTRB)	,STR0036	,cPic ,nTamanho,/*lPixel*/,{|| R685QTDTRB()}) //"Consumo"
	oCell := TRCell():New(oSection2,"TL_UNIDADE" 	,(cTRB)	,STR0037	,"@!" 				,03,/*lPixel*/) //"Uni"
	oCell := TRCell():New(oSection2," " 			,(cTRB)	,STR0038	,"@S3" 				,03,/*lPixel*/,{|| cBloq}) //"Blq."
	oCell := TRCell():New(oSection2," " 			,(cTRB)	,STR0039	,"@S8" 				,08,/*lPixel*/,{|| cDest}) //"Dest."
	#ELSE
	oSection2 := TRSection():New(oReport,STR0024,{"STL","STJ","ST9","ST4"}) //"Detalhes"
	oCell := TRCell():New(oSection2,"TL_TIPOREG" 	,"STL"	,STR0025	,"@S1" 				,01,/*lPixel*/) //"Prior."
	oCell := TRCell():New(oSection2,"TL_ORDEM" 		,"STL"	,STR0026 	,"@!" 				,06,/*lPixel*/) //"Ordem"
	oCell := TRCell():New(oSection2,"TJ_CODBEM" 	,"STJ"	,STR0027	,"@!" 				,16,/*lPixel*/) //"Bem"
	oCell := TRCell():New(oSection2,"T9_NOME"		,"ST9"	,STR0028	,"@S30"				,30,/*lPixel*/) //"Nome"
	oCell := TRCell():New(oSection2,"TJ_SERVICO" 	,"STJ"	,STR0029	,"@!"				,06,/*lPixel*/) //"Serv."
	oCell := TRCell():New(oSection2,"T4_NOME"		,"ST4"	,STR0028	,"@S30" 			,30,/*lPixel*/) //"Nome"
	oCell := TRCell():New(oSection2,"TL_TAREFA"		,"STL"	,STR0030	,"@!" 				,06,/*lPixel*/) //"Tarefa"
	oCell := TRCell():New(oSection2,"TL_DTINICI" 	,"STL"	,STR0031	,"99/99/9999" 		,10,/*lPixel*/) //"Data Inicio"
	oCell := TRCell():New(oSection2,"TL_HOINICI" 	,"STL"	,STR0032	,"99:99"		 	,05,/*lPixel*/) //"Hr.Ini."
	oCell := TRCell():New(oSection2,"TL_DTFIM" 		,"STL"	,STR0033	,"99/99/9999" 		,10,/*lPixel*/) //"Dt.Fim"
	oCell := TRCell():New(oSection2,"TL_HOFIM"	 	,"STL"	,STR0034	,"99:99" 			,05,/*lPixel*/) //"Hr.Fim"
	oCell := TRCell():New(oSection2,"TL_QUANREC" 	,"STL"	,STR0035	,"999"				,04,/*lPixel*/) //"Qtde"
	oCell := TRCell():New(oSection2,"TL_QUANTID"	,"STL"	,STR0036    ,cPic	            ,nTamanho,/*lPixel*/,{|| R685QTD()}) //"Consumo"
	oCell := TRCell():New(oSection2,"TL_UNIDADE" 	,"STL"	,STR0037	,"@!" 				,03,/*lPixel*/) //"Uni"
	oCell := TRCell():New(oSection2," " 			,"STL"	,STR0038	,"@S3" 				,03,/*lPixel*/,{|| cBloq}) //"Bloq."
	oCell := TRCell():New(oSection2," " 			,"STL"	,STR0039	,"@S8" 				,08,/*lPixel*/,{|| cDest}) //"Dest."
	oCell := TRPosition():New(oSection2,"ST9",1,{|| xFilial("ST9") + STJ->TJ_CODBEM})
	oCell := TRPosition():New(oSection2,"ST4",1,{|| xFilial("ST4") + STJ->TJ_SERVICO})
	//oCell := TRPosition():New(oSection2,"SB1",1,{|| xFilial("SB1") + TRB->PRODUTO})
	#ENDIF


Return oReport

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportPrint³ Autor ³ Rafael Diogo Richter  ³ Data ³25/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)
	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)

	Private nordem
	Private tipo
	Private nproduto
	Private nnome
	Private cBloq := "S"
	Private cDest := Space(1)


	If mv_par01 = 1          // PRODUTOS
		tiporeg  := 'P'
		calias   := 'SB1'
		tipo     := STR0007 //"Produto"
	Elseif mv_par01 = 2   // FUNCIONARIO
		tiporeg  := 'M'
		calias   := 'ST1'
		tipo     := STR0008 //"Funcionario"
	Elseif mv_par01 = 3   // FERRAMENTAS
		tiporeg  := 'F'
		calias   := 'SH4'
		tipo     := STR0009 //"Ferramenta"
	Elseif mv_par01 =4   // TERCEIROS
		tiporeg  := 'T'
		calias   := 'SA2'
		tipo     := STR0010 //"Terceiros"
	Else
		tiporeg  := 'E'
		calias   := 'ST0'
		tipo     := STR0014//"Especialidade"
	Endif

	#IFDEF TOP
	fMNTR685ST() //Monta TRB a partir de consulta SQL

	dbSelectArea((cTRB))
	dbGoTop()
	oReport:SetMeter(LastRec())
	While !Eof()
		If (cTRB)->TJ_TIPOOS = "B" .or. (cTRB)->TJ_TIPOOS = "L"
			nproduto := (cTRB)->TL_CODIGO

			Dbselectarea(calias)
			Dbsetorder(01)
			If Dbseek(xFilial(calias)+alltrim(nproduto))
				If mv_par01 = 1       // PRODUTOS
					nnome  := sb1->b1_desc
				Elseif mv_par01 = 2   // FUNCIONARIO
					nnome  := st1->t1_nome
				Elseif mv_par01 = 3   // FERRAMENTAS
					nnome := sh4->h4_DESCRI
				Elseif mv_par01 = 4   // TERCEIROS
					nnome := sa2->a2_nome
				Else                  //  ESPECIALIDADE
					nnome := st0->t0_nome
				Endif
			Endif


			oReport:SkipLine(2)
			oSection1:Init()
			oSection1:PrintLine()
			oSection2:Init()

			While !Eof() .And. (cTRB)->TL_CODIGO == nproduto
				nnome := ""

				oReport:IncMeter()

				nordem := (cTRB)->TL_ORDEM

				cBloq := STR0016 //"Sim"
				cDest := Space(1)

				If (cTRB)->TL_REPFIM = "N"
					cBloq := STR0017 //"Não"
				Endif

				If (cTRB)->TL_TIPOREG = "P"
					If (cTRB)->TL_DESTINO == "T"
						cDest := STR0018 //"Troca"
					Elseif (cTRB)->TL_DESTINO == "A"
						cDest := STR0019 //"Apoio"
					Elseif (cTRB)->TL_DESTINO == "S"
						cDest := STR0020 //"Substituição"
					Endif
				Endif

				If (cTRB)->TJ_TIPOOS = "B"
					If NGIFDBSEEK("ST9",(cTRB)->TJ_CODBEM,1)
						cNomeBem := SubStr(ST9->T9_NOME,1,20)
					Else
						cNomeBem := Space(Len(ST9->T9_NOME))
					EndIf
				ElseIf (cTRB)->TJ_TIPOOS = "L"
					If NGIFDBSEEK("TAF","X2"+(cTRB)->TJ_CODBEM,7)
						cNomeBem := SubStr(TAF->TAF_NOMNIV,1,20)
					Else
						cNomeBem := Space(Len(TAF->TAF_NOMNIV))
					EndIf
				EndIf

				If NGIFDBSEEK("ST4",(cTRB)->TJ_SERVICO,1)
					cNomeSer := SubStr(ST4->T4_NOME,1,20)
				Else
					cNomeSer := Space(Len(ST4->T4_NOME))
				EndIf

				oSection2:PrintLine()
				Dbselectarea((cTRB))
				Dbskip()
			End
			oSection1:Finish()
			oSection2:Finish()
		Else
			Dbselectarea((cTRB))
			Dbskip()
		Endif
		Dbselectarea((cTRB))
	End

	fMNTR685DE() //Deleta Tab. Temp.

	#ELSE
	Dbselectarea("STL")
	Dbsetorder(02)
	Dbseek(xFilial('STL')+tiporeg+"0  ")
	oReport:SetMeter(LastRec())
	While !Eof() .and. stl->tl_filial == xFilial("STL") .And.;
	stl->tl_tiporeg ==  tiporeg  .And. Alltrim(stl->tl_seqrela) = "0"

		Dbselectarea("STJ")
		Dbsetorder(01)
		If Dbseek(xFilial('STJ')+stl->tl_ordem+stl->tl_plano)
			If stj->tj_tipoos = "B" .or. stj->tj_tipoos = "L"
				nproduto := stl->tl_codigo

				Dbselectarea(calias)
				Dbsetorder(01)
				If Dbseek(xFilial(calias)+alltrim(nproduto))
					If mv_par01 = 1       // PRODUTOS
						nnome  := sb1->b1_desc
					Elseif mv_par01 = 2   // FUNCIONARIO
						nnome  := st1->t1_nome
					Elseif mv_par01 = 3   // FERRAMENTAS
						nnome := sh4->h4_DESCRI
					Elseif mv_par01 = 4   // TERCEIROS
						nnome := sa2->a2_nome
					Else                  //  ESPECIALIDADE
						nnome := st0->t0_nome
					Endif
				Endif

				oReport:SkipLine(2)
				oSection1:Init()
				oSection1:PrintLine()
				oSection2:Init()

				While !Eof() .and. stl->tl_filial == xFilial("STL") .And.;
				stl->tl_tiporeg ==  tiporeg  .And. Alltrim(stl->tl_seqrela) = "0";
				.And. stl->tl_codigo == nproduto

					If stl->tl_ordem >= mv_par03 .And. stl->tl_ordem <= mv_par04 .And. stl->tl_plano = mv_par02
						nnome := ""

						oReport:IncMeter()

						nordem := STL->TL_ORDEM

						Dbselectarea("STJ")
						Dbsetorder(01)
						Dbseek(xFilial("STJ")+nordem+MV_PAR02)

						cBloq := STR0016 //"Sim"
						cDest := Space(1)

						If STL->TL_REPFIM = "N"
							cBloq := STR0017 //"Não"
						Endif

						If STL->TL_TIPOREG = "P"
							If STL->TL_DESTINO == "T"
								cDest := STR0018 //"Troca"
							Elseif STL->TL_DESTINO == "A"
								cDest := STR0019 //"Apoio"
							Elseif STL->TL_DESTINO == "S"
								cDest := STR0020 //"Substituição"
							Endif
						Endif

						oSection2:PrintLine()
					Endif
					Dbselectarea("STL")
					Dbskip()
				End
				oSection1:Finish()
				oSection2:Finish()
			Else
				Dbselectarea("STL")
				Dbskip()
			Endif
		Endif
		Dbselectarea("STL")
	End
	#ENDIF

	//³ Devolve a condicao original do arquivo principam             ³
	RetIndex("STJ")
	RetIndex("STL")
	Set Filter To

	MS_FLUSH()

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR685R3
Desvio de ConSumo de insumos

@author  Inacio Luiz Kolling
@since   02/07/97
@version P12
/*/
//-------------------------------------------------------------------
Function MNTR685R3()

	Local cString    := "STL"
	Local cDesc1     := STR0001 //"Relatorio de apresentacao de consumo previsto por tipo de insumo do"
	Local cDesc2     := STR0002 //"plano de Manutencao. O usuario podera selecionar qual o tipo de insumo"
	Local cDesc3     := STR0003 //"deseja analisar, bem como estabelecer um filtro das Ordens de  Servico"
	Local wnrel      := "MNTR685"

	Private aReturn  := { STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private cPerg    := "MNT685    "
	Private Titulo
	Private Tamanho  := "G"

	//Variaveis utilizadas para qarametros!
	//mv_par01 - Tipo de Insumo ( Produto/Funcionario/Ferramenta/Terceiros/Especialidade)
	//mv_par02 - Plano de Manutencao
	//mv_par03 - De  Ordem de Servico
	//mv_par04 - Ate Ordem de Servico

	pergunte(cPerg,.F.)

	Titulo := STR0006 //"Projecao de Consumo Por Insumo"

	//Envia controle para a funcao SETPRINT
	wnrel := SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

	If nLastKey = 27
		Set Filter To
		Return
	Endif

	SetDefault(aReturn,cString)

	If nLastKey = 27
		Set Filter To
		Return
	Endif

	RptStatus({|lEnd| R685Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R685Imp  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 26/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR685                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R685Imp(lEnd,wnRel,titulo,tamanho)
	Local cRodaTxt := ""
	Local nCntImpr := 0
	Local calias
	Local achou    := .f.
	Local cPriorid := ""

	//³ Contadores de linha e pagina                                 ³
	Private li := 80 ,m_pag := 1
	Private Cabec1       := " "
	Private Cabec2       := " "
	Private nomeprog     := "MNTR685"
	Private ntipo        := 0
	Private nordem
	Private tipo
	Private nproduto
	Private nnome
	Private novoproduto := .f.
	Private quebracab := .f.


	//³ Verifica se deve comprimir ou nao                            ³
	nTipo  := IIF(aReturn[4]==1,15,18)


	//³ Monta os Cabecalhos                                          ³

	/*
	************************************************************************************************************************************
	*<empresa>                                                                                                        Folha..: xxxxx   *
	*SIGA /<nome .04                                 <     titulo do relatorio    >                                   DT.Ref.: dd/mm/aa*
	*Hora...: xx:xx:xx                                                                                                Emissao: dd/mm/aa*
	************************************************************************************************************************************
	Prior. Ordem  Bem              Nome                Serv.  Nome              Tarefa Data Inicio. Hora  Data Fim Hora  QTD  Consumo   Uni B D"
	********************************************************************************************************************************************
	1         2         3         4         5         6         7         8         9         0         1         2         3
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789

	Codigo..: xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	xxx xxxxxx xxxxxxxxxxxxxxxx xxxxxxxx19xxxxxxxxx xxxxxx xxxxxxxx17xxxxxxx xxxxxx xx/xx/xx xx:xx xx/xx/xx xx:xx xxx xxx.xxx,xx xxx S x
	*/

	If mv_par01 = 1          // PRODUTOS
		tiporeg  := 'P'
		calias   := 'SB1'
		tipo     := STR0007 //"Produto"
	Elseif mv_par01 = 2   // FUNCIONARIO
		tiporeg  := 'M'
		calias   := 'ST1'
		tipo     := STR0008 //"Funcionario"
	Elseif mv_par01 = 3   // FERRAMENTAS
		tiporeg  := 'F'
		calias   := 'SH4'
		tipo     := STR0009 //"Ferramenta"
	Elseif mv_par01 =4   // TERCEIROS
		tiporeg  := 'T'
		calias   := 'SA2'
		tipo     := STR0010 //"Terceiros"
	Else
		tiporeg  := 'E'
		calias   := 'ST0'
		tipo     := STR0014//"Especialidade"
	Endif

	#IFDEF TOP
	fMNTR685ST() //Monta TRB a partir de consulta SQL

	dbSelectArea((cTRB))
	dbGoTop()
	SetRegua(LastRec())
	While !Eof()
		IncRegua()
		If (cTRB)->TJ_TIPOOS = "B"
			nproduto := (cTRB)->TL_CODIGO
			novoproduto := .t.
			achou       := .T.
			nTotIns     := 0.00
			cUnidIns    := (cTRB)->TL_UNIDADE
			While !Eof() .And. (cTRB)->TL_CODIGO == nproduto
				nnome := space(36)
				cPriorid := Space(3)
				achou := .t.
				Dbselectarea(calias)
				Dbsetorder(01)
				If Dbseek(xFilial(calias)+alltrim(nproduto))
					If mv_par01 = 1// PRODUTOS
						nnome  := left(sb1->b1_desc,36)
					Elseif mv_par01 = 2// FUNCIONARIO
						nnome  := left(st1->t1_nome,36)
					Elseif mv_par01 = 3// FERRAMENTAS
						nnome := left(sh4->h4_DESCRI,36)
					Elseif mv_par01 = 4// TERCEIROS
						nnome := left(sa2->a2_nome,36)
					Else//  ESPECIALIDADE
						nnome := left(st0->t0_nome,36)
					Endif
				Endif

				vVETHORAS := NGTQUATINS((cTRB)->TL_CODIGO ,(cTRB)->TL_TIPOREG,(cTRB)->TL_USACALE,;
				(cTRB)->TL_QUANTID,(cTRB)->TL_TIPOHOR,(cTRB)->TL_DTINICI,;
				(cTRB)->TL_HOINICI,(cTRB)->TL_DTFIM  ,(cTRB)->TL_HOFIM  ,(cTRB)->TL_UNIDADE)
				nQTDHORAS := vVETHORAS[1]

				linhadetalhe((cTRB)->TL_QUANREC,nQTDHORAS         ,(cTRB)->TL_REPFIM,(cTRB)->TL_DESTINO,(cTRB)->TL_DTINICI,;
				(cTRB)->TL_HOINICI,(cTRB)->TL_DTFIM  ,(cTRB)->TL_HOFIM ,(cTRB)->TJ_PRIORID,(cTRB)->TL_ORDEM  ,;
				(cTRB)->TL_UNIDADE,(cTRB)->TL_TIPOREG,(cTRB)->TL_TAREFA,(cTRB)->TJ_CODBEM,(cTRB)->TJ_SERVICO)
				nTotIns += nQTDHORAS
				dbSelectArea((cTRB))
				dbSkip()
			End

			If achou
				somalinha()
			EndIf

			If nTotIns > 0
				If !achou
					somalinha()
				Endif
				lExist := .T.
				@li,192 Psay Replicate("-",14)
				somalinha()
				@li,159 Psay STR0015
				If cUnidIns = "H"
					@li,187+(13-Len(NtoH(nTotIns))) Psay NtoH(nTotIns)
				Else
					@li,190 Psay nTotIns picture "@E 9,999,999.99"
				EndIf
				@li,203 Psay cUnidIns
				somalinha()
			Endif
		Else
			dbSelectArea((cTRB))
			dbSkip()
		EndIf
		dbSelectArea((cTRB))
	End

	fMNTR685DE() //Deleta Tab. Temp.

	#ELSE
	Dbselectarea("STL")
	Dbsetorder(02)
	Dbseek(xFilial("STL")+tiporeg+"0  ")
	SetRegua(LastRec())
	While !Eof() .and. stl->tl_filial == xFilial("STL") .And.;
	stl->tl_tiporeg ==  tiporeg  .And. Alltrim(stl->tl_seqrela) = "0"

		IncRegua()
		Dbselectarea("STJ")
		Dbsetorder(01)
		If Dbseek(xFilial("STJ")+stl->tl_ordem+stl->tl_plano)
			If stj->tj_tipoos = "B"
				nproduto := stl->tl_codigo
				novoproduto := .t.
				nTotIns     := 0.00
				cUnidIns    := stl->tl_unidade
				While !Eof() .and. stl->tl_filial == xFilial("STL") .And.;
				stl->tl_tiporeg ==  tiporeg  .And. Alltrim(stl->tl_seqrela) = "0";
				.And. stl->tl_codigo == nproduto
					achou := .f.
					If stl->tl_ordem >= mv_par03 .And. stl->tl_ordem <= mv_par04 .And. stl->tl_plano = mv_par02

						nnome := space(36)

						achou := .t.
						Dbselectarea(calias)
						Dbsetorder(01)
						If Dbseek(xFilial(calias)+alltrim(nproduto))
							If mv_par01 = 1       // PRODUTOS
								nnome  := left(sb1->b1_desc,36)
							Elseif mv_par01 = 2   // FUNCIONARIO
								nnome  := left(st1->t1_nome,36)
							Elseif mv_par01 = 3   // FERRAMENTAS
								nnome := left(sh4->h4_DESCRI,36)
							Elseif mv_par01 = 4   // TERCEIROS
								nnome := left(sa2->a2_nome,36)
							Else                  //  ESPECIALIDADE
								nnome := left(st0->t0_nome,36)
							Endif
						Endif
						nordem := STL->TL_ORDEM

						Dbselectarea("STJ")
						Dbsetorder(01)
						Dbseek(xFilial("STJ")+nordem+MV_PAR02)

						vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
						STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
						STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
						nQTDHORAS := vVETHORAS[1]

						linhadetalhe(STL->TL_QUANREC,nQTDHORAS,STL->TL_REPFIM,STL->TL_DESTINO,STL->TL_DTINICI,;
						STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STJ->TJ_PRIORID,STL->TL_ORDEM,STL->TL_UNIDADE,STL->TL_TIPOREG)
						nTotIns += nQTDHORAS
					Endif
					Dbselectarea("STL")
					Dbskip()
				End

				If achou
					somalinha()
				Endif

				If nTotIns > 0
					If !achou
						somalinha()
					Endif
					lExist := .T.
					@li,192 Psay Replicate("-",14)
					somalinha()
					@li,159 Psay STR0015
					If cUnidIns = "H"
						@li,187+(13-Len(NtoH(nTotIns))) Psay NtoH(nTotIns)
					Else
						@li,190 Psay nTotIns picture "@E 9,999,999.99"
					EndIf
					@li,203 Psay cUnidIns
					somalinha()
				Endif
			Else
				Dbselectarea("STL")
				Dbskip()
			Endif
		Endif
		Dbselectarea("STL")
	End
	#ENDIF

	Roda(nCntImpr,cRodaTxt,Tamanho)

	//³ Devolve a condicao original do arquivo principam             ³
	If !lExist
		nDel := Help(" ",1,"NGTDADIMP") //"Nao existem dados para imprimir..."###"ATENCAO"

	Else
		RetIndex("STJ")
		RetIndex("STL")
		Set Filter To

		Set device to Screen

		If aReturn[5] = 1
			Set Printer To
			dbCommitAll()
			OurSpool(wnrel)
		Endif
		MS_FLUSH()
	EndIf
Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ LINHADETALHE³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 26/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR685                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function linhadetalhe(recurso,consumo,bloqueio,destino,dataprevista,horainicio,;
	datafim,horafim,prioridade,ordem,unidade,tipo,cTarefa,cCodBem,cServico)
	Local desbloqueio := STR0016 //"Sim"
	Local desdestino  := space(01), Dtvaz := CtoD("01/01/1980")

	Recurso      := If( ISNIL(recurso)     , 0    , recurso)
	Consumo      := If( ISNIL(consumo)     , 0    , consumo)
	Bloqueio     := If( ISNIL(bloqueio)    , " "  , bloqueio)
	Destino      := If( ISNIL(Destino)     , STL->TL_DESTINO , destino)
	Dataprevista := If( ISNIL(dataprevista), DtVaz, DataPrevista)
	HoraInicio   := If( ISNIL(HoraInicio)  , " "  , HoraInicio)
	Datafim      := If( ISNIL(datafim)     , DtVaz, DataFim)
	HoraFIm      := If( ISNIL(HoraFim)     , " "  , HoraFim)
	Prioridade   := If( ISNIL(Prioridade)  , "   ", Tipo)
	Ordem        := If( ISNIL(Ordem)       , " "  , Ordem)
	Unidade      := If( ISNIL(Unidade)     , " "  , Unidade)
	Tipo         := If( ISNIL(Tipo)        , STL->TL_TIPOREG , Tipo)
	cTarefa      := If( ISNIL(cTarefa)     , STL->TL_TAREFA  , cTarefa)
	cCodBem      := If( ISNIL(cCodBem)     , STJ->TJ_CODBEM  , cCodBem)
	cServico     := If( ISNIL(cServico)    , STJ->TJ_SERVICO , cServico)

	If bloqueio = "N"
		desbloqueio := STR0017 //"Não"
	Endif
	If Tipo = "P"
		If Destino == "T"
			desdestino := STR0018 //"Troca"
		Elseif Destino == "A"
			desdestino := STR0019 //"Apoio"
		Elseif Destino == "S"
			desdestino := STR0020 //"Substituição"
		Endif
	Endif

	If novoproduto
		somalinha()
		@li,000 Psay STR0012+nproduto+"       "+nnome //"Codigo..:"
		novoproduto := .f.
		quebracab   := .f.
	Endif
	Somalinha()
	If quebracab
		@li,000 Psay STR0012+nproduto+"       "+nnome //"Codigo..:"
		quebracab   := .f.
		Somalinha()
	Endif
	@li,000 Psay prioridade
	@li,007 Psay ordem
	@li,014 Psay cCodBem

	If STJ->TJ_TIPOOS = "B"
		Dbselectarea("ST9")
		Dbsetorder(01)
		If Dbseek(xFilial('ST9')+cCodBem)
			@li,031 Psay SubStr(ST9->T9_NOME,1,40)
		EndIf
	Else
		dbSelectArea("TAF")
		dbSetOrder(2)
		If dbSeek(xFilial("TAF")+"001"+AllTrim(cCodBem))
			@ Li,031 Psay SubStr(TAF->TAF_NOMNIV,1,56)
		EndIf
	EndIf

	@li,088 Psay cServico

	Dbselectarea("ST4")
	Dbsetorder(01)
	If Dbseek(xfilial('ST4') + cServico)
		@li,095 Psay SubStr(ST4->T4_NOME,1,40)
	Endif
	lExist := .T.
	@li,137 Psay cTarefa
	@li,145 Psay dataprevista   picture "99/99/99"
	@li,157 Psay horainicio     picture "99:99"
	@li,165 Psay datafim        picture "99/99/99"
	@li,176 Psay horafim        picture "99:99"
	@li,188 Psay recurso        picture "999"          // quantidade de recurso
	If unidade = "H" .And. tipo != "P"
		@li,189+(11-Len(NtoH(consumo))) Psay NtoH(consumo)
	Else
		@li,192 Psay consumo        picture "@E 999,999.99"   // quantidade de consumo
	EndIf
	@li,203 Psay unidade

	If tipo != "T"
		@li,209 Psay desbloqueio
	Endif
	@Li,215 Psay SubStr(desdestino,1,5)

return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaLinha()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR685        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/

static Function Somalinha()
	Li++
	If Li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		Cabec1()
		quebracab := .t.
	Endif
return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ Cabec1   ³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Cabecalho do relatorio                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR685        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/

Static Function Cabec1()
	@prow()+1,000 Psay replicate("*",220)
	@prow()+1,000 Psay STR0013 //"Pri Ordem  Bem              Nome                                                     Serv.  Nome                                      Tarefa  Data Inic. Hora   Data Fim   Hora   QTD    Consumo Uni. Bloq. Dest."
	@prow()+1,000 Psay replicate("*",220)
	li := prow()+1
	return


	****************************************************************************
Static function ISNIL(cVar)

	If valtype(cVAR) == "U"
		Return .t.
	Endif

Return .f.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ R685QTD  ³ Autor ³ Ricardo Dal Ponte     ³ Data ³ 22/05/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Transforma a quantidade do campo tl_quantid                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR685        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/

Function R685QTD()
	nQTDSTL := 0
	vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
	STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
	STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
	nQTDSTL := vVETHORAS[1]
Return If(STL->TL_TIPOREG == "P",nQTDSTL,Space(16-Len(NtoH(nQTDSTL)))+NtoH(nQTDSTL))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³R685QTDTRBºAutor  ³Wagner S. de Lacerdaº Data ³  30/09/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Transforma a quantidade do campo tl_quantid.               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MNTR685                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºProgramador ³   Data     ³ Descricao                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º            ³ xx/xx/xxxx ³                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function R685QTDTRB()
	nQTDSTL := 0
	vVETHORAS := NGTQUATINS((cTRB)->TL_CODIGO ,(cTRB)->TL_TIPOREG,(cTRB)->TL_USACALE,;
	(cTRB)->TL_QUANTID,(cTRB)->TL_TIPOHOR,(cTRB)->TL_DTINICI,;
	(cTRB)->TL_HOINICI,(cTRB)->TL_DTFIM  ,(cTRB)->TL_HOFIM  ,(cTRB)->TL_UNIDADE)
	nQTDSTL := vVETHORAS[1]

Return If((cTRB)->TL_TIPOREG == "P",nQTDSTL,NtoH(nQTDSTL))

//------------------------------------------------------------------------------
/*/{Protheus.doc} fMNTR685ST
Monta a TRB a partir da consulta SQL. (SQL to TRB)

@author	 Wagner S. de Lacerda
@since	 30/09/2010
@version P11/P12
@obs 	 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL
@return  .T.
/*/
//------------------------------------------------------------------------------
Static Function fMNTR685ST()

	Local aDBF   := {}
	Local cQuery := ""

	dbSelectArea("STL")
	aDBF := dbStruct()
	aAdd(aDBF, {"TJ_CODBEM" , "C",16, 0})
	aAdd(aDBF, {"TJ_TIPOOS" , "C", 1, 0})
	aAdd(aDBF, {"TJ_SERVICO", "C", 6, 0})
	aAdd(aDBF, {"TJ_PRIORID", "C", 3, 0})

	//Instancia classe FWTemporaryTable
	oTempTable  := FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices
	oTempTable:AddIndex( "Ind01", { "TL_FILIAL", "TL_CODIGO", "TL_ORDEM", "TL_PLANO"}  )
	//Cria a tabela temporaria
	oTempTable:Create()

	cQuery := "SELECT STL.*,STJ.TJ_CODBEM,STJ.TJ_TIPOOS,STJ.TJ_SERVICO,STJ.TJ_PRIORID FROM "+RetSqlName("STL")+" STL "
	cQuery += "JOIN "+RetSqlName("STJ")+" STJ ON (STJ.TJ_FILIAL = STL.TL_FILIAL "
	cQuery += "AND STJ.TJ_ORDEM = STL.TL_ORDEM "
	cQuery += "AND STJ.TJ_PLANO = '"+mv_par02+"' "
	cQuery += "AND STJ.D_E_L_E_T_ <> '*') "
	cQuery += "WHERE STL.TL_FILIAL = '"+xFilial("STL")+"' "
	cQuery += "AND STL.TL_ORDEM >= '"+mv_par03+"' AND STL.TL_ORDEM <= '"+mv_par04+"' " "
	cQuery += "AND STL.TL_TIPOREG = '"+tiporeg+"' "
	cQuery += "AND STL.TL_SEQRELA = '0' "
	cQuery += "AND STL.D_E_L_E_T_ <> '*' "
	cQuery += "ORDER BY STL.TL_FILIAL, STL.TL_CODIGO, STL.TL_ORDEM, STL.TL_PLANO "

	SqlToTrb(cQuery,aDBF,cTRB)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fMNTR685DEºAutor  ³Wagner S. de Lacerdaº Data ³  30/09/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Deleta a TRB e o arquivo criado.                           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MNTR685                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºProgramador ³   Data     ³ Descricao                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º            ³ xx/xx/xxxx ³                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fMNTR685DE()
	oTempTable:Delete()//Deleta Arquivo temporario
Return .T.
