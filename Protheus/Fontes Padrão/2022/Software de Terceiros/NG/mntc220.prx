#INCLUDE "Mntc220.ch"
#INCLUDE "PROTHEUS.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC220
Custo de Manutencao  ( Planejada X Nao Planejada )
@author Thiago Olis Machado
@since 07/08/01
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC220()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ mv_par01     // Periodo Inicio                               ³
	//³ mv_par02     // Periodo Fim                                  ³
	//³ mv_par03     // De Tipo de Manutencao                        ³
	//³ mv_par04     // Ate Tipo de Manutencao                       ³
	//³ mv_par05     // De Area Manutencao                           ³
	//³ mv_par06     // Ate Area de Manutencao                       ³
	//³ mv_par07     // De Centro de Custo                           ³
	//³ mv_par08     // Ate Centro de Custo                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	Private lOutro := .F.

	If Pergunte("MNT22G",.T.)
		Processa({|lEnd| NG220GRA()})
	EndIf

Return Nil
//---------------------------------------------------------------------
/*/{Protheus.doc} NG220GRA
Processa as O.S.
@author Thiago Olis Machado
@since 06/08/01
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function NG220GRA()

	Local oTmpTbl1
	Local oTmpTbl2

	Private cTRB  := GetNextAlias()
	Private cTRBV := GetNextAlias()

	lEnd     := .F.
	cCONDSTJ := 'STJ->TJ_SITUACA = "L" .And. STJ->TJ_TERMINO = "S" .And. STJ->TJ_TIPOOS = "B"'
	cCONDSTJ := cCONDSTJ + ' .And. (STJ->TJ_DTMRFIM >= MV_PAR01 .And. STJ->TJ_DTMRFIM <= MV_PAR02)'
	cCONDSTJ := cCONDSTJ + ' .And. (STJ->TJ_TIPO >= MV_PAR03 .And. STJ->TJ_TIPO <= MV_PAR04)'
	cCONDSTJ := cCONDSTJ + ' .And. (STJ->TJ_CODAREA >= MV_PAR05 .And. STJ->TJ_CODAREA <= MV_PAR06)'

	cCONDSTS := 'STS->TS_SITUACA = "L" .And. STS->TS_TERMINO = "S" .And. STS->TS_TIPOOS = "B"'
	cCONDSTS := cCONDSTS + ' .And. (STS->TS_DTMRFIM >= MV_PAR01 .And. STS->TS_DTMRFIM <= MV_PAR02)'
	cCONDSTS := cCONDSTS + ' .And. (STS->TS_TIPO >= MV_PAR03 .And. STS->TS_TIPO <= MV_PAR04)'
	cCONDSTS := cCONDSTS + ' .And. (STS->TS_CODAREA >= MV_PAR05 .And. STS->TS_CODAREA <= MV_PAR06)'

	aCampos := {}
	AAdd(aCAMPOS,{"ANOMESC"  ,"C",06,0})
	AAdd(aCAMPOS,{"NOMEMANO" ,"C",18,0})
	AAdd(aCAMPOS,{"VALORP"   ,"N",12,2})
	AAdd(aCAMPOS,{"VALORN"   ,"N",12,2})
	AAdd(aCAMPOS,{"VALORO"   ,"N",12,2})

	//Intancia classe FWTemporaryTable
	oTmpTbl1 := FWTemporaryTable():New( cTRB, aCampos )
	//Cria indices
	oTmpTbl1:AddIndex( "Ind01" , {"ANOMESC"} )
	//Cria a tabela temporaria
	oTmpTbl1:Create()

	//+--------------------------------------------------------------------------+
	//| Processa arquivo de O.S.                                                 |
	//+--------------------------------------------------------------------------+
	DbSelectArea("STJ")
	DbSetOrder(07)
	DbSeek(xFilial("STJ")+MV_PAR07,.T.)
	nReg := (LastRec() - Recno())

	DbSelectArea("STS")
	DbSetOrder(04)
	DbSeek(xFilial("STS")+MV_PAR07,.T.)
	nReg := nReg + (LastRec() - Recno())


	//FILTRA O PERIODO DE MANUTENCAO
	vSEQMES := {}
	vSERPR  := {}
	dDTPAR1 := MV_PAR01
	nDIA    := day(dDTPAR1)
	nMES    := Month(dDTPAR1)
	nANO    := year(dDTPAR1)
	nLopINI := val(SubStr(dtos(dDTPAR1), 1, 6))
	nLopFIM := val(SubStr(dtos(MV_PAR02), 1, 6))

	While nLopINI <= nLopFIM
		dData := dDTPAR1
		cMes  := SubStr(MESEXTENSO(Str(Month(dData))),1,3)
		cAno  := AllTrim(Str(Year(dData)))

		cANOMES := STR(nANO,4)+Strzero(nMES,2)

		DbSelectArea(cTRB)
		If !(cTRB)->(DbSeek(cANOMES))
			(cTRB)->(DbAppend())
			(cTRB)->ANOMESC  := cANOMES
			(cTRB)->NOMEMANO := MESEXTENSO(nMES)+"/"+ STR(nANO,4) //cMES
			(cTRB)->VALORP   := 0
			(cTRB)->VALORN   := 0
			(cTRB)->VALORO   := 0
		EndIf

		nMES  += 1

		If nMES > 12
			nMES := nMES -12
			nANO := nANO + 1
		EndIf

		nDIA1   := NGDIASMES(nMES,nANO)
		nDIA1   := If(nDIA <= nDIA1,nDIA,nDIA1)
		dDTPAR1 := cTod(Str(nDIA1)+Str(nMes)+Str(nAno))

		nLopINI := val(SubStr(dtos(dDTPAR1), 1, 6))
	End

	DbSelectArea("STJ")
	ProcRegua(nReg)
	While !Eof() .And. STJ->TJ_FILIAL  == xFilial("STJ") .And. STJ->TJ_CCUSTO <= MV_PAR08

		IncProc()
		If &(cCONDSTJ)

			DbSelectArea("ST4")
			DbSetOrder(01)
			DbSeek(xFilial("ST4")+STJ->TJ_SERVICO)

			DbSelectArea("STE")
			DbSetOrder(01)
			DbSeek(xFilial("STE")+ST4->T4_TIPOMAN)
			If STE->TE_CARACTE == "C"
				cTIPO_OS := "C"
			ElseIf STE->TE_CARACTE == "P"
				cTIPO_OS := "P"
			Else
				cTIPO_OS := "O"
				lOutro := .t.
			EndIf

			//Recalcula o Custo da O.S
			MNTA901OS("STJ")

			nRea := STJ->TJ_CUSTMDO + STJ->TJ_CUSTMAT + STJ->TJ_CUSTMAA + STJ->TJ_CUSTMAS + STJ->TJ_CUSTTER

			nMES := Month(STJ->TJ_DTMRFIM)
			nAno := Year(STJ->TJ_DTMRFIM)
			cANOMES := STR(nANO,4)+Strzero(nMES,2)

			DbSelectArea(cTRB)
			If !DbSeek(cANOMES)
				DbAppend()
				(cTRB)->ANOMESC  := cANOMES //STJ->TJ_CODBEM
				(cTRB)->NOMEMANO := MESEXTENSO(nMES)+"/"+ STR(nANO,4) //cMES //SubStr(ST9->T9_NOME,1,20)
			EndIf

			If cTIPO_OS == "P"
				(cTRB)->VALORP   := (cTRB)->VALORP + nREA
			ElseIf cTIPO_OS == "C"
				(cTRB)->VALORN   := (cTRB)->VALORN + nREA
			Else
				(cTRB)->VALORO   := (cTRB)->VALORO + nREA
			EndIf
		EndIf
		DbSelectArea("STJ")
		DbSkip()
	End

	//+--------------------------------------------------------------------------+
	//| Processa arquivo de O.S. Historico                                       |
	//+--------------------------------------------------------------------------+
	DbSelectArea("STS")
	While !Eof() .And. STS->TS_FILIAL  == xFilial("STS") .And. STS->TS_CCUSTO <= MV_PAR08

		IncProc()
		If &(cCONDSTS)

			DbSelectArea("ST4")
			DbSetOrder(01)
			DbSeek(xFilial("ST4")+STS->TS_SERVICO)

			DbSelectArea("STE")
			DbSetOrder(01)
			DbSeek( xFilial("STE") + ST4->T4_TIPOMAN )
			If STE->TE_CARACTE == "C"
				cTIPO_OS := "C"
			ElseIf STE->TE_CARACTE == "P"
				cTIPO_OS := "P"
			Else
				cTIPO_OS := "O"
				lOutro := .t.
			EndIf

			//Recalcula o Custo da O.S
			MNTA901OS("STS")

			nRea := STS->TS_CUSTMDO + STS->TS_CUSTMAT + STS->TS_CUSTMAA + STS->TS_CUSTMAS + STS->TS_CUSTTER

			nMES := Month(STS->TS_DTMRFIM)
			nAno := Year(STS->TS_DTMRFIM)
			cANOMES := STR(nANO,4)+Strzero(nMES,2)

			DbSelectArea(cTRB)
			If !DbSeek(cANOMES)
				DbAppend()
				(cTRB)->ANOMESC  := cANOMES //STS->TS_CODBEM
				(cTRB)->NOMEMANO := MESEXTENSO(nMES)+"/"+ STR(nANO,4) //cMES //SubStr(ST9->T9_NOME,1,20)
			EndIf

			If cTIPO_OS == "P"
				(cTRB)->VALORP := (cTRB)->VALORP + nREA
			ElseIf cTIPO_OS == "C"
				(cTRB)->VALORN := (cTRB)->VALORN + nREA
			Else
				(cTRB)->VALORO := (cTRB)->VALORO + nREA
			EndIf
		EndIf
		DbSelectArea("STS")
		DbSkip()
	End

	//+--------------------------------------------------------------------------+
	//| Inicio da Montagem do Gráfico                                            |
	//+--------------------------------------------------------------------------+
	DbSelectArea(cTRB)
	DbGotop()
	If Reccount() == 0
		Help(" ",1,"NGTDADGRAF") //"Nao existem dados para montar o grafico."###"ATENCAO"
		oTmpTbl1:Delete()
		Return .T.
	Endif

	aDBF3 := {{"CODIGO" , "C", 06,0} ,;
			  {"NOMECOD", "C", 18,0} ,;
			  {"VALOR1" , "N", 12,2} ,;
			  {"VALOR2" , "N", 12,2}}

	If lOutro
		Aadd(aDBF3,{"VALOR3" , "N", 12,2})
	Endif

	//Intancia classe FWTemporaryTable
	oTmpTbl2 := FWTemporaryTable():New( cTRBV, aDBF3 )
	//Index
	oTmpTbl2:AddIndex( "Ind02" , {"CODIGO"}  )
	//Cria a tabela temporaria
	oTmpTbl2:Create()

	DbSelectArea(cTRBV)
	DbGotop()
	DbSelectArea(cTRB)
	DbGotop()
	While !Eof()
		(cTRBV)->(DbAppend())
		(cTRBV)->CODIGO  := (cTRB)->ANOMESC
		(cTRBV)->NOMECOD := (cTRB)->NOMEMANO
		(cTRBV)->VALOR1  := (cTRB)->VALORP
		(cTRBV)->VALOR2  := (cTRB)->VALORN
		If lOutro
			(cTRBV)->VALOR3 := (cTRB)->VALORO
		Endif

		DbselectArea(cTRB)
		Dbskip()
	End

	// 1- linha titulo do grafico (janela)
	// 2- linha titulo da direita do grafico
	// 3- linha titulo superior do grafico
	// 4- linha titulo da direita do grafico
	// 5- linha titulo da inferior do grafico
	// 6- linha series do grafico
	// 7- leitura ("A" - Arquivo temporario,"M" - Matriz)
	// 8- alias doa arquivo temporario com os dados /ou
	// 9- matriz com os dados

	aServicos := {}
	Aadd(aServicos,STR0008) //"Preventiva"
	Aadd(aServicos,STR0009) //"Corretiva"
	If lOutro
		Aadd(aServicos,STR0011) //"Outros"
	Endif

	// Gráfico ## Custo de Manutenção ## Período de xx/xx/xxxx até xx/xx/xxxx
	NGGRAFICO( STR0004, ' ', ' ', STR0007 + STR0005 + DToC( MV_PAR01 ) + STR0006 + DtoC( MV_PAR02 ),;
		' ', aServicos, 'A', cTRBV )

	oTmpTbl1:Delete()
	oTmpTbl2:Delete()

Return .T.