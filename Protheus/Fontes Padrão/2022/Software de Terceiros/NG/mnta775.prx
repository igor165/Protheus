#INCLUDE "MNTA775.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA775
Cadastro de Orgaos Autuadores de Multa

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Function MNTA775()

	Local aNGBeginPrm := NGBeginPrm()
	Local oBrowse

	Private cCadastro := NGSX2NOME("TRZ") //"Cadastro de Orgaos Autuadores de Multa"
	Private aRotina   := MenuDef()
	Private lIntFin   := SuperGetMv("MV_NGMNTFI",.F.,"N") == "S"

	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias( "TRZ" )
	oBrowse:SetDescription( cCadastro )
	oBrowse:Activate()

	NGReturnPrm( aNGBeginPrm )

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Interface da rotina

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function MenuDef()
// Inicializa MenuDef com todas as opções
Return FWMVCMenu( "MNTA775" )

//------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ModelDef()

	Local oModel
	Local oStructTRZ := FWFormStruct(1,"TRZ")

	//Remove campos da struct
	If SuperGetMv("MV_NGMNTFI",.F.,"N") != "S"
		oStructTRZ:RemoveField('TRZ_FORNEC')
		oStructTRZ:RemoveField('TRZ_LOJA')
		oStructTRZ:RemoveField('TRZ_NOMFOR')
		oStructTRZ:RemoveField('TRZ_CONPAG')
	EndIf

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTA775")

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTA775_TRZ", Nil, oStructTRZ)

	oModel:SetDescription( NGSX2NOME("TRZ") )

Return oModel

//------------------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ViewDef()

	Local oModel	 := FWLoadModel("MNTA775")
	Local oStructTRZ := FWFormStruct(2,"TRZ")
	Local oView		 := FWFormView():New()

	//Remove campos da struct
	If  SuperGetMv("MV_NGMNTFI",.F.,"N") != "S"
		oStructTRZ:RemoveField('TRZ_FORNEC')
		oStructTRZ:RemoveField('TRZ_LOJA')
		oStructTRZ:RemoveField('TRZ_NOMFOR')
		oStructTRZ:RemoveField('TRZ_CONPAG')
	EndIf

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA775_TRZ" , oStructTRZ )

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100)

	// Associa um View a um box
	oView:SetOwnerView( "MNTA775_TRZ" , "MASTER" )

	// Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn( oView )

Return oView

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT775WHEN
Se existir Multa com Orgao Autuador, fecha o campo(Int.Fin)

@author Marcos Wagner Junior
@since 06/08/2014
@version P11
@return Nil
/*/
//---------------------------------------------------------------------
Function MNT775WHEN()

	Local lRet := .T.
	Local cAliasQry := ""
	Local cQuery := ""

	If ALTERA

		cAliasQry := GetNextAlias()
		cQuery := " SELECT 1 "
		cQuery += " FROM " + RetSqlName("TRX") + " TRX "
		cQuery += " WHERE TRX.TRX_CODOR  =  '"+TRZ->TRZ_CODOR+"'"
		cQuery += " AND   TRX.TRX_NUMSE2 <> ' ' "
		cQuery += " AND   TRX.D_E_L_E_T_ <> '*' "
		cQuery := ChangeQuery(cQuery)
		MPSysOpenQuery( cQuery , cAliasQry )
		dbGotop()

		If !Eof()
			lRet := .F.
		Endif

		(cAliasQry)->(dbCloseArea())

	EndIf

Return lRet
