#INCLUDE "mntc680.ch"
#INCLUDE "PROTHEUS.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC680
Consulta Geral de Serviço do Tipo

@author  Inacio Luiz Kolling
@since   18/09/98
@return  .T., Lógico
/*/
//---------------------------------------------------------------------

FUNCTION MNTC680()

	// Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	Local aNGBEGINPRM := NGBEGINPRM()

	PRIVATE aRotina := MenuDef()
	PRIVATE aEstru := { }

	cCadastro := OemToAnsi(STR0003) //"Servico do Tipo"

	// Endereca a funcao de BROWSE

	mBrowse( 6, 1,22,75,"STE")

	// Desativa tecla F12
	Set Key VK_F12 To

	// Recupera a Ordem Original do arquivo principal
	dbSelectArea("STE")
	dbSetOrder(1)

	// Devolve variaveis armazenadas (NGRIGHTCLICK)
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC680CON
Monta um browse dos serviços do tipo

@author  Inacio Luiz Kolling
@since   18/09/98
@return  Nulo
/*/
//---------------------------------------------------------------------

Function MNC680CON

	Local OldRot
	LOCAL ccondicao

	PRIVATE cCadastro := OemtoAnsi(STR0004) //"Cadastro Servico"

	OldRot := aCLONE(aROTINA)

	aRotina := { { STR0001   ,"MNT680PE",  0 , 1},; //"Pesquisar"
				{ STR0005  ,"NGCAD01"  , 0 , 2}} //"Visualizar"

	m->TE_TIPOMAN := STE->TE_TIPOMAN

	dbselectarea('ST4')
	DbSetOrder(4)

	cKey := M->TE_TIPOMAN

	bWHILE := {|| !eof() .AND. ST4->T4_TIPOMAN == M->TE_TIPOMAN }
	bFOR   := {|| T4_FILIAL  == xFilial("ST4") }

	NGCONSULTA("TRB4", cKEY, bWHILE, bFOR, aRotina,{})

	dbSelectArea("ST4")
	dbSetOrder(1)
	aRotina := aCLONE(OldRot)

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de Menu Funcional.

@author Ricardo Dal Ponte
@since 29/11/2006
@param	1. Nome a aparecer no cabecalho
		2. Nome da Rotina associada
		3. Reservado
		4. Tipo de Transação a ser efetuada:
			1 - Pesquisa e Posiciona em um Banco de Dados
			2 - Simplesmente Mostra os Campos
			3 - Inclui registros no Bancos de Dados
			4 - Altera o registro corrente
			5 - Remove o registro corrente do Banco de Dados
		5. Nivel de acesso
		6. Habilita Menu Funcional
@return Array
/*/
//-------------------------------------------------------------------------------

Static Function MenuDef()

	Local aRotina := { { STR0001  ,"AxPesqui",  0 , 1},; //"Pesquisar"
						{ STR0002   ,"MNC680CON", 0 , 2}} //"Consulta"

Return(aRotina)

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT680PE
Realiza a operação de pesquisa

@author  Giovana Niehues
@since   10/10/2019
@version P12
@return  .T., Lógico
/*/
//---------------------------------------------------------------------

Function MNT680PE()

	Local oDlgPesq, oOrdem, oChave, oBtOk, oBtCan, oBtPar
	Local cOrdem
	Local cChave  := Space(255)
	Local cAlias  := 'ST4'
	Local cAQuery := GetNextAlias()
	Local nOrdem  := 1
	Local nOpca
	Local aOrdens := {}

	//Pega Indice da tabela indicada.
	dbSelectArea( 'SIX' )
	dbSetOrder(1)
	dbSeek( cAlias )
	While !Eof() .AND. SIX->INDICE == cAlias
		If Alltrim( SIX->CHAVE ) <> 'T4_FILIAL+T4_TIPOMAN'
			aAdd( aOrdens, Alltrim( SIX->DESCRICAO ) )
			dbSelectArea( 'SIX' )
		EndIf
		dbSkip()
	End

	//Monta tela de pesquisa
	Define MsDialog oDlgPesq Title STR0006 From 00,00 To 100,500 PIXEL //"Pesquisa"

	@ 005, 005 ComboBox oOrdem Var cOrdem Items aOrdens Size 210,08 PIXEL OF oDlgPesq ON CHANGE nOrdem := oOrdem:nAt
	@ 020, 005 MsGet oChave Var cChave Size 210,08 of oDlgPesq PIXEL

	Define sButton oBtOk  from 05,218 Type 1 Action ( nOpcA := 1, oDlgPesq:End() ) Enable of oDlgPesq PIXEL
	Define sButton oBtCan from 20,218 Type 2 Action ( nOpcA := 0, oDlgPesq:End() ) Enable of oDlgPesq PIXEL
	Define sButton oBtPar from 35,218 Type 5 When .F. of oDlgPesq pixel

	Activate MsDialog oDlgPesq Center

	IIf( nOrdem == 4, nOrdem := 5, nOrdem)

	//Se confirma verifica a existencia da chave na tabela
	If nOpca == 1
		dbSelectArea( cAlias )
		Set Filter To ST4->T4_TIPOMAN == M->TE_TIPOMAN
		dbSetOrder( nOrdem )
		If dbSeek(xFilial( cAlias ) + Alltrim( cChave ) ) .OR. dbSeek( Alltrim( cChave ) )
			dbSelectArea( 'TRB4' ) //TRB
			dbGoTop()
			//Posiciona na tabela temporaria
			dbSeek( TRB4->(ST4->T4_SERVICO) )
		EndIf
		If !Found()
			Help(" ",1,"PESQ01")
		EndIf
	EndIf

	Set Filter To

Return .T.

