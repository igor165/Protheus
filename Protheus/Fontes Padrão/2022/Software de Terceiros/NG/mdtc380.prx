#INCLUDE "MDTC380.ch"
#Include "Protheus.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTC380  ³ Autor ³ Rafael Diogo Richter  ³ Data ³06/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta Geral de Questionario de Investigacao de Acidentes³±±
±±³			 ³ de Trabalho e Grafico									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION MDTC380()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Guarda conteudo e declara variaveis padroes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM( )

Private nSizeTMH := (TAMSX3("TMH_PERGUN")[1])
nSizeTMH := If( nSizeTMH > 0 , nSizeTMH , 60 )

lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .T. , .F. )
SetKey( VK_F9, { | | NGVersao( "MDTC380" , 1 ) } )
cCadastro := ""

PRIVATE aRotina := MenuDef()

If lSigaMdtps
	cCadastro := OemtoAnsi(STR0021)  //"Clientes"
	DbSelectArea("SA1")
	DbSetOrder(1)
	mBrowse( 6, 1,22,75,"SA1")
Else

	cCadastro := OemToAnsi(STR0022) //"Questionário"
	mBrowse( 6, 1,22,75,"TMG")

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Recupera a Ordem Original do arquivo principal               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("TMG")
	dbSetOrder(1)

Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna conteudo de variaveis padroes       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)
Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC380QUE ³ Autor ³ Rafael Diogo Richter	| Data ³07/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse com as perguntas do questionario           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC380                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDC380QUE()

	Local OldRot
	Local oTempTRBH
	Local oTempTRB

	Private cTRBTMH   := GetNextAlias()
	Private cTRBQuest := GetNextAlias()

	Private aVetinr := {}

	lTRBH := .T.
	MV_PAR01 := ctod(' /  /  ')
	MV_PAR02 := ctod(' /  /  ')

	OldRot := aCLONE(aROTINA)

	aRotina := { { STR0002  ,"NgC380VIS"   , 0 , 2}} //"Visualizar"

	lInverte:= .F.
	cMARCA  := GetMark()
	lQuery := .T.

	dbSelectArea("TMH")

	aDBF1 := {}
	AADD(aDBF1,{ "TMH_OK"      , "C" ,02, 0 })
	AADD(aDBF1,{ "TMH_QUESTI"  , "C" ,06, 0 })
	AADD(aDBF1,{ "TMH_NOMQUE"  , "C" ,20, 0 })
	AADD(aDBF1,{ "TMH_QUESTA"  , "C" ,03, 0 })
	AADD(aDBF1,{ "TMH_PERGUN"  , "C" ,nSizeTMH, 0 })

	aTRBH := {}
	AADD(aTRBH,{ "TMH_OK"      , NIL , " "      ,})
	AADD(aTRBH,{ "TMH_QUESTA"  , NIL , STR0005  ,})  //"Pergunta"
	AADD(aTRBH,{ "TMH_PERGUN"  , NIL , STR0006  ,})  //"Descricao Pergunta"

	//Cria TRB
	oTempTRBH := FWTemporaryTable():New( cTRBTMH, aDBF1 )
	oTempTRBH:AddIndex( "1", {"TMH_QUESTI","TMH_QUESTA"} )
	oTempTRBH:Create()

	If lSigaMdtps

		dbSelectArea("TMH")
		dbsetorder(2)  //TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI+TMH_QUESTA
		dbseek(xFilial("TMH")+cCliMdtps+TMG->TMG_QUESTI)
		While !eof()                              .and.;
			TMH->TMH_filial  == xFilial("TMH")  .and.;
			TMH->(TMH_CLIENT+TMH_LOJA) == cCliMdtps .and.;
			TMH->TMH_QUESTI  == TMG->TMG_QUESTI

				(cTRBTMH)->(DbAppend())
				(cTRBTMH)->TMH_OK      := If(Empty((cTRBTMH)->TMH_OK), "  ", cMARCA)
				(cTRBTMH)->TMH_QUESTI  := TMH->TMH_QUESTI
				(cTRBTMH)->TMH_NOMQUE  := SUBSTR(TMG->TMG_NOMQUE,1,20)
				(cTRBTMH)->TMH_QUESTA  := TMH->TMH_QUESTA
				(cTRBTMH)->TMH_PERGUN  := TMH->TMH_PERGUN

			dbSelectArea("TMH")
			DBSKIP()
		EndDo

		dbSelectArea(cTRBTMH)
		dbGoTop()
		MARKBROW( cTRBTMH, "TMH_OK",,aTRBH,lInverte, cMarca, "C380Invert()")

		lTEMMARK := .F.
		dbSelectArea(cTRBTMH)
		DbGoTop()

		Do While !EOF()
			If !Empty((cTRBTMH)->TMH_OK)
				lTEMMARK := .T.
				Exit
			Endif
			DbSelectArea(cTRBTMH)
			Dbskip(1)
		EndDo

		If !lTEMMARK
			oTempTRBH:Delete()
			Return nil
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Variaveis utilizadas para parametros                                 ³
		//³ mv_ch1     // De  Data                                               ³
		//³ mv_ch2     // Ate Data                                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Pergunte("MDT340",.T.)
			dbSelectArea(cTRBTMH)
			USE
			aRotina := aCLONE(OldRot)
			oTempTRBH:Delete()
			Return
		Endif

		aDBF2 := { {"QUESTA"  , "C", 03       , 0 } , ;
				   {"PERGUN"  , "C", nSizeTMH , 0 } , ;
				   {"SIM"     , "N", 10       , 0 } , ;
				   {"NAO"     , "N", 10       , 0 }   }

		oTempTRB := FWTemporaryTable():New( cTRBQuest, aDBF2 )
		oTempTRB:AddIndex( "1", {"QUESTA"} )
		oTempTRB:Create()

		DbSelectArea(cTRBTMH)
		DbGoTop()
		Do While !EOF()

			If !Empty((cTRBTMH)->TMH_OK)

				DbSelectArea("TO6")
				Dbsetorder(2)  //TO6_FILIAL+TO6_CLIENT+TO6_LOJA+TO6_QUESTI+TO6_QUESTA
				If Dbseek(xFilial('TO6')+cCliMdtps+(cTRBTMH)->TMH_QUESTI)

					Do While !eof() .and. TO6->TO6_FILIAL == xFilial("TO6")  .and.;
						TO6->TO6_QUESTI == (cTRBTMH)->TMH_QUESTI .and. TO6->(TO6_CLIENT+TO6_LOJA) = cCliMdtps

						If TO6->TO6_DTREAL >= MV_PAR01 .and. TO6->TO6_DTREAL <= MV_PAR02;
								.and.TO6->TO6_QUESTA = (cTRBTMH)->TMH_QUESTA

							dbSelectArea( cTRBQuest )
							If !dbSeek(TO6->TO6_QUESTA)
								( cTRBQuest )->(DbAppend())
								Replace ( cTRBQuest )->QUESTA with TO6->TO6_QUESTA
								Replace ( cTRBQuest )->PERGUN with (cTRBTMH)->TMH_PERGUN
							EndIf

							If TO6->TO6_RESPOS = '1'
								Replace ( cTRBQuest )->SIM with ( cTRBQuest )->SIM + 1
							Else
								Replace ( cTRBQuest )->NAO with ( cTRBQuest )->NAO + 1
							Endif

						EndIf
						DbSelectArea("TO6")
						Dbskip(1)

					EndDo

				EndIf

			EndIf
			DbSelectArea(cTRBTMH)
			Dbskip(1)

		EndDo

	Else

		dbSelectArea("TMH")
		dbsetorder(1)
		dbseek(xFilial("TMH")+TMG->TMG_QUESTI)
		While !eof()                              .and.;
			TMH->TMH_filial  == xFilial("TMH")  .and.;
			TMH->TMH_QUESTI  == TMG->TMG_QUESTI

				(cTRBTMH)->(DbAppend())
				(cTRBTMH)->TMH_OK      := If(Empty((cTRBTMH)->TMH_OK), "  ", cMARCA)
				(cTRBTMH)->TMH_QUESTI  := TMH->TMH_QUESTI
				(cTRBTMH)->TMH_NOMQUE  := SUBSTR(TMG->TMG_NOMQUE,1,20)
				(cTRBTMH)->TMH_QUESTA  := TMH->TMH_QUESTA
				(cTRBTMH)->TMH_PERGUN  := TMH->TMH_PERGUN

			dbSelectArea("TMH")
			DBSKIP()
		EndDo

		dbSelectArea(cTRBTMH)
		dbGoTop()
		MARKBROW(cTRBTMH,"TMH_OK",,aTRBH,lInverte, cMarca, "C380Invert()")

		lTEMMARK := .F.
		dbSelectArea(cTRBTMH)
		DbGoTop()

		Do While !EOF()
			If !Empty((cTRBTMH)->TMH_OK)
				lTEMMARK := .T.
				Exit
			EndIf
			DbSelectArea(cTRBTMH)
			Dbskip(1)
		EndDo

		If !lTEMMARK
			oTempTRBH:Delete()
			aROTINA := aCLONE(OldRot)
			Return
		EndIf

		// Variaveis utilizadas para parametros
		// mv_ch1     // De  Data
		// mv_ch2     // Ate Data

		If !Pergunte("MDT340",.T.)
			oTempTRBH:Delete()
			aRotina := aCLONE(OldRot)
			return
		Endif

		aDBF2 := { {"QUESTA"  , "C", 03       , 0 } , ;
				   {"PERGUN"  , "C", nSizeTMH , 0 } , ;
				   {"SIM"     , "N", 10       , 0 } , ;
				   {"NAO"     , "N", 10       , 0 }   }

		oTempTRB := FWTemporaryTable():New( cTRBQuest , aDBF2 )
		oTempTRB:AddIndex( "1", {"QUESTA"} )
		oTempTRB:Create()

		DbSelectArea(cTRBTMH)
		DbGoTop()
		Do While !EOF()

			If !Empty((cTRBTMH)->TMH_OK)

				DbSelectArea("TO6")
				Dbsetorder(2)
				If Dbseek(xFilial('TO6')+(cTRBTMH)->TMH_QUESTI)

					Do While !eof() .and. TO6->TO6_FILIAL == xFilial("TO6")  .and.;
						TO6->TO6_QUESTI == (cTRBTMH)->TMH_QUESTI

						If TO6->TO6_DTREAL >= MV_PAR01 .and. TO6->TO6_DTREAL <= MV_PAR02;
								.and.TO6->TO6_QUESTA = (cTRBTMH)->TMH_QUESTA

							dbSelectArea( cTRBQuest )
							If !dbSeek(TO6->TO6_QUESTA)
								( cTRBQuest )->(DbAppend())
								Replace ( cTRBQuest )->QUESTA with TO6->TO6_QUESTA
								Replace ( cTRBQuest )->PERGUN with (cTRBTMH)->TMH_PERGUN
							Endif

							If TO6->TO6_RESPOS = '1'
								Replace ( cTRBQuest )->SIM with ( cTRBQuest )->SIM + 1
							Else
								Replace ( cTRBQuest )->NAO with ( cTRBQuest )->NAO + 1
							EndIf

						EndIf
						DbSelectArea("TO6")
						Dbskip(1)

					EndDo

				Endif

			Endif
			DbSelectArea(cTRBTMH)
			Dbskip(1)

		EndDo

	Endif

	DbselectArea( cTRBQuest )
	Dbgotop()
	cTitulo := STR0007+Alltrim(TMG->TMG_NOMQUE)+' ( '+dtoc(mv_par01)+' a '+dtoc(mv_par02)+' )'  //'Questionario - '

	DEFINE DIALOG oDlg1 FROM 6,0 TO 34,100 TITLE cTitulo

	@ 04,020 BUTTON STR0008 OF oDlg1 SIZE 40,09 PIXEL ACTION (MDC380GRA())  //"&Grafico"
	@ 04,060 BUTTON STR0009 OF oDlg1 SIZE 40,09 PIXEL ACTION (MDC380IMP())  //"&Imprimir"

	@ 01,0.3 LISTBOX FIELDS QUESTA                                ,;
							PERGUN                                ,;
							PADL(Transform(SIM,"@E 999,999"),12)  ,;
							PADL(Transform(NAO,"@E 999,999"),12)   ;
						FieldSizes 40,200,70,70                    ;
						Size 388,195                                ;
						HEADERS STR0005                        ,;  //"Pergunta"
								STR0010                   ,;  //"Nome Pergunta"
								STR0011                             ,;  //"Sim"
								STR0012 //"Nao"

	ACTIVATE DIALOG oDlg1

	oTempTRBH:Delete()
	oTempTRB:Delete()

	aROTINA := aCLONE(OldRot)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC380GRA ³ Autor ³ Rafael Diogo Richter  ³ Data ³07/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta o Grafico das perguntas                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC380                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDC380GRA()

	Local oTempTable

	Private cTRBGraf := GetNextAlias()

	DbselectArea( cTRBQuest )
	Dbgotop()

	if reccount() == 0

	#IFDEF WINDOWS
		NDEL := Help(" ",1,"NGTDADGRAF")
		return .T.
	#ENDIF

	endif

	aDBF3 := { {"CODIGO" , "C", 10,0} ,;
			{"NOMECOD", "C", If(nSizeTMH>110,110,nSizeTMH),0} ,; //O Grafico nao suporta mais do que 110 caracteres
			{"VALOR1" , "N", 12,2} ,;
			{"VALOR2" , "N", 12,2}}

	oTempTable := FWTemporaryTable():New( cTRBGraf, aDBF3 )
	oTempTable:AddIndex( "1", {"CODIGO"} )
	oTempTable:Create()

	DbselectArea( cTRBQuest )
	Dbgotop()

	Do While !eof()

	( cTRBGraf )->(DbAppend())
	( cTRBGraf )->CODIGO  := ( cTRBQuest )->QUESTA
	( cTRBGraf )->NOMECOD := ( cTRBQuest )->PERGUN
	( cTRBGraf )->VALOR1  := ( cTRBQuest )->SIM
	( cTRBGraf )->VALOR2  := ( cTRBQuest )->NAO

	DbselectArea( cTRBQuest )
	Dbskip()

	EndDo

	// 1§ linha titulo do grafico (janela)
	// 2§ linha titulo da direita do grafico
	// 3§ linha titulo superior do grafico
	// 4§ linha titulo da direita do grafico
	// 5§ linha titulo da inferior do grafico
	// 6§ linha series do grafico
	// 7§ leitura ("A" - Arquivo temporario,"M" - Matriz)
	// 8§ alias doa arquivo temporario com os dados /ou
	// 9§ matriz com os dados

	vCRIGTXT := NGGRAFICO(" "+STR0022," ",STR0004,STR0004,STR0004,;
						{STR0011,STR0012},"A",( cTRBGraf )) //"Sim"###"Nao"

	dbSelectArea( cTRBGraf )
	Use

	DbselectArea( cTRBQuest )
	Dbgotop()

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NGC380VIS ³ Autor ³ Rafael Diogo Richter  ³ Data ³07/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Posiciona no arquivo correspondente                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC380                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function NgC380VIS()

If lSigaMdtps

	dbSelectArea("TMH")
	dbsetorder(2)  //TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI+TMH_QUESTA
	dbseek(xFilial("TMH")+cCliMdtps+(cTRBTMH)->TMH_QUESTI+(cTRBTMH)->TMH_QUESTA)
	NGCAD01('TMH', RECNO(),1)
	dbSelectArea(cTRBTMH)

Else

	dbSelectArea("TMH")
	dbsetorder(1)
	dbseek(xFilial("TMH")+(cTRBTMH)->TMH_QUESTI+(cTRBTMH)->TMH_QUESTA)
	NGCAD01('TMH', RECNO(),1)
	dbSelectArea(cTRBTMH)

Endif

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDC349IMP ³ Autor ³ Inacio Luiz Kolling   ³ Data ³11/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao do Questionario medico do periodo                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC910                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDC380IMP()

	Local cString    := cTRBQuest
	Local cDesc2     := ""
	Local cDesc3     := ""
	Local wnrel      := "MDTC380"

	Private cDesc1   := cTitulo
	Private aReturn  := { STR0014, 1,STR0015, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := cDesc1
	Private Tamanho  := "M"
	Private cPerg    := "MDT380"

	If nSizeTMH > 100
		Tamanho  := "G"
	Endif

	if reccount() = 0
		NDEL := Help(" ",1,"NGTDADIMP")
		return .T.
	endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel:=SetPrint(cString,wnrel,,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
	Set Filter To
	Return
	Endif

	SetDefault(aReturn,cString)

	If nLastKey = 27
	Set Filter To
	Return
	Endif

	RptStatus({|lEnd| C380Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return NIL
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ C380Imp  ³ Autor ³ Rafael Diogo Richter  ³ Data ³07/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC380                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function C380Imp(lEnd,wnRel,titulo,tamanho)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Variaveis                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local cRodaTxt := ""
	Local nCntImpr := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis para controle do cursor de progressao do relatorio ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis locais exclusivas deste programa                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contadores de linha e pagina                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private li := 80

	m_pag := 1

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se deve comprimir ou nao                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTipo  := IIF(aReturn[4]==1,15,18)

	Private Cabec1       := STR0023 //"Perg. Descrição da Pergunta                                                                                       Sim       Não"
	Private Cabec2       := ' '
	Private nomeprog     := "MDTC380"

	If nSizeTMH > 100
		Cabec1   := STR0024 //"Pergunta   Descrição da Pergunta                                                                                                                                                                       Sim       Não"
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define o indice de leitura do arquivo de Bens            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea( cTRBQuest )
	DbGoTop()
	SetRegua(LastRec())

	nSIM := 0
	nNAO := 0

	Do While !( cTRBQuest )->(Eof())
		IncRegua()

	If lEnd
		@ PROW()+1,001 PSay STR0016 //"CANCELADO PELO OPERADOR"
		Exit
	EndIf

	/*
	TAMANHO M
	_____________________________________________________________________________________________________________________________________
			1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	_____________________________________________________________________________________________________________________________________
	Perg. Descrição da Pergunta                                                                                       Sim       Não
	_____________________________________________________________________________________________________________________________________
	Questionário:  XXXXXX - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXX   1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890  9.999.999 9.999.999
																									TOTAL    9.999.999 9.999.999

	TAMANHO G
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________
			1         2         3         4         5         6         7         8         9       100       110       120       130       140       150       160       170       180       190       200       210       220
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________
	Pergunta   Descrição da Pergunta                                                                                                                                                                       Sim       Não
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________
	Questionário:  XXXXXX - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXX        123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890  9.999.999 9.999.999
																																															TOTAL    9.999.999 9.999.999
	*/

		SomaLi380()
		@li,000 Psay ( cTRBQuest )->QUESTA PICTURE '@!'
		If nSizeTMH <= 100
		@li,006 Psay SubStr(( cTRBQuest )->PERGUN,1,100) PICTURE '@!'
		@li,108 Psay ( cTRBQuest )->SIM    PICTURE '@E 9,999,999'
		@li,118 Psay ( cTRBQuest )->NAO    PICTURE '@E 9,999,999'
		Else
		@li,011 Psay SubStr(( cTRBQuest )->PERGUN,1,180) PICTURE '@!'
		@li,193 Psay ( cTRBQuest )->SIM    PICTURE '@E 9,999,999'
		@li,203 Psay ( cTRBQuest )->NAO    PICTURE '@E 9,999,999'
		Endif

		nSIM := nSIM + ( cTRBQuest )->SIM
		nNAO := nNAO + ( cTRBQuest )->NAO

		( cTRBQuest )->(DBSKIP())
	enddo

	If nSIM > 0 .or. nNAO > 0

		SomaLi380()
		SomaLi380()

		If nSizeTMH <= 100
			@li,099 Psay STR0017 //'TOTAL'
			@li,108 Psay nSIM    PICTURE '@E 9,999,999'
			@li,118 Psay nNAO    PICTURE '@E 9,999,999'
		Else
			@li,184 Psay STR0017 //'TOTAL'
			@li,193 Psay nSIM    PICTURE '@E 9,999,999'
			@li,203 Psay nNAO    PICTURE '@E 9,999,999'
		Endif

	Endif

	Roda(nCntImpr,cRodaTxt,Tamanho)
	Set Filter To
	Set device to Screen

	If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
	Endif
	MS_FLUSH()
	DBgotop()

Return NIL

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLi380³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTC380                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
static Function Somali380()

	Li++
	If Li > 58
		Cabec(cTitulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	EndIf

return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A380INVERT³ Autor ³ Paulo Pego            ³ Data ³ 09/11/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Inverte marcacoes - Windows                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA380                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION c380INVERT()

	LOCAL nREG := (cTRBTMH)->(RECNO())

	DBSELECTAREA(cTRBTMH)
	DBGOTOP()
	DO WHILE !EOF()
			(cTRBTMH)->TMH_OK := IIF(TMH_OK == "  ",cMARCA,"  ")
			DBSKIP()
	ENDDO

	(cTRBTMH)->(DBGOTO(nREG))
	lREFRESH := .T.

RETURN NIL


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMDT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³	  1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

	Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .T. , .F. )
	Local aRotina

	If lSigaMdtps
		aRotina := { { STR0001,   "AxPesqui"  , 0 , 1},; //"Pesquisar"
					{ STR0002,   "NGCAD01"   , 0 , 2},; //"Visualizar"
					{ STR0022,   "MDT380TMG" , 0 , 4} } //"Questionários"
	Else
		aRotina := { { STR0001    ,"AxPesqui"  , 0 , 1},;  //"Pesquisar"
					{ STR0002    ,"NGCAD01"  , 0 , 2},;  //"Visualizar"
					{ STR0003    ,"MDC380QUE" , 0 , 3} }  //"Questoes"
	Endif

Return aRotina

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDT090TAR  ³ Autor ³ Andre Perez Alvarez     ³ Data ³21/02/08  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta um browse com as questoes do cliente.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³  SIGAMDT                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function MDT380TMG()

	Local aArea	:= GetArea()
	Local oldROTINA := aCLONE(aROTINA)
	Local oldCad := cCadastro
	cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

	aRotina := { { STR0001, "AxPesqui" , 0 , 1},;  //"Pesquisar"
				{ STR0002, "NGCAD01" , 0 , 2},;  //"Visualizar"
				{ STR0003, "MDC380QUE", 0 , 3} }  //"Questoes"

	cCadastro := OemToAnsi(STR0022) //"Questionários"

	dbSelectArea("TMG")
	Set Filter To TMG->(TMG_CLIENT+TMG_LOJA) = cCliMdtps
	dbSetOrder(2)  //TMG_FILIAL+TMG_CLIENT+TMG_LOJA+TMG_QUESTI
	mBrowse( 6, 1,22,75,"TMG")

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Recupera a Ordem Original do arquivo principal               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("TMG")
	dbSetOrder(1)
	Set Filter To

	aROTINA := aCLONE(oldROTINA)
	RestArea(aArea)
	cCadastro := oldCad

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTC380Q     ³ Autor ³Andre E. Perez Alvarez ³ Data ³04/09/08  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mostra o nome do questionario                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MDTC380Q                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lTRBH   -> .T. - Usado o TRBH no programa MDTC380              ³±±
±±³          ³         -> .F. - o contrario                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ cDesc - Descricao da funcao                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MDTC380Q                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function MDTC380Q()

	Local cDesc
	Local aArea := GetArea()
	Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .T. , .F. )
	Local __lTRBH := If(Type("lTRBH")=="L",lTRBH,.F.)

	If lSigaMdtps
		If __lTRBH
			cDesc := Posicione( "TMG", 2, xFilial("TMG") + cCliMdtPs + (cTRBTMH)->TMH_QUESTI, "TMG_NOMQUE")
		Else
			cDesc := Posicione( "TMG", 2, xFilial("TMG") + cCliMdtPs + TMH->TMH_QUESTI, "TMG_NOMQUE")
		Endif
	Else
		If __lTRBH
			cDesc := Posicione( "TMG", 1, xFilial("TMG") + (cTRBTMH)->TMH_QUESTI, "TMG_NOMQUE")
		Else
			cDesc := Posicione( "TMG", 1, xFilial("TMG") + TMH->TMH_QUESTI, "TMG_NOMQUE")
		Endif
	Endif

	RestArea(aArea)

Return cDesc
