#INCLUDE "mntr660.ch"
#INCLUDE "PROTHEUS.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR660
description
@author  NG Informatica
@since   06/97
@version P12
/*/
//-------------------------------------------------------------------
Function MNTR660()

	Local cString    := "STJ"
	Local cDesc1     := STR0002 //"Relatorio de apresentacao das Ordens de Servico de manutencao penden- "
	Local cDesc2     := STR0003 //"tes. Atraves dos parametros o usuario podera efetuar a selecao deseja-"
	Local cDesc3     := STR0004 //"da."
	Local wnrel      :="MNTR660"

	Private aReturn  := { STR0005, 1,STR0006, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private titulo   := STR0001 //"O.S. de Manutencao Pendentes"
	Private Tamanho  := "M"
	Private cPerg    := "MNT660"

	//Variaveis utilizadas para parametros
	//mv_par01 - De Plano de Manutencao
	//mv_par02 - At‚ Plano de Manuten‡Æo
	//mv_par03 - Dias de atraso de selecao
	//mv_par04 - De  Centro de Custo
	//mv_par05 - Ate Centro de Custos
	//mv_par06 - De  Centro de Trabalho
	//mv_par07 - Ate Centro de Trabalho
	//mv_par08 - De  Familia de Bens
	//mv_par09 - Ate Familia de Bens
	//mv_par10 - De  Servico
	//mv_par11 - Ate Servico
	//mv_par12 - De  Bem
	//mv_par13 - Ate Bem
	//mv_par14 - Imprimir Descricao?    Nao/Sim
	Pergunte(cPerg,.F.)

	//Envia controle para a funcao SETPRINT
	wnrel := SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

	If nLastKey = 27
		Set Filter To
		Return
	EndIf
	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R660Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R660Imp  ³ Autor ³ NG Informatica Ltda.  ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR660                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R660Imp(lEnd,wnRel,titulo,tamanho)

Local cRodaTxt := ""
Local nCntImpr := 0
Local cCondicao
Local nTotRegs         := 0 ,nMult := 1 ,nPosAnt := 4 ,nPosAtu := 4 ,nPosCnt := 0
Local cChave           := Space(16)
Local lContinua        := .T.
Local cErroProduto     := Space(03)
Local cErroFuncionario := Space(03)
Local cErroFerramenta  := Space(03)
Local cErroBem         := Space(03)
Local nContador        := 0
Local cQuebra          := Space(09)

Private cabec1,cabec2
Private ntipo    := 0
Private nomeprog := "MNTR660"

cCONDSTJ := 'stj->tj_situaca = "L" .And. stj->tj_termino = "N" .And. '
cCONDSTJ := cCONDSTJ + '(STJ->TJ_CCUSTO >= MV_PAR04 .And. STJ->TJ_CCUSTO <= MV_PAR05)  .And.'
cCONDSTJ := cCONDSTJ + '(STJ->TJ_CENTRAB >= MV_PAR06 .And. STJ->TJ_CENTRAB <= MV_PAR07).And.'
cCONDSTJ := cCONDSTJ + '(STJ->TJ_SERVICO >= MV_PAR10 .And. STJ->TJ_SERVICO <= MV_PAR11)'
If MV_PAR15 = 1
	cCONDSTJ := cCONDSTJ + '.And. (STJ->TJ_CODBEM  >= MV_PAR12 .And. STJ->TJ_CODBEM <= MV_PAR13)'
	cCONDSTJ := cCONDSTJ + '.And. STJ->TJ_TIPOOS = "B"'
ElseIf MV_PAR15 = 2
	cCONDSTJ := cCONDSTJ + '.And. STJ->TJ_TIPOOS = "L"'
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private li := 80 ,m_pag := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se deve comprimir ou nao                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTipo  := IIF(aReturn[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta os Cabecalhos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cabec1 := STR0007 //"O.S.   Prior. Bem              Nome                                    Manut. Descricao                            Seq. Area   Atraso"
cabec2 := STR0008 //"           Pai              Nome                         .....Inicio..... .......Fim...... Ult.Man.          Cont. Prod Func Ferr Bem"

//          1         2         3         4         5         6         7         8         9         0         1         2         3
//0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
//______________________________________________________________________________________________________________________________________
//O.S.  Prior. Bem              Nome                                     Manut. Descricao                            Seq.  Area    Atraso
//             Pai              Nome                     ......Inicio.....  ......Fim........   Ult.Man.         Cont. Prod Func Ferr Bem
//______________________________________________________________________________________________________________________________________
//xxxxxx xxx   xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999 xxxxxx    999999
//             xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxx dd/mm/aaaa hh:mm dd/mm/aaaa hh:mm  dd/mm/aaaa 999.999.999 Sim  Nao  Sim  Nao
//
// Descricao: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
//            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

DbSelectArea("STJ")
DbSetOrder(05)
DbSeek(xFilial("STJ")+MV_PAR01,.T.)
SetRegua(LastRec())
While !Eof()                        .And.;
	STJ->TJ_FILIAL == xFilial("STJ") .And.;
	STJ->TJ_PLANO >= MV_PAR01        .And.;
	STJ->TJ_PLANO <= MV_PAR02

	If lEnd
		@ PROW()+1,001 Psay STR0009 //"CANCELADO PELO OPERADOR"
		Exit
	EndIf
	IncRegua()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Selecao dos registros conforme os parametros             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If mv_par15 == 3 .And. STJ->TJ_TIPOOS = "B"
		If STJ->TJ_CODBEM  <= MV_PAR12 .Or. STJ->TJ_CODBEM >= MV_PAR13
			DbSelectArea("STJ")
			DbSkip()
			Loop
		EndIf
	EndIf

	If &(cCONDSTJ)	//FILTRA AS CONDICOES DOS PARAMETROS
		DbSelectArea("ST9")
		DbSetOrder(01)
		DbSeek(xFilial("ST9")+STJ->TJ_CODBEM)
		If ST9->T9_CODFAMI < MV_PAR08 .Or. ST9->T9_CODFAMI > MV_PAR09
			DbSelectArea("STJ")
			Dbskip()
			Loop
		EndIf

		// seleciona OS por atraso
		DbSelectArea("STJ")
		nAtraso = dDataBase - STJ->TJ_DTMPINI

		If mv_par03 != 0
			If nAtraso < 0 .Or. nAtraso < MV_PAR03
				DbSkip()
				Loop
			EndIf
		EndIf

		If Li > 58
			Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Controle de quebra de pagina                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		If Empty(cQuebra)
			cQuebra = STJ->TJ_CCUSTO
			Quebra(cQuebra)
		Else
			If cQuebra != STJ->TJ_CCUSTO
				cQuebra = STJ->TJ_CCUSTO
				li = 59
				Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
				Quebra(cQuebra)
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao da Primeira Linha da O.S.                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		DbSelectArea("STF")
		DbSeek(xFilial("STF")+STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA)
		DbSelectArea("ST4")
		DbSeek(xFilial("ST4")+STJ->TJ_SERVICO)
		DbSelectArea("STJ")

		@ Li,000 Psay STJ->TJ_ORDEM
		@ Li,009 Psay STJ->TJ_PRIORID
		@ Li,013 Psay STJ->TJ_CODBEM
		If STJ->TJ_TIPOOS = "B"
			@ Li,030 Psay Substr(ST9->T9_NOME,1,32)
		Else
			dbSelectArea("TAF")
			dbSetOrder(2)
			dbSeek(xFilial("TAF")+"001"+AllTrim(STJ->TJ_CODBEM))
			@ Li,030 Psay Substr(TAF->TAF_NOMNIV,1,32)
		EndIf
		@ Li,068 Psay STJ->TJ_SERVICO
		@ Li,075 Psay Substr(ST4->T4_NOME,1,36)
		@ Li,115 Psay STJ->TJ_SEQRELA Picture "@!"
		@ Li,118 Psay STF->TF_CODAREA
		@ Li,126 Psay nAtraso Picture "999999"
		Li++

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o Bem tem Bem Pai                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		DbSelectArea("STC")
		DbSetOrder(03)
		cChave := STJ->TJ_CODBEM
		While .T.
			DbSeek(xFilial("STC")+cChave)
			If Found()
				cChave := STC->TC_CODBEM
				Dbskip()
				Loop
			EndIf
			Exit
		End

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se a O.S. teve problema                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		DbSelectArea("STA")
		DbSetOrder(02)
		If DbSeek(xFilial("STA")+STJ->TJ_ORDEM+STJ->TJ_PLANO+"P")
			cErroProduto = STR0010 //"Sim"
		Else
			cErroProduto = STR0011 //"Nao"
		EndIf

		If DbSeek(xFilial("STJ")+STJ->TJ_ORDEM+STJ->TJ_PLANO+"M")
			cErroFuncionario = STR0010 //"Sim"
		Else
			cErroFuncionario = STR0011 //"Nao"
		EndIf

		If DbSeek(xFilial("STJ")+STJ->TJ_ORDEM+STJ->TJ_PLANO+"F")
			cErroFerramenta = STR0010 //"Sim"
		Else
			cErroFerramenta = STR0011 //"Nao"
		EndIf

		If DbSeek(xFilial("STJ")+STJ->TJ_ORDEM+STJ->TJ_PLANO+"B")
			cErroBem = STR0010 //"Sim"
		Else
			cErroBem = STR0011 //"Nao"
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao da Segunda  Linha da O.S.                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		nContador = ST9->T9_POSCONT
		If STJ->TJ_CODBEM != cChave
			DbSelectArea("ST9")
			DbSetorder(01)
			DbSeek(xFilial("ST9")+cChave)
			@ Li,013 Psay cChave
			@ Li,030 Psay Substr(ST9->T9_NOME,1,22)
		EndIf

		DbSelectArea("STJ")
		DbSetorder(05)
		@ Li,053 Psay STJ->TJ_DTMPINI Picture "99/99/9999"
		@ Li,064 Psay STJ->TJ_HOMPINI Picture "99:99"
		@ Li,072 Psay STJ->TJ_DTMPFIM Picture "99/99/9999"
		@ Li,084 Psay STJ->TJ_HOMPFIM Picture "99:99"
		@ Li,092 Psay STF->TF_DTULTMA Picture "99/99/9999"
		@ Li,102 Psay nContador Picture "@E 999,999,999"
		@ Li,114 Psay Substr(cErroProduto,1,3)
		@ Li,119 Psay Substr(cErroFuncionario,1,3)
		@ Li,124 Psay Substr(cErroFerramenta,1,3)
		@ Li,129 Psay Substr(cErroBem,1,3)
		Li++
		If NGCADICBASE("TJ_MMSYP","A","STJ",.F.)
			cObs := NGMEMOSYP(STJ->TJ_MMSYP)
		Else
			cObs := STJ->TJ_OBSERVA
		EndIf
		If MV_PAR14 = 2 .And. !Empty(cObs)
			NG2IMPMEMO(cObs,100,000,STR0014,.F.,.F.)//"Descricao: "
		EndIf
	EndIf
	DbSelectArea("STJ")
	DbSkip()
End
Roda(nCntImpr,cRodaTxt,Tamanho)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("STJ")
RetIndex("STJ")
RetIndex("ST9")
RetIndex("STF")
RetIndex("STA")
RetIndex("ST4")
RetIndex("STC")
RetIndex("SI3")
Set Filter To

Set device to Screen
If aReturn[5] = 1
   Set Printer To
   dbCommitAll()
   OurSpool(wnrel)
EndIf
MS_FLUSH()
Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Quebra   ³ Autor ³ NG Informatica Ltdaa  ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Impressao do Cabecalho de Quebra                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ QUEBRA                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Quebra(cQuebra)

DbSelectArea("SI3")
DbSeek(xFilial("SI3")+STJ->TJ_CCUSTO)
@ Li,000 Psay STR0012 //"Bens Do Plano.: "
@ Li,016 Psay STJ->TJ_PLANO
@ Li,028 Psay STR0013 //"Centro De Custos: "
@ Li,046 Psay cQuebra
@ Li,069 Psay SI3->I3_DESC
Li++
@ Li,000 Psay "************************************************************************************************************************************"
Li++
DbSelectArea("STJ")
Return .T.