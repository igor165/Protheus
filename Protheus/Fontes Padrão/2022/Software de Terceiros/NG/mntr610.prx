#INCLUDE "Mntr610.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 1 //Versao do fonte
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR610  ³ Autor ³ NG Informatica        ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Bens da Manutencao                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR610
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)
Local cString    := "STF"
Local cDesc1     := STR0000 //"Relatorio dos bens cadastrados para o servico de manutencao. Para cada"
Local cDesc2     := STR0001 //"servico considerado, serao mostrados os bens com o servico planejado. "
Local cDesc3     := STR0002 //"A selecao podera ser feita por Centro de Custo e Centro de Trabalho."
Local wnrel      := "MNTR610"
Private aReturn  := {STR0003,1,STR0004,2,2,1,"",1} //"Zebrado"###"Administracao"
Private nLastKey := 0
Private cPerg    := "MNT610"
Private Tamanho  := "M"
Private titulo   := STR0005 //"Bens do Servico"

If !NGFUNCRPO("NGCANCELAIMP",,"NGUTIL.PRX") .Or. !NGFUNCRPO("NGIMTAETOPIN",,"NGUTIL05.PRX")
  Return
EndIf  

/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
  ³ Variaveis utilizadas para parametros ³
  ³ mv_par01  De  Servico                ³
  ³ mv_par02  Ate Servico                ³
  ³ mv_par03  De  Centro de Custos       ³
  ³ mv_par04  Ate Centro de Custos       ³
  ³ mv_par05  De  Centro Trabalho        ³
  ³ mv_par06  Ate Centro Trabalho        ³
  ³ mv_par07  Apresenta Etapas sim/nao   ³
  ³ mv_par08  Apresenta Insumos sim/nao  ³
  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ */
pergunte(cPerg,.F.)

wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

If NGCANCELAIMP(nLastKey,"STO")
   Return
EndIf
SetDefault(aReturn,cString)
If NGCANCELAIMP(nLastKey,"STO")
   Return
EndIf
  
RptStatus({|lEnd| R610Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

dbSelectArea("ST0")
Set Key VK_F9 To

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)
Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R610Imp  ³ Autor ³ Waldemiro L. Lustosa  ³ Data ³ 13.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR610                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R610Imp(lEnd,wnRel,titulo,tamanho)
Local cRodaTxt   := ""
Local nCntImpr   := 0, cChave  := Space(Len(ST9->T9_CODBEM))
Private li       := 80 ,m_pag := 1
Private Cabec1   := " ",Cabec2 := " "
Private nomeprog := "MNTR610"

nTipo  := IIF(aReturn[4]==1,15,18)

/*             1         2         3         4         5         6         7         8         9         0         1         2         3
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
Servico.: xxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  Seq.: 999  Nome Manut: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Area..: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Tipo.: xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxx
Bem...: xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  Familia...: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
Pai...: xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  C.Custo...: xxxxxxxxx            C.Trab..: xxxxxx    Calend: xxx
Prior.: xxx     Contador: xxxxxxxxx Dt Acomp.: dd/mm/aaaa Var/Dia...: xxxxxx  Limite Cont: xxxxxxxxx Sit.Bem: xxxx   Sit.Manut: xxxx
                Contador 2:
        Dt.Ult.Manut.: xx/xx/xxxx Parada: xxx Parada Antes: xxx xxx           Depois: xxx xxx  N Util: Adianta
        Tipo Acompan.: xxxxxxxx          Tempo Entre Manut: xxx xxx       Calendario: xxx      Prior.: xxx
        Cont.Ult.Man.: xxxxxxxxx    Incremento Entre Manut: xxxxxxxxx Periodo Acomp.: xxx xxx
        Cont.Bem.....: xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
        --------------------------------------------------TAREFA------------------------------------------------
        xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
                                        ----------------DEPENDENCIAS------------------------------------------------
                                         Tarefa                                        Sobreposicao
                                         xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      999%

            -----------------------------ETAPA---------------------------------
            xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

            -----------------------------OPCOES--------------------------------
                        Opcao      Tipo Res. Con. Informacao Tp. Manut. Bem              Serv.  Seq.
                        xxxxxxxxxx xxxxxxxxx xx   xxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxxxxxx xxxxxx 999

            --------------------------------------------INSUMOS------------------------------------------------iiiiiiiiiiiiiii
            Nome          Codigo                         Descricao                               Qtd Consumo   Uni Res Destino
            xxxxxxxxxxxxx xxxxxxxxxxxxxxxiiiiiiiiiiiiiii xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxx xxxxxx,xx xxx SIM Substit
*/
      
NGDBAREAORDE("STF",3)
dbSeek(xFilial('STF')+MV_PAR01,.T.)
SetRegua(LastRec())

While !Eof() .And. xFilial("STF") == STF->TF_FILIAL .And. STF->TF_SERVICO <= MV_PAR02
   IncRegua()
   If NGIFDBSEEK("ST9",STF->TF_CODBEM,1,.F.) .And.  ST9->T9_CCUSTO >= MV_PAR03 .And. ST9->T9_CCUSTO <= MV_PAR04;
      .And. ST9->T9_CENTRAB >= MV_PAR05 .And. ST9->T9_CENTRAB <= MV_PAR06

      nAtual := recno() 
      cChave := NGBEMPAI(ST9->T9_CODBEM)
      MNTR610LIN()       
      @ Li,000 PSay STR0007+" "+ST9->T9_CODBEM+"  "+substr(ST9->T9_NOME,1,33) //"Bem...:"    
      @ Li,067 PSay STR0008+" "+ST9->T9_CODFAMI+" "+NGSEEK("ST6",ST9->T9_CODFAMI,1,"T6_NOME") //"Familia...:"
      MNTR610LIN()

      If !Empty(cChave)
         @ Li,000 PSay STR0009+" "+cChave+"  "+NGSEEK("ST9",cChave,1,"substr(T9_NOME,1,33)")
      EndIf
      DbSelectArea("ST9")
      dbGoTo(nAtual)
      @ Li,067 PSay STR0010+" "+ST9->T9_CCUSTO  //"C.Custo...:"
      @ Li,100 PSay STR0011+" "+ST9->T9_CENTRAB //"C.Trab..:"
      @ Li,119 PSay STR0012+" "+ST9->T9_CALENDA //"Calend:"
      MNTR610LIN()

      @ Li,000 PSay STR0013+" "+ST9->T9_PRIORID //"Prior.:"
      @ Li,016 PSay STR0014+" "+Str(ST9->T9_POSCONT,9) //"Contador:"
      @ Li,036 PSay STR0015+" "+Dtoc(ST9->T9_DTULTAC) //"Dt.Acomp.:"
      @ Li,058 PSay STR0016+" "+Str(ST9->T9_VARDIA,6) //"Var/Dia...:"
      @ Li,077 PSay STR0017+"" +Str(ST9->T9_LIMICON,9) //"Limite Cont:"
      @ Li,100 PSay STR0018+" "+ NGRETSX3BOX("T9_SITBEM",ST9->T9_SITBEM) //"Sit.Bem:"
      @ Li,115 PSay STR0021+" "+ NGRETSX3BOX("T9_SITMAN",ST9->T9_SITMAN) //"Sit.Manut:"
      
      If NGIFDBSEEK("TPE",ST9->T9_CODBEM,1,.F.)
         MNTR610LIN()
         @ Li,014 PSay SubS(STR0014,1,Len(STR0014)-1)+" 2:"+" "+Str(TPE->TPE_POSCON,9)
         @ Li,036 Psay STR0015+" "+Dtoc(TPE->TPE_DTULTA)  //"Dt.Acomp.:"
         @ Li,058 Psay STR0016+" "+Str(TPE->TPE_VARDIA,6) //"Var/Dia...:"
         @ Li,077 Psay STR0017+""+Str(TPE->TPE_LIMICO,9) //"Limite Cont:"
      EndIf   
      MNTR610LIN(2)

      @ Li,008 PSay STR0022+" "+Dtoc(STF->TF_DTULTMA) //"Dt.Ult.Manut.:"   //"Dt.Ult.Manut.:" //"Parada:" "SIM","NAO" //"Parada:"
      @ LI,034 PSay STR0023+" "+NGRETSX3BOX("TF_PARADA",STF->TF_PARADA)    //"Parada:" "SIM","NAO" //"Parada:"
      @ Li,046 PSay STR0026+" "+Str(STF->TF_TEPAANT,3)+" "+STF->TF_UNPAANT //"Parada Antes:"
      @ Li,078 PSay STR0027+" "+Str(STF->TF_TEPADEP,3)+" "+STF->TF_UNPADEP //"Depois:"
      @ Li,095 PSay STR0028+" "+NGRETSX3BOX("TF_NAOUTIL",STF->TF_NAOUTIL)  //"N Util:"
      MNTR610LIN()

      @ Li,008 PSay STR0029 //"Tipo Acompan.:"
      cCodBem1 := STF->TF_CODBEM
      nPERACOM := ST9->T9_PERACOM
      cUNIACOM := ST9->T9_UNIACOM

      If ST9->T9_TEMCONT <> 'N' .AND. (ST9->T9_TEMCONT == 'I' .OR. ST9->T9_TEMCONT == 'P')
        cBEMAUX := SPACE(LEN(ST9->T9_CODBEM))
        If ST9->T9_TEMCONT == 'I'
           cBEMAUX := NGBEMIME(STF->TF_CODBEM)
        ElseIf ST9->T9_TEMCONT == 'P'
           cBEMAUX := cChave
        Endif

        If !Empty(cBEMAUX)  .And. NGIFDBSEEK("ST9",cBEMAUX,1,.F.)
           nPERACOM := ST9->T9_PERACOM
           cUNIACOM := ST9->T9_UNIACOM
        EndIf
      EndIf

      DbSelectArea("ST9")
      dbGoTo(nAtual)
      @ Li,023 Psay NGRETSX3BOX("TF_TIPACOM",STF->TF_TIPACOM)
      If STF->TF_TIPACOM = "T" .Or. STF->TF_TIPACOM = "A"
         @ Li,040 PSay STR0031+" "+Str(STF->TF_TEENMAN,3)+" "+NGRETSX3BOX("TF_UNENMAN",STF->TF_UNENMAN)
      EndIf
      @ Li,074 PSay STR0032+" "+STF->TF_CALENDA //"Calendario:"
      @ Li,095 PSay STR0071+" "+STF->TF_PRIORID //"Prior.:"

      MNTR610LIN()
      If STF->TF_TIPACOM <>  "T" .Or. STF->TF_TIPACOM = "A"
         @ Li,008 PSay STR0034+" "+Str(STF->TF_CONMANU,9) //"Cont.Ult.Man.:"
         @ Li,036 PSay STR0035+" "+Str(STF->TF_INENMAN,6) //"Incremento Entre Manut:"
         @ Li,070 PSay STR0036+" "+Str(nPERACOM,3)+" "+NGRETSX3BOX("T9_UNIACOM",cUNIACOM) //Tipo Acomp.:"
         MNTR610LIN()
         @ Li,008 PSay STR0037+" "+cCodBem1 //"Cont.Bem.....:"
         MNTR610LIN()
      EndIf
      NGIMTAETOPIN(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,MV_PAR07,MV_PAR08,"MNTR610LIN")  
   EndIf
   Li := 59
   NGDBSELSKIP("STF")
End  

NGRODAIMP(nCntImpr,cRodaTxt,Tamanho,wnrel,{"STF"})
Return NIL

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR610LIN³ Autor ³ NG Informatica Ltda   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³MNTR610LIN()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR610                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR610LIN(nLin)
Li += If(nLin <> Nil,nLin,1)
If Li > 58
   Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
   NGIFDBSEEK("ST4",STF->TF_SERVICO,1,.F.)
   @ Li,000 PSay STR0062+" "+STF->TF_SERVICO+" "+ST4->T4_NOME //"Servico.:"
   @ Li,062 PSay STR0063+" "+STF->TF_SEQRELA    //"Seq.:"
   @ Li,073 PSay STR0064+" "+STF->TF_NOMEMAN  //"Nome Manut:"
   Li++
   @ Li,000 PSay STR0065+" "+ST4->T4_CODAREA+" "+NGSEEK("STD",ST4->T4_CODAREA,1,"SUBSTR(STD->TD_NOME,1,30)")  //"Area...:"
   @ Li,050 PSay STR0066+" "+ST4->T4_TIPOMAN  //"Tipo.:"
   NGIFDBSEEK("STE",ST4->T4_TIPOMAN,1,.F.)
   @ Li,062 PSay STE->TE_NOME
   @ Li,114 PSay NGRETSX3BOX("TE_CARACTE",STE->TE_CARACTE) 
   Li++
   @ Li,000 PSay Replicate("*",132)
   Li++
EndIf
Return