#INCLUDE "MDTR820.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 2 //Versao do fonte
/*/


Ŀ
Funo     MDTR820   Autor  Thiago Machado         Data  15.08.00 
Ĵ
Descrio Relatorio dos EPI's necessarios por funcao.                 
          O usuario pode selecionar o codigo da Funao, obtendo como  
          resultado a relacao dos EPI's  que devem ser utilizados pe- 
          funcionarios que executam a funcao.                         
Ĵ
 Uso       Generico                                                   
ٱ

/*/
Function MDTR820()
//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  	 	  
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)
Local oReport
Local aArea := GetArea()
Local cOldFil := cFilAnt

Private cPerg   := ""
Private nSizeFil:= FwSizeFilial()
Private nSizeSRJ:= If((TAMSX3("RJ_FUNCAO")[1]) < 1,5,(TAMSX3("RJ_FUNCAO")[1]))
Private nSizeCod:= If((TAMSX3("B1_COD")[1]) < 1,2,(TAMSX3("B1_COD")[1]))
Private nTa1 	:= If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
Private nTa1L 	:= If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))
Private nSizeTD := nTa1+nTa1L

Private cModoSRJ:= NGSX2MODO("SRJ")
Private cModoTNB:= NGSX2MODO("TNB")
Private lModoCom:= (cModoTNB == "C" .and. cModoSRJ == "C")
Private cAliasFil:= "SRJ"

If cModoSRJ == "E"
	cAliasFil := "SRJ"
Elseif cModoTNB == "E"
	cAliasFil := "TNB"
Else
	cAliasFil := "TNB"
Endif
	
lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
cCliMdtps := ""
cPerg    :=If(!lSigaMdtPS,"MDT820    ","MDT820PS  ")
	
/*-------------------------------------
//PERGUNTAS PADRO						|
| MDT820    01      De  Funcao ?		|
| MDT820    02      Ate Funcao ?		|
| MDT820    03      De Filial ?		|
| MDT820    04      Ate Filial ?		|
|											|
//PERGUNTAS PRESTADOR DE SERVIO		|
| MDT820    01      De Cliente ?		|
| MDT820    02      Loja				|
| MDT820    03      At Cliente?		|
| MDT820    04      Loja				|
| MDT820    05      De  Funcao ?		|
| MDT820    06      Ate Funcao ?		|
| MDT820    07      De Filial ?		|
| MDT820    08      Ate Filial ?		|
----------------------------------------*/

If TRepInUse()
	//-- Interface de impressao
	oReport := ReportDef()
	oReport:SetPortrait()
	oReport:PrintDialog()
Else
	MDTR820R3()
EndIf

RestArea(aArea)

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//
NGRETURNPRM(aNGBEGINPRM)
cFilAnt := cOldFil
dbSelectArea("SM0")
dbSetOrder(1)
dbSeek(cEmpAnt+cFilAnt)

Return .T.
/*/


Ŀ
Funo     ReportDef Autor Andre E. Perez Alvarez  Data  31/07/06 
Ĵ
Descrio  Define as secoes impressas no relatorio                    
Ĵ
 Uso       MDTR490                                                    
|__________|____________________________________________________________| 

/*/
Static Function ReportDef()
Static oReport
Static oSection0
Static oSection1
Static oSection2

Static oCell

//LAYOUT
/*        1         2         3         4         5         6         7         8         9       100       110       120       130
012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
____________________________________________________________________________________________________________________________________

Epi's por Funcao
____________________________________________________________________________________________________________________________________

Funcao    Descricao
__________________________________________
xxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

Epi      Descricao
______________________________________________________________________

123456   1234567890123456789012345678901234567890
123456   1234567890123456789012345678901234567890
123456   1234567890123456789012345678901234567890
123456   1234567890123456789012345678901234567890

*/

//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport := TReport():New("MDTR820",OemToAnsi(STR0006),cPerg,{|oReport| ReportPrint()},;  //"Epi x Funcao"
STR0001+" "+STR0002+" "+STR0003)

Pergunte(oReport:uParam,.F.)

//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatrio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//                                                                        
//
//Ŀ
//Criacao da celulas da secao do relatorio                                
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de cdigo para impressao.                                 
//        Default : ExpC2                                                 
//                                                                        
//

If lSigaMdtps
	//********************* Secao 0 - Cliente
	oSection0 := TRSection():New (oReport,STR0018, {"TNB","SA1"} )  //"Cliente"
	oCell := TRCell():New(oSection0, "TNB_CLIENT"    , "TNB"  , STR0018, "@!", nTa1 )  //"Cliente"
	oCell := TRCell():New(oSection0, "TNB_LOJA"      , "TNB"  , STR0019, "@!", nTa1L)  //"Loja"
	oCell := TRCell():New(oSection0, "A1_NOME"       , "SA1"  , STR0020, "@!", 40  ) //"Nome"
	TRPosition():New(oSection0, "SA1", 1, {|| xFilial("SA1") + TNB->TNB_CLIENT+TNB->TNB_LOJA})
Else
	//********************* Secao 0 - Filial
	oSection0 := TRSection():New (oReport,STR0029, {"TNB"} )//"Filial"
	oCell := TRCell():New(oSection0, "TNB_FILIAL"    , "TNB"  , "Filial", "@!", nSizeFil )
	oCell := TRCell():New(oSection0, "NOMFIL"	, "TNB", STR0020, "@!", 40, /*lPixel*/, {|| cNomFil } ) //"Nome"
Endif

//********************* Secao 1 - Funcao
oSection1 := TRSection():New (oReport,STR0009, {"TNB","SRJ"} ) //Funo
oCell := TRCell():New(oSection1, "TNB_CODFUN"    , "TNB"  , STR0009, "@!", nSizeSRJ+5  )  //"Funcao"
oCell := TRCell():New(oSection1, "RJ_DESC"       , "SRJ"  , STR0010, "@!", 40        )  //"Descricao"
If lSigaMdtps
	TRPosition():New(oSection1, "SRJ", 1, {|| xFilial("SRJ") + TNB->TNB_CODFUN})
Endif

//********************* Secao 2 - Epi
oSection2 := TRSection():New (oReport,STR0011, {"SB1"} )  //"EPI"
oCell := TRCell():New (oSection2, "CODIGO" , , STR0011, "@!", nSizeCod+5, , {||  cCodEpi}) //"EPI"
oCell := TRCell():New (oSection2, "B1_DESC", "SB1", STR0010, "@!", 40, , ) //"Nome do Exame"
If TNB->(FieldPos("TNB_OBRIGA")) > 0
	oCell := TRCell():New (oSection2, "TNB_OBRIGA", "TNB", STR0012, "@!", 03, /*lPixel*/, {|| If (TNB->TNB_OBRIGA=="S",STR0013,STR0014) } ) //"Obrigatrio" ###"Sim"###"Nao"
Endif
If lSigaMdtps
	TRPosition():New (oSection2, "SB1", 1, {|| xFilial("SB1") + cCodEpi} )
Endif

Return oReport
/*/


Ŀ
Funo    ReportPrint Autor  Andre E. Perez Alvarez Data  31/07/06 
Ĵ
Descrio  Imprime o relatorio.                                        
Ĵ
 Uso       ReportDef                                                   
ٱ

/*/
Static Function ReportPrint()
Local cFuncao := "", cCliente := ""
Local aFiliais:= {}
Local aEpiFil := {}
Local i, j, nTemEpi
Local lPrintx := .F.
Private cNomFil := ""
Private cCodEpi := ""

If lSigaMdtps
	
	dbSelectArea("TNB")
	dbSetOrder(04)  //TNB_FILIAL+TNB_CLIENT+TNB_LOJA+TNB_CODFUN+TNB_CODEPI
	dbSeek(xFilial("TNB")+MV_PAR01+MV_PAR02,.T.)
	oReport:SetMeter(LastRec())
	
	//Ŀ
	// Correr TNB para ler os  EPI's por Funo.                
	//
	While !oReport:Cancel()	  				  .AND.;
		!Eof()                           	  .AND.;
		TNB->TNB_FILIAL == xFilial('TNB')	  .AND.;
		TNB->(TNB_CLIENT+TNB_LOJA) >= MV_PAR01+MV_PAR02 .AND. TNB->(TNB_CLIENT+TNB_LOJA) <= MV_PAR03+MV_PAR04
		
		oReport:IncMeter()
		
		If TNB->TNB_CODFUN < mv_par07 .or. TNB->TNB_CODFUN > mv_par08
			dbSkip()
			Loop
		Endif
		
		If cCliente <> TNB->(TNB_CLIENT+TNB_LOJA)
			cCliente := TNB->(TNB_CLIENT+TNB_LOJA)
			oSection0:Init()
			oSection0:PrintLine()
		Endif
		
		cFuncao := TNB->TNB_CODFUN
		oSection1:Init()
		oSection1:PrintLine()
		oSection2:Init()
		
		While !oReport:Cancel()	  				  .AND.;
			!Eof()                           	  .AND.;
			TNB->TNB_FILIAL == xFilial('TNB')	  .AND.;
			TNB->TNB_CODFUN == cFuncao 		  .AND.;
			TNB->(TNB_CLIENT+TNB_LOJA) == cCliente
				dbSelectArea("TN3")
				dbSetOrder(4)
				dbSeek(xFilial("TN3")+TNB->(TNB_CLIENT+TNB_LOJA+TNB_CODEPI))
				If TN3->TN3_GENERI == '2'
					dbSelectArea("TL0")
					dbSetOrder(4)
					dbSeek(xFilial("TL0")+cCliente+TNB->TNB_CODEPI)
					While TL0->(!Eof()) .and. TL0->TL0_EPIGEN == TNB->TNB_CODEPI
						nTemEpi := aScan(aEpiFil,{|x| x == TL0->TL0_EPIFIL})
						If nTemEpi == 0
							aAdd(aEpiFil,TL0->TL0_EPIFIL)
						EndIf
						dbSelectArea("TL0")
						dbSkip()
					End
				Else
					nTemEpi := aScan(aEpiFil,{|x| x == TNB->TNB_CODEPI})
					If nTemEpi == 0
						aAdd(aEpiFil,TNB->TNB_CODEPI)
					EndIf
				EndIf
				For j := 1 to Len(aEpiFil)
					//Posiciona no EPI
					dbSelectArea("SB1")
					dbSetOrder(1)
					dbSeek(xFilial("SB1",aFiliais[i,1])+aEpiFil[j])
					cCodEpi := aEpiFil[j]
					oSection2:PrintLine()
					lPrintx := .T.
				Next j
				aEpiFil := {}
		DbSkip()
		End
		
		oSection2:Finish()
		oSection1:Finish()
		If cCliente <> TNB->(TNB_CLIENT+TNB_LOJA)
			oSection0:Finish()
		Endif
		
	End
	
Else
	aFiliais := MDTRETFIL(cAliasFil, MV_PAR03, MV_PAR04)

	oReport:SetMeter(Len(aFiliais))
	For i:=1 to Len(aFiliais)
		oReport:IncMeter()
		//Ŀ
		// Correr TNB para ler os  EPI's por Funo.                
		//
		lFirst := .T.
		dbSelectArea("TNB")
		dbSetOrder(01)
		dbSeek(xFilial("TNB",aFiliais[i,1])+MV_PAR01,.T.)
		While !oReport:Cancel() .AND. !Eof() .AND. TNB->TNB_FILIAL == xFilial("TNB",aFiliais[i,1]) .AND. TNB->TNB_CODFUN <= MV_PAR02
			If lFirst .and. !lModoCom
				lFirst := .F.
				cNomFil := aFiliais[i,2]
				oSection0:Init()
				oSection0:PrintLine()
			Endif
			cFuncao := TNB->TNB_CODFUN
			//Posiciona na Funcao
			dbSelectArea("SRJ")
			dbSetOrder(1)
			dbSeek(xFilial("SRJ",aFiliais[i,1])+TNB->TNB_CODFUN)
			oSection1:Init()
			oSection1:PrintLine()
			oSection2:Init()
			While !oReport:Cancel() .AND. !Eof() .AND. TNB->TNB_FILIAL+TNB->TNB_CODFUN == xFilial("TNB",aFiliais[i,1])+cFuncao
					dbSelectArea("TN3")
					dbSetOrder(2)
					dbSeek(xFilial("TN3")+TNB->TNB_CODEPI)
					While TN3->(!Eof()) .and. TN3->TN3_CODEPI == TNB->TNB_CODEPI
						If TN3->TN3_GENERI == '2'
							dbSelectArea("TL0")
							dbSetOrder(1)
							dbSeek(xFilial("TL0")+TNB->TNB_CODEPI)
							While TL0->(!Eof()) .and. TL0->TL0_EPIGEN == TNB->TNB_CODEPI
								nTemEpi := aScan(aEpiFil,{|x| x == TL0->TL0_EPIFIL})
								If nTemEpi == 0
									aAdd(aEpiFil,TL0->TL0_EPIFIL)
								EndIf
								dbSelectArea("TL0")
								dbSkip()
							End
						Else
							nTemEpi := aScan(aEpiFil,{|x| x == TNB->TNB_CODEPI})
							If nTemEpi == 0
								aAdd(aEpiFil,TNB->TNB_CODEPI)
							EndIf
						EndIf
						dbSelectArea("TN3")
						TN3->(dbSkip())
					End
					For j := 1 to Len(aEpiFil)
						//Posiciona no EPI
						dbSelectArea("SB1")
						dbSetOrder(1)
						dbSeek(xFilial("SB1",aFiliais[i,1])+aEpiFil[j])
						cCodEpi := aEpiFil[j]
						oSection2:PrintLine()
						lPrintx := .T.
					Next j
					aEpiFil := {}

				dbSelectArea("TNB")
				dbSkip()
			End
			oSection2:Finish()
			oSection1:Finish()
			If !lModoCom .and. (Eof() .or. TNB->TNB_FILIAL != xFilial("TNB",aFiliais[i,1]))
				oSection0:Finish()
			Endif
		End
	Next i
Endif

If !lPrintx
	MsgInfo(STR0024)	//"No h nada para imprimir no relatrio."
	Return .F.
Endif

Return .T.
/*/


Ŀ
Funo     MDTR820R3 Autor  Thiago Machado         Data  15.08.00 
Ĵ
Descrio Relatorio dos EPI's necessarios por funcao (realese 3)      
Ĵ
 Uso       MDTR820                                                    
ٱ

/*/
Function MDTR820R3()
//Ŀ
// Define Variaveis                                             
//
Local wnrel   := "MDTR820"
Local cDesc1  := STR0001 //"Relatorio de apresentacao dos EPI's   por funcao. Baseado nos     "
Local cDesc2  := STR0002 //"parametros o usuario podera selecionar uma determinada funcao,    "
Local cDesc3  := STR0003 //"ou obter uma listagem detalhada de todos os EPI's por Funcao.     "
Local cString := "TNB"

Private nomeprog := "MDTR820"
Private tamanho  := "M"
Private aReturn  := { STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
Private titulo   := STR0006 //"EPI x Funcao"
Private ntipo    := 0
Private nLastKey := 0
Private cabec1, cabec2
Private nMasc    := 0
Private nFemin   := 0
Private cCodfun  := space(nSizeSRJ)
Private cCid     := space(8)

//Ŀ
// Verifica as perguntas selecionadas                           
//
pergunte(cPerg,.F.)

//Ŀ
// Envia controle para a funcao SETPRINT                        
//
wnrel:="MDTR820"

wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

If nLastKey == 27
	Set Filter to
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Set Filter to
	Return
Endif

#IFDEF WINDOWS
	RptStatus({|lEnd| R820Imp(@lEnd,wnRel,titulo,tamanho)},titulo)
#ELSE
	R820Imp(.F.,wnRel,titulo,tamanho)
#ENDIF
Return NIL

/*/


Ŀ
Funo     R820Imp   Autor  Thiago Machado         Data  15.08.00 
Ĵ
Descrio  Chamada do Relatrio                                       
Ĵ
 Uso       MDTR820                                                    
ٱ


/*/
Static Function R820Imp(lEnd,wnRel,titulo,tamanho)
//Ŀ
// Define Variaveis                                             
//
Local cRodaTxt := ""
Local nCntImpr := 0
Local lObrigat := .F.

Local cCliente := ""
Local cFunc := ""
Local lPri := .t.
Local aEpiFil := {}

//Ŀ
// Variaveis locais exclusivas deste programa                   
//
Local i
Local cCodFil := "", aFiliais := {}

//Ŀ
// Contadores de linha e pagina                                 
//
Private li := 80 ,m_pag := 1
nSizeCod := If((TAMSX3("B1_COD")[1]) < 1,2,(TAMSX3("B1_COD")[1]))

//Ŀ
// Verifica se deve comprimir ou nao                            
//
nTipo  := IIF(aReturn[4]==1,15,18)

If TNB->(FieldPos("TNB_OBRIGA")) > 0
	lObrigat := .T.
EndIf

//Ŀ
// Variaveis do Arquivo Temporario                              
//
Private aVetInr := {}, aDBF := {}
Private cTRB := GetNextAlias(), oTempTRB

//Ŀ
// Monta os Cabecalhos                                          
//
cabec1 := STR0007 //"Funcao  Descricao                             "
If lObrigat
	cabec2 := STR0021  //"        EPI              Descricao                                 Obrigatrio"
Else
	cabec2 := STR0022  //"        EPI              Descricao"
EndIf

/*
1         2         3         4         5         6         7         8
012345678901234567890123456789012345678901234567890123456789012345678901234567890
---------------------------------------------------------------------------------
Funcao  Descricao
EPI              Descricao                                 Obrigatrio
---------------------------------------------------------------------------------
Filial: xx

xxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  Sim
*/

aDBF := {}
AADD(aDBF,{"FUNCAO","C",nSizeSRJ,0})
AADD(aDBF,{"EPI"   ,"C",nSizeCod,0})
If lObrigat
	AADD(aDBF,{"OBRIGA","C",3 ,0})
EndIf
If lSigaMdtps
	AADD(aDBF,{"CLIENT"   ,"C",nTa1,0})
	AADD(aDBF,{"LOJA"     ,"C",nTa1L,0})
Else
	AADD(aDBF,{"FILIAL"   ,"C",nSizeFil	,0})
	AADD(aDBF,{"NOMFIL"   ,"C",40		,0})
	AADD(aDBF,{"RJ_DESC"  ,"C",25		,0})
	AADD(aDBF,{"B1_DESC"  ,"C",40		,0})
Endif

oTempTRB := FWTemporaryTable():New( cTRB, aDBF )
If lSigaMdtps
	oTempTRB:AddIndex( "1", {"CLIENT","LOJA","FUNCAO","EPI"} )
Else
	oTempTRB:AddIndex( "1", {"FILIAL","FUNCAO","EPI"} )
EndIf	
oTempTRB:Create()

If lSigaMdtps
	dbSelectArea("TNB")
	dbSetOrder(04)  //TNB_FILIAL+TNB_CLIENT+TNB_LOJA+TNB_CODFUN+TNB_CODEPI
	dbSeek(xFilial("TNB")+MV_PAR01+MV_PAR02,.T.)
	
	SetRegua(LastRec())
	
	//Ŀ
	// Correr TNB para ler os  EPI's por Funo.                
	//
	While !Eof()                   .And.;
		TNB->TNB_FILIAL == xFIlial('TNB')        .AND.;
		TNB->(TNB_CLIENT+TNB_LOJA) >= MV_PAR01+MV_PAR02 .AND. TNB->(TNB_CLIENT+TNB_LOJA) <= MV_PAR03+MV_PAR04
		
		If TNB->TNB_CODFUN < mv_par07 .or. TNB->TNB_CODFUN > mv_par08
			dbSkip()
			Loop
		Endif
		
		IncRegua()
		//Verifica NUMCAP para EPIs filhos
			dbSelectArea("TN3")
			dbSetOrder(1)
			dbSeek(xFilial("TN3")+TNB->(TNB_CLIENT+TNB_LOJA+TNB_CODEPI))
			If TN3->TN3_GENERI == '2'//Se no for genrico
				dbSelectArea("TL0")
				dbSetOrder(1)
				dbSeek(xFilial("TL0")+TNB->(TNB_CLIENT+TNB_LOJA+TNB_CODEPI))
				While TL0->(!Eof()) .and. TL0->(TL0_CLIENT+TL0_LOJACL+TL0_EPIGEN) == TNB->(TNB_CLIENT+TNB_LOJA+TNB_CODEPI)
					dbSelectArea(cTRB)
					(cTRB)->(DbAppend())
					(cTRB)->FUNCAO := TNB->TNB_CODFUN
					(cTRB)->EPI := TL0->TL0_EPIFIL
					If lObrigat
						(cTRB)->OBRIGA := If (TNB->TNB_OBRIGA=="S",STR0013,STR0014) //"Sim"###"No"
					EndIf
					(cTRB)->CLIENT := TNB->TNB_CLIENT
					(cTRB)->LOJA   := TNB->TNB_LOJA
					MsUnlock(cTRB)
					dbSelectArea("TL0")
					TL0->(dbSkip())
				End
			Else
				dbSelectArea(cTRB)   
				(cTRB)->(DbAppend())
				(cTRB)->FUNCAO := TNB->TNB_CODFUN
				(cTRB)->EPI := TNB->TNB_CODEPI
				If lObrigat
					(cTRB)->OBRIGA := If (TNB->TNB_OBRIGA=="S",STR0013,STR0014) //"Sim"###"No"
				EndIf
				(cTRB)->CLIENT := TNB->TNB_CLIENT
				(cTRB)->LOJA   := TNB->TNB_LOJA
				MsUnlock(cTRB)
			EndIf

		dbSelectArea("TNB")
		dbSKIP()
	End
	
	dbSelectArea(cTRB)
	dbGOTOP()
	If RecCount()==0
		MsgInfo(STR0024)	//"No h nada para imprimir no relatrio."
		Use
		Return .F.
	EndIf
	While !eof()
		
		If cCliente <> (cTRB)->CLIENT+(cTRB)->LOJA
			Somalinha()
			If !lPri
				@Li,000 PSAY Replicate("-",80)
				Somalinha()
			Endif
			dbSelectArea("SA1")
			dbSetOrder(1)
			dbSeek(xFilial("SA1")+(cTRB)->CLIENT+(cTRB)->LOJA)
			@Li,000 PSAY STR0023 + Alltrim((cTRB)->CLIENT) + "-" + Alltrim((cTRB)->LOJA) + " - " + AllTrim(SA1->A1_NOME)  //"Cliente: "
			cCliente := (cTRB)->CLIENT + (cTRB)->LOJA
		Endif
		
		cFUNC := (cTRB)->FUNCAO
		Somalinha()
		Somalinha()
		dbSelectArea("SRJ")
		dbSetOrder(1)
		dbSEEK(xfilial("SRJ")+(cTRB)->FUNCAO)
		@Li,000 PSAY Alltrim((cTRB)->FUNCAO)
		@Li,008 pSay Alltrim(SRJ->RJ_DESC)
		
		Do while ! eof() .and. (cTRB)->FUNCAO == cFUNC .and. cCliente == (cTRB)->CLIENT+(cTRB)->LOJA
			Somalinha()
			@Li,008 PSAY (cTRB)->EPI PICTURE "@!"
			dbSelectArea("SB1")
			dbSetOrder(1)
			dbSEEK(xfilial("SB1")+(cTRB)->EPI)
			@Li,046 PSAY SB1->B1_DESC PICTURE "@!"
			If lObrigat
				@Li,085 PSAY (cTRB)->OBRIGA PICTURE "@!"
			EndIf
			dbSelectArea(cTRB)
			dbSKIP()
		enddo
		somalinha()
		lPri := .f.
	Enddo
	
Else
	aFiliais := MDTRETFIL(cAliasFil, MV_PAR03, MV_PAR04)
	
	For i:=1 to Len(aFiliais)
		//Ŀ
		// Correr TNB para ler os  EPI's por Funcao.                
		//
		dbSelectArea("TNB")
		dbSetOrder(01)
		dbSeek(xFilial("TNB",aFiliais[i,1])+MV_PAR01,.T.)
		While !Eof() .And. TNB->TNB_FILIAL == xFilial("TNB",aFiliais[i,1]) .AND. TNB->TNB_CODFUN <= MV_PAR02
			//Posiciona na Funcao
			dbSelectArea("SRJ")
			dbSetOrder(1)
			dbSeek(xFilial("SRJ",aFiliais[i,1])+TNB->TNB_CODFUN)
			
			//Posiciona no EPI
			dbSelectArea("SB1")
			dbSetOrder(1)
			dbSeek(xFilial("SB1",aFiliais[i,1])+TNB->TNB_CODEPI)
		//Verifica NUMCAP para EPIs filhos
				dbSelectArea("TN3")
				dbSetOrder(2)
				dbSeek(xFilial("TN3",aFiliais[i,1])+TNB->TNB_CODEPI)
				While TN3->(!Eof()) .and. TN3->TN3_CODEPI == TNB->TNB_CODEPI
					If TN3->TN3_GENERI == '2'//Se no for genrico
						dbSelectArea("TL0")
						dbSetOrder(1)
						dbSeek(xFilial("TL0",aFiliais[i,1])+TN3->TN3_CODEPI)
						While TL0->(!Eof()) .and. TL0->TL0_EPIGEN == TN3->TN3_CODEPI
							nTemEpi := aScan(aEpiFil,{|x| x == TL0->TL0_EPIFIL})
							If nTemEpi == 0
								aAdd(aEpiFil,TL0->TL0_EPIFIL)
								dbSelectArea("SB1")
								dbSetOrder(1)
								dbSeek(xFilial("SB1",aFiliais[i,1])+TL0->TL0_EPIFIL)
								dbSelectArea(cTRB)
								RecLock(cTRB,.T.)
								(cTRB)->FILIAL := TNB->TNB_FILIAL
								(cTRB)->NOMFIL := aFiliais[i][2]
								(cTRB)->FUNCAO := TNB->TNB_CODFUN
								(cTRB)->EPI    := TL0->TL0_EPIFIL
								(cTRB)->RJ_DESC:= SRJ->RJ_DESC
								(cTRB)->B1_DESC:= SB1->B1_DESC
								If lObrigat
									(cTRB)->OBRIGA := If (TNB->TNB_OBRIGA=="S",STR0013,STR0014) //"Sim"###"No"
								EndIf
								MsUnlock(cTRB)
							EndIf
							dbSelectArea("TL0")
							TL0->(dbSkip())
						End
					Else
						nTemEpi := aScan(aEpiFil,{|x| x == TNB->TNB_CODFUN})
						If nTemEpi == 0
							aAdd(aEpiFil,TNB->TNB_CODFUN)
							dbSelectArea(cTRB)
							RecLock(cTRB,.T.)
							(cTRB)->FILIAL := TNB->TNB_FILIAL
							(cTRB)->NOMFIL := aFiliais[i][2]
							(cTRB)->FUNCAO := TNB->TNB_CODFUN
							(cTRB)->EPI    := TNB->TNB_CODEPI
							(cTRB)->RJ_DESC:= SRJ->RJ_DESC
							(cTRB)->B1_DESC:= SB1->B1_DESC
							If lObrigat
								(cTRB)->OBRIGA := If (TNB->TNB_OBRIGA=="S",STR0013,STR0014) //"Sim"###"No"
							EndIf
							MsUnlock(cTRB)
						EndIf
					EndIf
					dbSelectArea("TN3")
					TN3->(dbSkip())
				End

			dbSelectArea("TNB")
			dbSkip()
		End
	Next i
	
	cCodFil := ""
	dbSelectArea(cTRB)
	dbGoTop()
	SetRegua((cTRB)->(RecCount()))
	While !eof()
		IncProc()
		If lModoCom
			cCodFil := (cTRB)->FILIAL
			Somalinha()
		Else
			If cCodFil != (cTRB)->FILIAL
				If !Empty(cCodFil)
					Somalinha()
				Endif
				cCodFil := (cTRB)->FILIAL
				Somalinha()
				@Li,000 pSay STR0029+": "+AllTrim((cTRB)->FILIAL)+" - "+(cTRB)->NOMFIL//"Filial"
			Endif
		Endif
		cFunc := (cTRB)->FUNCAO
		Somalinha()
		Somalinha()
		@Li,000 PSAY Alltrim((cTRB)->FUNCAO)
		@Li,008 PSAY Alltrim((cTRB)->RJ_DESC)
		
		While ! eof() .and. (cTRB)->(FILIAL+FUNCAO) == cCodFil+cFUNC
			Somalinha()
			@Li,008 PSAY (cTRB)->EPI PICTURE "@!"
			@Li,046 PSAY (cTRB)->B1_DESC PICTURE "@!"
			If lObrigat
				@Li,085 PSAY (cTRB)->OBRIGA PICTURE "@!"
			EndIf
			dbSelectArea(cTRB)
			dbSkip()
			If !lModoCom .and. (Eof() .or. ((cTRB)->(FILIAL) != cCodFil .and. !Empty((cTRB)->FILIAL)))
				Somalinha()
				@Li,000 pSay __PrtThinLine()
			Endif
		End
	End
	If (cTRB)->(RecCount()) == 0
		MsgInfo(STR0024)//"No h nada para imprimir no relatrio."
		oTempTRB:Delete()
		dbSelectArea("TNB")
		dbSetOrder(01)
		Return .F.
	EndIf
Endif

Roda(nCntImpr,cRodaTxt,Tamanho)

//Ŀ
// Devolve a condicao original do arquivo principal             
//
RetIndex("TNB")

Set Filter To

Set device to Screen

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
End
MS_FLUSH()

oTempTRB:Delete()

dbSelectArea("TNB")
dbSetOrder(01)

Return NIL
/*/

Ŀ
 Funo    SomaLinha Autor  Thiago Machado         Data  15.08.00 
Ĵ
 Descrio Incrementa Linha e Controla Salto de Pagina                
Ĵ
 Uso       MDTR405                                                    
ٱ

/*/
Static Function Somalinha()
Li++
If Li > 58
	Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
EndIf
Return