#INCLUDE "NGIND004.ch"
#Include "Protheus.ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NGIND004 ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 01/07/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Cadastro de Tipos de Parametros P/ Variaveis   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NGIND004()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aRotina := MenuDef()
Private cCadastro := OemtoAnsi(STR0001) //"Tipos de Parâmetros"
Private aSMENU    := {}, aCHKDEL := {}, bNGGRAVA
Private aTabConF3 := {}
Private aNgButton := {}
Private cPrograma := "NGIND004"

If !NGCADICBASE("TZ4_RELACA","D","TZ4",.F.)
	If !NGINCOMPDIC("UPDINDIC","XXXXXXXXX / 2009")
		Return
	Endif
EndIf

SetKey( VK_F9, { | | NGVersao( "NGIND004" , 1 ) } )

aAdd( aCHKDEL , {"TZ4->TZ4_CODPAR+Str(nModulo,2)" , "TZ3", 4 } )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TZ4")
Set Filter To TZ4->TZ4_MODULO == Str(nModulo,2)
dbSetOrder(1)
mBrowse( 6, 1,22,75,"TZ4")
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ NG Informatica        ³ Data ³01/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMDT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina :=	{ { STR0002, 	"AxPesqui"  , 0 , 1},; //"Pesquisar"
                    { STR0003,	"NGCAD01"   , 0 , 2},; //"Visualizar"
                    { STR0004,   "NGIND4IC"   , 0 , 3},; //"Incluir"
                    { STR0005,   "NGIND4IC"   , 0 , 4},; //"Alterar"
                    { STR0006,   "NGCAD01"   , 0 , 5, 3} } //"Excluir"

Return aRotina

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NGIND4IC ³ Autor ³ Denis Hyroshi de Souza³ Data ³02/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de inclusao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGIND004                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NGIND4IC(cAlias,nRecno,nOpcx)
Local _oldBut := nil
Local _oldRel := nil
Local _oldGrv := nil
Local __nOpca := nil
Local _Inclui := Inclui
Local _Altera := Altera

If Type("aNgButton") == "A"
	_oldBut := aClone(aNgButton)
Endif
If Type("aRelac") == "A"
	_oldRel := aClone(aRelac)
Endif
If Type("bNGGRAVA") != "U"
	_oldGrv := bNGGRAVA
Endif
If Type("nOpca") == "N"
	__nOpca := nOpca
Endif

aNgButton := {}
aAdd(aNgButton,{"SDUFIELDS",{|| fNG004Camp() },STR0007    ,STR0008   }) //"Consultar Campos"###"Campos"
aAdd(aNgButton,{"SDUSEEK"  ,{|| fNG004F3()   },STR0009,STR0010}) //"Consulta Padrão (F3)"###"Cons.Pad."

aRelac := {}
aAdd( aRelac , { "TZ4_MODULO" , "Str(nModulo,2)" } )

bNGGRAVA := {|| NGI04GRAV() }

//Abre tela
If nOpcx == 3
	Inclui := .T.
	Altera := .F.
Endif
nRetCad := NGCAD01(cAlias,nRecno,nOpcx)

aNgButton := aClone(_oldBut)
aRelac    := aClone(_oldRel)
bNGGRAVA  := _oldGrv
nOpca     := __nOpca
Inclui    := _Inclui
Altera    := _Altera

Return nRetCad

//-------------------------------------------------------------------
/*/{Protheus.doc} fNG004Camp
Seleciona campos
@author  Denis Hyroshi de Souza
@since   02/07/2008
@version P12
/*/
//-------------------------------------------------------------------
Function fNG004Camp()

Local lOk      := .F.
Local aCpoTab  := {}
Local aAreaXXX := GetArea()
Local nPosCpo1 := 0
Local oList02, oDlg
Local nTamTot  := 0
Local nInd     := 0
Local aNgHeader:= {}

Private cCodCampo := M->TZ4_CAMPOS

If Empty(M->TZ4_TABELA)
	MsgInfo(STR0011,STR0012) // "O campo Tabela está vazio."###"Atenção"
	Return .F.
Endif

aNgHeader := NGHeader(M->TZ4_TABELA,,.F.) // Buscar os campos das tabela.
nTamTot := Len(aNgHeader)
For nInd := 1 To nTamTot
	If X3USO(aNgHeader[nInd,7]) // Verifica se o campo está como usado
		aAdd(aCpoTab, {aNgHeader[nInd,2],aNgHeader[nInd,1]} ) // Adiciona o valor no array.
	EndIf
Next nInd

If Len(aCpoTab) == 0
	MsgInfo(STR0013+M->TZ4_TABELA+".",STR0012) //"Nenhum campo foi encontrado na tabela "###"Atenção"
	Return .F.
Endif

aSort(aCpoTab,,,{|x,y| x[1] < y[1] })

DEFINE MSDIALOG oDlg FROM 150,005 TO 425,361  TITLE OemToAnsi(STR0014) PIXEL //"Selecione Campo"

	@ 08,008 Say OemToAnsi(STR0015) Of oDlg Pixel //"Campo"
	@ 08,045 MSGET cCodCampo Size 45,09 Valid fCpPes04(@oList02,aCpoTab,.T.) Picture "@!" Of oDlg Pixel
	@ 08,098 Button oBtnCcc Prompt STR0002 Size 30,11 Of oDlg Pixel Action fCpPes04(@oList02,aCpoTab,.T.) //"Pesquisar"

	@ 022,007 LISTBOX oList02 FIELDS HEADER STR0015,STR0016 SIZE 165,100 OF oDlg PIXEL ON DBLCLICK ( lOk := .T., nPosCpo1:=oList02:nAt,oDlg:End() ) //"Campo"###"Título"

	oList02:SetArray(aCpoTab)
	oList02:bLine := { || {aCpoTab[oList02:nAt,1],aCpoTab[oList02:nAt,2]}}
	fCpPes04(@oList02,aCpoTab,.F.)
	cCodCampo := Space(10)

	DEFINE SBUTTON FROM 126, 116 TYPE 1 ENABLE OF oDlg ACTION ( lOk := .T., nPosCpo1:=oList02:nAt,oDlg:End() )
	DEFINE SBUTTON FROM 126, 144 TYPE 2 ENABLE OF oDlg ACTION ( lOk := .F., oDlg:End() )

ACTIVATE MSDIALOG oDlg CENTERED

If ( lOk )
	M->TZ4_CAMPOS := aCpoTab[nPosCpo1,1] //IF( Empty(M->TZ4_CAMPOS) , "" , ALLTRIM(M->TZ4_CAMPOS) + "+" ) + aCpoTab[nPosCpo1]
EndIf

RestArea(aAreaXXX)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ VALTZ4X2 ³ Autor ³ Denis Hyroshi de Souza³ Data ³02/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida tabela selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGIND004                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function VALTZ4X2(cTabTZ4)
Local aAreaXXX := GetArea()
Local aAreaSX2 := SX2->(GetArea())
Local lRet := .T.

dbSelectArea("SX2")
dbSetOrder(1)
If !dbSeek(cTabTZ4)
	Help(" ",1,"REGNOIS")
	lRet := .F.
Endif

If lRet
	fNG004Camp()
Endif

RestArea(aAreaSX2)
RestArea(aAreaXXX)

Return lRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} VALTZ4XB
Valida consulta padrao (F3)

@param cTabTZ4, Caracter, Tabela utilizada.

@author Denis Hyroshi de Souza
@since 02/07/2008
@version P12

@return lRet, Lógico, Retorna verdadeiro ou falso mediante a verifição.
/*/
//------------------------------------------------------------------------------
Function VALTZ4XB(cTabTZ4)
Local aAreaXXX := GetArea()
Local aAreaSXB := SXB->(GetArea())
Local lRet := .T.
Local nTamSX5 := TAMSX3( "X5_CHAVE" )[ 1 ]

dbSelectArea("SXB")
dbSetOrder(1)
If !dbSeek( PadR(cTabTZ4,Len(SXB->XB_ALIAS)) )
	dbSelectArea("SX5")
	dbSetOrder(1)
	If !dbSeek(xFilial("SX5")+"00"+ PadR(cTabTZ4,nTamSX5) )
		Help(" ",1,"REGNOIS")
		lRet := .F.
	Endif
Else
	If !Empty(M->TZ4_CAMPOS)
		cTipoX3 := Posicione("SX3",2,M->TZ4_CAMPOS,"X3_TIPO")

		dbSelectArea("SXB")
		dbSetOrder(1)
		dbSeek( PadR(cTabTZ4,Len(SXB->XB_ALIAS)) + "5" )
		cNomeCPO := Alltrim(SXB->XB_CONTEM)
		nPos_ := At("->",cNomeCPO)
		If nPos_ > 0
			cNomeCPO := Alltrim(Substr(cNomeCPO,nPos_+2))
		Endif

		If cTipoX3 <> Posicione("SX3",2,cNomeCPO,"X3_TIPO")
			MsgInfo(STR0017+M->TZ4_CAMPOS+STR0018+cTipoX3+STR0019+Posicione("SX3",2,cNomeCPO,"X3_TIPO")+"'.",STR0020) //"O campo "###" é tipo '"###"' e o retorno da consulta padrão é tipo '"###"Inconsistência"
			lRet := .F.
		Endif
	Endif
Endif

RestArea(aAreaSXB)
RestArea(aAreaXXX)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ VALTZ4X3 ³ Autor ³ Denis Hyroshi de Souza³ Data ³02/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida os campos selecionados                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGIND004                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function VALTZ4X3(cCpoTZ4,cTabTZ4)
Local lRet     := .T.
Local aAreaXXX := GetArea()

If Empty(cTabTZ4)
	MsgInfo(STR0021,STR0012) //"O campo Tabela é obrigatório."###"Atenção"
	Return .F.
Endif

If !Empty(cCpoTZ4)
	If X3USO(Posicione("SX3",2,cCpoTZ4,"X3_USADO")) .and. Posicione("SX3",2,cCpoTZ4,"X3_CONTEXT") <> "V"
		If cTabTZ4 <> Posicione("SX3",2,cCpoTZ4,"X3_ARQUIVO")
			MsgInfo(STR0017+cCpoTZ4+STR0022+cTabTZ4+".",STR0012) //"O campo "###" não pertence a tabela "###"Atenção"
			lRet := .F.
		Endif
	Else
		MsgInfo(STR0017+cCpoTZ4+STR0023,STR0012) //"O campo "###" não pode ser utilizado."###"Atenção"
		lRet := .F.
	Endif
Else
	MsgInfo(STR0017+cCpoTZ4+STR0024,STR0012) //"O campo "###" não existe."###"Atenção"
	lRet := .F.
Endif

RestArea(aAreaXXX)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³InfCpoTZ4 ³ Autor ³ Denis Hyroshi de Souza³ Data ³02/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica variavel com campos concatenados e transforma     ³±±
±±³          ³ em um vetor de caracteres (cada linha corresponde a um     ³±±
±±³          ³ nome de campo)                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGIND004                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function InfCpoTZ4(cCpoTZ4)
Local aRet      := {}
Local nCont     := 0
Local cConteudo := Alltrim(cCpoTZ4)
Local nPosSinal

While !Empty(cConteudo) .and. nCont < 100
	nCont++ //Contador para nao entrar em loop

	nPosSinal := At("+",cConteudo)
	If nPosSinal > 0
		aAdd(aRet, Alltrim( Substr(cConteudo,1,nPosSinal-1) ) )
		cConteudo := Alltrim( Substr(cConteudo,nPosSinal+1) )
	Else
		aAdd(aRet, Alltrim(cConteudo) )
		cConteudo := ""
	Endif
End

Return aRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG004GRAV ³ Autor ³ Denis Hyroshi de Souza³ Data ³02/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a tela de gravacao                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGIND004                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NGI04GRAV()

If Inclui .or. Altera
	If M->TZ4_TIPO <> "5" .and. M->TZ4_TAMANH == 0
		MsgInfo(STR0025,STR0012) //"O campo Tamanho é obrigatório."###"Atenção"
		Return .F.
	Endif
	If M->TZ4_TIPO == "5"
		If Empty(M->TZ4_CAMPOS) .or. Empty(M->TZ4_TABELA)
			MsgInfo(STR0026,STR0012) //"Os campos Tabela e Campo são obrigatórios."###"Atenção"
			Return .F.
		Endif
	ElseIf M->TZ4_TIPO == "6"
		If Empty(M->TZ4_OPCOES)
			MsgInfo(STR0027,STR0012) //"O campo Lista Opções é obrigatório."###"Atenção"
			Return .F.
		Endif
	Endif
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NGI04VTIPO³ Autor ³ Denis Hyroshi de Souza³ Data ³02/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida o campo tipo                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGIND004                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NGI04VTIPO(cTipo)

If cTipo == "1" //Caracter
	M->TZ4_DECIMA := 0
	M->TZ4_PICTUR := Padr("@!", Len(M->TZ4_PICTUR) )
	M->TZ4_OPCOES := Space( Len(M->TZ4_OPCOES) )
ElseIf cTipo == "2" //Numerico
	M->TZ4_PICTUR := Space( Len(M->TZ4_PICTUR) )
	M->TZ4_OPCOES := Space( Len(M->TZ4_OPCOES) )
ElseIf cTipo == "3" //Logico
	M->TZ4_TAMANH := 1
	M->TZ4_DECIMA := 0
	M->TZ4_PICTUR := Space( Len(M->TZ4_PICTUR) )
	M->TZ4_OPCOES := Space( Len(M->TZ4_OPCOES) )
ElseIf cTipo == "4" //Data
	M->TZ4_TAMANH := 8
	M->TZ4_DECIMA := 0
	M->TZ4_PICTUR := Padr("99/99/9999", Len(M->TZ4_PICTUR) )
	M->TZ4_OPCOES := Space( Len(M->TZ4_OPCOES) )
ElseIf cTipo == "5" //Campo
	M->TZ4_TAMANH := 0
	M->TZ4_DECIMA := 0
	M->TZ4_PICTUR := Space( Len(M->TZ4_PICTUR) )
	M->TZ4_OPCOES := Space( Len(M->TZ4_OPCOES) )
ElseIf cTipo == "6" //Campo
	M->TZ4_TAMANH := 1
	M->TZ4_DECIMA := 0
	M->TZ4_PICTUR := Space( Len(M->TZ4_PICTUR) )
Endif

If cTipo != "5" //Campo
	M->TZ4_CAMPOS := Space( Len(M->TZ4_CAMPOS) )
	M->TZ4_TABELA := Space( Len(M->TZ4_TABELA) )
	M->TZ4_F3     := Space( Len(M->TZ4_F3) )
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fNG004F3  ³ Autor ³ Denis Hyroshi de Souza³ Data ³02/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Seleciona campos                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGIND004                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fNG004F3()

Local lOk      := .F.
Local aAreaXXX := GetArea()
Local aAreaSXB := SXB->(GetArea())
Local nPosCpo1 := 0
Local oList01, oDlg

Private cCodConsu := M->TZ4_F3

If Empty(M->TZ4_TABELA)
	MsgInfo(STR0011,STR0012) //"O campo Tabela está vazio."###"Atenção"
	Return .F.
Endif

If Type("aTabConF3") <> "A"
	aTabConF3 := {}
Endif

//Carrega as consultas padrao (F3)
If Len(aTabConF3) == 0
	Processa({|lEnd| aTabConF3 := fLoadF3() }) //Carrega os tipos de consulta padrao (F3)
Endif

If Len(aTabConF3) == 0
	MsgInfo(STR0028,STR0012) //"Nenhuma consulta padrão foi encontrado."###"Atenção"
	Return .F.
Endif

aSort(aTabConF3,,,{|x,y| x[1] < y[1] })

DEFINE MSDIALOG oDlg FROM 150,005 TO 425,361  TITLE OemToAnsi(STR0029) PIXEL //"Selecione a Consulta Padrão (F3)"

	@ 08,008 Say OemToAnsi(STR0030) Of oDlg Pixel //"Consulta (F3)"
	@ 08,050 MSGET cCodConsu Size 30,09 Valid fF3Pes04(@oList01,.T.) Picture "@!" Of oDlg Pixel
	@ 08,098 Button oBtnCcc Prompt STR0002 Size 30,11 Of oDlg Pixel Action fF3Pes04(@oList01,.T.) //"Pesquisar"

	@ 022,007 LISTBOX oList01 FIELDS HEADER STR0031,STR0032 SIZE 165,100 OF oDlg PIXEL ; //"Consulta"###"Descrição"
		ON DBLCLICK ( lOk := .T., nPosCpo1:=oList01:nAt,oDlg:End() )

	fF3Pes04(@oList01,.F.)
	oList01:SetArray(aTabConF3)
	oList01:bLine := { || {aTabConF3[oList01:nAt,1] , aTabConF3[oList01:nAt,2]}}
	cCodConsu := Space(6)

	DEFINE SBUTTON FROM 126, 116 TYPE 1 ENABLE OF oDlg ACTION ( lOk := .T., nPosCpo1:=oList01:nAt,oDlg:End() )
	DEFINE SBUTTON FROM 126, 144 TYPE 2 ENABLE OF oDlg ACTION ( lOk := .F., oDlg:End() )

ACTIVATE MSDIALOG oDlg CENTERED

If ( lOk )
	If VALTZ4XB(aTabConF3[nPosCpo1,1])
		M->TZ4_F3 := aTabConF3[nPosCpo1,1]
	Endif
EndIf

RestArea(aAreaSXB)
RestArea(aAreaXXX)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fF3Pes04 ³ Autor ³ Denis Hyroshi de Souza³ Data ³02/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pesquisa se existe a Consulta informada pelo usuario       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGIND004                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fF3Pes04(oList01,lMsg)
Local nPos := aScan( aTabConF3, {|x| Substr(x[1],1,Len(Trim(cCodConsu))) == Trim(cCodConsu) } )

If !Empty(cCodConsu)
	If nPos > 0
		oList01:nAt := nPos
		oList01:Refresh()
		oList01:SetFocus()
	ElseIf lMsg
		MsgInfo(STR0033,STR0012) //"Consulta Padrão não foi localizada."###"Atenção"
		Return .F.
	Endif
Endif

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fCpPes04 ³ Autor ³ Denis Hyroshi de Souza³ Data ³02/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pesquisa se existe o campo informado pelo usuario          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGIND004                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fCpPes04(oList02,aCpoTab,lMsg)
Local nPos := aScan( aCpoTab, {|x| Substr(x[1],1,Len(Trim(cCodCampo))) == Trim(cCodCampo) } )

If !Empty(cCodCampo)
	If nPos > 0
		oList02:nAt := nPos
		oList02:Refresh()
		oList02:SetFocus()
	ElseIf lMsg
		MsgInfo(STR0034,STR0012) //"Campo não foi localizado."###"Atenção"
		Return .F.
	Endif
Endif

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} fLoadF3
Combo Box para selecao da consulta padrao

@author Denis Hyroshi de Souza
@since 02/07/2008
@version P12

@return aOpcoes, Array, Contêm as consultas padrões.
/*/
//------------------------------------------------------------------------------
Static Function fLoadF3()
Local aArea   := GetArea()
Local aOpcoes := {}
Local cQuery  := ""
Local cAliasSX5 := GetNextAlias()

Private bFiltraBrw

aAdd(aOpcoes,{ Space(6) , Space(30) })

//|--------------|
//| Filtra SXB   |
//|--------------|
dbSelectArea("SXB")
Set Filter To SXB->XB_TIPO == '1'

ProcRegua(SXB->(RecCount())/6)

dbSelectArea("SXB")
dbSetOrder(1)
dbGoTop()
While !Eof()
	IncProc()
	aAdd(aOpcoes, { SXB->XB_ALIAS , Substr(XBDESCRI(),1,30) } )
	dbSkip()
End

cQuery := " SELECT X5_CHAVE AS CHAVE, X5_DESCRI AS DESCRI FROM " + RetSQLName( "SX5" )
cQuery += " WHERE X5_TABELA = '00' "
cQuery := ChangeQuery( cQuery )
MPSysOpenQuery( cQuery , cAliasSX5 )

dbSelectArea(cAliasSX5)
dbGoTop()
While (cAliasSX5)->(!Eof())
	aAdd(aOpcoes, { (cAliasSX5)->CHAVE , Substr((cAliasSX5)->DESCRI,1,30) } )
	(cAliasSX5)->(dbSkip())
End

( cAliasSX5 )->( dbCloseArea() )

//|-------------------------------------------------|
//| Deleta o filtro utilizando a funcao FilBrowse   |
//|-------------------------------------------------|
dbSelectArea("SXB")
Set Filter to

RestArea(aArea)

Return aOpcoes
