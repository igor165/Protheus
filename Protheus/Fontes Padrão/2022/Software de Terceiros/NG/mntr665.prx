#INCLUDE "Mntr665.ch" 
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR665  ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 21/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Resumo de Ordens de Servico Preventivas x Corretivas       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR665() 

	Local  WNREL     := "MNTR665"
	Local  LIMITE    := 132 
	Local  cdesc1    := STR0001 //"Relatorio de resumo das ordens de servico separando preventivas"
	Local  cdesc2    := STR0002 //"e corretivas, classificado por familia e bem"
	Local  cdesc3    := " "
	Local  cSTRING   := "ST9"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Guarda conteudo e declara variaveis padroes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := NGBEGINPRM( )

	Private NOMEPROG  := "MNTR665"
	Private TAMANHO   := "M"
	Private aRETURN   := {STR0003,1,STR0004,1,2,1,"",1}  //"Zebrado"###"Administracao"
	Private nTIPO     := 0
	Private nLASTKEY  := 0
	Private Titulo    := STR0005      //"Resumo de Ordens de Servico Preventivas x Corretivas"
	PRIVATE cPERG     := "MNT665"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as perguntas selecionadas                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     // Do  Familia	                                 ³
	//³ mv_par02     // Ate Familia  				   				 ³
	//³ mv_par03     // Da Bem                                       ³
	//³ mv_par04     // Ate Bem                                      ³
	//³ mv_par05     // De Data                                      ³
	//³ mv_par06     // Ate Data                                     ³
	//³ mv_par07     // Lista   (Todas, Abertas, Encerradas)         |
	//| mv_par08     // Imprimir Localizacao ?  Sim/Nao              |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	PERGUNTE(cPERG,.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	WNREL:=SETPRINT(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	IF nLASTKEY = 27
		SET FILTER TO
		DBSELECTAREA("ST9")
		RETURN
	ENDIF     
	SETDEFAULT(aRETURN,cSTRING)
	IF nLASTKEY = 27
		SET FILTER TO
		DBSELECTAREA("ST9")
		RETURN
	ENDIF

	RPTSTATUS({|lEND|MNTR665IM(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
	DBSELECTAREA("ST9")

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Retorna conteudo de variaveis padroes       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

RETURN NIL
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR665IM ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 21/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR665                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR665IM(lEND,WNREL,TITULO,TAMANHO)
	
	LOCAL cRODATXT := "",xx
	LOCAL nCNTIMPR := 0
	Local isPrint  := .F.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis locais exclusivas deste programa                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	PRIVATE cUltimoReg, cCondicao, nAtual
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contadores de linha e pagina                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PRIVATE li := 80 ,m_pag := 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se deve comprimir ou nao                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTIPO  := IIF(aRETURN[4]==1,15,18)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta os Cabecalhos                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	CABEC1 := ""
	CABEC2 := "" 

	//          1         2         3         4         5         6         7         8         9         0         1         2         3
	//0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	//------------------------------------------------------------------------------------------------------------------------------------
	//Ordens de Servico Por Familia e Bem                           Periodo de: xx/xx/xxxx ate xx/xx/xxxx
	//------------------------------------------------------------------------------------------------------------------------------------
	//Familia....: 07  Veiculos 			          
	//-----------------------Corretivas------------------------     -----------------------Preventivas-----------------------
	//Bem               Servico  Ordem   Dt. Aber.   Dt. Fech.      Bem               Servico  Ordem   Dt. Aber.   Dt. Fech.
	//xxxxxxxxxxxxxxxx  xxxxxx   xxxxxx  99/99/9999  99/99/9999     xxxxxxxxxxxxxxxx  xxxxxx   xxxxxx  99/99/9999  99/99/9999
	//xxxxxxxxxxxxxxxx  xxxxxx   xxxxxx  99/99/9999  99/99/9999     xxxxxxxxxxxxxxxx  xxxxxx   xxxxxx  99/99/9999  99/99/9999
	//xxxxxxxxxxxxxxxx  xxxxxx   xxxxxx  99/99/9999  99/99/9999     xxxxxxxxxxxxxxxx  xxxxxx   xxxxxx  99/99/9999  99/99/9999
	// 
	//Total do Bem.....: 3                                          3 
	//Total da Familia.: 3                                          3
	//
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta area de trabalho                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ LEITURA DAS ORDENS DE SERVICO                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	xx := 0
	DBSELECTAREA("ST6")
	DBSETORDER(1)
	DBSEEK(xFILIAL("ST6")+MV_PAR01,.T.)
	SETREGUA(LASTREC())   

	DO WHILE !EOF() .AND.;
	ST6->T6_FILIAL == xFILIAL("ST6") .AND.;
	ST6->T6_CODFAMI <= MV_PAR02

		lPrint := .t.
		nTotc := 0
		nTotp := 0     

		DBSELECTAREA("ST9")
		DBSETORDER(4)
		DBSEEK(xFILIAL("ST9")+ST6->T6_CODFAMI)

		DO WHILE !EOF() .AND.;
		ST9->T9_FILIAL == xFILIAL("ST9") .AND.;
		ST9->T9_CODFAMI <= ST6->T6_CODFAMI

			INCREGUA()
			IF lEND
				@ PROW()+1,001 PSAY STR0006 //"CANCELADO PELO OPERADOR"
				EXIT
			ENDIF 

			IF ST9->T9_CODBEM < MV_PAR03 .OR. ST9->T9_CODBEM > MV_PAR04
				DBSELECTAREA("ST9")
				DBSKIP()         
				LOOP
			ENDIF

			aDADOS  := {}
			aESPER1 := {}
			aESPER2 := {}         

			DBSELECTAREA("STS")
			DBSETORDER(2)
			DBSEEK(xFILIAL("STS")+"B"+ST9->T9_CODBEM)

			DO WHILE !EOF() .AND.;
			STS->TS_FILIAL == xFILIAL("STS") .AND.;
			STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM = ST9->T9_CODBEM

				IF STS->TS_DTORIGI < MV_PAR05 .OR. STS->TS_DTORIGI > MV_PAR06
					DBSKIP()         
					LOOP
				ENDIF  

				If STS->TS_CCUSTO < PadR( MV_PAR09, Len( STS->TS_CCUSTO ) ) .Or. STS->TS_CCUSTO > PadR( MV_PAR10, Len( STS->TS_CCUSTO ) )
					dbSkip()         
					Loop
				EndIf 

				IF STS->TS_SITUACA = "C"
					DBSKIP()    
					LOOP
				ENDIF    

				IF MV_PAR07 = 2 .AND. STS->TS_TERMINO = "S"
					DBSKIP()    
					LOOP
				ENDIF    

				IF MV_PAR07 = 3 .AND. STS->TS_TERMINO = "N"
					DBSKIP()    
					LOOP
				ENDIF  

				IF STS->TS_PLANO = "000000"
					AAdd(aDADOS,{STS->TS_DTMRFIM,"C",STS->TS_ORDEM,STS->TS_SERVICO,STS->TS_DTORIGI})
				ELSE
					AAdd(aDADOS,{STS->TS_DTMRFIM,"P",STS->TS_ORDEM,STS->TS_SERVICO,STS->TS_DTORIGI})
				ENDIF      

				DBSELECTAREA("STS")
				DBSKIP()                      
			ENDDO

			DBSELECTAREA("STJ")
			DBSETORDER(2)
			DBSEEK(xFILIAL("STJ")+"B"+ST9->T9_CODBEM)

			DO WHILE !EOF() .AND.;
			STJ->TJ_FILIAL == xFILIAL("STJ") .AND.;
			STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM = ST9->T9_CODBEM

				IF STJ->TJ_DTORIGI < MV_PAR05 .OR. STJ->TJ_DTORIGI > MV_PAR06
					DBSKIP()         
					LOOP
				ENDIF

				If STJ->TJ_CCUSTO < PadR( MV_PAR09, Len( STJ->TJ_CCUSTO ) ) .Or. STJ->TJ_CCUSTO > PadR( MV_PAR10, Len( STJ->TJ_CCUSTO ) )
					dbSkip()         
					Loop
				EndIf   

				IF STJ->TJ_SITUACA = "C"
					DBSKIP()    
					LOOP
				ENDIF

				IF MV_PAR07 = 2 .AND. STJ->TJ_TERMINO = "S"
					DBSKIP()    
					LOOP
				ENDIF    

				IF MV_PAR07 = 3 .AND. STJ->TJ_TERMINO = "N"
					DBSKIP()    
					LOOP
				ENDIF                  

				IF STJ->TJ_PLANO = "000000"
					AAdd(aDADOS,{STJ->TJ_DTMRFIM,"C",STJ->TJ_ORDEM,STJ->TJ_SERVICO,STJ->TJ_DTORIGI})
				ELSE
					AAdd(aDADOS,{STJ->TJ_DTMRFIM,"P",STJ->TJ_ORDEM,STJ->TJ_SERVICO,STJ->TJ_DTORIGI})
				ENDIF      

				DBSELECTAREA("STJ")
				DBSKIP()                      
			ENDDO

			Flag1 := .F.
			Flag2 := .F.
			lPrintLoc := .F.
			nC := 1
			nP := 1
			nEC := 0
			nEP := 0
			nOSC := 0
			nOSP := 0

			IF LEN(aDADOS) > 0 

				isPrint := .T.

				If lPrint
					SOMALINHA() 
					@ LI,000 PSAY STR0007 //"Familia....:"
					Dbselectarea("ST6") 
					Dbsetorder(1)
					Dbseek(xFilial("ST6")+ST9->T9_CODFAMI)
					@ LI,017 PSAY ST6->T6_NOME
					Somalinha()
					@ LI,0 PSAY STR0008 //"-----------------------Corretivas-----------------------      -----------------------Preventivas----------------------"
					somalinha()
					@ LI,000 PSAY STR0009 //"Bem               Servico  Ordem   Dt. Aber.   Dt. Fech.      Bem               Servico  Ordem   Dt. Aber.   Dt. Fech."
					lPrint := .f.  
				Endif

				FOR xx := 1 TO LEN(aDADOS)

					If MV_PAR08 == 1
						If !lPrintLoc
							cLoc := NGLocComp(ST9->T9_CODBEM,'1')
							If !Empty(cLoc)
								cLoc := STR0015+": "+cLoc  //"Localização"
								While Len(cLoc) > 0
									Somalinha()
									@ LI,000 PSAY SubStr(cLoc,1,118)
									cLoc := SubStr(cLoc,119)
									cLoc := If(!Empty(cLoc),Space(Len(STR0015+": ")),"")+cLoc  //"Localização"
								EndDo
								SomaLinha()
							EndIf
							lPrintLoc := .T.
						EndIf
					EndIf

					If aDADOS[xx][2] = "C"
						If Flag1 = .f.
							nOSC +=1  
							Somalinha()
							@ LI,000 PSAY ST9->T9_CODBEM
							@ LI,018 PSAY aDADOS[xx][4]
							@ LI,027 PSAY aDADOS[xx][3]	  	  
							@ LI,035 PSAY aDADOS[xx][5]
							@ LI,047 PSAY aDADOS[xx][1]
							Flag1 := .t.
							IF LEN(aESPER2) > 0 .and. nEP > 0
								nOSP +=1
								@ LI,062 PSAY ST9->T9_CODBEM
								@ LI,080 PSAY aESPER2[nP][4]
								@ LI,089 PSAY aESPER2[nP][3]
								@ LI,097 PSAY aESPER2[nP][5]
								@ LI,109 PSAY aESPER2[nP][1]
								nP += 1
								nEP -= 1
								If nEP >= 0
									Flag1 := .f.
								Endif
							Endif
						Else
							AAdd(aESPER1,{aDADOS[xx][1],"C",aDADOS[xx][3],aDADOS[xx][4],aDADOS[xx][5]}) 
							nEC += 1
						Endif  

					Elseif aDADOS[xx][2] = "P"
						If Flag1 = .t.
							nOSP +=1
							@ LI,062 PSAY ST9->T9_CODBEM
							@ LI,080 PSAY aDADOS[xx][4]
							@ LI,089 PSAY aDADOS[xx][3]	  	  
							@ LI,097 PSAY aDADOS[xx][5]
							@ LI,109 PSAY aDADOS[xx][1]
							Flag1 := .f.
							IF LEN(aESPER1) > 0 .and. nEC > 0
								nOSC +=1  
								Somalinha()
								@ LI,000 PSAY ST9->T9_CODBEM
								@ LI,018 PSAY aESPER1[nC][4]
								@ LI,027 PSAY aESPER1[nC][3]	  	  
								@ LI,035 PSAY aESPER1[nC][5] 
								@ LI,047 PSAY aESPER1[nC][1] 
								nC += 1 
								nEC -= 1 
								If nEC >= 0
									Flag1 := .t.
								Endif        
							Endif 	  
						Else
							AAdd(aESPER2,{aDADOS[xx][1],"P",aDADOS[xx][3],aDADOS[xx][4],aDADOS[xx][5]}) 
							nEP += 1
						Endif  
					Endif

				NEXT xx
			
			Endif		  	  

			IF nEC > 0
				For xx := 1 to nEC 
					Somalinha()     
					nOSC +=1
					@ LI,000 PSAY ST9->T9_CODBEM
					@ LI,018 PSAY aESPER1[nC][4]
					@ LI,027 PSAY aESPER1[nC][3]	  	  
					@ LI,035 PSAY aESPER1[nC][5]
					@ LI,047 PSAY aESPER1[nC][1]
					nC += 1
				Next xx   
			ElseIF nEP > 0
				For xx := 1 to nEP 
					nOSP +=1
					Somalinha()
					@ LI,062 PSAY ST9->T9_CODBEM
					@ LI,080 PSAY aESPER2[nP][4]
					@ LI,089 PSAY aESPER2[nP][3]	  	  
					@ LI,097 PSAY aESPER2[nP][5]
					@ LI,109 PSAY aESPER2[nP][1]
					nP += 1
				Next xx   
			Endif

			If nOSP > 0 .or. nOSC > 0 
				SOMALINHA()
				SOMALINHA()
				@ LI,000 PSAY STR0010 //"Total do Bem.....:"
				@ LI,019 PSAY nOSC Picture "999999"
				@ LI,062 PSAY nOSP
				nTotc += nOSC
				nTotp += nOSP 
				SOMALINHA()
			Endif	         	

			Dbselectarea("ST9")
			Dbskip()    	
		ENDDO 
		If nTotc > 0 .or. nTotp > 0
			@ LI,000 PSAY STR0011 //"Total da Familia.:"
			@ LI,019 PSAY nTotc Picture "999999"                
			@ LI,062 PSAY nTotp         
			SOMALINHA()
		Endif
		Dbselectarea("ST6")
		DBskip()        	
	ENDDO   

	If !isPrint

		ApMsgInfo( STR0029, STR0030 ) // Não há dados para impressão! ## Aviso
		Return
	
	EndIf

	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Apaga arquivo de Trabalho                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	RETINDEX("ST9")
	RETINDEX("STS")
	RETINDEX("STJ")
	SET FILTER TO
	SET DEVICE TO SCREEN
	IF aRETURN[5] == 1
		SET PRINTER TO
		dbCommitAll()
		OURSPOOL(WNREL)
	ENDIF
	MS_FLUSH()
RETURN NIL
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³Somalinha ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 21/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ Somalinha  ()                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Somalinha()
	Li := Li + 1
	If Li > 58
		CABEC(TITULO,CABEC1,CABEC2,NOMEPROG,TAMANHO,nTIPO)
		Cabecalh()  
	EndIf
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ Cabecalh ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 21/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Cabecalh da Ordem de Servico                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ Cabecalh()                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Cabecalh()    
	@ li,000 PSAY STR0012    //"Ordens de Servico Por Familia e Bem"
	@ li,062 PSAY STR0013 //"Periodo de:"
	@ li,074 PSAY mv_par05 
	@ li,085 PSAY STR0014 //"ate"
	@ li,090 PSAY mv_par06                                                                       
	somalinha()
	@ li,000 PSAY REPLICATE(chr(45),132) 
	li := prow()+1
RETURN
