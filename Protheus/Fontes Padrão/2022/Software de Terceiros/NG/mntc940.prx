#INCLUDE "mntc940.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 3 //Versao do fonte

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC940
Consulta Horas Homens por C.Custo ( mes )   
@author  Inacio Luiz Kolling
@since  19/03/98
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC940()
	//+-----------------------------------------------------------------------+
	//| Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  |
	//+-----------------------------------------------------------------------+
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

	Local oList
	Local oTmpTbl1
		
	Private aSize       := MsAdvSize()
	Private aObjects    := {}
	Private cCadastro := OemToAnsi(STR0001) //"Horas Homem por Servico no Ano"
	Private nprevist,nrealiza,hprevist,hrealiza,nCusto,nQTD1,nFol,nTempo,nSem
	Private ntotger,nJAN,nFEV,nMAR,nABR,nMAI,nJUN,nJUL,nAGO,nSETE,nOUT,nNOV,nDEZ
	
	Store 0.00 To nprevist,nrealiza,hprevist,hrealiza,nCusto,nQTD1,nFol
	Store 0    To nTempo,nSem
	
	Private vVETHORAS := {}
	Private cTRB := GetNextAlias() 

	aAdd(aObjects, { 015, 020, .T., .T. } )
	aAdd(aObjects, { 100, 100, .T., .T. } )
	aInfo   := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.T.)

	aRotina := {{ STR0002 ,"AxPesqui" , 0 , 1},; //"Pesquisar"
				{ STR0003 ,"NGCAD01" , 0 , 2}}   //"Visualizar"

	cCadastro := OemToAnsi(cCadastro)
	//+--------------------------------------------------------------+
	//| Variaveis utilizadas para parametros                         |
	//| mv_par01     // Ano                                          |
	//| mv_par02     // De  Servi‡o                                  |
	//| mv_par03     // Ate Servi‡o                                  |
	//| mv_par04     // De Centro Custo                              |
	//| mv_par05     // Ate Centro Custo                             |
	//+--------------------------------------------------------------+

	If !Pergunte("MNT94G",.T.)
		Return
	EndIf

	nMV_PAR01 := MV_PAR01
	nMV_PAR02 := MV_PAR02
	nMV_PAR03 := MV_PAR03                     
	nMV_PAR04 := MV_PAR04
	nMV_PAR05 := MV_PAR05

	aDBF := {{"SERVICO" , "C",06,0},;
			 {"JAN"     , "N",12,2},;
			 {"JANP"    , "N",06,2},;
			 {"FEV"     , "N",12,2},;
			 {"FEVP"    , "N",06,2},;
			 {"MAR"     , "N",12,2},;
			 {"MARP"    , "N",06,2},;
			 {"ABR"     , "N",12,2},;
			 {"ABRP"    , "N",06,2},;
			 {"MAI"     , "N",12,2},;
			 {"MAIP"    , "N",06,2},;
			 {"JUN"     , "N",12,2},;
			 {"JUNP"    , "N",06,2},;
			 {"JUL"     , "N",12,2},;
			 {"JULP"    , "N",06,2},;
			 {"AGO"     , "N",12,2},;
			 {"AGOP"    , "N",06,2},;
			 {"SETE"    , "N",12,2},;
			 {"SETP"    , "N",06,2},;
			 {"OUT"     , "N",12,2},;
			 {"OUTP"    , "N",06,2},;
			 {"NOV"     , "N",12,2},;
			 {"NOVP"    , "N",06,2},;
			 {"DEZ"     , "N",12,2},;
			 {"DEZP"    , "N",06,2},;
			 {"TOT"     , "N",12,2},;
			 {"TOTP"    , "N",06,2}}

	//Intancia classe FWTemporaryTable
	oTmpTbl1:= FWTemporaryTable():New( cTRB, aDBF )
	//Adiciona os Indices
	oTmpTbl1:AddIndex( "Ind01" , {"SERVICO"} )
	//Cria a tabela temporaria
	oTmpTbl1:Create()

	Processa({|| C940PRCO()})   // STJ & STS - HISTORICO
	Store 0.00 To ntotger,nJAN,nFEV,nMAR,nABR,nMAI,nJUN,nJUL,nAGO,nSETE,nOUT,nNOV,nDEZ

	DbSelectArea(cTRB)
	DbGoTop()
	If Reccount() = 0
		nDel := Help(" ",1,"NGTDADGRAF") //"Nao existem dados para montar o grafico..."###"ATENCAO"

		oTmpTbl1:Delete()

		Return .T.
	Else
		While !Eof()

			// CALCULA O TOTAL GERAL DO MES
			ntot  := 0.00
			ntot  := (cTRB)->JAN+(cTRB)->FEV+(cTRB)->MAR+(cTRB)->ABR+(cTRB)->MAI+(cTRB)->JUN+(cTRB)->JUL+(cTRB)->AGO+(cTRB)->SETE+(cTRB)->OUT+NOV+(cTRB)->DEZ

			// ACUMULA O TOTAL GERAL DOS MES
			ntotger := ntotger + ntot

			// ACUMULA O TOTAL DO MES
			nJAN  := nJAN  + (cTRB)->JAN
			nFEV  := nFEV  + (cTRB)->FEV
			nMAR  := nMAR  + (cTRB)->MAR
			nABR  := nABR  + (cTRB)->ABR
			nMAI  := nMAI  + (cTRB)->MAI
			nJUN  := nJUN  + (cTRB)->JUN
			nJUL  := nJUL  + (cTRB)->JUL
			nAGO  := nAGO  + (cTRB)->AGO
			nSETE := nSETE + (cTRB)->SETE
			nOUT  := nOUT  + (cTRB)->OUT
			nNOV  := nNOV  + (cTRB)->NOV
			nDEZ  := nDEZ  + (cTRB)->DEZ

			// GRAVA O TOTAL DO MES
			(cTRB)->TOT := ntot

			DbSelectArea(cTRB)
			Dbskip()
		End

		DbSelectArea(cTRB)
		DbGoTop()
		While !Eof()

			// GRAVA OS PERCENTUAIS DO MES ( SERVICO )

			(cTRB)->JANP := (cTRB)->JAN  / nJAN  * 100
			(cTRB)->FEVP := (cTRB)->FEV  / nFEV  * 100
			(cTRB)->MARP := (cTRB)->MAR  / nMAR  * 100
			(cTRB)->ABRP := (cTRB)->ABR  / nABR  * 100
			(cTRB)->MAIP := (cTRB)->MAI  / nMAI  * 100
			(cTRB)->JUNP := (cTRB)->JUN  / nJUN  * 100
			(cTRB)->JULP := (cTRB)->JUL  / nJUL  * 100
			(cTRB)->AGOP := (cTRB)->AGO  / nAGO  * 100
			(cTRB)->SETP := (cTRB)->SETE / nSETE * 100
			(cTRB)->OUTP := (cTRB)->OUT  / nOUT  * 100
			(cTRB)->NOVP := (cTRB)->NOV  / nNOV  * 100
			(cTRB)->DEZP := (cTRB)->DEZ  / nDEZ  * 100

			(cTRB)->TOTP := (cTRB)->TOT / ntotger * 100

			DbSelectArea(cTRB)
			Dbskip()
		End

		DbSelectArea(cTRB)
		(cTRB)->(DbAppend())
		(cTRB)-> SERVICO := 'TOTAL>'

		// GRAVA OS TOTAIS GERAL DO MES
		(cTRB)->JAN  := nJAN
		(cTRB)->FEV  := nFEV
		(cTRB)->MAR  := nMAR
		(cTRB)->ABR  := nABR
		(cTRB)->MAI  := nMAI
		(cTRB)->JUN  := nJUN
		(cTRB)->JUL  := nJUL
		(cTRB)->AGO  := nAGO
		(cTRB)->SETE := nSETE
		(cTRB)->OUT  := nOUT
		(cTRB)->NOV  := nNOV
		(cTRB)->DEZ  := nDEZ 

		// GRAVA OS PERCENTUAL GERAL DO MES

		(cTRB)->JANP := nJAN  / ntotger * 100
		(cTRB)->FEVP := nFEV  / ntotger * 100
		(cTRB)->MARP := nMAR  / ntotger * 100
		(cTRB)->ABRP := nABR  / ntotger * 100
		(cTRB)->MAIP := nMAI  / ntotger * 100
		(cTRB)->JUNP := nJUN  / ntotger * 100
		(cTRB)->JULP := nJUL  / ntotger * 100
		(cTRB)->AGOP := nAGO  / ntotger * 100
		(cTRB)->SETP := nSETE / ntotger * 100
		(cTRB)->OUTP := nOUT  / ntotger * 100
		(cTRB)->NOVP := nNOV  / ntotger * 100
		(cTRB)->DEZP := nDEZ  / ntotger * 100

		// GRAVA O TOTAL DO MES

		(cTRB)->TOT  := ntotger
		(cTRB)->TOTP := 100

	EndIf

	DbSelectArea(cTRB)
	DbGoTop()


	Define Dialog oDlg1 From aSize[7],0 To aSize[6],aSize[5] Title cCadastro+" "+str(nMV_PAR01,4) PIXEL

	oPnlTop := TPanel():New(00,00,,oDlg1,,,,,,aSize[5],21,.F.,.F.)
	oPnlTop:Align := CONTROL_ALIGN_TOP

	@ 04,005 Button STR0009 Of oPnlTop Size 40,09 Pixel Action (C940GRAFI()) //"&Grafico"
	@ 04,055 Button STR0010 Of oPnlTop Size 40,09 Pixel Action (C940PRINT()) //"&Imprimir"
	@ 04,105 Button STR0050 Of oPnlTop Size 40,09 Pixel Action (oDlg1:End()) //"&Sair"


	oPnlAll := TPanel():New(00,00,,oDlg1,,,,,,aSize[5],aSize[6],.F.,.F.)
	oPnlAll:Align := CONTROL_ALIGN_ALLCLIENT

	@ 1.3,0.5  Listbox oList Fields  SERVICO                         ,;
	NtoH(JAN) ,;
	PADL(Transform(JANP,"@E 9999.99"),7)    ,;
	NtoH(FEV) ,;
	PADL(Transform(FEVP,"@E 9999.99"),7)    ,;
	NtoH(MAR) ,;
	PADL(Transform(MARP,"@E 9999.99"),7)    ,;
	NtoH(ABR) ,;
	PADL(Transform(ABRP,"@E 9999.99"),7)    ,;
	NtoH(MAI) ,;
	PADL(Transform(MAIP,"@E 9999.99"),7)    ,;
	NtoH(JUN) ,;
	PADL(Transform(JUNP,"@E 9999.99"),7)    ,;
	NtoH(JUL) ,;
	PADL(Transform(JULP,"@E 9999.99"),7)    ,;
	NtoH(AGO) ,;
	PADL(Transform(AGOP,"@E 9999.99"),7)    ,;
	NtoH(SETE) ,;
	PADL(Transform(SETP,"@E 9999.99"),7)    ,;
	NtoH(OUT) ,;
	PADL(Transform(OUTP,"@E 9999.99"),7)    ,;
	NtoH(NOV) ,;
	PADL(Transform(NOVP,"@E 9999.99"),7)    ,;
	NtoH(DEZ) ,;
	PADL(Transform(DEZP,"@E 9999.99"),7)    ,;
	NtoH(TOT) ,;
	PADL(Transform(TOTP ,"@E 9999.99"),7)    ;                         
	FieldSizes 50,40,30,40,30,40,30,40,30,40,30,40,30,40,30,40,30,40,30,40,30,40,30,40,30,40,30;
	HEADERS STR0011                      ,; //"Servico "
	STR0012                      ,; //"       JAN"
	"%"                     ,;
	STR0013                      ,; //"       FEV"
	"%"                     ,;
	STR0014                      ,; //"       MAR"
	"%"                     ,;
	STR0015                      ,; //"       ABR"
	"%"                     ,;
	STR0016                      ,; //"       MAI"
	"%"                     ,;
	STR0017                      ,; //"       JUN"
	"%"                     ,;
	STR0018                      ,; //"       JUL"
	"%"                     ,;
	STR0019                      ,; //"       AGO"
	"%"                     ,;
	STR0020                      ,; //"       SETE"
	"%"                     ,;
	STR0021                      ,; //"       OUT"
	"%"                     ,;
	STR0022                      ,; //"       NOV"
	"%"                     ,;
	STR0023                      ,; //"       DEZ"
	"%"                     ,;
	STR0024                      ,; //"      TOTAL"
	"%",;
	Of oPnlAll

	oList:Align := CONTROL_ALIGN_ALLCLIENT

	Activate Dialog oDlg1

	oTmpTbl1:Delete()

	DbSelectArea("STJ")
	DbSetOrder(01)    

	//+-----------------------------------------------------------------------+
	//| Devolve variaveis armazenadas (NGRIGHTCLICK)                          |
	//+-----------------------------------------------------------------------+
	NGRETURNPRM(aNGBEGINPRM)

Return .T.  

//---------------------------------------------------------------------
/*/{Protheus.doc} C940PRCO
Processa as O.S. atual e O.S. historico
@author  Inacio Luiz Kolling 
@since   /06/97 
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function C940PRCO() 

	cCONDSTJ := 'STJ->TJ_TIPOOS = "B"'
	cCONDSTJ := cCONDSTJ + ' .And. STJ->TJ_SITUACA == "L" .And. STJ->TJ_TERMINO == "S" .AND. !EMPTY(STJ->TJ_DTMRINI)'                         
	cCONDSTJ := cCONDSTJ + ' .And. (STJ->TJ_SERVICO >= nMV_PAR02 .And. STJ->TJ_SERVICO <= nMV_PAR03)'
	cCONDSTJ := cCONDSTJ + ' .And. (STJ->TJ_CCUSTO >= nMV_PAR04 .And. STJ->TJ_CCUSTO <= nMV_PAR05)' 

	cCONDSTS := 'STS->TS_TIPOOS = "B"'
	cCONDSTS := cCONDSTS + ' .And. STS->TS_SITUACA == "L" .And. STS->TS_TERMINO == "S" .AND. !EMPTY(STS->TS_DTMRINI)'                         
	cCONDSTS := cCONDSTS + ' .And. (STS->TS_SERVICO >= nMV_PAR02 .And. STS->TS_SERVICO <= nMV_PAR03)'
	cCONDSTS := cCONDSTS + ' .And. (STS->TS_CCUSTO >= nMV_PAR04 .And. STS->TS_CCUSTO <= nMV_PAR05)' 

	DMV_PAR01 := CTOD('01/01/'+STR(nMV_PAR01,4))

	DbSelectArea("STL")
	DbSetOrder(06)
	DbSeek(xFilial("STL")+DTOS(DMV_PAR01),.T.)
	nReg := (LastRec() - Recno())

	DbSelectArea("STT")
	DbSetOrder(05)
	DbSeek(xFilial("STT")+DTOS(DMV_PAR01),.T.)
	nReg := nReg + (LastRec() - Recno())

	DbSelectArea("STL")
	ProcRegua(nReg)
	While !Eof() .And.  STL->TL_FILIAL == xFilial("STL") .And. year(STL->TL_DTINICI) == nMV_PAR01

		IncProc()

		If Alltrim(STL->TL_SEQRELA) <> "0" .And. STL->TL_TIPOREG == "M"
			DbSelectArea("STJ")
			DbSetOrder(01)  
			If DbSeek(xFilial("STJ")+STL->TL_ORDEM+STL->TL_PLANO)

				If &(cCONDSTJ)

					hrealiza := 0.00
					//PROCURA O CALENDARIO
					//ESTA FUNCAO ESTA NO NGUTIL.PRX

					cCALEND := PROCALEND(STJ->TJ_CODBEM,STJ->TJ_SERVICO,STJ->TJ_SEQRELA)

					vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
					STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
					STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
					nQTDHORAS := vVETHORAS[1]

					// ESTA FUNCAO ESTA NO NGCUSTOS.PRX
					PROCINSUMO(STL->TL_TIPOREG,STL->TL_CODIGO,STL->TL_SEQRELA,;
					If(NGCADICBASE('TL_PCTHREX','A','STL',.F.),STL->TL_PCTHREX,Val(STL->TL_HREXTRA)),nQTDHORAS,STL->TL_DTINICI,;
					STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)

					If hrealiza > 0

						C940GRAVA(STJ->TJ_SERVICO,STL->TL_DTINICI)

					EndIf
				EndIf
			EndIf
		EndIf
		DbSelectArea("STL")
		DbSkip()
	End   

	DbSelectArea("STT")
	While !Eof() .And. STT->TT_FILIAL == xFilial("STT") .And. Year(STT->TT_DTINICI) == nMV_PAR01

		IncProc()
		If Alltrim(STT->TT_SEQRELA) <> "0" .And. STT->TT_TIPOREG == 'M' 

			DbSelectArea("STS")
			DbSetOrder(01)
			If DbSeek(xFilial("STS")+STT->TT_ORDEM+STT->TT_PLANO)

				If &(cCONDSTS)

					hrealiza := 0.00

					// PROCURA O CALENDARIO
					// ESTA FUNCAO ESTA NO NGUTIL.PRX
					cCALEND := PROCALEND(STS->TS_CODBEM,STS->TS_SERVICO,STS->TS_SEQRELA)

					vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
					STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
					STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
					nQTDHORAS := vVETHORAS[1]

					// ESTA FUNCAO ESTA NO NGCUSTOS.PRX
					PROCINSUMO(STT->TT_TIPOREG,STT->TT_CODIGO,STT->TT_SEQRELA,;
					If(NGCADICBASE('TT_PCTHREX','A','STT',.F.),STT->TT_PCTHREX,Val(STT->TT_HREXTRA)),nQTDHORAS,STT->TT_DTINICI,;
					STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)

					If hrealiza > 0

						C940GRAVA(STS->TS_SERVICO,STT->TT_DTINICI)

					EndIf
				EndIf
			EndIf
		EndIf   
		DbSelectArea("STT")
		DbSkip()
	End

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} C940GRAVA
Grava no arquivo auxiliar
@author  Inacio Luiz Kolling
@since   /  /  
@version undefined
@param cservico, characters, descricao
@param ddataini, date, descricao
@type function
/*/
//---------------------------------------------------------------------
Function C940GRAVA(cservico,ddataini)

	DbSelectArea(cTRB)
	If !DbSeek(cservico)

		(cTRB)->(DbAppend())
		(cTRB)->SERVICO := cservico
		If Month(ddataini) = 1
			(cTRB)->JAN := hrealiza
		ElseIf Month(ddataini) = 2
			(cTRB)->FEV := hrealiza
		ElseIf Month(ddataini) = 3
			(cTRB)->MAR := hrealiza
		ElseIf Month(ddataini) = 4
			(cTRB)->ABR := hrealiza
		ElseIf Month(ddataini) = 5
			(cTRB)->MAI := hrealiza
		ElseIf Month(ddataini) = 6
			(cTRB)->JUN := hrealiza
		ElseIf Month(ddataini) = 7
			(cTRB)->JUL := hrealiza
		ElseIf Month(ddataini) = 8
			(cTRB)->AGO := hrealiza
		ElseIf Month(ddataini) = 9
			(cTRB)->SETE := hrealiza
		ElseIf Month(ddataini) = 10
			(cTRB)->OUT := hrealiza
		ElseIf Month(ddataini) = 11
			(cTRB)->NOV := hrealiza
		ElseIf Month(ddataini) = 12
			(cTRB)->DEZ := hrealiza
		EndIf

	Else

		If Month(ddataini) = 1
			(cTRB)->JAN := (cTRB)->JAN + hrealiza
		ElseIf Month(ddataini) = 2
			(cTRB)->FEV := (cTRB)->FEV + hrealiza
		ElseIf Month(ddataini) = 3
			(cTRB)->MAR := (cTRB)->MAR + hrealiza
		ElseIf Month(ddataini) = 4
			(cTRB)->ABR := (cTRB)->ABR + hrealiza
		ElseIf Month(ddataini) = 5
			(cTRB)->MAI := (cTRB)->MAI + hrealiza
		ElseIf Month(ddataini) = 6
			(cTRB)->JUN := (cTRB)->JUN + hrealiza
		ElseIf Month(ddataini) = 7
			(cTRB)->JUL := (cTRB)->JUL + hrealiza
		ElseIf Month(ddataini) = 8
			(cTRB)->AGO := (cTRB)->AGO + hrealiza
		ElseIf Month(ddataini) = 9
			(cTRB)->SETE := (cTRB)->SETE + hrealiza
		ElseIf Month(ddataini) = 10
			(cTRB)->OUT := (cTRB)->OUT + hrealiza
		ElseIf Month(ddataini) = 11
			(cTRB)->NOV := (cTRB)->NOV + hrealiza
		ElseIf Month(ddataini) = 12
			(cTRB)->DEZ := (cTRB)->DEZ + hrealiza
		EndIf

	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} C940GRAFI
/Grafico Custo das O.S. por Servico
@author Inacio Luiz Kolling 
@since   /  /  
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function C940GRAFI()

	Local oTmpTbl2
	
	Private cTRBV := GetNextAlias() 
	
	DbselectArea(cTRB)
	DbGotop()
	If Reccount() = 0
		nDel := Help(" ",1,"NGTDADGRAF") //"Nao existem dados para montar o grafico..."###"ATENCAO"
		Return .T.
	EndIf
	ctilucen := STR0025+str(nMV_PAR01,4) //"Distribuicao de Mao-de-Obra da Manutencao no Ano de "

	// ARQUIVO DE DADOS
	aDBF3 := {{"CODIGO" , "C", 10,0} ,;
			  {"NOMECOD", "C", 30,0} ,;
			  {"VALOR1" , "N", 12,2}}

	//Intancia classe FWTemporaryTable
	oTmpTbl2:= FWTemporaryTable():New( cTRBV, aDBF3 )
	//Adiciona os Indices
	oTmpTbl2:AddIndex( "Ind01" , {"CODIGO","NOMECOD","VALOR1"} )
	//Cria a tabela temporaria
	oTmpTbl2:Create()

	DbselectArea(cTRB)
	DbGotop()
	While !Eof()

		If Recno() <> Reccount()
			(cTRBV)->(DbAppend())
			(cTRBV)->CODIGO  := (cTRB)->SERVICO
			(cTRBV)->NOMECOD := NGSEEK('ST4',(cTRB)->SERVICO,1,'Substr(T4_NOME,1,30)')
			(cTRBV)->VALOR1  := (cTRB)->TOT
		EndIf
		DbSelectArea(cTRB)
		Dbskip()

	End

	// 1- linha titulo do grafico (janela)
	// 2- linha titulo da direita do grafico
	// 3- linha titulo superior do grafico
	// 4- linha titulo da direita do grafico
	// 5- linha titulo da inferior do grafico
	// 6- linha series do grafico
	// 7- leitura ("A" - Arquivo temporario,"M" - Matriz)
	// 8- alias doa arquivo temporario com os dados /ou
	// 9- matriz com os dados


	//+--------------------------------------------------------------+
	//| Alteracao 17/08/2006                                         |
	//| Ricardo Dal Ponte                                            |
	//+--------------------------------------------------------------+
	
	// Incluso o parametro "4", para gerar o grafico com padrao pizza 
	vCRIGTXT := NGGRAFICO(" "+ctilucen," ",ctilucen,STR0026,STR0027,{STR0028},"A",(cTRBV),,"4")

	oTmpTbl2:Delete()

	DbSelectArea(cTRB)
	Dbgotop()

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} C940PRINT
Impressao do Custo das O.S. por Servico 
@author Inacio Luiz Kolling
@since   /  /  
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function C940PRINT()

	Local cString    := (cTRB)
	Local cDesc1     := cCadastro+" "+str(nMV_PAR01,4)+"     "+"Filial..: "+SM0->M0_CODFIL+"  "+SM0->M0_NOME
	Local cDesc2     := ""
	Local cDesc3     := ""
	Local wnrel      := "MNTC940"

	Private aReturn  := { STR0029, 1,STR0030, 1, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := cDesc1
	Private Tamanho  := "G"

	DbSelectArea(cTRB)  
	DbGotop()
	If Reccount() = 0
		nDel := Help(" ",1,"NGTDADIMP") //"Nao existem dados para imprimir..."###"ATENCAO"
		Return .T.
	EndIf

	//+--------------------------------------------------------------+
	//| Envia controle para a funcao SETPRINT                        |
	//+--------------------------------------------------------------+
	wnrel:=SetPrint(cString,wnrel,,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		Return
	EndIf

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| C940Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return Nil 

//---------------------------------------------------------------------
/*/{Protheus.doc} C940Imp
Chamada do Relatório    
@author Inacio Luiz Kolling 
@since 26/06/97
@version undefined
@param lEnd, logical, descricao
@param wnRel, , descricao
@param titulo, , descricao
@param tamanho, , descricao
@type function
/*/
//---------------------------------------------------------------------
Static Function C940Imp(lEnd,wnRel,titulo,tamanho)

	//+------------------------------------------------------------+
	//| Define Variaveis                                           |
	//+------------------------------------------------------------+
	Local cRodaTxt := ""
	Local nCntImpr := 0

	//+------------------------------------------------------------+
	//| Contadores de linha e pagina                               |
	//+------------------------------------------------------------+
	Private li := 80
	m_pag := 1

	//+------------------------------------------------------------+
	//| Verifica se deve comprimir ou nao                          |
	//+------------------------------------------------------------+
	nTipo  := IIF(aReturn[4]==1,15,18)

	Private Cabec1    := STR0032 //"Servico Descricao                                      JANEIRO        %      FEVEREIRO        %          MARCO        %          ABRIL        %           MAIO        %          JUNHO        %"                         
	Private Cabec2    := STR0033 //"                                                         JULHO        %         AGOSTO        %       SETEMBRO        %        OUTUBRO        %       NOVEMBRO        %       DEZEMBRO        %              TOTAL         %"
	Private nomeprog  := "MNTC940"

	/*
	*****************************************************************************************************************************************************************************************************************************
	*<empresa>                                                                                                                                                                                                 Folha..: xxxxx   *
	*SIGA /<nome .04                                                                          <     titulo do relatorio    >                                                                                   DT.Ref.: dd/mm/aa*
	*Hora...: xx:xx:xx                                                                                                                                                                                         Emissao: dd/mm/aa*
	*****************************************************************************************************************************************************************************************************************************
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         200       210     220
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	*****************************************************************************************************************************************************************************************************************************
	Servico Descricao                                      JANEIRO        %      FEVEREIRO        %          MARCO        %          ABRIL        %           MAIO        %          JUNHO        %                          
	JULHO        %         AGOSTO        %       SETEMBRO        %        OUTUBRO        %       NOVEMBRO        %       DEZEMBRO        %              TOTAL         %
	*****************************************************************************************************************************************************************************************************************************
	xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99
	9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99     999.999.999,99  99999,99

	xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99
	9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99   9.999.999,99  9999,99     999.999.999,99  99999,99

	----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

	TOTAL GERAL DOS MESES 

	JANEIRO..: 999.999.999,99  9999,99%  FEVEREIRO.: 999.999.999,99  9999,99%  MARCO....: 999.999.999,99  9999,99%  ABRIL...: 999.999.999,99  9999,99%  MAIO.....: 999.999.999,99  9999,99%  JUNHO....: 999.999.999,99  9999,99%  
	JULHO....: 999.999.999,99  9999,99%  AGOSTO....: 999.999.999,99  9999,99%  SETEMBRO.: 999.999.999,99  9999,99%  OUTUBRO.: 999.999.999,99  9999,99%  NOVEMBRO.: 999.999.999,99  9999,99%  DEZEMBRO.: 999.999.999,99  9999,99%

	TOTAL GERAL-> 999.999.999,99 9999,99% 
	*/ 

	DbSelectArea(cTRB)
	DbGoTop()
	SetRegua(LastRec())
	While !(cTRB)->(Eof())

		IncRegua()
		If Recno() <> Reccount()

			NGSOMALI(58)
			@li,000 Psay (cTRB)->SERVICO Picture '@!  
			@LI,008 Psay NGSEEK('ST4',(cTRB)->SERVICO,1,'T4_NOME')
			@li,050+(12-LEN(NtoH((cTRB)->JAN))) Psay NtoH((cTRB)->JAN)  
			@li,064 Psay (cTRB)->JANP Picture '@E 9999.99'
			@li,074+(12-LEN(NtoH((cTRB)->FEV))) Psay NtoH((cTRB)->FEV)  
			@li,088 Psay (cTRB)->FEVP Picture '@E 9999.99'
			@li,098+(12-LEN(NtoH((cTRB)->MAR))) Psay NtoH((cTRB)->MAR)  
			@li,112 Psay (cTRB)->MARP Picture '@E 9999.99'
			@li,122+(12-LEN(NtoH((cTRB)->ABR))) Psay NtoH((cTRB)->ABR)  
			@li,136 Psay (cTRB)->ABRP Picture '@E 9999.99'
			@li,146+(12-LEN(NtoH((cTRB)->MAI))) Psay NtoH((cTRB)->MAI)  
			@li,160 Psay (cTRB)->MAIP Picture '@E 9999.99'
			@li,170+(12-LEN(NtoH((cTRB)->JUN))) Psay NtoH((cTRB)->JUN)  
			@li,184 Psay (cTRB)->JUNP Picture '@E 9999.99'

			NGSOMALI(58)
			@li,050+(12-LEN(NtoH((cTRB)->JUL))) Psay NtoH((cTRB)->JUL)  
			@li,064 Psay (cTRB)->JULP Picture '@E 9999.99'
			@li,074+(12-LEN(NtoH((cTRB)->AGO))) Psay NtoH((cTRB)->AGO)  
			@li,088 Psay (cTRB)->AGOP Picture '@E 9999.99'
			@li,098+(12-LEN(NtoH((cTRB)->SETE))) Psay NtoH((cTRB)->SETE)  
			@li,112 Psay (cTRB)->SETP Picture '@E 9999.99'
			@li,122+(12-LEN(NtoH((cTRB)->OUT))) Psay NtoH((cTRB)->OUT)  
			@li,136 Psay (cTRB)->OUTP Picture '@E 9999.99'
			@li,146+(12-LEN(NtoH((cTRB)->NOV))) Psay NtoH((cTRB)->NOV)  
			@li,160 Psay (cTRB)->NOVP Picture '@E 9999.99'
			@li,170+(12-LEN(NtoH((cTRB)->DEZ))) Psay NtoH((cTRB)->DEZ)  
			@li,184 Psay (cTRB)->DEZP Picture '@E 9999.99'

			@li,196+(14-LEN(NtoH((cTRB)->TOT))) Psay NtoH((cTRB)->TOT)  
			@li,213 Psay (cTRB)->TOTP Picture '@E 9999.99'
			NGSOMALI(58)

		Else 
			NGSOMALI(58)
			@li,000 Psay Replicate("-",220)
			NGSOMALI(58)
			@li,000 Psay STR0035 //"TOTAL GERAL DOS MESES"

			NGSOMALI(58) 
			NGSOMALI(58)
			@li,000 Psay STR0036 //"JANEIRO..:"
			@li,011+(12-LEN(NtoH((cTRB)->JAN))) Psay NtoH((cTRB)->JAN)  
			@li,027 Psay (cTRB)->JANP Picture '@E 9999.99%'   
			@li,037 Psay STR0037 //"FEVEREIRO.:"
			@li,049+(12-LEN(NtoH((cTRB)->FEV))) Psay NtoH((cTRB)->FEV)  
			@li,065 Psay (cTRB)->FEVP Picture '@E 9999.99%'   
			@li,075 Psay STR0038 //"MARCO....:"
			@li,086+(12-LEN(NtoH((cTRB)->MAR))) Psay NtoH((cTRB)->MAR)  
			@li,102 Psay (cTRB)->MARP Picture '@E 9999.99%'   
			@li,112 Psay STR0039 //"ABRIL...:"
			@li,122+(12-LEN(NtoH((cTRB)->ABR))) Psay NtoH((cTRB)->ABR)  
			@li,138 Psay (cTRB)->ABRP Picture '@E 9999.99%'   
			@li,148 Psay STR0040 //"MAIO.....:" 
			@li,159+(12-LEN(NtoH((cTRB)->MAI))) Psay NtoH((cTRB)->MAI)  
			@li,175 Psay (cTRB)->MAIP Picture '@E 9999.99%'   
			@li,185 Psay STR0041 //"JUNHO....:"
			@li,196+(12-LEN(NtoH((cTRB)->JUN))) Psay NtoH((cTRB)->JUN)  
			@li,212 Psay (cTRB)->JUNP Picture '@E 9999.99%'   

			NGSOMALI(58)
			@li,000 Psay STR0042 //"JULHO....:"
			@li,011+(12-LEN(NtoH((cTRB)->JUL))) Psay NtoH((cTRB)->JUL)  
			@li,027 Psay (cTRB)->JULP Picture '@E 9999.99%'   
			@li,037 Psay STR0043 //"AGOSTO....:"
			@li,049+(12-LEN(NtoH((cTRB)->AGO))) Psay NtoH((cTRB)->AGO)  
			@li,065 Psay (cTRB)->AGOP Picture '@E 9999.99%'   
			@li,075 Psay STR0044 //"SETEMBRO.:"
			@li,086+(12-LEN(NtoH((cTRB)->SETE))) Psay NtoH((cTRB)->SETE)  
			@li,102 Psay (cTRB)->SETP Picture '@E 9999.99%'   
			@li,112 Psay STR0045 //"OUTUBRO.:"
			@li,122+(12-LEN(NtoH((cTRB)->OUT))) Psay NtoH((cTRB)->OUT)  
			@li,138 Psay (cTRB)->OUTP Picture '@E 9999.99%'   
			@li,148 Psay STR0046 //"NOVEMBRO.:"
			@li,159+(12-LEN(NtoH((cTRB)->NOV))) Psay NtoH((cTRB)->NOV)  
			@li,175 Psay (cTRB)->NOVP Picture '@E 9999.99%'   
			@li,185 Psay STR0047 //"DEZEMBRO.:"
			@li,196+(12-LEN(NtoH((cTRB)->DEZ))) Psay NtoH((cTRB)->DEZ)  
			@li,212 Psay (cTRB)->DEZP Picture '@E 9999.99%'   

			NGSOMALI(58) 
			NGSOMALI(58)
			@li,000 Psay STR0048 //"TOTAL GERAL->" 
			@li,014+(14-LEN(NtoH((cTRB)->TOT))) Psay NtoH((cTRB)->TOT)  
			@li,029 Psay (cTRB)->TOTP Picture '@E 9999.99%' 

		EndIf
		(cTRB)->(DbSkip())
	End

	Roda(nCntImpr,cRodaTxt,Tamanho)
	Set Filter To
	Set Device To Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf
	MS_FLUSH()
	DbGotop()

Return Nil