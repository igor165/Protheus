#INCLUDE "MNTR475.CH"
#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR475  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³20/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Media de Comsumo de Combustivel por Equipamento            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR475() 

	Local oReport
	Local aArea := GetArea()
	
	Private cTRB	:= GetNextAlias() //Alias Tabela Temporária
	Private cTRB1	:= GetNextAlias() //Alias Tabela Temporária
	Private vVETHORAS := {}

	Private nTamPro := TamSX3("B1_COD")[1]-15

	SetKey( VK_F9, { | | NGVersao( "MNTR475" , 2 ) } )

	If FindFunction("TRepInUse") .And. TRepInUse()

		Pergunte("MNT475", .T. )

		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetPortrait() //Default Retrato  
		oReport:PrintDialog()
	Else
		MNTR475R3()
	EndIf
	RestArea(aArea) 

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³Elisangela Costa       ³ Data ³ 04/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR475                                                    ³±±
±±|__________|____________________________________________________________|±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef() 

	Local oReport
	Local oSection1, oSection2
	Local oCell, oCell2 

	Pergunte( "MNT475", .F. ) 


	//LAYOUT  SE MV_PAR11 = 1   
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
	___________________________________________________________________________________________________________________________________________ 
	Relatorio de Media de Combustivel Por Equipamento 
	___________________________________________________________________________________________________________________________________________ 
	Bem               Descricao
	___________________________________________________________________________________________________________________________________________
	xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	Produto         Nome do Produto                 Quant.1  Un.  Prim. Cont.  Prim. Media        Quant.2 Un.   Seg. Cont.  Seg. Media
	___________________________________________________________________________________________________________________________________________
	xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  99.999.999,99 xxx  999.999.999   999.999,99  99.999.999,99 xxx  999.999.999  999.999,99
	xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  99.999.999,99 xxx  999.999.999   999.999,99  99.999.999,99 xxx  999.999.999  999.999,99
	xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  99.999.999,99 xxx  999.999.999   999.999,99  99.999.999,99 xxx  999.999.999  999.999,99

	*/

	//LAYOUT  SE MV_PAR11 = 2   
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
	___________________________________________________________________________________________________________________________________________ 
	Relatorio de Media de Combustivel Por Equipamento 
	___________________________________________________________________________________________________________________________________________ 

	Bem               Descricao
	___________________________________________________________________________________________________________________________________________
	xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	Produto         Nome do Produto                 Quant.1  Un.  Prim. Cont.  Prim. Media        Quant.2 Un.   Seg. Cont.  Seg. Media
	___________________________________________________________________________________________________________________________________________
	xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  99.999.999,99 xxx  999.999.999   999.999,99  99.999.999,99 xxx  999.999.999  999.999,99
	xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  99.999.999,99 xxx  999.999.999   999.999,99  99.999.999,99 xxx  999.999.999  999.999,99
	xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  99.999.999,99 xxx  999.999.999   999.999,99  99.999.999,99 xxx  999.999.999  999.999,99

	Total Bem, Km rodado, media                
	___________________________________________________________________________________________________________________________________________
	99.999.999,99      999.999.999   999.999,99  99.999.999,99      999.999.999  999.999,99  

	___________________________________________________________________________________________________________________________________________
	OBSERVACAO: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx


	*/                          


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR475",OemToAnsi(STR0006),"MNT475",{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002+" "+STR0003)
	oReport:ParamReadOnly()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ mv_par01     // De Grupo                                     ³
	//³ mv_par02     // Ate Grupo                                    ³
	//³ mv_par03     // De Produto                                   ³
	//³ mv_par04     // Ate Produto                                  ³
	//³ mv_par05     // De Data Inicio                               ³
	//³ mv_par06     // Ate Data Fim                                 ³
	//³ mv_par07     // De Familia                                   ³
	//³ mv_par08     // Ate Familia                                  ³
	//³ mv_par09     // De Bem                                       ³
	//³ mv_par10     // Ate Bem                                      ³
	//³ mv_par11     // Totalizar por bem  PARAMETRO NOVO            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//Defenicao das secoes do relatorio
	If MV_PAR11 = 1  

		oSection1 := TRSection():New(oReport,STR0026,{(cTRB),"ST9"}) //Bem
		TRCell():New(oSection1,"TRB->CODBEM" ,(cTRB) ,STR0026 ,"@!" ,16  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem" 
		TRCell():New(oSection1,"T9_NOME"     ,"ST9" ,STR0027 ,"@!" ,40  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrição" 
		TRPosition():New(oSection1,"ST9",1,{|| xFilial("ST9") + TRB->CODBEM})

		oSection2 := TRSection():New(oReport,STR0038,{(cTRB),"SB1"})  //Produtos
		oCell := TRCell():New(oSection2,"TRB->PRODUTO" ,(cTRB) ,STR0028 ,"@!"               ,nTamPro+15,/*lPixel*/,/*{|| code-block de impressao }*/) //"Produto"
		oCell := TRCell():New(oSection2,"B1_DESC"      ,"SB1" ,STR0029 ,"@!"               ,40,/*lPixel*/,/*{|| code-block de impressao }*/) //"Nome do Produto" 
		oCell := TRCell():New(oSection2,"QUANTPR1"     ,"   " ,STR0030 ,"@E 99,999,999.99" ,13,/*lPixel*/,{||NGRETVAL(1)}) //"Quant.1" 
		oCell := TRCell():New(oSection2,"B1_UM"        ,"SB1" ,STR0031 ,"@!"               ,2 ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Un."
		oCell := TRCell():New(oSection2,"CONT1"        ,"   " ,STR0032 ,"@E 999,999,999"   ,11,/*lPixel*/,{||NGRETVAL(2)}) //"Prim. Cont."
		oCell := TRCell():New(oSection2,"MEDIACONT1"   ,"   " ,STR0033 ,"@E 999,999.99"    ,10,/*lPixel*/,{||NGRETVAL(3)}) //"Prim. Media"
		oCell := TRCell():New(oSection2,"QUANTPR2"     ,"   " ,STR0034 ,"@E 99,999,999.99" ,13,/*lPixel*/,{||NGRETVAL(4)}) //"Quant.2"
		oCell := TRCell():New(oSection2,"B1_UM"        ,"SB1" ,STR0031 ,"@!"               ,2 ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Un." 
		oCell := TRCell():New(oSection2,"CONT2"        ,"   " ,STR0035 ,"@E 999,999,999"   ,11,/*lPixel*/,{||NGRETVAL(5)}) //"Seg. Cont." 
		oCell := TRCell():New(oSection2,"MEDIACONT2"   ,"   " ,STR0036 ,"@E 999,999.99"    ,10,/*lPixel*/,{||NGRETVAL(6)}) //"Seg. Media"
		//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a
		//direita
		oSection2:Cell("QUANTPR1"):SetHeaderAlign("RIGHT") 
		oSection2:Cell("CONT1"):SetHeaderAlign("RIGHT")   
		oSection2:Cell("MEDIACONT1"):SetHeaderAlign("RIGHT")
		oSection2:Cell("QUANTPR2"):SetHeaderAlign("RIGHT")   
		oSection2:Cell("CONT2"):SetHeaderAlign("RIGHT") 
		oSection2:Cell("MEDIACONT2"):SetHeaderAlign("RIGHT")  
		TRPosition():New(oSection2,"SB1",1,{|| xFilial("SB1")+TRB->PRODUTO})
	Else
		oReport:SetTotalInLine(.F.)

		oSection1 := TRSection():New(oReport,STR0026,{(cTRB),"ST9"})  //Bem
		oSection1:SetHeaderBreak() // sempre que houver quebra imprime o cabeçalho da seção 
		oCell := TRCell():New(oSection1,"TRB->CODBEM" ,(cTRB) ,STR0026 ,"@!" ,16  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem"     
		oCell := TRCell():New(oSection1,"T9_NOME"     ,"ST9" ,STR0027 ,"@!" ,40  ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrição"
		TRPosition():New(oSection1,"ST9",1,{|| xFilial("ST9") + TRB->CODBEM})

		oSection2 := TRSection():New(oReport,STR0038,{(cTRB),"SB1"}) //Produtos 
		oCell2 := TRCell():New(oSection2,"TRB->PRODUTO" ,(cTRB) ,STR0028 ,"@!"               ,nTamPro+15,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Produto" 
		oCell2 := TRCell():New(oSection2,"B1_DESC"      ,"SB1" ,STR0029 ,"@!"               ,40,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Nome do Produto"
		oCell2 := TRCell():New(oSection2,"QUANTPR1"     ,"   " ,STR0030 ,"@E 99,999,999.99" ,13,/*lPixel*/,{||NGRETVAL(1)})                   //"Quant.1" 
		oCell2 := TRCell():New(oSection2,"B1_UM"        ,"SB1" ,STR0031 ,"@!"               ,2 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Un."
		oCell2 := TRCell():New(oSection2,"CONT1"        ,"   " ,STR0032 ,"@E 999,999,999"   ,11,/*lPixel*/,{||NGRETVAL(2)})  //"Prim. Cont."  
		oCell2 := TRCell():New(oSection2,"MEDIACONT1"   ,"   " ,STR0033 ,"@E 999,999.99"    ,10,/*lPixel*/,{||NGRETVAL(3)})  //"Prim. Media"  
		oCell2 := TRCell():New(oSection2,"QUANTPR2"     ,"   " ,STR0034 ,"@E 99,999,999.99" ,13,/*lPixel*/,{||NGRETVAL(4)})  //"Quant.2"
		oCell2 := TRCell():New(oSection2,"B1_UM"        ,"SB1" ,STR0031 ,"@!"               ,2 ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Un."
		oCell2 := TRCell():New(oSection2,"CONT2"        ,"   " ,STR0035 ,"@E 999,999,999"   ,11,/*lPixel*/,{||NGRETVAL(5)}) //"Seg. Cont." 
		oCell2 := TRCell():New(oSection2,"MEDIACONT2"   ,"   " ,STR0036 ,"@E 999,999.99"    ,10,/*lPixel*/,{||NGRETVAL(6)}) //"Seg. Media" 
		//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a
		//direita
		oSection2:Cell("QUANTPR1"):SetHeaderAlign("RIGHT") 
		oSection2:Cell("CONT1"):SetHeaderAlign("RIGHT")   
		oSection2:Cell("MEDIACONT1"):SetHeaderAlign("RIGHT")
		oSection2:Cell("QUANTPR2"):SetHeaderAlign("RIGHT")   
		oSection2:Cell("CONT2"):SetHeaderAlign("RIGHT") 
		oSection2:Cell("MEDIACONT2"):SetHeaderAlign("RIGHT")
		TRPosition():New(oSection2,"SB1",1,{|| xFilial("ST9") + TRB->PRODUTO})

		oBreak := TRBreak():New(oSection1,oSection1:Cell("TRB->CODBEM"),STR0024,.F.) //"Total Bem, Km rodado, media"                                                                 
		TRFunction():New(oSection2:Cell("QUANTPR1")    ,/*cId*/,"SUM"    ,oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		//TRFunction():New(oSection2:Cell("CONT1")       ,/*cId*/,"SUM"    ,oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		//TRFunction():New(oSection2:Cell("MEDIACONT1")  ,/*cId*/,"AVERAGE",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		TRFunction():New(oSection2:Cell("QUANTPR2")    ,/*cId*/,"SUM"    ,oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		//TRFunction():New(oSection2:Cell("CONT2")       ,/*cId*/,"SUM"    ,oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		//TRFunction():New(oSection2:Cell("MEDIACONT2")  ,/*cId*/,"AVERAGE",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	EndIf

Return oReport     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR475R3³ Autor ³ Inacio Luiz Kolling   ³ Data ³20/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Media de Comsumo de Combustivel por Equipamento            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR475                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/ 
Function MNTR475R3()

	Local cSTRING    := "SI3"
	Local cDESC1     := STR0001 //"Relatorio de Media de Combustivel Por Equipamentos."
	Local cDESC2     := STR0002 //"E permitido selecionar tipo de informacao a ser"
	Local cDESC3     := STR0003 //"apresentada, atraves dos parametros do relatorio."
	Local WNREL      := "MNTR475"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private nLASTKEY := 0
	Private TITULO   := STR0006 //"Relatorio de Media de Combustivel Por Equipamento"
	Private TAMANHO  := If(nTamPro > 0,"G","M")
	Private NOMEPROG := "MNTR475"
	Private nTIPO    := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ mv_par01     // De Grupo                                     ³
	//³ mv_par02     // Ate Grupo                                    ³
	//³ mv_par03     // De Produto                                   ³
	//³ mv_par04     // Ate Produto                                  ³
	//³ mv_par05     // De Data Inicio                               ³
	//³ mv_par06     // Ate Data Fim                                 ³
	//³ mv_par07     // De Familia                                   ³
	//³ mv_par08     // Ate Familia                                  ³
	//³ mv_par09     // De Bem                                       ³
	//³ mv_par10     // Ate Bem                                      ³
	//³ mv_par11     // Totalizar por bem  PARAMETRO NOVO            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	Private cPERG := "MNT475"
	Pergunte(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		Return
	EndIf
	SetDefault(aRETURN,cString)
	RptStatus({|lEND| R475IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R475Imp  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³20/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR475                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R475IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT := ""
	Local nCNTIMPR := 0
	Local nTOTREGS := 0,nMULT := 1,nPOSANT := 4,nPOSATU := 4,nPOSCNT := 0
	Local oARQTR4751
	
	Private li := 80 ,m_pag := 1
	Private CABEC1  := STR0017+Space(nTamPro)+STR0040 //"      Produto          Nome Do Produto                Quant.1 Un. Prim. Cont. Prim. Media        Quant.2 Un.  Seg. Cont.   Seg.Media"
	Private CABEC2  := " " 
	Private aVETINR := {}   
	Private nCONT1, cQuantPr1
	Private nCONT2, cQuantPr2    
	nTIPO := IIf(aRETURN[4]==1,15,18)

	/*
	1         2         3         4         5         6         7         8         9         100       110       120       132
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	*************************************************************************************************************************************
	Produto         Nome Do Produto              Quantidade Un. Prim. Cont. Prim. Media     Quantidade Un.  Seg. Cont.   Seg.Media
	Produto          Nome Do Produto                Quant.1 Un. Prim. Cont. Prim. Media        Quant.2 Un.  Seg. Cont.   Seg.Media"       
	*************************************************************************************************************************************
	Bem.: xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx 99.999.999,99 xxx 999.999.999  999.999,99  99.999.999,99 xxx 999.999.999  999.999,99
	xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx 99.999.999,99 xxx 999.999.999  999.999,99  99.999.999,99 xxx 999.999.999  999.999,99

	*/
	aDBFR4751 := {{"CODBEM"  ,"C",16,0},;
				  {"PRODUTO" ,"C",nTamPro+15,0},;
				  {"QUANTID" ,"N",16,2},;
				  {"QTIDPRI" ,"N",16,2}}  
	
	//Cria Tabela Temporária 
	oARQTR4751 := NGFwTmpTbl(cTRB,aDBFR4751,{{"CODBEM","PRODUTO"}})
	
	aDBFR4752 := {{"CODBEM"  ,"C",16,0},;
				  {"PRODUTO" ,"C",nTamPro+15,0},;
				  {"CONTAD11","N",12,0},;
				  {"CONTAD21","N",12,0},;
				  {"CONTAD12","N",12,0},;
				  {"CONTAD22","N",12,0},;
				  {"DTMENOR" ,"D",08,0},;
				  {"DTMAIOR" ,"D",08,0},;
				  {"HRMENOR" ,"C",05,0},;
				  {"HRMAIOR" ,"C",05,0},;
				  {"TEMCONT1","C",01,0},;
				  {"TEMCONT2","C",01,0}}

	//Cria Tabela Temporária 
	oARQTR4752 := NGFwTmpTbl(cTRB1,aDBFR4751,{{"CODBEM","PRODUTO"}})
	
	Processa({|lEND| MNTR457STL()},STR0018) //"Processando Itens das O.S. Normais..."
	Processa({|lEND| MNTR457STT()},STR0019) //"Processando Itens das O.S. Historico..."
	Processa({|lEND| R475PRTRB1()},STR0025) //"Selecionando Registros..."

	Dbselectarea(cTRB)
	Dbgotop()
	SetRegua(LastRec())
	lIMPOBS := .t.
	While !Eof()
		cBEM := TRB->CODBEM
		DbSelectArea("ST9")
		DbSetOrder(1)
		Dbseek(xFILIAL("ST9")+cBEM)
		Ngsomali(58)
		If mv_par11 = 2
			If lIMPOBS
				@ Li,000 Psay STR0022
				Ngsomali(58)
				@ Li,000 Psay STR0023
				Ngsomali(58)
				Ngsomali(58)
				lIMPOBS := .f.
			Endif
		Endif

		@ Li,000 Psay STR0020+cBEM+" "+ST9->T9_NOME //"Bem..: "

		//Faz a leitura do TRB1 
		//NGR475LRB1()

		nTQDTBEM1 := 0
		nTQDTBEM2 := 0
		nTQDTCON1 := 0
		nTQDTCON2 := 0
		//lPRIME    := .T. 
		DbSelectArea(cTRB)
		While !Eof() .And. TRB->CODBEM == cBEM
			IncRegua()

			//Faz a leitura do TRB1 
			NGR475LRB1()

			DbSelectArea("SB1")
			DbSetOrder(1)
			DbSeek(xFILIAL("SB1")+TRB->PRODUTO)
			Ngsomali(58)
			@ Li,006 Psay TRB->PRODUTO               Picture "@!"
			@ Li,023+nTamPro Psay Substr(SB1->B1_DESC,1,23)
			@ Li,048+nTamPro Psay cQuantPr1 Picture "@E 99,999,999.99"
			@ Li,062+nTamPro Psay SB1->B1_UM                 Picture "@!"
			@ Li,066+nTamPro Psay nCONT1                     Picture "@E 999,999,999"
			@ Li,079+nTamPro Psay cQuantPr1/nCONT1           Picture "@E 999,999.99"
			@ Li,091+nTamPro Psay cQuantPr2 Picture "@E 99,999,999.99"
			@ Li,105+nTamPro Psay SB1->B1_UM                 Picture "@!"
			@ Li,109+nTamPro Psay nCONT2                     Picture "@E 999,999,999"
			@ Li,122+nTamPro Psay cQuantPr2/nCONT2           Picture "@E 999,999.99"
			nTQDTBEM1 := nTQDTBEM1 + cQuantpr1
			nTQDTBEM2 := nTQDTBEM2 + cQuantpr2
			nTQDTCON1 := nTQDTCON1 + nCONT1
			nTQDTCON2 := nTQDTCON2 + nCONT2
			DbSelectArea(cTRB)
			DbSkip()
		End

		Ngsomali(58)
		If mv_par11 = 2
			@ Li,048+nTamPro Psay Replicate("-",13)
			@ Li,091+nTamPro Psay Replicate("-",13)

			Ngsomali(58)
			@ Li,000 Psay STR0024
			@ Li,048+nTamPro Psay nTQDTBEM1 Picture "@E 99,999,999.99"

			@ Li,091+nTamPro Psay nTQDTBEM2  Picture "@E 99,999,999.99"
			
		Endif
		Ngsomali(58)
	End
	Roda(nCNTIMPR,cRODATXT,TAMANHO)

	//Deleta o arquivo temporario fisicamente 
	oARQTR4751:Delete() 
	NGDELETRB("TRB1" ,cARQTR4752) 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principam             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex("ST9")
	Set Filter To
	Set Device To Screen
	If aRETURN[5] = 1
		Set Printer To
		DbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()
Return Nil  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³ Elisangela Costa      ³ Data ³ 29/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ReportDef                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local oARQTR4751
	Local oARQTR4752

	Private aVETINR := {}   

	Private nTQDTBEM1, nTQDTBEM2
	Private nTQDTCON1, nTQDTCON2 
	Private nCONT1, cQuantPr1
	Private nCONT2, cQuantPr2    

	If !Empty(oReport:uParam)
		Pergunte(oReport:uParam,.F.)
	EndIf	   


	aDBFR4751 := {{"CODBEM"  ,"C",16,0},;
				  {"PRODUTO" ,"C",nTamPro+15,0},;
				  {"QUANTID" ,"N",16,2},;
				  {"QTIDPRI" ,"N",16,2}} 
	
	//Cria Tabela Temporária 
	oARQTR4751 := NGFwTmpTbl(cTRB,aDBFR4751,{{"CODBEM","PRODUTO"}})

	aDBFR4752 := {{"CODBEM"  ,"C",16,0},;
				  {"PRODUTO" ,"C",nTamPro+15,0},;
				  {"CONTAD11","N",12,0},;
				  {"CONTAD21","N",12,0},;
				  {"CONTAD12","N",12,0},;
				  {"CONTAD22","N",12,0},;
				  {"DTMENOR" ,"D",08,0},;
				  {"DTMAIOR" ,"D",08,0},;
				  {"HRMENOR" ,"C",05,0},;
				  {"HRMAIOR" ,"C",05,0},;
				  {"TEMCONT1","C",01,0},;
				  {"TEMCONT2","C",01,0}}

	//Cria Tabela Temporária 
	oARQTR4752 := NGFwTmpTbl(cTRB1,aDBFR4751,{{"CODBEM","PRODUTO"}})

	Processa({|lEND| MNTR457STL()},STR0018) //"Processando Itens das O.S. Normais..."
	Processa({|lEND| MNTR457STT()},STR0019) //"Processando Itens das O.S. Historico..."
	Processa({|lEND| R475PRTRB1()},STR0025) //"Selecionando Registros..."

	If mv_par11 = 1   

		DbSelectArea(cTRB) 
		DbGotop()
		oReport:SetMeter(RecCount())
		While !Eof() .And. !oReport:Cancel() 

			cBEM := TRB->CODBEM 
			//Faz a leitura do TRB1 
			//NGR475LRB1()

			oSection1:Init() 
			oSection1:PrintLine()  
			oSection2:Init() 

			nTQDTBEM1 := 0
			nTQDTBEM2 := 0
			nTQDTCON1 := 0
			nTQDTCON2 := 0
			//lPRIME    := .T. 
			DbSelectArea(cTRB)
			While !Eof() .And. !oReport:Cancel() .And. TRB->CODBEM == cBEM   

				oReport:IncMeter()  
	
				//Faz a leitura do TRB1 
				NGR475LRB1()

				nTQDTBEM1 := nTQDTBEM1 + cQuantpr1
				nTQDTBEM2 := nTQDTBEM2 + cQuantpr2
				nTQDTCON1 := nTQDTCON1 + nCONT1
				nTQDTCON2 := nTQDTCON2 + nCONT2

				oSection2:PrintLine()     

				DbSelectArea(cTRB)
				DbSkip()    
			End
			oSection2:Finish()  
			oSection1:Finish()
		End   
	Else

		oSection1:Init()  
		DbSelectArea(cTRB) 
		DbGotop()
		oReport:SetMeter(RecCount())
		While !Eof() .And. !oReport:Cancel() 

			cBEM := TRB->CODBEM 

			//Faz a leitura do TRB1 
			//NGR475LRB1()

			oSection1:PrintLine()  
			oSection2:Init() 

			nTQDTBEM1 := 0
			nTQDTBEM2 := 0
			nTQDTCON1 := 0
			nTQDTCON2 := 0
			//lPRIME    := .T. 
			DbSelectArea(cTRB)
			While !Eof() .And. !oReport:Cancel() .And. TRB->CODBEM == cBEM   

				oReport:IncMeter()  

				//Faz a leitura do TRB1 
				NGR475LRB1()

				nTQDTBEM1 := nTQDTBEM1 + cQuantpr1
				nTQDTBEM2 := nTQDTBEM2 + cQuantpr2
				nTQDTCON1 := nTQDTCON1 + nCONT1
				nTQDTCON2 := nTQDTCON2 + nCONT2

				oSection2:PrintLine()     

				DbSelectArea(cTRB)
				DbSkip()    
			End  
			oSection2:Finish()
		End   
		oSection1:Finish()
		oReport:ThinLine()
		oReport:PrintText( STR0022+" "+STR0023 )  //"Observação:"//"Devido a selecao do parametro de totalizar por bem, e possivel que sejam totalizados produtos de diferentes unidades."
	EndIf 
	//Deleta o arquivo temporario fisicamente  
	oARQTR4751:Delete()
	oARQTR4752:Delete()
	 
Return .T.


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR475STL³ Autor ³ Inacio Luiz Kolling   ³ Data ³20/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os itens das O.S. normais                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR475                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR457STL()

	cCONDSTL := 'Val(stl->tl_seqrela) > 0 .And. stl->tl_tiporeg = "P" .And. '
	cCONDSTL := cCONDSTL + '(stl->tl_codigo >= mv_par03 .And. stl->tl_codigo <= mv_par04)'

	cCONDSTJ := 'stj->tj_situaca = "L" .And. stj->tj_tipoos = "B" .And.'
	cCONDSTJ := cCONDSTJ + '(stj->tj_codbem >= mv_par09 .And. stj->tj_codbem <= mv_par10)'

	cCONDSB1 := 'sb1->b1_grupo >= mv_par01 .And. sb1->b1_grupo <= mv_par02'
	cCONDST9 := 'st9->t9_codfami >= mv_par07 .And. st9->t9_codfami <= mv_par08'

	Dbselectarea("STL")
	Dbsetorder(6)
	Dbseek(xFILIAL("STL")+DTOS(MV_PAR05),.T.)
	ProcRegua(LastRec())

	While !Eof() .AND. STL->TL_FILIAL == xFILIAL("STL") .AND. STL->TL_DTINICI <= MV_PAR06
		IncProc()
		DbSelectArea("STJ")
		DbSetOrder(1)
		DbSeek(xFILIAL("STJ")+STL->TL_ORDEM+STL->TL_PLANO)
		If &(cCONDSTJ)

			If &(cCONDSTL)
				DbSelectArea("SB1")
				DbSetOrder(1)
				DbSeek(xFILIAL("SB1")+STL->TL_CODIGO)

				If &(cCONDSB1)
					DbSelectArea("ST9")
					DbSetOrder(1)
					DbSeek(xFILIAL("ST9")+STJ->TJ_CODBEM)

					If &(cCONDST9)

						vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
						STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
						STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
						nQTDHORAS := vVETHORAS[1]

						R475GRTRAB(STJ->TJ_CODBEM,STL->TL_CODIGO,nQTDHORAS,STL->TL_DTINICI,STL->TL_HOINICI)
					EndIf
				EndIf
			EndIf
		EndIf
		DbSelectArea("STL")
		DbSkip()
	End
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR475STT³ Autor ³ Inacio Luiz Kolling   ³ Data ³20/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os itens das O.S. Historicos                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR475                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR457STT()

	cCONDSTT := 'Val(stt->tt_seqrela) > 0 .And. stt->tt_tiporeg = "P" .And. '
	cCONDSTT := cCONDSTT + '(stt->tt_codigo >= mv_par03 .And. stt->tt_codigo <= mv_par04)'

	cCONDSTS := 'sts->ts_situaca = "L" .And. sts->ts_tipoos = "B" .And.'
	cCONDSTS := cCONDSTS + '(sts->ts_codbem >= mv_par09 .And. sts->ts_codbem <= mv_par10)'

	cCONDSB1 := 'sb1->b1_grupo >= mv_par01 .And. sb1->b1_grupo <= mv_par02'
	cCONDST9 := 'st9->t9_codfami >= mv_par07 .And. st9->t9_codfami <= mv_par08'

	DbSelectArea("STT")
	DbSetOrder(5)
	DbSeek(xFILIAL("STT")+DTOS(MV_PAR05),.T.)
	ProcRegua(LastRec())

	While !Eof() .AND. STT->TT_FILIAL == xFILIAL("STT") .AND. STT->TT_DTINICI <= MV_PAR06
		IncProc()
		DbSelectArea("STS")
		DbSetOrder(1)
		DbSeek(xFILIAL("STS")+STT->TT_ORDEM+STT->TT_PLANO)
		If &(cCONDSTS)

			If &(cCONDSTT)
				DbSelectArea("SB1")
				DbSetOrder(1)
				DbSeek(xFILIAL("SB1")+STT->TT_CODIGO)

				If &(cCONDSB1)
					DbSelectArea("ST9")
					DbSetOrder(1)
					DbSeek(xFILIAL("ST9")+STS->TS_CODBEM)

					If &(cCONDST9)
						vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
						STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
						STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
						nQTDHORAS := vVETHORAS[1]

						R475GRTRAB(STS->TS_CODBEM,STT->TT_CODIGO,nQTDHORAS,STT->TT_DTINICI,STT->TT_HOINICI)
					EndIf                                                                           
				EndIf
			EndIf
		EndIf
		DbSelectArea("STT")
		DbSkip()
	End
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³R475GRATRB³ Autor ³ Inacio Luiz Kolling   ³ Data ³21/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Grava no arquivo temporario                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR475        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function R475GRTRAB(cCODBEM,cPROD,nQUANT,dDTLEI,cHORAINIC)
	DbSelectArea(cTRB)
	If !DbSeek(cCODBEM+cPROD)
		DbAppend()
		TRB->CODBEM  := cCODBEM
		TRB->PRODUTO := cPROD
		TRB->QTIDPRI := nQUANT
	EndIf
	TRB->QUANTID := TRB->QUANTID+nQUANT

	DbSelectArea(cTRB1)
	If !DbSeek(cCODBEM+cPROD)
		DbSelectArea(cTRB1)
		DbAppend()
		(cTRB1)->CODBEM  := cCODBEM
		(cTRB1)->PRODUTO := cPROD
		(cTRB1)->DTMENOR := dDTLEI
		(cTRB1)->DTMAIOR := dDTLEI
		(cTRB1)->HRMENOR := cHORAINIC
		(cTRB1)->HRMAIOR := cHORAINIC 
	Else
		If dDTLEI <= (cTRB1)->DTMENOR
			If dDTLEI < (cTRB1)->DTMENOR
				(cTRB1)->DTMENOR := dDTLEI
				(cTRB1)->HRMENOR := cHORAINIC
			Else
				(cTRB1)->HRMENOR := cHORAINIC
			EndIf
		EndIf

		If dDTLEI >= (cTRB1)->DTMAIOR
			If dDTLEI > (cTRB1)->DTMAIOR  
				(cTRB1)->DTMAIOR := dDTLEI
				(cTRB1)->HRMAIOR := cHORAINIC 
			Else
				(cTRB1)->HRMAIOR := cHORAINIC 
			EndIf
		EndIf
	EndIf
Return .T.   

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³R475PRTRB1³ Autor ³ Elisangela Costa      ³ Data ³03/07/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Reprocessa o TRB1                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR475                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function R475PRTRB1(cCODBEM,cPROD,nQUANT,dDTLEI,cHORAINIC)

	DbSelectArea(cTRB1)
	DbGotop()  
	ProcRegua(LastRec())
	While !Eof()        

		IncProc()
		DbSelectArea("STP")
		DbSetOrder(2)
		If DbSeek(xFILIAL("STP")+(cTRB1)->CODBEM)
			//Procura o lancamento anterior do contador 1 com a aplicacao da data menor do insumo
			nCONT1MEN := NGACUMEHIS((cTRB1)->CODBEM,(cTRB1)->DTMENOR,(cTRB1)->HRMENOR,1,"A")
			(cTRB1)->CONTAD11 := nCONT1MEN[2]

			//Procura o lancamento do contador 1 com a aplicacao da data maior do insumo
			nCONT1MAI := NGACUMHIST((cTRB1)->CODBEM,(cTRB1)->DTMAIOR,(cTRB1)->HRMAIOR,1)
			(cTRB1)->CONTAD12 := nCONT1MAI[2]
			(cTRB1)->TEMCONT1 := "S"
		EndIf

		DbSelectArea("TPP")
		DbSetOrder(2)
		If DbSeek(xFILIAL("TPP")+(cTRB1)->CODBEM)
			//Procura o lancamento anterior do contador 2 com a aplicacao da data menor do insumo
			nCONT2MEN := NGACUMEHIS((cTRB1)->CODBEM,(cTRB1)->DTMENOR,(cTRB1)->HRMENOR,2,"A")
			(cTRB1)->CONTAD21 := nCONT2MEN[2]
			//Procura o lancamento do contador 2 com a aplicacao da data maior do insumo
			nCONT2MAI := NGACUMHIST((cTRB1)->CODBEM,(cTRB1)->DTMAIOR,(cTRB1)->HRMAIOR,2)
			(cTRB1)->CONTAD22 := nCONT2MAI[2]
			(cTRB1)->TEMCONT2 := "S"
		EndIf
		Dbselectarea(cTRB1)
		Dbskip()
	End          

Return .T. 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³NGRETVAL  ³ Autor ³Elisangela Costa       ³ Data ³ 05/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o valor dos campos da secao 3 dos relatorio         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³REPORTDEF                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function NGRETVAL(nCAM)

	Local nVALORCAM := 0

	If nCAM = 1
		nVALORCAM := cQuantPr1  
	ElseIf nCAM = 2                 
		nVALORCAM := nCONT1
	ElseIf nCAM = 3  
		nVALORCAM := cQuantPr1/nCONT1
	ElseIf nCAM = 4
		nVALORCAM := cQuantPr2
	ElseIf nCAM = 5 
		nVALORCAM := nCONT2
	ElseIf nCAM = 6 
		nVALORCAM := cQuantPr2/nCONT2
	EndIf 
Return nVALORCAM 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³NGR475RETS³ Autor ³Elisangela Costa       ³ Data ³ 05/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Faz a leitura do TRB1                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³REPORTDEF                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function NGR475LRB1()

	DbSelectArea(cTRB1)
	DbSeek(cBEM+TRB->PRODUTO)

	//Contador 1
	nCONT1  := 0
	cQuantPr1 := TRB->QUANTID
	If (cTRB1)->TEMCONT1 = "S"
		If (cTRB1)->CONTAD11 = 0 
			DbSelectArea("STP")
			DbSetOrder(5)
			DbSeek(xFILIAL("STP")+(cTRB1)->CODBEM+DTOS((cTRB1)->DTMENOR)+(cTRB1)->HRMENOR,.T.)
			If STP->TP_CODBEM = (cTRB1)->CODBEM
				nCONT1    := (cTRB1)->CONTAD12 - STP->TP_ACUMCON
				cQuantPr1 := TRB->QUANTID - TRB->QTIDPRI  
			EndIf   
		Else
			nCONT1 := (cTRB1)->CONTAD12 - (cTRB1)->CONTAD11
		EndIf
	EndIf

	//Contador 2
	nCONT2  := 0
	cQuantPr2 := TRB->QUANTID
	If (cTRB1)->TEMCONT2 = "S"
		If (cTRB1)->CONTAD21 = 0 
			DbSelectArea("TPP")
			DbSetOrder(5)
			DbSeek(xFILIAL("TPP")+(cTRB1)->CODBEM+DTOS((cTRB1)->DTMENOR)+(cTRB1)->HRMENOR,.T.)
			If TPP->TPP_CODBEM = (cTRB1)->CODBEM
				nCONT2    := (cTRB1)->CONTAD22 - TPP->TPP_ACUMCO 
				cQuantPr2 := TRB->QUANTID - TRB->QTIDPRI  
			EndIf    
		Else
			nCONT2 := (cTRB1)->CONTAD22 - (cTRB1)->CONTAD21 
		EndIf
	EndIf   
Return .T. 