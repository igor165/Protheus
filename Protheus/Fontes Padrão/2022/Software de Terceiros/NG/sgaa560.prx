#Include 'Protheus.ch'
#INCLUDE 'FWMVCDEF.CH'        
#INCLUDE 'SGAA560.CH'

#DEFINE _nVERSAO 1 //Versao do fonte

Static __aMetas := {}

//-------------------------------------------------------------------
/*/{Protheus.doc} SGAA560
Cadastro de Variáveis dos Objetivos e Metas

@author Thiago Henrique dos Santos
@since 06/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Function SGAA560()  

	Local oBrowse := FWMBrowse():New()   
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

	//valida se update foi aplicado
	IF !NGCADICBASE('TDN_VAR','A','TDN',.F.)
		NGINCOMPDIC("UPDSGA16","XXXXXX")
		Return .F.
	Endif

	oBrowse:SetAlias("TDN")
	oBrowse:SetDescription(STR0001) //#Variáveis dos Objetivos e Metas         

	oBrowse:Activate()
					  
	NGRETURNPRM(aNGBEGINPRM)
Return NIL           

//-------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Modelo de dados para o cadastro de variáveis.

@author Thiago Henrique dos Santos
@since 06/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Static Function ModelDef()

	Local oStruTDN := FWFormStruct(1,"TDN")    
	Local oStruTDO := FWFormStruct(1,"TDO")  
	Local oModel

	//oStruTDN:SetProperty( 'TDN_VAR' , MODEL_FIELD_VALID,{|oModeLCab| ExistChav('TDN',oModelCab:GetValue('TDN_VAR'),1)})
	//oStruTDN:SetProperty( 'TDN_DESCRI' , MODEL_FIELD_VALID,{|oModelCab| ExistChav('TDN',oModelCab:GetValue('TDN_DESCRI'),1)})
	//oStruTDO:SetProperty( 'TDO_DTFIM' , MODEL_FIELD_VALID,{|oModelGrid| ValidData(oModelGrid,'TDO_DTFIM')})

	oModel:= MPFormModel():New('SGAA560',{|oModel|CarregaMetas(oModel)}/*Pre-Validacao*/, {|oModel|PosValid(oModel)}, /*Commit*/,/*Cancel*/)
	oModel:AddFields('TDNMASTER',/*owner*/,oStruTDN,/*bPreValidacao*/, /*bPosValidacao*/ , /*bCarga*/ )
	oModel:AddGrid('TDODETAIL','TDNMASTER',oStruTDO,{|oModelGrid|PreLinha(oModelGrid)}/*bLinePre*/, {|oModelGrid|ValidLinha(oModelGrid)}/*bLinePost*/, /*bPreVal*/, /*bPosVal*/, /*BLoad*/ )  

	oModel:SetDescription(STR0001) //#Variáveis dos Objetivos e Metas   
	oModel:GetModel('TDNMASTER'):SetDescription(STR0002)  //#Variável
	oModel:GetModel('TDODETAIL'):SetDescription( STR0003 ) //#Resultados     
	oModel:SetRelation( 'TDODETAIL', { { 'TDO_FILIAL', 'xFilial( "TDO" )' },{ 'TDO_VAR', 'TDN_VAR' }},TDO->( IndexKey( 1 ) ) )
	oModel:SetPrimaryKey( { "TDO_FILIAL", "TDO_VAR"} )

Return oModel                                     


//-------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Visualizador de dados do Cadastro de Metas

@author Thiago Henrique dos Santos
@since 06/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Static Function ViewDef()
	Local oModel := FWLoadModel('SGAA560')
	Local oStruTDN := FWFormStruct(2,"TDN")     
	Local oStruTDO := FWFormStruct(2,"TDO")     
	Local oView          
	//Local nOperation := oModel:GetOperation()

	oStruTDO:RemoveField('TDO_VAR')

	oView:= FWFormView():New()
	oView:SetModel(oModel)
	oView:AddField( 'VIEW_TDN', oStruTDN, 'TDNMASTER' )  
	oView:AddGrid( 'VIEW_TDO', oStruTDO, 'TDODETAIL' )

		
	oView:CreateHorizontalBox( 'SUPERIOR', 20 )
	oView:CreateHorizontalBox( 'INFERIOR', 80 )

	oView:SetOwnerView( 'VIEW_TDN', 'SUPERIOR' )
	oView:SetOwnerView( 'VIEW_TDO', 'INFERIOR' )

Return oView


//-------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Menu Funcional do Cadastro de Metas

@return aRotina - Estrutura
			[n,1] Nome a aparecer no cabeçaalho
			[n,2] Nome da Rotina associada
			[n,3] Reservado
			[n,4] Tipo de Transação a ser efetuada:
				1 - Pesquisa e Posiciona em um Banco de Dados
				2 - Simplesmente Mostra os Campos
				3 - Inclui registros no Bancos de Dados
				4 - Altera o registro corrente
				5 - Remove o registro corrente do Banco de Dados
				6 - Alteração sem inclusão de registros
				7 - Cópia
				8 - Imprimir
			[n,5] Nivel de acesso
			[n,6] Habilita Menu Funcional

@author Thiago Henrique dos Santos
@since 06/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Static Function MenuDef()  

	Local aRotina := {}

	ADD OPTION aRotina Title 'Visualizar' Action 'VIEWDEF.SGAA560' OPERATION 2 ACCESS 0
	ADD OPTION aRotina Title 'Incluir'    Action 'VIEWDEF.SGAA560' OPERATION 3 ACCESS 0
	ADD OPTION aRotina Title 'Alterar'    Action 'VIEWDEF.SGAA560' OPERATION 4 ACCESS 0
	ADD OPTION aRotina Title 'Excluir'    Action 'VIEWDEF.SGAA560' OPERATION 5 ACCESS 0
	ADD OPTION aRotina Title 'Imprimir'   Action 'VIEWDEF.SGAA560' OPERATION 8 ACCESS 0
	ADD OPTION aRotina Title 'Copiar'     Action 'VIEWDEF.SGAA560' OPERATION 9 ACCESS 0 

Return aRotina                                                       

//-------------------------------------------------------------------
/*/{Protheus.doc} ValidData
Valida a linha de resultado.
     
@param oModelGrid - Modelo de dados da grid

@author Thiago Henrique dos Santos
@since 06/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Static Function ValidLinha(oModelGrid)
	Local lRet := oModelGrid:GetValue('TDO_DTINI') <= oModelGrid:GetValue('TDO_DTFIM') 
	Local nI := 0
	Local dDtIni := oModelGrid:GetValue('TDO_DTINI')
	Local dDtFim := oModelGrid:GetValue('TDO_DTFIM') 
	local nAtu := oModelGrid:GetLine()       

	If !lRet                   
		Help( " ",1, 'SGAA56001')	//"Período inválido.#A data inicial deve ser menor ou igual a data final."   		
	Else

		For nI := 1 To oModelGrid:GetQtdLine()
		
			oModelGrid:GoLine(nI)
			If(!oModelGrid:IsDeleted( nI ) .AND. nI <> nAtu )
			
				If dDtIni >= oModelGrid:GetValue('TDO_DTINI') .AND. dDtIni <= oModelGrid:GetValue('TDO_DTFIM') 
					lRet := .F.
				ElseIf dDtFim >= oModelGrid:GetValue('TDO_DTINI') .AND. dDtFim <= oModelGrid:GetValue('TDO_DTFIM') 
					lRet := .F.			
				ElseIf dDtIni < oModelGrid:GetValue('TDO_DTINI') .AND. dDtFim > oModelGrid:GetValue('TDO_DTFIM') 
					lRet := .F.			
				Endif
			
				If !lRet  
				
					Help( " ",1, 'SGAA56002')	//"Período inválido.#"Sobreposição de períodos."#"Corrija as datas dos períodos sobrepostos." 		
					Exit
					
				Endif
			Endif   
		Next nI

	Endif    

	oModelGrid:GoLine(nAtu)
     
Return lRet                                                          

//-------------------------------------------------------------------
/*/{Protheus.doc} ValidData
Obtém as Metas e lancamentos que utilizaram a variável
     
@param oModelGrid - Modelo de dados da grid

@author Thiago Henrique dos Santos
@since 06/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Static Function CarregaMetas(oModel)
	Local cMemoFor :=""
	local nOperation := oModel:GetOperation()

	__aMetas := {}


	If  nOperation == MODEL_OPERATION_UPDATE               

		DbSelectArea("TDP")
		TDP->(DbSetOrder(1))
	   
	   TDP->(DbSeek(xFilial("TDP")))      
		//buscando formulas
		While TDP->(!Eof()) .AND. TDP->TDP_FILIAL == xFilial("TDP")
					  
			cMemoFor := MSMM(TDP->TDP_FORMUL)	
			
			If "#"+oModel:GetValue('TDNMASTER','TDN_DESCRI')+"#" $ cMemoFor
					
				//buscando lancamentos
				DbSelectArea("TBK")
				TBK->(DbSetOrder(3))
				TBK->(DbSeek(xFilial("TBK")+TDP->TDP_CODFOR))
				
				While TBK->(!Eof()) .AND. TBK->TBK_FILIAL == xFilial("TBK") .AND. TBK->TBK_CODFOR == TDP->TDP_CODFOR
				
					AADD(__aMetas,TBK->TBK_DTRESUL)			
					 
					TBK->(DbSkip())
				Enddo
			
			Endif
		
			TDP->(DbSkip())
		Enddo

	Endif

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} PreLinha
Pre-Valida 
     
@param oModelGrid - Modelo de dados da grid

@author Thiago Henrique dos Santos
@since 06/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Static Function PreLinha(oModelGrid)
	Local lRet := .T.
	Local nI := 0
	Local dDtIni := oModelGrid:GetValue('TDO_DTINI')
	Local dDtFim := oModelGrid:GetValue('TDO_DTFIM') 

	For nI:=1 to len(__aMetas)

		If __aMetas[nI] >= dDtIni .AND. __aMetas[nI] <= dDtFim
		
			lRet := .F.
			
			Help( " ",1, 'SGAA56003')	//"Não permitido. "#"Resultado já lançado."#"Estorne os lançamentos do período.
			EXIT
		
		EndIF

	Next nI

Return lRet                                                          


//-------------------------------------------------------------------
/*/{Protheus.doc} PosValid
Pós validação. Não permiti excluir variável utilizada em fórmula. 
     
@param oModel - Modelo de dados da grid

@author Thiago Henrique dos Santos
@since 06/02/2013
@version P11
/*/
//-------------------------------------------------------------------                        
Static Function PosValid(oModel)

	Local cMemoFor :=""
	local nOperation := oModel:GetOperation()
	Local lRet := .T.

	__aMetas := {}


	If  nOperation == MODEL_OPERATION_DELETE               

		DbSelectArea("TDP")
		TDP->(DbSetOrder(1))
	   
	   TDP->(DbSeek(xFilial("TDP")))      
		//buscando formulas
		While TDP->(!Eof()) .AND. TDP->TDP_FILIAL == xFilial("TDP")
					  
			cMemoFor := MSMM(TDP->TDP_FORMUL)	
			
			If "#"+oModel:GetValue('TDNMASTER','TDN_DESCRI')+"#" $ cMemoFor  
			
				Help( " ",1, 'SGAA56004')
			
				Return .F.
			
			Endif
			
			TDP->(DbSkip())
		Enddo
	Endif

Return lRet