#INCLUDE "MNTA070.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

#DEFINE _nVERSAO 3 //Versao do fonte

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA070
Cadastro de Fabricantes de Bens

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Function MNTA070()
	
	Local aNGBeginPrm := NGBeginPrm( _nVERSAO )
	Local oBrowse
	
	Private aRotina	:= MenuDef()
	
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias( "ST7" )
	oBrowse:SetDescription( NgSX2Nome( "ST7" ) ) // "Fabricantes de Bens"
	oBrowse:Activate()
	
	NGReturnPrm( aNGBeginPrm )
	
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Interface da rotina

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function MenuDef()
// Inicializa MenuDef com todas as opções
Return FWMVCMenu( "MNTA070" )

//------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ModelDef()

	Local oModel
	Local oStructST7 := FWFormStruct(1,"ST7")
	Local bCommit	 := { |oModel| CommitInfo( oModel ) }

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTA070", , , bCommit )

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTA070_ST7", Nil, oStructST7 )

	oModel:SetDescription( NgSX2Nome( "ST7" ) )

Return oModel

//------------------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel("MNTA070")
	Local oView  := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA070_ST7" , FWFormStruct(2,"ST7") )	

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100 )

	// Associa um View a um box
	oView:SetOwnerView( "MNTA070_ST7" , "MASTER" )

	// Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn( oView )

Return oView

//------------------------------------------------------------------------------
/*/{Protheus.doc} CommitInfo
Grava todas as informações

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function CommitInfo( oModel )

	Local nOpc := oModel:GetOperation()

	//--------------------------------------------------------------------------
	// Grava informações do Model
	//--------------------------------------------------------------------------
	FwFormCommit( oModel )

	//--------------------------------------------------------------------------
	// Integração com PIMS
	//--------------------------------------------------------------------------
	If SuperGetMV("MV_PIMSINT",.F.,.F.) .And. FindFunction("NGIntPIMS")
		NGIntPIMS( "ST7" , ( "ST7" )->( RecNo() ) , nOpc )
	EndIf

Return .T.