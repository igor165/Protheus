#INCLUDE "Mntc665.ch"
#Include "Protheus.ch"
#Include "DbTree.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTC665   ³ Autor ³ Thiago Olis Machado   ³ Data ³ 22.08.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Analise de Similaridade   (Refeito em 25/08/2004 - Inacio   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³Emitir relatorio com bens similares aos parametros informa- ³±±
±±³          ³dos na pesquisa.                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAMNT                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTC665()

	Local aNGBEGINPRM := NGBEGINPRM()

	Private aRotina   := MenuDef()
	Private cCadastro := OemToAnsi(STR0004) //"Análise de Similaridade"
	Private aChkDel := {}
	Private asMenu := {}, oMenu

	dbSelectArea("ST6")
	dbSetOrder(1)
	MBrowse(6,1,22,75,"ST6")

	NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNC665SIM ³ Autor ³ Thiago Olis Machado   ³ Data ³22/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Obter filtos do ST9 e STB                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNC665SIM()

	Local nI, oFont, oGet, cCmbOp
	Local lOk      := .F.
	Local aPages   := {}
	Local aTitles  := {}
	Local aVar     := {}
	Local aAltCols := { "TB_CARACTE", "TPC_CONDOP", "TB_DETALHE", "TB_INFO02", "TB_UNIDADE" }
	Local aSize    := MsAdvSize(,.F.,430)

	Private oDlg, cRet, cRet1
	Private n         := 1
	Private x         := 0
	Private nControGd := 1
	Private aCols     := {}
	Private aHeader   := {}


	aAdd(aTitles, OemToAnsi(STR0005) )  //"Bens"
	aAdd(aPages, "Header 1")

	aAdd(aTitles, OemToAnsi(STR0006) )  //"Caracteristicas"
	aAdd(aPages, "Header 2" )

	fMontaHead()

	dbSelectArea("SX3")
	dbSetOrder(1)
	aAdd(aCols,{Space(TAMSX3("TB_CARACTE")[1]),; // Indica a característica.
				Space(TAMSX3("TB_NOME")[1]   ),; // Indica o nome da característica
				Space(TAMSX3("TPC_CONDOP")[1]),; // Indica a operação
				Space(TAMSX3("TB_DETALHE")[1]),; // Indica o 'Detalhe 1' da característica
				Space(TAMSX3("TB_INFO02")[1] ),; // Indica o 'Detalhe 2' da característica
				Space(TAMSX3("TB_UNIDADE")[1]),; // Indica a unidade da característica
				.F.} )                           // Indica registros deletados

	Define MsDialog oDlg Title cCadastro From aSize[7],0 To aSize[6],aSize[5] Of oMainWnd Pixel

		oFolder := TFolder():New( 1, 0, aTitles, aPages, oDlg,,,, .F., .F., 320, 200, )
		oFolder:aDialogs[1]:oFont := oDlg:oFont
		oFolder:aDialogs[2]:oFont := oDlg:oFont

		//Filtro de Bens
		dbSelectArea("ST9")
		BuildExpr(,,@cRet,,,oFolder:aDialogs[1])

		oGet := MsNewGetDados():New( 0, 0, 1000, 1000, GD_INSERT + GD_UPDATE + GD_DELETE, ;
		       {|| MNTC665LOK( oGet )},{|| MNTC665LOK( oGet, .F. )},, aAltCols,, 9999,,,, oFolder:aDialogs[2], aHeader, aCols )

		oGet:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	Activate Dialog oDlg On Init EnchoiceBar(oDlg,{|| lOk := .T., IIf( MNTC665TOK( oGet ), oDlg:End(), lOk := .F.)} ,{||oDlg:End()},;
								 AlignObject(oDlg,{oFolder},1))

	If lOk
		NG665GRATRB(oGet)
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTC665LOK ³ Autor ³ In cio Luiz Kolling   ³ Data ³25/08/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao da linha do getdados                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTC665LOK( oGet, lLinOk )

	Local nCaract, nCondOp,nInfo02, nDetail, nI
	Local aColsOk, aHeadOk
	Local lRet := .T.

	Default lLinOk := .F.

	aColsOk := aClone( oGet:aCols )
	aHeadOk := aClone( oGet:aHeader )
	n       := oGet:nAt

	nDetail := aScan( aHeader, {|x| Trim(Upper(x[2])) == "TB_DETALHE" })
	nCaract := aScan( aHeader, {|x| Trim(Upper(x[2])) == "TB_CARACTE" })
	nCondOp := aScan( aHeader, {|x| Trim(Upper(x[2])) == "TPC_CONDOP" })
	nInfo02 := aScan( aHeader, {|x| Trim(Upper(x[2])) == "TB_INFO02" })
	For nI := 1 To Len(aColsOk)
		If !ATail(aColsOk[nI])
			If !lLinOk .Or. nI == n
				If Empty(aColsOk[nI][nCaract])
					ShowHelpDlg( "CARACTERÍSTICA", { STR0079 }, 5,;	//"O campo Característica não foi informado."
					{ STR0080 }, 5)						//"Informe uma característica para prosseguir."
					lRet := .F.
				Endif

				If lRet .And. Empty(aColsOk[nI][nDetail])
					ShowHelpDlg( "DETALHE", { STR0081 }, 5,;	//"O campo Detalhe não foi informado."
					{ STR0082 }, 5)			//"Informe uma característica para prosseguir."
					lRet := .F.
				EndIf
				If lRet .And. Empty(aColsOk[nI][nCondOp])
					ShowHelpDlg( "OPERADOR", { STR0083 }, 5,; //"O campo Detalhe não foi informado."
					{ STR0084 }, 5)			//"Informe o Detalhe para prosseguir."
					lRet := .F.
				EndIf
				If lRet .And. Empty(aColsOk[nI][nCondOp])
					ShowHelpDlg( "OPERADOR", { STR0085 }, 5,; //"O campo Operador não foi informado."
					{ STR0086 }, 5)					//"Informe um operador para prosseguir."
					lRet := .F.
				EndIf
				If lRet
					If aColsOk[nI][nCondOp] == '7' .And. Empty(aColsOk[nI][nInfo02])
						ShowHelpDlg( "OPERADOR", { STR0087 }, 5,;	//"O campo Detalhe 2 é obrigatório quando o operador for 'Entre'."
						{ STR0088 }, 5)			//"Informe o Detalhe 2 para prosseguir."
						lRet := .F.
					EndIf
				EndIf
				If lRet
					If nI <> n .And. !ATail(aColsOk[n])
						If aColsOk[nI][nCaract] == aColsOk[n][nCaract]
							Help(" ", 1, "JAEXISTINF",, aHeadOk[nCaract][1])
							lRet := .F.
							Exit
						Endif
					Endif
				EndIf
				If !lRet
					Exit
				EndIf
			Endif
		Endif
	Next nI

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTC665TOK ³ Autor ³ In cio Luiz Kolling   ³ Data ³25/08/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao da final do getdados                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTC665TOK( oGet )

	Local lRet  := .T.

	If !MNTC665LOK( oGet, .F. )
		lRet := .F.
	EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG665GRATRB³ Autor ³ Thiago Olis Machado   ³ Data ³26/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grava arquivo com filtro e monta MARKBROWSE                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTC665                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG665GRATRB(oGet)

	Local i := 0
	Local nCondOp, nCaract, nDetail, nUnidad

	Private aRotina := {{ STR0002 ,"NG665VIS", 0, 2},; //"Visualizar"
	{ STR0007 ,"NG665MAN", 0, 3},; //"Manutencao"
	{ STR0008 ,"NG665HIS", 0, 3},; //"Historico "
	{ STR0009 ,"NG665IMP", 0, 3}}  //"Imprimir  "

	Private cTRBXY := GetNextAlias()
	Private lRetorno	:= .F.
	Private oTmpTRB
	Private aIdxTRB

	aCols   := aClone( oGet:aCols )
	aHeader := aClone( oGet:aHeader )

	nCaract := aScan( aHeader, {|x| Trim( Upper( x[2] ) ) == "TB_CARACTE" })
	nCondOp := aScan( aHeader, {|x| Trim( Upper( x[2] ) ) == "TPC_CONDOP" })
	nDetail := aScan( aHeader, {|x| Trim( Upper( x[2] ) ) == "TB_DETALHE" })
	nUnidad := aScan( aHeader, {|x| Trim( Upper( x[2] ) ) == "TB_UNIDADE" })
	nConte2 := aScan( aHeader, {|x| Trim( Upper( x[2] ) ) == "TB_INFO02"  })

	aDadosSTB := {}

	For i:= 1 To Len(aCols)
		If !ATail(aCols[i])
			If !Empty(aCols[i][nCaract])

				cOp3 := Alltrim(aCols[i][nCondOp])
				aAdd(aDadosSTB, { cOp3, aCols[i][nCaract], aCols[i][nDetail], aCols[i][nConte2], aCols[i][nUnidad]})

			Endif
		Endif
	Next i

	aDBF := {}

	aAdd( aDBF, {"OK"     , "C", 2                       , 0})
	aAdd( aDBF, {"CODBEM" , "C", TAMSX3("T9_CODBEM")[1]  , 0})
	aAdd( aDBF, {"FAMILIA", "C", TAMSX3("T9_CODFAMI")[1] , 0})
	aAdd( aDBF, {"NOMBEM" , "C", TAMSX3("T9_NOME")[1]    , 0})
	aAdd( aDBF, {"CCUSTO" , "C", TAMSX3("T9_CCUSTO")[1]  , 0})
	aAdd( aDBF, {"CTRAB"  , "C", TAMSX3("T9_CENTRAB")[1] , 0})
	aAdd( aDBF, {"ESTRUT" , "C", 1                       , 0})

	aIdxTRB   := {{"CODBEM"}}
	oTmpTRB := NGFwTmpTbl(cTRBXY,aDBF,aIdxTRB)

	aTRB := {}

	aAdd( aTRB, {"OK"     , Nil, " "      ,})
	aAdd( aTRB, {"CODBEM" , Nil, STR0010  ,}) //"Bem"
	aAdd( aTRB, {"FAMILIA", Nil, STR0011  ,}) //"Familia"
	aAdd( aTRB, {"NOMBEM" , Nil, STR0012  ,}) //"Nome do Bem"
	aAdd( aTRB, {"CCUSTO" , Nil, STR0013  ,}) //"Centro Custo"
	aAdd( aTRB, {"CTRAB"  , Nil, STR0014  ,}) //"C. Trabalho"
	aAdd( aTRB, {"ESTRUT" , Nil, STR0015  ,}) //"Tem Estrutura"

	NGTRBSEL()

	If lRetorno
		//NGDELETRB(cTRBXY,cArqTrb)
		oTmpTRB:Delete()
		Return .T.
	EndIf

	cMarca   := GetMark()
	lInverte := .F.

	dbSelectArea(cTRBXY)
	dbGoTop()

	If EoF()
		MsgStop(STR0016) //"Não existem dados a serem visualizados."
	Else
		MARKBROW(cTRBXY,"OK","",aTRB,lINVERTE,cMARCA,"MNTC665INV()")
	EndIf

	//NGDELETRB(cTRBXY,cArqTrb)
	oTmpTRB:Delete()
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTC665INV³ Autor ³ In cio Luiz Kolling   ³ Data ³26/08/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Inverte marcacoes - Windows                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTC665                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTC665INV()

	Local nREG := 0

	dbSelectArea(cTRBXY)

	nReg := Recno()

	dbGoTop()
	While !Eof()
		(cTRBXY)->OK := IIF(!Empty(OK) ," ",cMARCA)
		dbSelectArea(cTRBXY)
		dbSkip()
	EndDo

	dbGoTo(nREG)
	lRefresh := .T.

Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NGTRBSEL   ³ Autor ³ Thiago Olis Machado   ³ Data ³26/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Faz a Selecao dos arquivos conforme filtro                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NGTRBSEL()

	Local i := 0
	Local cTpInfo

	If Empty(cRet) .And. Empty(aDadosSTB)
		MsgStop(STR0016) //"Não existem dados a serem visualizados."
		lRetorno := .T.
		Return .T.
	EndIf

	cFiltro := cRet

	dbSelectArea("ST9")
	dbSetOrder(04)
	dbSeek(xFilial("ST9") + ST6->T6_CODFAMI)
	While !EoF() .And. xFilial("ST9") == ST9->T9_FILIAL  .And.;
	ST9->T9_CODFAMI == ST6->T6_CODFAMI

		lSimila := .T.

		If !Empty(cRet)
			If !&(cFiltro)
				lSimila := .F.
			EndIf
		Endif

		If lSimila
			If !Empty(aDadosSTB)
				For i := 1 To Len(aDadosSTB)

					lSimila := .T.

					dbSelectArea("STB")
					dbSetOrder(1)
					If dbSeek( xFilial("STB") + ST9->T9_CODBEM + aDadosSTB[i][2])

						cTpInfo := NGSEEK("TPR", aDadosSTB[i][2], 1, "TPR_TPINFO")

						// Caso tenha a característica e não esteja de acordo com o filtro lSimila := .F.
						If !fCheckFil( STB->TB_DETALHE, STB->TB_INFO02, aDadosSTB[i][1], aDadosSTB[i][3], aDadosSTB[i][4], cTpInfo ) .Or.;
						STB->TB_UNIDADE <> aDadosSTB[i][5]

							lSimila := .F.
							Exit
						EndIf
					Else
						// Caso não tenha a característica lSimila := .F.
						lSimila := .F.
						Exit
					EndIf
				Next i
			EndIf
		EndIf

		If lSimila
			(cTRBXY)->(DbAppend())
			(cTRBXY)->CODBEM := ST9->T9_CODBEM
			(cTRBXY)->FAMILIA:= ST9->T9_CODFAMI
			(cTRBXY)->NOMBEM := ST9->T9_NOME
			(cTRBXY)->CCUSTO := ST9->T9_CCUSTO
			(cTRBXY)->CTRAB  := ST9->T9_CENTRAB
			(cTRBXY)->ESTRUT := ST9->T9_ESTRUTU
		EndIf

		dbSelectArea("ST9")
		dbSkip()
	EndDo

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG665VIS   ³ Autor ³ Thiago Olis Machado   ³ Data ³26/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Faz a visualizacao do arquivo selecionado                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG665VIS()

	Local aOldRot := aClone( aRotina )

	aRotina := { {STR0001, "AXPesqui" , 0, 1},;    //"Pesquisar"
	{STR0002, "NG665VIS" , 0, 2}}     //"Visualizar"

	dbSelectArea("ST9")
	dbSetOrder(01)
	dbSeek(xFilial("ST9") + (cTRBXY)->CODBEM)
	NG080FOLD("ST9",Recno(),2)

	aRotina := aClone(aOldRot)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG665MAN   ³ Autor ³ Thiago Olis Machado   ³ Data ³26/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Apresenta as manutencoes daquele determinado Bem             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG665MAN()

	dbSelectArea("ST9")
	dbSetOrder(01)
	If dbSeek(xFilial("ST9") + (cTRBXY)->CODBEM)
		MNC600CON()
	Else
		If EoF()
			Help( "", 1, "ARQVAZIO" )
		Endif
	Endif
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG665HIS   ³ Autor ³ Thiago Olis Machado   ³ Data ³26/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Apresenta as Ordens de Servico daquele Bem                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG665HIS()

	dbSelectArea("ST9")
	dbSetOrder(01)
	dbSeek(xFilial("ST9") + (cTRBXY)->CODBEM)
	MNC510HIS("ST9", Recno(), 3)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NG665IMP ³ Autor ³ Thiago Olis Machado   ³ Data ³22/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de informacoes do bem                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG665IMP()

	Local cString := "ST9"
	Local lTemArc := .F.
	Local cDesc1  := STR0025 //"Relatorio dos Bens escolhidos resultante da similaridade."
	Local cDesc2  := ""
	Local cDesc3  := ""
	Local wnrel   := "MNTC665"

	Private aReturn  := { STR0026, 1, STR0027, 2, 2, 1, "", 1 }  //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := STR0028 //"Similaridade de Bens"
	Private Tamanho  := "M"

	dbSelectArea(cTRBXY)

	nRECTRB := Recno()

	dbGoTop()
	While !Eof()
		If !Empty((cTRBXY)->OK)
			lTemArc := .T.
			Exit
		Endif
		dbSkip()
	EndDo

	If !lTemArc
		MsgInfo(STR0075, STR0073)
		dbGoTo(nRECTRB)
		Return
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel := SetPrint( cString, wnrel, " ", titulo, cDesc1, cDesc2, cDesc3, .F., "" )

	If nLastKey = 27
		Set Filter To
		dbSelectArea("STI")
		Return
	Endif

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R600Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

	dbSelectArea("STI")

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R600Imp  ³ Autor ³ Thiago Olis Machado   ³ Data ³22/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime informacoes do bem                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NG665IMP                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R600Imp(lEnd,wnRel,titulo,tamanho)

	Local cRodaTxt  := ""
	Local nCntImpr  := 0

	Private li := 80 ,m_pag := 1
	Private Cabec1       := " "
	Private Cabec2       := " "
	Private nomeprog     := "MNTC665"

	nTipo := IIf( aReturn[4] == 1, 15, 18 )

	/*        1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012

	Bem.......: XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Calend: XXX XXXXXXXX27XXXXXXXXXXXXXXXXX  Bem Padrao: XXX
	Pai.......: XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	Fornecedor: XXXXXX XX        XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Fabricante: XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	Modelo....: XXXXXXXXXXXXXXXXXXXX Serie..: XXXXXXXXXXXXXXXX  Dt.Compra.: DD/MM/AAAA      Cod.Imob: XXXXXXXX     Prazo Gar.: XXXXXXX
	Dt Garant.: dd/mm/aaaa           Prior .: XXX               Cod.Estoq.: XXXXXXXXXXXXXXX Tem Estr: XX           Contador..: XXXXXXXXX
	Dt Acomp..: dd/mm/aaaa           Var/Dia: XXXXXX            Limite Con: XXXXXX          Sit.Bem.: XXXXXXXXX    Sit.Manut.: XXXXXXXXXX
	Custo Hora: 999.999,99            Local.: XXXXXX            Tipo(PCP).: X             Recur/Ferr: XXXXXX            Chapa: XXXXXX
	Vida Util.: X             Valor Vida Ut.: 999.999.999  Unid.Vida Util.: X           Valor Compra: X.XXX.XXX.XXX,XX
	Cliente...: XXXXXX                 Nome.: XXXXXXXXXXXXXXXXX30XXXXXXXXXXX         Data Instalacao: XX/XX/XXXX
	Ponderacao: Jan  Fev  Mar  Abr  Mai  Jun  Jul  Ago  Set  Out  Nov   Dez
	X,XX X,XX X,XX X,XX X,XX X,XX X,XX X,XX X,XX X,XX X,XX, X,XX

	Caracter..: XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXX  XX
	XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXX  XX

	*/

	dbSelectArea(cTRBXY)
	nRECTRB := Recno()
	dbGoTop()
	SetRegua(LastRec())
	While !EoF()
		IncRegua()
		If !Empty((cTRBXY)->OK)
			dbSelectArea("ST9")
			dbSetOrder(01)
			dbSeek(xFilial("ST9")+(cTRBXY)->CODBEM)

			NGSOMALI(58)
			@ Li,000 Psay STR0067 //"C.Custo.: "
			@ Li,010 Psay st9->t9_ccusto
			@ Li,031 Psay NGSEEK('SI3',st9->t9_ccusto,1,'Substr(I3_DESC,1,24)')

			@ Li,056 Psay STR0068 //"C.Trabalho.: "
			@ Li,070 Psay st9->t9_centrab
			@ Li,078 Psay NGSEEK('SHB',st9->t9_centrab,1,'Substr(HB_NOME,1,24)')

			@ Li,091 Psay STR0069 //"Familia.: "
			@ Li,101 Psay st9->t9_codfami
			@ Li,108 Psay NGSEEK('ST6',st9->t9_codfami,1,'Substr(T6_NOME,1,15)')

			NGSOMALI(58)
			@ Li,000 Psay Replicate("*",132)

			NGSOMALI(58)

			/*------------------------------------------------------------
			Verifica se o Bem tem Bem Pai
			------------------------------------------------------------*/
			dbSelectArea("STC")
			dbSetOrder(03)
			cChave := ST9->T9_CODBEM

			While .T.
				dbSeek(xFilial('STC')+cChave)
				If Found()
					cChave := STC->TC_CODBEM
					dbSkip()
					Loop
				EndIf
				Exit
			EndDo

			nAtual := ST9->(Recno())  // guarda o endereco do Bem Atual

			@ Li,000 Psay STR0030 //"Bem.......:"
			@ Li,012 Psay ST9->T9_CODBEM
			@ Li,029 Psay Substr(ST9->T9_NOME,1,40)
			@ Li,070 Psay STR0031 //"Calend:"
			@ Li,078 Psay ST9->T9_CALENDA
			@ Li,082 Psay NGSEEK('SH7',ST9->T9_CALENDA,1,'Substr(H7_DESCRI,1,27)')

			dbSelectArea("ST9")

			@ Li,111 Psay STR0032 //"Bem Padrao:"

			If ST9->T9_PADRAO == 'S'
				@ Li,123 Psay STR0033 //"SIM"
			Else
				@ Li,123 Psay STR0034 //"NAO"
			EndIf

			NGSOMALI(58)

			/*------------------------------------------------------------
			Impressão da Segunda Linha do Bem
			------------------------------------------------------------*/
			If ST9->T9_CODBEM != cChave
				dbSelectArea("ST9")
				dbSetOrder(01)
				dbSeek(xFilial("ST9") + cChave)

				@ Li,000 Psay STR0035 //"Pai.......:"
				@ Li,012 Psay cChave
				@ Li,029 Psay ST9->T9_NOME

				NGSOMALI(58)
			EndIf

			dbSelectArea("ST9")
			dbSetOrder(02)
			dbGoTo(nAtual)

			/*------------------------------------------------------------
			Impressão da Terceira Linha do Bem
			------------------------------------------------------------*/
			@ Li,000 Psay STR0036 //"Fornecedor:"
			@ Li,012 Psay ST9->T9_FORNECE
			@ Li,019 Psay ST9->T9_LOJA
			@ Li,029 Psay NGSEEK('SA2',ST9->T9_FORNECE+ST9->T9_LOJA,1,'Substr(A2_NOME,1,39)')

			@ Li,070 Psay STR0037 //"Fabricante:"
			@ Li,082 Psay ST9->T9_FABRICA
			@ Li,090 Psay NGSEEK('ST7',ST9->T9_FABRICA,1,'T7_NOME')

			NGSOMALI(58)

			/*------------------------------------------------------------
			Impressão da Quarta Linha do Bem
			------------------------------------------------------------*/
			@ Li,000 Psay STR0038 //"Modelo....:"
			@ Li,012 Psay ST9->T9_MODELO
			@ Li,033 Psay STR0039 //"Serie..:"
			@ Li,042 Psay ST9->T9_SERIE
			@ Li,060 Psay STR0040 //"Dt.Compra.:"
			@ Li,072 Psay ST9->T9_DTCOMPR Picture '99/99/9999'
			@ Li,088 Psay STR0041 //"Cod.Imob:"
			@ Li,098 Psay ST9->T9_CODIMOB
			@ Li,111 Psay STR0076 //"Prazo Gar.:"
			@ Li,123 Psay ST9->T9_PRGARAN Picture "@E 999999"
			@ Li,130 Psay ST9->T9_UNGARAN

			NGSOMALI(58)

			/*------------------------------------------------------------
			Impressão da Quinta Linha do Bem
			------------------------------------------------------------*/
			@ Li,000 Psay STR0042 //"Dt.Garant.:"
			@ Li,012 Psay ST9->T9_DTGARAN Picture '99/99/9999'
			@ Li,033 Psay STR0043 //"Prior..:"
			@ Li,042 Psay ST9->T9_PRIORID
			@ Li,060 Psay STR0044 //"Cod.Estoq.:"
			@ Li,072 Psay ST9->T9_CODESTO
			@ Li,088 Psay STR0045 //"Tem Estr:"
			@ Li,098 Psay ST9->T9_ESTRUTU
			@ Li,111 Psay STR0046 //"Contador..:"
			@ Li,123 Psay ST9->T9_POSCONT Picture '999999999'

			NGSOMALI(58)

			/*------------------------------------------------------------
			Impressão da Sexta Linha do Bem
			------------------------------------------------------------*/
			@ Li,000 Psay STR0047 //"Dt Acomp..:"
			@ Li,012 Psay ST9->T9_DTULTAC Picture '99/99/9999'
			@ Li,033 Psay STR0048 //"Var/Dia:"
			@ Li,042 Psay ST9->T9_VARDIA
			@ Li,060 Psay STR0049 //"Limite Con:"
			@ Li,072 Psay ST9->T9_LIMICON
			@ Li,088 Psay STR0050 //"Sit.Bem.:"
			@ Li,098 Psay ST9->T9_SITBEM
			@ Li,111 Psay STR0051 //"Sit.Manut.:"
			@ Li,123 Psay ST9->T9_SITMAN

			NGSOMALI(58)

			@ Li,000 Psay STR0052 //"Custo Hora:"
			@ Li,012 Psay ST9->T9_CUSTOHO Picture "@E 999,999.99"
			@ Li,034 Psay STR0053 //"Local.:"
			@ Li,042 Psay ST9->T9_LOCAL
			@ Li,060 Psay STR0054 //"Tipo(PCP).:"
			@ Li,072 Psay ST9->T9_FERRAME
			@ Li,086 Psay STR0055 //"Recur/Ferr:"
			@ Li,098 Psay ST9->T9_RECFERR
			@ Li,116 Psay STR0056 //"Chapa:"
			@ Li,123 Psay ST9->T9_CHAPA

			NGSOMALI(58)

			@ Li,000 Psay STR0057 //"Vida Util.:"
			@ Li,012 Psay ST9->T9_CONTDES
			@ Li,026 Psay STR0058 //"Valor Vida Ut.:"
			@ Li,042 Psay ST9->T9_VALODES Picture '@E 999,999,999'
			@ Li,055 Psay STR0059 //"Unid.Vida Util.:"
			@ Li,072 Psay ST9->T9_UNIDDES
			@ Li,084 Psay STR0060 //"Valor Compra:"
			@ Li,098 Psay ST9->T9_VALCPA Picture '@E 999,999,999.99'

			NGSOMALI(58)

			@ Li,000 Psay STR0061 //"Cliente...:"
			@ Li,012 Psay ST9->T9_CLIENTE
			@ Li,035 Psay STR0062 //"Nome.:"
			@ Li,042 Psay NGSEEK('SA1',ST9->T9_CLIENTE,1,'Substr(A1_NOME,1,30)')
			@ Li,081 Psay STR0063 //"Data Instalacao:"
			@ Li,098 Psay ST9->T9_DTINSTA Picture '99/99/9999'
			@ Li,111 Psay STR0064 //"Contador 2:"
			@ Li,126 Psay NGSEEK("TPE",ST9->T9_CODBEM,1,"TPE_POSCON") Picture '999999'

			/*------------------------------------------------------------
			Impressão da Linha de Ponderação de Uso do Bem
			------------------------------------------------------------*/
			NGSOMALI(58)

			@ Li,000 Psay STR0065 //"Ponderacao:  Jan  Fev  Mar  Abr  Mai  Jun  Jul  Ago  Set  Out  Nov  Dez"

			NGSOMALI(58)

			If !Empty(ST9->T9_PONDERA)
				@ Li,012 Psay ST9->T9_PONDERA
				NGSOMALI(58)
			EndIf

			/*------------------------------------------------------------
			Impressão Linha de Características do Bem
			------------------------------------------------------------*/
			dbSelectArea("STB")
			dbSetOrder(01)
			If dbSeek(xFilial('STB') + ST9->T9_CODBEM)
				@ Li,000 Psay STR0066 //"Caracter..:"

				While !EoF() .And. STB->TB_FILIAL == xFilial("STB") .And.;
				STB->TB_CODBEM == ST9->T9_CODBEM

					@ Li,012 Psay STB->TB_CARACTE
					@ Li,020 Psay NGSEEK("TPR", STB->TB_CARACTE, 1, "Substr(TPR_NOME,1,TAMSX3('TB_NOME')[1])")

					@ Li,056 Psay NGRetSX3Box( "TB_CONDOP", STB->TB_CONDOP)
					@ Li,063 Psay STB->TB_DETALHE
					@ Li,080 Psay STB->TB_INFO02
					@ Li,096 Psay STB->TB_UNIDADE

					NGSOMALI(58)

					dbSelectArea("STB")
					dbSkip()
				EndDo
			Endif

			/*------------------------------------------------------------
			Impressão Linha de Descrição
			------------------------------------------------------------*/
			NGIMPMEMO( ST9->T9_DESCRIC, 80, 0, STR0071, .F., .F.,, 58) //"Descricao.: "
			NGSOMALI(58)

			@ Li,000 Psay Replicate("-",132)
			NGSOMALI(58)
			NGSOMALI(58)
		EndIf

		dbSelectArea(cTRBXY)
		dbSkip()
	EndDo

	Roda(nCntImpr,cRodaTxt,Tamanho)

	dbSelectArea(cTRBXY)
	dbGoTo(nRECTRB)

	RetIndex("ST9")
	Set Filter To
	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif

	MS_FLUSH()

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ricardo Dal Ponte     ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³	  1 - Pesquisa e Posiciona em um Banco de Dados            ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

	Local aRotina := { {STR0001,"AxPesqui"  , 0 , 1},;  //"Pesquisar"
	{STR0002,"NGCAD01"   , 0 , 2},;  //"Visualizar"
	{STR0003,"MNC665SIM" , 0 , 3}}   //"Similaridade"

Return (aRotina)


//-----------------------------------------------------------------
/*/{Protheus.doc} MNT665WSTB
Valida When do campo TB_CONDOP utilizado na função MNC665SIM

@author Pedro Henrique Soares de Souza
@since 01/08/2014
@return .T.
/*/
//-----------------------------------------------------------------
Function MNT665WSTB(lSTB)

	Local nCondOp
	Local lRet := .T.

	Default lSTB := .F.

	If lSTB
		nCondOp := aScan( aHeader, {|x| AllTrim( Upper( x[2] ) ) == "TB_CONDOP" })
	Else
		nCondOp := aScan( aHeader, {|x| AllTrim( Upper( x[2] ) ) == "TPC_CONDOP" })
	EndIf

	If nCondOp > 0
		If aCols[n][nCondOp] != '7'
			lRet := .F.
		EndIf
	EndIf

Return lRet

//----------------------------------------------------------------
/*/{Protheus.doc} fCheckFil
Verifica se a etapa faz parte do filtro

@author Pedro Henrique Soares de Souza
@since 05/08/2014

@param cConteudo1 Indica o 'Detalhe 1' da tabela STB
@param cConteudo2 Indica o 'Detalhe 2' da tabela STB
@param cOp Indica a operação utilizada no filtro
@param cInfo1 Indica o 'Detalhe 1' utilizada no filtro
@param cInfo2 Indica o 'Detalhe 2' utilizada no filtro
@param cTpInfo Indica o tipo de informação da característicak

@return lRet Lógico
/*/
//----------------------------------------------------------------
Static Function fCheckFil( cConteudo1, cConteudo2, cOp, cInfo1, cInfo2, cTpInfo)

	Local aArea   := GetArea()
	Local lRet    := .F.
	Local cCondOp, cCondicao

	// Caso o tipo de informação seja Caráter ou Data adiciona aspas simples para validação
	If cTpInfo $ '1/3'
		cInfo1      := "'" + AllTrim(cInfo1) + "'"
		cInfo2      := "'" + AllTrim(cInfo2) + "'"
		cConteudo1  := "'" + AllTrim(cConteudo1) + "'"

		If !Empty( cConteudo2 )
			cConteudo2  := "'" + AllTrim(cConteudo2) + "'"
		EndIf
	EndIf

	// Verifica o tipo de operação do filtro
	Do Case
		Case cOp == "1"
		cCondOp := '#X1# == #Y1#'
		Case cOp == "2"
		cCondOp := '#X1# <> #Y1#'
		Case cOp == "3"
		cCondOp := '#X1# < #Y1#'
		Case cOp == "4"
		cCondOp := '#X1# > #Y1#'
		Case cOp == "5"
		cCondOp := '#X1# <= #Y1#'
		Case cOp == "6"
		cCondOp := '#X1# >= #Y1#'
		Case cOp == "7"
		cCondOp := '#X1# > #Y1# .AND. #X1# < #Y2#'
		OtherWise
		cCondOp := Space(TAMSX3('TB_CONDOP')[1])
	EndCase

	/*---------------------------------------------------------------
	Caso o 'Detalhe 02' da característica da STB esteja vazia...
	(Isso indica que a operação dela é 'Igual', se não, indica que
	a operação é 'Entre'.
	---------------------------------------------------------------*/
	If Empty( cConteudo2 )

		/*---------------------------------------------------------------
		Caso o tipo de informação seja Data, adiciona conversor CToD para
		comparação futura.
		---------------------------------------------------------------*/
		If cTpInfo == '3'
			cCondicao := StrTran( cCondicao, '#X1#', 'CToD(#X1#)')
			cCondicao := StrTran( cCondicao, '#Y1#', 'CToD(#Y1#)')
			cCondicao := StrTran( cCondicao, '#Y2#', 'CToD(#Y2#)')
		EndIf

		cCondicao := StrTran( cCondOp, '#X1#', cConteudo1 )
		cCondicao := StrTran( cCondicao, '#Y1#', cInfo1 )

		// Caso a operação do filtro seja 'Entre', substitui também o 'Detalhe 02'
		If cOp == '7'
			cCondicao := StrTran( cCondicao, '#Y2#', cInfo2 )
		EndIf
	Else
		If cOp == '1'
			//Caso a opção da fórmula seja 'Igual'
			cCondicao := '#Y1# > #X1# .AND. #Y1# < #X2#'

		ElseIf cOp == '2'
			//Caso a opção da fórmula seja 'Diferente'
			cCondicao := '#Y1# <= #X1# .OR. #Y1# >= #X2#'

		ElseIf cOp == '7'
			//Caso a opção da fórmula seja 'Entre'
			cCondicao := '(#Y1# > #X1# .AND. #Y1# < #X2#) .OR. (#Y2# > #X1# .AND. #Y2# < #X2#) .OR. '
			cCondicao += '(#Y1# <= #X2# .AND. #Y2# > #X1#)'

			/*---------------------------------------------------------------
			Caso o tipo de informação seja Data, adiciona conversor CToD para
			comparação futura.
			---------------------------------------------------------------*/
			If cTpInfo == '3'
				cCondicao := StrTran( cCondicao, '#Y2#', 'CToD(#Y2#)')
			EndIf

			// Subsitui Y2 pelo 'Detalhe 02' do filtro
			cCondicao := StrTran( cCondicao, '#Y2#', cInfo2 )

		Else
			/*---------------------------------------------------------------
			Caso opção da fórmula seja >, <, >= ou <=, utiliza apenas o
			operador, e cria uma nova validação
			---------------------------------------------------------------*/
			cCondOp := StrTran( cCondOp, '#X1#', '' )
			cCondOp := StrTran( cCondOp, '#Y1#', '' )

			cCondicao := '#X1# ' + cCondOp + ' #Y1# .OR. #X2# ' + cCondOp + ' #Y1#'
		EndIf

		/*---------------------------------------------------------------
		Caso o tipo de informação seja Data, adiciona conversor CToD para
		comparação futura.
		---------------------------------------------------------------*/
		If cTpInfo == '3'
			cCondicao := StrTran( cCondicao, '#X1#', 'CToD(#X1#)')
			cCondicao := StrTran( cCondicao, '#X2#', 'CToD(#X2#)')
			cCondicao := StrTran( cCondicao, '#Y1#', 'CToD(#Y1#)')
		EndIf

		cCondicao := StrTran( cCondicao, '#X1#', cConteudo1 )
		cCondicao := StrTran( cCondicao, '#X2#', cConteudo2 )
		cCondicao := StrTran( cCondicao, '#Y1#', cInfo1 )
	EndIf

	lRet := &(cCondicao)

	RestArea(aArea)
Return lRet
//-----------------------------------------------------------------
/*/{Protheus.doc} MNT665VSTB
Valida os campos utilizados no filtro de características
@author Pedro Henrique Soares de Souza
@since 01/08/2014
@return .T.
/*/
//-----------------------------------------------------------------
Function MNT665VSTB()
	Local nDetail, nConte2, nCaract
	Local cOldTpInfo, cNewTpInfo
	Local lRet := .T.
	// Verifica se foi executado update de características (Item 18)
	Do Case
		Case "TB_CARACTE" $ ReadVar()
		lRet := ExistCPO( 'TPR', M->TB_CARACTE )
		If lRet
			nCaract := aScan( aHeader, {|x| AllTrim( Upper( x[2] ) ) == "TB_CARACTE" })
			nDetail := aScan( aHeader, {|x| AllTrim( Upper( x[2] ) ) == "TB_DETALHE" })
			nConte2 := aScan( aHeader, {|x| AllTrim( Upper( x[2] ) ) == "TB_INFO02" })
			cOldTpInfo := NGSEEK("TPR", aCols[n][nCaract], 1, "TPR_TPINFO" )
			cNewTpInfo := NGSEEK("TPR", M->TB_CARACTE, 1, "TPR_TPINFO" )
			If cOldTpInfo != cNewTpInfo
				aCols[n][nDetail] := ""
				aCols[n][nConte2] := ""
			EndIf
		EndIf
	EndCase

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} fMontaHead
Monta o cabeçalho da grid conforme SX3

@author  Maicon André Pinheiro
@since   16/04/2018
/*/
//-------------------------------------------------------------------
Static Function fMontaHead()

	Local cTitle   := ""
	Local cCampo   := ""
	Local cPict    := ""
	Local cValid   := ""
	Local cUso     := ""
	Local cType    := ""
	Local cF3      := ""
	Local cContext := ""
	Local cCBOX    := ""
	Local cRelacao := ""
	Local cWhen    := ""
	Local nTam     := 0
	Local nDec     := 0

	If Posicione("SX3",2,"TB_CARACTE","X3_ARQUIVO") <> ""

		cTitle   := X3TITULO("TB_CARACTE")
		cCampo   := "TB_CARACTE"
		cPict    := X3Picture("TB_CARACTE")
		nTam     := TAMSX3("TB_CARACTE")[1]
		nDec     := TAMSX3("TB_CARACTE")[2]
		cValid   := "ExistCPO( 'TPR', M->TB_CARACTE )"
		cUso     := Posicione("SX3",2,"TB_CARACTE","X3_USADO")
		cType    := Posicione("SX3",2,"TB_CARACTE","X3_TIPO")
		cF3      := Posicione("SX3",2,"TB_CARACTE","X3_F3")
		cContext := Posicione("SX3",2,"TB_CARACTE","X3_CONTEXT")
		cCBOX    := X3CBOX("TB_CARACTE")
		cRelacao := Posicione("SX3",2,"TB_CARACTE","X3_RELACAO")
		cWhen    := Posicione("SX3",2,"TB_CARACTE","X3_WHEN")
		aAdd(aHeader,{cTitle, cCampo, cPict, nTam, nDec, cValid, cUso, cType, cF3, cContext, cCBOX, cRelacao, cWhen})

	EndIf

	If Posicione("SX3",2,"TB_NOME","X3_ARQUIVO") <> ""

		cTitle   := X3TITULO("TB_NOME")
		cCampo   := "TB_NOME"
		cPict    := "@!"
		nTam     := TAMSX3("TB_NOME")[1]
		nDec     := TAMSX3("TB_NOME")[2]
		cValid   := Posicione("SX3",2,"TB_NOME","X3_VALID")
		cUso     := Posicione("SX3",2,"TB_NOME","X3_USADO")
		cType    := Posicione("SX3",2,"TB_NOME","X3_TIPO")
		cF3      := Posicione("SX3",2,"TB_NOME","X3_F3")
		cContext := Posicione("SX3",2,"TB_NOME","X3_CONTEXT")
		cCBOX    := X3CBOX("TB_NOME")
		cRelacao := "''"
		cWhen    := Posicione("SX3",2,"TB_NOME","X3_WHEN")
		aAdd(aHeader,{cTitle, cCampo, cPict, nTam, nDec, cValid, cUso, cType, cF3, cContext, cCBOX, cRelacao, cWhen})

	EndIf

	cCmbOp := STR0077
	aAdd(aHeader,{ NGRETTITULO("TPC_CONDOP"), "TPC_CONDOP", "@!", 1, 0, "Pertence('1234567')", '€€€€€€€€€€€€€€ ', "C", '', "R", cCmbOp, "'1'", '.T.'})

	If Posicione("SX3",2,"TB_DETALHE","X3_ARQUIVO") <> ""

		cTitle   := X3TITULO("TB_DETALHE")
		cCampo   := "TB_DETALHE"
		cPict    := "@!"
		nTam     := TAMSX3("TB_DETALHE")[1]
		nDec     := TAMSX3("TB_DETALHE")[2]
		cValid   := Posicione("SX3",2,"TB_DETALHE","X3_VALID")
		cUso     := Posicione("SX3",2,"TB_DETALHE","X3_USADO")
		cType    := Posicione("SX3",2,"TB_DETALHE","X3_TIPO")
		cF3      := Posicione("SX3",2,"TB_DETALHE","X3_F3")
		cContext := Posicione("SX3",2,"TB_DETALHE","X3_CONTEXT")
		cCBOX    := X3CBOX("TB_DETALHE")
		cRelacao := Posicione("SX3",2,"TB_DETALHE","X3_RELACAO")
		cWhen    := Posicione("SX3",2,"TB_DETALHE","X3_WHEN")
		aAdd(aHeader,{cTitle, cCampo, cPict, nTam, nDec, cValid, cUso, cType, cF3, cContext, cCBOX, cRelacao, cWhen})

	EndIf

	If Posicione("SX3",2,"TB_INFO02","X3_ARQUIVO") <> ""

		cTitle   := X3TITULO("TB_INFO02")
		cCampo   := "TB_INFO02"
		cPict    := "@!"
		nTam     := TAMSX3("TB_INFO02")[1]
		nDec     := TAMSX3("TB_INFO02")[2]
		cValid   := Posicione("SX3",2,"TB_INFO02","X3_VALID")
		cUso     := Posicione("SX3",2,"TB_INFO02","X3_USADO")
		cType    := Posicione("SX3",2,"TB_INFO02","X3_TIPO")
		cF3      := Posicione("SX3",2,"TB_INFO02","X3_F3")
		cContext := Posicione("SX3",2,"TB_INFO02","X3_CONTEXT")
		cCBOX    := X3CBOX("TB_INFO02")
		cRelacao := Posicione("SX3",2,"TB_INFO02","X3_RELACAO")
		cWhen    := "MNT665WSTB()"
		aAdd(aHeader,{cTitle, cCampo, cPict, nTam, nDec, cValid, cUso, cType, cF3, cContext, cCBOX, cRelacao, cWhen})

	EndIf

	If Posicione("SX3",2,"TB_UNIDADE","X3_ARQUIVO") <> ""

		cTitle   := X3TITULO("TB_UNIDADE")
		cCampo   := "TB_UNIDADE"
		cPict    := "@!"
		nTam     := TAMSX3("TB_UNIDADE")[1]
		nDec     := TAMSX3("TB_UNIDADE")[2]
		cValid   := Posicione("SX3",2,"TB_UNIDADE","X3_VALID")
		cUso     := Posicione("SX3",2,"TB_UNIDADE","X3_USADO")
		cType    := Posicione("SX3",2,"TB_UNIDADE","X3_TIPO")
		cF3      := Posicione("SX3",2,"TB_UNIDADE","X3_F3")
		cContext := Posicione("SX3",2,"TB_UNIDADE","X3_CONTEXT")
		cCBOX    := X3CBOX("TB_UNIDADE")
		cRelacao := Posicione("SX3",2,"TB_UNIDADE","X3_RELACAO")
		cWhen    := Posicione("SX3",2,"TB_UNIDADE","X3_WHEN")
		aAdd(aHeader,{cTitle, cCampo, cPict, nTam, nDec, cValid, cUso, cType, cF3, cContext, cCBOX, cRelacao, cWhen})

	EndIf

Return