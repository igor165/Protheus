#INCLUDE "Mntr715.ch"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR715  ³ Autor ³ Elisangela Costa      ³ Data ³ 10/05/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Horas trabalhadas no contrato                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR715()
	Local vVETHORAS  := {}
	Local cSTRING    := "SI3"
	Local cDESC1     := STR0001 //"Relatorio de Horas trabalhadas no contrato."
	Local cDESC2     := STR0002 //"E permitido selecionar tipo de informacao a ser"
	Local cDESC3     := STR0003 //"apresentada, atraves dos parametros do relatorio."
	Local WNREL      := "MNTR715"

	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private nLASTKEY := 0
	Private TITULO   := STR0006 //"Horas Trabalhadas no Contrato"
	Private TAMANHO  := "G"
	Private NOMEPROG := "MNTR715"
	Private nTIPO    := 0

	SetKey( VK_F9, { | | NGVersao( "MNTR715" , 2 ) } )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros!                        ³
	//³ mv_par01     // De Contrato                                  ³
	//³ mv_par02     // Ate Contrato                                 ³
	//³ mv_par03     // De Periodo                                   ³
	//³ mv_par04     // Ate Periodo                                  ³
	//³ mv_par05     // Tipo de Custo (Medio,Standard)               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cPERG := "MNT715"
	Pergunte(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		Return
	EndIf
	SetDefault(aRETURN,cString)
	RptStatus({|lEND| R715IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)

	Set Key VK_F9 To

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R715Imp  ³ Autor ³ Elisangela Costa      ³ Data ³10/05/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR715                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R715IMP(lEND,WNREL,TITULO,TAMANHO)
	Local nTGCUSTO,nTGDISPO,nHORAGER,nMINUGER,nHORATCON,nMINTCON
	Local nTGCUSTCON,nTGDISPCON,nTOTALC,nHORATBFIM,nMINTBFIM
	Local nCNTIMPR   := 0
	Local cRODATXT   := ""
	Local oTempTable //Obj. Tabela Temporaria

	Private li       := 80 ,m_pag := 1
	Private CABEC1   := STR0007//"O.s        Bem                   Descricao                                   Serv.      Descricao                                       Seq.  Dt.Inicio       Hr.In.    Dt.Fim          Hr.Fim       Tempo        Valor"
	Private CABEC2   := " "
	Private aARRCALE := {}
	Private cTRB	 := GetNextAlias()    

	nTIPO := IIf(aRETURN[4]==1,15,18)

	/*
	1         2         3         4         5         6         7         8         9        100       110       120       130       140       150       160       170       180       190       200       210       220              
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________
	O.s        Bem                   Descricao                                   Serv.      Descricao                                       Seq.  Dt.Inicio       Hr.In.    Dt.Fim          Hr.Fim       Tempo          Valor 
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________

	Contrato..: XXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   Valor Contrato: 999.999.999,99
	Fornecedor: XXXXXXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Loja.: XXXX 

	*****************************************************************************************************************************************************************************************************************************

	Funcionario: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  
	xxxxxx 	xxxxxxxxxxxxxxxx 		 xxxxxxxxxxxxxxxxxxxx 						   xxxxxx 	   xxxxxxxxxxxxxxxxxxxx 								   xx    99/99/9999 		99:99  	99/99/9999 	  99:99 		99999:99   9999.999,99                                                                                 9999.999,99
	xxxxxx 	xxxxxxxxxxxxxxxx 		 xxxxxxxxxxxxxxxxxxxx 						   xxxxxx 	   xxxxxxxxxxxxxxxxxxxx 								   xx    99/99/9999 		99:99  	99/99/9999 	  99:99 		99999:99   9999.999,99 

	*** Total do Funcionario:  Disponivel: 99999:99   Aproveitamento: 999,99 %                                       99999:99 9999.999,99

	Funcionario: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  
	xxxxxx 	xxxxxxxxxxxxxxxx 		 xxxxxxxxxxxxxxxxxxxx 						   xxxxxx 	   xxxxxxxxxxxxxxxxxxxx 								   xx    99/99/9999 		99:99  	99/99/9999 	  99:99 		99999:99   9999.999,99                                                                                 9999.999,99
	xxxxxx 	xxxxxxxxxxxxxxxx 		 xxxxxxxxxxxxxxxxxxxx 						   xxxxxx 	   xxxxxxxxxxxxxxxxxxxx 								   xx    99/99/9999 		99:99  	99/99/9999 	  99:99 		99999:99   9999.999,99 

	*** Total do Funcionario:  Disponivel: 99999:99   Aproveitamento: 999,99 %                                       99999:99 9999.999,99

	*** Total do Contrato...:  Disponivel: 99999:99   Aproveitamento: 999,99 %                                       99999:99 9999.999,99

	*** Total Geral.........:  Disponivel: 99999:99   Aproveitamento: 999,99 %                                       99999:99 9999.999,99

	*/

	aCAMPOS := {}
	aAdd(aCAMPOS,{"CONTRATO" ,"C",08,0})
	aAdd(aCAMPOS,{"FUNCIONA" ,"C",06,0})
	aAdd(aCAMPOS,{"ORDEM"    ,"C",06,0})
	aAdd(aCAMPOS,{"CODBEM"   ,"C",16,0})
	aAdd(aCAMPOS,{"SEQRELA"  ,"C",02,0})
	aAdd(aCAMPOS,{"SERVICO"  ,"C",06,0})
	aAdd(aCAMPOS,{"DATAINI"  ,"D",08,0})
	aAdd(aCAMPOS,{"DATAFIM"  ,"D",08,0})
	aAdd(aCAMPOS,{"HORAINI"  ,"C",05,0})
	aAdd(aCAMPOS,{"HORAFIM"  ,"C",05,0})
	aAdd(aCAMPOS,{"QTEMPO"   ,"C",08,0})
	aAdd(aCAMPOS,{"VALCUSTO" ,"N",12,2})

	//Intancia classe FWTemporaryTable
	oTempTable  := FWTemporaryTable():New( cTRB, aCAMPOS )
	//Cria indices
	oTempTable:AddIndex( "Ind01" ,  {"CONTRATO","FUNCIONA","CODBEM","ORDEM","SEQRELA"}  )
	//Cria a tabela temporaria
	oTempTable:Create()	

	Processa({|lEND| MNTR715CON()},STR0008)  //"Processando O.S. do Contrato..."

	Store 0.00 To nTGCUSTO,nTGDISPO,nHORAGER,nMINUGER

	dbSelectArea(cTRB)
	DbGotop()
	If reccount() > 0
		SetRegua(LastRec())
		While !Eof()

			Store 0.00 To nHORATCON,nMINTCON,nTGCUSTCON,nTGDISPCON
			cCONTRATO := (cTRB)->CONTRATO
			lPRIMEI   := .T.
			Li        := 100
			Somalin()

			dbSelectArea(cTRB)
			While !Eof() .And. (cTRB)->CONTRATO == cCONTRATO

				Store 0.00 To nTOTALC,nHORATBFIM,nMINTBFIM
				cORDEM  := " "
				cFUNCIO := (cTRB)->FUNCIONA

				If lPRIMEI
					lPRIMEI := .F.
				Else
					Somalin()
					Somalin()
				EndIf

				@ Li,000 Psay STR0009 + cFUNCIO+"  "+NGSEEK('ST1',cFUNCIO,1,'T1_NOME')

				Somalin()

				dbSelectArea(cTRB)
				While !Eof() .And. (cTRB)->CONTRATO == cCONTRATO .And. (cTRB)->FUNCIONA == cFUNCIO

					IncRegua()
					Somalin()
					If (cTRB)->ORDEM <> cORDEM
						@ Li,000 Psay (cTRB)->ORDEM
						@ Li,011 Psay Alltrim((cTRB)->CODBEM)
						@ Li,033 Psay NGSEEK('ST9',(cTRB)->CODBEM,1,'SubStr(T9_NOME,1,40)')
						@ Li,077 Psay Alltrim((cTRB)->SERVICO)
						@ Li,088 Psay NGSEEK('ST4',(cTRB)->SERVICO,1,'SubStr(T4_NOME,1,40)')
						cORDEM := (cTRB)->ORDEM
					EndIf

					@ Li,136 Psay (cTRB)->SEQRELA  Picture "@!"
					@ Li,142 Psay (cTRB)->DATAINI  Picture "99/99/9999"
					@ Li,158 Psay (cTRB)->HORAINI  Picture "99:99"
					@ Li,168 Psay (cTRB)->DATAFIM  Picture "99/99/9999"
					@ Li,184 Psay (cTRB)->HORAFIM  Picture "99:99"
					@ Li,194 + 8 - (Len(Alltrim((cTRB)->QTEMPO))) Psay Alltrim((cTRB)->QTEMPO)
					@ Li,204 Psay Transform((cTRB)->VALCUSTO,"999,999.99")

					//Separa as horas dos minutos em decimal
					cHORAI := Alltrim((cTRB)->QTEMPO)
					nPOSTI := At (':',cHORAI)
					cHORI  := Substr(cHORAI,1,nPOSTI-1)
					cMINI  := Substr(cHORAI,nPOSTI+1,2)
					nHORI  := Val(cHORI)
					nMINI  := Val(cMINI)

					nHORATBFIM += nHORI
					nMINTBFIM  += nMINI
					nTOTALC    += (cTRB)->VALCUSTO

					dbSelectArea(cTRB)
					dbSkip()
				End

				If nMINTBFIM >= 60
					nHORATBFIM += 1
					nMINTBFIM -= 60
				Endif
				nHORATCON += nHORATBFIM
				nMINTCON  += nMINTBFIM

				cTHORATRAB := Alltrim(Str(nHORATBFIM,4)) + ':' + Strzero(nMINTBFIM,2)

				//Retorna as horas disponiveis no calendario do funcionario
				nDISPO := NGHODCALE()

				Somalin()
				Somalin()
				@ Li,000 Psay STR0010 //"*** Total do Funcionario:"
				@ Li,027 Psay STR0011 //"Disponivel: "
				@ Li,040 + 8 - (Len(Alltrim(MTOH(nDISPO)))) Psay Alltrim(MTOH(nDISPO))

				nTOTALDISP := MTOH(nDISPO)
				nAPROVEITA := (HTON(cTHORATRAB) * 100) / HTON(nTOTALDISP)

				@ Li,054 Psay STR0018 //"Aproveitamento: "
				@ Li,070 Psay nAPROVEITA Picture "@E 999.99%"
				@ Li,194 + 8 - (Len(Alltrim(cTHORATRAB))) Psay Alltrim(cTHORATRAB)
				@ Li,204 Psay  Transform(nTOTALC,"999,999.99")

				nTGCUSTCON += nTOTALC
				nTGDISPCON += nDISPO

				dbSelectArea(cTRB)

			End
			If nMINTCON  >= 60
				nHORATCON += 1
				nMINTCON  -= 60
			Endif

			nHORAGER += nHORATCON
			nMINUGER += nMINTCON

			cTHORATRAB := Alltrim(Str(nHORATCON,4)) + ':' + strzero(nMINTCON,2)

			Somalin()
			Somalin()
			@ Li,000 Psay STR0017 //"*** Total do Contrato"
			@ Li,027 Psay STR0011 //"Disponivel: "
			@ Li,040 + 8 - (Len(Alltrim(MTOH(nTGDISPCON)))) Psay Alltrim(MTOH(nTGDISPCON))

			nTDISPON  := MTOH(nTGDISPCON)
			nPERCAPRO := (HTON(cTHORATRAB)* 100) / HTON(nTDISPON)

			@ Li,054 Psay STR0018 //"Aproveitamento: "
			@ Li,070 Psay nPERCAPRO Picture "@E 999.99%"
			@ Li,194 + 8 - (Len(Alltrim(cTHORATRAB))) Psay Alltrim(cTHORATRAB)
			@ Li,204 Psay  Transform(nTOTALC,"999,999.99")

			nTGCUSTO += nTGCUSTCON
			nTGDISPO += nTGDISPCON

		End

		If nMINUGER >= 60
			nHORAGER += 1
			nMINUGER -= 60
		Endif

		cTHORATRAB := Alltrim(Str(nHORAGER,4)) + ':' + strzero(nMINUGER,2)

		Somalin()
		Somalin()
		@ Li,000 Psay STR0012 //"*** Total Geral"
		@ Li,027 Psay STR0011 //"Disponivel: "
		@ Li,040 + 8 - (Len(Alltrim(MTOH(nTGDISPO)))) Psay Alltrim(MTOH(nTGDISPO))

		nTDISPON  := MTOH(nTGDISPO)
		nPERCAPRO := (HTON(cTHORATRAB)* 100) / HTON(nTDISPON)

		@ Li,054 Psay STR0018 //"Aproveitamento: "
		@ Li,070 Psay nPERCAPRO Picture "@E 999.99%"
		@ Li,194 + 8 - (Len(Alltrim(cTHORATRAB))) Psay Alltrim(cTHORATRAB)
		@ Li,204 Psay Transform(nTGCUSTO,"999,999.99")
	Endif
	Roda(nCNTIMPR,cRODATXT,TAMANHO)

	// Apaga arquivo de Trabalho
	oTempTable:Delete()//Deleta tabela temporaria

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principam             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex("ST9")
	Set Filter To
	Set Device To Screen
	If aRETURN[5] = 1
		Set Printer To
		DbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR715CON³ Autor ³Elisangela Costa       ³ Data ³10/05/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os insumos aplicados no contrato                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³R715IMP                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR715CON()
	dbSelectArea("TP3")
	dbSetOrder(01)
	dbSeek(xFilial("TP3")+MV_PAR01,.T.)
	ProcRegua(LastRec())
	While !Eof() .And. TP3->TP3_FILIAL == xFilial("TP3") .And. TP3->TP3_CONTRA <= MV_PAR02

		dbSelectArea("ST1")
		dbSetOrder(04)
		dbSeek(xFilial("ST1")+TP3->TP3_CONTRA)
		While !Eof() .And. ST1->T1_FILIAL == xFilial("ST1") .And. ST1->T1_CONTRAT == TP3->TP3_CONTRA

			cCODFUN := ST1->T1_CODFUNC + Replicate(' ',Len(STL->TL_CODIGO)-Len(ST1->T1_CODFUNC))

			//Faz a leitura dos insumos atuais
			dbSelectArea("STL")
			dbSetOrder(08)
			dbSeek(xFilial("STL")+"M"+cCODFUN)
			While !Eof() .And. STL->TL_FILIAL == xFilial("STL") .And. STL->TL_TIPOREG == "M" .And. STL->TL_CODIGO == cCODFUN

				IncProc() 
				If Alltrim(STL->TL_SEQRELA) <> "0" .And. STL->TL_DTINICI >= MV_PAR03 .And. STL->TL_DTINICI <= MV_PAR04

					nQTDHORAS := 0 
					dbSelectArea("STJ")
					dbSetOrder(01)
					If dbSeek(xFilial("STJ")+STL->TL_ORDEM+STL->TL_PLANO)

						If STJ->TJ_SITUACA == "L"

							vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
							STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
							STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
							nQTDHORAS := vVETHORAS[1]
							cHORATRA  := vVETHORAS[3]

							nEXTRA   := If(MV_PAR05 == 2, 1+( If(NGCADICBASE('TL_PCTHREX','A','STL',.F.),STL->TL_PCTHREX,VAL(STL->TL_HREXTRA))/100), 1)
							nCUST  := MNTR715IN(nEXTRA) //Retorna o valor hora da Mao de Obra
							nCUSTO := If(MV_PAR05 = 1,STL->TL_CUSTO * nEXTRA,nCUST * nQTDHORAS)

							dbSelectArea(cTRB)
							(cTRB)->(DbAppend())
							(cTRB)->CONTRATO := ST1->T1_CONTRAT
							(cTRB)->FUNCIONA := STL->TL_CODIGO
							(cTRB)->ORDEM    := STL->TL_ORDEM
							(cTRB)->CODBEM   := STJ->TJ_CODBEM
							(cTRB)->SERVICO  := STJ->TJ_SERVICO
							(cTRB)->SEQRELA  := STL->TL_SEQRELA
							(cTRB)->DATAINI  := STL->TL_DTINICI
							(cTRB)->DATAFIM  := STL->TL_DTFIM
							(cTRB)->HORAINI  := STL->TL_HOINICI
							(cTRB)->HORAFIM  := STL->TL_HOFIM   
							(cTRB)->QTEMPO   := cHORATRA     
							(cTRB)->VALCUSTO := nCUSTO 

						EndIf  
					EndIf 
				EndIf  
				dbSelectArea("STL")
				dbSkip()
			End

			//Faz a leitura dos insumos do historico
			dbSelectArea("STT")
			dbSetOrder(03)
			dbSeek(xFilial("STT")+"M"+cCODFUN)
			While !Eof() .And. STT->TT_FILIAL == xFilial("STT") .And. STT->TT_TIPOREG == "M" .And. STT->TT_CODIGO == cCODFUN

				IncProc()
				If Alltrim(STT->TT_SEQRELA) <> "0" .And. STT->TT_DTINICI >= MV_PAR03 .And. STT->TT_DTINICI <= MV_PAR04

					nQTDHORAS := 0 
					dbSelectArea("STS")
					dbSetOrder(01)
					If dbSeek(xFilial("STS")+STT->TT_ORDEM+STT->TT_PLANO)

						If STS->TS_SITUACA == "L" 

							vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
							STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
							STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
							nQTDHORAS := vVETHORAS[1]
							cHORATRA  := vVETHORAS[3]

							nEXTRA   := If(MV_PAR05 == 2, 1+( If(NGCADICBASE('TT_PCTHREX','A','STT',.F.),STT->TT_PCTHREX,VAL(STT->TT_HREXTRA)) /100), 1)
							nCUST  := MNTR715IN(nEXTRA) //Retorna o valor hora da Mao de Obra 
							nCUSTO := If(MV_PAR05 = 1,STT->TT_CUSTO * nEXTRA,nCUST * nQTDHORAS)

							dbSelectArea(cTRB)
							(cTRB)->(DbAppend())
							(cTRB)->CONTRATO := ST1->T1_CONTRAT
							(cTRB)->FUNCIONA := STT->TT_CODIGO
							(cTRB)->ORDEM    := STT->TT_ORDEM
							(cTRB)->CODBEM   := STS->TS_CODBEM
							(cTRB)->SERVICO  := STS->TS_SERVICO  
							(cTRB)->SEQRELA  := STT->TT_SEQRELA
							(cTRB)->DATAINI  := STT->TT_DTINICI
							(cTRB)->DATAFIM  := STT->TT_DTFIM
							(cTRB)->HORAINI  := STT->TT_HOINICI
							(cTRB)->HORAFIM  := STT->TT_HOFIM
							(cTRB)->QTEMPO   := cHORATRA 
							(cTRB)->VALCUSTO := nCUSTO   
						EndIf  
					EndIf 
				EndIf  
				dbSelectArea("STT")
				dbSkip()
			End
			dbSelectArea("ST1")
			dbSkip()
		End
		dbSelectArea("TP3")
		dbSkip()
	End
Return .T.               

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR715IN ³ Autor ³Elisangela Costa       ³ Data ³10/05/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o valor hora do funcionario                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³R715IMP                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR715IN(nEXTRA)
Return ST1->T1_SALARIO * nEXTRA

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ Somalin  ³ Autor ³ Elisangela Costa      ³ Data ³ 11/05/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR715                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function Somalin()
	Li++
	If Li > 58
		Cabec(Titulo,Cabec1,Cabec2,NomeProg,Tamanho,nTipo)
		@ Li,000 Psay STR0013+" "+Alltrim(cCONTRATO)+" - "+;
		NGSEEK("TP3",cCONTRATO,1,"TP3_DESCRI") //"Contrato..: "
		@ Li,070 Psay STR0014+Alltrim(Transform(TP3->TP3_VALOR,"999,999,999.99"))
		Somalin()
		@ Li,000 Psay STR0015+" "+Alltrim(TP3->TP3_FORNEC)+" - "+;
		NGSEEK('SA2',TP3->TP3_FORNEC+TP3->TP3_LOJA,1,'A2_NOME') //"Fornecedor: "
		@ Li,079 Psay STR0016+TP3->TP3_LOJA //"Loja.: "  
		Somalin()
		@ Li,000 Psay Replicate("*",220)
		Somalin()
		Somalin()
	EndIf
Return  .T.   

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NGHODCALE ³ Autor ³ Elisangela Costa      ³ Data ³ 11/05/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Calcula Horas disponiveis no calendario do funcionario      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³R715Imp                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function NGHODCALE()
	Local DtPar02 := mv_par04
	nDISPON := 0.00
	dDTATU  := MV_PAR03

	dbSelectArea("ST1")
	dbSetOrder(01)
	dbSeek(xFilial("ST1")+cFUNCIO)

	lFuncDem := .f.
	nPOS := ASCAN(aARRCALE,{|x| x[1] == ST1->T1_TURNO})
	If AllTrim(GetMv("MV_NGMNTRH")) $ "SX"
		dbSelectArea("SRA")
		dbSetOrder(01)
		If dbSeek(xFilial("SRA")+st1->t1_codfunc)
			If !Empty(sra->ra_demissa)
				If sra->ra_demissa >= mv_par03 .And. sra->ra_demissa <= mv_par04
					lFuncDem := .t.
					DtPar02  := sra->ra_demissa
				Endif
			Endif
		Endif
	Endif

	If !lFuncDem

		If nPOS == 0
			IF SuperGetMv("MV_NGFLUT",.F.,"-1") != "-1"
				//Trata o calendario flutuante
				If AllTrim(GetMv("MV_NGFLUT")) == "S"
					dbSelectArea("TP4")
					dbSetOrder(1)
					If dbSeek(xFilial("TP4")+ST1->T1_EQUIPE)
						While dDTATU <= MV_PAR04
							dbSelectArea("TP6")
							dbSetOrder(1)
							If dbSeek(xFilial("TP6")+TP4->TP4_CODIGO+DTOS(dDTATU))
								While !Eof() .And. TP6->TP6_FILIAL = Xfilial("TP6") .And.;
								TP6->TP6_EQUIPE == TP4->TP4_CODIGO
									aDIAMAN := NG_H7(TP6->TP6_CALEND)
									While dDTATU >= TP6->TP6_DTINI .And. dDTATU <= TP6->TP6_DTFIM .And. dDTATU <= MV_PAR04
										If !Empty(aDIAMAN)
											nSEM    := If( DOW(dDTATU) == 1, 7, DOW(dDTATU)-1)
											nDISPON += HtoM(aDIAMAN[nSEM][03])
										EndIf

										Aadd(aARRCALE,{TP6->TP6_CALEND,nDISPON})
										dDTATU += 1
									End
									dbSelectArea("TP6")
									dbSkip()
								End
							EndIf
							dDTATU += 1
						End
					EndIf
				Else
					aDIAMAN := NG_H7(ST1->T1_TURNO)
					While dDTATU <= MV_PAR04
						If !Empty(aDIAMAN)
							nSEM    := If( DOW(dDTATU) == 1, 7, DOW(dDTATU)-1)
							nDISPON += HtoM(aDIAMAN[nSEM][03])
						EndIf
						dDTATU += 1
					End
					Aadd(aARRCALE,{ST1->T1_TURNO,nDISPON})
				EndIf
			Else
				aDIAMAN := NG_H7(ST1->T1_TURNO)
				While dDTATU <= MV_PAR04
					If !Empty(aDIAMAN)
						nSEM    := If( DOW(dDTATU) == 1, 7, DOW(dDTATU)-1)
						nDISPON += HtoM(aDIAMAN[nSEM][03])
					EndIf
					dDTATU += 1
				End

				Aadd(aARRCALE,{ST1->T1_TURNO,nDISPON})
			EndIf
		Else
			nDISPON := aARRCALE[nPOS][2]
		EndIf
	Else
		dbSelectArea('SX6')
		dbSetOrder(1)
		IF SuperGetMv("MV_NGFLUT",.F.,"-1") != "-1"
			//Trata o calendario flutuante
			If AllTrim(GetMv("MV_NGFLUT")) == "S"
				dbSelectArea("TP4")
				dbSetOrder(1)
				If dbSeek(xFilial("TP4")+ST1->T1_EQUIPE)
					While dDTATU <= DtPar02
						dbSelectArea("TP6")
						dbSetOrder(1)
						If dbSeek(xFilial("TP6")+TP4->TP4_CODIGO+DTOS(dDTATU))
							While !Eof() .And. TP6->TP6_FILIAL = Xfilial("TP6") .And.;
							TP6->TP6_EQUIPE == TP4->TP4_CODIGO
								aDIAMAN := NG_H7(TP6->TP6_CALEND)
								While dDTATU >= TP6->TP6_DTINI .And. dDTATU <= TP6->TP6_DTFIM .And. dDTATU <= DtPar02
									If !Empty(aDIAMAN)
										nSEM   := If( DOW(dDTATU) == 1, 7, DOW(dDTATU)-1)
										nDISPO += HtoM(aDIAMAN[nSEM][03])
									EndIf
									dDTATU += 1
								End
								dbSelectArea("TP6")
								dbSkip()
							End
						EndIf
						dDTATU += 1
					End
				EndIf
			Else
				dDTATU  := mv_par03
				aDIAMAN := NG_H7(ST1->T1_TURNO)
				While dDTATU <= DtPar02
					If !Empty(aDIAMAN)
						nSEM   := If( DOW(dDTATU) == 1, 7, DOW(dDTATU)-1)
						nDISPO += HtoM( aDIAMAN[nSEM][03] )
					Endif
					dDTATU += 1
				End
			EndIf
		Else
			dDTATU  := mv_par03
			aDIAMAN := NG_H7(ST1->T1_TURNO)
			While dDTATU <= DtPar02
				If !Empty(aDIAMAN)
					nSEM   := If( DOW(dDTATU) == 1, 7, DOW(dDTATU)-1)
					nDISPO += HtoM( aDIAMAN[nSEM][03] )
				Endif
				dDTATU += 1
			End
		EndIf
	Endif
Return nDISPON