#INCLUDE "SGAA350.ch"
#include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SGAA350  ³ Autor ³ Rafael Diogo Richter  ³ Data ³14/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para cadastro do Retorno da Ocorrência do P.E.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TBB - Plano Emergencial                                     ³±±
±±³          ³TBC - Processos do Plano Emergencial                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGAA350()
Local aCores

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK)                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM()

SGA350VPRO()

DbSelectArea("TC2")
DbSetOrder(1)
aCores  := {{"Sg350Chk() == 1"	,"BR_VERDE"   },;
             {"Sg350Chk() == 2"	,"BR_VERMELHO"},;
             {"Sg350Chk() == 3"	,"BR_AMARELO"}}

mBrowse( 6, 1,22,75,"TC2",,,,,,aCores )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Sg350Chk ³ Autor ³ Rafael Diogo Richter  ³ Data ³14/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para validar o Semaforo.                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TC2 - Retorno da Ocorrencia.                                ³±±
±±³          ³TC3 - Opcoes do Retorno da Ocorrencia.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg350Chk(nOpc)
Local nRet := 2
Local n := 0, n1 := 0
Local cCodPla, cCodOco, cDtOco, cHrOco

DbSelectArea("TC2")
DbSetOrder(1)
cCodPla := TC2->TC2_CODPLA
cCodOco := TC2->TC2_CODOCO
cDtOco  := TC2->TC2_DATA
cHrOco  := TC2->TC2_HORA
DbSelectArea("TC3")
DbSetOrder(1)
DbSeek(xFILIAL("TC3")+cCodPla+cCodOco+DtoS(cDtOco)+cHrOco)
While !Eof() .And. TC3->TC3_FILIAL == xFILIAL("TC3") .And. TC3->TC3_CODPLA == cCodPla .And.;
				  TC3->TC3_CODOCO == cCodOco .And. TC3->TC3_DTOCO == cDtOco .And. TC3->TC3_HROCO == cHrOco
	n++
	If !Empty(TC3->TC3_OK)
		n1++
	EndIf
   Dbskip()
End

If n == n1
	nRet := 1
Elseif n > n1 .and. n1 <> 0
	nRet := 3
Else
	nRet := 2
Endif

Return nRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Sg350Leg ³ Autor ³ Rafael Diogo Richter  ³ Data ³14/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para mostrar a legenda do semaforo.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg350Leg()
Local aLegenda	:=	{{"ENABLE",STR0040},;  //"Totalmente Respondido"
					 {"BR_AMARELO",STR0002},; //"Parcialmente Respondido"
					 {"BR_VERMELHO",STR0003}} //"Não Respondido"

BrwLegenda(STR0001,STR0004,aLegenda) //"Retorno da Ocorrência do Plano Emergencial"###"Legenda"

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Sg350Resp³ Autor ³ Rafael Diogo Richter  ³ Data ³15/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para Responder as Acoes a serem tomadas.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg350Resp(cAlias,nRecno,nOpcx)
Local aArea := GetArea()
Local oDlg1
Local nOpca := 2
Local aPages := {},aTitles:= {}
Local nControl := 0, lGetd := .t.
Local i
Local aOldRot := aClone(aRotina)
Local aHCGet	:= {}
Local aSize 			:= MsAdvSize( .T. , .F. , 430 )
Local oTempTRB

Private lTCY		:= AliasInDic( "TCY" )
Private lInverte := .f.
Private lFirst		:= .F.
Private oGet01,oGet02,oGet03,oMark
Private n := 1
Private cPlano 	:= TC2->TC2_CODPLA
Private aVETINR	:= {}
Private cTRB350
Private oMenu
Private aContExt 	:= {}

nOpcx := If(nOpcx!=2,4,nOpcx)

lQuery := .T.
inclui := .F.
altera := .T.

aRotina :=	{{STR0005	, "AxPesqui"  	, 0 , 1},; //"Pesquisar"
             { STR0006	, "Sg350Resp"  , 0 , 2},; //"Visualizar"
             { STR0007	, "Sg350Resp"	, 0 , 4},; //"Responder"
             { STR0007	, "Sg350Resp"	, 0 , 4},; //"Responder"
             { STR0004	, "Sg350Leg" 	, 0 , 6}}  //"Legenda"

aDBF := {}

AADD(aDBF,{ "TC3_CODPLA"	, "C" ,06, 0 })
AADD(aDBF,{ "TC3_CODOCO"	, "C" ,06, 0 })
AADD(aDBF,{ "TC3_DTOCO"		, "D" ,08, 0 })
AADD(aDBF,{ "TC3_HROCO"		, "C" ,05, 0 })
AADD(aDBF,{ "TC3_CODACA"	, "C" ,06, 0 })
AADD(aDBF,{ "TC3_DESACA"	, "C" ,60, 0 })
AADD(aDBF,{ "TC3_CODPAC"	, "C" ,06, 0 })
AADD(aDBF,{ "TC3_OK"		, "C" ,02, 0 })
AADD(aDBF,{ "TC3_DTINIC"	, "D" ,08, 0 })
AADD(aDBF,{ "TC3_DTFIM"		, "D" ,08, 0 })

aTRB := {}
AADD(aTRB,{ "TC3_OK"      , NIL , "  "		,})
AADD(aTRB,{ "TC3_CODACA"  , NIL , STR0008	,}) //"Código"
AADD(aTRB,{ "TC3_DESACA"  , NIL , STR0009	,}) //"Descrição"
AADD(aTRB,{ "TC3_CODPAC"  , NIL , STR0010	,}) //"Plano de Ação"
AADD(aTRB,{ "TC3_DTINIC"  , NIL , STR0011	,}) //"Data Início"
AADD(aTRB,{ "TC3_DTFIM"	  , NIL , STR0012	,}) //"Data Término"

cTRB350 := GetNextAlias()

oTempTRB := FWTemporaryTable():New( cTRB350, aDBF )
oTempTRB:AddIndex( "1", {"TC3_CODPLA","TC3_CODOCO","TC3_DTOCO","TC3_HROCO","TC3_CODACA"} )
oTempTRB:Create()

Dbselectarea("TC3")
Dbsetorder(1)
If Dbseek(xFilial("TC3")+TC2->TC2_CODPLA+TC2->TC2_CODOCO+DTOS(TC2->TC2_DATA)+TC2->TC2_HORA)
	While !Eof() .and. xFilial("TC3")== TC3->TC3_FILIAL .And. TC3->TC3_CODPLA == TC2->TC2_CODPLA .And.;
			TC3->TC3_CODOCO == TC2->TC2_CODOCO .And. TC3->TC3_DTOCO == TC2->TC2_DATA .And.;
			TC3->TC3_HROCO == TC2->TC2_HORA

		DbSelectArea(cTRB350)
		DbSetOrder(1)
		If !DbSeek(TC3->TC3_CODPLA+TC3->TC3_CODOCO+DTOS(TC3->TC3_DTOCO)+TC3->TC3_HROCO+TC3->TC3_CODACA)
			RecLock(cTRB350,.T.)
			If !Empty(TC3->TC3_OK)
				(cTRB350)->TC3_OK      := cMarca
			Else
				(cTRB350)->TC3_OK      := "  "
			EndIf
			(cTRB350)->TC3_CODPLA		:= TC3->TC3_CODPLA
			(cTRB350)->TC3_CODOCO		:= TC3->TC3_CODOCO
			(cTRB350)->TC3_DTOCO			:= TC3->TC3_DTOCO
			(cTRB350)->TC3_HROCO			:= TC3->TC3_HROCO
			(cTRB350)->TC3_CODACA		:= TC3->TC3_CODACA
			DbSelectArea("TBO")
			DbSetOrder(1)
			If DbSeek(xFilial("TBO")+TC3->TC3_CODPLA+TC3->TC3_CODACA)
				(cTRB350)->TC3_DESACA  := SubStr(TBO->TBO_DESACA,1,60)
			EndIf
			(cTRB350)->TC3_CODPAC	:= TC3->TC3_CODPAC
			(cTRB350)->TC3_DTINIC	:= TC3->TC3_DTINIC
			(cTRB350)->TC3_DTFIM		:= TC3->TC3_DTFIM
			MsUnLock(cTRB350)
		EndIf

		DbSelectArea("TC3")
		DbSkip()
	End
Else
	MsgStop(STR0013,STR0014) //"Nenhuma Ação que possa ser respondida foi encontrada para esta Ocorrência."###"Aviso!"
	RestArea(aArea)
	oTempTRB:Delete()
	Return .T.
EndIf

Dbselectarea(cTRB350)
Dbgotop()

nOpca := 2

Aadd(aTitles,OemToAnsi(STR0015)) //"Ações Executadas"
Aadd(aPages,"Header 1")
nControl++
Aadd(aTitles,OemToAnsi(STR0016)) //"Impactos Relacionados"
Aadd(aPages,"Header 2")
nControl++
Aadd(aTitles,OemToAnsi(STR0017)) //"Contatos Externos"
Aadd(aPages,"Header 3")
nControl++
If lTCY
	Aadd(aTitles,OemToAnsi(STR0042)) //"Danos Relacionados"
	Aadd(aPages,"Header 4")
	nControl++
EndIf


Define MsDialog oDLG1 Title STR0007 From aSize[ 7 ] , 0 To aSize[ 6 ] , aSize[ 5 ] Of oMainWnd Pixel //"Responder"

	oFld350 						:= TFolder():New(0,0,aTitles,aPages,oDLG1,,,,.f.,.f.,315,181,)
	oFld350:Align 					:= CONTROL_ALIGN_ALLCLIENT
	oFld350:bSetOption 			:= { || }
	oFld350:aDialogs[1]:oFont	:= oDLG1:oFont
	oFld350:aDialogs[2]:oFont	:= oDLG1:oFont
	oFld350:aDialogs[3]:oFont	:= oDLG1:oFont

   NgPopUp(asMenu,@oMenu)
   oDlg1:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg1)}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Folder 01                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

   @ 20,040 Button STR0020 Of oFld350:aDialogs[1] Size 40,11 Pixel Action (Sg350Dt()) //"&Data Início/Término"

   oMark       					:= MsSelect():NEW(cTRB350,"TC3_OK",,aTRB,@lINVERTE,@cMARCA,{35,1,143,315},,,oFld350:aDialogs[1])
   oMark:bMark					:= {|| SGA350PAC(cMARCA,lINVERTE,nOpcx)}
   oMark:oBrowse:lHASMARK		:= .T.
   oMark:oBrowse:lCANALLMARK	:= .T.
   oMark:oBrowse:bALLMARK		:= {|| SGA350Marc(cMARCA,nOpcx) }
   oMark:oBrowse:Align			:= CONTROL_ALIGN_ALLCLIENT

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Folder 02                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

    aHCGet	:= CriaGetImp() //Retorna aHeader e aCols de Impactos.

	oGet01					:= MsNewGetDados():New(005,022,150,302,IIF(!INCLUI.And.!ALTERA,0,GD_INSERT+GD_UPDATE+GD_DELETE),"Sg350Lin1(,oGet01)","Sg350Lin1(1,oGet01)",,,,9999,,,,oFld350:aDialogs[2],aHCGet[1],aHCGet[2])
	oGet01:oBrowse:Align	:= CONTROL_ALIGN_ALLCLIENT
	oGet01:oBrowse:Default()
	oGet01:oBrowse:Refresh()
	oGet01:Refresh()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Folder 03                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	aHCGet	:= CriaGetCE() //Retorna aHeader e aCols de Contatos Externos.

	oGet02 					:= MsNewGetDados():New(005,022,150,302,IIF(!INCLUI.And.!ALTERA,0,GD_INSERT+GD_UPDATE),"Sg350Lin1(,oGet02)",,,,,Len(aHCGet[2]),,,,oFld350:aDialogs[3],aHCGet[1],aHCGet[2])
	oGet02:oBrowse:Align	:= CONTROL_ALIGN_ALLCLIENT

	oGet02:oBrowse:Default()
	oGet02:oBrowse:Refresh()
	oGet02:Refresh()

	If lTCY
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Folder 04                                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		aHCGet	:= CriaGetDan() //Retorna aHeader e aCols de Danos.

		oGet03 					:= MsNewGetDados():New(005,022,150,302,IIF(!INCLUI.And.!ALTERA,0,GD_INSERT+GD_UPDATE+GD_DELETE),"Sg350Lin2(,oGet03)","Sg350Lin2(1,oGet03)",,,,9999,,,,oFld350:aDialogs[4],aHCGet[1],aHCGet[2])
		oGet03:oBrowse:Align	:= CONTROL_ALIGN_ALLCLIENT
		oGet03:oBrowse:Default()
		oGet03:oBrowse:Refresh()
		oGet03:Refresh()

	EndIf
	bOk 		:= {|| If(nOpcx==2,(lGrava:=.F.,oDlg1:End()),If(!SG350Val(),lGrava:=.F.,(lGrava:=.T.,oDlg1:End())))}
	bCancel	:= {|| lGrava := .F., oDlg1:End()}
	Activate MsDialog oDlg1 On Init (EnchoiceBar(oDlg1,{||Eval(bOk)},{||Eval(bCancel)}),AlignObject(oDlg1,{oFld350},1)) Centered

If lGrava
	DbSelectArea(cTRB350)
	Dbgotop()
	While !Eof()

		DbSelectArea("TC3")
		Dbsetorder(1)
		DbSeek(xFilial("TC3")+(cTRB350)->TC3_CODPLA+(cTRB350)->TC3_CODOCO+DTOS((cTRB350)->TC3_DTOCO)+(cTRB350)->TC3_HROCO+(cTRB350)->TC3_CODACA)

		RecLock('TC3',.F.)
		TC3->TC3_OK 			:= (cTRB350)->TC3_OK
		TC3->TC3_CODPAC 		:= (cTRB350)->TC3_CODPAC
		TC3->TC3_DTINIC	    := (cTRB350)->TC3_DTINIC
		TC3->TC3_DTFIM		:= (cTRB350)->TC3_DTFIM
		MsUnLock('TC3')

		DbSelectArea(cTRB350)
		Dbskip()
	End

	DbSelectArea(cTRB350)
	Dbgotop()

	For i := 1 To Len(oGet01:aCols)

		DbSelectArea("TC6")
		DbSetOrder(1)

		If DbSeek(xFilial("TC6")+(cTRB350)->TC3_CODOCO+oGet01:aCols[i][1])
			RecLock('TC6',.f.)
		Else
			RecLock('TC6',.t.)
		EndIf
		If !oGet01:aCols[i][Len(oGet01:aCols[i])]
			TC6->TC6_FILIAL := xFilial('TC6')
			TC6->TC6_CODOCO := (cTRB350)->TC3_CODOCO
			TC6->TC6_CODIMP := oGet01:aCols[i][1]
		Else
			DbDelete()
		EndIf
		MsUnlock('TC6')
	Next i

	If lTCY
		For i := 1 To Len(oGet03:aCols)
			DbSelectArea("TCY")
			DbSetOrder(1)
			If DbSeek(xFilial("TCY")+(cTRB350)->TC3_CODOCO+oGet03:aCols[i][1])
			   RecLock('TCY',.f.)
			Else
		   	RecLock('TCY',.t.)
			EndIf
			If !oGet03:aCols[i][Len(oGet03:aCols[i])]
				TCY->TCY_FILIAL := xFilial('TCY')
				TCY->TCY_CODOCO := (cTRB350)->TC3_CODOCO
				TCY->TCY_CODDAN := oGet03:aCols[i][1]
			Else
				DbDelete()
			EndIf
			MsUnlock('TCY')
		Next i
	EndIf

	If Len(aContExt) > 0
		For i := 1 To Len(aContExt)
			DbSelectArea("TC7")
			DbSetOrder(1)
			If DbSeek(xFilial("TC7")+aContExt[i][1]+aContExt[i][2])
				RecLock('TC7',.f.)
				TC7->TC7_DTCHEG := oGet02:aCols[i][3]
				TC7->TC7_HRCHEG := oGet02:aCols[i][4]
				MsUnlock('TC7')
			EndIf
		Next
	EndIf

Endif

RestArea(aArea)

oTempTRB:Delete()

aRotina := aClone(aOldRot)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGA350Marc³ Autor ³ Rafael Diogo Richter  ³ Data ³15/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para Inverter a marcacao do browse.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGA350Marc(cMarca,nOpcx)
Local aArea := GetArea()

If nOpcx == 2
	Return .T.
EndIf

dbSelectArea(cTRB350)
nRecTRB := (cTRB350)->(RecNo())
dbGoTop()
While !Eof()
	(cTRB350)->TC3_OK := IF(TC3_OK == "  ",cMARCA,"  ")
	dbSkip()
End
dbGoTo(nRecTRB)

RestArea(aArea)
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SG350Val ³ Autor ³ Rafael Diogo Richter  ³ Data ³15/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para validar se alguma acao foi marcada.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SG350Val()
Local aArea := GetArea(), lOk := .F.

DbSelectArea(cTRB350)
Dbgotop()
While !Eof()

	If !Empty((cTRB350)->TC3_OK)
		RestArea(aArea)
		lOk := .T.
		Exit
	EndIf

	DbSelectArea(cTRB350)
	Dbskip()
End

If !lOk
	MsgStop(STR0041,STR0014) //"Selecione pelo menos uma Ação ou clique no botão 'Fechar'."###"Aviso!"
	DbSelectArea(cTRB350)
	Dbgotop()
	RestArea(aArea)
	Return .F.
EndIf

If !Sg350Lin1(1,oGet01)
	lOk := .F.
EndIf

If lOk .And. lTCY .And. !Sg350Lin2(1,oGet03)
	lOk := .F.
EndIf

Return lOk


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGA350PAC ³ Autor ³ Rafael Diogo Richter  ³ Data ³15/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para solicitar um Plano de Acao quando marcado.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function SGA350PAC(cMarca,lInverte,nOpcx)
Local cFieldMarca := "TC3_OK"
Local lRet := .T.
Local i, nRecTRB
Local dDataTC7 := StoD( Space( 8 ) )
Local dDataTRB := StoD( Space( 8 ) )
Local dDataAtual := StoD( Space( 8 ) )
Local cShowData
Local aDatasTRB := {}

//Em 'visualizar': Inverte marcacao para o original
If nOpcX != 4
	RecLock(cTRB350,.f.)
	(cTRB350)->TC3_OK := If(IsMark(cFieldMarca,cMarca,lInverte),"",cMarca)
	MsUnLock(cTRB350)
Else
	If IsMark(cFieldMarca,cMarca,lInverte)
		If MsgYesNo(STR0022,STR0023) //"Deseja vincular um plano de ação a essa ação ?"###"AVISO"
			lRet := ConPad1(,,, "TAA350",,,.f.)
			If lRet
				RecLock(cTRB350,.f.)
				(cTRB350)->TC3_CODPAC := TAA->TAA_CODPLA
				(cTRB350)->TC3_DTINIC := TAA->TAA_DTINPR
				(cTRB350)->TC3_DTFIM  := TAA->TAA_DTFIRE
				MsUnLock(cTRB350)
			Else
				RecLock(cTRB350,.f.)
				(cTRB350)->TC3_OK := "  "
				(cTRB350)->TC3_CODPAC := ""
				(cTRB350)->TC3_DTINIC := CTOD("  /  /  ")
				(cTRB350)->TC3_DTFIM  := CTOD("  /  /  ")
				MsUnLock(cTRB350)
			EndIf
		Else
			If !SG350Dt()
				RecLock(cTRB350,.f.)
				(cTRB350)->TC3_OK := "  "
				(cTRB350)->TC3_CODPAC := ""
				(cTRB350)->TC3_DTINIC := CTOD("  /  /  ")
				(cTRB350)->TC3_DTFIM  := CTOD("  /  /  ")
				MsUnLock(cTRB350)
			EndIf
		EndIf
	Else
		//Pega a menor data da GetDados de Contatos Externos
		For i:= 1 To Len(oGet02:aCols)
			dDataTC7 := If( !Empty( oGet02:aCols[i][3] ) .And. ( Empty( dDataTC7 ) .Or. dDataTC7 > oGet02:aCols[i][3] ) , oGet02:aCols[i][3] , dDataTC7 )
		Next

		//Data a ser desmarcada no MarkBrowse
		dDataAtual := (cTRB350)->TC3_DTINIC
		cCodigo	:= (cTRB350)->TC3_CODACA
		nRecTRB := (cTRB350)->(Recno())

		dbSelectArea( cTRB350 )
		( cTRB350 )->( dbGoTop() )
		For i:= 1 To Len(cTRB350)
			If cCodigo <> (cTRB350)->TC3_CODACA .And. !Empty((cTRB350)->TC3_DTINIC)
				aAdd( aDatasTRB, (cTRB350)->TC3_DTINIC)
			EndIf
			dbSkip()
		Next

		aDatasTRB := aSort( aDatasTRB )

		dbGoTo(nRecTRB)

		If !Empty( dDataTC7 )
			If ( Len( aDatasTRB ) < 1 .Or. Empty( aDatasTRB[ 1 ] ) )
		    	lRet := .F.
			ElseIf aDatasTRB[ 1 ] > dDataAtual .And. aDatasTRB[ 1 ] > dDataTC7
				lRet := .F.
			EndIf
		EndIf

		If lRet
			RecLock(cTRB350,.F.)//Limpa
			(cTRB350)->TC3_CODPAC := ""
			(cTRB350)->TC3_DTINIC := CTOD("  /  /  ")
			(cTRB350)->TC3_DTFIM  := CTOD("  /  /  ")
			MsUnLock(cTRB350)
		Else
			(cTRB350)->TC3_OK := cMarca
			ShowHelpDlg( 	STR0036 , ;//"ATENÇÃO"
							{ STR0051 } , 1 , ; //"Não é possível desmarcar esta opção, pois a data de Contatos Externos está preenchida."
							{ STR0052 + DTOC( dDataTC7 ) } , 1 ) //"Para completar esta ação, a data de Contatos Externos deve ser menor que "
		EndIf
	EndIf

	oMark:oBrowse:Refresh()

EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SG350LIN1 ³ Autor ³ Rafael Diogo Richter  ³ Data ³27/04/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida a linha digitada no GetDados.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SGAA350                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         Atualizacoes Sofridas Desde a Construcao Inicial.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg350Lin1(nOpc,oGet)
Local nx,nPos
Local nQtd := 0
Local aHeader := {}, aCols := {}, n
Local lRet := .T.

Default nOpc := 0

aHeader	:= oGet:aHeader
aCols	:= oGet:aCols
n		:= oGet:nAt

nPos    := aScan( aHeader, { |x| Trim( Upper(x[2]) ) == 'TC6_CODIMP'})

If nPos > 0
	For nx:=1 To Len(aCols)
		If !aCols[nx][Len(aCols[nx])]
			If Empty(aCols[nx][nPos])
				ShowHelpDLG( STR0036, { STR0054 + aHeader[nPos,1] + STR0057 } , 2 , { STR0056 + aHeader[nPos,1] + "." } , 2 )//"O campo "##" na aba Impactos Relacionados, não foi preenchido."##"Favor preencher o campo "
				lRet := .F.
			Endif
		Endif
	Next

	aEval( aCols, { |x| nQtd += Iif( ! x[Len(aHeader)+1] .And. x[nPos] == aCols[n][nPos], 1, 0 ) } )

	If nQtd > 1
		Help(" ",1,STR0036,,STR0037,3,1) //"ATENÇÃO"###"Campo Impacto Duplicado"
		lRet := .F.
	EndIf
EndIf

PutFileInEof( "TC6" )
PutFileInEof( "TAE" )

Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SG350LIN2 ³ Autor ³ Rafael Diogo Richter  ³ Data ³27/04/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida a linha digitada no GetDados.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SGAA350                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         Atualizacoes Sofridas Desde a Construcao Inicial.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg350Lin2(nOpc,oGet)
Local nx, nPos
Local nQtd := 0
Local aHeader := {}, aCols := {}, n
Local lRet := .T.

Default nOpc := 0

aHeader	:= oGet:aHeader
aCols		:= oGet:aCols
n			:= oGet:nAt

nPos    := aScan( aHeader, { |x| Trim( Upper(x[2]) ) == 'TCY_CODDAN'})

If nPos > 0
	For nx:=1 To Len(aCols)
		If !aCols[nx][Len(aCols[nx])]
			If Empty(aCols[nx][nPos])
				ShowHelpDLG( STR0036, { STR0054 + aHeader[nPos,1] + STR0055 } , 2 , { STR0056 + aHeader[nPos,1] + "." } , 2 )//"O campo "##" na aba Danos Relacionados, não foi preenchido."##"Favor preencher o campo "
				lRet := .F.
			Endif
		Endif
	Next

	aEval( aCols, { |x| nQtd += iif( ! x[Len(aHeader)+1] .And. x[nPos] == aCols[n][nPos], 1, 0 ) } )

	If nQtd > 1
   		Help(" ",1,STR0043,,STR0037,3,1) //"ATENÇÃO"###"Campo Dano Duplicado"
   		lRet := .F.
	EndIf
EndIf

PutFileInEof( "TCY" )
PutFileInEof( "TG8" )
Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Sg350Dt  ³ Autor ³ Rafael Diogo Richter  ³ Data ³02/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta tela para informar a data inicio e fim.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SGAA350                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         Atualizacoes Sofridas Desde a Construcao Inicial.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Sg350Dt()
Local nExec := 0
Local dDtInicio := (cTRB350)->TC3_DTINIC
Local dDtTermino := (cTRB350)->TC3_DTFIM

Define Msdialog oDlg7 From 300,120 To 410,350 Title Oemtoansi(STR0038) Pixel //"Data de Início/Término"
	oDlg7:lEscClose := .F.

	@ 0, 0 BITMAP oBmp RESNAME "PROJETOAP" oF oDlg7 SIZE 35,250 NOBORDER WHEN .F. PIXEL
   @ 008,015  Say Oemtoansi(STR0011) Of oDlg7 Pixel //"Data Início"
   @ 007,050  MsGet dDtInicio Picture '99/99/9999' Size 60,7 Valid fValDtIni( dDtInicio , (cTRB350)->TC3_DTOCO, dDtTermino ) HASBUTTON Of oDlg7 Pixel
   @ 025,015  Say Oemtoansi(STR0012)  Of oDlg7 Pixel //"Data Término"
   @ 024,050  MsGet dDtTermino Picture '99/99/9999' Size 60,7 Valid VALDATA(dDtInicio,dDtTermino,"DATAINVALI") HASBUTTON Of oDlg7 Pixel

   Define SButton From 40, 55 Type 1 Enable Of oDlg7 Action ( nExec:=1,If(Sg350VAc(dDtInicio,dDtTermino),oDlg7:End(),Nil)) Pixel
   Define SButton From 40, 85 Type 2 Enable Of oDlg7 Action ( nExec:=2,oDlg7:End() ) Pixel

Activate Msdialog oDlg7 Centered

If nExec == 1
	RecLock(cTRB350,.f.)
	(cTRB350)->TC3_CODPAC := ""
	(cTRB350)->TC3_DTINIC := dDtInicio
	(cTRB350)->TC3_DTFIM  := dDtTermino
	MsUnLock(cTRB350)
Else
	Return .F.
EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} fValDtIni

Valida data inicial quando a resposta da ocorrencia não for por plano de
ação.

@author Guilherme Benkendorf
@since 29/12/2014
@version MP11
@return Nil
/*/
//---------------------------------------------------------------------
Static Function fValDtIni( dDataIni , dDataOco , dDtTermino)
	Local lRet

	If !Empty( dDataIni ) .And. dDataIni < dDataOco
		ShowHelpDlg( "DTINIINV" ,{ "A data de início da ação não pode ser inferior à da ocorrência" } , 2 ,;//"A data de início da ação não pode ser inferior que a data da ocorrência."
											 { STR0045 + DTOC( dDataOco ) + "." } , 2 ) //"Informar data início superior ou igual à: "
		lRet := .F.
	EndIf

	If !Empty(dDtTermino)
		If dDataIni > dDtTermino
			Help(STR0046) //"Data Inicial deve ser maior do que a Data Término"
			lRet := .F.
		EndIf
	EndIf

Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina :=	{{ STR0005	, "AxPesqui" 	, 0, 1},; //"Pesquisar"
                   { STR0006	, "Sg350Resp"	, 0, 2},; //"Visualizar"
                   { STR0007	, "Sg350Resp"	, 0, 4},; //"Responder"
                   { STR0004	, "Sg350Leg"	, 0, 6}} //"Legenda"

Return aRotina

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  Sg350VAc   ³ Autor ³ Rafael Diogo Richter  ³ Data ³22/02/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para validar o GetDados ao trocar o folder.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg350VAc(dDtInicio,dDtTermino)

If Empty(dDtInicio) .Or. Empty(dDtTermino)
	MsgStop(STR0039,STR0036) //"É necessário informar todos os campos."###"Atenção"
	Return .F.
EndIf

Return .T.

//---------------------------------------------------------------------------------------------------
/* Static Function CriaGetImp()
Função para criação da MsNewGetDados do folder de impactos.

@return

@sample
CriaGetImp()

@author Juliani Schlickmann Damasceno
@since 03/02/2014
@version 1.0
*/
//---------------------------------------------------------------------------------------------------
Static Function CriaGetImp()

Local aHeaderGD	:= {}
Local aColsGD		:= {}
Local nOpcx		:= 0

aHeaderGD 	:= CabecGetd("TC6",{'TC6_CODOCO'},2)

	//Inclui coluna de registro atraves de funcao generica
ADHeadRec("TC6",aHeaderGD)

aColsGD   := MakeGetd("TC6",(cTRB350)->TC3_CODOCO,aHeaderGD,;
	"TC6->TC6_FILIAL == xFilial('TC6') .And. TC6->TC6_CODOCO == (cTRB350)->TC3_CODOCO")

If Empty(aColsGD) .Or. nOpcx == 3
	aColsGD	:= BlankGetd(aHeaderGD)
EndIf

Return {aHeaderGD, aColsGD}

//---------------------------------------------------------------------------------------------------
/* Static Function CriaGetDan()
Função para criação da MsNewGetDados do folder de danos.

@return

@sample
CriaGetImp()

@author Juliani Schlickmann Damasceno
@since 04/02/2014
@version 1.0
*/
//---------------------------------------------------------------------------------------------------

Static Function CriaGetDan()

Local aHeadTCY	:= {}
Local aColsTCY	:= {}
Local nOpcx		:= 0

aHeadTCY	:= {}
aColsTCY	:= {}

aHeadTCY 	:= CabecGetd("TCY",{"TCY_CODOCO"},2)

		//Inclui coluna de registro atraves de funcao generica
ADHeadRec("TCY",aHeadTCY)

aColsTCY   := MakeGetd("TCY",(cTRB350)->TC3_CODOCO,aHeadTCY,;
	"TCY->TCY_FILIAL == xFilial('TCY') .And. TCY->TCY_CODOCO == (cTRB350)->TC3_CODOCO")

If Empty(aColsTCY) .Or. nOpcx == 3
	aColsTCY := BlankGetd(aHeadTCY)
EndIf

Return {aHeadTCY, aColsTCY}

//---------------------------------------------------------------------------------------------------
/* Static Function CriaGetCE()
Função para criação do aHeader e aCols do folder de contatos externos.

@return

@sample
CriaGetCE()

@author Juliani Schlickmann Damasceno
@since 03/02/2014
@version 1.0
*/
//---------------------------------------------------------------------------------------------------
Static Function CriaGetCE()

	Local aHeaderGD := {}
	Local aColsGD   := {}
	Local nUsado    := 0
	Local aArea     := GetArea()
	Local aCampos   := { "TBU_DESCON", "TBU_DESFOR", "TC7_DTCHEG", "TC7_HRCHEG" }
	Local nFld      := 0
	Local cField    := ''
	Local aTField   := {}
	Local cTitulo   := ''
	Local aColsAux  := {}

	Local aExc := { {'TBU_DESCON', 'X3_TITULO', STR0018 },;
				    {'TBU_DESFOR', 'X3_TITULO', STR0019 } }

	aHeaderGD := NGHeadExc( aCampos, , , , aExc )

	aColsAux := BlankGetd(aHeaderGD)[ 1 ]

	nUsado := Len(aHeaderGD)

	nDesCont	:= aSCAN(aHeaderGD, {|x| AllTrim(Upper(X[2])) == "TBU_DESCON" })
	nDesFor		:= aSCAN(aHeaderGD, {|x| AllTrim(Upper(X[2])) == "TBU_DESFOR" })
	nDataCheg	:= aSCAN(aHeaderGD, {|x| AllTrim(Upper(X[2])) == "TC7_DTCHEG" })
	nHoraCheg	:= aSCAN(aHeaderGD, {|x| AllTrim(Upper(X[2])) == "TC7_HRCHEG" })

	dbSelectArea("TBU")
	dbSetOrder(1)
	If TBU->(DbSeek(xFilial('TBU')+cPlano))

		//Checa quantidade de linhas utilizadas

		nCnt		:= 0
		nItensCont	:= 0

		dbSelectArea("TBU")
		dbSeek(xFilial('TBU')+cPlano)
		Do While !Eof() .And. TBU->TBU_FILIAL == xFilial('TBU')  .And.	cPlano == TBU->TBU_CODPLA
			dbSelectArea("TC7")
			dbSetOrder(1)
			If dbSeek(xFilial("TC7")+TC2->TC2_CODOCO+TBU->TBU_CODCON)
				nCnt++
			EndIf
			dbSelectArea("TBU")
			dbSkip()
		EndDo

		If nCnt > 0

			nCnt := 0

			dbSelectArea("TBU")
			dbSetOrder(1)
			dbSeek(xFilial('TBU')+cPlano)
			Do While !EOF() .And.	TBU->TBU_FILIAL == xFilial('TBU') .And. cPlano == TBU->TBU_CODPLA
				dbSelectArea("TC7")
				dbSetOrder(1)
				If dbSeek(xFilial("TC7")+TC2->TC2_CODOCO+TBU->TBU_CODCON)
					nCnt++
					nUsado++

					//Montando aCols
					aAdd(aColsGD, aCLone(aColsAux))

					aColsGD[nCnt][nDesCont]	 := TBU->TBU_DESCON
					aColsGD[nCnt][nDesFor]	 := " "
					aColsGD[nCnt][nDataCheg] := CtoD(Space(8))
					aColsGD[nCnt][nHoraCheg] := Space(5)

					DbSelectArea("SA2")
					DbSetOrder(1)
					If DbSeek(xFilial("SA2")+TBU->TBU_FORNEC)
						aColsGD[nCnt][nDesFor]	:= SA2->A2_NOME
					EndIf

					DbSelectArea("TC7")
					DbSetOrder(1)
					dbSeek(xFilial("TC7")+TC2->TC2_CODOCO+TBU->TBU_CODCON)
					Do While !Eof() .and. 	xFilial("TC7") == TC7->TC7_FILIAL .and.;
								TC2->TC2_CODOCO == TC7->TC7_CODOCO .and.;
								TBU->TBU_CODCON == TC7->TC7_CODCON

						aColsGD[nCnt][nDataCheg]	:= TC7->TC7_DTCHEG
						aColsGD[nCnt][nHoraCheg]	:= TC7->TC7_HRCHEG
						aAdd( aContExt, { TC2->TC2_CODOCO,TBU->TBU_CODCON,TC7->TC7_AVISAD,TC7->TC7_DTCONT,TC7->TC7_HRCONT,TC7->TC7_DTCHEG,TC7->TC7_HRCHEG } )

						DbSelectArea("TC7")
						DbSkip()
					EndDo

				Endif
				dbSelectArea('TBU')
				dbSkip()
			EndDo
		EndIf
	EndIf

	If Empty(aColsGD)
		aColsGD := BlankGetd(aHeaderGD)
	Endif

	RestArea(aArea)

Return {aHeaderGD, aColsGD}

//--------------------------------------------------------------------------------------
/* Function SGA350VPRO

SGA350VPRO

@author Juliani Schlickmann Damasceno
@since 12/07/2014
@version 1.0
*/
//--------------------------------------------------------------------------------------
Function SGA350VPRO()

_SetOwnerPrvt("aRotina",MenuDef())
_SetOwnerPrvt("cCadastro",OemtoAnsi(STR0001))//"Retorno da Ocorrência do Plano Emergencial"
_SetOwnerPrvt("aChkDel",{})
_SetOwnerPrvt("bNgGrava",)
_SetOwnerPrvt("nTotal1",0)
_SetOwnerPrvt("lVisual",.F.)
_SetOwnerPrvt("lIntGPE",.T.)
_SetOwnerPrvt("cMarca",GetMark())
_SetOwnerPrvt("lGrava",.T.)

Return
//---------------------------------------------------------------------
/*/{Protheus.doc} SG350DTVAL()
Validação do campo TC7_DTCHEG.

@author Gabriel Augusto Werlich
@since 19/02/2015
@version MP11/12
@return lRet
/*/
//---------------------------------------------------------------------
Function SG350DTVAL()

	Local lRet			:= .T.
	Local dData		:= StoD( Space( 8 ) )
	Local aArea		:= GetArea()
	Local aAreaTRB	:= ( cTRB350 )->( GetArea() )

	If Empty(M->TC7_DTCHEG)
		lRet := .T.
	    Return lRet
	EndIf

	dbSelectArea( cTRB350 )
	( cTRB350 )->( dbGoTop() )
	While ( cTRB350 )->( !Eof() )
		dData := If( !Empty( ( cTRB350 )->TC3_DTINIC ) .And. ( Empty( dData ) .Or. dData > ( cTRB350 )->TC3_DTINIC ) , ( cTRB350 )->TC3_DTINIC , dData )
		dbSkip()
	EndDo

	If Empty( dData )
		ShowHelpDlg( 	STR0036 , ;//"ATENÇÃO"
				{ STR0047 } , 1 , ; //"Não há ações executadas."
				{ STR0048 } , 1 ) //"Executar ao menos uma ação para a resposta da ocorrência."
		lRet := .F.

	ElseIf M->TC7_DTCHEG < dData
		ShowHelpDlg( 	STR0036 , ; //"ATENÇÃO"
						{ STR0049 } , 1 , ; //"A Data de Chegada não pode ser menor do que a data de início das ações."
						{ STR0050 + DtoC( dData ) + "." } , 1 )//"Favor informar uma data maior que "
		lRet := .F.
	EndIf

	RestArea( aArea )
	RestArea( aAreaTRB )

Return lRet
//---------------------------------------------------------------------
/*/{Protheus.doc} SG350TAA()
Filtro da consulta padrão 350TAA.

@author Gabriel Augusto Werlich
@since 26/02/2015
@version MP11/12
@return lRet
/*/
//---------------------------------------------------------------------
Function SG350TAA()
Local lRet := .F.

If TAA->TAA_DTINPR >= TC2->TC2_DATA .And. TAA->TAA_DTFIRE <= TC2->TC2_DATA
	lRet:= .T.
EndIf

Return lRet
//---------------------------------------------------------------------
/*/{Protheus.doc} SG350WNHR()
When do campo TC7_HRCHEG, o campo de Data deve estar preenchido para
liberar o campo de hora.

@author Gabriel Augusto Werlich
@since 26/02/2015
@version MP11/12
@return lRet
/*/
//---------------------------------------------------------------------
Function SG350WNHR()
Local lRet := .T.
Local nDataCheg	:= aSCAN(aHeader, {|x| AllTrim(Upper(X[2])) == "TC7_DTCHEG" })
	If nDataCheg > 0
		lRet := !Empty(aCols[n][nDataCheg])
	EndIf
Return lRet
