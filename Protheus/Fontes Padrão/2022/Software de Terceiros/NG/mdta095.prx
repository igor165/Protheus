#INCLUDE "MDTA095.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

#DEFINE _nVersao 2

//---------------------------------------------------------------------
/*/{Protheus.doc} MDTA095
Programa de Cadastro de Fontes Geradora de Risco

@return
@Param lFontPS - Indica se esta sendo aberto pelo fonte correto no PS.

@sample MDTA095()

@author Paulo Pego - Refeito por: Jean Pytter da Costa
@since 11/12/99 - Revisão: 24/08/15
/*/
//---------------------------------------------------------------------
Function MDTA095( lFontPS )

	// Armazena as variáveis
	Local aNGBEGINPRM := NGBEGINPRM( _nVersao )

	Local oBrowse

	Local lSigaMdtPS := SuperGetMv("MV_MDTPS",.F.,"N") == "S"

	Default lFontPS := IsInCallStack( "MDTA095PS" )

	//Caso houver algum inconsistencia será retornado as variaveis.
	If !fValExeRot( lFontPS )
		oBrowse := FWMBrowse():New()

			oBrowse:SetAlias( "TN7" )			// Alias da tabela utilizada
			oBrowse:SetMenuDef( "MDTA095" )	// Nome do fonte onde esta a função MenuDef
			oBrowse:SetDescription( STR0007 ) // Descrição do browse ###"Fontes Geradoras de Risco"
			If lSigaMdtPS

				nSizeSA1 := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
				nSizeLo1 := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))

				cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

				oBrowse:SetFilterDefault( "TN7->(TN7_CLIENT+TN7_LOJA) == cCliMdtPs" )
			EndIf
		oBrowse:Activate()
	EndIf
	// Devolve as variáveis armazenadas
	NGRETURNPRM(aNGBEGINPRM)

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Definição do Menu (padrão MVC).

@author Jean Pytter da Costa
@since 24/08/15

@return aRotina array com o Menu MVC
/*/
//---------------------------------------------------------------------
Static Function MenuDef()
//Inicializa MenuDef com todas as opções
Return FWMVCMenu( "MDTA095" )
//---------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Definição do Modelo (padrão MVC).

@author Jean Pytter da Costa
@since 24/08/15

@return oModel objeto do Modelo MVC
/*/
//---------------------------------------------------------------------
Static Function ModelDef()

    // Cria a estrutura a ser usada no Modelo de Dados
	Local oStructTN7 := FWFormStruct( 1 ,"TN7" , /*bAvalCampo*/ , /*lViewUsado*/ )

	// Modelo de dados que será construído
	Local oModel

	// Cria o objeto do Modelo de Dados
	// cID     Identificador do modelo
	// bPre    Code-Block de pre-edição do formulário de edição. Indica se a edição esta liberada
	// bPost   Code-Block de validação do formulário de edição
	// bCommit Code-Block de persistência do formulário de edição
	// bCancel Code-Block de cancelamento do formulário de edição
	oModel := MPFormModel():New( "MDTA095" , /*bPre*/ , { | oModel | fMPosValid( oModel ) } /*bPos*/ , /*bCommit*/ , /*bCancel*/ )
		//--------------------------------------------------
		// Componentes do Modelo
		//--------------------------------------------------

		// Adiciona ao modelo um componente de Formulário Principal
		// cId          Identificador do modelo
		// cOwner       Identificador superior do modelo
		// oModelStruct Objeto com  a estrutura de dados
		// bPre         Code-Block de pré-edição do formulário de edição. Indica se a edição esta liberada
		// bPost        Code-Block de validação do formulário de edição
		// bLoad        Code-Block de carga dos dados do formulário de edição
		oModel:AddFields( "TN7MASTER" , Nil , oStructTN7 , /*bPre*/ , /*bPost*/ , /*bLoad*/ )
			// Adiciona a descrição do Modelo de Dados (Geral)
			oModel:SetDescription( STR0007 /*cDescricao*/ ) //"Fontes Geradoras de Risco"
			// Adiciona a descricao do Componente do Modelo de Dados
			oModel:GetModel( "TN7MASTER" ):SetDescription( STR0007 ) //"Fontes Geradoras de Risco"

Return oModel

//---------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Definição da View (padrão MVC).

@author Jackson Machado
@since 13/09/13

@return oView objeto da View MVC
/*/
//---------------------------------------------------------------------
Static Function ViewDef()

	// Cria um objeto de Modelo de dados baseado no ModelDef() do fonte informado
	Local oModel := FWLoadModel( "MDTA095" )

	// Cria a estrutura a ser usada na View
	Local oStructTN7 := FWFormStruct( 2 , "TN7" , /*bAvalCampo*/ , /*lViewUsado*/ )

	// Interface de visualização construída
	Local oView

	// Cria o objeto de View
	oView := FWFormView():New()
		// Objeto do model a se associar a view.
		oView:SetModel( oModel )
		// Adiciona no View um controle do tipo formulário (antiga Enchoice)
		// cFormModelID - Representa o ID criado no Model que essa FormField irá representar
		// oStruct - Objeto do model a se associar a view.
		// cLinkID - Representa o ID criado no Model ,Só é necessári o caso estamos mundando o ID no View.
		oView:AddField( "VIEW_TN7" , oStructTN7 , "TN7MASTER" )
			//Adiciona um titulo para o formulário
			oView:EnableTitleView( "VIEW_TN7" , STR0007 )	// Descrição do browse ###"Fontes Geradoras de Risco"
			// Cria os componentes "box" horizontais para receberem elementos da View
			// cID		  	Id do Box a ser utilizado
			// nPercHeight  Valor da Altura do box( caso o lFixPixel seja .T. é a qtd de pixel exato)
			// cIdOwner 	Id do Box Vertical pai. Podemos fazer diversas criações uma dentro da outra.
			// lFixPixel	Determina que o valor passado no nPercHeight é na verdade a qtd de pixel a ser usada.
			// cIDFolder	Id da folder onde queremos criar o o box se passado esse valor, é necessário informar o cIDSheet
			// cIDSheet     Id da Sheet(Folha de dados) onde queremos criar o o box.
			oView:CreateHorizontalBox( "TELATN7" , 100,/*cIDOwner*/,/*lFixPixel*/,/*cIDFolder*/,/*cIDSheet*/ )
		// Associa um View a um box
		oView:SetOwnerView( "VIEW_TN7" , "TELATN7" )

Return oView

//---------------------------------------------------------------------
/*/{Protheus.doc} fMPosValid
Pós-validação do modelo de dados.

@author Jackson Machado
@since 13/09/13

@param oModel - Objeto do modelo de dados (Obrigatório)

@return Lígico - Retorna verdadeiro caso validacoes estejam corretas
/*/
//---------------------------------------------------------------------
Static Function fMPosValid( oModel )

	Local lRet			:= .T.
	Local lSigaMdtPS 	:= SuperGetMv("MV_MDTPS",.F.,"N") == "S"

	Local aAreaTN7		:= TN7->( GetArea() )

	Local nOperation	:= oModel:GetOperation() // Operação de ação sobre o Modelo
	Local oModelTN7		:= oModel:GetModel( "TN7MASTER" )

	Private aCHKSQL 	:= {} // Variável para consistência na exclusão (via SX9)
	Private aCHKDEL 	:= {} // Variável para consistência na exclusão (via Cadastro)

	// Recebe SX9 - Formato:
	// 1 - Domínio (tabela)
	// 2 - Campo do Domínio
	// 3 - Contra-Domínio (tabela)
	// 4 - Campo do Contra-Domínio
	// 5 - Condição SQL
	// 6 - Comparação da Filial do Domínio
	// 7 - Comparação da Filial do Contra-Domínio
	aCHKSQL := NGRETSX9( "TN7" )

	// Recebe relação do Cadastro - Formato:
	// 1 - Chave
	// 2 - Alias
	// 3 - Ordem (Índice)
	If lSigaMdtPS
		aAdd(aCHKDEL, { "TN7->TN7_CLIENT+TN7->TN7_LOJA+TN7->TN7_FONTE" , "TN0" , 5 } )
	Else
		aAdd(aCHKDEL, { "TN7->TN7_FONTE" , "TN0" , 5 } )
	EndIf

	If nOperation == MODEL_OPERATION_DELETE //Exclusão

		If !NGCHKDEL( "TN7" )
			lRet := .F.
		EndIf

		If lRet .And. !NGVALSX9( "TN7" , {} , .T. , .T. )
			lRet := .F.
		EndIf

	EndIf

	RestArea( aAreaTN7 )

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} fValExeRot
Valida execução da rotina MDTA095PS

@return lIncons Boolean Indica se há inconsistencia na execução.
@param lFontPS  Boolean Indica se foi feito a chamada pelo MDTA095PS

@sample fValExeRot(.T.)
@author Jean Pytter da Costa

@since 24/08/2015
/*/
//---------------------------------------------------------------------
Static Function fValExeRot( lFontPS )

	Local lIncons 		:= .F.
	Local lSigaMdtPS 	:= SuperGetMv("MV_MDTPS",.F.,"N") == "S"

	//Verifica se usuario de acesso tem permissão para a execução.
	If FindFunction("MDTRESTRI") .And. !MDTRESTRI(cPrograma)
		lIncons := .T.
	EndIf

	If !lIncons .And. lSigaMDTPS .And. !lFontPS
		ShowHelpDlg( 	"NOCALLPS" , ;
						{ STR0010 } , 1 , ; //"Função incorreta."
						{ STR0011 + ; //"Faz se necessário a alteração do Menu. A rotina 'Fonte Geradora' deverá chamar o programa MDTA095PS."
							STR0012 } , 3 )//"Favor contate administrador de sistemas"
		lIncons := .T.
	EndIf

Return lIncons