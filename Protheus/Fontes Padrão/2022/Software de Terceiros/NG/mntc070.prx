#INCLUDE "MNTC070.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTC070
Bens do Fabricante

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Function MNTC070( cFabricante )
	
	Local aNGBeginPrm := NGBeginPrm()
	Local oBrowse, cFilterBrw
	
	Default cFabricante := Space( TamSx3("T9_FABRICA")[1] )
	
	Private aRotina := MenuDef()

	// Log de Acesso LGPD
	If FindFunction( 'FWPDLogUser' )
		FWPDLogUser( 'MNTC070()' )
	EndIf
	
	cFilterBrw := " ST9->T9_FILIAL  == '" + xFilial("ST9") + "' .And. "
	cFilterBrw += " ST9->T9_FABRICA == '" + cFabricante + "' "
	
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias( "ST9" )
	oBrowse:SetFilterDefault( cFilterBrw )
	oBrowse:SetDescription( STR0001 ) // "Bens "
	oBrowse:Activate()
	
	NGReturnPrm( aNGBeginPrm )
	
Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Interface da rotina

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function MenuDef()

	Local aRotina := {}
	
	ADD OPTION aRotina TITLE STR0003 ACTION 'VIEWDEF.MNTC070' OPERATION 2 ACCESS 0 //"Visualizar"

Return aRotina

//------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ModelDef()

	Local oModel
	Local oStructST9 := FWFormStruct(1,"ST9")

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTC070")

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTC070_ST9", Nil, oStructST9 )

	oModel:SetDescription(STR0001)

Return oModel

//------------------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel("MNTC070")
	Local oView  := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTC070_ST9" , FWFormStruct(2,"ST9") )

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100 )

	// Associa um View a um box
	oView:SetOwnerView( "MNTC070_ST9" , "MASTER" )
	
	// Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn( oView )

Return oView