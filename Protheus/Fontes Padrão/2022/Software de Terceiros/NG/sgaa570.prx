#Include 'Protheus.ch'
#INCLUDE 'SGAA570.CH'  

#DEFINE _nVERSAO 2 //Versao do fonte 
 
//-------------------------------------------------------------------
/*/{Protheus.doc} SGAA570
Cadastro de Fórmulas dos Objetivos e Metas

@author Thiago Henrique dos Santos
@since 07/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Function SGAA570()

	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO) 
	Private aRotina := MenuDef()
	Private cCadastro := STR0006        

	Private aChkDel := {}, bNgGrava   

	//valida se update foi aplicado
	IF !NGCADICBASE('TDN_VAR','A','TDN',.F.)
		NGINCOMPDIC("UPDSGA16","XXXXXX")
		Return .F.
	Endif 

	DbSelectArea("TDP")
	DbSetOrder(1)
	mBrowse( 6, 1,22,75,"TDP") 

	NGRETURNPRM(aNGBEGINPRM)
Return                        


//-------------------------------------------------------------------
/*/{Protheus.doc} SGA570PRO
Processas visualização/inclusão/alteração/exclusão do cadastro de 
fórmulas para objetivos e metas.

@param cAlias - Alias da tabela
@param nRecno - número do registro recno
@param nOpcx  - Opção do menu

@author Thiago Henrique dos Santos
@since 07/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Function SGA570PRO(cAlias, nRecno, nOpcx)
	Local cTitulo := cCadastro //+ " - " + SubStr( aRotina[ nOpcx ][ 01 ], 2, Len( aRotina[ nOpcx ] [ 01 ] ) ) // Titulo da janela     
	Local oDlg  			 // Objeto para montar a janela de cadastro
	Local oMenu
	Local lVisual := .t.
	Local nControl:= 0,nOK := 0
	Local aPages:= {},aTitles:= {}
	Local Ni,oFont,oGet,cGet
	Local aSize := MsAdvSize()
	Local nLeft := 0
	Local oPnlTDet, oTDet, cTDet
	Local oFontB := TFont():New("Arial",,14,,.T.)   
	Local oPnlAll

	Private nPula := 70
	Private aCbFixo  := {}
	Private aCbVar  := {}
	Private oCbFixo,oCbVar           // Objeto do primeiro combo box
	Private cFormula   := Space( Len( TDP->TDP_CODFOR ) )
	Private cDescricao := Space( Len( TDP->TDP_DESCRI ) )
	Private cCbFixo := Space( 30 )
	Private cCbVar :=  TamSx3("TDN_DESCRI")[1]
	Private cMemoFor := ''
	Private oMemo
	Private aSvATela := {}, aSvAGets := {}, aTela := {}, aGets := {}, anao := {}

	CarBoxVar()

	AADD(aCbFixo,"")
	AADD(aCbFixo,STR0015)//#"Nº de Resultados"
	AADD(aCbFixo,STR0016)//#"Média de Resultados"
	AADD(aCbFixo,STR0017)//#"Soma de Resultados"
	AADD(aCbFixo,STR0018)//#"Resultado Mínimo"
	AADD(aCbFixo,STR0019)//#"Resultado Máximo"
	AADD(aCbFixo,STR0020)//#"Meta Início"
	AADD(aCbFixo,STR0021)//#"Meta Final"

	If !(Alltrim(GetTheme()) == "FLAT") .And. !SetMdiChild()
		aSize[7] := aSize[7]-50
		aSize[6] := aSize[6]-30
		aSize[5] := aSize[5]-14
		nLeft := 5
	EndIf

	Aadd(aTitles,OemToAnsi(STR0013)) //"Operadores"
	Aadd(aPages,"Header 1")
	nControl++
	Aadd(aTitles,OemToAnsi(STR0012)) //"Comparadores"
	Aadd(aPages,"Header 2")
	nControl++
	Aadd(aTitles,OemToAnsi(STR0014))//"Números"
	Aadd(aPages,"Header 3")
	nControl++

	If nOpcx <> 3
	   cFormula   := TDP->TDP_CODFOR
	   cDescricao := TDP->TDP_DESCRI
	   cMemoFor      := MSMM(TDP->TDP_FORMUL)	
	   //cMemoFor      := TDP->TDP_FORMUL
	EndIf                               

	If AllTrim( Str( nOpcx ) ) $ '25'
	   lVisual    := .f.
	EndIf

	Define MsDialog oDlg From aSize[7],nLeft to aSize[6],aSize[5] Title cTitulo Pixel

	oPnlAll := TPanel():New(01,01,,oDlg,,,,,,10,10,.F.,.F.)
	oPnlAll:Align := CONTROL_ALIGN_ALLCLIENT 

	oPanel1 := TPanel():New(01,01,,oPnlAll,,,,,,10,10,.F.,.F.)
	oPanel1:Align := CONTROL_ALIGN_TOP
	oPanel1:nHeight := 130

	oPanel2 := TPanel():New(01,01,,oPnlAll,,,,,,10,10,.F.,.F.)
	oPanel2:Align := CONTROL_ALIGN_ALLCLIENT

	oPanel21 := TPanel():New(01,01,,oPanel2,,,,,,10,10,.F.,.F.)
	oPanel21:Align := CONTROL_ALIGN_TOP
	oPanel21:nHeight := 50

	oPanel22 := TPanel():New(01,01,,oPanel2,,,,,,10,10,.F.,.F.)
	oPanel22:Align := CONTROL_ALIGN_ALLCLIENT

	AADD(aNao,"TDP_FORMUL")

	aChoice  := NGCAMPNSX3("TDP",aNao)

	aTela := {}
	aGets := {}
	dbselectarea("TDP")
	RegToMemory("TDP",(nOpcx == 3))
	oEnc01:= MsMGet():New("TDP",nRecno,nOpcx,,,,aChoice,{14,0,50,280},,,,,,oPanel1,,,.f.,"aSvATela")
	oEnc01:oBox:bGotFocus := {|| NgEntraEnc("TA3")}
	oEnc01:oBox:Align := CONTROL_ALIGN_ALLCLIENT
	aSvATela := aClone(aTela)
	aSvAGets := aClone(aGets)

	@ 006.5,008 Say OemToAnsi(STR0009) Size 70,7 Of oPanel21 Pixel //#"Variáveis Prontas:"
	@ 005,055 ComboBox oCbFixo Var cCbFixo Items aCbFixo Size 60, 65 Of oPanel21 Pixel When lVisual

	@ 006.5,0125 Say OemToAnsi(STR0022) Size 70,7 Of oPanel21 Pixel //#"Variáveis:"
	@ 005,0150 ComboBox oCbVar Var cCbVar Items aCbVar Size 120, 65 Of oPanel21 Pixel When lVisual

	oCbFixo:bChange := {||cCbVar := IIF(!Empty(cCbFixo),"",cCbVar)}
	oCbVar:bChange  := {||cCbFixo := IIF(!Empty(cCbVar),"",cCbFixo)}

	oFolder := TFolder():New(6,1,aTitles,aPages,oPanel22,,,,.f.,.f.,270,40,)
	oFolder:aDialogs[1]:oFont := oDlg:oFont
	oFolder:aDialogs[2]:oFont := oDlg:oFont
	oFolder:Align := CONTROL_ALIGN_TOP
	oFolder:nHeight := 90

	oPnlTDet := TPanel():New(900,900,,oPanel22,,,,,RGB(67,70,87),200,200,.F.,.F.)
	oPnlTDet:Align := CONTROL_ALIGN_TOP
	oPnlTDet:nHeight := 25

	cTDet := "Detalhamento:"
	@ 002,008 SAY oTDet VAR cTDet SIZE 200, 20 Font oFontB Color RGB(255,255,255) OF oPnlTDet PIXEL

	oMemo:= tMultiget():New(120,008,{|u|if(Pcount()>0,cMemoFor:=u,cMemoFor)},oPanel22,250,40,,.F.,,,,.T.,,,,,,.T.)
	oMemo:Align := CONTROL_ALIGN_ALLCLIENT

	@ 060,008 TO 080,248 Label '' of oFolder:aDialogs[1] Pixel

	@ 010,010 Button oBtn1 Prompt "+" Size 20,10 Of oFolder:aDialogs[1] Pixel Action AddFormula("+")
	@ 010,040 Button oBtn2 Prompt "-" Size 20,10 Of oFolder:aDialogs[1] Pixel Action AddFormula("-")
	@ 010,070 Button oBtn3 Prompt "*" Size 20,10 Of oFolder:aDialogs[1] Pixel Action AddFormula("*")
	@ 010,100 Button oBtn4 Prompt "/" Size 20,10 Of oFolder:aDialogs[1] Pixel Action AddFormula("/")
	@ 010,130 Button oBtn5 Prompt "(" Size 20,10 Of oFolder:aDialogs[1] Pixel Action AddFormula("(")
	@ 010,160 Button oBtn6 Prompt ")" Size 20,10 Of oFolder:aDialogs[1] Pixel Action AddFormula(")")
	@ 010,190 Button oBtn7 Prompt STR0010 Size 20,10 Of oFolder:aDialogs[1] Pixel Action AddFormula("If")
	@ 010,220 Button oBtn8 Prompt STR0011 Size 20,10 Of oFolder:aDialogs[1] Pixel Action AddFormula(",")

	@ 010,010 Button oBtn11 Prompt "="  Size 20,10 Of oFolder:aDialogs[2] Pixel Action AddFormula("=")
	@ 010,040 Button oBtn12 Prompt "<>" Size 20,10 Of oFolder:aDialogs[2] Pixel Action AddFormula("<>")
	@ 010,070 Button oBtn13 Prompt "<"  Size 20,10 Of oFolder:aDialogs[2] Pixel Action AddFormula("<")
	@ 010,100 Button oBtn14 Prompt "<=" Size 20,10 Of oFolder:aDialogs[2] Pixel Action AddFormula("<=")
	@ 010,130 Button oBtn15 Prompt ">"  Size 20,10 Of oFolder:aDialogs[2] Pixel Action AddFormula(">")
	@ 010,160 Button oBtn16 Prompt ">=" Size 20,10 Of oFolder:aDialogs[2] Pixel Action AddFormula(">=")

	@ 010,010 Button oBtn1 Prompt "1" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("1")
	@ 010,035 Button oBtn2 Prompt "2" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("2")
	@ 010,060 Button oBtn3 Prompt "3" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("3")
	@ 010,085 Button oBtn4 Prompt "4" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("4")
	@ 010,110 Button oBtn5 Prompt "5" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("5")
	@ 010,135 Button oBtn6 Prompt "6" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("6")
	@ 010,160 Button oBtn7 Prompt "7" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("7")
	@ 010,185 Button oBtn8 Prompt "8" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("8")
	@ 010,210 Button oBtn7 Prompt "9" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("9")
	@ 010,235 Button oBtn8 Prompt "0" Size 20,10 Of oFolder:aDialogs[3] Pixel Action AddFormula("0")

	@ 005,295 Button oBtn9 Prompt STR0007 Size 40,12 Of oPanel21 Pixel Action AddFormula()  //"Adiciona"
	@ 005,345 Button oBtn10 Prompt STR0008 Size 40,12 Of oPanel21 Pixel Action (cMemoFor := '',oMemo:Refresh(),nPula := 70) //"Limpa Filtro"

	If !lVisual
	   oBtn1:Disable()
	   oBtn2:Disable()
	   oBtn3:Disable()
	   oBtn4:Disable()
	   oBtn5:Disable()
	   oBtn6:Disable()
	   oBtn7:Disable()
	   oBtn8:Disable()
	   oBtn9:Disable()
	   oBtn10:Disable()                           
	   oBtn11:Disable()
	   oBtn12:Disable()
	   oBtn13:Disable()
	   oBtn14:Disable()
	   oBtn15:Disable()
	   oBtn16:Disable()
	EndIf	 

	@ 1000,1000 MsGet oGet Var cGet Picture "@!" Size 1,01 Of oFolder:aDialogs[2]

	NGPOPUP(aSMenu,@oMenu)
	oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)} 
	oEnc01:oBox:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
	Activate MsDialog oDlg On Init EnchoiceBar(oDlg,{||nOpca:=1,IIF(!GravaForm(nOpcx),nOpca := 0,oDlg:End())},{||oDlg:End()})

Return .t.                                                           

//-------------------------------------------------------------------
/*/{Protheus.doc} CarBoxVar
Carrega as informações no ComboBox de Variáveis

@author Thiago Henrique dos Santos 
@since 07/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Static Function CarBoxVar()

	DbSelectArea("TDN")
	TDN->(DbSetOrder(2))
	TDN->(DbGoTop())               

	AADD(aCbVar,"")

	While TDN->(!Eof())

		If TDN->TDN_FILIAL == xFilial( "TDN" )

			AADD( aCbVar,TDN->TDN_DESCRI )
			
		EndIf

		TDN->( DbSkip() )
	   
	Enddo                                                                

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} AddFormula
Adciona operações na formula.
                     
@param cOP   - Operação a ser inserida
@author Thiago Henrique dos Santos 
@since 07/02/2013
@version P11
/*/
//-------------------------------------------------------------------

Static Function AddFormula(cOp)

	If Len( cMemoFor )  >=  nPula
	//	cMemoFor += Chr(10)
		nPula += 70
	EndIf
	If cOp == Nil

		If !Empty(cCbFixo)
		
			cMemoFor +=  '#'+cCbFixo+'# '
		
		ElseIf !Empty(cCbVar)         
		
			cMemoFor +=  '#'+PadR(cCbVar,TamSx3("TDN_DESCRI")[1])+'# '
		
		Endif	
		
	ElseIf cOp $ "1234567890"
		If SubStr(cMemoFor,Len(cMemoFor)-1,1) $ "1234567890"
			cMemoFor := RTRIM(cMemoFor)+cOp+" "
		Else
			cMemoFor += cOp+" "
		EndIf
	Else
		cMemoFor += cOp+" "
	Endif
	oMemo:Refresh()
Return .t.      

//-------------------------------------------------------------------
/*/{Protheus.doc} GravaForm
Rotina de gravação. 
                              
@param nOpcx - Opção escolhida no menu
@author Thiago Henrique dos Santos 
@since 07/02/2013
@version P11                                                             
/*/
//-------------------------------------------------------------------
Static Function GravaForm(nOpcx)


	If nOpcx == 5 //exclusao   

		//validar se a formula está em alguma meta
		DbSelectArea("TAA")
		TAA->(DbSetOrder(1))
		TAA->(DbSeek(xFilial("TAA")))
		
		While TAA->(!Eof()) .AND. TAA->TAA_FILIAL == xFilial("TAA")
			If TAA->TAA_FORMUL == TDP->TDP_CODFOR .OR. TAA->TAA_FORFEC == TDP->TDP_CODFOR 
			
				ShowHelpDlg(STR0023, {STR0030,STR0031},5,{STR0032,""},5)//"Atenção"#"Fórmula utilizada em plano de ação."#"Não é possível excluir fórmula."#"Altere ou exclua plano de ação."		
				Return .F.
			Endif
		
			TAA->(DbSkip())
		Enddo
				
		MSMM(TDP->TDP_FORMUL,80,,cMemoFor,2,,,"TDP","TDP_FORMUL")
		RecLock("TDP",.F.)
		TDP->(DbDelete())
		TDP->(MsUnlock())
		
	ElseIF nOpcx == 4 .OR. nOpcx == 3 //alteração ou inclusão

		If !ValidaForm()
			Return .F.
		Endif    
		

		RecLock("TDP",IIF(nOpcx == 3,.T.,.F.))
		TDP->TDP_FILIAL := xFilial("TDP") 
		TDP->TDP_CODFOR := M->TDP_CODFOR
		TDP->TDP_DESCRI := M->TDP_DESCRI                      
		
		//TDP->TDP_FORMUL := cMemoFor
		TDP->(MsUnlock())
		
		If nOpcx == 3
			MSMM(,80,,cMemoFor,1,,,"TDP","TDP_FORMUL")
		Else 
		
			MSMM(TDP->TDP_FORMUL,80,,cMemoFor,1,,,"TDP","TDP_FORMUL")
		
		Endif
	Endif

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} ValidaForm
Rotina de validação da fórmula. 
                     
@author Thiago Henrique dos Santos 
@since 07/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Static Function ValidaForm()

	Local lSair := .F.          
	Local nTotal := 0
	Local cVar := ""
	Local i := 0                  
	Local Ind := 1
	Local nCount := 0
	Local aCampo := {}
	Local cCampo := ""
	Local bErro := ErrorBlock({|e|ShowHelpDlg(STR0023,{STR0026+CRLF+e:Description},1,{STR0025}),lRet := .F.})//"Atenção"###"Fórmula Inválida."###"Preencha a fórmula com uma expressão válida."
	Local lRet := .T. 

	If Empty(M->TDP_CODFOR)

		ShowHelpDlg(STR0023,{STR0028},1,{STR0029}) //"Atenção"###"O campo TDP_CODFOR é obrigatório."###"Preencha o campo TDP_CODFOR com um valor válido."
		Return .F.

	Endif

	If Empty(cMemoFor)

		ShowHelpDlg(STR0023,{STR0024},1,{STR0025}) //"Atenção"###"A fórmula não pode estar vazia."###"Preencha a fórmula com uma expressão válida."
		Return .F.
		
	Endif


	For i := 1 To Len( cMemoFor )
	   If SubStr( cMemoFor, Ind, 1 ) == '#' 
		  If nCount > 1
			 nCount := 0
			 aAdd( aCampo, { 0 } ) 
			 Ind++
		  EndIf
	   EndIf 
	   If SubStr( cMemoFor, Ind, 1 ) <> "#" .and. nCount == 0
		  aAdd( aCampo, { SubStr( cMemoFor, Ind, 1 ) } )
	   Else 
		  nCount ++
	   EndIf                                   
	  Ind++
	Next



	BEGIN SEQUENCE           
		For i := 1 To Len( aCampo )
			If ValType( aCampo[i][1] ) == 'N'
			   cCampo += Str( aCampo[i][1] )
			Else                             
			   cCampo += aCampo[i][1]
			EndIf   
		Next i                 
		
		xResult := &cCampo
	END SEQUENCE


	ErrorBlock(bErro)


Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Monta o menu da rotina

@author Thiago Henrique dos Santos 
@since 07/02/2013
@version P11
/*/
//-------------------------------------------------------------------
Static Function MenuDef()
	Local aRotina := {	{ STR0001  , "AxPesqui"  , 0 , 1},;   //"Pesquisar"
						{ STR0002  , "SGA570PRO"   , 0 , 2},;   //"Visualizar"
						{ STR0003  , "SGA570PRO"   , 0 , 3},;   //"Incluir"
						{ STR0004  , "SGA570PRO"   , 0 , 4},;   //"Alterar"
						{ STR0005  , "SGA570PRO"   , 0 , 5}}  //"Excluir"

Return aRotina
