#INCLUDE "MDTC350.ch"
#Include "Protheus.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} MDTC350
Consulta Geral de Programa de Saude

@sample	MDTC350()

@author	Luis Fellipy Bett
@since	25/01/2022

@return .T., Sempre verdadeiro
/*/
//---------------------------------------------------------------------
Function MDTC350()

	//Guarda conteúdo e declara variáveis padrões
	Local aNGBEGINPRM := NGBEGINPRM()

	Private aRotina := MenuDef()
	Private aChoice	:= {}
	Private aNao	:= {}

	//Caso o usuário tenha acesso à rotina
	If FindFunction( "MDTRESTRI" ) .And. MDTRESTRI( cPrograma ) .And. AmIIn( 35 )

		Private cPrograma := 'MDTC350'
		
		cCadastro := OemToAnsi( STR0004 ) //"Programa de Saude"
		
		//Abre o browse
		mBrowse( 6, 1, 22, 75, "TMO" )
		
		//Recupera a Ordem Original do arquivo principal
		dbSelectArea( "TMO" )
		dbSetOrder( 1 )
		
	EndIf

	//Retorna conteúdo das variáveis padrões
	NGRETURNPRM( aNGBEGINPRM )

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Criação do menu da rotina

@sample	MenuDef()

@author	Luis Fellipy Bett
@since	25/01/2022

@return aRotina, Array, Array contendo os itens do menu da rotina
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

	Local aRotina := {{ STR0001, "AxPesqui", 0, 1 },; //"Pesquisar"
					{ STR0002, "NGCAD01", 0, 2 },; //"Visualizar"
					{ STR0003, "MDC350FPR", 0, 3 }} //"Func.X Prog."

Return aRotina

//---------------------------------------------------------------------
/*/{Protheus.doc} MDC350FPR
Monta um browse com os Funcionarios X Programa

@sample	MDC350FPR()

@author	Luis Fellipy Bett
@since	25/01/2022

@return Nil, Sempre nulo
/*/
//---------------------------------------------------------------------
Function MDC350FPR()

	//Salva o aRotina
	Local OldRot := aClone( aRotina )

	aRotina := {{ STR0001 ,"AXPESQUI", 0, 1 },; //"Pesquisar"
				{ STR0005 ,"MDTC350PRG", 0, 2 },; //"Vis. Prog."
				{ STR0006 ,"MDTC350FIC", 0, 2 },; //"Vis. Ficha" 
				{ STR0007 ,"MDTC350EFU", 0, 4, 0 },; //"Exames Func."
				{ STR0008 ,"MDTC350MED", 0, 5, 0 }} //"Medicamentos"
	
	M->TMN_CODPROG := TMO->TMO_CODPROG  
	
	dbSelectArea( "TMN" )
		
	cKey := M->TMN_CODPROG
	
	bWHILE := {|| !Eof() .And. TMN->TMN_CODPROG == M->TMN_CODPROG }
	
	bFOR := { || TMN->TMN_FILIAL  == xFilial( "TMN" ) .And. TMN->TMN_CODPROG == M->TMN_CODPROG }
	
	dbSelectArea( "TMN" )
	dbSetOrder( 1 )
	
	NGCONSULTA( "TRBN", cKey, bWHILE, bFOR, aRotina, NIL, , , , , , , .F. )
	
	dbSelectArea( "TMN" )
	dbSetOrder( 1 )

	//Retorna o aRotina
	aRotina := aClone( OldRot )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MDTC350FIC
Posiciona no registro corresponde dA TM0 e abre a ficha médica para visualização

@sample	MDTC350FIC()

@author	Luis Fellipy Bett
@since	25/01/2022

@return Nil, Sempre nulo
/*/
//---------------------------------------------------------------------
Function MDTC350FIC()
	
	aChoice := NGCAMPNSX3( "TM0", { "TM0_CLIENT", "TM0_LOJA", "TM0_NOMCLI" } )

	NGVISUPOS( "TM0", TRBN->TMN_NUMFIC )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MDTC350EFU
Posiciona no registro corresponde do TM0

@sample	MDTC350EFU()

@author	Luis Fellipy Bett
@since	25/01/2022

@return Nil, Sempre nulo
/*/
//---------------------------------------------------------------------
Function MDTC350EFU()

	dbSelectArea( "TM0" )
	dbSetOrder( 1 )
	dbSeek( xFilial( "TM0" ) + TRBN->TMN_NUMFIC )

	//Chama a função pra montar o browse com os exames do funcionário
	MDC305EFU() 

	dbSelectArea( "TMN" )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MDTC350MED
Posiciona no registro corresponde do TM0

@sample	MDTC350MED()

@author	Luis Fellipy Bett
@since	25/01/2022

@return Nil, Sempre nulo
/*/
//---------------------------------------------------------------------
Function MDTC350MED()

	Local cAliasAux := Alias()

	dbSelectArea( "TM0" )
	dbSetOrder( 1 )
	dbSeek( xFilial( "TM0" ) + TRBN->TMN_NUMFIC )

	//Chama função para montar o browse com os medicamentos do funcionário
	MDC300MED()

	dbSelectArea( cAliasAux )
	dbSetOrder( 1 )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MDTC350PRG
Posiciona no registro corresponde da TMO e abre a programação para visualização

@sample	MDTC350PRG()

@author	Luis Fellipy Bett
@since	25/01/2022

@return Nil, Sempre nulo
/*/
//---------------------------------------------------------------------
Function MDTC350PRG()

	aChoice := NGCAMPNSX3( "TMO", { "TMO_CLIENT", "TMO_LOJA", "TMO_NOMCLI" } )

	NGVISUPOS( "TMO", TRBN->TMN_CODPROG )

Return
