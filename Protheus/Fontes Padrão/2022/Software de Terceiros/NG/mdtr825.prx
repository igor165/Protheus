#INCLUDE "mdtr825.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 2 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTR825  ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 12/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Emitir o resumo de doencas ocupacionais                     ³±±
±±³          ³conforme o Quadro IV da NR4.                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ MDTR825(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDTR825
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cString := "TNC"
Local wnrel   := "MDTR825"
Local cDesc1  := STR0001 //"Emitir o resumo de doencas ocupacionais "
Local cDesc2  := STR0002 //"conforme o Quadro IV da NR4."
Local cDesc3  := " "

Private aReturn  := { STR0003, 1, STR0004, 2, 2, 1, "",1 }  //"Zebrado"###"Administracao"
Private nomeprog := "MDTR825"
Private tamanho  := "M"
Private titulo   := STR0005 //"Doencas Ocupacionas"
Private ntipo    := 0
Private nLastKey := 0
Private cPerg    := ""
Private cabec1   := " "
Private cabec2   := " "
Private cAliasCC := "SI3"
Private cDescrCC := "SI3->I3_DESC"
Private cFilialCC:= "SI3->I3_FILIAL"
Private cCodcc   := "SI3->I3_CUSTO"
Private nSizeSI3 := If((TAMSX3("I3_CUSTO")[1]) < 1,9,(TAMSX3("I3_CUSTO")[1]))
Private nSizeFil := FwSizeFilial()
Private lCompTNC := (NGSX2MODO("TNC") == "C")
Private lMdtps   := .f.
Private cCodcli, cSeek1, cSeek2
Private aVetinr := {}
nTa1 := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
nSizeTD := nTa1

If Alltrim(GETMV("MV_MCONTAB")) == "CTB"
	cAliasCC := "CTT"
	cDescrCC := "CTT->CTT_DESC01"
	cString  := "CTT"
	cFilialCC := "CTT->CTT_FILIAL"
	cCodcc  := "CTT->CTT_CUSTO"
Endif
If SuperGetMv("MV_NGMDTPS",.F.,"N") == "S"
	lMdtps := .t.
Endif        

lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
cPerg    := If(!lSigaMdtPS,"MDT825    ","MDT825PS  ")

/*---------------------------------------------------
//PERGUNTAS PADRÃO										|
| MDT825    ¦01      ¦De  Data ?						|
| MDT825    ¦02      ¦Ate  Data ?						|
| MDT825    ¦03      ¦Data do mapa ?					|
| MDT825    ¦04      ¦Responsavel ?					|
| MDT825    ¦05      ¦Imprimir zero (0) ?				|
| MDT825    ¦06      ¦Imprimir Total de Empregados ?	|
| MDT825    ¦07      ¦De Filial ?						|
| MDT825    ¦08      ¦Ate Filial ?						|
|															|
//PERGUNTAS PRESTADOR DE SERVIÇO						|
|		   01       De Cliente ?							|
|		   02       Loja									|
| 		   03       Até Cliente ?						|
| 		   04       Loja									|
| MDT825    ¦05      ¦De  Data ?						|
| MDT825    ¦06      ¦Ate  Data ?						|
| MDT825    ¦07      ¦Data do mapa ?					|
| MDT825    ¦08      ¦Responsavel ?					|
| MDT825    ¦09      ¦Imprimir zero (0) ?				|
| MDT825    ¦10      ¦Imprimir Total de Empregados ?	|
| MDT825    ¦11      ¦De Filial ?						|
| MDT825    ¦12      ¦Ate Filial ?						|
-----------------------------------------------------*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01             // De  Data		                     ³
//³ mv_par02             // Ate Data        		             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

If nLastKey == 27
	Set Filter to
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Set Filter to
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)
	Return
Endif

If lSigaMdtps
	dDataMap := mv_par07
	cCodResp := mv_par08
	nZero := mv_par09
Else
	dDataMap := mv_par03
	cCodResp := mv_par04
	nZero := mv_par05
Endif

RptStatus({|lEnd| R825Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)

Return NIL
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R825Imp  ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 12/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTR825                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R825Imp(lEnd,wnRel,titulo,tamanho)
Local cRodaTxt := ""
Local nCntImpr := 0
Local nYY,nXX,i
Local oTempTRB, oTempTRB2

Private aFiliais := {}, cFilAtual := cFilAnt, cNomFil := Upper(Substr(SM0->M0_NOME, 1, 40))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private li := 80 ,m_pag := 1 ,nQtTotFunc  := 0 ,lPRINT := .t.
Private lImpTotEmp := If(lSigaMdtps,Mv_par10==1,Mv_par06==1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se deve comprimir ou nao                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTipo  := IIF(aReturn[4]==1,15,18)

/*
          1         2         3         4         5         6         7         8         9         0         1        12        13
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
________________________________________________________________________________________________________________________
|				                                                                                                         |
| DOENÇAS OCUPACIONAIS______________________________________________________________________   DATA DO MAPA: ___/___/___ |
|																						   		               		     |
| RESPONSAVEL:__________________________________________________________    ASS:________________________________________ |
|________________________________________________________________________________________________________________________|
|   Tipo de   | No. Absoluto |   Setores de   | No. Absoluto de |  No. de  | No. de Trabalhadores | No. de Trabalhadores |
|   Doenca    |   de Casos   | Atividades dos | Casos (% Total  |  Obitos  |  Transferidos Para   |    Definitivamente   |
|             |              |   Portadores   | dos Empregados) |          |     Outro Setor      |    Incapacitados     |
|_____________|______________|________________|_________________|__________|______________________|______________________|
|  XXXXXXXX   |    999999    |     999999     |     999999      |  999999  |        999999        |        999999        |
|_____________|______________|________________|_________________|__________|______________________|______________________|
|  XXXXXXXX	  |   99999999   |99999999        |   99999999      | 99999999 |       99999999       |     99999999         |
|_____________|______________|________________|_________________|__________|______________________|______________________|
|  XXXXXXXX	  |   99999999   |99999999        |   99999999      | 99999999 |       99999999       |     99999999         |
|_____________|______________|________________|_________________|__________|______________________|______________________|


          1         2         3         4         5         6         7         8         9         0         1        12        13
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012

							RESUMO DAS DOENCAS DETECTADAS EM MAIS DE UM SETOR
							
Doenca..: Z8.09 - 

Centro de Custo  Nome                       Ocorrencias
101              Producao                             3
							
*/
//============================================================================
aDBF := {}
AADD(aDBF,{ "CODCID"   , "C" ,08, 0 }) 
AADD(aDBF,{ "QTCASOS"  , "N" ,06, 0 }) 
AADD(aDBF,{ "QTOBITOS" , "N" ,06, 0 }) 
AADD(aDBF,{ "QTTRANSF" , "N" ,06, 0 }) 
AADD(aDBF,{ "QTINCAPA" , "N" ,06, 0 }) 
AADD(aDBF,{ "CLIENTE"  , "C" ,If(lSigaMdtps,nSizeTD,nTa1), 0 })

//Cria TRB
oTempTRB := FWTemporaryTable():New( "TRBA", aDBF )
If lMdtps .OR. lSigaMdtps
	oTempTRB:AddIndex( "1", {"CLIENTE","CODCID"} )
	cSeek1 := "Substr(TNC->TNC_CC,1,"+StrZero(nSizeTD,2)+") + TMT->TMT_CID"
Else
	oTempTRB:AddIndex( "1", {"CODCID"} )
	cSeek1 := "TMT->TMT_CID"
EndIf
oTempTRB:Create()

aDBF2 := {}
AADD(aDBF2,{ "CCUSTO"   , "C" ,nSizeSI3, 0 }) 
AADD(aDBF2,{ "CODCID"   , "C" ,08      , 0 }) 
AADD(aDBF2,{ "QTOCOR"   , "N" ,06      , 0 }) 
AADD(aDBF2,{ "CLIENTE"  , "C" ,If(lSigaMdtps,nSizeTD,nTa1), 0 })

oTempTRB2 := FWTemporaryTable():New( "TRBC", aDBF2 )
If lMdtps .OR. lSigaMdtps
	oTempTRB2:AddIndex( "1", {"CLIENTE","CODCID","CCUSTO"} )
	cSeek2 := "Substr(TNC->TNC_CC,1,"+StrZero(nSizeTD,2)+") + TMT->TMT_CID + TNC->TNC_CC"
Else
	oTempTRB2:AddIndex( "1", {"CODCID","CCUSTO"} )
	cSeek2 := "TMT->TMT_CID + TNC->TNC_CC"
EndIf
oTempTRB2:Create()

If !lSigaMdtps .and. !lMdtps 
	aFiliais := MDTRETFIL("SRA", MV_PAR07, MV_PAR08)
Else
	aFiliais := { {cFilAnt, Upper(Substr(SM0->M0_NOME, 1, 40)) } }
Endif

For i:=1 to Len(aFiliais)
	lImpRel := .f.
	lPRINT := .t.
	
	cFilAtual	:= aFiliais[i,1]
	cNomFil		:= aFiliais[i,2]
	
	dbSelectArea("SM0")
	dbSeek(cEmpAnt+cFilAtual)
	
	dbSelectArea("TRBA")
	ZAP
	
	dbSelectArea("TRBC")
	ZAP
			
	nRegTotal := ( TNC->(LastRec()) * 2 )
	SetRegua(nRegTotal)
	
	If lSigaMdtps
		
		aDadosTNC := {}
		DbSelectArea("TNC")
		DbSetOrder(13)
		DbSeek(xFilial("TNC",cFilAtual)+mv_par01+mv_par02,.t.)
		While !EOF() .AND. TNC->TNC_Filial == xFilial("TNC",cFilAtual) .and. ;
			TNC->(TNC_CLIENT+TNC_LOJA) >= mv_par01+mv_par02 .and. TNC->(TNC_CLIENT+TNC_LOJA) <= mv_par03+mv_par04
			
			IncRegua()

			If TNC->TNC_DTACID >= mv_par05 .AND. TNC->TNC_DTACID <= mv_par06 .and. TNC->TNC_INDACI == "3"
				aAdd( aDadosTNC , { TNC->TNC_ACIDEN, TNC->TNC_CATINS, TNC->TNC_NUMFIC, TNC->TNC_VITIMA,;
				TNC->TNC_TRANSF, TNC->TNC_TIPCAT, TNC->TNC_CC, TNC->TNC_APOSEN,;
				TNC->TNC_CLIENT+TNC->TNC_LOJA } )
			Endif
			
			DbSelectArea("TNC")
			DbSkip()
		End
		aSort(aDadosTNC,,,{|x,y| x[2]+x[6] < y[2]+y[6] })
		
		For nXX := 1 To Len(aDadosTNC)
			IncRegua()
			cCodInss := aDadosTNC[nXX,2]
			If !Empty(cCodInss)
				nRegValido := nXX
				For nYY := nXX To Len(aDadosTNC)
					If cCodInss == aDadosTNC[nYY,2]
						nRegValido := nYY
					Else
						Exit
					Endif
				Next nYY
				nXX := nRegValido
			Endif
			
			nCasos := 0
			nObito := 0
			nTrans := 0
			nIncap := 0
			
			nCasos := 1
			If aDadosTNC[nXX,4] == "3"
				nObito := 1
			Endif
			If aDadosTNC[nXX,5] == "1"
				nTrans := 1
			Endif
			If aDadosTNC[nXX,8] == "1"
				nIncap := 1
			Endif
			Dbselectarea("TMT")
			Dbsetorder(7)  //TMT_FILIAL+TMT_CLIENT+TMT_LOJA+TMT_ACIDEN+TMT_NUMFIC
			If Dbseek(xFilial("TMT",cFilAtual)+aDadosTNC[nXX,1]+aDadosTNC[nXX,3])
				MDT825GRAVA(aDadosTNC[nXX,7],TMT->TMT_CID)
				lImpRel := .t.
			Endif
		Next nXX
		
	Else
		
		aDadosTNC := {}
		DbSelectArea("TNC")
		DbSetOrder(1)
		DbSeek(xFilial("TNC",cFilAtual))
		While !EOF() .AND. TNC->TNC_Filial == xFilial("TNC",cFilAtual)
			
			IncRegua()

			If lCompTNC
				If TNC->TNC_FILFUN != cFilAtual
					DbSelectArea("TNC")
					DbSkip()
					Loop
				Endif
			Endif
			If TNC->TNC_DTACID >= Mv_par01 .and. TNC->TNC_DTACID <= Mv_par02 .and. TNC->TNC_INDACI == "3"
				aAdd( aDadosTNC , { TNC->TNC_ACIDEN, TNC->TNC_CATINS, TNC->TNC_NUMFIC, TNC->TNC_VITIMA,;
				TNC->TNC_TRANSF, TNC->TNC_TIPCAT, TNC->TNC_CC, TNC->TNC_APOSEN } )
			Endif
			
			DbSelectArea("TNC")
			DbSkip()
		End
		aSort(aDadosTNC,,,{|x,y| x[2]+x[6] < y[2]+y[6] })
		
		For nXX := 1 To Len(aDadosTNC)
			IncRegua()
			cCodInss := aDadosTNC[nXX,2]
			If !Empty(cCodInss)
				nRegValido := nXX
				For nYY := nXX To Len(aDadosTNC)
					If cCodInss == aDadosTNC[nYY,2]
						nRegValido := nYY
					Else
						Exit
					Endif
				Next nYY
				nXX := nRegValido
			Endif
			
			nCasos := 0
			nObito := 0
			nTrans := 0
			nIncap := 0
			
			nCasos := 1
			If aDadosTNC[nXX,4] == "3"
				nObito := 1
			Endif
			If aDadosTNC[nXX,5] == "1"
				nTrans := 1
			Endif
			If aDadosTNC[nXX,8] == "1"
				nIncap := 1
			Endif
			Dbselectarea("TMT")
			Dbsetorder(7)
			If Dbseek(xFilial("TMT",cFilAtual)+aDadosTNC[nXX,1]+aDadosTNC[nXX,3])
				MDT825GRAVA(aDadosTNC[nXX,7],TMT->TMT_CID)
				lImpRel := .t.
			Endif
		Next nXX
		
	Endif
	
	If lImpRel
		MDT825FUNC()
		MDT825RESUMO()
		If lMdtps
			//Senao tiver dados p/ imprimir para o cliente, imprime o quadro vazio (a lei exige)
			dbSelectArea(cAliasCC)
			dbSetOrder(1)
			dbSeek(xFilial(cAliasCC,cFilAtual))
			While !eof() .and. xFilial(cAliasCC,cFilAtual) == &(cFilialCC)
				
				If cCodCli <> SubSTR(&(cCodCC),1,nTa1)
					cCodCli := SubSTR(&(cCodCC),1,nTa1)
					dbSelectArea("TRBC")
					If !dbSeek(cCodCli)
						_MDT825RESUMO()
					Endif
				Endif
				
				dbSelectArea(cAliasCC)
				dbSkip()
			End
		Elseif lSigaMdtps
			dbSelectArea(cAliasCC)
			dbSetOrder(1)
			dbSeek(xFilial(cAliasCC,cFilAtual)+mv_par01+mv_par02,.t.)
			While !eof() .and. xFilial(cAliasCC,cFilAtual) == &(cFilialCC) .and. Substr(&(cCodCC),1,nSizeTD) <= mv_par03+mv_par04
				
				If cCodCli <> SubSTR(&(cCodCC),1,nSizeTD)
					cCodCli := SubSTR(&(cCodCC),1,nSizeTD)
					dbSelectArea("TRBC")
					If !dbSeek(cCodCli)
						_MDT825RESUMO()
					Endif
				Endif
				
				dbSelectArea(cAliasCC)
				dbSkip()
			End
		Endif
		Roda(nCntImpr,cRodaTxt,Tamanho)
	Else   //Senao tiver dados p/ imprimir, imprime o quadro vazio (a lei exige)
		If lMdtps
			//Senao tiver dados p/ imprimir para o cliente, imprime o quadro vazio (a lei exige)
			dbSelectArea(cAliasCC)
			dbSetOrder(1)
			dbSeek(xFilial(cAliasCC,cFilAtual))
			While !eof() .and. xFilial(cAliasCC,cFilAtual) == &(cFilialCC)
				
				If cCodCli <> SubSTR(&(cCodCC),1,nTa1)
					cCodCli := SubSTR(&(cCodCC),1,nTa1)
					_MDT825RESUMO()
				Endif
				
				dbSelectArea(cAliasCC)
				dbSkip()
			End
		Elseif lSigaMdtps
			dbSelectArea(cAliasCC)
			dbSetOrder(1)
			dbSeek(xFilial(cAliasCC,cFilAtual)+mv_par01+mv_par02,.t.)
			While !eof() .and. xFilial(cAliasCC,cFilAtual) == &(cFilialCC) .and. Substr(&(cCodCC),1,nSizeTD) <= mv_par03+mv_par04
				
				If cCodCli <> SubSTR(&(cCodCC),1,nSizeTD)
					cCodCli := SubSTR(&(cCodCC),1,nSizeTD)
					_MDT825RESUMO()
				Endif
				
				dbSelectArea(cAliasCC)
				dbSkip()
			End
		Else
			MDT825FUNC()
			_MDT825RESUMO()
		Endif
	Endif
Next i

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("TNC")

Set Filter To

Set device to Screen

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif
//SET CENTURY ON
MS_FLUSH()  

dbSelectArea("TRBC")
//use 
oTempTRB2:Delete()
dbSelectArea("TRBA")
//use
oTempTRB:Delete()
dbSelectArea("TNC")
dbSetOrder(01)

Return NIL

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³QTEFUNCC() ³ Autor³Denis Hyroshi de Souza ³ Data  ³ 12/07/02³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Acumula o Total de Funcionario                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ QTEFUNCC()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTR825                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/   
Static Function MDT825FUNC()
Local dDataIni := StoD("")
Local dDataFim := StoD("")

If lSigaMdtps
	dDataIni := Mv_par05
	dDataFim := Mv_par06
Else
	dDataIni := Mv_par01
	dDataFim := Mv_par02
Endif

lAvgFunPE := If( SuperGetMv("MV_MDTAVGF",.F.,"N") == "S", .t. , .f. ) 
If lAvgFunPE //Se existir parametro MV_MDTAVGF e este for igual a S, faz a media mensal de funcionarios

	nRegTotal += ( SRA->(LastRec()) * Round((dDataFim - dDataIni) / 30,0) )
	SetRegua(nRegTotal)

	nQtTotFunc := 0
	nQtdeTemp := 0
	nContador := 0
	dDataTemp := dDataIni
	While dDataTemp <= dDataFim
		_Mvpar01 := StoD(	StrZero(Year(dDataTemp),4) + ;
							StrZero(Month(dDataTemp),2) + ;
							"01" )
		_Mvpar02 := ""
		If Month(dDataTemp) == 12
			_Mvpar02 := StoD(  StrZero(Year(dDataTemp)+1,4) + ;
								"01" + ;
								"01" ) - 1
		Else
			_Mvpar02 := StoD(  StrZero(Year(dDataTemp),4) + ;
								StrZero(Month(dDataTemp)+1,2) + ;
								"01" ) - 1
		Endif

		nQtdeTemp += MDTCAGED(_Mvpar01,_Mvpar02,,,cFilAtual,,,.T.)
		nContador++

		If Month(dDataTemp) == 12
			dDataTemp := StoD(  StrZero(Year(dDataTemp)+1,4) + ;
								"01" + ;
								"01" )
		Else
			dDataTemp := StoD(  StrZero(Year(dDataTemp),4) + ;
								StrZero(Month(dDataTemp)+1,2) + ;
								"01" )
		Endif
	End
	nQtTotFunc := nQtdeTemp / nContador
Else
	nRegTotal += SRA->(LastRec())
	SetRegua(nRegTotal)
	nQtTotFunc := MDTCAGED(dDataIni,dDataFim,,,cFilAtual,,,.T.)
Endif

Return  .t.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MDT825GRAVA³ Autor³Denis Hyroshi de Souza ³ Data  ³ 12/07/02³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Acumula Dados da tabela de Acidentes                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ MDT825GRAVA()	    	                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTR825                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/   
Static Function MDT825GRAVA(cTNC_CC,cTMT_CID)

DbSelectArea("TRBA")
If DbSeek(&cSeek1)
	RecLock("TRBA",.f.)
	TRBA->QTOBITOS   += nObito
	TRBA->QTCASOS    += nCasos
	TRBA->QTTRANSF   += nTrans
	TRBA->QTINCAPA   += nIncap
	MsUnlock()
Else
	TRBA->(DbAppend())
	If lSigaMdtps
		TRBA->CLIENTE    := Substr(cTNC_CC,1,nSizeTD)
	Else
		TRBA->CLIENTE    := Substr(cTNC_CC,1,nTa1)
	Endif
	TRBA->CODCID		:= cTMT_CID
	TRBA->QTOBITOS   := nObito
	TRBA->QTCASOS    := nCasos
	TRBA->QTTRANSF   := nTrans
	TRBA->QTINCAPA   := nIncap
EndIf

DbSelectArea("TRBC")
If DbSeek(&cSeek2)
	RecLock("TRBC",.f.)
	TRBC->QTOCOR  += 1
	MsUnlock()
Else
	TRBC->(DbAppend())
	If lSigaMdtps
		TRBC->CLIENTE  := Substr(cTNC_CC,1,nSizeTD)
	Else
		TRBC->CLIENTE  := Substr(cTNC_CC,1,nTa1)
	Endif
	TRBC->CODCID	  := cTMT_CID
	TRBC->CCUSTO   := cTNC_CC
	TRBC->QTOCOR   := 1
EndIf

Return  .t.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   MDT825RESUMO³ Autor³Denis Hyroshi de Souza ³ Data  ³ 12/07/02³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Imprime o Resumo dos Acidentes Com Vitimas                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ MDT825RESUMO()                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Codigo Centro de Custo                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTR825                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/   
Static Function MDT825RESUMO() 

Dbselectarea("TRBA")
DbGotop()
cCodcli := TRBA->CLIENTE

While !Eof()
  
  If lmdtps .or. lSigaMdtps
  	If cCodcli != TRBA->CLIENTE
  		MDT825SETOR()
  		MDT825QUAD()
  		lPRINT := .t.
  		cCodcli := TRBA->CLIENTE
  	EndIf
  EndIf
  
  nRelativo := ((TRBA->QTCASOS) * 100) / nQtTotFunc 
  If lPRINT

    SomaLinha()
    @ Li,000 PSay " "

    SomaLinha()
    @ Li,001 PSay Replicate("_",120)
    Somalinha()
    @ Li,000 PSay "|"
    @ Li,121 PSay "|"

    SomaLinha()
	If Empty(dDataMap)    					                   
    	@ Li,000 PSay STR0007 //"| DOENCAS OCUPACIONAIS______________________________________________________________________   DATA DO MAPA: ___/___/___ |"
    Else
    	@ Li,000 PSay STR0022  //"| DOENCAS OCUPACIONAIS______________________________________________________________________   DATA DO MAPA:"
    	@ Li,109 PSay DtoC(dDataMap)
	    @ Li,121 PSay "|"    
    Endif
    
    Somalinha()
    @ Li,000 PSay "|"
    @ Li,121 PSay "|"    
    
    Somalinha()
	If Empty(cCodResp)    
    	@ Li,000 PSay STR0008 //"| RESPONSAVEL:__________________________________________________________    ASS:________________________________________ |"
	Else
    	@ Li,000 PSay STR0023  //"| RESPONSAVEL:"
    	@ Li,015 PSay SubStr( NGSEEK("TMK",cCodResp,1,"TMK->TMK_NOMUSU"),1,40 )    	   
    	@ Li,076 PSay STR0024  //"ASS:________________________________________ |"
	Endif
    
    SomaLinha()
    @ Li,000 PSay "|"
    @ Li,001 PSay Replicate("_",120)
    @ Li,121 PSay "|"
    
    Somalinha()
    @ Li,000 PSay STR0009 //"|   Tipo de   | No. Absoluto |   Setores de   |  No. Absoluto   |  No. de  | No. de Trabalhadores | No. de Trabalhadores |"
    Somalinha()
    @ Li,000 PSay STR0010 //"|   Doenca    |   de Casos   | Atividades dos |  (% Total dos   |  Obitos  |  Transferidos Para   |   Definitivamente    |"
    Somalinha()
    @ Li,000 PSay STR0030 //"|             |              | Portadores (*) |   Empregados)   |          |     Outro Setor      |    Incapacitados     |"
    Somalinha()
    @ Li,000 PSay STR0031 //"|_____________|______________|________________|_________________|__________|______________________|______________________|"
    
    lPRINT := .f.
  Endif 
  
  Somalinha()
  @ Li,000 PSay "|"
  @ Li,003 PSay TRBA->CODCID	
  @ Li,014 PSay "|" 
  @ Li,019 PSay TRBA->QTCASOS Picture "999999"  
  @ Li,029 PSay "|" 
  //@ Li,035 PSay CCusto
  @ Li,046 PSay "|" 
  @ Li,051 PSay nRelativo Picture "999999.99"
  @ Li,064 PSay "|" 
  @ Li,067 PSay TRBA->QTOBITOS Picture "999999"
  @ Li,075 PSay "|"   
  @ Li,084 PSay TRBA->QTTRANSF Picture "999999"
  @ Li,098 PSay "|"   
  @ Li,107 PSay TRBA->QTINCAPA Picture "999999"
  @ Li,121 PSay "|"    
    
  SomaLinha()
  @ Li,000 PSay "|_____________|______________|________________|_________________|__________|______________________|______________________|"  
  
  DbSelectArea("TRBA")
  DbSkip()  
End

If !lPrint
	MDT825SETOR()
	MDT825QUAD()
Endif

Return .t.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o  _MDT825RESUMO³ Autor³Andre Perez Alvarez      Data  ³ 29/02/08³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Imprime o Resumo dos Acidentes Com Vitimas                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ _MDT825RESUMO()                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTR825                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/   
Static Function _MDT825RESUMO() 

SomaLinha()
@ Li,000 PSay " "

SomaLinha()
@ Li,001 PSay Replicate("_",120)
Somalinha()
@ Li,000 PSay "|"
@ Li,121 PSay "|"

SomaLinha()
If Empty(dDataMap)
	@ Li,000 PSay STR0007 //"| DOENCAS OCUPACIONAIS______________________________________________________________________   DATA DO MAPA: ___/___/___ |"
Else
	@ Li,000 PSay STR0022  //"| DOENCAS OCUPACIONAIS______________________________________________________________________   DATA DO MAPA:"
	@ Li,109 PSay DtoC(dDataMap)
	@ Li,121 PSay "|"
Endif

Somalinha()
@ Li,000 PSay "|"
@ Li,121 PSay "|"

Somalinha()
If Empty(cCodResp)
	@ Li,000 PSay STR0008 //"| RESPONSAVEL:__________________________________________________________    ASS:________________________________________ |"
Else
	@ Li,000 PSay STR0023  //"| RESPONSAVEL:"
	@ Li,015 PSay SubStr( NGSEEK("TMK",cCodResp,1,"TMK->TMK_NOMUSU"),1,40 )
	@ Li,076 PSay STR0024  //"ASS:________________________________________ |"
Endif

SomaLinha()
@ Li,000 PSay "|"
@ Li,001 PSay Replicate("_",120)
@ Li,121 PSay "|"

Somalinha()
@ Li,000 PSay STR0009 //"|   Tipo de   | No. Absoluto |   Setores de   |  No. Absoluto   |  No. de  | No. de Trabalhadores | No. de Trabalhadores |"
Somalinha()
@ Li,000 PSay STR0010 //"|   Doenca    |   de Casos   | Atividades dos |  (% Total dos   |  Obitos  |  Transferidos Para   |   Definitivamente    |"
Somalinha()
@ Li,000 PSay STR0030 //"|             |              | Portadores (*) |   Empregados)   |          |     Outro Setor      |    Incapacitados     |"
Somalinha()
@ Li,000 PSay STR0031 //"|_____________|______________|________________|_________________|__________|______________________|______________________|"

If nZero == 1
	Somalinha()
	@ Li,000 PSay "|" + STR0029  //0 (zero)
	@ Li,014 PSay "|" + STR0029  //0 (zero)
	@ Li,029 PSay "|" + STR0029  //0 (zero)
	//@ Li,035 PSay CCusto
	@ Li,046 PSay "|" + STR0029  //0 (zero)
	@ Li,064 PSay "|" + STR0029  //0 (zero)
	@ Li,075 PSay "|" + STR0029  //0 (zero)
	@ Li,098 PSay "|" + STR0029  //0 (zero)
	@ Li,121 PSay "|"
Else
	Somalinha()
	@ Li,000 PSay "|"
	@ Li,014 PSay "|"
	@ Li,029 PSay "|"
	//@ Li,035 PSay CCusto
	@ Li,046 PSay "|"
	@ Li,064 PSay "|"
	@ Li,075 PSay "|"
	@ Li,098 PSay "|"
	@ Li,121 PSay "|"
Endif

SomaLinha()
@ Li,000 PSay "|_____________|______________|________________|_________________|__________|______________________|______________________|"

_MDT825SETOR()
MDT825QUAD()

Return .t.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   |MDT825SETOR³ Autor³Denis Hyroshi de Souza ³ Data  ³ 12/07/02³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Imprime o Resumo dos Acidentes Com Vitimas                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTR825                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/   
Static Function MDT825SETOR()
lFirst := .t.
Dbselectarea("TRBC")
If lMdtps .or. lSigaMdtps
	dbSeek(cCodcli)
Else
	DbGotop()
EndIf

While !Eof()
	
	If lMdtps .or. lSigaMdtps
		If TRBC->CLIENTE != cCodcli
			Exit
		EndIf
	EndIf
	
	If lFirst
		lFirst := .f.
		Somalinha()
		@ Li,000 PSay "|"
		@ Li,121 PSay "|"
		
		Somalinha()
		@ Li,000 PSay "|"
		@ Li,038 PSay STR0032  //"(*) RESUMO DAS DOENCAS ENCONTRADAS POR SETORES"
		@ Li,121 PSay "|"
	Endif
	
	Somalinha()
	@ Li,000 PSay "|"
	@ Li,121 PSay "|"
	
	Somalinha()
	
	Dbselectarea("TMR")
	Dbsetorder(1)
	Dbseek(xFilial("TMR")+TRBC->CODCID)
	@ Li,000 PSay "|"
	@ Li,002 PSay STR0013 //"Doenca..:"
	@ Li,012 PSay Alltrim(TRBC->CODCID) + " - " + Substr(TMR->TMR_DOENCA,1,100)
	@ Li,121 PSay "|"
	
	Somalinha()
	@ Li,000 PSay "|"
	@ Li,121 PSay "|"
	
	Somalinha()
	@ Li,000 PSay "|"
	@ li,002 psay STR0014  //"Centro de Custo          Nome                       Ocorrencias"
	@ Li,121 PSay "|"
	cCid :=  TRBC->CODCID
	
	While !eof() .and. cCid = TRBC->CODCID
		Dbselectarea(cAliasCC)
		Dbsetorder(1)
		Dbseek(xFilial(cAliasCC,cFilAtual)+TRBC->CCUSTO)
		
		Somalinha()
		@ Li,000 PSay "|"
		@ Li,002 PSay TRBC->CCUSTO
		@ Li,027 PSay &(cDescrCC)
		@ Li,058 PSay TRBC->QTOCOR Picture "999999"
		@ Li,122 PSay "|"
		
		DbSelectArea("TRBC")
		DbSkip()
	enddo
Enddo
SomaLinha()
@ Li,000 PSay "|"
@ Li,001 PSay Replicate("_",120)
@ Li,121 PSay "|"

Return .t.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o | _MDT825SETOR³ Autor³Andre Perez Alvarez    ³ Data  ³ 29/02/08³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Imprime o Resumo dos Acidentes Com Vitimas                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTR825                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/   
Static Function _MDT825SETOR()

Local nX
 
Somalinha()
@ Li,000 PSay "|"
@ Li,121 PSay "|"

Somalinha()
@ Li,000 PSay "|"
@ Li,038 PSay STR0032  //"(*) RESUMO DAS DOENCAS ENCONTRADAS POR SETORES"
@ Li,121 PSay "|"     

Somalinha()
@ Li,000 PSay "|"
@ Li,121 PSay "|"

Somalinha()

@ Li,000 PSay "|"
@ Li,002 PSay STR0013 //"Doenca..:"
@ Li,121 PSay "|"    

Somalinha()
@ Li,000 PSay "|"
@ Li,121 PSay "|"

Somalinha()
@ Li,000 PSay "|"
@ li,002 psay STR0014  //"Centro de Custo          Nome                       Ocorrencias"
@ Li,121 PSay "|"  

For nX := 1 To 4
	Somalinha()  
	@ Li,000 PSay "|"
	@ Li,121 PSay "|" 		
Next nX

SomaLinha()
@ Li,000 PSay "|"
@ Li,001 PSay Replicate("_",120)
@ Li,121 PSay "|"

Return .t.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³Denis Hyroshi de Souza ³ Data ³ 12/07/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaLinha()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTR825                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Somalinha()
    Li++
    If Li > 58 
        lPRINT := .t.
        Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
    EndIf
Return    

 
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   | MDT825QUAD ³ Autor³Liber de Esteban       ³ Data  ³ 12/07/02³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Imprime o quadro com dados da empresa                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTR825                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/   

Static Function MDT825QUAD()

If lMdtps .or. lSigaMdtps // se a empresa for prestadora de serviços
	dbSelectArea("SA1")
	dbSetOrder(01)
	dbSeek(xFilial("SA1",cFilAtual) + cCodcli)
	cNome := SA1->A1_NOME
	cEndereco := SA1->A1_END
	cCidade := SA1->A1_MUN
	If empty(SA1->A1_DDD)
		cTelefone := SA1->A1_TEL
	Else
		cTelefone := "(" + Alltrim(SA1->A1_DDD) + ")" + Alltrim(SA1->A1_TEL)
	EndIf
Else
	dbSelectArea("SM0")
	dbSeek(cEmpAnt+cFilAtual)
	cNome		:= SM0->M0_NOMECOM
	cEndereco	:= SM0->M0_ENDCOB
	cCidade		:= SM0->M0_CIDCOB
	cTelefone	:= SM0->M0_TEL
EndIf

Somalinha()  
@ Li,000 PSay "|"
@ Li,002 PSay STR0033  //"DADOS DA EMPRESA"
@ Li,121 PSay "|" 	

Somalinha()  
@ Li,000 PSay "|"
@ Li,121 PSay "|" 	

Somalinha()  
@ Li,000 PSay "|"
@ Li,002 PSay STR0034 + Substr(cNome,1,47)  //"EMPRESA.: "
@ Li,060 PSay STR0035 + cCidade  //"CIDADE..: "
@ Li,121 PSay "|" 	

Somalinha()  
@ Li,000 PSay "|"
@ Li,002 PSay Substr(STR0036 + cEndereco,1,56)  //"ENDERECO: "
@ Li,060 PSay STR0037 + cTelefone  //"TELEFONE: "
@ Li,121 PSay "|" 	   

If lImpTotEmp
	Somalinha()
	@ Li,000 PSay "|"
	If ( nQtTotFunc - Int(nQtTotFunc) ) == 0
		@ Li,002 PSay STR0040 + Alltrim(Str(nQtTotFunc,10)) //"TOTAL DE EMPREGADOS: "
	Else
		@ Li,002 PSay STR0040 + Alltrim( TransForm(nQtTotFunc,"@E 999,999.9") ) //"TOTAL DE EMPREGADOS: "
	Endif
	@ Li,121 PSay "|"
Endif

Somalinha()
@ Li,000 PSay "|"
@ Li,001 PSay Replicate("_",120)
@ Li,121 PSay "|"

li := 60

Return .t.