#INCLUDE "MNTR280.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR280  ³ Autor ³ Deivys Joenck         ³ Data ³ 09/10/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consumo Medio da Frota                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR280()
	Local WNREL      := "MNTR280"
	Local LIMITE     := 132 
	Local cDESC1     := STR0001 //"Relatorio de acompanhamento e analise da media de consumo dos veiculos da frota"
	Local cDESC2     := STR0002 //"O usuario podera selecionar os bens de um determinado Centro de Custo, e Familia,"
	Local cDESC3     := STR0003 //"de um periodo de tempo.O produto deve ser selecionado atraves dos Parametros."
	Local cSTRING    := "ST9"

	Private NOMEPROG := "MNTR280"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1}    //"Zebrado"###"Administracao"
	Private TITULO   := STR0001 //"Relatorio de Acompanhamento e Analise da Media de Consumo dos Veiculos da Frota"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
	SetKey( VK_F9, { | | NGVersao( "MNTR280" , 2 ) } )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as perguntas selecionadas                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros!                        ³
	//³ mv_par01     // Produto                                      ³
	//³ mv_par02     // De  Servico                                  ³
	//³ mv_par03     // Ate Servico                                  ³
	//³ mv_par04     // De  Centro Custo                             ³
	//³ mv_par05     // Ate Centro Custo                             ³
	//³ mv_par06     // De  Centro Trabalho                          ³
	//³ mv_par07     // Ate Centro Trabalho                          ³
	//³ mv_par08     // De  Familia                                  ³
	//³ mv_par09     // Ate Familia                                  ³
	//³ mv_par10     // De  Frota                                    ³
	//³ mv_par11     // Ate Frota                                    ³
	//³ mv_par12     // De  Data                                     ³
	//³ mv_par13     // Ate Data                                     ³
	//³ mv_par14     // Tratamento de media (Contador/Cons.,         ³
	//³                 Consumo/Contador                             ³  
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cPERG := "MNT280"
	Pergunte(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("ST9")
		Return
	EndIf     
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND| MNTR280IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO) 

	DbSelectArea("ST9")
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR280IMP³ Autor ³ Deivys Joenck         ³ Data ³ 09/10/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR280                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR280IMP(lEND,WNREL,TITULO,TAMANHO)
	
	Local cRODATXT := ""
	Local oTempTable	  //Tabela Temporaria
	Local nCNTIMPR := 0
	Local cCONDST9 :=  'ST9->T9_TEMCONT <> "N" .And. ';
	+'ST9->T9_CODBEM >= MV_PAR10 .And. ST9->T9_CODBEM <= MV_PAR11'

	Local cCONDSTS :=  'STS->TS_SITUACA = "L" .And. ';
	+'STS->TS_SERVICO >= MV_PAR02 .And. STS->TS_SERVICO <= MV_PAR03 .And. ';
	+'STS->TS_CCUSTO >= MV_PAR04 .And. STS->TS_CCUSTO <= MV_PAR05 .And. ';
	+'STS->TS_CENTRAB >= MV_PAR06 .And. STS->TS_CENTRAB <= MV_PAR07 .And. ';
	+'STS->TS_TIPOOS = "B"'

	Local cCONDSTJ :=  'STJ->TJ_SITUACA = "L" .And. ';
	+'STJ->TJ_SERVICO >= MV_PAR02 .And. STJ->TJ_SERVICO <= MV_PAR03 .And. ';
	+'STJ->TJ_CCUSTO >= MV_PAR04 .And. STJ->TJ_CCUSTO <= MV_PAR05 .And. ';
	+'STJ->TJ_CENTRAB >= MV_PAR06 .And. STJ->TJ_CENTRAB <= MV_PAR07 .And. ';
	+'STJ->TJ_TIPOOS = "B"'

	Local nTOTREGS := 0,nMULT := 1,nPOSANT := 4,nPOSATU := 4,nPOSCNT := 0
	Private li := 80 ,m_pag := 1,nDifSum := 0,nCONTACUM1 := 0,nCONTACUM2 := 0,nCONT2AC1 := 0,nCONT2AC2 := 0
	nTIPO  := IIF(aRETURN[4]==1,15,18)
	
	//Alias da Tabela Temporaria
	Private cTRB := GetNextAlias()
	
	//Monta os Cabecalhos                                          ³

	CABEC1 := STR0006+"                                             ----Segundo Contador----" //"                                                      --Abastec.Inicial--   --Abastec. Final--"

	If MV_PAR15 = 1
		CABEC2 := STR0007+"       Total       Media" //"             Veiculo           Descricao                        Data           Contador        Acumulado   Data           Contador        Acumulado         Total         Consumo  Un.        Media  "
	Else
		CABEC2 := STR0017+"       Total       Media" //"Veiculo           Descricao                                     Data           Contador        Acumulado   Data           Contador        Acumulado         Total         Consumo  Un.        Media  "
	EndIf
	/*/
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5          6         7         8         9        20        21        22
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789001234567890123456789012345678901234567890123456789012345678901234567890
	******************************************************************************************************************************************************************************************************************************
	-------------Abastec.Inicial------------   --------------Abastec. Final------------                                                  ----Segundo Contador----
	Veiculo           Descricao                        Data           Contador        Acumulado   Data           Contador        Acumulado         Total         Consumo  Un.        Media         Total       Media
	******************************************************************************************************************************************************************************************************************************
	Centro de Custo..: xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx     
	Centro de Trabalho..: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
	Familia..: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
	xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   99/99/9999  999,999,999  999,999,999,999   99/99/9999  999,999,999  999,999,999,999   9999,999,999    9999,999.99  xxx   999,999.99  9999,999,999  999,999.99 
	xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   99/99/9999  999,999,999  999,999,999,999   99/99/9999  999,999,999  999,999,999,999   9999,999,999    9999,999.99  xxx   999,999.99  
	xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   99/99/9999  999,999,999  999,999,999,999   99/99/9999  999,999,999  999,999,999,999   9999,999,999    9999,999.99  xxx   999,999.99
	TOTAL DA FAMILIA..:                                                             xx,999,999,999  xx,999,999.99       9999,999.99
	TOTAL DO CENTRO DE TRABALHO..:                                                  xx,999,999,999  xx,999,999.99       9999,999.99
	TOTAL DO CENTRO DE CUSTO..:                                                     xx,999,999,999  xx,999,999.99       9999,999.99

	TOTAL GERAL..:                                                                 xxx,999,999,999 xxx,999,999.99      99999,999.99
	/*/

	DbSelectArea("ST9")

	aDBF := {{"CUSTO"  ,"C",Len(st9->t9_ccusto),0},;
				{"CENTRAB","C",06,0},;
				{"FAMILIA","C",06,0},;
				{"CODBEM" ,"C",16,0},;
				{"DATALAN","D",08,0},;
				{"QUANTID","N",09,2},;
				{"UNIDADE","C",03,0},;
				{"HORA"   ,"C",05,0}}
	
	//Intancia classe FWTemporaryTable
	oTempTable  := FWTemporaryTable():New( cTRB, aDBF )
	If MV_PAR15 = 1
        //Cria indices
		oTempTable:AddIndex( "Ind01" , {"CUSTO","CENTRAB","FAMILIA","CODBEM","DATALAN","HORA"} )
	Else
        //Cria indices
		oTempTable:AddIndex( "Ind01" , {"CODBEM","DATALAN","HORA"} )
  	EndIf            
	//Cria a tabela temporaria
	oTempTable:Create()

	DbSelectArea("ST9")
	DbSetOrder(4)
	DbSeek(xFILIAL("ST9")+MV_PAR08,.T.)
	SetRegua(LastRec())
	
	Do While !Eof() .And. ST9->T9_FILIAL == xFILIAL("ST9") .And.;
		ST9->T9_CODFAMI <= MV_PAR09
	
			IncRegua()
			If lEND
				@ PROW()+1,001 Psay STR0008 //"CANCELADO PELO OPERADOR"
				Exit
			EndIf
			If &(cCONDST9)
	
				DbSelectArea("STS")
				DbSetOrder(2)
				DbSeek(xFILIAL("STS")+"B"+ST9->T9_CODBEM)
				Do While !Eof() .And. STS->TS_FILIAL == xFILIAL("STS") .And.;
				STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM == ST9->T9_CODBEM
	
					If &(cCONDSTS)
	
						DbSelectArea("STT")
						DbSetOrder(2)
						DbSeek(xFILIAL("STT")+STS->TS_ORDEM+STS->TS_PLANO+'P'+MV_PAR01)
						Do While !Eof()                       .And.;
						STT->TT_FILIAL  == xFILIAL("STT")  .And.;
						STT->TT_ORDEM   == STS->TS_ORDEM   .And.;
						STT->TT_PLANO   == STS->TS_PLANO   .And.;
						STT->TT_TIPOREG == 'P'             .And.;
						STT->TT_CODIGO  == MV_PAR01       
	
							If Alltrim(STT->TT_SEQRELA) <> "0" .And. STT->TT_DTINICI >= MV_PAR12 .And. STT->TT_DTINICI <= MV_PAR13
								GRAVATRB(STS->TS_CCUSTO,STS->TS_CENTRAB,ST9->T9_CODBEM,ST9->T9_CODFAMI,;
								STT->TT_QUANTID,STT->TT_DTINICI,STT->TT_UNIDADE,STT->TT_HOINICI)
							EndIf
							DbSelectArea("STT")
							DbSkip()
						EndDo         
					EndIf   
					DbSelectArea("STS")
					DbSkip()         
				EndDo

			DbSelectArea("STJ")
			DbSetOrder(2)
			DbSeek(xFILIAL("STJ")+"B"+ST9->T9_CODBEM)
			Do While !Eof() .And. STJ->TJ_FILIAL == xFILIAL("STJ") .And.;
			STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM == ST9->T9_CODBEM

				If &(cCONDSTJ)

					DbSelectArea("STL")
					DbSetOrder(4)
					DbSeek(xFILIAL("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO+'P'+MV_PAR01)
					Do While !Eof()                       .And.;
					STL->TL_FILIAL  == xFILIAL("STL")  .And.;
					STL->TL_ORDEM   == STJ->TJ_ORDEM   .And.;
					STL->TL_PLANO   == STJ->TJ_PLANO   .And.;
					STL->TL_TIPOREG == 'P'             .And.; 
					STL->TL_CODIGO  == MV_PAR01

						If Alltrim(STL->TL_SEQRELA) <> "0" .And. STL->TL_DTINICI >= MV_PAR12 .And. STL->TL_DTINICI <= MV_PAR13
							GRAVATRB(STJ->TJ_CCUSTO,STJ->TJ_CENTRAB,ST9->T9_CODBEM,ST9->T9_CODFAMI,;
							STL->TL_QUANTID,STL->TL_DTINICI,STL->TL_UNIDADE,STL->TL_HOINICI)
						EndIf
						DbSelectArea("STL")
						DbSkip()
					EndDo         
				EndIf      
				DbSelectArea("STJ")
				DbSkip()         
			EndDo 
		EndIf   
		DbSelectArea("ST9")
		DbSkip()
	EndDo         

	DbSelectArea(cTRB)
	DbGotop()
	nCONTFAM := 0 
	nCONTCEN := 0 
	nCONTCUS := 0 
	nTOTTGER := 0.00
	nTOTCGER := 0.00
	nTOTMGER := 0.00

	//IMPRESSAO ANALITICO
	Do While !Eof() .And. MV_PAR15 = 1
		cCUSTO   := (cTRB)->CUSTO
		nTOTTCUS := 0.00
		nTOTCCUS := 0.00
		nTOTBEM  := 0

		NGSOMALI(58)
		@ LI,000 Psay STR0009 //"Centro de Custo..: "
		@ LI,019 Psay (cTRB)->CUSTO
		@ LI,040 Psay NGSEEK('SI3',(cTRB)->CUSTO,1,'SI3->I3_DESC')

		DbSelectArea(cTRB)
		Do While !Eof() .AND. (cTRB)->CUSTO == cCUSTO

			cCENTRAB  := (cTRB)->CENTRAB
			nTOTTCEN  := 0.00
			nTOTCCEN  := 0.00

			NGSOMALI(58)
			@ LI,005 Psay STR0010 //"Centro de Trabalho..: "
			@ LI,027 Psay (cTRB)->CENTRAB
			@ LI,035 Psay NGSEEK('SHB',(cTRB)->CENTRAB,1,'SHB->HB_NOME')

			DbSelectArea(cTRB)
			Do While !Eof()               .And.;
			(cTRB)->CUSTO   == cCUSTO     .And.;  
			(cTRB)->CENTRAB == cCENTRAB

				cCODFAMI := (cTRB)->FAMILIA
				nTOTTFAM := 0.00
				nTOTCFAM := 0.00
				nTOTMFAM := 0.00

				NGSOMALI(58)
				@ LI,010 Psay STR0011
				@ LI,021 Psay (cTRB)->FAMILIA
				@ LI,029 Psay NGSEEK('ST6',(cTRB)->FAMILIA,1,'ST6->T6_NOME')         

				DbSelectArea(cTRB)
				Do While !Eof()             .And.;
				(cTRB)->CUSTO   == cCUSTO   .And.;  
				(cTRB)->CENTRAB == cCENTRAB .And.;
				(cTRB)->FAMILIA == cCODFAMI

					cCODBEM  := (cTRB)->CODBEM
					nTOTBEM  := 0
					dDATAINI := (cTRB)->DATALAN
					dDATAFIM := (cTRB)->DATALAN
					cHORAINI := (cTRB)->HORA 
					cHORAFIM := (cTRB)->HORA

					//Retorna Contador e Acumulado anterior
					nCONTANT   := NGACUMEHIS(cCODBEM,dDATAINI,cHORAINI,1,"A")
					nCONT1     := nCONTANT[1] //Contador
					nCONTACUM1 := nCONTANT[2] //Acumulado

					//Retorna Segundo Contador e Acumulado anterior
					nCONT2ANT   := NGACUMEHIS(cCODBEM,dDATAINI,cHORAINI,2,"A")
					nCONT21     := nCONT2ANT[1] //Contador
					nCONT2AC1 := nCONT2ANT[2] //Acumulado

					nDifSum := 0
					If nCONTACUM1 = 0
						nDifSum := (cTRB)->QUANTID
					EndIf
					cUnidade := (cTRB)->UNIDADE

					DbSelectArea(cTRB)
					Do While !Eof()              .And.;
					(cTRB)->CUSTO   == cCUSTO    .And.;
					(cTRB)->CENTRAB == cCENTRAB  .And.;
					(cTRB)->FAMILIA == cCODFAMI  .And.;
					(cTRB)->CODBEM  == cCODBEM 

						nTOTBEM  := nTOTBEM + (cTRB)->QUANTID
						dDATAFIM := (cTRB)->DATALAN
						cHORAFIM := (cTRB)->HORA
						DbSkip()
					End   
					If nDifSum > 0
						nTotBem -= nDifSum       
					EndIf	

					If nCONTACUM1 = 0
						aCONTEXATO := NGACUMHIST(cCODBEM,dDATAINI,cHORAINI,1)
						nCONT1     := aCONTEXATO[1] //Contador
						nCONTACUM1 := aCONTEXATO[2] //Acumulado
					Else
						dDATAINI   := nCONTANT[3] //Data do contador anterior
					EndIf	
					aCONTEXATO := NGACUMHIST(cCODBEM,dDATAFIM,cHORAFIM,1)
					nCONT2     := aCONTEXATO[1] //Contador
					nCONTACUM2 := aCONTEXATO[2] //Acumulado

					//segundo contador
					If nCONT2AC1 = 0
						aCONTEXATO2 := NGACUMHIST(cCODBEM,dDATAINI,cHORAINI,2)
						nCONT21     := aCONTEXATO2[1] //Contador
						nCONT2AC1 := aCONTEXATO2[2] //Acumulado
					Else
						dDATAINI   := nCONT2ANT[3] //Data do contador anterior
					EndIf	
					aCONTEXATO2 := NGACUMHIST(cCODBEM,dDATAFIM,cHORAFIM,2)
					nCONT22     := aCONTEXATO2[1] //Contador
					nCONT2AC2 := aCONTEXATO2[2] //Acumulado

					NGSOMALI(58)
					@ LI,013 Psay cCODBEM
					@ LI,031 Psay Substr(NGSEEK('ST9',cCODBEM,1,'ST9->T9_NOME'),1,30)
					@ LI,064 Psay dDATAINI   Picture "99/99/9999"
					@ LI,076 Psay nCONT1     Picture '@E 999,999,999'
					@ LI,089 Psay nCONTACUM1 Picture '@E 999,999,999,999' 
					@ LI,107 Psay dDATAFIM   Picture "99/99/9999"
					@ LI,119 Psay nCONT2     Picture '@E 999,999,999'
					@ LI,132 Psay nCONTACUM2 Picture '@E 999,999,999,999' 
					nCONTAD := nCONTACUM2 - nCONTACUM1
					@ LI,150 Psay nCONTAD   Picture '@E 999,999,999'
					@ LI,167 Psay nTOTBEM   Picture '@E 999,999.99'
					@ LI,179 Psay cUnidade   
					nCONTAD2 := nCONT2AC2 - nCONT2AC1
					If MV_PAR14 = 1
						@ LI,185 Psay nCONTAD/nTOTBEM Picture '@E 999,999.99'
						@ LI,198 Psay nCONTAD2   Picture '@E 999,999,999'
						@ LI,211 Psay nCONTAD2/nTOTBEM Picture '@E 999,999.99'
					Else
						@ LI,185 Psay nTOTBEM/nCONTAD Picture '@E 999,999.99' 
						@ LI,198 Psay nCONTAD2   Picture '@E 999,999,999'
						@ LI,211 Psay nTOTBEM/nCONTAD2 Picture '@E 999,999.99'
					EndIf      
					nTOTTFAM := nTOTTFAM+nCONTAD                        
					nTOTCFAM := nTOTCFAM+nTOTBEM
					nCONTFAM := nCONTFAM+1
					DbSelectArea(cTRB)
				EndDo
				NGSOMALI(58)
				@ LI,064 Psay STR0012
				@ LI,147 Psay nTOTTFAM Picture "@E 99,999,999,999"
				@ LI,164 Psay nTOTCFAM Picture "@E 99,999,999.99"
				If MV_PAR14 = 1
					@ LI,184 Psay nTOTTFAM/nTOTCFAM Picture "@E 9999,999.99"
				Else
					@ LI,184 Psay nTOTCFAM/nTOTTFAM Picture "@E 9999,999.99"
				EndIf   
				nTOTTCEN := nTOTTCEN+nTOTTFAM
				nTOTCCEN := nTOTCCEN+nTOTCFAM
				nCONTCEN := nCONTCEN+1
			EndDo
			NGSOMALI(58)
			@ LI,064 Psay STR0014
			@ LI,147 Psay nTOTTCEN Picture "@E 99,999,999,999"
			@ LI,164 Psay nTOTCCEN Picture "@E 99,999,999.99"
			If MV_PAR14 = 1
				@ LI,184 Psay nTOTTCEN/nTOTCCEN Picture "@E 9999,999.99"
			Else
				@ LI,184 Psay nTOTCCEN/nTOTTCEN Picture "@E 9999,999.99" 
			EndIf  
			nTOTTCUS := nTOTTCUS+nTOTTCEN
			nTOTCCUS := nTOTCCUS+nTOTCCEN
			nCONTCUS := nCONTCUS+1                                                                      
		EndDo
		NGSOMALI(58)
		@ LI,064 Psay STR0013
		@ LI,147 Psay nTOTTCUS Picture "@E 99,999,999,999"
		@ LI,164 Psay nTOTCCUS Picture "@E 99,999,999.99"
		If MV_PAR14 = 1
			@ LI,184 Psay nTOTTCUS/nTOTCCUS Picture "@E 9999,999.99"
		Else
			@ LI,184 Psay nTOTCCUS/nTOTTCUS Picture "@E 9999,999.99" 
		EndIf   
		nTOTTGER := nTOTTGER+nTOTTCUS
		nTOTCGER := nTOTCGER+nTOTCCUS 
		NGSOMALI(58)
		NGSOMALI(58)
	EndDo 
	If nTOTTGER != 0.00
		@ LI,064 Psay STR0015
		@ LI,146 Psay nTOTTGER Picture "@E 999,999,999,999"
		@ LI,163 Psay nTOTCGER Picture "@E 999,999,999.99"
		If MV_PAR14 = 1
			@ LI,183 Psay nTOTTGER/nTOTCGER Picture "@E 99999,999.99"
		Else
			@ LI,183 Psay nTOTCGER/nTOTTGER Picture "@E 99999,999.99"
		EndIf      
	EndIf

	//IMPRESSAO SINTETICA
	Do While !Eof() .And. MV_PAR15 = 2
		nCONTAD := 0
		nTOTBEM := 0
		nCONTAD2:= 0

		cCUSTO  	:= (cTRB)->CUSTO
		cCENTRAB  	:= (cTRB)->CENTRAB
		cCODFAMI 	:= (cTRB)->FAMILIA

		cCODBEM  := (cTRB)->CODBEM
		nTOTBEM  := 0
		dDATAINI := (cTRB)->DATALAN
		dDATAFIM := (cTRB)->DATALAN
		cHORAINI := (cTRB)->HORA 
		cHORAFIM := (cTRB)->HORA

		//Retorna Contador e Acumulado anterior
		nCONTANT   := NGACUMEHIS(cCODBEM,dDATAINI,cHORAINI,1,"A")
		nCONT1     := nCONTANT[1] //Contador
		nCONTACUM1 := nCONTANT[2] //Acumulado                    

		//Retorna Segundo Contador e Acumulado anterior
		nCONT2ANT   := NGACUMEHIS(cCODBEM,dDATAINI,cHORAINI,2,"A")
		nCONT21     := nCONT2ANT[1] //Contador
		nCONT2AC1 := nCONT2ANT[2] //Acumulado

		nDifSum := 0
		If nCONTACUM1 = 0
			nDifSum := (cTRB)->QUANTID
		EndIf
		cUnidade := (cTRB)->UNIDADE

		dDATAFIM := (cTRB)->DATALAN
		cHORAFIM := (cTRB)->HORA

		DbSelectArea(cTRB)
		Do While !Eof() .And. (cTRB)->CODBEM  == cCODBEM 
			nTOTBEM  := nTOTBEM + (cTRB)->QUANTID
			dDATAFIM := (cTRB)->DATALAN
			cHORAFIM := (cTRB)->HORA
			DbSkip()
		End   

		If nDifSum > 0
			nTotBem -= nDifSum       
		EndIf	

		If nCONTACUM1 = 0
			aCONTEXATO := NGACUMHIST(cCODBEM,dDATAINI,cHORAINI,1)
			nCONT1     := aCONTEXATO[1] //Contador
			nCONTACUM1 := aCONTEXATO[2] //Acumulado
		Else
			dDATAINI   := nCONTANT[3] //Data do contador anterior
		EndIf	
		aCONTEXATO := NGACUMHIST(cCODBEM,dDATAFIM,cHORAFIM,1)
		nCONT2     := aCONTEXATO[1] //Contador
		nCONTACUM2 := aCONTEXATO[2] //Acumulado

		//segundo contador
		If nCONT2AC1 = 0
			aCONTEXATO2 := NGACUMHIST(cCODBEM,dDATAINI,cHORAINI,2)
			nCONT21     := aCONTEXATO2[1] //Contador
			nCONT2AC1 := aCONTEXATO2[2] //Acumulado
		Else
			dDATAINI   := nCONT2ANT[3] //Data do contador anterior
		EndIf	
		aCONTEXATO2 := NGACUMHIST(cCODBEM,dDATAFIM,cHORAFIM,2)
		nCONT22     := aCONTEXATO2[1] //Contador
		nCONT2AC2 := aCONTEXATO2[2] //Acumulado

		NGSOMALI(58)
		@ LI,000 Psay cCODBEM
		@ LI,018 Psay Substr(NGSEEK('ST9',cCODBEM,1,'ST9->T9_NOME'),1,40)
		@ LI,064 Psay dDATAINI   Picture "99/99/9999"
		@ LI,076 Psay nCONT1     Picture '@E 999,999,999'
		@ LI,089 Psay nCONTACUM1 Picture '@E 999,999,999,999' 
		@ LI,107 Psay dDATAFIM   Picture "99/99/9999"
		@ LI,119 Psay nCONT2     Picture '@E 999,999,999'
		@ LI,132 Psay nCONTACUM2 Picture '@E 999,999,999,999' 
		nCONTAD := nCONTACUM2 - nCONTACUM1
		@ LI,150 Psay nCONTAD   Picture '@E 999,999,999'
		@ LI,167 Psay nTOTBEM   Picture '@E 999,999.99'
		@ LI,179 Psay cUnidade  
		nCONTAD2 := nCONT2AC2 - nCONT2AC1
		If MV_PAR14 = 1
			@ LI,185 Psay nCONTAD/nTOTBEM Picture '@E 999,999.99'
			@ LI,198 Psay nCONTAD2   Picture '@E 999,999,999'
			@ LI,211 Psay nCONTAD2/nTOTBEM Picture '@E 999,999.99'
		Else
			@ LI,185 Psay nTOTBEM/nCONTAD Picture '@E 999,999.99'
			@ LI,198 Psay nCONTAD2   Picture '@E 999,999,999'
			@ LI,211 Psay nTOTBEM/nCONTAD2 Picture '@E 999,999.99'
		EndIf      

		DbSelectArea(cTRB)

		nTOTTGER := nTOTTGER+nCONTAD
		nTOTCGER := nTOTCGER+nTOTBEM 
	EndDo 

	If nTOTTGER != 0.00 .And. MV_PAR15 = 2
		NGSOMALI(58)
		NGSOMALI(58)
		@ LI,064 Psay STR0015
		@ LI,146 Psay nTOTTGER Picture "@E 999,999,999,999"
		@ LI,163 Psay nTOTCGER Picture "@E 999,999,999.99"

		If MV_PAR14 = 1
			@ LI,183 Psay nTOTTGER/nTOTCGER Picture "@E 99999,999.99"
		Else
			@ LI,183 Psay nTOTCGER/nTOTTGER Picture "@E 99999,999.99"
		EndIf      
	EndIf

	Roda(nCNTIMPR,cRODATXT,TAMANHO)
	Set Device To Screen
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

	oTempTable:Delete() //Deleta tabela temporária

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GRAVATRB ³ Autor ³ Deivys Joenck         ³ Data ³ 09/10/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava o arquivo temporario.                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR280                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GRAVATRB(cCUSTO,cCENTRAB,cCODBEM,cFAMILIA,nQUANTID,dDTLANCA,cUNIDADE,cHORA)
	(cTRB)->(DbAppend())
	(cTRB)->CUSTO   := cCUSTO
	(cTRB)->CENTRAB := cCENTRAB
	(cTRB)->CODBEM  := cCODBEM
	(cTRB)->FAMILIA := cFAMILIA
	(cTRB)->DATALAN := dDTLANCA
	(cTRB)->QUANTID := nQUANTID 
	(cTRB)->UNIDADE := cUNIDADE
	(cTRB)->HORA    := cHORA
Return .T.