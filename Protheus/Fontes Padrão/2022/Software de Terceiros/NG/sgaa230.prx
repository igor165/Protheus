#INCLUDE "SGAA230.ch"
#INCLUDE "Protheus.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} SGAA230
Programa para cadastro da Politica Ambiental

@author  Rafael Diogo Richter
@since   18/11/2004

@return  boolean, Sempre verdadeiro
/*/
//-------------------------------------------------------------------
Function SGAA230()

	// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  ³
	Local aNGBEGINPRM := NGBEGINPRM()
	local cNewTitle := ""

	Private aRotina := MenuDef()
	Private cCadastro := OemtoAnsi(STR0008)//"Política Ambiental"
	Private lFlag := NGCADICBASE( "TAU_FLAG" , "A" , "TAU" , .F. )

	If Amiin(56)	 //Verifica se o usuário possui licença para acessar a rotina.

		If !NGCADICBASE("TAU_MISSAO","D","TAU",.F.)
			If !NGINCOMPDIC("UPDSGA05","00002903-07")
				Return .F.
			EndIf
		EndIf

		If ExistBlock("SGAA230A") //Ponto de entrada para alteração de título da rotina Politica Ambiental
			cNewTitle:= ExecBlock("SGAA230A",.F.,.F.)
			If ValType(cNewTitle) == "C" .AND. !Empty(cNewTitle)
				cCadastro := cNewTitle
			EndIf
		EndIf
		
		// Endereca a funcao de BROWSE
		dbSelectArea("TAU")
		dbSetOrder(1)
		mBrowse( 6, 1,22,75,"TAU" )

		EndIf
	
	// Devolve variaveis armazenadas (NGRIGHTCLICK)
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de Menu Funcional.

@return aRotina  - 	Array com as opções de menu.
					Parametros do array a Rotina:
					1. Nome a aparecer no cabecalho
					2. Nome da Rotina associada
					3. Reservado
					4. Tipo de Transa‡„o a ser efetuada:
						1 - Pesquisa e Posiciona em um Banco de Dados
						2 - Simplesmente Mostra os Campos
						3 - Inclui registros no Bancos de Dados
						4 - Altera o registro corrente
						5 - Remove o registro corrente do Banco de Dados
					5. Nivel de acesso
					6. Habilita Menu Funcional

@sample MenuDef()

@author Rafael Diogo Richter
@since 29/11/2006
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

	Local aRotina := { 	{ STR0001, "AxPesqui",   0, 1 },;//"Pesquisar"
						{ STR0002, "SG230Cad",   0, 2 },;//"Visualizar"
						{ STR0003, "SG230Cad",   0, 3 },;//"Incluir"
						{ STR0004, "SG230Cad",   0, 4 },;//"Alterar"
						{ STR0005, "SG230Cad",   0, 5, 3 },;//"Excluir"
						{ STR0006, "SGAR060",    0, 6 },;//"Imprimir"
						{ STR0010, "MsDocument", 0, 4 } }//"Conhecimento"

Return aRotina

//-------------------------------------------------------------------
/*/{Protheus.doc} SG230Cad

Responsável por executar o CRUD da politica ambiental

@author  Rafael Diogo Richter
@since   07/05/2008

@sample  SG230Cad("TAU", , 3)

@param   cAlias, Caracter, Alias do registro que está sendo alterado
@param   nRecno, Numerico, Numero do registro da tabela sendo alterado
@param   nOpcx, Numerico, Opcao de menu sendo executada

@return  boolean, Sempre verdadeiro
/*/
//-------------------------------------------------------------------
Function SG230Cad(cAlias,nRecno,nOpcx)
	
	Local oDlg, oMenu, oFolder
	Local nOpca := 0
	Local aPages:= {}, aTitles:= {}
	Local aSize := MsAdvSize(,.f.,430)
	Local aItens	:= StrTokArr( NGSEEKDIC( "SX3" , "TAU_FLAG" , 2 , "X3CBox()" ) , ";" )
	Local aItens2	:= StrTokArr( NGSEEKDIC( "SX3" , "TAU_TIPO" , 2 , "X3CBox()" ) , ";" )
	Local dDtVige := CTOD("  /  /  "), aAreaTAU := GetArea()
	Local oPanel, oPnlCampos, oPnlMemo, oPnlMemo2, oPnlMemo3, oPnlMemo4
	Local oFontB := TFont():New("Arial",,-12,,.T.)

	Private cPolitica := Space(Len(TAU->TAU_CODPOL))
	Private dDtVigenc := CTOD("  /  /  ")
	Private cModCria  := If( nModulo == 35 , "2" , If( nModulo == 56 , "1" , "3" ) )
	Private cTipPol	  := ""
	Private oMemoDes, cMemoDes := "", oMemoMis, cMemoMis := "", oMemoVis, cMemoVis := "", oMemoVal, cMemoVal := ""

	//Nao permite alterar politicas anteriores a atual
	If nOpcx == 4
		dDtVige := TAU->TAU_DTVIGE
		dbSelectArea("TAU")
		dbSetOrder(1)
		dbGoTop()
		While !Eof() .And. xFilial("TAU") == TAU->TAU_FILIAL
			If TAU->TAU_DTVIGE > dDtVige
				nOpcx := 2
				Exit
			EndIf
			dbSelectArea("TAU")
			dbSkip()
		End
		RestArea(aAreaTAU)
		dbSelectArea("TAU")
		dbGoTo(nRecno)
	EndIf

	Aadd(aTitles,OemToAnsi(STR0011)) //"Política"
	Aadd(aPages,"Header 1")
	Aadd(aTitles,OemToAnsi(STR0012)) //"Missão"
	Aadd(aPages,"Header 2")
	Aadd(aTitles,OemToAnsi(STR0013)) //"Visão"
	Aadd(aPages,"Header 3")
	Aadd(aTitles,OemToAnsi(STR0014)) //"Valores"
	Aadd(aPages,"Header 4")

	If nOpcx <> 3
		cPolitica	:= TAU->TAU_CODPOL
		dDtVigenc	:= TAU->TAU_DTVIGE
		If lFlag
			cModCria	:= TAU->TAU_FLAG
			cTipPol		:= TAU->TAU_TIPO
		EndIf
		If NGCADICBASE('TAU_MMSYP','A','TAU',.F.)
			cMemoDes 	:= MSMM(TAU->TAU_MMSYP,,,,3)
			cMemoMis 	:= MSMM(TAU->TAU_MMMISS,,,,3)
			cMemoVis 	:= MSMM(TAU->TAU_MMVISA,,,,3)
			cMemoVal 	:= MSMM(TAU->TAU_MMVALO,,,,3)
		Else
			cMemoDes 	:= TAU->TAU_DESC
			cMemoMis 	:= TAU->TAU_MISSAO
			cMemoVis 	:= TAU->TAU_VISAO
			cMemoVal 	:= TAU->TAU_VALORE
		EndIf
	EndIf

	Define MsDialog oDlg From aSize[7],0 To aSize[6],aSize[5] Title cCadastro Pixel //"Política Ambiental"

	oFolder := TFolder():New(7.5,0,aTitles,aPages,oDlg,,,,.f.,.f.,333,194,)
	oFolder:aDialogs[1]:oFont := oDlg:oFont
	oFolder:aDialogs[2]:oFont := oDlg:oFont
	oFolder:aDialogs[3]:oFont := oDlg:oFont
	oFolder:aDialogs[4]:oFont := oDlg:oFont
	oFolder:Align := CONTROL_ALIGN_ALLCLIENT

	oPnlCampos := TPanel():New(01,01,,oFolder:aDialogs[1],,,,,,1,50,.F.,.F.)
		oPnlCampos:Align := CONTROL_ALIGN_TOP

	oPanel := TPanel():New(00,00,,oFolder:aDialogs[1],,,,,RGB(67,70,87),10,10,.F.,.F.)
		oPanel:Align := CONTROL_ALIGN_TOP

	oPnlMemo := TPanel():New(500,500,,oFolder:aDialogs[1],,,,,,500,500,.F.,.F.)
		oPnlMemo:Align := CONTROL_ALIGN_ALLCLIENT

	oPnlMemo2 := TPanel():New(500,500,,oFolder:aDialogs[2],,,,,,500,500,.F.,.F.)
		oPnlMemo2:Align := CONTROL_ALIGN_ALLCLIENT

	oPnlMemo3 := TPanel():New(500,500,,oFolder:aDialogs[3],,,,,,500,500,.F.,.F.)
		oPnlMemo3:Align := CONTROL_ALIGN_ALLCLIENT

	oPnlMemo4 := TPanel():New(500,500,,oFolder:aDialogs[4],,,,,,500,500,.F.,.F.)
		oPnlMemo4:Align := CONTROL_ALIGN_ALLCLIENT

	//Folder 1
	@ 010,008   SAY OemToAnsi(STR0011) Color CLR_HBLUE Of oPnlCampos Pixel //"Política"
	@ 008,035   MSGET cPolitica Size 40,08 When If(nOpcx <> 3,.F.,.T.) VALID Sg230Val() Of oPnlCampos Pixel

	@ 010,260   SAY OemToAnsi(STR0016) Color CLR_HBLUE Of oPnlCampos Pixel //"Dt. Vigência"
	@ 008,295   MSGET dDtVigenc Size 60,08 When If(nOpcx == 2 .Or. nOpcx == 5,.F.,.T.) Of oPnlCampos Pixel HasButton

	If lFlag
		@ 024,008   SAY OemToAnsi(STR0023) Color CLR_HBLUE Of oPnlCampos Pixel //"Módulo"
		@ 022,035   COMBOBOX oCbx Var cModCria ITEMS aItens Valid MdtValTip( cModCria , "TAU" ) Size 60,08 When If(nOpcx == 2 .Or. nOpcx == 5,.F.,.T.) Of oPnlCampos Pixel

		@ 024,260   SAY OemToAnsi(STR0025) Of oPnlCampos Pixel //"Tipo"
		@ 022,295   COMBOBOX oCbx Var cTipPol ITEMS aItens2 Valid Pertence("1234") Size 60,08 When If(nOpcx == 2 .Or. nOpcx == 5,.F.,.T.) Of oPnlCampos Pixel
	EndIf

	@ 002, 005 SAY OemToAnsi(STR0017) Font oFontB Color CLR_WHITE Of oPanel Pixel
	@ 039 + If( lFlag , 10 , 0 ),018 Get oMemoDes Var cMemoDes Of oPnlMemo Memo Size aSize[1],aSize[2] Pixel
	oMemoDes:lReadOnly:= If(nOpcx == 2 .Or. nOpcx == 5,.T.,.F.)
	oMemoDes:Align := CONTROL_ALIGN_ALLCLIENT

	//Folder 2
	@ 023,018 Get oMemoMis Var cMemoMis Of oPnlMemo2 Memo Size aSize[1],aSize[2] Pixel
	oMemoMis:lReadOnly:= If(nOpcx == 2 .Or. nOpcx == 5,.T.,.F.)
	oMemoMis:Align := CONTROL_ALIGN_ALLCLIENT

	//Folder 3
	@ 023,018 Get oMemoVis Var cMemoVis Of oPnlMemo3 Memo Size aSize[1],aSize[2] Pixel
	oMemoVis:lReadOnly:= If(nOpcx == 2 .Or. nOpcx == 5,.T.,.F.)
	oMemoVis:Align := CONTROL_ALIGN_ALLCLIENT

	//Folder 4
	@ 023,018 Get oMemoVal Var cMemoVal Of oPnlMemo4 Memo Size aSize[1],aSize[2] Pixel
	oMemoVal:lReadOnly:= If(nOpcx == 2 .Or. nOpcx == 5,.T.,.F.)
	oMemoVal:Align := CONTROL_ALIGN_ALLCLIENT

	If Len(aSMenu) > 0
		NGPOPUP(aSMenu,@oMenu)
		oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
	EndIf

	Activate MsDialog oDlg On Init (EnchoiceBar(oDlg,{||nOpca:=1,If(!SG230TOK(nOpcx,nRecno), nOpca := 0, oDlg:End())},{|| nOpca := 0, oDlg:End()})) Centered

	If nOpca == 1 .and. nOpcx != 2
		Sg230Grava(nOpcx)
	EndIf

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} Sg230Grava

Funcao para validar e gravar as informacoes

@author  Rafael Diogo Richter
@since   08/05/2008

@sample  Sg230Grava(3)

@param   nOpcx, Numerico, Opcao de menu sendo executada

@return  boolean, Sempre verdadeiro
/*/
//-------------------------------------------------------------------
Static Function Sg230Grava(nOpcx)

	Local cMmSYP, cMmMis, cMmVis, cMmVal

	If nOpcx != 3
		If NGCADICBASE('TAU_MMSYP','A','TAU',.F.)
			cMmSYP := TAU->TAU_MMSYP
			cMmMis := TAU->TAU_MMMISS
			cMmVis := TAU->TAU_MMVISA
			cMmVal := TAU->TAU_MMVALO
		EndIf
	EndIf

	If nOpcx == 3 .or. nOpcx == 4

		dbSelectArea("TAU")
		dbSetOrder(1)
		If dbSeek(xFilial("TAU")+cPolitica)
			Reclock("TAU",.F.)
		Else
			Reclock("TAU",.T.)
		EndIf
		TAU->TAU_FILIAL := xFilial("TAU")
		TAU->TAU_CODPOL := cPolitica
		TAU->TAU_DTVIGE	:= dDtVigenc
		If lFlag
			TAU->TAU_FLAG := cModCria
			TAU->TAU_TIPO := cTipPol
		EndIf
		If NGCADICBASE('TAU_MMSYP','A','TAU',.F.)
			MSMM(cMmSYP,,,cMemoDes,1,,,"TAU","TAU_MMSYP")
			MSMM(cMmMis,,,cMemoMis,1,,,"TAU","TAU_MMMISS")
			MSMM(cMmVis,,,cMemoVis,1,,,"TAU","TAU_MMVISA")
			MSMM(cMmVal,,,cMemoVal,1,,,"TAU","TAU_MMVALO")
		Else
			TAU->TAU_DESC	:= cMemoDes
			TAU->TAU_MISSAO	:= cMemoMis
			TAU->TAU_VISAO	:= cMemoVis
			TAU->TAU_VALORE	:= cMemoVal
		EndIf
		MsUnlock("TAU")
	ElseIf nOpcx == 5
		dbSelectArea("TAU")
		Reclock("TAU",.F.)
		DbDelete()
		MsUnlock("TAU")
		If NGCADICBASE('TAU_MMSYP','A','TAU',.F.)
			MSMM(cMmSYP,,,,2)
			MSMM(cMmMis,,,,2)
			MSMM(cMmVis,,,,2)
			MSMM(cMmVal,,,,2)
		EndIf
	EndIf

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} SG230Val

Funcao para validar e gravar as informacoes

@author  Rafael Diogo Richter
@since   08/05/2008

@sample  SG230Val()

@return  boolean, Retorna a existencia do código na tabela TAU
/*/
//-------------------------------------------------------------------
Static Function Sg230Val()
Return ExistChav("TAU",cPolitica)

//-------------------------------------------------------------------
/*/{Protheus.doc} Sg230Grava

Validacao dos campos da tela

@author  Roger Rodrigues
@since   08/12/2011

@sample  Sg230Grava(3)

@param   nOpcx, Numerico, Opcao de menu sendo executada
@param   nRecno, Numerico, Numero do registro da tabela sendo alterado

@return  boolean, ok dos campos de tela
/*/
//-------------------------------------------------------------------
Static Function SG230TOK(nOpcx, nRecno)
	
	Local lRet := .T.
	Local cModVal := ""
	Local aAreaPol := GetArea()

	If nOpcx != 2 .or. nOpcx != 5
		If Empty(cPolitica)
			Help(1," ","OBRIGAT2",,STR0011,3,0) //"Política"
			Return .F.
		ElseIf Empty(dDtVigenc)
			Help(1," ","OBRIGAT2",,STR0016,3,0) //"Dt. Vigência"
			Return .F.
		ElseIf lFlag .And. Empty(cModCria)
			Help(1," ","OBRIGAT2",,STR0023,3,0) //"Dt. Vigência"
			Return .F.
		EndIf
		If !Sg230Val()
			Return .F.
		EndIf
		If nOpcx == 3 .or. (TAU->TAU_DTVIGE <> dDtVigenc)
			dbSelectArea("TAU")
			dbSetOrder(1)
			dbSeek(xFilial("TAU"))
			While !Eof() .and. xFilial("TAU") == TAU->TAU_FILIAL
				If dDtVigenc <= TAU->TAU_DTVIGE .And. cPolitica <> TAU->TAU_CODPOL .And. ;
					If( lFlag , If( cModCria <> "3" , TAU->TAU_FLAG == "3" .Or. TAU->TAU_FLAG == cModCria , .T. ) , .T. )
					lRet := .F.
					ShowHelpDlg(STR0018,{STR0019},1,{STR0020}) //"Atenção"###"Data da Vigência informada ja possui Política Ambiental."###"Informe uma data posterior."
					Exit
				EndIf
				If lFlag
					cModVal := TAU->TAU_FLAG
				EndIf
				DbSkip()
			End
		EndIf
		RestArea(aAreaPol)
		dbSelectArea("TAU")
		dbGoTo(nRecno)
	EndIf

Return lRet
