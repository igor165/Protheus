#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV009    ³ Autor ³ Inacio Luiz Kolling   ³ Data ³18/11/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula a quantidade de horas disponivel                     ³±±
±±³          ³ ( Homem hora de relogio ponto - HHRP )                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data  - Data inicio                                       ³±±
±±³          ³ Ate_Data - Ate data                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nQtdho   - Quantidade de horas disponivel                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV009(De_Data,Ate_Data)
Local dDeData, dAteData, nIndisp := 0
Local nDispo := 0.00
Local cQryAlias := ""
Local cQryExec  := ""
Local cNgMntRh	:= AllTrim(GetMv("MV_NGMNTRH"))

// Variáveis de Histórico de Indicadores
Local lMV_HIST := NGI6MVHIST()
Local aParams := {}
Local cCodIndic := "MNTV009"
Local nResult := 0

Private aARRCALE := {},aDIAMAN := {}
Private nQuantExc := 0.00
Private dDeD := De_Data, dAteD := Ate_Data
Private nQtdho := 0

// Armazena os Parâmetros
If lMV_HIST
	aParams := {}
	aAdd(aParams, {"DE_DATA"   , De_Data})
	aAdd(aParams, {"ATE_DATA"  , Ate_Data})
	NGI6PREPPA(aParams, cCodIndic)
EndIf

If ValType(De_Data) != "D" .or. ValType(Ate_Data) != "D"
	NGI6PREPVA(cCodIndic, nResult)
	Return nResult
Endif

// Query
If lMV_HIST
	cQryAlias := GetNextAlias()
	cQryExec := "SELECT * FROM " + RetSQLName("ST1") + " ST1 "
	cQryExec += "INNER JOIN " + RetSQLName("SRA") + " SRA ON "
	cQryExec += " (SRA.RA_FILIAL = " + If(xFilial("SRA") == xFilial("ST1"), "ST1.T1_FILIAL", ValToSQL( xFilial("SRA") ) ) + " AND SRA.RA_MAT = ST1.T1_CODFUNC "
	cQryExec += " AND SRA.RA_ADMISSA >= " + ValToSQL(De_Data) + " AND SRA.RA_DEMISSA <= " + ValToSQL(Ate_Data) + " "
	cQryExec += " AND SRA.D_E_L_E_T_ <> '*') "
	cQryExec += "INNER JOIN " + RetSQLName("TP4") + " TP4 ON "
	cQryExec += " (TP4.TP4_FILIAL = " + If(xFilial("TP4") == xFilial("ST1"), "ST1.T1_FILIAL", ValToSQL( xFilial("TP4") ) ) + " AND TP4.TP4_CODIGO = ST1.T1_EQUIPE "
	cQryExec += " AND TP4.D_E_L_E_T_ <> '*') "
	cQryExec += "INNER JOIN " + RetSQLName("TP6") + " TP6 ON "
	cQryExec += " (TP6.TP6_FILIAL = " + If(xFilial("TP6") == xFilial("ST1"), "ST1.T1_FILIAL", ValToSQL( xFilial("TP6") ) ) + " AND TP6.TP6_EQUIPE = TP4.TP4_CODIGO "
	cQryExec += " AND TP6.TP6_DTINI >= " + ValToSQL(De_Data) + " AND TP6.TP6_DTFIM <= " + ValToSQL(Ate_Data) + " "
	cQryExec += " AND TP6.D_E_L_E_T_ <> '*') "
	cQryExec += "WHERE ST1.T1_FILIAL = " + ValToSQL(xFilial("ST1")) + " AND ST1.D_E_L_E_T_ <> '*' "
	cQryExec := ChangeQuery(cQryExec)
	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQryExec), cQryAlias, .T., .T.)
	dbSelectArea(cQryAlias)
	NGI6PREPDA(cQryAlias, cCodIndic)
	dbCloseArea()
EndIf

If ValType(dDeD) == "D" .and. ValType(dAteD) == "D"
	//nQtdho := NGTEMDISMOD(dDeD,dAteD,"N")
	dbSelectArea("ST1")
	dbSetOrder(1)
	If Dbseek(xFilial("ST1"))
		While !Eof() .And. ST1->T1_FILIAL = xFilial("ST1")
			dDeData := dDeD
			dAteData := dAteD
			dbSelectArea("SRA")
			dbSetOrder(1)
			If dbSeek(xFilial("SRA")+ST1->T1_CODFUNC)
				If dDeData < SRA->RA_ADMISSA
					dDeData := SRA->RA_ADMISSA
				Elseif dAteData < SRA->RA_ADMISSA
					dbSelectArea("ST1")
					dbSkip()
					Loop
				Elseif !Empty(SRA->RA_DEMISSA)
					If dAteData > SRA->RA_DEMISSA
						dAteData := SRA->RA_DEMISSA
					Elseif dDeData > SRA->RA_DEMISSA
						dbSelectArea("ST1")
						dbSkip()
						Loop
					Endif
				Endif
			Endif

			If GetNewPar("MV_NGFLUT","N") == "S"
				MNTVFLU(dDeData, dAteData)
			Else
				MNTVNFLU(dDeData, dAteData)
			EndIf

			dbSelectArea("ST1")
			dbSkip()
		End
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Integracao com RH                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbselectArea("ST1")
	dbsetorder(01)
	dbseek(xFilial("ST1"))
	ProcRegua(LastRec())
	While !EoF() .And. xFilial("ST1") = ST1->T1_FILIAL
		If cNgMntRh $ "SX"
			//Encontra calendario em array aARRCALE
			nPosC  := aScan(aARRCALE,{|x| x[1] = ST1->T1_TURNO})
			If nPosC = 0
				aDIAMAN := NG_H7(ST1->T1_TURNO)
				aAdd(aARRCALE,{ST1->T1_TURNO,aDIAMAN})
				nPosC := Len(aARRCALE)
			EndIf

			dDtAtu := dDeD
			If !Empty(aARRCALE[nPosC])
				While dDtAtu <= dAteD
					nSem := If( DOW(dDTATU) == 1, 7, DOW(dDTATU)-1)
					If !NGFRHAFAST(ST1->T1_CODFUNC,dDtAtu,dDtAtu,/*msg tela*/.F.,/*so demitidos*/.F.)
						nIndisp += HtoN(aARRCALE[nPosC,2][nSem,3])  //soma horas de indisponibilidade
					Endif
					dDtAtu := dDtAtu + 1
				End
			Else
				nIndisp := 0
			EndIf

		EndIf
		dbSelectArea("ST1")
		dbSkip()
	End

	nQtdho := nQtdho - nQUANTEXC - nIndisp
Endif

// RESULTADO
nResult := nQtdho
NGI6PREPVA(cCodIndic, nResult)

Return nResult

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |  MNTVFLU | Autor ³    Jackson Machado    ³ Data ³ 08/04/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo das horas disponiveis e realizadas (Turno Flutua.) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTVFLU                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTVFLU(dDeData, dAteData)

dbSelectArea("TP4")
dbSetOrder(1)
If dbSeek(xFilial("TP4")+ST1->T1_EQUIPE)
	While dDeData <= dAteD
		dbSelectArea("TP6")
		dbSetOrder(1)
		If dbSeek(xFilial("TP6")+TP4->TP4_CODIGO) //+DTOS(dDTATU))
			While !Eof() .And. TP6->TP6_FILIAL = Xfilial("TP6") .And.;
				TP6->TP6_EQUIPE == TP4->TP4_CODIGO
				While dDeData >= TP6->TP6_DTINI .And.  dDeData <= TP6->TP6_DTFIM .And. dDeData <= dAteD

					nPOS := ASCAN(aARRCALE,{|x| x[1] == TP6->TP6_CALEND})

					If nPOS = 0
						aDIAMAN := NG_H7(TP6->TP6_CALEND)
						Aadd(aARRCALE,{TP6->TP6_CALEND,aDIAMAN})
					Else
						aDIAMAN := aARRCALE[nPOS][2]
					EndIf

					If !Empty(aDIAMAN)
						nSEM    := If( DOW(dDeData) == 1, 7, DOW(dDeData)-1)
						nQtdho  +=  HTON(aDIAMAN[nSEM][03])

					  	//Tratamento da Excecao do Calendario
						nQuantD := HTON(aDIAMAN[nSEM][03])
						If nQtdho > 0
							vHOREXC   := NG_H9(dDeData)
							If !Empty(vHOREXC)
								nQuantDEx := HTON(vHOREXC[3])
								nQUANTEXC += nQuantDEx
								nQuantD   := nQuantD - nQuantDEx
							EndIf
						EndIf
					Endif
					dDeData := dDeData + 1
				End
				DbSelectArea("TP6")
				DbSkip()
			End
		EndIf
		dDeData := dDeData + 1
	End
Else
	MNTVNFLU(dDeData, dAteData)
EndIf

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    | MNTVNFLU ³ Autor ³ 	 Jackson Machado    ³ Data ³ 08/04/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo das horas disponiveis e realizadas (Nao Turno Flut)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTVNFLU                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTVNFLU(dDeData, dAteData)

nPOS := ASCAN(aARRCALE,{|x| x[1] == ST1->T1_TURNO})

If nPOS = 0
	aDIAMAN := NG_H7(ST1->T1_TURNO)
	Aadd(aARRCALE,{ST1->T1_TURNO,aDIAMAN})
Else
	aDIAMAN := aARRCALE[nPOS][2]
EndIf

If Empty(aDIAMAN)
	MsgInfo("Calendario/Turno"+" "+ST1->T1_TURNO+" "+"do funcionario"+" "+ST1->T1_CODFUNC+" "+"nao cadastrado"+chr(13); //"Calendario/Turno"###"do funcionario"###"nao cadastrado"
	+"O processo sera cancelado...","ATENCAO") //"O processo sera cancelado..."###"ATENCAO"
	lRETOR := .t.
	Return
Endif

While dDeData <= dAteData
	If !Empty(aDIAMAN)
		nSEM    := If( DOW(dDeData) == 1, 7, DOW(dDeData)-1)
		nQtdho  += HtoN( aDIAMAN[nSEM][03] )

		//Tratamento da Excecao do Calendario
		nQuantD := HtoN(aDIAMAN[nSEM][03])
		If nQtdho > 0
			vHOREXC := NG_H9(dDeData)
			If !Empty(vHOREXC)
				nQuantDEx := HtoN(vHOREXC[3])
				nQUANTEXC += nQuantDEx
				nQuantD   := nQuantD - nQuantDEx
			EndIf
		EndIf
	Endif
	dDeData := dDeData + 1
End
//	nQtdho += NGCALEHDIS(ST1->T1_TURNO,dDeData,dAteData,"N")
Return