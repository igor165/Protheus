#include "mdta690.ch"
#include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTA690  ³ Autor ³ Thiago Olis Machado   ³ Data ³16/07/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa Cadastros de Questionario Acidente                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDTA690

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := NGBEGINPRM( )
	Local cPerg :=  PADR( "MDT690A" , 10 ) //Grupo de perguntas

	lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .T. , .F. )

	Private lCombo2 := NGCADICBASE("TMH_COMBO2","D","TMH",.F.)
	Private aRotina := MenuDef()

	//Efetua as perguntas.
	Pergunte( cPerg , .F. )

	//---------------------------------------
	// Adiciona funcionalidade ao F12
	//---------------------------------------
	SetKey( VK_F12 , { | | MDT690OBD(cPerg)} )


	If lSigaMdtps
		cCadastro := OemtoAnsi(STR0015)  //"Clientes"

		DbSelectArea("SA1")
		DbSetOrder(1)

		mBrowse( 6, 1,22,75,"SA1")
	Else

		aAC  :=  {STR0001,STR0002 }  //"Abandona"###"Confirma"
		aCRA :=  {STR0002,STR0003,STR0001}  //"Confirma"###"Redigita"###"Abandona"

		cCadastro  := STR0016 //"Acidentes"
		aTrocaF3   := {}
		aNGFIELD := {}

		cCadastro := OemtoAnsi(cCadastro)
		aPos1 := {  15,  1, 95,315 }

		dbSelectArea( "TNC" )
		SetBrwCHGAll( .F. ) // nao apresentar a tela para informar a filial
		mBrowse( 6, 1,22,75,"TNC")

		dbSelectArea("TNC")
		dbSetOrder(1)

	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG690INC  ³ Autor ³ Thiago Olis Machado   ³ Data ³16/07/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa Cadastros de Questionario do Acidente             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG690INC(cAlias,nReg,nOpcx)

	Local nx
	Local aNoFields := {}
	Local cSeekTO6
	Local cCondTO6

	Private oDlg3, oGet, cCodAnt, oMenu
	Private nCnt   := 0
	Private nItens := 0
	Private aHeader := {}
	Private aCols := {}

	dbSelectArea("TO6")
	dbSetOrder(1)

	M->TO6_DTREAL := dDataBase
	M->TO6_QUESTI := space(len(TO6->TO6_QUESTI))
	cNOMQUE       := space(20)

	If lSigaMdtps
		M->TO6_CLIENT := SA1->A1_COD
		M->TO6_LOJA := SA1->A1_LOJA
	Endif

	If nOpcx <> 3

	M->TO6_DTREAL := TO6->TO6_DTREAL
	M->TO6_QUESTI := TO6->TO6_QUESTI
	QUESTI690(M->TO6_QUESTI)

	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Montando aHeader                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SX3")
	dbSetOrder(2)
	nUsado  := 0

	aAdd(aNoFields, 'TO6_ACIDEN')
	aAdd(aNoFields, 'TO6_QUESTI')
	aAdd(aNoFields, 'TO6_NOMQUE')
	aAdd(aNoFields, 'TO6_DTREAL')
	aAdd(aNoFields, 'TO6_DESRES')

	If lSigaMdtps
		cCondTO6 := "TO6->(TO6_FILIAL+TO6_CLIENT+TO6_LOJA+TO6_ACIDEN+DtoS(TO6_DTREAL)+TO6_QUESTI)"
		cSeekTO6 := "xFilial('TO6')+cCliMdtps+M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI"
		#IFDEF TOP
			aAdd(aNoFields, 'TO6_CLIENT')
			aAdd(aNoFields, 'TO6_LOJA')
			cQuery := "SELECT * FROM "+RetSqlName("TO6")+" TO6 WHERE TO6.D_E_L_E_T_ = ' ' AND TO6.TO6_CLIENT = '"+SA1->A1_COD+"' AND TO6.TO6_LOJA = '"+SA1->A1_LOJA+"' "
			FillGetDados( nOpcx, "TO6", 1, xFilial("TO6")+cCliMdtps+M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI+TMH->TMH_QUESTA, {|| "TO6->TO6_FILIAL + TO6->TO6_CLIENT + TO6->TO6_LOJA + TO6->TO6_ACIDEN + DTOS(TO6->TO6_DTREAL) + TO6->TO6_QUESTI + TO6->TO6_QUESTA"}, {|| .T.},aNoFields,,,cQuery,{|| MACOL690(nOpcx)})
		#ELSE
			cKEY := "cCliMdtps+M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI"
			cGET := "TO6->TO6_FILIAL == '"+xFilial("TO6")+" .And. TO6->(TO6_CLIENT+TO6_LOJA) == " + "'" + SA1->(A1_COD+A1_LOJA) + "'"
			FillGetDados( nOpcx, "TO6", 1, cKey, {|| }, {|| .T.},aNoFields,,,,{|| NGMontaAcols("TO6",&cKEY,cGET)})
		#ENDIF
	Else
		cCondTO6 := "TO6->(TO6_FILIAL+TO6_ACIDEN+DtoS(TO6_DTREAL)+TO6_QUESTI)"
		cSeekTO6 := "xFilial('TO6')+M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI"
		#IFDEF TOP
			cQuery := "SELECT * FROM "+RetSqlName("TO6")+" TO6 WHERE TO6.D_E_L_E_T_ = ' '"
			FillGetDados( nOpcx, "TO6", 1, xFilial("TO6")+M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI+TMH->TMH_QUESTA, {|| "TO6->TO6_FILIAL + TO6->TO6_ACIDEN + DTOS(TO6->TO6_DTREAL) + TO6->TO6_QUESTI + TO6->TO6_QUESTA"}, {|| .T.},aNoFields,,,cQuery,{|| MACOL690(nOpcx)})
		#ELSE
			cKEY := "M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI"
			cGET := "TO6->TO6_FILIAL == '"+xFilial("TO6")+"'
			FillGetDados( nOpcx, "TO6", 1, cKey, {|| }, {|| .T.},aNoFields,,,,{|| NGMontaAcols("TO6",&cKEY,cGET)})
		#ENDIF
	Endif

	If Empty(aCols) .Or. nOpcx == 3
	aCols := BlankGetd(aHeader)
	EndIf
	/*
	dbSeek("TO6_QUESTA")
	nUsado := nUsado + 1
	AADD(aHeader,{ TRIM(x3_titulo),x3_campo,x3_picture,x3_tamanho,x3_decimal,x3_valid,x3_usado,x3_tipo,x3_arquivo,x3_context,x3_f3,.F.} )

	dbSeek("TO6_PERGUN")
	nUsado := nUsado + 1
	AADD(aHeader,{ TRIM(x3_titulo),x3_campo,x3_picture,x3_tamanho,x3_decimal,X3_VALID,x3_usado,x3_tipo,x3_arquivo,x3_context,x3_f3,.F.} )

	dbSeek("TO6_RESPOS")
	nUsado := nUsado + 1
	AADD(aHeader,{ TRIM(x3_titulo),x3_campo,x3_picture,x3_tamanho,x3_decimal,x3_valid,x3_usado,x3_tipo,x3_arquivo,x3_context,x3_f3,x3_when} )

	dbSeek("TO6_QTRESP")
	nUsado := nUsado + 1
	AADD(aHeader,{ TRIM(x3_titulo),x3_campo,x3_picture,x3_tamanho,x3_decimal,x3_valid,x3_usado,x3_tipo,x3_arquivo,x3_context,x3_f3,x3_when } )

	dbSeek("TO6_COMRES")
	nUsado := nUsado + 1
	AADD(aHeader,{ TRIM(x3_titulo),x3_campo,x3_picture,x3_tamanho,x3_decimal,x3_valid,x3_usado,x3_tipo,x3_arquivo,x3_context,x3_f3,x3_when } )

	dbSelectArea("SX3")
	dbSetOrder(1)

	PRIVATE aCOLS[1][nUsado+1]

	aCOLS[1][1] := SPACE(3)
	aCOLS[1][2] := SPACE(60)
	aCOLS[1][3] := SPACE(1)
	aCOLS[1][4] := 0.00
	aCOLS[1][5] := SPACE(30)
	aCOLS[1][6] := .F.

	nQUESTA := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_QUESTA" })
	nPERGUN := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_PERGUN" })
	nRESPOS := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_RESPOS" })
	nQTRESP := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_QTRESP" })
	nCOMRES := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_COMRES" })

	If nOpcx <> 3

	MACOL690()

	Endif
	*/

	nQUESTA := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_QUESTA" })
	nPERGUN := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_PERGUN" })
	nRESPOS := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_RESPOS" })
	nQTRESP := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_QTRESP" })
	nCOMRES := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_COMRES" })

	cTudoOk  :="A690TUDOK()"
	cLinhaOk :="A690LINHOK()"

	#IFDEF WINDOWS

		nopca := 0
		DEFINE MSDIALOG oDlg3 TITLE OemToAnsi(cCadastro) From 9,0 To 28,80 OF oMainWnd

		@ 1.4,1   SAY OemToAnsi(STR0007) //"Acidente"
		@ 1.4,5   MSGET M->TO6_ACIDEN Picture "@!" SIZE 30,10 when .F.

		@ 1.4,11  SAY OemToAnsi(STR0012) //"Descricao"
		@ 1.4,15  MSGET cAciden picture '@!' SIZE 180,10 When .F.

		@ 2.4,1   SAY OemToAnsi(STR0013) //"Realizacao"
		@ 2.4,5   MSGET M->TO6_DTREAL Picture "99/99/99" SIZE 43,10 when INCLUI Hasbutton

		@ 3.4,1   SAY OemToAnsi(STR0006) //"Questionario"
		@ 3.4,5   MSGET M->TO6_QUESTI Picture "@!" Valid MDT690VQTI();
				.And. MACOL690(4) .And. QUESTI690(M->TO6_QUESTI) F3 If(lSigaMdtps,"MDTTMG","TMG")  SIZE 30,10 when INCLUI Hasbutton

		@ 3.4,11  SAY OemToAnsi(STR0014) //"Nome"
		@ 3.4,15  MSGET cNOMQUE picture '@!' SIZE 180,10 When .F.

		oGet := MSGetDados():New(60,1,145,315,nOpcx,"A690LINHOK","A690TUDOK","",.F.,,,,nItens)

		If NGCADICBASE("TMH_COMBO","A","TMH",.F.)
			oGet:oBrowse:aColumns[nRESPOS]:bData := {|| fRetBox690() }
		Endif

		NGPOPUP(asMenu,@oMenu)
		oDlg3:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg3)}

		ACTIVATE MSDIALOG oDlg3 ON INIT EnchoiceBar(oDlg3,{||nopca:=1,If(!A690TUDOK(),nopca := 0,oDlg3:End())},{||oDlg3:End()})

	#ENDIF

	If nopca == 1

	dbSelectArea("TO6")
	dbSetOrder(If(lSigaMdtps,3,1))

	If nOpcx == 3 .Or. nOpcx == 4

		For nx := 1 to nItens

			If !Empty(aCols[nx][nRESPOS]) // BRANCO

				If !dbSeek(&(cSeekTO6)+aCols[nx][nQUESTA])
					RecLock("TO6",.T.)
					TO6->TO6_FILIAL := xFilial('TO6')
					TO6->TO6_ACIDEN := M->TO6_ACIDEN
					TO6->TO6_DTREAL := M->TO6_DTREAL
					TO6->TO6_QUESTI := M->TO6_QUESTI
					TO6->TO6_QUESTA := aCols[nx][nQUESTA]
					TO6->TO6_RESPOS := aCols[nx][nRESPOS]
					TO6->TO6_QTRESP := aCols[nx][nQTRESP]
					TO6->TO6_COMRES := aCols[nx][nCOMRES]

					If lSigaMdtps
						TO6->TO6_CLIENT := SA1->A1_COD
						TO6->TO6_LOJA   := SA1->A1_LOJA
					Endif
				Else
					RecLock("TO6",.F.)
					TO6->TO6_QUESTA := aCols[nx][nQUESTA]
					TO6->TO6_RESPOS := aCols[nx][nRESPOS]
					TO6->TO6_QTRESP := aCols[nx][nQTRESP]
					TO6->TO6_COMRES := aCols[nx][nCOMRES]
				Endif

				MsUnLock('TO6')

			Else

				If dbSeek(&(cSeekTO6)+aCols[nx][nQUESTA])
					RecLock("TO6",.F.)
					dbDelete()
					MsUnLock('TO6')
				Endif

			Endif
		Next

	Elseif nOpcx == 5

		dbSeek(&(cSeekTO6))

		While !EOF() .And. &(cSeekTO6) == &(cCondTO6)

			RecLock("TO6",.F.)
			dbDelete()
			MsUnLock('TO6')

			dbSkip()
		End

	Endif

	Endif

	dbSelectArea("TO6")

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A690TUDOK³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/02/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistencia final                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A690TUDOK

	Local nFor
	Local nPos := nRESPOS

	If Len(aCols) > 0
		For nFor := 1 To Len(aCols)
			If	Empty(aCols[nFor][nPos])
				MsgInfo(STR0025,STR0019) //"Há perguntas que ainda não foram respondidas!"###"ATENÇÃO"
				Return .F.
			EndIf
		Next nFor
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A690LINHOK³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/02/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistencia da linha                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A690LINHOK

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³QUESTI690 ³ Autor ³ Inacio Luiz Kolling   ³ Data ³04/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Consistencia do QUESTI e NOMQUE                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function QUESTI690(vQUESTI)

	Local nIndTMG
	Local cSeekTMG

	If lSigaMdtps
		nIndTMG := 2  //TMG_FILIAL+TMG_CLIENT+TMG_LOJA+TMG_QUESTI
		cSeekTMG := xFilial('TMG')+cCliMdtps+vQUESTI
	Else
		nIndTMG := 1
		cSeekTMG := xFilial('TMG')+vQUESTI
	Endif

	dbSelectArea("TMG")
	dbSetOrder(nIndTMG)
	If dbSeek(cSeekTMG)
		cNOMQUE := TMG->TMG_NOMQUE
	Else
		Help(" ",1,"MSG_QUEST")
		Return .F.
	Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MACOL690  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³04/04/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta ou remonta o GETDADOS                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MACOL690(nOpcx)

	Local nIndTMH
	Local cSeekTMH
	Local cCondTMH
	Local cSeekTO6
	Local cFichaMed := "", cSexo := ""

	If lSigaMdtps
		nIndTMH := NGRETORDEM("TMH","TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI+TMH_QUESTA",.T.)
		If nIndTMH == 0
			nIndTMH := 1
		Endif
		cSeekTMH := xFilial('TMH')+cCliMdtps+M->TO6_QUESTI
		cCondTMH := "TMH->(TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI)"
		cSeekTO6 := "xFilial('TO6')+cCliMdtps+M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI+TMH->TMH_QUESTA"
	Else
		nIndTMH := 1
		cSeekTMH := xFilial('TMH')+M->TO6_QUESTI
		cCondTMH := "TMH->(TMH_FILIAL+TMH_QUESTI)"
		cSeekTO6 := "xFilial('TO6')+M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI+TMH->TMH_QUESTA"
	Endif

	If nOpcx == 3
	aCols := BlankGetd(aHeader)
	Return .T.
	EndIf

	nQUESTA := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_QUESTA" })
	nPERGUN := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_PERGUN" })
	nRESPOS := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_RESPOS" })
	nQTRESP := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_QTRESP" })
	nCOMRES := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_COMRES" })

	dbSetOrder(1)
	dbSelectArea("TMH")
	dbSetOrder(nIndTMH)
	dbSeek(cSeekTMH)

	nCnt   := 0
	nItens := 0


	dbSelectArea("TNC")
	dbSetOrder(1)
	If dbSeek(xFilial("TNC")+M->TO6_ACIDEN)
		cFichaMed := TNC->TNC_NUMFIC
	dbSelectArea("TM0")
	dbSetOrder(1)
	If dbSeek(xFilial("TM0")+cFichaMed)
		cSexo := TM0->TM0_SEXO
	EndIf
	EndIf

	dbSetOrder(1)
	dbSelectArea("TMH")
	dbSetOrder(nIndTMH)
	dbSeek(cSeekTMH)

	Do While !EOF() .And. cSeekTMH == &(cCondTMH)

		If Empty(TMH->TMH_INDSEXO) .Or. Empty(cSexo)
			nCnt := nCnt + 1
		ElseIf cSexo ==  TMH->TMH_INDSEXO .Or. 	TMH->TMH_INDSEXO == "3"
		nCnt := nCnt + 1
	End
		Dbskip()

	End

	If nCnt > 0
	Private aNOME[nCNT][Len(aHeader)+1]
	aCOLS:=aCLONE(aNOME)
	dbSelectArea("TMH")
	dbSetOrder(nIndTMH)
	dbSeek(cSeekTMH)

	nCnt := 0
	Do While !EOF() .And. cSeekTMH == &(cCondTMH)

		If Empty(TMH->TMH_INDSEXO) .Or. Empty(cSexo)
				nCnt := nCnt + 1
			ElseIf cSexo ==  TMH->TMH_INDSEXO .Or. 	TMH->TMH_INDSEXO == "3"
		nCnt := nCnt + 1
		Else
			dbSkip()
			Loop
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Montando aCols                                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		aCOLS[nCnt][nQUESTA] := TMH->TMH_QUESTA
		aCOLS[nCnt][nPERGUN] := TMH->TMH_PERGUN

		dbSelectArea("TO6")
		dbSetOrder(If(lSigaMdtps,3,1))
		If dbSeek( &(cSeekTO6) )
			aCOLS[nCnt][nQTRESP] := TO6->TO6_QTRESP
			aCOLS[nCnt][nCOMRES] := TO6->TO6_COMRES
			aCOLS[nCnt][nRESPOS] := TO6->TO6_RESPOS
		Else
			If NGCADICBASE("TMH_COMBO","A","TMH",.F.)
					aCOLS[nCnt][nRESPOS] := " "
				Else
					aCOLS[nCnt][nRESPOS] := '3'
				Endif
			aCOLS[nCnt][nQTRESP] := 0.00
			aCOLS[nCnt][nCOMRES] := space(30)
		Endif

			aCols[nCnt][	Len(aHeader)-1		] := "TO6"
			aCols[nCnt][	Len(aHeader)		] := ("TO6")->(Recno())
		aCOLS[nCnt][	Len(aCOLS[nCnt])	] := .F.

		nItens := nItens + 1
		dbSelectArea( 'TMH' )
		dbSkip()

	End
	Else
	HELP(" ",1,"NGSSEPERG")//'Nao existe pergunta cadastrada para este questionario'
	Return .F.
	Endif

	If INCLUI
		oGet:FORCEREFRE()
	Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMDT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

	Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .T. , .F. )
	Local aRotina

	If lSigaMdtps
		aRotina := { { STR0004, "AxPesqui"  , 0 , 1},; //"Pesquisar"
					{ STR0005, "NGCAD01"   , 0 , 2},; //"Visualizar"
					{ STR0016, "MDT690TNC" , 0 , 4} } //"Acidentes"
	Else
		aRotina := { { STR0004, "AxPesqui" , 0 , 1}  ,; //"Pesquisar"
					{ STR0005, "NGCAD01" , 0 , 2}  ,; //"Visualizar"
					{ STR0006, "A690CALLBR" , 0 , 3,0} }  //"Questionario"
	Endif

Return aRotina

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDT690TNC ³ Autor ³ Andre Perez Alvarez   ³ Data ³25/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta o browse com os acidentes do cliente.                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDT690TNC()

	Local aArea	    := GetArea()
	Local oldROTINA := aCLONE(aROTINA)
	Local oldCad    := cCadastro
	Local nCps      := 0

	cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA
	Private wnrel := "MDTA690"

	aRotina := { { STR0004, "AxPesqui"   , 0 , 1  } ,; //"Pesquisar"
				 { STR0005, "NGCAD01"    , 0 , 2  } ,; //"Visualisar"
				 { STR0006, "A690CALLBR" , 0 , 3,0} }  //"Questionario"

	Private  aAC  :=  {STR0001,STR0002} //"Abandona"###"Confirma"
	Private	 aCRA :=  {STR0002,STR0003,STR0001}  //"Confirma"###"Redigita"###"Abandona"

	Private cCadastro  := STR0016 //"Acidentes"
	Private aTrocaF3   := {}, aNGFIELD := {}

	cCadastro := OemtoAnsi(cCadastro)
	aPos1 := {  15,  1, 95,315 }

	aCHOICE := {}

	aCHOICE := NGCAMPNSX3( "TNC" , { "TNC_CLIENT", "TNC_LOJA", "TNC_FILIAL" } )

	dbSelectArea( "TNC" )
	Set Filter To TNC->(TNC_CLIENT+TNC_LOJA) == cCliMdtps
	dbSetOrder(13) //TNC_FILIAL+TNC_CLIENT+TNC_LOJA+TNC_ACIDEN
	mBrowse( 6, 1,22,75,"TNC")

	dbSelectArea( "TNC" )
	Set Filter To

	aROTINA := aCLONE(oldROTINA)
	RestArea(aArea)
	cCadastro := oldCad

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDT650PER ³ Autor ³ Andre Perez Alvarez   ³ Data ³25/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inicializa o campo TO6_NOMQUE                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDT690QUE(lIniBrw)

	Local cDesc
	Local aArea := GetArea()
	Local lPrest := .F.

	If Type("cCliMdtPs") == "C"
		If !Empty(cCliMdtPs)
			lPrest := .T.
		Endif
	Endif

	If lPrest
		If lIniBrw
			cDesc := Posicione( "TMG", 2, xFilial("TMG") + cCliMdtps + TO6->TO6_QUESTI, "TMG_NOMQUE")
		Else
			cDesc := Posicione( "TMG", 2, xFilial("TMG") + cCliMdtps + M->TO6_QUESTI, "TMG_NOMQUE")
		Endif
	Else
		If lIniBrw
			cDesc := Posicione( "TMG", 1, xFilial("TMG") + TO6->TO6_QUESTI, "TMG_NOMQUE")
		Else
			cDesc := Posicione( "TMG", 1, xFilial("TMG") + M->TO6_QUESTI, "TMG_NOMQUE")
		Endif
	Endif

	RestArea(aArea)

Return cDesc

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o  MDTVLDQUESTA ³ Autor ³ Andre Perez Alvarez   ³ Data ³25/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida o campo TO6_QUESTA (Prestador de servico)            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDTVLDQUESTA()

	Local lPrest := .F.

	If Type("cCliMdtPs") == "C"
		If !Empty(cCliMdtPs)
			lPrest := .T.
		Endif
	Endif

	If lPrest
		nTMHi := NGRETORDEM("TMH","TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI+TMH_QUESTA",.T.)
		If nTMHi == 0
			nTMHi := 1
		Endif
		Return (  EXISTCPO("TMH",cCliMdtps+M->TO6_QUESTI+M->TO6_QUESTA,nTMHi) .And. EXISTCHAV("TO6",cCliMdtps+M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI+M->TO6_QUESTA,3)  )
	Else
		Return (  EXISTCPO("TMH",M->TO6_QUESTI+M->TO6_QUESTA,1) .And. EXISTCHAV("TO6",M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI+M->TO6_QUESTA,1) )
	Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDT690VQTI³ Autor ³ Denis                 ³ Data ³ 25/10/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida campo TO6_QUESTI                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDT690VQTI()

	Local lPrest := .F.

	If Type("cCliMdtPs") == "C"
		If !Empty(cCliMdtPs)
			lPrest := .T.
		Endif
	Endif

	If lPrest
		Return (EXISTCPO("TMG",cCliMdtps+M->TO6_QUESTI,2) .And. ExistChav("TO6",cCliMdtps+M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI,3))
	Else
		Return (EXISTCPO("TMG",M->TO6_QUESTI,1) .And. ExistChav("TO6",M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI))
	Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fRetBox690ºAutor  ³Wagner S. de Lacerdaº Data ³  28/06/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Mostra campo de acordo com item selecionado                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MDTA690                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fRetBox690()

	Local nInitcBox ,aCombo:={}, nTam
	Local nTamCpo := Len(aCols[n][nRESPOS])
	Local cCampo  := ""
	Local cCombo  := ""
	Local cComboRes:= If (NGCADICBASE( "TMH_RESPOS", "A", "TMH", .F. ), "TMH->TMH_RESPOS", "TMH->TMH_COMBO+TMH->TMH_COMBO2" )

	If lSigaMdtps
		nTMHi := NGRETORDEM("TMH","TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI+TMH_QUESTA",.T.)
		If nTMHi == 0
			nTMHi := 1
		Endif
		dbSelectArea("TMH")
		dbSetOrder(nTMHi)
		dbSeek( xFilial("TMH") + cCliMdtps + M->TO6_QUESTI + aCols[n,nQUESTA])
	Else
		dbSelectArea("TMH")
		dbSetOrder(1)
		dbSeek( xFilial("TMH") + M->TO6_QUESTI + aCols[n,nQUESTA] )
	Endif
	cCombo := Alltrim( &cComboRes )
	If Empty(cCombo)
		cCombo := "1="+Capital(STR0022)+";"+"2="+Capital(STR0023)+";"+"3="+Capital(STR0024) //Sim //Nao //Sem resposta
	Endif

	If Len(aCols) > 0 .And. n > 0 .And. n <= Len(aCols)
		cCampo := aCols[n][nRESPOS]
		aCombo := RetSx3Box(cCombo,@nInitcBox,@nTam,nTamCpo)
		nIniPos := Ascan(aCombo,{|x| cCampo $ x[2]})
		nIniPos := Iif(nIniPos == 0,Len(aCombo),nIniPos)
		cCampo := aCombo[nIniPos,3]
	EndIf

Return cCampo

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³IMPMDT411 ³ Autor ³Jackson Machado        ³ Data ³ 28/02/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao do relatorio de questionario                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MDTA690                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function IMPMDT411()

	Local aDados := {}
	dbSelectArea("TO6")
	If lSigaMdtps
		dbSetOrder(3)
		aAdd( aDados , { "Mv_par01" , SA1->A1_COD } )
		aAdd( aDados , { "Mv_par02" , SA1->A1_LOJA } )
		aAdd( aDados , { "Mv_par03" , TO6->TO6_QUESTI } )
		aAdd( aDados , { "Mv_par04" , TO6->TO6_ACIDEN } )
		aAdd( aDados , { "Mv_par05" , TO6->TO6_ACIDEN } )
		aAdd( aDados , { "Mv_par06" , TO6->TO6_DTREAL } )
		aAdd( aDados , { "Mv_par07" , TO6->TO6_DTREAL } )
		aAdd( aDados , { "Mv_par08" , 2 } )
	Else
		dbSetOrder(1)
		aAdd( aDados , { "Mv_par01" , TO6->TO6_QUESTI } )
		aAdd( aDados , { "Mv_par02" , TO6->TO6_ACIDEN } )
		aAdd( aDados , { "Mv_par03" , TO6->TO6_ACIDEN } )
		aAdd( aDados , { "Mv_par04" , TO6->TO6_DTREAL } )
		aAdd( aDados , { "Mv_par05" , TO6->TO6_DTREAL } )
		aAdd( aDados , { "Mv_par06" , 2 } )
	Endif

	MDTR411( aDados, .F. )
	//Inicializa a Pergunta de Bloqueio
	Pergunte( "MDT690A" , .F. )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³MDT690IBRWºAutor  ³Wagner S. de Lacerdaº Data ³  28/06/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ InitBrw do campo TO6_DESRES                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MDTA690                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDT690IBRW()

	Local OldAlias := GetArea()
	Local cRet := Space(30)
	Local nPos,nPos1,cDesc
	Local cCboxTMH := ""
	Local cComboRes:= If (NGCADICBASE( "TMH_RESPOS", "A", "TMH", .F. ), "TMH->TMH_RESPOS", "TMH->TMH_COMBO+TMH->TMH_COMBO2" )
	Local lEditBox := .F.

	If Type("lCombTO6") == "L"
		If lCombTO6
			lEditBox := .T.
		Endif
	Endif

	If TO6->TO6_RESPOS == "1"
		cRet := STR0022 //"SIM"
	ElseIf TO6->TO6_RESPOS == "2"
		cRet := STR0023 //"NAO"
	ElseIf TO6->TO6_RESPOS == "3"
		cRet := STR0024 //"SEM RESPOSTA"
	ElseIf !Empty(TO6->TO6_RESPOS) .And. lEditBox
		If lSigaMdtps
			nTMHi := NGRETORDEM("TMH","TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI+TMH_QUESTA",.T.)
			If nTMHi == 0
				nTMHi := 1
			Endif
			dbSelectArea("TMH")
			dbSetOrder(nTMHi)
			If dbSeek(xFilial("TMH")+cCliMdtps+TO6->TO6_QUESTI+TO6->TO6_QUESTA)
				cCboxTMH := &cComboRes
			Endif
		Else
			dbSelectArea("TMH")
			dbSetOrder(01)
			If dbSeek(xFilial("TMH")+TO6->TO6_QUESTI+TO6->TO6_QUESTA)
				cCboxTMH := &cComboRes
			Endif
		Endif

		If !Empty(cCboxTMH)
			nPos := At( Substr(TO6->TO6_RESPOS,1,1)+"=" , cCboxTMH )
			If nPos > 0
				nPos1 := At( ";" , Substr( cCboxTMH , nPos+2 ) )
				cDesc := Alltrim(Substr( cCboxTMH , nPos+2 ))
				If nPos1 > 0
					cDesc := Alltrim(Substr( cCboxTMH , nPos+2 , nPos1-1 ))
				Endif
				cRet := PadR(cDesc,30)
			Endif
		Endif
	Endif

	RestArea(OldAlias)

Return Upper(cRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fChgGD690 ºAutor  ³Wagner S. de Lacerdaº Data ³  28/06/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao chamada no WHEN do campo TO6_RESPOS                 º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MDTA690                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fChgGD690()

	Local cCombBox := ""
	Local cOldAlias:= Alias()
	Local nRESPOS := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO6_RESPOS" })

	If lSigaMdtps
		nTMHi := NGRETORDEM("TMH","TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI+TMH_QUESTA",.T.)
		If nTMHi == 0
			nTMHi := 1
		Endif
		dbSelectArea("TMH")
		dbSetOrder(nTMHi)
		dbSeek( xFilial("TMH") + cCliMdtps + M->TO6_QUESTI + aCols[n,nQUESTA])
	Else
		dbSelectArea("TMH")
		dbSetOrder(1)
		dbSeek( xFilial("TMH") + M->TO6_QUESTI + aCols[n,nQUESTA] )
	Endif

	cCombBox := Alltrim(TMH->TMH_COMBO)
	If Empty(cCombBox)
		cCombBox := "1="+Capital(STR0022)+";"+"2="+Capital(STR0023)+";"+"3="+Capital(STR0024) //Sim //Nao //Sem resposta
	Endif
	oGet:aInfo[nRESPOS][2] := cCombBox
	oGet:oBrowse:Refresh()

	If !Empty(cOldAlias)
		dbSelectArea(cOldAlias)
	Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDTTMG690 ³ Autor ³ Jackson Machado	     ³ Data ³ 28/02/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida campo TMG_QUESTI                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDTTMG690()

	Local lPrest := .F.

	If Type("cCliMdtPs") == "C"
		If !Empty(cCliMdtPs)
			lPrest := .T.
		Endif
	Endif

	If lPrest
		Return (EXISTCHAV("TMG",cCliMdtps+M->TMG_QUESTI,2))
	Else
		Return (EXISTCHAV("TMG",M->TMG_QUESTI,1))
	Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDTA690ACI³ Autor ³ Jackson Machado		  ³ Data ³28/02/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Função para filtrar os acidentes				                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTA690                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDTA690ACI(cAlias,nRecno,nOpcx)

	Local aOld := aClone(aRotina)
	Local aPesq := {}
	Local cExprFilTop := " TO6_ACIDEN = " + ValToSQL( TNC->TNC_ACIDEN ) + " AND " + ;
							" TO6_FILIAL = " + ValToSQL( xFilial( "TO6" ) ) + " AND " + ;
							" TO6_QUESTA = " + ValToSQL( "001" )

	if lSigaMdtPs
		cExprFilTop += " AND TO6_CLIENT||TO6_LOJA = " + ValToSQL( cCliMdtps )
	EndIf

	aRotina := { { STR0005 , "MDTA690CAD"  , 0 , 2},;//"Visualizar"
				{ STR0008 , "MDTA690CAD"  , 0 , 3},;//"Incluir"
				{ STR0009 , "MDTA690CAD"  , 0 , 4},;//"Alterar"
				{ STR0010 , "MDTA690CAD"  , 0 , 5},;//"Excluir"
				{ STR0031	 , "IMPMDT411"	  , 0 , 6} } //"Imprimir"

	mBrowse( 6 , 1 , 22 , 75 , "TO6" , , , , , , , , , , , , , , cExprFilTop )

	aRotina := aClone(aOld)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDTA690CAD³ Autor ³ Denis Hyroshi de Souza³ Data ³01/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tela de cadastro da Ficha Ocupacional                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ AMI2A002                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDTA690CAD(cAlias,nRecno,nOpcx)

	Local oMainWnd
	Local lVisual := .F.
	Local nYY,nXX
	Local aBtn_Topo := {}
	Local aAreaTMO := GetArea()
	Local nIndTMH, cSeekTMH, cCondTMH
	Local cComboRes:= If (NGCADICBASE( "TMH_RESPOS", "A", "TMH", .F. ), "TMH->TMH_RESPOS", "TMH->TMH_COMBO+TMH->TMH_COMBO2" )
	Local aOldRot  := aClone(aRotina)
	Local nQuesti := 0
	Local cSexo := ""
	nOpcx := nOpcx + 1

	aRotina := { { STR0004	, "AxPesqui"  , 0 , 1},;//"Pesquisar"
				{ STR0005	, "NGCAD01"  , 0 , 2},;//"Visualizar"
				{ STR0008	, "NGCAD01"  , 0 , 3},;//"Incluir"
				{ STR0009	, "NGCAD01"  , 0 , 4},;//"Alterar"
				{ STR0010	, "NGCAD01"  , 0 , 5, 3} }//"Excluir"


	aAdd( aBtn_Topo , {"BMPVISUAL" ,{|| MDTA690TO6(.F.,nOpcx) }	,STR0029,STR0029} ) //"Ficha"##"Ficha"
	aAdd( aBtn_Topo , {"HISTORIC"  ,{|| fImpRelat() }			,STR0030,STR0031} ) //"Imprimir Questionário"###"Imprimir"

	If nOpcx == 2 .Or. nOpcx == 5
		lVisual := .T.
	Endif

	Private oFont    := TFont():New("Arial",,-10,.F.,.F.)
	Private oFont12  := TFont():New("Arial",,-12,.T.,.T.)
	Private oFont16  := TFont():New("Arial",,-16,.T.,.T.)
	Private cAliasCTT := "SI3"
	Private oDlgInd, oFolder1
	Private oMenu
	Private aCadTipo := {}
	Private oIndNG
	Private cIndNG
	Private lAccPag2 := .F.
	Private aAC := {STR0001,STR0002},aCRA:= {STR0002,STR0003,STR0001}//"Abandona"##"Confirma"##"Confirma"##"Redigita"##"Abandona"
	Private aHeader[0],Continua,nUsado:=0
	Private aSize := MsAdvSize(,.F.,430), aObjects := {}
	//Variáveis de controle da TO6
	Private nOrdTO6 := NGRETORDEM("TO6","TO6_FILIAL+TO6_ACIDEN+TO6_QUESTI+DTOS(TO6_DTREAL)+TO6_QUESTA+TO6_RESPOS",.F.)
	Private cSOrd := ""

	Aadd(aObjects,{200,200,.T.,.F.})
	aInfo := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.T.)
	If aSize[6] > 900
		aPosObj[1,3] := aPosObj[1,3] * ( aSize[6] / 560 )
	ElseIf aSize[6] > 650
		aPosObj[1,3] := aPosObj[1,3] * ( aSize[6] / 590 )
	Else
		aPosObj[1,3] := aPosObj[1,3] * ( aSize[6] / 610 )
	Endif

	If Alltrim(GETMV("MV_MCONTAB")) == "CTB"
		cAliasCTT := "CTT"
	Endif

	Private cX_MF_Fun := Space(40)
	Private cX_MF_Cus := Space(40)

	dbSelectArea("TO6")
	If lSigaMdtps
		nOrd := NGRETORDEM("TO6","TO6_FILIAL+TO6_CLIENT+TO6_LOJA+TO6_ACIDEN+TO6_QUESTI+DTOS(TO6_DTREAL)",.F.)
		dbSetOrder(nOrd)
	Else
		nOrd := NGRETORDEM("TO6","TO6_FILIAL+TO6_ACIDEN+DTOS(TO6_DTREAL)+TO6_QUESTI",.F.)
		dbSetOrder(nOrd)
	Endif
	aAreaTNC := TNC->(GetArea())
	dbSelectArea("TO6")
	RegToMemory("TO6",(nOpcx == 3))
	RestArea(aAreaTNC)

	//Inicializa grupo de perguntas
	Pergunte( "MDT690A" , .F. )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³TELA INICIAL          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nOpcPar := 0
	If nOpcx==3
		nOpcPar := MDTA690TO6(nOpcx==3,nOpcx)
		If nOpcPar == 1
			Begin Transaction
				MDTAGrv690(nOpcx,1)
			End Transaction
		Endif
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³TELA INICIAL          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nOpca := 0

	If lSigaMdtps
		If NGRETORDEM("TMH","TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI+TMH_QUESTA",.F.) > 0
			nIndTMH := NGRETORDEM("TMH","TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI+TMH_QUESTA",.F.)
		Else
			nIndTMH := 1
		Endif
		cSeekTMH:= cCliMdtps+M->TO6_QUESTI
		cCondTMH:= "TMH->(TMH_FILIAL+TMH_CLIENT+TMH_LOJA+TMH_QUESTI)"
	Else
		nIndTMH := 1
		cSeekTMH:= M->TO6_QUESTI
		cCondTMH:= "TMH->(TMH_FILIAL+TMH_QUESTI)"
	Endif
	//Variáveis de pesquisa das perguntas e resposta do questionário
	If lSigaMdtPs
		nOrdTO6 := NGRETORDEM("TO6","TO6_FILIAL+TO6_CLIENT+TO6_LOJA+TO6_ACIDEN+TO6_QUESTI+DTOS(TO6_DTREAL)+TO6_QUESTA+TO6_RESPOS",.F.)
		cSOrd	  := cCliMdtPs+M->TO6_ACIDEN+M->TO6_QUESTI+DTOS(M->TO6_DTREAL)
	Else
		cSOrd	  := M->TO6_ACIDEN+M->TO6_QUESTI+DTOS(M->TO6_DTREAL)
	Endif
	nQuesti := 0
	If nOpcPar == 1 .Or. nOpcx != 3
		cTipoFic := M->TO6_QUESTI
		dbSelectArea("TMH")
		dbSetOrder(nIndTMH)
		dbSeek(xFilial("TMH")+cSeekTMH)
		While !Eof() .And. xFilial("TMH")+cSeekTMH == &(cCondTMH)
			//Verifica se usuario precisa responder a pergunta
			aTipoTMH := {}
			If !Empty( &cComboRes )
				aTipoTMH := fRetCombo(Alltrim( &cComboRes ))
			Endif
			aTemp    := Array(Len(aTipoTMH),3)
			For nXX := 1 To Len(aTemp)
				If (TMH->TMH_TPLIST=="1")
				aTemp[nXX,1] := 0
			Else
				aTemp[nXX,1] := .F.
			Endif
				If nOpcx != 3
				dbSelectArea("TO6")
				dbSetOrder(nOrdTO6)
				If dbSeek( xFilial("TO6") + cSOrd + TMH->TMH_QUESTA +SubStr(aTipoTMH[nXX],1,1) )
					If (TMH->TMH_TPLIST=="1")
					aTemp[nXX,1] := 1
				Else
					aTemp[nXX,1] := .T.
				Endif
					Endif
				Else
				If SubStr(aTipoTMH[nXX],1,1) == TMH->TMH_DEFAUL
					If (TMH->TMH_TPLIST=="1")
						aTemp[nXX,1] := 1
					Else
						aTemp[nXX,1] := .T.
					Endif
				Endif
			Endif
		Next nXX

			//Se não for inclusão
			cMemoM6 := ""
			If nOpcx != 3
				dbSelectArea("TO6")
				dbSetOrder(nOrdTO6)
				If dbSeek( xFilial("TO6") + cSOrd + TMH->TMH_QUESTA + "#" )
					cMemoM6 := Alltrim(TO6->TO6_DESCRI)
				Endif
			Endif
			//1 - Codigo Questão
			//2 - Descrição Questão
			//3 - Grupo
			//4 - Array de Opções
			//5 - Cbox
			//6 - Indica se é RADIO (.T.) ou CHECK (.F.)
			//7 - Indica se tem campo Memo
			//8 - Array (respostas,objeto)
			//9 - Ordem
			//10- Campo Memo
		dbSelectArea("TM0")
		dbSetOrder(1)
			If dbSeek(xFilial("TM0")+TNC->TNC_NUMFIC)
				cSexo := TM0->TM0_SEXO
			EndIf

			If Empty(TMH->TMH_INDSEXO) .Or. Empty(cSexo)
				aADD( aCadTipo , { TMH->TMH_QUESTA , Capital(TMH->TMH_PERGUN) , TMH->TMH_CODGRU , aTipoTMH ,;
				&cComboRes , (TMH->TMH_TPLIST=="1") ,;
				(TMH->TMH_ONMEMO=="1") , aTemp , TMH->TMH_ORDEM , cMemoM6 } )
				nQuesti += 1
			ElseIf cSexo ==  TMH->TMH_INDSEXO .Or. 	TMH->TMH_INDSEXO == "3"
			aADD( aCadTipo , { TMH->TMH_QUESTA , Capital(TMH->TMH_PERGUN) , TMH->TMH_CODGRU , aTipoTMH ,;
				&cComboRes , (TMH->TMH_TPLIST=="1") ,;
				(TMH->TMH_ONMEMO=="1") , aTemp , TMH->TMH_ORDEM , cMemoM6 } )
				nQuesti += 1
		EndIf

			dbSelectArea("TMH")
			dbSkip()
		End
		If nQuesti == 0
			If Altera .Or. Inclui
				MsgStop(STR0040) //"Não existe nenhuma pergunta para o sexo da ficha médica."
				Return .F.
			EndIf
		EndIf
		aSort( aCadTipo ,,, { |x,y| x[9] < y[9] } )

		If Len(aCadTipo) > 0
			oIndNG:=Array(Len(aCadTipo),3)
			cIndNG:=Array(Len(aCadTipo))
		Endif

		aAreaTO6 := {}
		cChvTmp  := xFilial("TO6") + TO6->TO6_ACIDEN
		dbSelectArea("TO6")
		dbSetOrder(nOrdTO6)
		dbSeek( cChvTmp )
		While !Eof() .And. cChvTmp == TO6->TO6_FILIAL+TO6->TO6_ACIDEN
			If Replicate("#",Len(TO6->TO6_QUESTA)) == TO6->TO6_QUESTA
				If aScan(aAreaTO6, {|x| x == TO6->TO6_DTREAL }) == 0
					aAdd( aAreaTO6 , TO6->TO6_DTREAL )
				Endif
			Endif
			dbSelectArea("TO6")
			dbSkip()
		End
		aSort( aAreaTO6 ,,, { |x,y| x > y } )
		For nXX := 1 To Len(aAreaTO6)
			If nXX == 1
				cObsAnt := ""
			Endif
			cObsAnt += DtoC(aAreaTO6[nXX]) + "  "
		Next nXX

		nFatMlt := 1.95
		If aPosObj[1,4] <= 410
			nFatMlt := 1.9
		ElseIf aPosObj[1,4] <= 550
			nFatMlt := 1.93
		Endif

		DEFINE MSDIALOG oDlgInd TITLE OemToAnsi(cCadastro) From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL
		oDlgInd:LESCCLOSE := .F.

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ PAGINA 2 - TO6                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nLinObj := 2
		nTam := 12
		@ 0,0 SCROLLBOX oScroll VERTICAL OF oDlgInd BORDER
		oScroll:Align := CONTROL_ALIGN_ALLCLIENT

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Titulo do Grupo                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPanelTmp := TPaintPanel():new(nLinObj-1,-4,aPosObj[1,4]+5,14,oScroll)
		oPanelTmp:addShape("id=2;type=2;left=0;top=0;width="+Alltrim(Str(aPosObj[1,4]*2.2,5))+";height=28;"+;
					"gradient=1,0,0,0,36,0.0,#808090,0.1,#808090,1.0,#FFFFFF;pen-width=1;"+;
					"pen-color=#B0C4DE;can-move=0;can-mark=0;is-blinker=1;")
		@ 2, 11 SAY oGrpNome Prompt TNC->TNC_DESACI PIXEL OF oPanelTmp Font oFont16 COLOR CLR_WHITE
		@ 2, (aPosObj[1,4]/2) SAY oGrpDtEx Prompt SubStr(DtoC(M->TO6_DTREAL),1,6)+StrZero(Year(M->TO6_DTREAL),4) PIXEL OF oPanelTmp Font oFont16 COLOR CLR_WHITE
		@ 2, aPosObj[1,4]-120 SAY oGrpQues Prompt Space(20) PIXEL OF oPanelTmp Font oFont16 COLOR CLR_WHITE

		nLinObj += 25

		cOldGrupo := "#"
		For nYY := 1 to Len(aCadTipo)
			If cOldGrupo <> aCadTipo[nYY,3]
				cOldGrupo := aCadTipo[nYY,3]
				cDesGrupo := " "
				dbSelectArea("TK0")
				dbSetOrder(01)
				If dbSeek( xFilial("TK0") + cOldGrupo )
					If !Empty(TK0->TK0_DESCRI)
						cDesGrupo := Capital( Alltrim(TK0->TK0_DESCRI) )
					Endif
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Titulo do Grupo                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oPanelTmp := TPaintPanel():new(nLinObj-1,0,aPosObj[1,4],10,oScroll)
				oPanelTmp:addShape("id=1;type=1;left=0;top=0;width="+Alltrim(Str(aPosObj[1,4]*2,5))+";height=20;"+;
					"gradient=1,0,0,0,15,0.0,#FFFFFF,0.1,#FFFFFF,1.0,#FFFFFF;pen-width=1;"+;
					"pen-color=#FFFFFF;can-move=0;can-mark=0;is-blinker=1;")

				oPanelTmp:addShape("id=2;type=2;left=10;top=0;width="+Alltrim(Str(aPosObj[1,4]*nFatMlt,5))+";height=20;"+;
					"gradient=1,0,0,0,15,0.0,#FFFFFF,0.1,#FDFBFD,1.0,#CDD1D4;pen-width=1;"+;
					"pen-color=#B0C4DE;can-move=0;can-mark=0;is-blinker=1;")

				@ 1, 11 SAY oIndNG[nYY,3] Prompt Space(40) PIXEL OF oPanelTmp Font oFont12
				oIndNG[nYY,3]:SetText(cDesGrupo)
				nLinObj += 13
			Endif

			cStrYY := Alltrim( Str(nYY) )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Titulo Questão                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nPosOld := nLinObj+7
			@ nLinObj, 009 SAY oIndNG[nYY,1] Prompt Space(40) PIXEL OF oScroll Font oFont12
			oIndNG[nYY,1]:bSetGet := ;
				&("{|u| If(pCount() == 0, aCadTipo["+cStrYY+",2], aCadTipo["+cStrYY+",2]:=u)}")
			oIndNG[nYY,1]:SetText(aCadTipo[nYY,2])

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Montando lista de opcoes (radio ou check)      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nLimCol := aSize[5]/7
			nAcumLi := 0
			For nXX := 1 To Len(aCadTipo[nYY,4])
				cStrXX := Alltrim( Str(nXX) )
				cDescBox := SubStr(aCadTipo[nYY,4,nXX],3)
				If (nAcumLi + Len(cDescBox) + 5) > nLimCol .Or. nXX == 1
					nLinObj += 9
					nAcumLi := 0
				Endif
				If aCadTipo[nYY,6]
					aCadTipo[nYY,8,nXX,2] := TBtnBmp2():New( nLinObj*2,26+(nAcumLi*7),14,14,;
					If(aCadTipo[nYY,8,nXX,1]==0,"ngradiono","ngradiook"),,,,{||},oScroll,,,.T. )
					@ nLinObj, 22+(nAcumLi*3.5) SAY aCadTipo[nYY,8,nXX,3] Prompt Space(30) PIXEL OF oScroll
					aCadTipo[nYY,8,nXX,3]:SetText(cDescBox)
					If lVisual
						aCadTipo[nYY,8,nXX,2]:lReadOnly := .T.
					Else
					aCadTipo[nYY,8,nXX,3]:bLClicked := &("{|| fRadMdt690("+cStrYY+","+cStrXX+") }")
						aCadTipo[nYY,8,nXX,2]:bAction := &("{|| fRadMdt690("+cStrYY+","+cStrXX+") }")
					Endif
				Else
					aCadTipo[nYY,8,nXX,2] := TCheckBox():New(nLinObj-1,13+(nAcumLi*3.5),cDescBox,,oScroll,13+(Len(cDescBox)*3.5),7,,,,,,,,.T.)
					aCadTipo[nYY,8,nXX,2]:bSetGet := &("{|u| If(PCount()==0,aCadTipo["+cStrYY+",8,"+cStrXX+",1],aCadTipo["+cStrYY+",8,"+cStrXX+",1]:=u)}")
					If lVisual
						aCadTipo[nYY,8,nXX,2]:lReadOnly := .T.
					Endif
				Endif
				nAcumLi += Len(cDescBox) + 5
			Next nXX

			If aCadTipo[nYY,7]
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Campo Memo                                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nLinObj += 10
				oIndNG[nYY,2] := TMultiget():New(nLinObj,12,,oScroll,aPosObj[1,4]-30,18,,,,,,.T.)
				oIndNG[nYY,2]:EnableHScroll(.T.)
				oIndNG[nYY,2]:EnableVScroll(.T.)
				oIndNG[nYY,2]:bWhen := &("{ || MDT690OBS("+ cStrYY +") }") //Função para atualizar o When do campo Memo
				oIndNG[nYY,2]:bSetGet := ;
					&("{|u| If(pCount() == 0, aCadTipo["+cStrYY+",10], aCadTipo["+cStrYY+",10]:=u)}")
				nLinObj += 12
				If lVisual
					oIndNG[nYY,2]:lReadOnly := .T.
				Endif
			Endif
			nLinObj += 13
			@ nPosOld,09 TO nLinObj-3,aPosObj[1,4]-15 OF oScroll PIXEL

		Next nYY

	//	nLinObj += 16
	//	@ nLinObj, 082 BUTTON oBtnImASO Prompt STR0032 Size 65,10 Of oScroll Pixel Action fImpRelat()//"Imprimir Questionário"
		nLinObj += 13
		@ nLinObj, 012 SAY " " PIXEL OF oScroll

		ACTIVATE MSDIALOG oDlgInd ON INIT EnchoiceBar(oDlgInd,{|| nOpca := 1,oDlgInd:End()},{|| nOpca := 2,oDlgInd:End()},,aBtn_Topo)

		If nOpca == 1
			Begin Transaction
				MDTAGrv690(nOpcx,2)
			End Transaction
		Endif
	Endif

	aRotina := aClone(aOldRot)

	If nOpcx == 5
		aRotina[4,4] := 2
	EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDTA690TO6³ Autor ³ NG INFORMATICA        ³ Data ³01/10/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tela de cadastro da tabela TO6                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MDTA145                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDTA690TO6(lWhenTO6,nOpcx__)

	Local nOpTemp := 0
	Local aSitExa := {STR0032,STR0033}//"1=Ativo"##"2=Inativo"

	DEFINE MSDIALOG oDlgPar TITLE OemToAnsi(cCadastro) From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL
	oDlgPar:LESCCLOSE := .F.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ PAGINA 1 - TO6                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nLinObj := 2
	@ 0,0 SCROLLBOX oScroll2 VERTICAL OF oDlgPar BORDER
	oScroll2:Align := CONTROL_ALIGN_ALLCLIENT

	@ 28, 012 SAY oS_DtExa Prompt STR0034 PIXEL OF oScroll2 Font oFont12//"Data"
	@ 28, 085 MSGET oG_DtExa VAR M->TO6_DTREAL SIZE 60,9 VALID f690Data(M->TO6_DTREAL) WHEN lWhenTO6 PIXEL OF oScroll2 HasButton
	@ 42, 012 SAY oS_TpExa Prompt STR0035 PIXEL OF oScroll2 Font oFont12//"Código do Questionário"
	@ 42, 085 MSGET oG_TpExa VAR M->TO6_QUESTI SIZE 60,8 WHEN lWhenTO6 VALID VALRegis() F3 If(lSigaMdtps,"MDTTMG","TMG") PIXEL OF oScroll2 HasButton


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Titulo do Grupo                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oPanelTmp := TPaintPanel():new(nLinObj-1,-4,aPosObj[1,4]+5,14,oScroll2)
	oPanelTmp:addShape("id=2;type=2;left=0;top=0;width="+Alltrim(Str(aPosObj[1,4]*2.2,5))+";height=28;"+;
                "gradient=1,0,0,0,36,0.0,#808090,0.1,#808090,1.0,#FFFFFF;pen-width=1;"+;
                "pen-color=#B0C4DE;can-move=0;can-mark=0;is-blinker=1;")
	@ 2, 11 SAY oGrpNome Prompt TNC->TNC_DESACI PIXEL OF oPanelTmp Font oFont16 COLOR CLR_WHITE

	oS_DtExa:SetFocus()
	ACTIVATE MSDIALOG oDlgPar ON INIT EnchoiceBar(oDlgPar,{|| If(fNextPnl(.T.) .And. f690Data(M->TO6_DTREAL),(nOpTemp := 1,oDlgPar:End()),nOpTemp := 0) },;
														  {|| nOpTemp := 2,oDlgPar:End()})

Return nOpTemp

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ f690Data ³ Autor ³ NG INFORMATICA        ³ Data ³01/10/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida data do questionario                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MDTA145                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function f690Data(dTO6_DTREAL)

	If dTO6_DTREAL > dDataBase
		Help(" ",1,"NGATENCAO",,STR0036,3,1) //"A data do questionário não pode ser maior que a data atual."
		Return .F.
	Endif
	If !NaoVazio(dTO6_DTREAL)
		Return .F.
	Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDTAGrv690³ Autor ³ NG INFORMATICA        ³ Data ³01/10/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao chamada para gravacao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MDTA690                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MDTAGrv690(nOpcx,nTipo)

	Local ny,nx,nOrdT,cOrd,nXX,nYY,nZZ
	Local aTO6s := {}
	nOrdT := NGRETORDEM("TO6","TO6_FILIAL+TO6_ACIDEN+TO6_QUESTI+DTOS(TO6_DTREAL)+TO6_QUESTA+TO6_RESPOS",.F.)

	If lSigaMdtPs
		nOrdT := NGRETORDEM("TO6","TO6_FILIAL+TO6_CLIENT+TO6_LOJA+TO6_ACIDEN+TO6_QUESTI+DTOS(TO6_DTREAL)+TO6_QUESTA+TO6_RESPOS",.F.)
	Endif

	If nOpcx == 3 .Or. nOpcx == 4
		If nTipo == 2
			For nYY := 1 to Len(aCadTipo)
				aRespost := {}
				For nXX := 1 To Len(aCadTipo[nYY,4])
					cStrXX := Alltrim( Str(nXX) )
					If aCadTipo[nYY,6] //Radio
						If ValType(aCadTipo[nYY,8,nXX,1]) == "N"
							If aCadTipo[nYY,8,nXX,1] == 1
								aAdd( aRespost , aCadTipo[nYY,4,nXX] )
							Endif
						Endif
					Else //Check
						If ValType(aCadTipo[nYY,8,nXX,1]) == "L"
							If aCadTipo[nYY,8,nXX,1]
								aAdd( aRespost , aCadTipo[nYY,4,nXX] )
							Endif
						Endif
					Endif
				Next nXX
				If ValType(aCadTipo[nYY,10]) == "C" //Adiciona campo memo para gravação
					If !Empty(aCadTipo[nYY,10])
						aAdd( aRespost , "#" )
					Endif
				Endif

				//Se encontrou informação para a pergunta, grava
				For nZZ := 1 To Len(aRespost)
					dbSelectArea("TO6")
					dbSetOrder(nOrdT)
					If lSigaMdtPs
						cSeek := cCliMdtPs+TNC->TNC_ACIDEN+M->TO6_QUESTI+DTOS(M->TO6_DTREAL)
					Else
						cSeek := TNC->TNC_ACIDEN+M->TO6_QUESTI+DTOS(M->TO6_DTREAL)
					Endif
					If dbSeek( xFilial("TO6") + cSeek + aCadTipo[nYY,1]+aRespost[nZZ])
						RecLock("TO6",.F.)
						TO6->TO6_RESPOS := aRespost[nZZ]
					Else
						RecLock("TO6",.T.)
						TO6->TO6_FILIAL := xFilial("TO6")
						TO6->TO6_ACIDEN := TNC->TNC_ACIDEN
						TO6->TO6_QUESTI := M->TO6_QUESTI
						TO6->TO6_DTREAL := M->TO6_DTREAL
						TO6->TO6_QUESTA := aCadTipo[nYY,1]
						TO6->TO6_RESPOS := aRespost[nZZ]
						If lSigaMdtps
							TO6->TO6_CLIENT := SA1->A1_COD
							TO6->TO6_LOJA   := SA1->A1_LOJA
						Endif
					Endif
					If aRespost[nZZ] == "#"
						TO6->TO6_DESCRI := aCadTipo[nYY,10]
					Endif
					TO6->(MsUnLock())
					aAdd(aTO6s , TO6->(Recno()) )
				Next nZZ
			Next nYY

			//Inclui ou altera o cadastro de itens do Tipo de Ficha
			dbSelectArea("TO6")
			dbSetOrder(nOrdT)
			If lSigaMdtPs
				cSeek  := cCliMdtPs +M->TO6_ACIDEN+ M->TO6_QUESTI+DTOS(M->TO6_DTREAL)
				cWhile := "xFilial('TO6') == TO6->TO6_FILIAL .And. cCliMdtPs == TO6->TO6_CLIENT+TO6->TO6_LOJA .And. M->TO6_ACIDEN+M->TO6_QUESTI+DTOS(M->TO6_DTREAL) == TO6->TO6_ACIDEN+TO6->TO6_QUESTI+DTOS(TO6->TO6_DTREAL)"
			Else
				cSeek  := M->TO6_ACIDEN+M->TO6_QUESTI+DTOS(M->TO6_DTREAL)
				cWhile := "xFilial('TO6') == TO6->TO6_FILIAL .And. M->TO6_ACIDEN+M->TO6_QUESTI+DTOS(M->TO6_DTREAL) == TO6->TO6_ACIDEN+TO6->TO6_QUESTI+DTOS(TO6->TO6_DTREAL)"
		Endif
		If dbSeek( xFilial("TO6") + cSeek)
				While !Eof() .And. &cWhile
					If TO6->TO6_QUESTA == Replicate("#",Len(TO6->TO6_QUESTA)) .Or. TO6->TO6_QUESTA == Replicate("@",Len(TO6->TO6_QUESTA))
						dbSelectArea("TO6")
						dbSkip()
						Loop
					Endif
					If aScan(aTO6s, {|x| x == TO6->(Recno()) }) == 0
						dbSelectArea("TO6")
						RecLock("TO6",.F.)
						dbDelete()
						TO6->(MsUnLock())
					Endif
					dbSelectArea("TO6")
					dbSkip()
				End
			Endif
		Endif

	ElseIf nOpcx == 5
		dbSelectArea("TO6")
		dbSetOrder(nOrdT)
		If lSigaMdtPs
			cSeek := cCliMdtPs +M->TO6_ACIDEN+ M->TO6_QUESTI+ DTOS(M->TO6_DTREAL)
			cWhile:= "xFilial('TO6') == TO6->TO6_FILIAL .And. cCliMdtPs == TO6->TO6_CLIENT+TO6->TO6_LOJA .And. M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI == TO6->TO6_ACIDEN+DTOS(TO6->TO6_DTREAL)+TO6->TO6_QUESTI"
		Else
			cSeek := M->TO6_ACIDEN + M->TO6_QUESTI + DTOS(M->TO6_DTREAL)
			cWhile:= "xFilial('TO6') == TO6->TO6_FILIAL .And. M->TO6_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI == TO6->TO6_ACIDEN+DTOS(TO6->TO6_DTREAL)+TO6->TO6_QUESTI "
		Endif
		If dbSeek( xFilial("TO6") + cSeek)
			While !Eof() .And. &cWhile
				dbSelectArea("TO6")
				RecLock("TO6",.F.)
				dbDelete()
				TO6->(MsUnLock())
				dbSkip()
			End
		Endif
	Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  fRetCombo³ Autor ³ Denis Hyroshi de Souza³ Data ³07/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se a formula esta correta e faz a gravacao         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fRetCombo(cVar)

	Local aArray1 := RetSx3Box(cVar,,,1)
	Local nCont,aArray2 := {}

	For nCont := 1 To Len(aArray1)
		If !Empty(aArray1[nCont][1])
			AADD(aArray2,Alltrim(aArray1[nCont][1]))
		Endif
	Next nCont

Return aClone(aArray2)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  fNextPnl ³ Autor ³ Denis Hyroshi de Souza³ Data ³07/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ir a proxima tela                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fNextPnl(lIncReg)

	If Empty(M->TO6_DTREAL)
		MsgInfo(STR0037)//"O campo Data do exame é obrigatório."
		Return .F.
	Endif
	If Empty(M->TO6_QUESTI)
		MsgInfo(STR0038)//"O campo Tipo de exame é obrigatório."
		Return .F.
	Endif
	If !ExistChav( "TO6" , TNC->TNC_ACIDEN + M->TO6_QUESTI + DTOS(M->TO6_DTREAL) )
		Return .F.
	Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fRadMdt690 ³ Autor ³ Denis Hyroshi de Souza³ Data ³07/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao do Radio Group                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fRadMdt690(nTmpYY,nTmpXX)

	Local nX

	For nX := 1 To Len(aCadTipo[nTmpYY,8])
		If nX == nTmpXX
			aCadTipo[nTmpYY,8,nX,1] := 1
			aCadTipo[nTmpYY,8,nX,2]:LoadBitmaps("ngradiook")
		Else
			If aCadTipo[nTmpYY,8,nX,1] <> 0
				aCadTipo[nTmpYY,8,nX,1] := 0
				aCadTipo[nTmpYY,8,nX,2]:LoadBitmaps("ngradiono")
			Endif
		Endif
	Next nX

	oScroll:Refresh()

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fImpRelat ³ Autor ³ Denis Hyroshi de Souza³ Data ³07/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime Ficha Medica Ocupacional                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fImpRelat()

	Local aOldTipo := aClone(aCadTipo)

	Private nPag410 := 0
	Private oPrint
	Private nSizeTMH := (TAMSX3("TMH_PERGUN")[1])
	Private lPrintTel := .T.
	nSizeTMH := If( nSizeTMH > 0 , nSizeTMH , 60 )

	If lSigaMdtps
		MV_PAR01 := SA1->A1_COD
		MV_PAR02 := SA1->A1_LOJA
		MV_PAR03 := M->TO6_QUESTI
		MV_PAR04 := TNC->TNC_ACIDEN
		MV_PAR05 := TNC->TNC_ACIDEN
		MV_PAR06 := M->TO6_DTREAL
		MV_PAR07 := M->TO6_DTREAL
		MV_PAR08 := 1
	Else
		MV_PAR01 := M->TO6_QUESTI
		MV_PAR02 := TNC->TNC_ACIDEN
		MV_PAR03 := TNC->TNC_ACIDEN
		MV_PAR04 := M->TO6_DTREAL
		MV_PAR05 := M->TO6_DTREAL
		MV_PAR06 := 1
	Endif
	cDESQUEST := ALLTRIM(TMG->TMG_NOMQUE)
	fImpMod4(aCadTipo)

	aCadTipo := aClone(aOldTipo)

	//Inicializa a Pergunta de Bloqueio
	Pergunte( "MDT690A" , .F. )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VALRegis  ºAutor  ³Taina A. Cardoso    º Data ³  10/11/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Validacao para nao ter duplicacao de registro.              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MDTA145                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function VALRegis()

	Private nOrdem

	dbSelectArea("TO6")
	If lSigaMdtPs
		nOrdem := NGRETORDEM("TO6","TO6_FILIAL+TO6_CLIENT+TO6_LOJA+TO6_ACIDEN+TO6_QUESTI+DTOS(TO6_DTREAL)",.F.)
		cSTO6	 := cCliMdtPs+TNC->TNC_ACIDEN+M->TO6_QUESTI+DTOS(M->TO6_DTREAL)
	Else
		nOrdem := NGRETORDEM("TO6","TO6_FILIAL+TO6_ACIDEN+DTOS(TO6_DTREAL)+TO6_QUESTI",.F.)
	cSTO6	 := TNC->TNC_ACIDEN+DTOS(M->TO6_DTREAL)+M->TO6_QUESTI
	Endif
	dbSetORder(nOrdem)
	If dbSeek(xFilial("TO6")+cSTO6)
		MsgStop(STR0039)//"Já existe um Questionário para essa Data"
		Return .F.
	EndIf

	If !MDT690VQTI()
		Return .F.
	EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A690CALLBRºAutor  ³Roger Rodrigues     º Data ³  10/12/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Faz chamada da funcao de cadastro                           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MDTA055                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A690CALLBR(cAlias, nRecno, nOpcx)

	If Empty(TNC->TNC_ACIDEN)
		ShowHelpDlg( STR0019 , { STR0041 } , 2 , { STR0042 } , 2 )//"ATENÇÃO"##"Não existe um acidente cadastrado."##"Favor, cadastrar primeiro um acidente."
	Else
		MDTA690ACI(cAlias, nRecno, nOpcx)
	Endif

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MDT690OBS
Habilita campo de observação se opção da Resposta for 'Sim' ou
se for varias opções para selecionar.

@Param nPosObj - Posição do Objeto

@author Jean Pytter da Costa
@since 27/07/2015
@return lRet
/*/
//---------------------------------------------------------------------
Static Function MDT690OBS( nPosObj )

	Local lRet := .F.
	Local nOpcoes := Len( aCadTipo[ nPosObj , 4 ] ) //Variavel para verificar se possui opções para escolher

	If nOpcoes > 0 .And. mv_par01 == 1
		nPosYes := aScan( aCadTipo[ nPosObj , 4 ] , { |x| UPPER( STR0022 ) $ AllTrim( UPPER( x ) ) } ) //"Sim"###Verifica se Sim esta nas opções
		If nPosYes > 0
			If ValType( aCadTipo[ nPosObj , 8 , nPosYes , 1 ] ) == "N" //Verifica se a posição é numérica
				lRet := aCadTipo[ nPosObj , 8 , nPosYes , 1 ] == 1
				If !lRet //Se for a opção Não, vai limpar Memo
					aCadTipo[ nPosObj , 10] := ""
				EndIf
			EndIf
		EndIf
		If ValType( aCadTipo[ nPosObj , 8 , 1 , 1 ] ) == "L" //Verifica se a posição é Lógica
			lRet := .T. //Se for Opção Exclusiva, e a selecionada for diferente de 'Sim'.
		EndIf
	Else
	   lRet := .T.
	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} MDT690OBD
Função responsavel por habilitar o preenchimento do campo de Observação,
mesmo não existindo campos de perguntas.

@Param cPerg - Pergunta utilizada.

@author Guilherme Freudenburg
@since 09/05/2016
@return lRet
/*/
//---------------------------------------------------------------------
Static Function MDT690OBD(cPerg)

	Local nBck := 0 //Backup para a opção selecionada
	Local lPerg := .F. //Determina se foi alterado a opção

	//Cria Backup da opção selecionada atualmente
	nBck := mv_par01

	lPerg:= Pergunte( cPerg , .T. )

	If !lPerg
		mv_par01 := nBck //Caso não confirme será retorna a opção anterior
	Endif

Return