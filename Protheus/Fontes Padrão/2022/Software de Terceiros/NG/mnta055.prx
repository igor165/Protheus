#INCLUDE "MNTA055.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA055
Cadastro de Localização

@author Vitor Emanuel Batista
@since 10/02/2012
@version P11
@return Nil
/*/
//---------------------------------------------------------------------
Function MNTA055()

	Local oBrowse

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 19, 95 )

		//Caso possua o ponto de entrada 'MNTA0551' deverá trocar para o modelo MVC.
		If FindFunction('U_MNTA0551') .And. !FindFunction('U_MNTA055')
			MsgInfo( STR0008, STR0007 ) // 'O ponto de entrada "MNTA0551" deve ser alterado conforme o novo modelo. Favor pesquisar a ISSUE MNG-6281 no TDN.'##'PONTO DE ENTRADA MNTA0551'
		EndIf

		oBrowse := FWMBrowse():New()
			oBrowse:SetAlias("TPS")					// Alias da tabela utilizada
			oBrowse:SetMenuDef("MNTA055")			    // Nome do fonte onde esta a função MenuDef
			oBrowse:SetDescription(STR0006)  	    // Descrição do browse //"Localizacao"
			oBrowse:Activate()

	EndIf

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Opções de menu

@author Vitor Emanuel Batista
@since 10/02/2012
@version P11
@return aRotina - Estrutura
	[n,1] Nome a aparecer no cabecalho
	[n,2] Nome da Rotina associada
	[n,3] Reservado
	[n,4] Tipo de Transação a ser efetuada:
		1 - Pesquisa e Posiciona em um Banco de Dados
		2 - Simplesmente Mostra os Campos
		3 - Inclui registros no Bancos de Dados
		4 - Altera o registro corrente
		5 - Remove o registro corrente do Banco de Dados
		6 - Alteração sem inclusão de registros
		7 - Cópia
		8 - Imprimir
	[n,5] Nivel de acesso
	[n,6] Habilita Menu Funcional
/*/
//---------------------------------------------------------------------
Static Function MenuDef()
//Inicializa MenuDef com todas as opções
Return FWMVCMenu( 'MNTA055' )


//---------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author Vitor Emanuel Batista
@since 10/02/2012
@version P11
@return Nil
/*/
//---------------------------------------------------------------------
Static Function ModelDef()

	Local oModel

	Local oStructTPS

	oStructTPS := FWFormStruct(1,"TPS")

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTA055",/*bPre*/,/*bValid*/,/*bCommit*/,/*bCancel*/)

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTA055_TPS", Nil, oStructTPS,/*bPre*/,/*bPost*/,/*bLoad*/)

	//Descrição do Model
	oModel:SetDescription(STR0006)

Return oModel

//---------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author Vitor Emanuel Batista
@since 10/02/2012
@version P11
@return Nil
/*/
//---------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel("MNTA055")
	Local oView  := Nil

	oStruTPS := FWFormStruct(2,"TPS")

	oView := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA055_TPS" , oStruTPS , /*cLinkID*/ )	//

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100,/*cIDOwner*/,/*lFixPixel*/,/*cIDFolder*/,/*cIDSheet*/ )

	// Associa um View a um box
	oView:SetOwnerView( "MNTA055_TPS" , "MASTER" )

	//Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn(oView)


Return oView
