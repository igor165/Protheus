#INCLUDE "MNTA110.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

#DEFINE _nVERSAO 1 //Versao do fonte
//-----------------------------------------------------------
/*/{Protheus.doc} MNTA110
Programa de manutencao de Tipos de Manutencao

@author Pablo Servin
@since 07/04/2014
@version MP11
@return Nil
/*/
//-------------------------------------------------------------
Function MNTA110()
		
	Local aNGBEGINPRM := NGBEGINPRM( _nVERSAO, "MNTA110" )
	Local oBrowse
	
	// Altera a TQD para se adaptar ao padrão MVC.
	fMNT110ALTTQD()
	
	oBrowse := FWMBrowse():New()
		
	oBrowse:SetAlias( "STE" ) // Alias da tabela utilizada
	oBrowse:SetMenuDef( "MNTA110" ) // Nome do fonte onde esta a função MenuDef
	oBrowse:SetDescription( STR0009 )  // Descrição do browse // "Tipos de Manutencao"
	oBrowse:Activate()
	
	NGRETURNPRM(aNGBEGINPRM)

Return Nil

//-----------------------------------------------------------
/*/{Protheus.doc} MNTA110
Regras da modelagem da gravação

@author Pablo Servin
@since 07/04/2014
@version MP11
@return oModel
/*/
//-----------------------------------------------------------
Static Function ModelDef()

	Local oStruct110 := FWFormStruct( 1, "STE" )
	Local oModel
	
	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New( "MNTA110" )
	
	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields( "MNTA110_STE", Nil, oStruct110 )
	oModel:SetPrimaryKey( {} )
	oModel:GetModel( "MNTA110_STE" ):SetDescription( STR0009 )

Return oModel

//-----------------------------------------------------------
/*/{Protheus.doc} ViewDef
Interface da rotina

@author Pablo Servin
@since 07/04/2014
@version MP11
@return oView
/*/
//-------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel( "MNTA110" )
	Local oStruct110 := FWFormStruct( 2, "STE" )
	Local oView	
	
	oView := FWFormView():New()
	// Objeto do model a se associar a view.
	oView:SetModel( oModel )	
	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA110_STE", oStruct110, Nil )
	// Criar um "box" horizontal para receber elementos da view
	oView:CreateHorizontalBox( "TELA", 100 )
	// Associa um View a um box
	oView:SetOwnerView( "MNTA110_STE", "TELA" )

	NGMVCUserBtn( oView )

Return oView

//-----------------------------------------------------------
/*/{Protheus.doc} MenuDef
Interface da rotina

@author Pablo Servin
@since 07/04/2014
@version MP11
@return FWMVCMenu( "MNTA110" )
/*/
//-----------------------------------------------------------
Static Function MenuDef()
Return FWMVCMenu( "MNTA110" )

//-----------------------------------------------------------
/*/{Protheus.doc} MenuDef
Função que adapta o clique da direita ao padrão MVC.

@author Pablo Servin
@since 08/04/2014
@version MP11
@return FWMVCMenu( "MNTA110" )
/*/
//-----------------------------------------------------------
Static Function fMNT110ALTTQD()
	
	Local cPrograma := PadR("MNTA110", TAMSX3("TQD_PROGRA")[1])
	Local cFuncao := PadR("MNTC110", TAMSX3("TQD_FUNCAO")[1])
	
	dbSelectArea( "TQD" )
	dbSetOrder( 01 )
	
	If dbSeek( xFilial ( "TQD" ) + cPrograma + cFuncao )
		RecLock( "TQD", .F.)
		TQD->TQD_PARAM := "oView:GetValue( 'MNTA110_STE','TE_TIPOMAN' )"
		MsUnlock()
	EndIf

Return .T.

//-----------------------------------------------------------
/*/{Protheus.doc} MNT110VLDCPOS
Função que adapta o clique da direita ao padrão MVC.

@author Pablo Servin
@since 08/04/2014
@version MP11
@return lRet
/*/
//-----------------------------------------------------------
Function MNT110VLDCPOS( cCampo )

	Local lRet
	
	Do Case
		Case cCampo == 'TE_TIPOMAN'
			lRet := EXISTCHAV( "STE", M->TE_TIPOMAN )
		Case cCampo == 'TE_CARACTE'
			lRet := PERTENCE( "PCO" )
		OtherWise
			lRet := .T.
	End Case

Return lRet