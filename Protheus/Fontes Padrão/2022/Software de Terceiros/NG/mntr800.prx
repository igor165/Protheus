#INCLUDE "mntr800.ch"
#INCLUDE "PROTHEUS.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR800
Defeitos mais frequentes

@author  Elisangela Costa
@since   03/02/03
@version P12
/*/
//-------------------------------------------------------------------
Function MNTR800()

	Local oReport
	Local aArea := GetArea()

	//Alias Para Tabelas Temporárias
	Private cTRB  := GetNextAlias()
	Private cTRB1 := GetNextAlias()
	Private cTRB2 := GetNextAlias()
	Private cTRB3 := GetNextAlias()

	Private cPerg := "MNT800"

	If FindFunction("TRepInUse") .And. TRepInUse()

		Pergunte(cPerg, .T. )

		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetPortrait() //Default Retrato
		oReport:PrintDialog()
	Else
		MNTR800R3()
	EndIf
	Set Key VK_F9 To
	RestArea(aArea)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ Elisangela Costa      ³ Data ³ 12/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR800                                                    ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef()
	Local oReport
	Local oSection1, oSection2

	Pergunte(cPerg, .F. )

	//LAYOUT
	//MV_PAR11 = 1 (Centro de Custo)

	/*        1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	_____________________________________________________________________________________________________________________________________
	Defeitos Mais Frequentes
	_____________________________________________________________________________________________________________________________________
	Centro de Custos     Descricao
	_____________________________________________________________________________________________________________________________________
	xxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Prob.  Descricao            Quant.  Perc.   Causa  Descricao            Quant. Perc.   Sol.   Descricao            Quant. Perc.
	_____________________________________________________________________________________________________________________________________
	xxxxxx xxxxxxxxxxxxxxxxxxxx   9999  999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9
	xxxxxx xxxxxxxxxxxxxxxxxxxx   9999  999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9


	*/

	//MV_PAR11 = 2 (Familia)

	/*        1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	_____________________________________________________________________________________________________________________________________
	Defeitos Mais Frequentes
	_____________________________________________________________________________________________________________________________________
	Familia  Descricao
	_____________________________________________________________________________________________________________________________________
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Prob.  Descricao            Quant.  Perc.   Causa  Descricao            Quant. Perc.   Sol.   Descricao            Quant. Perc.
	_____________________________________________________________________________________________________________________________________
	xxxxxx xxxxxxxxxxxxxxxxxxxx   9999  999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9
	xxxxxx xxxxxxxxxxxxxxxxxxxx   9999  999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9


	*/

	//MV_PAR11 = 3 (Fabricante)

	/*        1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	_____________________________________________________________________________________________________________________________________
	Defeitos Mais Frequentes
	_____________________________________________________________________________________________________________________________________
	Fabricante  Descricao
	_____________________________________________________________________________________________________________________________________
	xxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Prob.  Descricao            Quant.  Perc.   Causa  Descricao            Quant. Perc.   Sol.   Descricao            Quant. Perc.
	_____________________________________________________________________________________________________________________________________
	xxxxxx xxxxxxxxxxxxxxxxxxxx   9999  999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9
	xxxxxx xxxxxxxxxxxxxxxxxxxx   9999  999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9


	*/

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR800",OemToAnsi(STR0006),cPerg,{|oReport| ReportPrint(oReport)},;
	STR0001+". "+STR0002+" "+STR0003)
	oReport:ParamReadOnly()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros!                        ³
	//³ mv_par01     // De Centro Custo                              ³
	//³ mv_par02     // Ate Centro Custo                             ³
	//³ mv_par03     // De Centro Trabalho                           ³
	//³ mv_par04     // Ate Centro Trabalho                          ³
	//³ mv_par05     // De Familia                                   ³
	//³ mv_par06     // Ate Familia                                  ³
	//³ mv_par07     // De Fabricante                                ³
	//³ mv_par08     // Ate Fabricante                               ³
	//³ mv_par09     // De data                                      ³
	//³ mv_par10     // Ate data                                     ³
	//³ mv_par11     // Tipo de Analise(Centro Custo                 ³
	//³                                  /Familia                    ³
	//³                                  /Fabricante                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If MV_PAR11 =1 //(Centro de Custos)
		oSection1 := TRSection():New(oReport,STR0014,{(cTRB),"SI3","SHB"})
		TRCell():New(oSection1,"(cTRB)->CCUSTO"  ,(cTRB),STR0014,"@!",20)  //"Centro Custo"
		TRCell():New(oSection1,"I3_DESC"      ,"SI3",STR0015,"@!",40)  //"Descrição"
		TRCell():New(oSection1,"(cTRB)->CENTRAB" ,(cTRB),STR0016,"@!",6)   //"Centro Trabalho"
		TRCell():New(oSection1,"HB_NOME"      ,"SHB",STR0015,"@!",40)  //"Descrição"
		TRPosition():New(oSection1,"SI3",1,{|| xFilial("SI3") + (cTRB)->CCUSTO})
		TRPosition():New(oSection1,"SHB",1,{|| xFilial("SHB") + (cTRB)->CENTRAB})
	ElseIf MV_PAR11 =2 //(Familia)
		oSection1 := TRSection():New(oReport,STR0022,{(cTRB),"ST6"})
		TRCell():New(oSection1,"(cTRB)->FAMILIA" ,(cTRB),STR0022,"@!",6)  //"Familia"
		TRCell():New(oSection1,"T6_NOME"      ,"ST6",STR0015,"@!",40) //"Descrição"
		TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6") + (cTRB)->FAMILIA})
	Else //Fabricante
		oSection1 := TRSection():New(oReport,STR0023,{(cTRB),"ST7"})
		TRCell():New(oSection1,"(cTRB)->FABRIC" ,(cTRB),STR0023,"@!",6)   //"Fabricante"
		TRCell():New(oSection1,"T7_NOME"     ,"ST7",STR0015,"@!",40)  //"Descrição"
		TRPosition():New(oSection1,"ST7",1,{|| xFilial("ST7") + (cTRB)->FABRIC})
	EndIf

	oSection2 := TRSection():New(oReport,STR0024,{(cTRB1),(cTRB2),(cTRB3),"ST8"}) //"Defeitos"
	oCell := TRCell():New(oSection2,"(cTRB1)->PROBLEMA1" ,(cTRB1),STR0017,"@!"       ,6 ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Prob."
	oCell := TRCell():New(oSection2,"NOMEPROB"        ,"   " ,STR0015,"@!"       ,20,/*lPixel*/,{|| MTRN800(1) }) //"Descrição"
	oCell := TRCell():New(oSection2,"(cTRB1)->QUANTDPRO" ,(cTRB1),STR0018,"9999"     ,4 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Quant."
	oCell := TRCell():New(oSection2,"PERCENPRO"       ,"   " ,STR0019,"@E 999.9" ,4 ,/*lPixel*/,{|| MTRPER800(1) })  //"Perc."
	oCell := TRCell():New(oSection2,"(cTRB2)->CAUSA2"    ,(cTRB2),STR0020,"@!"       ,6 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Causa"
	oCell := TRCell():New(oSection2,"NOMECAUSA"        ,"ST8",STR0015,"@!"       ,20,/*lPixel*/,{|| MTRN800(2)})  //"Descrição"
	oCell := TRCell():New(oSection2,"(cTRB2)->QUANTDCAU" ,(cTRB2),STR0018,"9999"     ,4 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Quant."
	oCell := TRCell():New(oSection2,"PERCENCAU"       ,"   " ,STR0019,"@E 999.9" ,4 ,/*lPixel*/,{|| MTRPER800(2) }) //"Perc."
	oCell := TRCell():New(oSection2,"(cTRB3)->SOLUCAO3"  ,(cTRB3),STR0021,"@!"       ,6 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Sol."
	oCell := TRCell():New(oSection2,"NOMESOLU"        ,"ST8" ,STR0015,"@!"       ,20,/*lPixel*/,{|| MTRN800(3)})  //"Descrição"
	oCell := TRCell():New(oSection2,"(cTRB3)->QUANTDSOL" ,(cTRB3),STR0018,"9999"     ,4 ,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Quant."
	oCell := TRCell():New(oSection2,"PERCENSOL"       ,"   " ,STR0019,"@E 999.9" ,4 ,/*lPixel*/,{|| MTRPER800(3) })  //"Perc."
	//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a direita
	oSection2:Cell("(cTRB1)->QUANTDPRO"):SetHeaderAlign("RIGHT")
	oSection2:Cell("PERCENPRO"):SetHeaderAlign("RIGHT")
	oSection2:Cell("(cTRB2)->QUANTDCAU"):SetHeaderAlign("RIGHT")
	oSection2:Cell("PERCENCAU"):SetHeaderAlign("RIGHT")
	oSection2:Cell("(cTRB3)->QUANTDSOL"):SetHeaderAlign("RIGHT")
	oSection2:Cell("PERCENSOL"):SetHeaderAlign("RIGHT")

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR800  ³ Autor ³ Elisangela Costa      ³ Data ³ 03/02/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Defeitos mais frequentes                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR800R3()

	Local cString    := "ST9"
	Local cdesc1     := STR0001 //"Relatorio de Defeitos Mais Frequentes"
	Local cdesc2     := STR0002 //"E permitido selecionar tipo de informacao a ser"
	Local cdesc3     := STR0003 //"apresentada, atraves dos parametros do relatorio."
	Local wnrel      := "MNTR800"

	Private aReturn  := { STR0004, 1,STR0005, 1, 2, 1, "",1 }  //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := STR0006 //"Defeitos Mais Frequentes"
	Private Tamanho  := "M"
	Private nomeprog := "MNTR800"
	Private cperg    := "MNT800"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros!                        ³
	//³ mv_par01     // De Centro Custo                              ³
	//³ mv_par02     // Ate Centro Custo                             ³
	//³ mv_par03     // De Centro Trabalho                           ³
	//³ mv_par04     // Ate Centro Trabalho                          ³
	//³ mv_par05     // De Familia                                   ³
	//³ mv_par06     // Ate Familia                                  ³
	//³ mv_par07     // De Fabricante                                ³
	//³ mv_par08     // Ate Fabricante                               ³
	//³ mv_par09     // De data                                      ³
	//³ mv_par10     // Ate data                                     ³
	//³ mv_par11     // Tipo de Analise(Centro Custo                 ³
	//³                                  /Familia                    ³
	//³                                  /Fabricante                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	Pergunte(cperg,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel:=SetPrint(cString,wnrel,cperg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

	If nLastKey = 27
		Set Filter To
		Return
	Endif

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R800Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return NIL
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R800Imp  ³ Autor ³ Elisangela Costa      ³ Data ³ 03/02/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR800                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function R800IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRodaTxt := ""
	Local nCntImpr := 0

	// Variaveis para criação da Tabela Temporária
	Local aINDR8001 := {}
	Local oARQTR8001
	Local oARQTR8002
	Local oARQTR8003
	Local oARQTR8004

	Private li := 80 ,m_pag := 1
	Private CABEC1 := STR0007 //"Prob.  Descricao            Quant. Perc.   Causa  Descricao            Quant. Perc.   Sol.   Descricao            Quant. Perc."
	Private CABEC2 := ""
	Private aVETINR := {}
	nTipo    := IIF(aReturn[4]==1,15,18)

	/*/                                                                                                    1         1         1         1
	IF MV_PAR11 = 1
	1         2         3         4         5         6         7         8         9         0         1         2         3
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
	______________________________________________________________________________________________________________________________________
	Prob.  Descricao            Quant. Perc.   Causa  Descricao            Quant. Perc.   Sol.   Descricao            Quant. Perc.
	______________________________________________________________________________________________________________________________________

	Centro de Custos..: xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx Centro de Trabalho..: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	**************************************************************************************************************************************
	xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9
	xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9   xxxxxx xxxxxxxxxxxxxxxxxxxx   9999 999,9


	IF MV_PAR11 = 2
	1         1         1         1
	1         2         3         4         5         6         7         8         9         0         1         2         3
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
	______________________________________________________________________________________________________________________________________
	Prob.  Descricao            Quant.Perc.   Causa  Descricao            Quant.Perc.   Sol.   Descricao            Quant.Perc.
	______________________________________________________________________________________________________________________________________

	Familia..: xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	**************************************************************************************************************************************
	xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99   xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999.99   xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99
	xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99   xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999.99   xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99


	IF MV_PAR11 = 3
	1         1         1         1
	1         2         3         4         5         6         7         8         9         0         1         2         3
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
	______________________________________________________________________________________________________________________________________
	Prob.  Descricao            Quant.Perc.   Causa  Descricao            Quant.Perc.   Sol.   Descricao            Quant.Perc.
	______________________________________________________________________________________________________________________________________

	Fabricate..: xxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	**************************************************************************************************************************************
	xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99   xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999.99   xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99
	xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99   xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999.99   xxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99
	/*/

	//TRB 1
	aDBFR8001 := {{"CCUSTO"   ,"C",Len(stj->tj_ccusto),0},;
				  {"CENTRAB"  ,"C",Len(stj->tj_centrab),0},;
				  {"FAMILIA"  ,"C",06,0},;
				  {"FABRIC"   ,"C",06,0},;
				  {"PROBLEMA" ,"C",06,0},;
				  {"CAUSA"    ,"C",06,0},;
				  {"SOLUCAO"  ,"C",06,0}}

	Do Case
		Case MV_PAR11 = 1
			aINDR8001  := {{"CCUSTO","CENTRAB","PROBLEMA"}}
		Case MV_PAR11 = 2
			aINDR8001  := {{"FAMILIA","PROBLEMA","CAUSA","SOLUCAO"}}
		OtherWise
			aINDR8001  := {{"FABRIC","PROBLEMA","CAUSA","SOLUCAO"}}
	End Do

	//Cria Tabela Temporária
	oARQTR8001 := NGFwTmpTbl(cTRB, aDBFR8001, aINDR8001)

	//TRB 2
	aDBFR8002 := {{"PROBLEMA1","C",06,0},;
				  {"QUANTDPRO","N",04,0}}

	//Cria Tabela Temporária
	oARQTR8002 := NGFwTmpTbl(cTRB1, aDBFR8002, {{"PROBLEMA1"}})

	//TRB 3
	aDBFR8003 := {{"PROBLEMA2","C",06,0},;
				  {"CAUSA2"   ,"C",06,0},;
				  {"QUANTDCAU","N",04,0}}

	//Cria Tabela Temporária
	oARQTR8003 := NGFwTmpTbl(cTRB2, aDBFR8003, {{"PROBLEMA2","CAUSA2"}})

	//TRB 4
	aDBFR8004 := {{"PROBLEMA3","C",06,0},;
				  {"CAUSA3"   ,"C",06,0},;
				  {"SOLUCAO3" ,"C",06,0},;
				  {"QUANTDSOL","N",04,0}}

	//Cria Tabela Temporária
	oARQTR8004 := NGFwTmpTbl(cTRB3, aDBFR8004, {{"PROBLEMA3","CAUSA3","SOLUCAO3"}})

	Processa({|lEND| MTR800TJTS()},STR0013)

	//+-----------------------+
	//| Inicio da Impressao   |
	//+-----------------------+
	DbSelectArea(cTRB)
	DbGotop()
	If MV_PAR11 = 1 //Se o tipo da analise for por Centro de Custo + Centro de Trabalho
		While !Eof()
			cCusto1 := (cTRB)->CCUSTO
			cCtrab1 := (cTRB)->CENTRAB
			nTProCC := 0 //Total dos Problemas por Centro de Custos
			While !Eof() .And. (cTRB)->CCUSTO = cCusto1  .And. (cTRB)->CENTRAB =cCtrab1

				nTProCC +=1
				GravaTrb() //Grava arquivos de trabalho

				DbSelectArea(cTRB)
				DbSkip()
			End
			//Faz a impressao dos dados do relatorio
			MTR800IMPD(nTProCC)
			DbSelectArea(cTRB)
		End
	ElseIf MV_PAR11 = 2 //Se o tipo da analise for por Familia de Bens
		While !Eof()
			cFamilia1 := (cTRB)->FAMILIA
			nTProFa   := 0   //Total dos Problemas por Familia
			While !Eof() .And. (cTRB)->FAMILIA = cFamilia1

				nTProFa +=1
				GravaTrb()//Grava arquivos de trabalho

				DbSelectArea(cTRB)
				DbSkip()
			End
			//Faz a impressao dos dados do relatorio
			MTR800IMPD(nTProFa)
			DbSelectArea(cTRB)
		End
	Else
		While !Eof()
			cFabric1 := (cTRB)->FABRIC
			nTProFabr   := 0   //Total dos Problemas por Fabricante
			While !Eof() .And. (cTRB)->FABRIC = cFabric1

				nTProFabr +=1
				GravaTrb()//Grava arquivos de trabalho

				DbSelectArea(cTRB)
				DbSkip()
			End
			//Faz a impressao dos dados do relatorio
			MTR800IMPD(nTProFabr)
			DbSelectArea(cTRB)
		End
	EndIf
	Roda(nCntImpr,cRodaTxt,Tamanho)

	//Deleta o arquivo temporario fisicamente
	oARQTR8001:Delete()
	oARQTR8002:Delete()
	oARQTR8003:Delete()
	oARQTR8004:Delete()

	//+---------------------------------------------------+
	//| Devolve a condicao original do arquivo principam  |
	//+---------------------------------------------------+
	RetIndex("STJ")
	RetIndex("STS")
	Set Filter To
	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif
	MS_FLUSH()
Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³  Elisangela Costa     ³ Data ³ 21/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR270                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)

	// Variaveis para criação da Tabela Temporária
	Local aINDR8001 := {}
	Local oARQTR8001
	Local oARQTR8002
	Local oARQTR8003
	Local oARQTR8004

	Private aVETINR := {}

	If !Empty(oReport:uParam)
		Pergunte(oReport:uParam,.F.)
	EndIf

	//TRB 1
	aDBFR8001 := {{"CCUSTO"   ,"C",Len(stj->tj_ccusto),0},;
				  {"CENTRAB"  ,"C",Len(stj->tj_centrab),0},;
				  {"FAMILIA"  ,"C",06,0},;
				  {"FABRIC"   ,"C",06,0},;
				  {"PROBLEMA" ,"C",06,0},;
				  {"CAUSA"    ,"C",06,0},;
				  {"SOLUCAO"  ,"C",06,0}}
	Do Case
		Case MV_PAR11 = 1
			aINDR8001  := {{"CCUSTO","CENTRAB","PROBLEMA"}}
		Case MV_PAR11 = 2
			aINDR8001  := {{"FAMILIA","PROBLEMA","CAUSA","SOLUCAO"}}
		OtherWise
			aINDR8001  := {{"FABRIC","PROBLEMA","CAUSA","SOLUCAO"}}
	End Do

	//Cria Tabela Temporária
	oARQTR8001 := NGFwTmpTbl(cTRB, aDBFR8001, aINDR8001)

	//TRB 2
	aDBFR8002 := {{"PROBLEMA1","C",06,0},;
				  {"QUANTDPRO","N",04,0}}

	//Cria Tabela Temporária
	oARQTR8002 := NGFwTmpTbl(cTRB1, aDBFR8002, {{"PROBLEMA1"}})

	//TRB 3
	aDBFR8003 := {{"PROBLEMA2","C",06,0},;
				  {"CAUSA2"   ,"C",06,0},;
				  {"QUANTDCAU","N",04,0}}

	//Cria Tabela Temporária
	oARQTR8003 := NGFwTmpTbl(cTRB2, aDBFR8003, {{"PROBLEMA2","CAUSA2"}})

	//TRB 4
	aDBFR8004 := {{"PROBLEMA3","C",06,0},;
				  {"CAUSA3"   ,"C",06,0},;
				  {"SOLUCAO3" ,"C",06,0},;
				  {"QUANTDSOL","N",04,0}}

	//Cria Tabela Temporária
	oARQTR8004 := NGFwTmpTbl(cTRB3, aDBFR8004, {{"PROBLEMA3","CAUSA3","SOLUCAO3"}})

	Processa({|lEND| MTR800TJTS()},STR0013)

	nTSolu  := 0
	nTCausa := 0
	nTProbl := 0
	DbSelectArea(cTRB)
	DbGotop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()

		If MV_PAR11 = 1 //Se o tipo da analise for por Centro de Custo + Centro de Trabalho
			cCusto1 := (cTRB)->CCUSTO
			cCtrab1 := (cTRB)->CENTRAB
			nTProbl := 0 //Total dos Problemas por Centro de Custos

			oSection1:Init()
			oSection1:PrintLine()
			oSection2:Init()
			While !Eof() .And. !oReport:Cancel() .And. (cTRB)->CCUSTO = cCusto1  .And. (cTRB)->CENTRAB =cCtrab1
				oReport:IncMeter()
				nTProbl +=1
				GravaTrb() //Grava arquivos de trabalho
				DbSelectArea(cTRB)
				DbSkip()
			End
		ElseIf MV_PAR11 = 2 //Familia
			cFamilia1 := (cTRB)->FAMILIA
			nTProbl   := 0   //Total dos Problemas por Familia

			oSection1:Init()
			oSection1:PrintLine()
			oSection2:Init()
			While !Eof() .And. !oReport:Cancel() .And. (cTRB)->FAMILIA = cFamilia1
				oReport:IncMeter()
				nTProbl +=1
				GravaTrb() //Grava arquivos de trabalho
				DbSelectArea(cTRB)
				DbSkip()
			End
		Else //Fabricante
			cFabric1 := (cTRB)->FABRIC
			nTProbl  := 0   //Total dos Problemas por Fabricante

			oSection1:Init()
			oSection1:PrintLine()
			oSection2:Init()
			While !Eof() .And. !oReport:Cancel() .And. (cTRB)->FABRIC = cFabric1
				oReport:IncMeter()
				nTProbl +=1
				GravaTrb()//Grava arquivos de trabalho
				DbSelectArea(cTRB)
				DbSkip()
			End
		EndIf
		//Faz a impressao dos dados da secao 2
		MTR800ISE(oReport,oSection1,oSection2)
		DbSelectArea(cTRB)
	End

	//Deleta o arquivo temporario fisicamente
	oARQTR8001:Delete()
	oARQTR8002:Delete()
	oARQTR8003:Delete()
	oARQTR8004:Delete()

Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLin  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 18/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ somlir850()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR850        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function SomaLin()
	Li++
	If Li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		If MV_PAR11 = 1
			@Li,000 Psay STR0009 + cCusto1 +" "+ NGSEEK("SI3",cCusto1,1,SUBSTR("I3_DESC",1,25)) //"CENTRO DE CUSTOS..: "
			@Li,069 Psay STR0010 + cCtrab1 +" "+ NGSEEK("SHB",cCtrab1,1,SUBSTR("HB_NOME",1,30)) //"CENTRO DE TRABALHO..: "
		ElseIf MV_PAR11 = 2
			@Li,000 Psay STR0011 + cFamilia1 +" "+ NGSEEK("ST6",cFamilia1,1,SUBSTR("T6_NOME",1,40)) //"FAMILIA..: "
		Else
			@Li,000 Psay STR0012 + cFabric1 +" "+ NGSEEK("ST7",cFabric1,1,SUBSTR("T7_NOME",1,40)) //"FABRICANTE..: "
		EndIf
		SomaLin()
		@Li,000 Psay replicate("*",132)
		SomaLin()
	EndIf
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaCausa³ Autor ³ Elisangela Costa      ³ Data ³ 05/02/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Soma as causas relacionadas ao Problema                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaCausa()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR800        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function SomaCausa(cProb)
	nTotCausa := 0
	DbSelectArea(cTRB2)
	DbSeek(cProb)
	While !Eof() .And. (cTRB2)->PROBLEMA2 = cProb
		If !Empty((cTRB2)->CAUSA2)
			nTotCausa += (cTRB2)->QUANTDCAU
		EndIf
		DbSelectArea(cTRB2)
		DbSkip()
	End
Return nTotCausa

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaSolu ³ Autor ³ Elisangela Costa      ³ Data ³ 05/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Soma as solucoes relacionadas ao Problema + Causa          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ Somasolu()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR800        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function SomaSolu(cProbl,cCausa)
	nTotSolu := 0
	DbSelectArea(cTRB3)
	DbSeek(cProbl+cCausa)
	While !Eof() .And. (cTRB3)->PROBLEMA3 = cProbl .And. (cTRB3)->CAUSA3 = cCausa
		nTotSolu += (cTRB3)->QUANTDSOL
		DbSelectArea(cTRB3)
		DbSkip()
	End
Return nTotSolu

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaSoVa ³ Autor ³ Elisangela Costa      ³ Data ³ 05/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Soma as solucoes sem causas e so com problema              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomasoVu()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR800        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function SomaSoVa(cProbl,cCau)
	nTotS := 0
	DbSelectArea(cTRB3)
	DbSeek(cProbl+cCau)
	While !Eof() .And. (cTRB3)->PROBLEMA3 = cProbl .And. (cTRB3)->CAUSA3 = cCau
		If Empty((cTRB3)->CAUSA3)
			nTotS += (cTRB3)->QUANTDSOL
		EndIf
		DbSelectArea(cTRB3)
		DbSkip()
	End
Return nTotS

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ GravaTrb ³ Autor ³ Elisangela Costa      ³ Data ³ 05/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Grava arquivos temporarios                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ GravaTrb()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR800        !                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function GravaTrb()
	DbSelectArea(cTRB1)
	If !DbSeek((cTRB)->PROBLEMA)
		(cTRB1)->(DbAppend())
		(cTRB1)->PROBLEMA1 := (cTRB)->PROBLEMA
	EndIf
	(cTRB1)->QUANTDPRO += 1

	If !Empty((cTRB)->CAUSA) .Or. !Empty((cTRB)->SOLUCAO)
		DbSelectArea(cTRB2)
		If !DbSeek((cTRB)->PROBLEMA+(cTRB)->CAUSA)
			(cTRB2)->(DbAppend())
			(cTRB2)->PROBLEMA2 := (cTRB)->PROBLEMA
			(cTRB2)->CAUSA2    := (cTRB)->CAUSA
		EndIf
		(cTRB2)->QUANTDCAU += 1
	EndIf

	If !Empty((cTRB)->SOLUCAO)
		DbSelectArea(cTRB3)
		If !DbSeek((cTRB)->PROBLEMA+(cTRB)->CAUSA+(cTRB)->SOLUCAO)
			(cTRB3)->(DbAppend())
			(cTRB3)->PROBLEMA3:= (cTRB)->PROBLEMA
			(cTRB3)->CAUSA3   := (cTRB)->CAUSA
			(cTRB3)->SOLUCAO3 := (cTRB)->SOLUCAO
		EndIf
		(cTRB3)->QUANTDSOL += 1
	EndIf
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MTR800TJTS³ Autor ³ Elisangela Costa      ³ Data ³ 12/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Faz a leitura do STJ e STS                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR800                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MTR800TJTS()

	cCONDSTJ := 'stj->tj_situaca = "L" .And. stj->tj_tipoos = "B" .And.'
	cCONDSTJ := cCONDSTJ + '(stj->tj_centrab >= mv_par03 .And. stj->tj_centrab <= mv_par04) .And.'
	cCONDSTJ := cCONDSTJ + '(stj->tj_dtorigi >= mv_par09 .And. stj->tj_dtorigi <= mv_par10)'

	cCONDSTS := 'sts->ts_situaca = "L" .And. stj->tj_tipoos = "B" .And.'
	cCONDSTS := cCONDSTS + '(sts->ts_centrab >= mv_par03 .And. sts->ts_centrab <= mv_par04) .And.'
	cCONDSTS := cCONDSTS + '(sts->ts_dtorigi >= mv_par09 .And. sts->ts_dtorigi <= mv_par10)'

	cCONDST9 := '(st9->t9_codfami >= mv_par05 .And. st9->t9_codfami <= mv_par06) .And.'
	cCONDST9 := cCONDST9 + '(st9->t9_fabrica >= mv_par07 .And. st9->t9_fabrica <= mv_par08)'

	DbSelectArea("STJ")
	DbSetOrder(07)
	DbSeek(xFilial("STJ")+MV_PAR01,.T.)
	ProcRegua(LastRec())
	While !Eof() .and. STJ->TJ_FILIAL == xFilial("STJ") .and. STJ->TJ_CCUSTO <= MV_PAR02

		IncProc()

		If &(cCONDSTJ)
			DbSelectArea("ST9")
			DbSetOrder(1)
			If DbSeek(xFilial("ST9")+STJ->TJ_CODBEM)
				If &(cCONDST9)
					DbSelectArea("STN")
					DbSetOrder(01)
					If DbSeek(xFilial('STN')+STJ->TJ_ORDEM+STJ->TJ_PLANO)
						While !eof() .and. xfilial('STN') = STN->TN_filial .and.;
						STN->TN_ORDEM = STJ->TJ_ORDEM .and. STN->TN_PLANO = STJ->TJ_PLANO

							(cTRB)->(DbAppend())
							(cTRB)->CCUSTO   := STJ->TJ_CCUSTO
							(cTRB)->CENTRAB  := STJ->TJ_CENTRAB
							(cTRB)->FAMILIA  := ST9->T9_CODFAMI
							(cTRB)->FABRIC   := ST9->T9_FABRICA
							(cTRB)->PROBLEMA := STN->TN_CODOCOR
							(cTRB)->CAUSA    := STN->TN_CAUSA
							(cTRB)->SOLUCAO  := STN->TN_SOLUCAO

							DbSelectArea("STN")
							Dbskip()
						End
					EndIf
				EndIf
			EndIf
		EndIf

		DbSelectArea("STJ")
		Dbskip()
	End

	DbSelectArea("STS")
	DbSetOrder(05)
	DbSeek(xFilial("STS")+MV_PAR01,.T.)
	ProcRegua(LastRec())
	While !Eof() .and. STS->TS_FILIAL == xFilial("STS") .and. STS->TS_CCUSTO <= MV_PAR02

		IncProc()
		If &(cCONDSTS)
			DbSelectArea("ST9")
			DbSetOrder(1)
			If DbSeek(xFilial("ST9")+STS->TS_CODBEM)
				If &(cCONDST9)
					DbSelectArea("STU")
					DbSetOrder(01)
					If DbSeek(xFilial('STU')+STS->TS_ORDEM+STS->TS_PLANO)
						While !eof() .and. xfilial('STU') = STU->TU_filial .and.;
						STU->TU_ORDEM = STS->TS_ORDEM .and. STU->TU_PLANO = STS->TS_PLANO

							(cTRB)->(DbAppend())
							(cTRB)->CCUSTO   := STS->TS_CCUSTO
							(cTRB)->CENTRAB  := STS->TS_CENTRAB
							(cTRB)->FAMILIA  := ST9->T9_CODFAMI
							(cTRB)->FABRIC   := ST9->T9_FABRICA
							(cTRB)->PROBLEMA := STU->TU_CODOCOR
							(cTRB)->CAUSA    := STU->TU_CAUSA
							(cTRB)->SOLUCAO  := STU->TU_SOLUCAO

							DbSelectArea("STU")
							Dbskip()
						End
					EndIf
				EndIf
			EndIf
		EndIf
		DbSelectArea("STS")
		Dbskip()
	End
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MTRPER800 ³ Autor ³ Elisangela Costa      ³ Data ³ 12/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Calcula o percentual                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR800                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MTRPER800(nCAMR800)

	Local nPERCENT := 0

	If nCAMR800 = 1
		nPERCENT := (cTRB1)->QUANTDPRO * 100 /  nTProbl
	ElseIf nCAMR800 = 2
		nPERCENT := (cTRB2)->QUANTDCAU * 100 / nTCausa
	Else
		nPERCENT := (cTRB3)->QUANTDSOL * 100 / nTSolu
	EndIf

Return nPERCENT

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MTRN800   ³ Autor ³ Elisangela Costa      ³ Data ³ 12/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Retorna o nome do problema, causa e solucao                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR800                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MTRN800(nCAMR800)

	cNOMEST8 := ""
	If nCAMR800 = 1
		cNOMEST8 := NGSEEK("ST8",(cTRB1)->PROBLEMA1,1,"SUBSTR(T8_NOME,1,20)")
	ElseIf nCAMR800 = 2
		cNOMEST8 := NGSEEK("ST8",(cTRB2)->CAUSA2,1,"SUBSTR(T8_NOME,1,20)")
	Else
		cNOMEST8 := NGSEEK("ST8",(cTRB3)->SOLUCAO3,1,"SUBSTR(T8_NOME,1,20)")
	EndIf

Return cNOMEST8

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MTR800IMPD³ Autor ³ Elisangela Costa      ³ Data ³ 12/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Faz a impressao dos dados do relatorio                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR800                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MTR800IMPD(nTOTPROB)

	Li := 80
	DbSelectArea(cTRB1)
	DbGotop()
	While !Eof()

		nTCausa := 0
		cProblema := (cTRB1)->PROBLEMA1

		Somalin()
		@ Li,000 Psay cProblema Picture "!@"
		@ Li,007 Psay NGSEEK("ST8",cProblema,1,"SUBSTR(T8_NOME,1,20)")
		@ Li,030 Psay (cTRB1)->QUANTDPRO Picture "9999"
		@ Li,035 Psay (cTRB1)->QUANTDPRO * 100 /nTOTPROB Picture "@E 999.9"

		//Conta a quantidade de Causas relacionadas ao Problema
		nTCausa := SomaCausa(cProblema)

		lPri1 := .T.
		lPriCau1 := .F.
		DbSelectArea(cTRB2)
		DbSeek((cTRB1)->PROBLEMA1)
		While !Eof() .And. (cTRB2)->PROBLEMA2 = cProblema
			If !Empty((cTRB2)->CAUSA2)

				nTSolu := 0
				lPriCau1 := .T.
				If lPri1
					lPri1 := .F.
				Else
					Somalin()
				EndIf
				@ Li,043 Psay (cTRB2)->CAUSA2 Picture "!@"
				@ Li,050 Psay NGSEEK("ST8",(cTRB2)->CAUSA2,1,"SUBSTR(T8_NOME,1,20)")
				@ Li,073 Psay (cTRB2)->QUANTDCAU Picture "9999"
				@ Li,078 Psay (cTRB2)->QUANTDCAU * 100 /nTCausa  Picture "@E 999.9"

				//Conta a quantidade de Solucao relacionadas ao Problema + Causa
				nTSolu := SomaSolu(cProblema,(cTRB2)->CAUSA2)

				lPri2 := .T.
				DbSelectArea(cTRB3)
				DbSeek((cTRB1)->PROBLEMA1+(cTRB2)->CAUSA2)
				While !Eof() .And. (cTRB3)->PROBLEMA3 = (cTRB1)->PROBLEMA1 .And. (cTRB3)->CAUSA3 = (cTRB2)->CAUSA2

					If lPri2
						lPri2 := .F.
					Else
						Somalin()
					EndIf
					@ Li,086 Psay (cTRB3)->SOLUCAO3 Picture "!@"
					@ Li,093 Psay NGSEEK("ST8",(cTRB3)->SOLUCAO3,1,"SUBSTR(T8_NOME,1,20)")
					@ Li,116 Psay (cTRB3)->QUANTDSOL Picture "9999"
					@ Li,121 Psay (cTRB3)->QUANTDSOL * 100 / nTSolu  Picture "@E 999.9"
					DbSelectArea(cTRB3)
					DbDelete()
					DbSkip()
				End
			EndIf
			DbSelectArea(cTRB2)
			DbSkip()
		End
		cCausa  := Space(6)

		//Conta a quantidade de Solucao relacionadas ao Problema + Causa
		nTSolu := SomaSoVa(cProblema,cCausa)

		lPri3 := .T.
		DbSelectArea(cTRB3)
		DbGotop()
		If Reccount() > 0

			DbSelectArea(cTRB3)
			DbSeek(cProblema)
			While !Eof() .And. (cTRB3)->PROBLEMA3 = cProblema

				If lPri3
					lPri3 := .F.
					If lPriCau1
						Somalin()
					EndIf
				Else
					Somalin()
				EndIf

				@ Li,086 Psay (cTRB3)->SOLUCAO3 Picture "!@"
				@ Li,093 Psay NGSEEK("ST8",(cTRB3)->SOLUCAO3,1,"SUBSTR(T8_NOME,1,20)")
				@ Li,116 Psay (cTRB3)->QUANTDSOL Picture "9999"
				@ Li,121 Psay (cTRB3)->QUANTDSOL * 100 /nTSolu   Picture "@E 999.9"
				DbSelectArea(cTRB3)
				DbDelete()
				DbSkip()
			End
		EndIf
		DbSelectArea(cTRB1)
		DbSkip()
	End
	DbSelectArea(cTRB1)
	Zap
	DbSelectArea(cTRB2)
	Zap
	DbSelectArea(cTRB3)
	Zap

Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MTR800ISE ³ Autor ³ Elisangela Costa      ³ Data ³ 12/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Faz a impressao dos dados da secao 2                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR800                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MTR800ISE(oReport,oSection1,oSection2)

	DbSelectArea(cTRB1)
	DbGotop()
	While !Eof() .And. !oReport:Cancel()

		cProblema  := (cTRB1)->PROBLEMA1
		cPROBLEMA1 := " "

		//Conta a quantidade de Causas relacionadas ao Problema
		nTCausa    := SomaCausa(cProblema)

		DbSelectArea(cTRB2)
		If DbSeek((cTRB1)->PROBLEMA1)
			While !Eof() .And. !oReport:Cancel() .And. (cTRB2)->PROBLEMA2 = cProblema
				If !Empty((cTRB2)->CAUSA2)

					//Conta a quantidade de Solucao relacionadas ao Problema + Causa
					nTSolu := SomaSolu(cProblema,(cTRB2)->CAUSA2)
					cCAUSA := " "

					DbSelectArea(cTRB3)
					If DbSeek((cTRB1)->PROBLEMA1+(cTRB2)->CAUSA2)
						While !Eof() .And. !oReport:Cancel() .And. (cTRB3)->PROBLEMA3 = (cTRB1)->PROBLEMA1 .And. (cTRB3)->CAUSA3 = (cTRB2)->CAUSA2

							If cPROBLEMA1 = cProblema
								oSection2:Cell("(cTRB1)->PROBLEMA1"):Hide()
								oSection2:Cell("NOMEPROB"):Hide()
								oSection2:Cell("(cTRB1)->QUANTDPRO"):Hide()
								oSection2:Cell("PERCENPRO"):Hide()
							Else
								oSection2:Cell("(cTRB1)->PROBLEMA1"):Show()
								oSection2:Cell("NOMEPROB"):Show()
								oSection2:Cell("(cTRB1)->QUANTDPRO"):Show()
								oSection2:Cell("PERCENPRO"):Show()
							EndIf

							If cCAUSA  = (cTRB2)->CAUSA2
								oSection2:Cell("(cTRB2)->CAUSA2"):Hide()
								oSection2:Cell("NOMECAUSA"):Hide()
								oSection2:Cell("(cTRB2)->QUANTDCAU"):Hide()
								oSection2:Cell("PERCENCAU"):Hide()
							Else
								oSection2:Cell("(cTRB2)->CAUSA2"):Show()
								oSection2:Cell("NOMECAUSA"):Show()
								oSection2:Cell("(cTRB2)->QUANTDCAU"):Show()
								oSection2:Cell("PERCENCAU"):Show()
							EndIf

							cPROBLEMA1 := (cTRB1)->PROBLEMA1
							cCAUSA     := (cTRB2)->CAUSA2

							oSection2:PrintLine()

							DbSelectArea(cTRB3)
							DbDelete()
							DbSkip()
						End
					Else
						If cPROBLEMA1 = cProblema
							oSection2:Cell("(cTRB1)->PROBLEMA1"):Hide()
							oSection2:Cell("NOMEPROB"):Hide()
							oSection2:Cell("(cTRB1)->QUANTDPRO"):Hide()
							oSection2:Cell("PERCENPRO"):Hide()
						Else
							oSection2:Cell("(cTRB1)->PROBLEMA1"):Show()
							oSection2:Cell("NOMEPROB"):Show()
							oSection2:Cell("(cTRB1)->QUANTDPRO"):Show()
							oSection2:Cell("PERCENPRO"):Show()
						EndIf

						cPROBLEMA1 := (cTRB1)->PROBLEMA1
						oSection2:PrintLine()
					EndIf
				EndIf
				DbSelectArea(cTRB2)
				DbSkip()
			End
		Else
			DbSelectArea(cTRB3)
			DbGotop()
			If Reccount() > 0
			Else
				If cPROBLEMA1 = cProblema
					oSection2:Cell("(cTRB1)->PROBLEMA1"):Hide()
					oSection2:Cell("NOMEPROB"):Hide()
					oSection2:Cell("(cTRB1)->QUANTDPRO"):Hide()
					oSection2:Cell("PERCENPRO"):Hide()
				Else
					oSection2:Cell("(cTRB1)->PROBLEMA1"):Show()
					oSection2:Cell("NOMEPROB"):Show()
					oSection2:Cell("(cTRB1)->QUANTDPRO"):Show()
					oSection2:Cell("PERCENPRO"):Show()
				EndIf
				cPROBLEMA1 := (cTRB1)->PROBLEMA1
				oSection2:PrintLine()
			EndIf
		EndIf

		//Conta a quantidade de Solucao relacionadas ao Problema + Causa
		cCausa := Space(6)
		nTSolu := SomaSoVa(cProblema,cCausa)

		DbSelectArea(cTRB3)
		DbGotop()
		If Reccount() > 0

			oSection2:Cell("(cTRB1)->PROBLEMA1"):Hide()
			oSection2:Cell("NOMEPROB"):Hide()
			oSection2:Cell("(cTRB1)->QUANTDPRO"):Hide()
			oSection2:Cell("PERCENPRO"):Hide()
			oSection2:Cell("(cTRB2)->CAUSA2"):Hide()
			oSection2:Cell("NOMECAUSA"):Hide()
			oSection2:Cell("(cTRB2)->QUANTDCAU"):Hide()
			oSection2:Cell("PERCENCAU"):Hide()

			DbSelectArea(cTRB3)
			DbSeek(cProblema)
			While !Eof() .And. !oReport:Cancel() .And. (cTRB3)->PROBLEMA3 = cProblema
				oSection2:PrintLine()
				DbSelectArea(cTRB3)
				DbDelete()
				DbSkip()
			End
		EndIf
		DbSelectArea(cTRB1)
		DbSkip()
	End
	oSection2:Cell("(cTRB1)->PROBLEMA1"):Show()
	oSection2:Cell("NOMEPROB"):Show()
	oSection2:Cell("(cTRB1)->QUANTDPRO"):Show()
	oSection2:Cell("PERCENPRO"):Show()
	oSection2:Cell("(cTRB2)->CAUSA2"):Show()
	oSection2:Cell("NOMECAUSA"):Show()
	oSection2:Cell("(cTRB2)->QUANTDCAU"):Show()
	oSection2:Cell("PERCENCAU"):Show()
	oSection2:Finish()
	oSection1:Finish()
	oSection1:SetPageBreak()

	DbSelectArea(cTRB1)
	Zap
	DbSelectArea(cTRB2)
	Zap
	DbSelectArea(cTRB3)
	Zap
Return .T.