#INCLUDE "MNTR130.ch"
#INCLUDE "PROTHEUS.CH" 

#DEFINE _nVERSAO 1 //Versao do fonte
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR130  ³ Autor ³ Elisangela Costa      ³ Data ³10/08/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Solicitacoes de servico pendentes                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³ TQB - Solicitacao de Servico                               ³±±
±±³Tabelas   ³ ST9 - Cadastro de Bens                                     ³±±
±±³          ³ TAF - Estrutura Organizacional                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR130()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Guarda conteudo e declara variaveis padroes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Local oReport
Local aArea := GetArea()
Private lTEMFACI := NGINTESTORG()
Private lFacilit := (FindFunction("MNTINTFAC") .and. MNTINTFAC())
Private cPERG := "MNT130"

If FindFunction("TRepInUse") .And. TRepInUse()
	oReport := ReportDef()
	oReport:SetLandscape()//Default Paisagem
	oReport:PrintDialog()
Else
	MNTR130R3()
EndIf

RestArea(aArea)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna conteudo de variaveis padroes       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)
Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ MNTR130R3³ Autor ³ Ricardo Dal Ponte     ³ Data ³ 30/01/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Solicitacoes de servico pendentes                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR130R3()

Local WNREL      := "MNTR130"
Local LIMITE     := 132
Local cDESC1     := STR0001 //"Solicitacoes de servicos pendentes. As solicitacoes de servicos"
Local cDESC2     := STR0002 //"pendentes poderao ser selecionadas atraves da opcao de parametros"
Local cDESC3     := ""
Local cSTRING    := "ST9"
Local lCorrige   := .F.

Private NOMEPROG := "MNTR130"
Private TAMANHO  := "G"
Private aRETURN  := {STR0003,1,STR0004,1,2,1,"",1} //"Zebrado"###"Administracao"
Private TITULO   := STR0005 //"Solicitacao de Servico Pendente"
Private nTIPO    := 0
Private nLASTKEY := 0
Private CABEC1,CABEC2

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros!                        ³
//³ mv_par01     De C.Custo                                      ³
//³ mv_par02     Ate C.Custo                                     ³
//³ mv_par03     De C.Trabalho                                   ³
//³ mv_par04     Ate C.Trabalho                                  ³
//³ mv_par05     De Familia de Bens                              ³
//³ mv_par06     Ate Familia de Bens                             ³
//³ mv_par07     De Bem                                          ³
//³ mv_par08     Ate Bem                                         ³
//³ mv_par09     De Localizacao                                  ³
//³ mv_par10     Ate Localizacao                                 ³ 
//³ mv_par11     De Data Abertura                                ³
//³ mv_par12     Ate Data Abertura                               ³
//³ mv_par13     Imprime Servico (Nao,Sim)                       ³ 
//³ mv_par14     Tipo (Bem, Localizacao, Todos)                  ³ 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 
Pergunte(cPERG,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
If nLASTKEY = 27
   Set Filter To
   Dbselectarea("ST9")
   Return
EndIf
SetDefault(aReturn,cSTRING)

RptStatus({|lEND| MNTR130IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
Dbselectarea("ST9")
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR130IMP³ Autor ³ Elisangela Costa      ³ Data ³ 10/08/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR130                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR130IMP(lEND,WNREL,TITULO,TAMANHO)
Local cRODATXT := ""
Local nCNTIMPR := 0
Local xx
Local cMemo
Private li := 80, m_pag := 0

nTIPO  := IIf(aReturn[4]==1,15,18)
CABEC1 := "Solic.  Tipo         Bem/Localizacao   Descricao                  C.Custo               Descricao             C.Trab.  Descricao             Dt.Abertura  Hr.Abertura  Solicitante"
CABEC2 := " "

If !lFacilit
	CABEC1 += "                     Ramal"
Endif

cCondicao := '(TQB->TQB_SOLUCA = "A" .Or. TQB->TQB_SOLUCA = "D" ) .And.'
cCondicao += 'TQB->TQB_CENTRA >= MV_PAR03 .And. TQB->TQB_CENTRA <= MV_PAR04 .And. '
cCondicao += 'TQB->TQB_DTABER >= MV_PAR11 .And. TQB->TQB_DTABER <= MV_PAR12'  

/*/
          1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         210       220       230
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
*******************************************************************************************************************************************************************************************************************************
Solic.  Tipo         Bem/Localizacao   Descricao                  C.Custo               Descricao             C.Trab.  Descricao             Dt.Abertura  Hr.Abertura  Solicitante                     Ramal
*******************************************************************************************************************************************************************************************************************************
xxxxxx  xxxxxxxxxxx  xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx  xxxxxx   xxxxxxxxxxxxxxxxxxxx  99/99/9999   99:99        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxx 
Servico...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                                                                                                                    
                                                                                                                                                                                            
xxxxxx xxxxxxxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxx xxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxx 99/99/9999  99:99       xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxx 
Servico...: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                                                                                                                    

*/
DbSelectArea("TQB")
DbSetOrder(07)
DbSeek(xFilial("TQB")+MV_PAR01,.T.)
SetRegua(LastRec())
While !Eof() .And. TQB->TQB_FILIAL == xFilial("TQB") .And. TQB->TQB_CCUSTO <= MV_PAR02
	
	If !&(cCondicao)
		dbSelectArea("TQB")
		dbSkip()
		Loop
	Endif
			
	If (MV_PAR14 == 1 .and. TQB->TQB_TIPOSS <> "B") .or. (MV_PAR14 == 2 .and. TQB->TQB_TIPOSS <> "L") .or. Empty(TQB->TQB_TIPOSS)
		dbSelectArea("TQB")
		dbSkip()
		Loop
	Endif
		
	If TQB->TQB_TIPOSS == "B"
		dbSelectArea("ST9")
		dbSetOrder(01)
		If (TQB->TQB_CODBEM < MV_PAR07 .Or. TQB->TQB_CODBEM > MV_PAR08)  .or. !dbSeek(xFilial("ST9")+TQB->TQB_CODBEM) .or.;
			(ST9->T9_CODFAMI < MV_PAR05 .Or. ST9->T9_CODFAMI > MV_PAR06)
			dbSelectArea("TQB")
			dbSkip()
			Loop
		Endif
	Else
		If !lTEMFACI .or. (TQB->TQB_CODBEM < MV_PAR09 .Or. TQB->TQB_CODBEM > MV_PAR10)
			dbSelectArea("TQB")
			dbSkip()
			Loop
		EndIf
	EndIf

	IncRegua()
	NGSOMALI(58)
	@LI,000 Psay TQB->TQB_SOLICI  Picture "@!"
	
	If TQB->TQB_TIPOSS == "B"
		@li,008 Psay STR0007  Picture "@!"  //"BEM"
		@li,021 Psay TQB->TQB_CODBEM  Picture "@!"
		DbSelectArea("ST9")
		DbSetOrder(01)
		If DbSeek(xFilial("ST9")+TQB->TQB_CODBEM)
			@li,039 Psay SubStr(ST9->T9_NOME,1,25)Picture "@!"
		EndIf
	Else
		@li,008 Psay STR0008  Picture "@!"  //"LOCALIZACAO"
		@li,021 Psay TQB->TQB_CODBEM  Picture "@!"
		DbSelectArea("TAF")
		DbSetOrder(07)
		If DbSeek(xFilial("TAF")+"X"+"2"+TQB->TQB_CODBEM)
			@li,039 Psay SubStr(TAF->TAF_NOMNIV,1,25)Picture "@!"
		EndIf
	EndIf
	
	@li,066 Psay TQB->TQB_CCUSTO Picture "@!"
	@li,088 Psay NGSEEK("CTT",TQB->TQB_CCUSTO,1,"Substr(CTT_DESC01,1,20)") Picture "@!"
	@li,110 Psay TQB->TQB_CENTRA Picture "@!"
	@li,119 Psay NGSEEK("SHB",TQB->TQB_CENTRA,1,"Substr(HB_NOME,1,20)") Picture "@!"
	@li,141 Psay TQB->TQB_DTABER Picture "99/99/9999"
	@li,154 Psay TQB->TQB_HOABER Picture "99:99"
	@li,167 Psay SubStr(UsrRetName(TQB->TQB_CDSOLI),1,30) Picture "@!"
	If !lFacilit
		@li,199 Psay TQB->TQB_RAMAL  Picture "@!"
	Endif
	
	If MV_PAR13 == 2
		cMemo := MSMM(TQB->TQB_CODMSS,,,,3)
		If !Empty(cMemo)
			NGIMPMEMO(cMemo,150,000,STR0009,.F.,.T.) //"Servico..: "
			NGSOMALI(58)
		EndIf
	EndIf
		
	dbSelectArea("TQB")
	dbSetOrder(7)
	dbSkip()
End

Roda(nCntImpr,cRodaTxt,Tamanho)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("ST9")
RetIndex("TQB")
RetIndex("TAF")
Set Filter To
Set device to Screen
If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
EndIf
MS_FLUSH()

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG130VLO  ³ Autor ³ Elisangela Costa      ³ Data ³10/08/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida parametro de Localizacao                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MNTR130                                                     ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG130VLOC(cPARAME1,cPARAME2,nTIPO)

If !lTEMFACI
	Return .T.
Else
	If nTIPO == 1
		If Empty(cPARAME1)
			Return .T.
		Else
			DbSelectArea("TAF")
			DbSetOrder(07)
			If !DbSeek(xFilial("TAF")+"X"+"2"+cPARAME1)
				MsgInfo(STR0010,STR0011)  //"Codigo da localizacao invalido."###"ATENCAO"
				Return .F.
			EndIf
		EndIf
	Else
		If cPARAME2 == "ZZZ"
			If(Atecodigo("TAF",cPARAME1,cPARAME2,16),.t.,.f.)
		Else
			If Empty(cPARAME2)
				Help(" ",1,"ATEINVALID")
				Return .F.
			ElseIf cPARAME2 < cPARAME1
				Help(" ",1,"DEATEINVAL")
				Return .F.
			EndIf
			
			DbSelectArea("TAF")
			DbSetOrder(07)
			If !DbSeek(xFilial("TAF")+"X"+"2"+cPARAME2)
				MsgInfo(STR0012,STR0011) //"Codigo da localizacao invalido"###"ATENCAO"
				Return .F.
			EndIf
		EndIf
	EndIf
EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ Elisangela Costa      ³ Data ³ 30/01/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR450                                                    ³±±
±±|__________|____________________________________________________________|±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef()
Local oReport
Local oSection1
Local oSection2
Local oCell

Private c1SOLICI := ""
Private c1TIPOSS := ""
Private c1CODBEM := ""
Private c1NOMBEM := ""
Private c1CCUSTO := ""
Private c1NOMCUS := ""
Private c1NOMCTR := ""
Private c1DTABER := ""
Private c1HOABER := ""
Private c1USUARI := ""
Private c1RAMAL  := ""

Private c2DTMEMO  := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MNTR130",OemToAnsi(STR0005),cPerg,{|oReport| ReportPrint(oReport)},STR0001+" "+STR0002)

oReport:SetLandscape()  //Default Paisagem
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis utilizadas para parametros                         ³
³ mv_par01     // De  Familia                                  ³
³ mv_par02     // At‚ Familia                                  ³
³ mv_par03     // De  Bem                                      ³
³ mv_par04     // At‚ Bem                                      ³
³ mv_par05     // Servico                                      ³
³ mv_par06     // Sequencia                                    ³
³ mv_par07     // Data In¡cio                                  ³
³ mv_par08     // Data Fim                                     ³
³ mv_par09     // Considera inativo                            ³
³ mv_par10     // Tipo de Custo (1-Medio, 2-Standard)          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

oSection1 := TRSection():New(oReport,STR0024,{"TQB", "ST9", "TAF", "CTT","SHB"})//"Solicitacao de Servico"
oCell := TRCell():New(oSection1,"c1SOLICI"  ,"",STR0013,"@!",7,/*lPixel*/,{||c1SOLICI}) //"Solic."
oCell := TRCell():New(oSection1,"c1TIPOSS"  ,"",STR0014,"@!",7,/*lPixel*/,{||c1TIPOSS}) //"Tipo"
oCell := TRCell():New(oSection1,"c1CODBEM"  ,"",STR0015,"@!",16,/*lPixel*/,{||c1CODBEM}) //"Bem/Localizacao"
oCell := TRCell():New(oSection1,"c1NOMBEM"  ,"",STR0016,"@!",20,/*lPixel*/,{||c1NOMBEM}) //"Descricao"
oCell := TRCell():New(oSection1,"c1CCUSTO"  ,"",STR0017,"@!",Len(TQB->TQB_CCUSTO),/*lPixel*/,{||c1CCUSTO}) //"C.Custo"
oCell := TRCell():New(oSection1,"c1NOMCUS"  ,"",STR0016,"@!",20,/*lPixel*/,{||c1NOMCUS}) //"Descricao"
oCell := TRCell():New(oSection1,"c1NOMCTR"  ,"",STR0018,"@!",20,/*lPixel*/,{||c1NOMCTR})//"Centro Trab."
oCell := TRCell():New(oSection1,"c1DTABER"  ,"",STR0019,"99/99/9999",10,/*lPixel*/,{||c1DTABER}) //"Dt.Abertura"
oCell := TRCell():New(oSection1,"c1HOABER"  ,"",STR0020,"99:99",10,/*lPixel*/,{||c1HOABER}) //"Hr.Abertura"
oCell := TRCell():New(oSection1,"c1USUARI"  ,"",STR0021,"@!",20,/*lPixel*/,{||c1USUARI})//"Solicitante"
If !lFacilit
	oCell := TRCell():New(oSection1,"c1RAMAL"   ,"",STR0022,"@!",8,/*lPixel*/,{||c1RAMAL})//"Ramal"
Endif
TRPosition():New(oSection1,"TQB",1,{|| xFilial("TQB")+TQB->TQB_SOLICI})
TRPosition():New(oSection1,"ST9",1,{|| xFilial("ST9")+TQB->TQB_CODBEM})
TRPosition():New(oSection1,"TAF",7,{|| xFilial("TAF")+"X"+"2"+TQB->TQB_CODBEM})
TRPosition():New(oSection1,"CTT",1,{|| xFilial("CTT")+TQB->TQB_CCUSTO})
TRPosition():New(oSection1,"SHB",1,{|| xFilial("SHB")+TQB->TQB_CENTRA})

oSection2 := TRSection():New(oReport,STR0023,{"TQB"})
oCell := TRCell():New(oSection2,"c2DTMEMO" ,"",STR0023   ,"@!" ,100 , /*lPixel*/,{||c2DTMEMO},,.T.) //"Servico"

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³  Elisangela Costa     ³ Data ³ 21/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR270                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)
Local lImprime := .F.
Private oSection1 := oReport:Section(1)
Private oSection2 := oReport:Section(2)

cCondicao := '(TQB->TQB_SOLUCA = "A" .Or. TQB->TQB_SOLUCA = "D" ) .And.'
cCondicao += 'TQB->TQB_CENTRA >= MV_PAR03 .And. TQB->TQB_CENTRA <= MV_PAR04 .And. '
cCondicao += 'TQB->TQB_DTABER >= MV_PAR11 .And. TQB->TQB_DTABER <= MV_PAR12'  

dbSelectArea("TQB")
dbSetOrder(07)
dbSeek(xFilial("TQB")+MV_PAR01,.T.)
oReport:SetMeter(LastRec())
While !Eof() .And. !oReport:Cancel() .And. TQB->TQB_FILIAL == xFilial("TQB") .And. TQB->TQB_CCUSTO <= MV_PAR02
	
	c1SOLICI := ""
	c1TIPOSS := ""
	c1CODBEM := ""
	c1NOMBEM := ""
	c1CCUSTO := ""
	c1NOMCUS := ""
	c1NOMCTR := ""
	c1DTABER := ""
	c1HOABER := ""
	c1USUARI := ""
	c1RAMAL  := ""
	
	oReport:IncMeter()
	
	If !&(cCondicao)
		dbSelectArea("TQB")
		dbSkip()
		Loop
	Endif
			
	If (MV_PAR14 == 1 .and. TQB->TQB_TIPOSS <> "B") .or. (MV_PAR14 == 2 .and. TQB->TQB_TIPOSS <> "L") .or. Empty(TQB->TQB_TIPOSS)
		dbSelectArea("TQB")
		dbSkip()
		Loop
	Endif
		
	If TQB->TQB_TIPOSS == "B"
		dbSelectArea("ST9")
		dbSetOrder(01)
		If (TQB->TQB_CODBEM < MV_PAR07 .Or. TQB->TQB_CODBEM > MV_PAR08)  .or. !dbSeek(xFilial("ST9")+TQB->TQB_CODBEM) .or.;
			(ST9->T9_CODFAMI < MV_PAR05 .Or. ST9->T9_CODFAMI > MV_PAR06)
			dbSelectArea("TQB")
			dbSkip()
			Loop
		Endif
	Else
		If !lTEMFACI .or. (TQB->TQB_CODBEM < MV_PAR09 .Or. TQB->TQB_CODBEM > MV_PAR10)
			dbSelectArea("TQB")
			dbSkip()
			Loop
		EndIf
	EndIf
		
	oSection1:Init()

	lImprime := .T.
	
	c1SOLICI := TQB->TQB_SOLICI
	
	If TQB->TQB_TIPOSS = "B"
		c1TIPOSS := STR0007//"BEM"
		c1CODBEM := TQB->TQB_CODBEM
		DbSelectArea("ST9")
		DbSetOrder(01)
		If DbSeek(xFilial("ST9")+TQB->TQB_CODBEM)
			c1NOMBEM :=SubStr(ST9->T9_NOME,1,25)
		EndIf
	Else
		c1TIPOSS := STR0008 //"LOCALIZACAO"
		c1CODBEM := TQB->TQB_CODBEM
		DbSelectArea("TAF")
		DbSetOrder(07)
		If DbSeek(xFilial("TAF")+"X"+"2"+TQB->TQB_CODBEM)
			c1NOMBEM :=SubStr(TAF->TAF_NOMNIV,1,25)
		EndIf
	EndIf
	
	c1CCUSTO := TQB->TQB_CCUSTO
	
	C1NOMCUS := NGSEEK("CTT",TQB->TQB_CCUSTO,1,"Substr(CTT_DESC01,1,20)")
	
	c1NOMCTR := NGSEEK("SHB",TQB->TQB_CENTRA,1,"Substr(HB_NOME,1,20)")
	c1DTABER := TQB->TQB_DTABER
	c1HOABER := TQB->TQB_HOABER
	c1USUARI := SubStr(UsrRetName(TQB->TQB_CDSOLI),1,30)
	If !lFacilit
		c1RAMAL  := TQB->TQB_RAMAL
	Endif
	
	oSection1:PrintLine()
	
	If MV_PAR13 == 2
		oSection1:Finish()
		oSection2:Init()
		c2DTMEMO := MSMM(TQB->TQB_CODMSS,,,,3)
		oSection2:PrintLine()
		oSection2:Finish()
		oReport:SkipLine()
	EndIf
	dbSelectArea("TQB")
	dbSetOrder(7)
	dbSkip()
End

If lImprime
	oSection1:Finish()	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("ST9")
RetIndex("TQB")
RetIndex("TAF")
Set Filter To
Return .T.