#INCLUDE "MNTA760.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA760  ³ Autor ³ Thiago Olis Machado   ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualizar a posicao final da inspecao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function MNTA760()

	Local aNGBeginPrm := {}

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 19, 95 )

		// Armazena variaveis p/ devolucao (NGRIGHTCLICK)
		aNGBeginPrm := NGBeginPrm()

		Private cCadastro  := OemtoAnsi(STR0001) //"Atualizar a inspeção final da inspeção"
		Private cPerg := "MNA760"

		If Pergunte(cPerg,.t.)
			Processa({|lEnd| Mnt760Ini()})
		EndIf

		NgReturnPrm( aNgBeginPrm )
	
	EndIf

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNT760VD ³ Autor ³ Thiago Olis Machado   ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao de data                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Mnt760VD()

	If !NaoVazio(Mv_Par03)
		Return .f.
	EndIf

	If Mv_Par03 > dDataBase
		Help(" ",1,"NGATENCAO",,STR0005,3,1)  //"Data informada não pode ser maior que a data corrente."
		Return .f.
	EndIf
Return .t.
//------------------------------------------------------------------------------
/*/{Protheus.doc} MNT760INI
Processa as inspecoes
@type function

@author Thiago Olis Machado
@since 18/12/2006

@sample NGMUCanReq( , .F. )

@param lEnd     , Lógico, Barra de processamento.
@param lImpRel  , Lógico, Indica se deve imprimir o relatório.
/*/

//------------------------------------------------------------------------------
Function Mnt760Ini(lEnd,lImpRel)
	Local i,cOrdem 	:= ''
	Local cNGOCINS	:= GETMV("MV_NGOCINS")
	Local aIncons	:= {} //Conteúdo para relatório de inconsistências de inspeção (MNTR545)
	Local lRetImp	:= .F. //Verifica se tem daods para imprimir o relatório e atualizar a inspeção.
	
	Default lImpRel := .T.

	cAliasQry := GetNextAlias()
	cQuery := " SELECT *"
	cQuery += " FROM "+RetSqlName("TRC")+" TRC WHERE TRC.TRC_CICLO IN(SELECT MAX(TR9.TR9_CICLO)"
	cQuery += " FROM "+RetSqlName("TR9")+" TR9"
	cQuery += " WHERE TR9.TR9_ORDEM = TRC.TRC_ORDEM"
	cQuery += " AND TR9.TR9_FILIAL  >= '"+MV_PAR01+"' AND TR9.TR9_FILIAL <= '" +MV_PAR02+"'
	cQuery += " AND TR9.TR9_DTINSP >=  '"+DTOS(MV_PAR03)+"'"
	cQuery += " AND TR9.TR9_STATUS = '3'"
	cQuery += " AND TR9.TR9_FILIAL = TRC.TRC_FILIAL"
	cQuery += " GROUP BY TR9.TR9_ORDEM)"
	cQuery := ChangeQuery(cQuery)
	DBUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	ProcRegua(LastRec())

	If !EoF()

		dbGoTop()
		While !EoF()

			IncProc()
			If lEnd
				@ Prow()+1,001 PSay STR0006
				Exit
			EndIf

			DbSelectArea("TR9")
			DbSetOrder(1)
			cFilTR9 := NGTROCAFILI("TR9",(cAliasQry)->TRC_FILIAL)
			If DbSeek(cFilTR9+(cAliasQry)->TRC_ORDEM+(cAliasQry)->TRC_PLANO+(cAliasQry)->TRC_CICLO)
				For i:= 1 To 10
					If i = 10
						lNewLoc := .f.
						If !Empty((cAliasQry)->TRC_NFOG10)
							DbSelectArea("TQS")
							DbSetOrder(01)
							cFilTQS := NGTROCAFILI("TQS",(cAliasQry)->TRC_FILIAL)
							If DbSeek(cFilTQS+(cAliasQry)->TRC_NFOG10)
								RecLock("TQS",.f.)
								TQS->TQS_POSIC  := (cAliasQry)->TRC_LOCP10
								TQS->TQS_SULCAT := Mnt760MS(10)
								TQS->TQS_EIXO	 := AllTrim((cAliasQry)->TRC_EIXO)
								TQS->TQS_DTMEAT := TR9->TR9_DTINSP
								TQS->TQS_HRMEAT := TR9->TR9_HRINSP
								TQS->TQS_TIPEIX := Mnt760TE(10)
								TQS->(MsUnLock())
							Endif

							//atualiza STZ
							cPneu := (cAliasQry)->TRC_NFOG10+Space(Len(TQS->TQS_CODBEM)-Len((cAliasQry)->TRC_NFOG10))
							Dbselectarea("STZ")
							Dbsetorder(1)
							cFilSTZ := NGTROCAFILI("STZ",(cAliasQry)->TRC_FILIAL)
							If Dbseek(cFilSTZ+cPneu+'E')
								While !EoF() .And. STZ->TZ_FILIAL == cFilSTZ .AND.;
								STZ->TZ_CODBEM == cPneu .And. STZ->TZ_TIPOMOV = 'E'
									If Empty(STZ->TZ_DATASAI) .And. STZ->TZ_LOCALIZ <> (cAliasQry)->TRC_LOCP10
										lNewLoc := .t.
										RecLock("STZ",.F.)
										STZ->TZ_TIPOMOV := 'S'
										STZ->TZ_DATASAI := TR9->TR9_DTINSP
										STZ->TZ_CONTSAI := TR9->TR9_KMATU
										STZ->TZ_HORASAI := TR9->TR9_HRINSP
										STZ->TZ_CAUSA   := cNGOCINS
										STZ->TZ_ORDEM   := TR9->TR9_ORDEM
										STZ->TZ_PLANO   := TR9->TR9_PLANO
										STZ->(MsUnLock())
									Endif
									Dbselectarea("STZ")
									Dbskip()
								End
							Endif

							//inclui registro de entrada stz
							If lNewLoc
								DbSelectArea("ST9")
								DbSetOrder(16)
								DbSeek(TR9->TR9_FROTA+"A")
								RecLock("STZ",.T.)
								STZ->TZ_FILIAL  := NGTROCAFILI("STZ",(cAliasQry)->TRC_FILIAL)
								STZ->TZ_CODBEM  := cPneu
								STZ->TZ_BEMPAI  := TR9->TR9_FROTA
								STZ->TZ_TIPOMOV := 'E'
								STZ->TZ_DATAMOV := TR9->TR9_DTINSP
								STZ->TZ_POSCONT := TR9->TR9_KMATU
								STZ->TZ_LOCALIZ := (cAliasQry)->TRC_LOCP10
								STZ->TZ_TEMCONT := "P"
								STZ->TZ_TEMCPAI := ST9->T9_TEMCONT
								STZ->TZ_HORAENT := TR9->TR9_HRINSP
								STZ->TZ_HORACO1 := TR9->TR9_HRINSP
								STZ->TZ_ORDEM   := TR9->TR9_ORDEM
								STZ->TZ_PLANO   := TR9->TR9_PLANO
								MsUnLock("STZ")
							EndIf
							DbSelectArea("STC")
							DbSetOrder(01)
							If DbSeek(xFilial("STC")+TR9->TR9_FROTA+(cAliasQry)->TRC_NFOG10)
								RecLock("STC",.f.)
								STC->TC_LOCALIZ := (cAliasQry)->TRC_LOCP10
								STC->(MsUnLock())
							EndIf
						EndIf

						cHoraTQS := Alltrim(TQS->TQS_HRMEAT)
						lHoraOk  := If(cHoraTQS = ":" .Or. Empty(cHoraTQS),.F.,.T.)
						If !Empty((cAliasQry)->TRC_NFOG10) .And. !Empty(TR9->TR9_DTINSP) .And.;
						lHoraOk .And. !Empty(TQS->TQS_BANDAA)
							DbSelectArea("TQV")
							DbSetOrder(01)
							cEspaco := Space(Len(TQS->TQS_CODBEM)-Len((cAliasQry)->TRC_NFOG10))
							cFilTQV := NGTROCAFILI("TQV",(cAliasQry)->TRC_FILIAL)
							If !DbSeek(cFilTQV+(cAliasQry)->TRC_NFOG10+cEspaco+Dtos(TR9->TR9_DTINSP)+TQS->TQS_HRMEAT+TQS->TQS_BANDAA)
								RecLock("TQV",.t.)
								TQV->TQV_FILIAL  := xFilial("TQV")
								TQV->TQV_CODBEM  := (cAliasQry)->TRC_NFOG10
								TQV->TQV_DTMEDI  := TR9->TR9_DTINSP
								TQV->TQV_HRMEDI  := TQS->TQS_HRMEAT
								TQV->TQV_BANDA   := TQS->TQS_BANDAA
								TQV->TQV_DESENH  := TQS->TQS_DESENH
								TQV->TQV_SULCO   := TQS->TQS_SULCAT
								TQV->(MsUnLock())
							EndIf
						EndIf
					Else
						lNewLoc := .f.
						cFogo  := '(cAliasQry)->TRC_NFOGO'+AllTrim(Str(i))
						cLocPn := '(cAliasQry)->TRC_LOCPN'+AllTrim(Str(i))
						If !Empty(&cFogo)
							DbSelectArea("TQS")
							DbSetOrder(01)
							cFilTQS := NGTROCAFILI("TQS",(cAliasQry)->TRC_FILIAL)
							If DbSeek(cFilTQS+&cFogo)
								RecLock("TQS",.f.)
								TQS->TQS_POSIC  := &cLocPn
								TQS->TQS_SULCAT := Mnt760MS(i)
								TQS->TQS_EIXO	 := AllTrim((cAliasQry)->TRC_EIXO)
								TQS->TQS_DTMEAT := TR9->TR9_DTINSP
								TQS->TQS_HRMEAT := TR9->TR9_HRINSP
								TQS->TQS_TIPEIX := Mnt760TE(i)
								TQS->(MsUnLock())
							Endif

							//atualiza STZ
							cPneu := &cFogo+Space(Len(TQS->TQS_CODBEM)-Len(&cFogo))
							Dbselectarea("STZ")
							Dbsetorder(1)
							cFilSTZ := NGTROCAFILI("STZ",(cAliasQry)->TRC_FILIAL)
							If Dbseek(cFilSTZ+cPneu+'E')
								While !Eof() .And. STZ->TZ_FILIAL == cFilSTZ .AND.;
								STZ->TZ_CODBEM == cPneu .And. STZ->TZ_TIPOMOV = 'E'
									If Empty(STZ->TZ_DATASAI) .And. STZ->TZ_LOCALIZ <> &cLocPn
										lNewLoc := .t.
										RecLock("STZ",.F.)
										STZ->TZ_TIPOMOV := 'S'
										STZ->TZ_DATASAI := TR9->TR9_DTINSP
										STZ->TZ_CONTSAI := TR9->TR9_KMATU
										STZ->TZ_HORASAI := TR9->TR9_HRINSP
										STZ->TZ_CAUSA   := cNGOCINS
										STZ->TZ_ORDEM   := TR9->TR9_ORDEM
										STZ->TZ_PLANO   := TR9->TR9_PLANO
										STZ->(MsUnLock())
									Endif
									Dbselectarea("STZ")
									Dbskip()
								End
							Endif

							//inclui registro de entrada stz
							If lNewLoc
								DbSelectArea("ST9")
								DbSetOrder(16)
								DbSeek(TR9->TR9_FROTA+"A")
								RecLock("STZ",.T.)
								STZ->TZ_FILIAL  := NGTROCAFILI("STZ",(cAliasQry)->TRC_FILIAL)
								STZ->TZ_CODBEM  := cPneu
								STZ->TZ_BEMPAI  := TR9->TR9_FROTA
								STZ->TZ_TIPOMOV := 'E'
								STZ->TZ_DATAMOV := TR9->TR9_DTINSP
								STZ->TZ_POSCONT := TR9->TR9_KMATU
								STZ->TZ_LOCALIZ := &cLocPn
								STZ->TZ_TEMCONT := "P"
								STZ->TZ_TEMCPAI := ST9->T9_TEMCONT
								STZ->TZ_HORAENT := TR9->TR9_HRINSP
								STZ->TZ_HORACO1 := TR9->TR9_HRINSP
								STZ->TZ_ORDEM   := TR9->TR9_ORDEM
								STZ->TZ_PLANO   := TR9->TR9_PLANO
								MsUnLock("STZ")
							EndIf

							DbSelectArea("STC")
							DbSetOrder(01)
							If DbSeek(xFilial("STC")+TR9->TR9_FROTA+&cFogo)
								RecLock("STC",.f.)
								STC->TC_LOCALIZ := &cLocPn
								STC->(MsUnLock())
							EndIf

							cHoraTQS := Alltrim(TQS->TQS_HRMEAT)
							lHoraOk  := If(cHoraTQS = ":" .Or. Empty(cHoraTQS),.F.,.T.)

							If !Empty(&cFogo) .And. !Empty(TR9->TR9_DTINSP) .And.;
							lHoraOk .And. !Empty(TQS->TQS_BANDAA)
								DbSelectArea("TQV")
								DbSetOrder(01)
								cFilTQV := NGTROCAFILI("TQV",(cAliasQry)->TRC_FILIAL)
								If !DbSeek(cFilTQV+&cFogo+Space(Len(TQV->TQV_CODBEM)-Len(&cFogo))+;
								Dtos(TR9->TR9_DTINSP)+TQS->TQS_HRMEAT+TQS->TQS_BANDAA)
									RecLock("TQV",.t.)
									TQV->TQV_FILIAL  := xFilial("TQV")
									TQV->TQV_CODBEM  := &cFogo
									TQV->TQV_DTMEDI  := TR9->TR9_DTINSP
									TQV->TQV_HRMEDI  := TQS->TQS_HRMEAT
									TQV->TQV_BANDA   := TQS->TQS_BANDAA
									TQV->TQV_DESENH  := TQS->TQS_DESENH
									TQV->TQV_SULCO   := TQS->TQS_SULCAT
									TQV->(MsUnLock())
								Endif
							EndIf
						EndIf
					EndIf
				Next
				RecLock("TR9",.f.)
				TR9->TR9_STATUS := '4'
				TR9->(MsUnLock())
				If (cAliasQry)->TRC_ORDEM <> cOrdem
					NGFIMOS((cAliasQry)->TRC_ORDEM,(cAliasQry)->TRC_PLANO,,,,,,,"SIM",TR9->TR9_DTINSP,TR9->TR9_HRINSP)
					cOrdem := (cAliasQry)->TRC_ORDEM
				EndIf
			EndIf

			DbSelectArea(cAliasQry)
			DbSkip()
		End
	Else
		//Adiciona o conteúdo dos parâmetros para envio ao relatório de inconsistências
		aAdd(aIncons,MV_PAR01)
		aAdd(aIncons,MV_PAR02)
		aAdd(aIncons,MV_PAR03)
		//Chama relatório de posições de inspeção de pneus
		If lImpRel
			lRetImp := MNTR545(aIncons)
		EndIf
		If lRetImp
			//Atualiza as posições dos pneus
			MNT760INI(,.F.)
		EndIf
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNT760MS ³ Autor ³ Thiago Olis Machado   ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Obtem o menor sulco das medicoes.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Mnt760MS(nPneu)
	Local aArea := GetArea()
	Local x,nSulco := 9999
	Local aSulco := {}

	For x := 1 To 3
		cNFogo := '(cAliasQry)->TRC_PN'+AllTrim(Str(nPneu))+'M'+AllTrim(Str(x))
		nSulco := Min(nSulco,&(cNFogo))
		aAdd(aSulco,nSulco)
	Next
	//Ordena os valores para descartar valor zero
	aSulco := aSort(aSulco)
	nSulco := If(aSulco[1] == 0, If(aSulco[2] == 0,aSulco[3],aSulco[2]),aSulco[1])

	RestArea(aArea)
Return nSulco

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNT760TE ³ Autor ³ Thiago Olis Machado   ³ Data ³18/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Obtem o tipo do eixo onde esta o pneu                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Mnt760TE()
	Local aArea := GetArea()
	Local nEixo := '1'

	DbSelectArea("ST9")
	DbSetOrder(16)
	DbSeek(TR9->TR9_FROTA)

	DbSelectArea("TQ1")
	DbSetOrder(1)
	DbSeek(xFilial("TQ1")+ST9->T9_CODFAMI+ST9->T9_TIPMOD)
	While !Eof() .and. TQ1->TQ1_FILIAL == xFilial("TQ1")   .and.;
	TQ1->TQ1_DESENH == ST9->T9_CODFAMI  .and.;
	TQ1->TQ1_TIPMOD == ST9->T9_TIPMOD

		If TQ1->TQ1_EIXO = (cAliasQry)->TRC_EIXO
			nEixo := TQ1->TQ1_TIPEIX
			Exit
		EndIf

		DbSkip()
	End

	RestArea(aArea)
Return nEixo
