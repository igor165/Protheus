#INCLUDE "mntc760.ch"
#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTC760  ³ Autor ³ Paulo Prgo            ³ Data ³ 06/07/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta Previsao de consumo de Produtos                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAMNT                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTC760()

Private oTempTRB
Private cAliasTRB := GetNextAlias()

aDBF := {{"DTPREV"   ,"D" ,08,0},;
          {"QUANT"   ,"N" ,05,0},;
          {"CODBEM"  ,"C" ,16,0},;
          {"NOMBEM"  ,"C" ,40,0},;
          {"CODSERV" ,"C" ,06,0},;
          {"NOMSERV" ,"C" ,40,0} }

aFIELD := {}
AAdd(aFIELD,{"DTPREV"   ,STR0001 , "99/99/99" }) //"Data"
AAdd(aFIELD,{"QUANT"    ,STR0002 , "99999"    }) //"Quantidade"
AAdd(aFIELD,{"CODBEM"   ,STR0003 , "@!"       }) //"Equipamento"
AAdd(aFIELD,{"NOMBEM"   ,STR0004 , "@!"       }) //"Descricao"
AAdd(aFIELD,{"CODSERV"  ,STR0005 , "@!"       }) //"Servico"
AAdd(aFIELD,{"NOMSERV"  ,STR0004 , "@!"       }) //"Descricao"

aHEADER := {}
AAdd(aHEADER,"DATA"     )
AAdd(aHEADER,"QUANT"    )
AAdd(aHEADER,"CODBEM"   )
AAdd(aHEADER,"NOMBEM"   )
AAdd(aHEADER,"CODSERV"  )
AAdd(aHEADER,"NOMSERV"  )

oTempTRB := FWTemporaryTable():New( cAliasTRB, aDBF )
oTempTRB:AddIndex( "1", {"DTPREV"} )
oTempTRB:Create()

cCOD   := Space(15)
cDesc  := Space(40)
cEntr  := Space(10)

Define FONT NgFont NAME "Courier New" Size 6, 0
Define MsDialog oDlg2 Title STR0006 From 8,0 To 38,80 Of oMainWnd //"Previsao Consumo de Produtos"

    @ 01.5,01  Say STR0007 //"Codigo"
    @ 01  ,5.5 MsGet cCOD Valid MNTC760P() F3 "SB1"

    @ 02.5,01  Say STR0008 //"Produto"
    @ 02.5,5.5 Say oDESC Var cDESC

    @ 03.5,01  Say STR0009 //"Prazo Entrega"
    @ 03.5,5.5 Say oENTR Var cENTR

    @ 4.5,00 ListBox oBrw        ;
           Fields DtoC(DTPREV), STR(QUANT,5), CODBEM, NOMBEM, CODSERV ,NOMSERV ;
           FieldSizes 60,60,80,200,60,200                                       ;
           Size 315,090                                                         ;
           HEADERS STR0001,STR0002,STR0010,STR0011,STR0005,STR0012 //"Data"###"Quantidade"###"Bem"###"Nome do Bem"###"Servico"###"Nome Servico"

Activate MsDialog oDlg2 On Init EnchoiceBar(oDlg2,{||nOpca:=1,oDlg2:End()},{||oDlg2:End()})

oTempTRB:Delete()

DbSelectArea("ST9")
Return Nil

****************************************************************************
Function MNTC760P()
Local cCOMPL

DbSelectArea("SB1")
If !DbSeek(xFilial("SB1") + cCOD)
   Help("",1, "CODNEXIST")
   Return .F.
EndIf

cDESC := SB1->B1_DESC

cCompl := " "
cEntr  := Str(sb1->b1_pe,5)

If     sb1->b1_tipe == "H"
       cCOMPL := If(sb1->b1_pe > 1, STR0013, STR0014) //"Horas"###"Hora"
ElseIf sb1->b1_tipe == "M"
       cCOMPL := If(sb1->b1_pe > 1, STR0015, STR0016) //"Meses"###"Mes"
ElseIf sb1->b1_tipe == "S"
       cCOMPL := If(sb1->b1_pe > 1, STR0017, STR0018) //"Semanas"###"Semana"
ElseIf sb1->b1_tipe == "A"
       cCOMPL := If(sb1->b1_pe > 1, STR0019, STR0020) //"Anos"###"Ano"
Else
       cCOMPL := If(sb1->b1_pe > 1, STR0021, STR0022) //"Dias"###"Dia"
EndIf

cENTR := cENTR + " " + cCOMPL

oDESC:Refresh()
oENTR:Refresh()

DbSelectArea("STG")
DbSetOrder(02)
DbSeek(xFilial("STG")+"P"+cCOD)
While !Eof() .And. STG->TG_FILIAL  == xFILIAL("STG") .And.  STG->TG_TIPOREG == "P" .And. ;
   STG->TG_CODIGO  == cCOD

   dINI := dDATABASE
   dFIM := CtoD("01/01/2030")
   aDAT := NGPRIMAN(STG->TG_CODBEM, STG->TG_SERVICO, STG->TG_SEQRELA, dINI, dFIM, "N")

   DbSelectArea("ST9")
   DbSeek(xFilial("ST9")+STG->TG_CODBEM)

   DbSelectArea("ST4")
   DbSeek(xFilial("ST4")+STG->TG_SERVICO)

   (cAliasTRB)->(DbAppend())
   (cAliasTRB)->DTPREV  := aDAT[2]
   (cAliasTRB)->QUANT   := STG->TG_QUANTID
   (cAliasTRB)->CODBEM  := STG->TG_CODBEM
   (cAliasTRB)->NOMBEM  := ST9->T9_NOME
   (cAliasTRB)->CODSERV := STG->TG_SERVICO
   (cAliasTRB)->NOMSERV := ST4->T4_NOME

   DbSelectArea("STG")
   DbSkip()
End

DbSelectArea("STG")
DbSetOrder(01)

DbSelectArea(cAliasTRB)
DbGotop()

obrw:Refresh()
Return .T.