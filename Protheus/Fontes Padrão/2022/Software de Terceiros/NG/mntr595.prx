#INCLUDE "MNTR595.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR595   ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 01/03/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Acidentes por Tipo de Evento		              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR595()

	WNREL      := "MNTR595"
	LIMITE     := 220
	cDESC1     := STR0001 //"O relatório apresentará a quantidade de eventos para cada "
	cDESC2     := STR0002 //"mês do ano escolhido pelo usuário. "
	cDESC3     := " "
	cSTRING    := "TRH"

	Private NOMEPROG := "MNTR595"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0003,1,STR0004,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0005 //"Relatório de Acidentes por Tipo de Evento"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
	Private aVETINR := {}
	Private cPERG := "MNR595"
	Private aPerg :={}

	SetKey( VK_F9, { | | NGVersao( "MNTR595" , 1 ) } )

	Pergunte(cPERG,.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TRH")
		Return
	EndIf
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR595IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0009,STR0010) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("TRH")

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR595IMP | Autor ³ Marcos Wagner Junior  ³ Data ³ 01/03/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR595                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR595IMP(lEND,WNREL,TITULO,TAMANHO)
	Local nI
	Local cOldTipaci := ""
	Local cOldFiltro := TRH->(dbFilter())
	Private cRODATXT := ""
	Private nCNTIMPR := 0
	Private li := 80 ,m_pag := 1
	Private cNomeOri
	Private aVetor := {}
	Private aTotGeral := {}
	Private nAno, nMes
	Private nTotCarga := 0, nTotManut := 0
	Private nTotal := 0


	nTIPO  := IIf(aReturn[4]==1,15,18)

	CABEC1 := ""
	CABEC2 := ""

	cAliasQry2 := GetNextAlias()
	cQuery2 := " SELECT * "
	cQuery2 += " FROM " + RetSqlName("TRH")+" TRH"
	cQuery2 += " WHERE TRH.TRH_EVENTO  = '1'"
	cQuery2 += " AND   TRH.TRH_DTACID  >= '"+AllTrim(Str(MV_PAR01))+"0101'"
	cQuery2 += " AND   TRH.TRH_DTACID  <= '"+AllTrim(Str(MV_PAR01))+"1231'"
	cQuery2 += " AND   TRH.TRH_TIPACI  >= '"+MV_PAR02+"'"
	cQuery2 += " AND   TRH.TRH_TIPACI  <= '"+MV_PAR03+"'"
	cQuery2 += " AND   TRH.TRH_GRAVID  >= '"+AllTrim(Str(MV_PAR04))+"'"
	cQuery2 += " AND   TRH.TRH_GRAVID  <= '"+AllTrim(Str(MV_PAR05))+"'"
	cQuery2 += " AND   TRH.D_E_L_E_T_ <> '*' "
	cQuery2 += " ORDER BY TRH.TRH_TIPACI"
	cQuery2 := ChangeQuery(cQuery2)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery2),cAliasQry2, .F., .T.)
	dbGoTop()
	While !Eof()
		dbSelectArea("TRH")
		dbSetOrder(1)
		If dbSeek(xFilial("TRH") + (cAliasQry2)->TRH_NUMSIN)
			If ((Empty(cOldFiltro) .Or. &(cOldFiltro)) .AND. (((cAliasQry2)->TRH_TIPACI <> cOldTipaci) .OR. Empty(cOldTipaci)))
				cOldTipaci := (cAliasQry2)->TRH_TIPACI
				Aadd(aVetor,{(cAliasQry2)->TRH_TIPACI,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0})
			EndIf
		EndIf
		dbSelectArea(cAliasQry2)
		dbSkip()
	End

	Aadd(aTotGeral,{'Total',0,0,0,0,0,0,0,0,0,0,0,0,0})

	cAliasQry := GetNextAlias()
	cQuery := " SELECT TRH.TRH_NUMSIN, TRH.TRH_DTACID, TRH.TRH_TIPACI, TRH.TRH_VALGUI, TRH.TRH_VALANI, TRH.TRH_NUMBO "
	cQuery += " FROM " + RetSqlName("TRH")+" TRH"
	cQuery += " WHERE TRH.TRH_EVENTO  = '1'"
	cQuery += " AND   TRH.TRH_DTACID  >= '"+AllTrim(Str(MV_PAR01))+"0101'"
	cQuery += " AND   TRH.TRH_DTACID  <= '"+AllTrim(Str(MV_PAR01))+"1231'"
	cQuery += " AND   TRH.TRH_TIPACI  >= '"+MV_PAR02+"'"
	cQuery += " AND   TRH.TRH_TIPACI  <= '"+MV_PAR03+"'"
	cQuery += " AND   TRH.TRH_GRAVID  >= '"+AllTrim(Str(MV_PAR04))+"'"
	cQuery += " AND   TRH.TRH_GRAVID  <= '"+AllTrim(Str(MV_PAR05))+"'"
	cQuery += " AND   TRH.D_E_L_E_T_ <> '*' "
	cQuery += " ORDER BY TRH.TRH_TIPACI"
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)


	dbSelectArea(cAliasQry)
	dbGoTop()
	If !Eof()
		While !Eof()

			dbSelectArea("TRH")
			dbSetOrder(1)
			If dbSeek(xFilial("TRH") + (cAliasQry)->TRH_NUMSIN)
				If Empty(cOldFiltro) .Or. &(cOldFiltro)

					nMes := Val(SubStr((cAliasQry)->TRH_DTACID,5,2))
					For nI := 1 to Len(aVetor)
						If aVetor[nI][1] = (cAliasQry)->TRH_TIPACI

							aVetor[nI][nMes+1] := aVetor[nI][nMes+1] + 1
							aVetor[nI][14] := aVetor[nI][14] + 1
							DbSelectArea("TRK")
							DbSetOrder(01)
							If DbSeek(xFilial("TRK")+(cAliasQry)->TRH_NUMSIN)
								While !Eof() .AND. TRK->TRK_FILIAL = xFilial("TRK") .AND. TRK->TRK_NUMSIN = (cAliasQry)->TRH_NUMSIN
									aVetor[nI][15] := aVetor[nI][15] + TRK->TRK_VALAVA//PREJUIZO CARGA
									aVetor[nI][17] := aVetor[nI][17] + TRK->TRK_VALAVA//PREJUIZO TOTAL
									nTotCarga := nTotCarga + TRK->TRK_VALAVA

									dbSelectArea("TRK")
									dbSkip()

								End
							Endif

							aVetor[nI][17] := aVetor[nI][17] + (cAliasQry)->TRH_VALGUI + (cAliasQry)->TRH_VALANI


							DbSelectArea("TRO")
							DbSetOrder(01)
							If DbSeek(xFilial("TRO")+(cAliasQry)->TRH_NUMSIN)
								While !Eof() .AND. TRO->TRO_FILIAL = xFilial("TRO") .AND. TRO->TRO_NUMSIN = (cAliasQry)->TRH_NUMSIN
									aVetor[nI][17] := aVetor[nI][17] + TRO->TRO_VALPRE

									dbSelectArea("TRO")
									dbSkip()
								End
							Endif

							DbSelectArea("TRL")
							DbSetOrder(01)
							If DbSeek(xFilial("TRL")+(cAliasQry)->TRH_NUMSIN)
								While !Eof() .AND. TRL->TRL_FILIAL = xFilial("TRL") .AND. TRL->TRL_NUMSIN = (cAliasQry)->TRH_NUMSIN
									aVetor[nI][17] := aVetor[nI][17] + TRL->TRL_VALPRE

									dbSelectArea("TRL")
									dbSkip()
								End
							Endif

							DbSelectArea("TRV")
							DbSetOrder(01)
							If DbSeek(xFilial("TRV")+(cAliasQry)->TRH_NUMSIN)
								While !Eof() .AND. TRV->TRV_FILIAL = xFilial("TRV") .AND. TRV->TRV_NUMSIN = (cAliasQry)->TRH_NUMSIN
									aVetor[nI][17] := aVetor[nI][17] - TRV->TRV_VALRES

									dbSelectArea("TRV")
									dbSkip()
								End
							Endif

							DbSelectArea("TRM")
							DbSetOrder(01)
							If DbSeek(xFilial("TRM")+(cAliasQry)->TRH_NUMSIN)
								While !Eof() .AND. TRM->TRM_FILIAL = xFilial("TRM") .AND. TRM->TRM_NUMSIN = (cAliasQry)->TRH_NUMSIN
									aVetor[nI][17] := aVetor[nI][17] + TRM->TRM_VALVIT

									dbSelectArea("TRM")
									dbSkip()
								End
							Endif

							DbSelectArea("TRT")
							DbSetOrder(02)
							If DbSeek(xFilial("TRT")+(cAliasQry)->TRH_NUMSIN)
								While !Eof() .AND. TRT->TRT_FILIAL = xFilial("TRT") .AND. TRT->TRT_NUMSIN = (cAliasQry)->TRH_NUMSIN
									dbSelectArea("STJ")
									dbSetOrder(01)
									If dbSeek(xFilial("STJ")+TRT->TRT_NUMOS+TRT->TRT_PLANO)
										If (STJ->TJ_TERMINO = 'N') .OR. (STJ->TJ_TERMINO = 'S')// .AND. (STJ->TJ_SEQRELA <> '0'))
											dbSelectArea("STL")
											dbSetOrder(01)
											If dbSeek(xFilial("STL")+TRT->TRT_NUMOS+TRT->TRT_PLANO)
												While !Eof() .AND. STL->TL_FILIAL = xFilial("STL") .AND.;
												STL->TL_ORDEM = TRT->TRT_NUMOS  .AND.;
												STL->TL_PLANO = TRT->TRT_PLANO
													If STL->TL_SEQRELA <> '0'
														aVetor[nI][16] := aVetor[nI][16] + STL->TL_CUSTO
														aVetor[nI][17] := aVetor[nI][17] + STL->TL_CUSTO
														nTotManut := nTotManut + STL->TL_CUSTO
													EndIf
													dbSelectArea("STL")
													dbSkip()
												End
											Endif
										Endif
									Endif
									dbSelectArea("TRT")
									dbSkip()
								End
							Endif
						Endif
					Next
					dbSelectArea(cAliasQry)
					dbSkip()
					Loop

				EndIf
				dbSelectArea(cAliasQry)
				dbSkip()
				Loop
			EndIf
			dbSelectArea(cAliasQry)
			dbSkip()
			Loop
		End
	Else
		MsgInfo(STR0011,STR0012) //"Não existem dados para montar o relatório!"###"ATENÇÃO"
		Return .F.
	EndIf

	(cALIASQRY)->(dbCloseArea())

	For nI := 1 to Len(aVetor)
		nTotal := nTotal + aVetor [nI][17]
	Next

	NgSomaLi(58)
	@ Li,000 	 Psay STR0013 //"Tipo Acidente"
	@ Li,027 	 Psay "|"
	@ Li,029 	 Psay STR0014 //"JAN"
	@ Li,034 	 Psay STR0015 //"FEV"
	@ Li,039 	 Psay STR0016 //"MAR"
	@ Li,044 	 Psay STR0017 //"ABR"
	@ Li,049 	 Psay STR0018 //"MAI"
	@ Li,054 	 Psay STR0019 //"JUN"
	@ Li,059 	 Psay STR0020 //"JUL"
	@ Li,064 	 Psay STR0021 //"AGO"
	@ Li,069 	 Psay STR0022 //"SET"
	@ Li,074 	 Psay STR0023 //"OUT"
	@ Li,079 	 Psay STR0024 //"NOV"
	@ Li,084 	 Psay STR0025 //"DEZ"
	@ Li,090 	 Psay STR0026 //"TOTAL"
	@ Li,100 	 Psay STR0027 //"PREJ.CARGA"
	@ Li,118 	 Psay "%"
	@ Li,125 	 Psay STR0028 //"%AC."
	@ Li,133 	 Psay STR0029 //"PREJ. MNT"
	@ Li,150 	 Psay "%"
	@ Li,156 	 Psay STR0028 //"%AC."
	@ Li,166	 Psay STR0040 //PREJ.OUTROS
	@ Li,185 	 Psay STR0030 //"PREJ. TOTAL"
	@ Li,208 	 Psay "%"
	@ Li,214 	 Psay STR0028 //"%AC."

	NgSomaLi(58)
	@ Li,000 	 Psay Replicate("-",220)
	NgSomaLi(58)

	dbSelectArea("TRH")
	dbSetOrder(1)
	dbGoTop()
	For nI := 1 to Len(aVetor)

		cDesc := Posicione("SX5", 1, xFilial("SX5")+"AF"+aVetor[nI][1], "X5Descri()")
		@ Li,000 	 Psay cDesc
		@ Li,027 	 Psay "|"
		@ Li,029 	 Psay aVetor[nI][2]
		aTotGeral[1][2] := aTotGeral[1][2] + aVetor[nI][2]
		@ Li,034 	 Psay aVetor[nI][3]
		aTotGeral[1][3] := aTotGeral[1][3] + aVetor[nI][3]
		@ Li,039 	 Psay aVetor[nI][4]
		aTotGeral[1][4] := aTotGeral[1][4] + aVetor[nI][4]
		@ Li,044 	 Psay aVetor[nI][5]
		aTotGeral[1][5] := aTotGeral[1][5] + aVetor[nI][5]
		@ Li,049 	 Psay aVetor[nI][6]
		aTotGeral[1][6] := aTotGeral[1][6] + aVetor[nI][6]
		@ Li,054 	 Psay aVetor[nI][7]
		aTotGeral[1][7] := aTotGeral[1][7] + aVetor[nI][7]
		@ Li,059 	 Psay aVetor[nI][8]
		aTotGeral[1][8] := aTotGeral[1][8] + aVetor[nI][8]
		@ Li,064 	 Psay aVetor[nI][9]
		aTotGeral[1][9] := aTotGeral[1][9] + aVetor[nI][9]
		@ Li,069 	 Psay aVetor[nI][10]
		aTotGeral[1][10] := aTotGeral[1][10] + aVetor[nI][10]
		@ Li,074 	 Psay aVetor[nI][11]
		aTotGeral[1][11] := aTotGeral[1][11] + aVetor[nI][11]
		@ Li,079 	 Psay aVetor[nI][12]
		aTotGeral[1][12] := aTotGeral[1][12] + aVetor[nI][12]
		@ Li,084 	 Psay aVetor[nI][13]
		aTotGeral[1][13] := aTotGeral[1][13] + aVetor[nI][13]
		@ Li,090 	 Psay aVetor[nI][14]
		aTotGeral[1][14] := aTotGeral[1][14] + aVetor[nI][14]
		@ Li,096 	 Psay aVetor[nI][15] Picture '@E 999,999,999.99'

		@ Li,112 Psay PADL(Transform((aVetor[nI][15]/nTotCarga*100),"@R 999.99"),6)+'%'
		@ Li,128 	 Psay aVetor[nI][16] Picture '@E 999,999,999.99'

		@ Li,144 Psay PADL(Transform((aVetor[nI][16]/nTotManut*100),"@R 999.99"),6)+'%'
		@ Li,163 	 Psay (aVetor[nI][17]-aVetor[nI][16]-aVetor[nI][15]) Picture '@E 999,999,999.99'
		@ Li,182 	 Psay aVetor[nI][17] Picture '@E 999,999,999.99'

		@ Li,202 Psay PADL(Transform((aVetor[nI][17]/nTotal*100),"@R 999.99"),6)+'%'

		If nI == 1

			@ Li,122 Psay PADL(Transform((aVetor[nI][15]/nTotCarga*100),"@R 999.99"),6)+'%'
			@ Li,154 Psay PADL(Transform((aVetor[nI][16]/nTotManut*100),"@R 999.99"),6)+'%'
			@ Li,212 Psay PADL(Transform((aVetor[nI][17]/nTotal*100),"@R 999.99"),6)+'%'
		Else
			@ Li,122 Psay PADL(Transform((aVetor[nI][15]/nTotCarga*100)+(aVetor[nI-1][15]/nTotCarga*100),"@R 999.99"),6)+'%'
			@ Li,154 Psay PADL(Transform((aVetor[nI][16]/nTotManut*100)+(aVetor[nI-1][16]/nTotManut*100),"@R 999.99"),6)+'%'
			@ Li,212 Psay PADL(Transform((aVetor[nI][17]/nTotal*100)+(aVetor[nI-1][17]/nTotal*100),"@R 999.99"),6)+'%'

		Endif
		NgSomaLi(58)
		//EndIf
		//dbSelectArea("TRH")
		//dbSkip()
	Next
	@ Li,000 	 Psay Replicate("-",220)
	NgSomaLi(58)
	@ Li,000 	 Psay aTotGeral[1][1]
	@ Li,027 	 Psay "|"
	@ Li,029 	 Psay aTotGeral[1][2]
	@ Li,034 	 Psay aTotGeral[1][3]
	@ Li,039 	 Psay aTotGeral[1][4]
	@ Li,044 	 Psay aTotGeral[1][5]
	@ Li,049 	 Psay aTotGeral[1][6]
	@ Li,054 	 Psay aTotGeral[1][7]
	@ Li,059 	 Psay aTotGeral[1][8]
	@ Li,064 	 Psay aTotGeral[1][9]
	@ Li,069 	 Psay aTotGeral[1][10]
	@ Li,074 	 Psay aTotGeral[1][11]
	@ Li,079 	 Psay aTotGeral[1][12]
	@ Li,084 	 Psay aTotGeral[1][13]
	@ Li,090 	 Psay aTotGeral[1][14]
	@ Li,096 	 Psay nTotCarga Picture '@E 999,999,999.99'
	@ Li,128 	 Psay nTotManut Picture '@E 999,999,999.99'
	@ Li,163 	 Psay (nTotal-nTotCarga-nTotManut)	 Picture '@E 999,999,999.99'
	@ Li,182 	 Psay nTotal	 Picture '@E 999,999,999.99'


	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principal             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex('TRH')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MNTR590VAL| Autor ³Marcos Wagner Junior   ³ Data ³ 13/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o |Valida todos codigos De... , Ate...								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR590                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR595VAL(nOpc,cParDe,cParAte,cTabela)

	Local lRet := .f.

	If (Empty(cParDe) .AND. cParAte = 'ZZ')
		lRet := .t.
	Else
		If nOpc == 1
			If Empty(cParDe)
				lRet := .t.
			Else
				lRet := IIf(Empty(cParDe),.t.,ExistCpo("SX5",cTabela+cParDe))
				If !lRet
					lRet := .f.
				EndIf
			Endif
		ElseIf nOpc == 2
			If (cParAte == 'ZZ')
				lRet := .t.
			Else
				lRet := IIF(ATECODIGO("SX5",cTabela+cParDe,cTabela+cParAte,05),.T.,.F.)
				If !lRet
					lRet := .f.
				EndIf
			EndIf
		EndIf
	Endif

	If lRet
		If !Empty(MV_PAR02) .AND. !Empty(MV_PAR03)
			If MV_PAR02 > MV_PAR03
				MsgStop(STR0031,STR0012) //"De Tipo Acidente não pode ser maior que Até Tipo Acidente!"###"Atenção"
				lRet := .f.
			Endif
		Endif
	Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNT595GRAV| Autor ³ Marcos Wagner Junior  ³ Data ³ 06/09/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao dos Parametros	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR595                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT595GRAV()

	If !Empty(MV_PAR04) .AND. !Empty(MV_PAR05)
		If MV_PAR04 > MV_PAR05
			MsgStop(STR0038,STR0012) //"De Gravidade não pode ser maior que Até Gravidade!"###"ATENÇÃO"
			Return .f.
		Endif
	Endif

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNT595ANO | Autor ³ Marcos Wagner Junior  ³ Data ³ 12/11/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao dos Parametros	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR595                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT595ANO()

	cAno := AllTrim(Str(MV_PAR01))
	If Len(cAno) != 4
		MsgStop(STR0041,STR0012) //"O Ano informado deverá conter 4 dígitos!"###"ATENÇÃO"
		Return .f.
	Endif
	If MV_PAR01 > Year(dDATABASE)
		MsgStop(STR0039+AllTrim(Str(Year(dDATABASE)))+'!',STR0012) //"Ano informado não poderá ser maior que "###"ATENÇÃO"
		Return .f.
	Endif

Return .t.