#INCLUDE "MDTA241.ch"
#Include "Protheus.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTA241  ³ Autor ³ Vitor Emanuel Batista ³ Data ³ 25/02/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Ocorrencias por Funcionario                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MDTA241()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Guarda conteudo e declara variaveis padroes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM()


Private lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := STR0001 //"Funcionário x Ocorrências"
PRIVATE cPrograma := "MDTA241"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lSigaMdtps

	cCadastro := OemtoAnsi(STR0024)  //"Clientes"

	DbSelectArea("SA1")
	DbSetOrder(1)
	mBrowse( 6, 1,22,75,"SA1")

Else
	DbSelectArea("SRA")
	DbSetOrder(1)
	mBrowse( 6, 1,22,75,"SRA")
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna conteudo de variaveis padroes       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} NGFUN241
Programa de Inclusao,Alteracao,Exclusao de Ocorrencias por
Funcionarios(TO8)

@author Vitor Emanuel Batista
@since 25/02/2008
@sample MDTA241
@return .T.
/*/
//---------------------------------------------------------------------
Function NGFUN241()
Local oDlg, oGet, i
Local aNoFields := {}
Local nOp := 3//Opcao da GetDados
Local nTamCli := TAMSX3("A1_COD")[1]+TAMSX3("A1_LOJA")[1]
//-----------------------------------------------------------
//- Variaveis de tamanho de tela e objetos                  -
//-----------------------------------------------------------
Private aSize := MsAdvSize(,.f.,430), aObjects := {}
Aadd(aObjects,{050,050,.t.,.t.})
Aadd(aObjects,{100,100,.t.,.t.})
aInfo := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
aPosObj := MsObjSize(aInfo, aObjects,.t.)

//----------------------------------------------------------
//- Monta a entrada de dados do arquivo                    -
//----------------------------------------------------------
Private aTELA := {},aGETS := {}
Private aCols := {}, aHeader := {}
Private aChoice := {}

//Verifica se o funcionario nao esta bloqueado
If !SitMdtFunc(SRA->RA_MAT,.F.,.T.,.T.)
	ShowHelpDlg(STR0020,{STR0021,STR0022},2,;//"Atenção"#"Este funcionário está bloqueado para uso."#"Somente será possível a visualização das ocorrências."
	{STR0023})//"Entre em contato com o administrador do sistema ou o responsável pelo registro para identificar o motivo do bloqueio."
	nOp := 2
Endif
//-------------------------------------------
//- Monta o cabecalho                       -
//-------------------------------------------
dbSelectArea("SX2")
dbSeek("TO8")

aAdd(aNoFields, 'TO8_MAT')
aAdd(aNoFields, 'TO8_NOME')

#IFDEF TOP
	cQuery := "SELECT * FROM "+RetSqlName("TO8")+" TO8 WHERE TO8.TO8_FILIAL = '"+xFilial("TO8")+"' AND TO8.TO8_MAT = '"+SRA->RA_MAT+"'"+ ;
			  "AND TO8.D_E_L_E_T_ = ' '"
	FillGetDados( 4, "TO8", 2, xFilial("TO8")+SRA->RA_MAT,  {|| TO8->TO8_FILIAL + TO8->TO8_MAT}, {|| .T.},aNoFields,,,cQuery)
#ELSE
	cKEY := "SRA->RA_MAT"
	cGET := "TO8->TO8_FILIAL == '"+xFilial("TO8")+"' .And. TO8->TO8_MAT == '" + SRA->RA_MAT + "'"
	FillGetDados( 4, "TO8", 2, cKey, {|| }, {|| .T.},aNoFields,,,,{|| NGMontaAcols("TO8",&cKEY,cGET)})
#ENDIF

If Empty(aCols)
   aCols := BlankGetd(aHeader)
EndIf

PutFileInEof( "TO8" )

nOpca := 0
DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd Pixel

	oPanel := TPanel():New(,,,oDlg,,,,,RGB(255,255,255),, aPosObj[1,3], .F., .F. )
	oPanel:Align := CONTROL_ALIGN_TOP

	//Atribui valor as varáiveis de tela
	dbSelectarea("SRA")
	RegToMemory("SRA",.F.)

	//Monta aChoice
	aADD(aChoice,"RA_MAT")
	aADD(aChoice,"RA_NOME")
	aADD(aChoice,"RA_NASC")
	aADD(aChoice,"RA_SEXO")
	aADD(aChoice,"RA_CC")
	aADD(aChoice,"RA_DESCCC")
	aADD(aChoice,"RA_CODFUNC")
	aADD(aChoice,"RA_DESCFUN")
	aADD(aChoice,"RA_ADMISSA")

	//Monta enchoice de fornecedor
	oEnchoice := Msmget():New("SRA", SRA->(Recno()),2,,,,aCHOICE,,,3,,,,oPanel,,,,,.T.,)
	oEnchoice:oBox:Align := CONTROL_ALIGN_ALLCLIENT


	dbSelectArea("TO8")
	oGet := MSGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOp,"NG241LinOk()","AllWaysTrue()","",.T.,,,,3000)
	oGet:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	//Implementa Click da Direita
	If Len(aSMenu) > 0
		NGPOPUP(aSMenu,@oMenu)
		oDlg:bRClicked	:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
		oPanel:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
	Endif

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=1,if(oGet:TudoOk(),oDlg:End(),nOpca := 0)},{||oDlg:End(),nOpca := 0})CENTERED

If nOpcA == 1
	Begin Transaction

	NG241GRAV()
	EvalTrigger()  //Processa Gatilhos

	End Transaction
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG241GRAV ³ Autor ³ Vitor Emanuel Batista ³ Data ³ 25/02/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava os dados na TO8                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG241GRAV()
Local i, j
Local nCODOCO := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO8_CODOCO" })
Local nDTOCO  := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TO8_DTOCOR" })
Local nIndex  := 1
Local cSeek   := ""

If lSigaMdtPs
	nIndex := 3
EndIf

//Coloca os deletados por primeiro
ASORT(aCols,,, { |x, y| x[Len(aCols[n])] .and. !y[Len(aCols[n])] } )
For i:=1 To Len(aCols)
	If lSigaMdtPs
		cSeek := xFilial("TO8")+cCliMdtPs+aCols[i][nCODOCO]+SRA->RA_MAT+DTOS(aCols[i][nDTOCO])
	Else
		cSeek := xFilial("TO8")+aCols[i][nCODOCO]+SRA->RA_MAT+DTOS(aCols[i][nDTOCO])
	EndIf
	If !aCols[i][Len(aCols[i])] .AND. !Empty(aCols[i][nCODOCO])
		dbSelectArea("TO8")
		dbSetOrder(nIndex)
		If dbSeek(cSeek)
			RecLock("TO8",.F.)
		Else
			RecLock("TO8",.T.)
		Endif
		TO8->TO8_FILIAL := xFilial("TO8")
		TO8->TO8_MAT 	:= SRA->RA_MAT
		If lSigaMdtPs
			TO8->TO8_CLIENT := SA1->A1_COD
			TO8->TO8_LOJA   := SA1->A1_LOJA
		EndIf
		For j:=1 to FCount()
			If "_FILIAL"$Upper(FieldName(j)) .or. "_MAT"$Upper(FieldName(j)).or.;
				"_CLIENT"$Upper(FieldName(j)) .or. "_LOJA"$Upper(FieldName(j))
				Loop
			Endif
			If (nPos := aScan(aHeader, {|x| AllTrim(Upper(x[2])) == AllTrim(Upper(FieldName(j))) })) > 0
				FieldPut(j, aCols[i][nPos])
			Endif
		Next j
		MsUnlock("TO8")
	ElseIf !Empty(aCols[i][nCODOCO])
		dbSelectArea("TO8")
		dbSetOrder(nIndex)
		If dbSeek(cSeek)
			RecLock("TO8",.F.)
			dbDelete()
			MsUnlock("TO8")
		Endif
	Endif
Next i

DbSelectArea("SRA")

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG241LinOk³ Autor ³ Vitor Emanuel Batista ³ Data ³ 25/02/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se a linha digitada esta Ok                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function NG241LinOk(lFim)
Local f
Local nCodOco := aSCAN(aHEADER, {|x| Trim(Upper(x[2])) == "TO8_CODOCO"})
Local nDtOcor := aSCAN(aHEADER, {|x| Trim(Upper(x[2])) == "TO8_DTOCOR" })
Local nGravid := aSCAN(aHEADER, {|x| Trim(Upper(x[2])) == "TO8_GRAVID" })
Default lFim := .F.

If Len(aCols) == 1
	If Empty(aCols[1][nCodOco]) .or. aCols[1][Len(aCols[1])]
		Return .T.
	Endif
Endif
//Percorre aCols
For f:= 1 to Len(aCols)
	If !aCols[f][Len(aCols[f])]
		If f == n .or. lFim
			//Verifica se os campos obrigatórios estão preenchidos
			If Empty(aCols[f][nCodOco])
				//Mostra mensagem de Help
				Help(1," ","OBRIGAT2",,aHeader[nCodOco][1],3,0)
				Return .F.
			ElseIf Empty(aCols[f][nDtOcor])
				//Mostra mensagem de Help
				Help(1," ","OBRIGAT2",,aHeader[nDtOcor][1],3,0)
				Return .F.
			ElseIf Empty(aCols[f][nGravid])
				//Mostra mensagem de Help
				Help(1," ","OBRIGAT2",,aHeader[nGravid][1],3,0)
				Return .F.
			Endif
		Endif
		//Verifica se é somente LinhaOk
		If f <> n .and. !aCols[n][Len(aCols[n])]
			If aCols[f][nCodOco] == aCols[n][nCodOco] .and. aCols[f][nDtOcor] == aCols[n][nDtOcor]
				Help(" ",1,"JAEXISTINF")
				Return .F.
			Endif
		Endif
	Endif
Next f

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Vitor Emanuel Batista ³ Data ³25/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MDTA241                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

Local aRotina
Local aNovBot := {}
Private lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

If lSigaMdtps
	aRotina :=	{ { STR0012,   "AxPesqui"  , 0 , 1},; //"Pesquisar"
	              { STR0013,   "NGCAD01"   , 0 , 2},; //"Visualizar"
	              { STR0025,   "MDT241FU"  , 0 , 4} } //"Funcionarios"
Else

	aRotina := { { STR0012, "AxPesqui", 0 , 1},;   //"Pesquisar"
				 { STR0013, "NGCAD01", 0 , 2},;   //"Visualizar"
     			 { STR0014, "NGFUN241", 0 , 4}}   //"Ocorrencias"

	If ExistBlock( "MDTA2411" ) //Adicionar botões na Rotina
		If ValType( aNovBot := ExecBlock( "MDTA2411", .F., .F.) ) == "A"
			aEval( aNovBot , { | x | aAdd( aRotina , x ) } )
		EndIf
	End

EndIf

Return aRotina
//---------------------------------------------------------------------
/*/{Protheus.doc} MDT241FU
Browse com a lista dos funcionarios de um cliente

@return Nil

@sample
MDT241FU()

@author Jackson Machado
@since 13/11/2012
@version 1.0
/*/
//---------------------------------------------------------------------
Function MDT241FU()
    Local nSizeCli, nSizeLoj
    Local aArea			:= GetArea()
	Local aAreaSRA		:= SRA->( GetArea() )
	Local oldROTINA   	:= aCLONE( aROTINA )

	Private cCliMdtPs 	:= SA1->A1_COD+SA1->A1_LOJA
	Private aIndexSRA	:= { }

	aRotina := { { STR0012, "AxPesqui", 0 , 1},;   //"Pesquisar"
				 { STR0013, "NGCAD01", 0 , 2},;   //"Visualizar"
     			 { STR0014, "NGFUN241", 0 , 4}}   //"Ocorrencias"

	nSizeCli := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
	nSizeLoj := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))

	cFiltraRh := " Substr( RA_CC , 1 , "+StrZero(nSizeCli+nSizeLoj,2)+" ) == '"+cCliMdtPs+"'"
	bFiltraBrw := { || FilBrowse( "SRA" , @aIndexSRA , @cFiltraRH ) }
	Eval( bFiltraBrw )

	dbSelectArea( "SRA" )
	dbSetOrder( 1 )
	mBrowse( 6 , 1 , 22 , 75 , "SRA" )

	EndFilBrw( "SRA" , aIndexSRA )

	RestArea( aAreaSRA )
	RestArea( aArea )
	aROTINA := aCLONE( oldROTINA )
Return