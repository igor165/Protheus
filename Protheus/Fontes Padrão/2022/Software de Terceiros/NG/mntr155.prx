#INCLUDE "MNTR155.ch"
#include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR155   ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 06/07/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Rastreamento de Pneus			                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function MNTR155()  

	Local oTempTable		//Obj. Tabela Temp.
	Local nSizeFil := IIf(FindFunction("FWSizeFilial"), FwSizeFilial(), Len(TQS->TQS_FILIAL))

	WNREL      := "MNTR155"
	LIMITE     := 182
	cDESC1     := STR0013 //"O relatorio apresentará toda a vida do pneu com suas "
	cDESC2     := STR0014 //"movimentações, entrada/saída da estrutura, etc. "
	cDESC3     := " "
	cSTRING    := "TQF"       


	Private NOMEPROG		:= "MNTR155"
	Private TAMANHO 		:= "G"
	Private aRETURN  		:= {STR0015,1,STR0016,1,2,1,"",1}   //"Zebrado"###"Administracao"
	Private TITULO   		:= STR0017  //"Relatório de Rastreamento de Pneus"
	Private nTIPO    		:= 0
	Private nLASTKEY 		:= 0
	Private CABEC1,CABEC2 
	Private aVETINR 		:= {}    
	Private cPERG 			:= "MNR155"   	
	Private cTRBB			:= GetNextAlias()// Alias Tab. Temp.

	aCampos  := {}
	aAdd(aCAMPOS,{"DATAMO"   ,"D",08,0})
	aAdd(aCAMPOS,{"HORAMO"   ,"C",05,0})
	aAdd(aCAMPOS,{"CCUSTO"   ,"C",20,0})
	aAdd(aCAMPOS,{"FILIAL"   ,"C",nSizeFil,0})
	aAdd(aCAMPOS,{"PNEU"     ,"C",16,0})
	aAdd(aCAMPOS,{"MEDIDA"   ,"C",06,0})
	aAdd(aCAMPOS,{"MODELO"   ,"C",10,0})
	aAdd(aCAMPOS,{"DESENHO"  ,"C",10,0})
	aAdd(aCAMPOS,{"BANDA"    ,"C",02,0})
	aAdd(aCAMPOS,{"SULCO"    ,"N",TAMSX3('TQS_SULCAT')[1],2})
	aAdd(aCAMPOS,{"DESENHO2" ,"C",10,0})
	aAdd(aCAMPOS,{"BANDA2"   ,"C",02,0})
	aAdd(aCAMPOS,{"SULCO2"   ,"N",TAMSX3('TQS_SULCAT')[1],2})
	aAdd(aCAMPOS,{"KMPNEU"   ,"N",TAMSX3('TZ_POSCONT')[1],0})
	aAdd(aCAMPOS,{"SERVICO"  ,"C",14,0})
	aAdd(aCAMPOS,{"FORNEC"   ,"C",25,0})
	aAdd(aCAMPOS,{"FROTA"    ,"C",16,0})
	aAdd(aCAMPOS,{"POSICAO"  ,"C",06,0})
	aAdd(aCAMPOS,{"DESTINO"  ,"C",02,0})
	aAdd(aCAMPOS,{"MOTIVO"   ,"C",06,0})

	//Intancia classe FWTemporaryTable
    oTempTable := FWTemporaryTable():New( cTRBB, aCAMPOS )      
    //Cria indices
    oTempTable:AddIndex( "Ind01" , {"PNEU","DATAMO","HORAMO","FROTA"} )
    //Cria a tabela temporaria
    oTempTable:Create()

	Pergunte(cPERG,.F.)
	
	//Envia controle para a funcao SETPRINT 
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TR2")
		oTempTable:Delete()//Deleta tablera temporária
		Return
	EndIf     
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR155IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0011,STR0012) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("TR2")

	oTempTable:Delete()//Deleta tabela temporária
	
Return .T.    

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR155IMP | Autor ³ Marcos Wagner Junior  ³ Data ³ 06/07/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR155                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR155IMP(lEND,WNREL,TITULO,TAMANHO)

	// Retorna o modo de compartilhamento da tabela ST9. 1=Empresa, 2=Unidade de Negócio e 3=Filial.
	Local cModoComp := FwModeAccess( "ST9",1 )+FwModeAccess( "ST9",2 )+FwModeAccess( "ST9",3 )
	Local cDesc     := ''
	Local cTire     := ''
	Local cBranchTQT := xFilial( 'TQT' )
	Local cBranchST9 := xFilial( 'ST9' )
	Local cBemTQS    := ''
	Local cBemAntigo := ''
	Local cBandAntig := ''
	Local lSeekTQS   := .F.

	Private cRODATXT := ""
	Private nCNTIMPR := 0     
	Private li := 80 ,m_pag := 1    
	Private cNomeOri

	nTIPO  := IIf(aReturn[4]==1,15,18)                                                                                                                                                                                               

	CABEC1 := STR0047 // Pneu              Descrição                                           Motivo      Descrição
	CABEC2 := STR0027 //    Data       Hora  Centro Custo         Filial     Desenho    Banda Sulco      Km Veic.   Serviço        Fornecedor                Frota            Posição Destino             Motivo    

	If cModoComp == "CCC" // Se a Empresa/Unidade/Filial estiverem compartilhadas.
		MV_PAR03 := " "
		MV_PAR04 := " "
	ElseIf cModoComp == "ECC" // Se a Unidade/Filial estiverem compartilhadas.
		MV_PAR03 := SubStr( MV_PAR03,1,2 )
		MV_PAR04 := SubStr( MV_PAR04,1,2 )
	ElseIf cModoComp == "EEC" // Se a Filial estiver compartilhada.
		MV_PAR03 := SubStr( MV_PAR03,1,5 )
		MV_PAR04 := SubStr( MV_PAR04,1,5 )
	EndIf

	cAliasQry1 := GetNextAlias()
	cQuery1 := " SELECT  STZ.TZ_DATAMOV, STZ.TZ_FILIAL, STZ.TZ_CODBEM, TQS.TQS_MEDIDA, TQS.TQS_DESENH, "
	cQuery1 += "         TQS.TQS_BANDAA, TQS.TQS_SULCAT, STZ.TZ_BEMPAI, STZ.TZ_LOCALIZ, STZ.TZ_TIPOMOV, "
	cQuery1 += "         STZ.TZ_POSCONT, STZ.TZ_CONTSAI, STZ.TZ_HORAENT, STZ.TZ_DATASAI, STZ.TZ_HORASAI "
	cQuery1 += " FROM " + RetSqlName("TQS")+" TQS, " + RetSqlName("STZ")+" STZ "
	cQuery1 += " WHERE STZ.TZ_DATAMOV >= '"+DTOS(MV_PAR01)+"'"
	cQuery1 += " AND   STZ.TZ_DATAMOV <= '"+DTOS(MV_PAR02)+"'"
	cQuery1 += " AND   " + NGMODCOMP("TQS","TQS",">=",,MV_PAR03)
	cQuery1 += " AND   " + NGMODCOMP("TQS","TQS","<=",,MV_PAR04)
	cQuery1 += " AND   TQS.TQS_MEDIDA >= '"+MV_PAR05+"'"
	cQuery1 += " AND   TQS.TQS_MEDIDA <= '"+MV_PAR06+"'"
	cQuery1 += " AND   TQS.TQS_CODBEM >= '"+MV_PAR07+"'"
	cQuery1 += " AND   TQS.TQS_CODBEM <= '"+MV_PAR08+"'"
	cQuery1 += " AND   TQS.TQS_FILIAL = STZ.TZ_FILIAL "
	cQuery1 += " AND   TQS.TQS_CODBEM = STZ.TZ_CODBEM "
	cQuery1 += " AND   TQS.D_E_L_E_T_ <> '*'"
	cQuery1 += " AND   STZ.D_E_L_E_T_ <> '*'"
	cQuery1 := ChangeQuery(cQuery1)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery1),cAliasQry1, .F., .T.) 

	dbSelectArea(cAliasQry1)
	While !Eof()
		dbSelectArea(cTRBB)
		RecLock(cTRBB,.T.)
		(cTRBB)->DATAMO  := If((cAliasQry1)->TZ_TIPOMOV='E',STOD((cAliasQry1)->TZ_DATAMOV),STOD((cAliasQry1)->TZ_DATASAI))
		If (cAliasQry1)->TZ_TIPOMOV = 'E'
			(cTRBB)->HORAMO := (cAliasQry1)->TZ_HORAENT
			aNgFilTPN := NgFilTPN((cAliasQry1)->TZ_CODBEM,STOD((cAliasQry1)->TZ_DATAMOV),(cAliasQry1)->TZ_HORAENT)
		Else
			(cTRBB)->HORAMO := (cAliasQry1)->TZ_HORASAI
			aNgFilTPN := NgFilTPN((cAliasQry1)->TZ_CODBEM,STOD((cAliasQry1)->TZ_DATASAI),(cAliasQry1)->TZ_HORASAI)
		Endif	
		(cTRBB)->CCUSTO  := If(!Empty(aNgFilTPN[2]),aNgFilTPN[2],MNT155SEEK((cAliasQry1)->TZ_CODBEM,'T9_CCUSTO'))
		(cTRBB)->FILIAL  := (cAliasQry1)->TZ_FILIAL
		(cTRBB)->PNEU    := (cAliasQry1)->TZ_CODBEM
		(cTRBB)->MEDIDA  := (cAliasQry1)->TQS_MEDIDA	
		(cTRBB)->DESENHO := (cAliasQry1)->TQS_DESENH
		(cTRBB)->BANDA   := (cAliasQry1)->TQS_BANDAA
		(cTRBB)->SULCO   := (cAliasQry1)->TQS_SULCAT
		(cTRBB)->DESENHO2 := (cAliasQry1)->TQS_DESENH
		(cTRBB)->BANDA2   := (cAliasQry1)->TQS_BANDAA
		(cTRBB)->SULCO2   := (cAliasQry1)->TQS_SULCAT
		MNR155TQV((cAliasQry1)->TZ_FILIAL,(cAliasQry1)->TZ_CODBEM,If((cAliasQry1)->TZ_TIPOMOV='E',(cAliasQry1)->TZ_DATAMOV,(cAliasQry1)->TZ_DATASAI),;
		If((cAliasQry1)->TZ_TIPOMOV='E',(cAliasQry1)->TZ_HORAENT,(cAliasQry1)->TZ_HORASAI))
		(cTRBB)->KMPNEU  := If((cAliasQry1)->TZ_TIPOMOV='E',(cAliasQry1)->TZ_POSCONT,(cAliasQry1)->TZ_CONTSAI)
		(cTRBB)->SERVICO := If((cAliasQry1)->TZ_TIPOMOV='E',STR0028,STR0029) //"Entrada"###"Saída"
		(cTRBB)->FROTA   := (cAliasQry1)->TZ_BEMPAI	
		(cTRBB)->POSICAO := (cAliasQry1)->TZ_LOCALIZ	
		MsUnlock(cTRBB)	
		dbSelectArea(cAliasQry1)
		dbSkip()
	End 
	(cAliasQry1)->(dbCloseArea())

	cAliasQry4 := GetNextAlias()
	cQuery4 := " SELECT  TR4.TR4_DTANAL, TR4.TR4_FILIAL, TR4.TR4_CODBEM, TQS.TQS_MEDIDA, TQV.TQV_DESENH, "
	cQuery4 += "         TQV.TQV_BANDA, TQV.TQV_SULCO, TR4.TR4_DESTIN, TR4.TR4_MOTIVO, TR4.TR4_HRANAL, "
	cQuery4 += "         TQS.TQS_DESENH, TQS.TQS_BANDAA, TQS.TQS_SULCAT "
	cQuery4 += " FROM " + RetSqlName("TQS")+" TQS, " + RetSqlName("TR4")+" TR4, " + RetSqlName("TQV")+" TQV "
	cQuery4 += " WHERE TR4.TR4_DTANAL >= '"+DTOS(MV_PAR01)+"'"
	cQuery4 += " AND   TR4.TR4_DTANAL <= '"+DTOS(MV_PAR02)+"'"
	cQuery4 += " AND   " + NGMODCOMP("TQS","TQS",">=",,MV_PAR03)
	cQuery4 += " AND   " + NGMODCOMP("TQS","TQS","<=",,MV_PAR04)
	cQuery4 += " AND   TQS.TQS_MEDIDA >= '"+MV_PAR05+"'"
	cQuery4 += " AND   TQS.TQS_MEDIDA <= '"+MV_PAR06+"'"
	cQuery4 += " AND   TQS.TQS_CODBEM >= '"+MV_PAR07+"'"
	cQuery4 += " AND   TQS.TQS_CODBEM <= '"+MV_PAR08+"'"
	cQuery4 += " AND   TQS.TQS_FILIAL = TR4.TR4_FILIAL "
	cQuery4 += " AND   TQS.TQS_CODBEM = TR4.TR4_CODBEM "
	cQuery4 += " AND   TQS.TQS_CODBEM = TQV.TQV_CODBEM "
	cQuery4 += " AND   TQV.TQV_DTMEDI = TR4.TR4_DTANAL "
	cQuery4 += " AND   TQV.TQV_HRMEDI = TR4.TR4_HRANAL "
	cQuery4 += " AND   TQS.D_E_L_E_T_ <> '*'"
	cQuery4 += " AND   TR4.D_E_L_E_T_ <> '*'"
	cQuery4 += " AND   TQV.D_E_L_E_T_ <> '*'"
	cQuery4 := ChangeQuery(cQuery4)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery4),cAliasQry4, .F., .T.) 

	dbSelectArea(cAliasQry4)
	While !Eof()
		dbSelectArea(cTRBB)
		RecLock(cTRBB,.T.)
		(cTRBB)->DATAMO  := STOD((cAliasQry4)->TR4_DTANAL)
		(cTRBB)->HORAMO  := (cAliasQry4)->TR4_HRANAL
		aNgFilTPN := NgFilTPN((cAliasQry4)->TR4_CODBEM,STOD((cAliasQry4)->TR4_DTANAL),(cAliasQry4)->TR4_HRANAL)
		(cTRBB)->CCUSTO  := If(!Empty(aNgFilTPN[2]),aNgFilTPN[2],MNT155SEEK((cAliasQry4)->TR4_CODBEM,'T9_CCUSTO'))
		(cTRBB)->FILIAL  := (cAliasQry4)->TR4_FILIAL
		(cTRBB)->PNEU    := (cAliasQry4)->TR4_CODBEM
		(cTRBB)->MEDIDA  := (cAliasQry4)->TQS_MEDIDA
		(cTRBB)->DESENHO := (cAliasQry4)->TQV_DESENH
		(cTRBB)->BANDA   := (cAliasQry4)->TQV_BANDA
		(cTRBB)->SULCO   := (cAliasQry4)->TQV_SULCO
		(cTRBB)->DESENHO2 := (cAliasQry4)->TQS_DESENH
		(cTRBB)->BANDA2   := (cAliasQry4)->TQS_BANDAA
		(cTRBB)->SULCO2   := (cAliasQry4)->TQS_SULCAT      
		DbSelectArea("STP")
		DbSetOrder(9)
		DbSeek((cAliasQry4)->TR4_CODBEM)
		While !Eof() .AND. STP->TP_CODBEM = (cAliasQry4)->TR4_CODBEM
			If STP->TP_DTLEITU < STOD((cAliasQry4)->TR4_DTANAL)
				(cTRBB)->KMPNEU  := STP->TP_POSCONT
			ElseIf STP->TP_DTLEITU = STOD((cAliasQry4)->TR4_DTANAL) .AND. STP->TP_HORA < (cAliasQry4)->TR4_HRANAL
				(cTRBB)->KMPNEU  := STP->TP_POSCONT
			Endif
			dbSkip()
		End
		(cTRBB)->SERVICO := STR0031 //"Análise Técnica"
		(cTRBB)->DESTINO := (cAliasQry4)->TR4_DESTIN
		(cTRBB)->MOTIVO  := (cAliasQry4)->TR4_MOTIVO
		MsUnlock(cTRBB)	
		dbSelectArea(cAliasQry4)
		dbSkip()
	End
	(cAliasQry4)->(dbCloseArea())

	cAliasQry3 := GetNextAlias()
	cQuery3 := " SELECT  STJ.TJ_DTMRINI, STJ.TJ_HOMRINI, STJ.TJ_DTMPINI, STJ.TJ_HOMPINI, STJ.TJ_FILIAL, STJ.TJ_CODBEM, "
	cQuery3 += "         TQS.TQS_MEDIDA, TQS.TQS_DESENH, STJ.TJ_CCUSTO, STJ.TJ_POSCONT, STJ.TJ_SERVICO, TQS.TQS_BANDAA, TQS.TQS_SULCAT "
	cQuery3 += " FROM " + RetSqlName("TQS")+" TQS, " + RetSqlName("STJ")+" STJ "
	cQuery3 += " WHERE STJ.TJ_DTMPINI >= '"+DTOS(MV_PAR01)+"'"
	cQuery3 += " AND   STJ.TJ_DTMPINI <= '"+DTOS(MV_PAR02)+"'"
	cQuery3 += " AND   " + NGMODCOMP("TQS","TQS",">=",,MV_PAR03)
	cQuery3 += " AND   " + NGMODCOMP("TQS","TQS","<=",,MV_PAR04)
	cQuery3 += " AND   TQS.TQS_MEDIDA >= '"+MV_PAR05+"'"
	cQuery3 += " AND   TQS.TQS_MEDIDA <= '"+MV_PAR06+"'"
	cQuery3 += " AND   TQS.TQS_CODBEM >= '"+MV_PAR07+"'"
	cQuery3 += " AND   TQS.TQS_CODBEM <= '"+MV_PAR08+"'"
	cQuery3 += " AND   TQS.TQS_FILIAL = STJ.TJ_FILIAL "
	cQuery3 += " AND   TQS.TQS_CODBEM = STJ.TJ_CODBEM "
	cQuery3 += " AND   TQS.D_E_L_E_T_ <> '*'"
	cQuery3 += " AND   STJ.D_E_L_E_T_ <> '*'"
	cQuery3 := ChangeQuery(cQuery3)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery3),cAliasQry3, .F., .T.) 

	dbSelectArea(cAliasQry3)
	While !Eof()
		dbSelectArea(cTRBB)
		RecLock(cTRBB,.T.)
		(cTRBB)->DATAMO  := STOD((cAliasQry3)->TJ_DTMPINI)
		(cTRBB)->CCUSTO  := If(!Empty((cAliasQry3)->TJ_CCUSTO),(cAliasQry3)->TJ_CCUSTO,MNT155SEEK((cAliasQry3)->TJ_CODBEM,'T9_CCUSTO'))
		(cTRBB)->FILIAL  := (cAliasQry3)->TJ_FILIAL
		(cTRBB)->PNEU    := (cAliasQry3)->TJ_CODBEM
		(cTRBB)->MEDIDA  := (cAliasQry3)->TQS_MEDIDA          
		If !Empty((cAliasQry3)->TJ_DTMRINI)
			(cTRBB)->HORAMO := (cAliasQry3)->TJ_HOMRINI
			cDataManut := (cAliasQry3)->TJ_DTMRINI
			cHoraManut := (cAliasQry3)->TJ_HOMRINI
		Else
			(cTRBB)->HORAMO := (cAliasQry3)->TJ_HOMPINI
			cDataManut := (cAliasQry3)->TJ_DTMPINI
			cHoraManut := (cAliasQry3)->TJ_HOMPINI
		Endif
		(cTRBB)->DESENHO := (cAliasQry3)->TQS_DESENH
		(cTRBB)->BANDA   := (cAliasQry3)->TQS_BANDAA
		(cTRBB)->SULCO   := (cAliasQry3)->TQS_SULCAT
		(cTRBB)->DESENHO2 := (cAliasQry3)->TQS_DESENH
		(cTRBB)->BANDA2   := (cAliasQry3)->TQS_BANDAA
		(cTRBB)->SULCO2   := (cAliasQry3)->TQS_SULCAT
		MNR155TQV((cAliasQry3)->TJ_FILIAL,(cAliasQry3)->TJ_CODBEM,cDataManut,cHoraManut)
		(cTRBB)->KMPNEU  := (cAliasQry3)->TJ_POSCONT
		(cTRBB)->SERVICO := (cAliasQry3)->TJ_SERVICO
		
		MsUnlock(cTRBB)	
		dbSelectArea(cAliasQry3)
		dbSkip()
	End
	(cAliasQry3)->(dbCloseArea())

	cAliasQry2 := GetNextAlias()
	cQuery2 := " SELECT  TR7.TR7_DTRECI, TR7.TR7_FILIAL, TR8.TR8_CODBEM, TQS.TQS_MEDIDA, TQS.TQS_DESENH, "
	cQuery2 += "         TQS.TQS_BANDAA, TQS.TQS_SULCAT, TR8.TR8_MOTIVO, TR8.TR8_TIPMOD, TR7.TR7_HRRECI, "
	cQuery2 += "         TR7.TR7_FORNEC, TR7.TR7_LOJA "
	cQuery2 += " FROM " + RetSqlName("TQS")+" TQS, " + RetSqlName("TR7")+" TR7, " + RetSqlName("TR8")+" TR8 "
	cQuery2 += " WHERE TR7.TR7_DTRECI >= '"+DTOS(MV_PAR01)+"'"
	cQuery2 += " AND   TR7.TR7_DTRECI <= '"+DTOS(MV_PAR02)+"'"
	cQuery2 += " AND   " + NGMODCOMP("TQS","TQS",">=",,MV_PAR03)
	cQuery2 += " AND   " + NGMODCOMP("TQS","TQS","<=",,MV_PAR04)
	cQuery2 += " AND   TQS.TQS_MEDIDA >= '"+MV_PAR05+"'"
	cQuery2 += " AND   TQS.TQS_MEDIDA <= '"+MV_PAR06+"'"
	cQuery2 += " AND   TQS.TQS_CODBEM >= '"+MV_PAR07+"'"
	cQuery2 += " AND   TQS.TQS_CODBEM <= '"+MV_PAR08+"'"
	cQuery2 += " AND   TQS.TQS_FILIAL = TR8.TR8_FILIAL "
	cQuery2 += " AND   TQS.TQS_CODBEM = TR8.TR8_CODBEM "
	cQuery2 += " AND   TR7.TR7_FILIAL = TR8.TR8_FILIAL "
	cQuery2 += " AND   TR7.TR7_LOTE   = TR8.TR8_LOTE "
	cQuery2 += " AND   TQS.D_E_L_E_T_ <> '*'"
	cQuery2 += " AND   TR7.D_E_L_E_T_ <> '*'"
	cQuery2 += " AND   TR8.D_E_L_E_T_ <> '*'"
	cQuery2 := ChangeQuery(cQuery2)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery2),cAliasQry2, .F., .T.) 

	dbSelectArea(cAliasQry2)
	While !Eof()
		dbSelectArea(cTRBB)
		RecLock(cTRBB,.T.)
		(cTRBB)->DATAMO  := STOD((cAliasQry2)->TR7_DTRECI)
		(cTRBB)->HORAMO  := (cAliasQry2)->TR7_HRRECI
		aNgFilTPN := NgFilTPN((cAliasQry2)->TR8_CODBEM,STOD((cAliasQry2)->TR7_DTRECI),(cAliasQry2)->TR7_HRRECI)
		(cTRBB)->CCUSTO  := If(!Empty(aNgFilTPN[2]),aNgFilTPN[2],MNT155SEEK((cAliasQry2)->TR8_CODBEM,'T9_CCUSTO'))
		(cTRBB)->FILIAL  := (cAliasQry2)->TR7_FILIAL
		(cTRBB)->PNEU    := (cAliasQry2)->TR8_CODBEM
		(cTRBB)->MEDIDA  := (cAliasQry2)->TQS_MEDIDA
		(cTRBB)->MODELO  := (cAliasQry2)->TR8_TIPMOD	
		(cTRBB)->DESENHO := (cAliasQry2)->TQS_DESENH
		(cTRBB)->BANDA   := (cAliasQry2)->TQS_BANDAA
		(cTRBB)->SULCO   := (cAliasQry2)->TQS_SULCAT
		(cTRBB)->DESENHO2 := (cAliasQry2)->TQS_DESENH
		(cTRBB)->BANDA2   := (cAliasQry2)->TQS_BANDAA
		(cTRBB)->SULCO2   := (cAliasQry2)->TQS_SULCAT
		MNR155TQV((cAliasQry2)->TR7_FILIAL,(cAliasQry2)->TR8_CODBEM,(cAliasQry2)->TR7_DTRECI,(cAliasQry2)->TR7_HRRECI)
		DbSelectArea("STP")
		DbSetOrder(9)
		DbSeek((cAliasQry2)->TR8_CODBEM)
		While !Eof() .AND. STP->TP_CODBEM = (cAliasQry2)->TR8_CODBEM
			If STP->TP_DTLEITU < STOD((cAliasQry2)->TR7_DTRECI)
				(cTRBB)->KMPNEU  := STP->TP_POSCONT
			ElseIf STP->TP_DTLEITU = STOD((cAliasQry2)->TR7_DTRECI) .AND. STP->TP_HORA < (cAliasQry2)->TR7_HRRECI
				(cTRBB)->KMPNEU  := STP->TP_POSCONT
			Endif
			dbSkip()
		End
		(cTRBB)->SERVICO := STR0030 //"Rec. de Pneus "
		(cTRBB)->MOTIVO  := (cAliasQry2)->TR8_MOTIVO
		(cTRBB)->FORNEC  := Trim( (cAliasQry2)->TR7_FORNEC ) + '/' + (cAliasQry2)->TR7_LOJA
		MsUnlock(cTRBB)	
		dbSelectArea(cAliasQry2)
		dbSkip()
	End
	(cAliasQry2)->(dbCloseArea())

	dbSelectArea(cTRBB)
	dbGoTop()
	dbSetOrder(01)
	SetRegua(LastRec())                                                                 

	If Eof()
		MsgInfo(STR0044,STR0045) //"Não existem dados para montar o relatório."###"ATENÇÃO"
		Return .F.
	Else
		
		Do While (cTRBB)->( !EoF() )

			If lEnd
				@ Prow()+1,001 psay STR0032 //"CANCELADO PELO OPERADOR"
				Exit
			EndIf

			dbSelectArea("ST9")
			dbSetOrder(16)  	
			If dbSeek((cTRBB)->PNEU+"A")
				If ST9->T9_STATUS < MV_PAR09 .OR. ST9->T9_STATUS > MV_PAR10
					dbSelectArea(cTRBB)  
					Dbskip()
					Loop
				Endif
			Else
				If dbSeek((cTRBB)->PNEU)
					If ST9->T9_STATUS < MV_PAR09 .OR. ST9->T9_STATUS > MV_PAR10
						dbSelectArea(cTRBB)
						Dbskip()
						Loop
					Endif
				Endif
			Endif

			NGSomaLi( 58 )
			
			@ Li,000 psay Trim( (cTRBB)->PNEU )
			@ Li,018 psay PadR( Posicione( 'ST9', 1, cBranchST9 + (cTRBB)->PNEU  , 'T9_NOME' ), 50 )
			@ Li,070 psay Trim( (cTRBB)->MEDIDA )
			@ Li,082 psay Trim( Posicione( 'TQT', 1, cBranchTQT + (cTRBB)->MEDIDA, 'TQT_DESMED' ) )

			NGSomaLi( 58 )

			cTire := (cTRBB)->PNEU
			Do While (cTRBB)->( !EoF() ) .And. cTire == (cTRBB)->PNEU

				NGSomaLi( 58 )
				
				@ Li,000 psay Space( 2 )
				@ Li,003 psay (cTRBB)->DATAMO
				@ Li,014 psay (cTRBB)->HORAMO	
				@ Li,020 psay (cTRBB)->CCUSTO
				@ Li,041 psay Trim( (cTRBB)->FILIAL )
				
				cBemTQS := (cTRBB)->PNEU
				lSeekTQS := .F.
				
				dbSelectArea(cTRBB)
				dbSkip()
				If !Eof()
					If cBemTQS != (cTRBB)->PNEU
						lSeekTQS := .T.
					Endif
				Else
					lSeekTQS := .T.
				Endif 

				dbSelectArea(cTRBB)
				dbSkip(-1)
				
				If lSeekTQS
					RecLock(cTRBB,.F.)
					(cTRBB)->DESENHO := (cTRBB)->DESENHO2
					(cTRBB)->BANDA   := (cTRBB)->BANDA2
					(cTRBB)->SULCO   := (cTRBB)->SULCO2
					MsUnlock(cTRBB)
				Endif
				
				If cBemAntigo == (cTRBB)->PNEU .AND. cBandAntig != (cTRBB)->BANDA
					@ Li,052 psay (cTRBB)->DESENHO
				Endif

				cDesc := "  "
				If (cTRBB)->BANDA = '1'
					cDesc := "OR"
				ElseIf (cTRBB)->BANDA = '2'
					cDesc := "R1"
				ElseIf (cTRBB)->BANDA = '3'
					cDesc := "R2"
				ElseIf (cTRBB)->BANDA = '4'
					cDesc := "R3"
				ElseIf (cTRBB)->BANDA = '5'
					cDesc := "R4"
				Endif			

				@ Li,063 psay cDesc
				@ Li,069 psay (cTRBB)->SULCO
				@ Li,080 psay (cTRBB)->KMPNEU
				@ Li,091 psay (cTRBB)->SERVICO
				@ Li,106 psay (cTRBB)->FORNEC
				@ Li,132 psay (cTRBB)->FROTA
				@ Li,149 psay (cTRBB)->POSICAO

				cDesc :=    "                    "
				If (cTRBB)->DESTINO = '1'
					cDesc := STR0033 //"Ressolar            "
				ElseIf (cTRBB)->DESTINO = '2' 
					cDesc := STR0034 //"Consertar           "
				ElseIf (cTRBB)->DESTINO = '3'
					cDesc := STR0035 //"Estoque Usado       "
				ElseIf (cTRBB)->DESTINO = '4'
					cDesc := STR0036 //"Estoque Reformado   "
				ElseIf (cTRBB)->DESTINO = '5'
					cDesc := STR0037 //"Análise Fornecedor  "
				ElseIf (cTRBB)->DESTINO = '6'
					cDesc := STR0038 //"Removido de Serviço "
				ElseIf (cTRBB)->DESTINO = '7'
					cDesc := STR0039	 //"Estoque Novo        "
				Endif

				@ Li,157 psay cDesc
				@ Li,177 psay (cTRBB)->MOTIVO

				cBemAntigo := (cTRBB)->PNEU
				cBandAntig := (cTRBB)->BANDA

				(cTRBB)->( dbSkip() )

			EndDo

			NGSomaLi( 58 )

		EndDo	

	Endif

	RODA(nCNTIMPR,cRODATXT,TAMANHO)       

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principal             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex('ST9')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil     
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MNR155CC  | Autor ³Marcos Wagner Junior   ³ Data ³ 06/07/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o |Valida todos codigos De... , Ate..., com excessao da Filial ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR155                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MNR155CC(nOpc,cParDe,cParAte,cTabela)  

	If (Empty(cParDe) .AND. cParAte = 'ZZZZZZ' )
		Return .T.
	Else
		If nOpc == 1
			If Empty(cParDe)
				Return .T.
			Else		
				lRet := IIf(Empty(cParDe),.T.,ExistCpo(cTabela,cParDe))
				If !lRet
					Return .F.
				EndIf 
			Endif 
		ElseIf nOpc == 2      
			If (cParAte == 'ZZZZZZ') 
				Return .T.
			Else
				lRet := IIF(ATECODIGO(cTabela,cParDe,cParAte,06),.T.,.F.)
				If !lRet 
					Return .F.
				EndIf  
			EndIf
		EndIf    
	Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNR155DT  ³ Autor ³Marcos Wagner Junior   ³ Data ³ 06/07/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro De/Ate Período                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR155                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNR155DT()

	If !Empty(MV_PAR01) .AND. !Empty(MV_PAR02)
		If  MV_PAR02 < MV_PAR01
			MsgStop(STR0040)  //"De Período não pode ser inferior à Até Período!"
			Return .F.
		Endif
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNT155SEEK³ Autor ³Marcos Wagner Junior  ³ Data ³ 29/01/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Busca Centro de Custo da ST9 com indice 16					  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT155SEEK(cKey,cFIELD)
	Local cRET,aAreaSe := GetArea()

	DbSelectArea("ST9")
	DbSetOrder(16)
	If DbSeek(cKey+'A')
		cRET := &cFIELD.
	Else
		cRet := " "
	Endif

	RestArea(aAreaSe)

Return cRET

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNR155ST  ³ Autor ³Marcos Wagner Junior  ³ Data ³ 31/01/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida os codigos De... , Ate... Status do Bem				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNR155ST(nOpc,cParDe,cParAte)
	Local lRet := .T.

	If (Empty(cParDe) .AND. cParAte = 'ZZ')
		Return .T.
	Else
		If nOpc == 1
			If Empty(cParDe)
				Return .T.
			Else		
				dbSelectArea("TQY")
				dbSetOrder(01)
				If dbSeek(xFilial("TQY")+cParDe)
					If TQY->TQY_CATBEM != '3'
						MsgStop(STR0043)//"O Status digitado não é um status de pneu!"
						lRet := .F.
					Endif
				Else
					lRet := ExistCpo("TQY",cParDe)
				Endif
				If !lRet
					Return .F.
				EndIf 
			Endif 
		ElseIf nOpc == 2      
			If (cParAte == 'ZZ')
				Return .T.
			Else
				dbSelectArea("TQY")
				dbSetOrder(01)
				If dbSeek(xFilial("TQY")+cParAte)
					If TQY->TQY_CATBEM != '3'
						MsgStop(STR0043)//"O Status digitado não é um status de pneu!"
						lRet := .f.
					Endif
				Else
					lRet := ExistCpo("TQY",cParAte)
				Endif
				If !lRet
					Return .F.
				EndIf		
				lRet := IIF(ATECODIGO("TQY",cParDe,cParAte,10),.T.,.F.)
			EndIf
		EndIf    
	Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNR155TQV ³ Autor ³Marcos Wagner Junior  ³ Data ³ 31/01/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna um Array(Banda e Sulco) do Pneu em determinada data³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNR155TQV(cFilialPar,cBemPar,cDataPar,cHoraPar)
	Local lPrimeiro := .T.

	dbSelectArea("TQV")
	dbSetOrder(01)
	dbSeek(cFilialPar+cBemPar)
	While !Eof() .AND. TQV->TQV_FILIAL = cFilialPar .AND. TQV->TQV_CODBEM = cBemPar
		If lPrimeiro
			(cTRBB)->BANDA   := TQV->TQV_BANDA
			(cTRBB)->SULCO   := TQV->TQV_SULCO
			(cTRBB)->DESENHO := TQV->TQV_DESENH	
			lPrimeiro := .F.
		Endif
		If TQV->TQV_DTMEDI < STOD(cDataPar)
			(cTRBB)->BANDA   := TQV->TQV_BANDA
			(cTRBB)->SULCO   := TQV->TQV_SULCO
			(cTRBB)->DESENHO := TQV->TQV_DESENH
		ElseIf TQV->TQV_DTMEDI = STOD(cDataPar) .AND. TQV->TQV_HRMEDI < cHoraPar
			(cTRBB)->BANDA   := TQV->TQV_BANDA
			(cTRBB)->SULCO   := TQV->TQV_SULCO
			(cTRBB)->DESENHO := TQV->TQV_DESENH
		Endif
		dbSkip()
	End

Return .T.
