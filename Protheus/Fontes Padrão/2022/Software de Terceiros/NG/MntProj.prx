#INCLUDE "rwmake.ch"
#INCLUDE "mProject.CH"

User Function MntProj()

Local lRet := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de variaveis utilizadas no programa atraves da funcao    ³
//³ SetPrvt, que criara somente as variaveis definidas pelo usuario,    ³
//³ identificando as variaveis publicas do sistema utilizadas no codigo ³
//³ Incluido pelo assistente de conversao do AP6 IDE                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SetPrvt("NTAR,ACONFIG,ACALBASE,OAPP,VISIBLE,LPRIMEIRO")
SetPrvt("START,CF3INS,CCODIGO,CNOMCOD,CSEEK,ATEMPCPO")
SetPrvt("OPROJECT,CCALFUNC,NLIMITE,NW,NX,CORDEM")
SetPrvt("CHORAINI,CHORAFIM,CINSUMO,NQTDHORA,NPOS3,CHORA")
SetPrvt("CMINU,NPOS1,NPOS2,CDIA,CMES,CANO")
SetPrvt("ADIA,POS,NHORA,NFOR,NDIAS,DATU")
SetPrvt("NINI,II,NFIM,XX,NRETORNO,DRET")
SetPrvt("NPOSSEP1,CDTSTART,NBARST1,NBARST2,CMESSTART,CDIASTART")
SetPrvt("NPOSCPO,XRET,")

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ MNTPROJ  | Autor ³Denis Hyroshi de Souza ³ Data ³08/04/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Funcao para integracao do SIGAMNT  com  o Ms-Project        |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³SIGAMNT                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/

lRet := MNTPROJSX1() //Criar SX1's

Aviso("Integracao Microsoft Project 2000","Atencao! Certifique-se de que o formato da data no Microsoft Project (Ferramentas - Opcoes) esta configurado corretamente : 31/12/00 12:33",{"Ok"},2)

If lRet .And. Pergunte("MNTPRJ",.t.)
   OSPROJ() //Monta Projeto e abre o Ms-Project
EndIf

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³OSPROJ()  | Autor ³Denis Hyroshi de Souza ³ Data ³13/03/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Funcao para integracao do Protheus com  o Project            ±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³SIGAMNT                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function OSPROJ()
Local nTar     := 0
Local aConfig  := {}
Local oApp
Local cPLANOSTJ, lPLANOVAL
Private aCalBase  := {PJSUNDAY, PJMONDAY, PJTUESDAY, PJWEDNESDAY, PJTHURSDAY, PJFRIDAY, PJSATURDAY}

cCONDSTJ := 'STJ->TJ_SITUACA == "P" .And. STJ->TJ_TERMINO == "N"'
cCONDSTJ += ' .And. STJ->TJ_CODBEM  >= MV_PAR03 .And. STJ->TJ_CODBEM <= MV_PAR04'
cCONDSTJ += ' .And. STJ->TJ_DTORIGI >= MV_PAR05 .And. STJ->TJ_DTORIGI <= MV_PAR06'
cCONDSTJ += ' .And. STJ->TJ_SERVICO >= MV_PAR07 .And. STJ->TJ_SERVICO <= MV_PAR08'
cCONDSTJ += ' .And. STJ->TJ_CENTRAB >= MV_PAR09 .And. STJ->TJ_CENTRAB <= MV_PAR10'
cCONDSTJ += ' .And. STJ->TJ_CCUSTO  >= MV_PAR13 .And. STJ->TJ_CCUSTO <= MV_PAR14'

If ApOleClient("MsProject")
   oApp:= MsProject():New()
   oApp:Quit( 0 )
   oApp:Destroy()
   oApp:= MsProject():New()
   oApp:VISIBLE:= .f.
   oApp:Projects:Add()

   oApp:TableEdit('Ap6View',.T.,.T.,.T.,,'ID',       ,				       ,06,PJCENTER ,.T.,.T.,PJDATEDEFAULT,1,,PJCENTER)
   oApp:TableEdit('Ap6View',.T.,   ,.T.,,,'Text3'    , "Plano"			 ,06,PJLEFT	  ,.T.,.T.,PJDATEDEFAULT,1,,PJCENTER)
   oApp:TableEdit('Ap6View',.T.,   ,.T.,,,'Text1'    , "Ordem"			 ,06,PJLEFT	  ,.T.,.T.,PJDATEDEFAULT,1,,PJCENTER)
   oApp:TableEdit('Ap6View',.T.,   ,.T.,,,'Name'	    , 'Nome da Tarefa',24,PJLEFT   ,.T.,.T.,PJDATEDEFAULT,1,,PJCENTER)
   oApp:TableEdit('Ap6View',.T.,   ,.T.,,,'Duration' , "Duração"		    ,12,PJRIGHT  ,.T.,.T.,PJDATEDEFAULT,1,,PJCENTER)
   oApp:TableEdit('Ap6View',.T.,   ,.T.,,,'Start'    , "Início"		    ,20,PJRIGHT  ,.T.,.T.,PJDATEDEFAULT,1,,PJCENTER)
   oApp:TableEdit('Ap6View',.T.,   ,.T.,,,'Finish'   , "Fim"			    ,20,PJRIGHT  ,.T.,.T.,PJDATEDEFAULT,1,,PJCENTER)
   oApp:TableApply('Ap6View' )

   dbselectarea("STJ")
   dbsetorder(03)
   dbseek(xFilial("STJ")+MV_PAR01,.T.)
   While !Eof() .And. xFilial("STJ") == STJ->TJ_FILIAL .And. STJ->TJ_PLANO  <= MV_PAR02


      cPLANOSTJ := STJ->TJ_PLANO
      lPLANOVAL := .T.

      If cPLANOSTJ > "000001"
         dbSelectArea("STI")
         dbSetOrder(01)
         If dbSeek(xFilial("STI")+cPLANOSTJ)
            If  STI->TI_SITUACA <> "P" .Or. STI->TI_TERMINO <> "N"
                lPLANOVAL := .F.
            EndIf
         EndIf
      EndIf

      dbSelectArea("STJ")
      While !Eof() .And. STJ->TJ_FILIAL == xFILIAL("STJ") .And. STJ->TJ_PLANO == cPLANOSTJ


         If !lPLANOVAL
            dbSelectArea("STJ")
            dbSkip()
            Loop
         EndIf

         If &(cCONDSTJ)
	         DbSelectArea("ST9")
		      DbSetOrder(1)
		      If DbSeek(xFilial("ST9")+STJ->TJ_CODBEM)
		         If ST9->T9_CODFAMI < MV_PAR11 .Or. ST9->T9_CODFAMI > MV_PAR12 //FILTRA FAMILIA DO BEM
		            DbSelectArea("STJ")
				      DbSkip()
				      Loop
			      EndIf
		      Else
	  	         DbSelectArea("STJ")
			      DbSkip()
			      Loop
		      EndIf

		      lPrimeiro := .t.
		      DbSelectArea("STL")
		      DbSetOrder(01)
		      DbSeek(xFILIAL("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO)
		      While !Eof() .And. STL->TL_FILIAL == xFILIAL("STL") .And.;
	 	         STL->TL_ORDEM == STJ->TJ_ORDEM  .And. STL->TL_PLANO  == STJ->TJ_PLANO

		         If Alltrim(STL->TL_SEQRELA) <> "0"
		            DbSelectArea("STL")
		            DbSkip()
		   	      Loop
			      EndIf

			      If lPrimeiro
			         nTar++
			         oApp:Projects(1):Tasks:Add(Alltrim(STJ->TJ_CODBEM)+" - "+If(STJ->TJ_TIPOOS == "B",Alltrim(NGSEEK("ST9",STJ->TJ_CODBEM,1,"SUBSTR(T9_NOME,1,20)")),;
		             Alltrim(NGSEEK("TAF","X2"+Substr(STJ->TJ_CODBEM,1,3),7,"SUBSTR(TAF_NOMNIV,1,20)")))+" / "+Alltrim(STJ->TJ_SERVICO))
  			         oApp:Projects(1):Tasks(nTar):SetField('PJTASKTEXT3',STJ->TJ_PLANO)
    		         oApp:Projects(1):Tasks(nTar):SetField('PJTASKTEXT1',STJ->TJ_ORDEM)
			         oApp:Projects(1):Tasks(nTar):SetField('PJTASKNUMBER1',STJ->TJ_SEQRELA)
			         oApp:Projects(1):Tasks(nTar):SetField('PJTASKTEXT2',STJ->TJ_CCUSTO)
			         If nTar > 1
	  		            oApp:Projects(1):Tasks(nTar):OutLineOutIndent()
			         EndIf
				      oApp:Projects(1):Tasks(nTar):Start := DTOC(STJ->TJ_DTMPINI)+" "+STJ->TJ_HOMPINI
				      oApp:Projects(1):Tasks(nTar):SetField('PJTASKFINISH',DTOC(STJ->TJ_DTMPFIM)+" "+STJ->TJ_HOMPFIM)
			      EndIf

			      cF3INS := "SB1"
			      CCodigo := SubStr(STL->TL_CODIGO,1,15)
			      If STL->TL_TIPOREG == "T"
			         cF3INS := "SA2"
			         CCodigo := SubStr(STL->TL_CODIGO,1,6)
			      ElseIf STL->TL_TIPOREG == "M"
			         cF3INS := "ST1"
			         CCodigo := SubStr(STL->TL_CODIGO,1,6)
			      ElseIf STL->TL_TIPOREG == "E"
			         cF3INS := "ST0"
			         CCodigo := SubStr(STL->TL_CODIGO,1,3)
			      ElseIf STL->TL_TIPOREG == "F"
			        cF3INS := "SH4"
			        CCodigo := SubStr(STL->TL_CODIGO,1,6)
			      EndIf

			      DbSelectArea(cF3INS)
			      Dbsetorder(1)
			      Dbseek(xFilial(cF3INS)+CCodigo)
			      cNOMCOD := Space(20)

			      If     cF3INS == "SB1"
			             cNOMCOD := SubStr(SB1->B1_DESC,1,20)
			      ElseIf cF3INS == "ST1"
			             cNOMCOD := SubStr(ST1->T1_NOME,1,20)
			      ElseIf cF3INS == "SA2"
			             cNOMCOD := SubStr(SA2->A2_NOME,1,20)
			      ElseIf cF3INS == "SH4"
			             cNOMCOD := SubStr(SH4->H4_DESCRI,1,20)
			      ElseIf cF3INS == "ST0"
			             cNOMCOD := SubStr(ST0->T0_NOME,1,20)
			      EndIf

			      nTar++
			      oApp:Projects(1):Tasks:Add(ALLTRIM(STL->TL_CODIGO)+" - "+ALLTRIM(cNOMCOD))
			      If lPrimeiro
			         oApp:Projects(1):Tasks(nTar):OutLineIndent()
			      EndIf
			      oApp:Projects(1):Tasks(nTar):SetField('PJTASKNUMBER1',STL->TL_SEQRELA)
			      oApp:Projects(1):Tasks(nTar):Start := DTOC(STL->TL_DTINICI)+" "+STL->TL_HOINICI
			      oApp:Projects(1):Tasks(nTar):SetField('PJTASKFINISH',DTOC(STL->TL_DTFIM)+" "+STL->TL_HOFIM)
			      cSeek := "STJ->TJ_ORDEM+STJ->TJ_PLANO+STL->TL_TAREFA+STL->TL_TIPOREG+STL->TL_CODIGO+STL->TL_SEQRELA"

			      oApp:Projects(1):Tasks(nTar):SetField('PJTASKTEXT5',"'"+&cSeek+"'")
			      lPrimeiro := .f.

			      Dbselectarea("STL")
			      Dbskip()
		      End
		   EndIf
	      Dbselectarea("STJ")
	      Dbskip()
      End
   End

   If nTar > 0
      oApp:VISIBLE:= .T.
      If MSGYESNO("Deseja que as alterações feitas no Project sejam sincronizadas com o SIGAMNT ?")
         Processa({|| PROJMNT(@oApp)})
	   EndIf
   Else
      Msgstop("Não há dados a serem mostrados.")
   EndIf
   oApp:Quit( 0 )
   oApp:Destroy()
Else
   MsgInfo("Project não instalado!")
EndIf
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ PROJMNT  |  Autor³Denis Hyroshi de Souza ³ Data ³13/03/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Sincroniza as alteracoes do Project para o SIGAMNT           ±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function PROJMNT(oApp)
Local nx,nw
Local cHoraini,cHorafim,cOrdem,cPlanoSTJ

Local aTempCpo := {}
Local oProject := oApp:Projects(1)
Local cCALFUNC := Space(Len(sh7->h7_codigo))
Local nLimite

oApp:VISIBLE:= .F.
nLimite := oApp:Projects(1):Tasks:Count

ProcRegua(nLimite)
For nw := 1 to nLimite
   IncProc()
   nx := nw
   If !Empty(oApp:Projects(1):Tasks(nx):GetField('PJTASKWBS'))
  	   If Val(oApp:Projects(1):Tasks(nx):GetField('PJTASKOUTLINELEVEL')) == 1

		 	cOrdem    := Substr(Alltrim(oApp:Projects(1):Tasks(nx):GetField('PJTASKTEXT1')),1,6)
		  	cPlanoSTJ := Substr(Alltrim(oApp:Projects(1):Tasks(nx):GetField('PJTASKTEXT3')),1,6)
			DbSelectArea("STJ")
			DbSetOrder(1)
		 	If DbSeek(xFilial("STJ")+cOrdem+cPlanoSTJ)
			   RecLock("STJ",.F.)
				cHoraini := Alltrim(oApp:Projects(1):Tasks(nx):GetField('PJTASKSTART'))
				cHorafim := Alltrim(oApp:Projects(1):Tasks(nx):GetField('PJTASKFINISH'))
				STJ->TJ_DTMPINI := MNTREADDATA(MNTGETFIELD(aTempCpo,oProject,nx,'PJTASKSTART'))
				STJ->TJ_HOMPINI := MNTREADHORA(MNTGETFIELD(aTempCpo,oProject,nx,'PJTASKSTART'))
				STJ->TJ_DTMPFIM := MNTREADDATA(MNTGETFIELD(aTempCpo,oProject,nx,'PJTASKFINISH'))
				STJ->TJ_HOMPFIM := MNTREADHORA(MNTGETFIELD(aTempCpo,oProject,nx,'PJTASKFINISH'))
				MsUnlock("STJ")
			EndIf
	   Else
		   cOrdem := Substr(Alltrim(oApp:Projects(1):Tasks(nx):GetField('PJTASKTEXT5')),2,6)
		   cInsumo := Alltrim(oApp:Projects(1):Tasks(nx):GetField('PJTASKTEXT5'))
			DbSelectArea("STL")
		 	DbSetOrder(01)
			If Dbseek(xFILIAL("STL")+Substr(cInsumo,2,Len(cInsumo)-2))
			   DbSelectArea("STJ")
			   DbSetOrder(01)
			   If Dbseek(xFILIAL('STJ')+cOrdem+cPlanoSTJ)

				   dbSelectArea("STL")
				   RecLock("STL",.F.)
				   cHoraini := Alltrim(oApp:Projects(1):Tasks(nx):GetField('PJTASKSTART'))
				   cHorafim := Alltrim(oApp:Projects(1):Tasks(nx):GetField('PJTASKFINISH'))
				   STL->TL_DTINICI := MNTREADDATA(MNTGETFIELD(aTempCpo,oProject,nx,'PJTASKSTART'))
	  	  		   STL->TL_HOINICI := MNTREADHORA(MNTGETFIELD(aTempCpo,oProject,nx,'PJTASKSTART'))
				   STL->TL_DTFIM   := MNTREADDATA(MNTGETFIELD(aTempCpo,oProject,nx,'PJTASKFINISH'))
				   STL->TL_HOFIM   := MNTREADHORA(MNTGETFIELD(aTempCpo,oProject,nx,'PJTASKFINISH'))

				   If STL->TL_TIPOREG != "P"
				      nQTDHORA := 0
				      If STL->TL_TIPOREG = 'M'
	                  cCALFUNC := NGSEEK('ST1',Substr(STL->TL_CODIGO,1,6),1,"T1_TURNO")
	                  If STL->TL_USACALE = "S" .And. !Empty(cCALFUNC)
	                     nQTDHORA        := NGCALENHORA(STL->TL_DTINICI,STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,cCALFUNC)
	                     STL->TL_QUANTID := If(STL->TL_TIPOHOR = "D",NGCONVERHORA(nQTDHORA,"S","D"),nQTDHORA)
	                  Else
	                     STL->TL_QUANTID := If(STL->TL_TIPOHOR = "D",NGCALCH100(STL->TL_DTINICI,STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM);
	                                          ,NGCALCH060(STL->TL_DTINICI,STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM))
	                  EndIf
	               Else
	                  STL->TL_QUANTID :=  If(STL->TL_TIPOHOR = "D",NGCALCH100(STL->TL_DTINICI,STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM);
	                                         ,NGCALCH060(STL->TL_DTINICI,STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM))
	               Endif
	               STL->TL_CUSTO   := NGCALCUSTI(STL->TL_CODIGO,STL->TL_TIPOREG,;
	                                                STL->TL_QUANTID,STL->TL_LOCAL,STL->TL_TIPOHOR)
				  	   STL->TL_UNIDADE := "H"
				   EndIf
	            MsUnlock("STL")
				EndIf
		   EndIf
  	   EndIf
   EndIf
Next
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTPROJDATE³ Autor ³Denis Hyroshi de SOuza³ Data ³ 01/02/2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de leitura da data a partir do campo no MS-Project     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAPMS                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß

Static Function MNTPROJDATE(cRead,nOPcao,nretorno)
Local nPos1,nPos2,nPos3,cDia,cMes,cAno
If nOPcao == 2
   nPos3 := AT(":",cRead)
   If nPos3 == 0
  	  Return nretorno
   EndIf
   cHora := StrZero(Val(Substr(cRead,nPos3-2,2)),2)
   cMinu := StrZero(Val(Substr(cRead,nPos3+1,2)),2)
   Return cHora+":"+cMinu
EndIf
nPos1 := AT("/",cRead)
nPos2 := RAT("/",cRead)
cDia  := StrZero(Val(Substr(cRead,nPos1-2,2)),2)
cMes  := StrZero(Val(Substr(cRead,nPos2-2,2)),2)
cAno  := StrZero(Val(Substr(cRead,nPos2+1,2)),2)

Return CTOD(cDia+"/"+cMes+"/"+cAno)
*/
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³TIMEPROJ   ³ Autor ³Denis Hyroshi de SOuza³ Data ³ 01/02/2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de leitura da data a partir do campo no MS-Project     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAPMS                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß

Static Function TIMEPROJ(dINI,hINI,dFIM,hFIM,cCOD,cUni)
Local aDIA := {},Pos := 1,xx,ii,nHORA := 0.00,dAtu
Local nRetorno, nFor := 0, nDias := 0

aDIA := NG_H7(cCOD)
If Len(aDIA) == 0
   Help(" ",1,"CALENDINEX")
   Return -1.00
Endif
dAtu := dINI
nINI := HtoM(hINI)
For ii := 1 to ((dFIM - dINI)+1)
   nFor := nHORA
   Pos := If(DOW(dATU)==1,7,DOW(dATU)-1)
   If ii > 1
      nINI := HtoM(aDIA[Pos][1])
   EndIf
   If dFIM == dAtu
      nFim  := HtoM(hFIM)
      nHORA += (nFIM - nINI)
   Else
      nHORA += HtoM(aDIA[Pos][2]) - nINI
      nFim  := HtoM(aDIA[Pos][2])
   EndIf
   For xx := 1 To Len(aDIA[Pos][4])
      If (nINI < aDIA[Pos][4][xx][1]) .and. (nFIM > aDIA[Pos][4][xx][2])
          nHORA -= (aDIA[pos][4][xx][2] - aDIA[pos][4][xx][1])
      EndIf
   Next xx
   If nHORA > nFor
	  nDias++
   EndIf
   dAtu++
Next ii

nRetorno := nHORA/60
If Alltrim(cUni) == "D"
   nRetorno := nDias
ElseIf Alltrim(cUni) == "S"
   nRetorno := Round((nDias)/7,2)
ElseIf Alltrim(cUni) == "M"
   nRetorno := Round((nDias)/30,2)
EndIf
Return nRetorno
*/

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTPROJSX1
Funcao para criar SX1's

@param lRet, Lógico, Determina se a rotina está atualizada.

@author Denis Hyroshi de Souza
@since 08/04/2004

@return Sempre Verdadeiro
/*/
//---------------------------------------------------------------------
Static Function MNTPROJSX1()

	Local lRet     := .T.
	Local cPARAMX1 := PadR( "MNTPRJ", Len(Posicione("SX1", 1, "MNTPRJ", "X1_GRUPO")) )

	dbSelectArea("SX1")
	dbSetOrder(01)
	If !dbSeek(cPARAMX1+"01") // Caso não existe as perguntas.
		ShowHelpDlg( "ATENÇÃO!" ,     ; // "ATENÇÃO!"
					{ "O dicionário de dados está desatualizado, o que pode comprometer a utilização de algumas rotinas." }, 2, ; // "O dicionário de dados está desatualizado, o que pode comprometer a utilização de algumas rotinas."
					{ "Favor aplicar as atualizações contidas no pacote da issue DNG-2319" }, 2 )  // "Favor aplicar as atualizações contidas no pacote da issue DNG-2319"
		lRet := .F.
	EndIf

Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTREADDATA³ Autor ³ Elisangela Costa     ³ Data ³ 10-03-2005 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de leitura da data a partir do campo no MS-Project     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³SIGAMNT                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNTREADDATA(cRead)

Local dRet
Local cDtStart
Local nBarSt1
Local nBarSt2
Local cDiaStart
Local cMesStart

If .T.
	dRet := CTOD(Substr(cRead,1,8))
Else
	nPosSep1 := AT(" ",cRead)
	cDtStart := Substr(cRead,1,(nPosSep1-1))
	nBarSt1 := AT("/",cDtStart)
	nBarSt2 := RAT("/",cDtStart)
	If nBarSt1 == 2
	   cMesStart := StrZero(Val(Substr(cDtStart,1,1)),2)
	Else
	   cMesStart := StrZero(Val(Substr(cDtStart,1,2)),2)
	EndIf
	If nBarSt2 == 4
	   cDiaStart := StrZero(Val(Substr(cDtStart,3,1)),2)
	ElseIf nBarSt2 == 6
	   cDiaStart := StrZero(Val(Substr(cDtStart,4,2)),2)
	ElseIf nBarSt2 == 5
	   If nBarSt1 == 2
	  	  cDiaStart := StrZero(Val(Substr(cDtStart,3,2)),2)
	   Else
	  	  cDiaStart := StrZero(Val(Substr(cDtStart,4,1)),2)
	   EndIf
	EndIf
	dRet := CTOD(cDiaStart+"/"+cMesStart+"/"+Substr(cDtStart,(Len(cDtStart)-1),2))
EndIf

Return dRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTREADHORA³ Autor ³ Elisangela Costa     ³ Data ³ 11-03-2005 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de leitura da hora a partir do campo no MS-Project     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³SIGAMNT                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNTREADHORA(cRead)
Local cHora
Local nPosSep1

If .T.
    cHora := Substr(cRead,Len(cRead)-4,5)
Else
	nPosSep1 := AT(" ",cRead)
	cHora  := Substr(cRead,(nPosSep1+1),((Len(cRead)-nPosSep1)-3))
	If Len(Alltrim(cHora)) == 4
	   cHora := "0" + Alltrim(cHora)
	EndIf
	If Substr(cRead,(Len(cRead)-1),2) == "PM"
	   If Substr(cHora,1,2) <> "12"
	  	  cHora := StrZero((Val(Substr(cHora,1,2))+12),2) + Substr(cHora,3,3)
	   EndIf
	Else
	   If Substr(cHora,1,2) == "12"
	  	  cHora := "00" + Substr(cHora,3,3)
	   EndIf
	EndIf
EndIf

Return cHora

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTGETFIELD ³ Autor ³ Elisangela Costa     ³ Data ³ 11-03-2005 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cache de armazenamento de Objeto para melhorar o desempenho    ³±±
±±³          ³na integracao com o Projet                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNTGETFIELD(aTempCpos,oProject,nk,cField)
Local nPosCpo := aScan(aTempCpos,{|x| x[1]==1 .And. x[2] == nk .and. x[3]==cField})
Local xRet

If nPosCpo >0
	xRet := aTempCpos[nPosCpo][4]
Else
	aAdd(aTempCpos,{1,nk,cField,oProject:Tasks(nk):GetField(cField)})
	xRet := aTempCpos[Len(aTempCpos)][4]
EndIf

Return xRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MSPROJVALP³ Autor ³Elisangela Costa       ³ Data ³27/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro de plano de manutencao                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cVARPLA = Valor do parametro                                ³±±
±±³          ³nPAR = Numero do Parametro de Plano                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTPROJ                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function MSPROJVALP(cVARPLA,nPAR)

If nPAR == 1
   If Empty(MV_PAR01)
      Return .T.
   Else
      If !Existcpo("STI",MV_PAR01)
         Return .F.
      EndIf
   EndIf
Else
   If !Atecodigo("STI",MV_PAR01,MV_PAR02,6)
      Return .F.
   EndIf
EndIf

If !Empty(cVARPLA) .And. cVARPLA <> "ZZZZZZ" .And. cVARPLA > "000001"
   dbSelectArea("STI")
   dbSetOrder(01)
   If dbSeek(xFilial("STI")+cVARPLA)
      If STI->TI_SITUACA <> "P" .Or. STI->TI_TERMINO <> "N"
         MsgStop("Informe um plano que esteja pendente.","ATENÇÃO")
         Return .F.
      EndIf
   EndIf
EndIf

Return .T.
