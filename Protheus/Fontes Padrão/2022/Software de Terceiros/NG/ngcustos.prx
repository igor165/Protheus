#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NGCUSTOS ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 11/11/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula o custo da Ordem de Servi‡o                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void NGCUSTOS(void)                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ PORDEM   -> N£mero da ordem de Servi‡o                     ³±±
±±³          ³ PPLANO   -> N£mero do Plano de Manuten‡Æo                  ³±±
±±³          ³ PINSUMO  -> Tipo de insumo ( NIL = TODOS )                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ RARRAY   ->   Tabela de retorno                            ³±±
±±³          ³               - RARRAYR[1][1] -> Horas Previsto            ³±±
±±³          ³               - RARRAYR[1][2] -> Horas Realizado           ³±±
±±³          ³               - RARRAYR[1][3] -> Custo Previsto            ³±±
±±³          ³               - RARRAYR[1][4] -> Custo Realizado           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION NGCUSTOS(PORDEM,PPLANO,PINSUMO)

PRIVATE nCusto   := 0.00
PRIVATE nQTD1    := 0.00
PRIVATE nTempo   := 0
PRIVATE nSem     := 0
PRIVATE nFol     := 0.00
PRIVATE nprevist := 0.00
PRIVATE nrealiza := 0.00
PRIVATE hprevist := 0.00
PRIVATE hrealiza := 0.00
PRIVATE AcustoOS := {}
PRIVATE Tpinsumo := PINSUMO

// O. Servi‡o normal  ( STJ )

dbSelectArea("STJ")
dbSetOrder(01)

if dbSeek(xfilial('STJ')+PORDEM+PPLANO)

  While !eof() .and. xfilial('STJ') == stj->tj_filial;
               .and. stj->tj_ordem == PORDEM;
               .and. stj->tj_plano == PPLANO

     If STJ->TJ_TERMINO != "S" .OR. STJ->TJ_SITUACA != "L"
         dbSkip()
         Loop
     EndIf

     // PROCURA O CALENDARIO
     // ESTA FUNCAO ESTµ NO NGUTIL.PRX

     cCALEND := PROCALEND(STJ->TJ_CODBEM,STJ->TJ_SERVICO,STJ->TJ_SEQRELA)
	 CALCSTLIN(STJ->TJ_ORDEM,STJ->TJ_PLANO)

     dbSelectArea("STJ")
     DbSkip()

  end
endif

// O. Servi‡o Historico ( STS)

dbSelectArea("STS")
dbSetOrder(01)
if dbSeek(xfilial('STS')+PORDEM+PPLANO)

   While !eof() .and. xfilial('STS') == sts->ts_filial;
                .and. sts->ts_ordem == PORDEM;
                .and. sts->ts_plano == PPLANO

       If STS->TS_TERMINO != "S" .OR. STS->TS_SITUACA != "L"
           dbSkip()
           Loop
       EndIf

       // PROCURA O CALENDARIO
       // ESTA FUNCAO ESTµ NO NGUTIL.PRX

	   cCALEND := PROCALEND(STS->TS_CODBEM,STS->TS_SERVICO,STS->TS_SEQRELA)
	   CALCSTTIN(STS->TS_ORDEM,STS->TS_PLANO)

       dbSelectArea("STS")
       DbSkip()
   end

endif

AADD(AcustoOS,{hprevist,hrealiza,nprevist,nrealiza})

return AcustoOS

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CALCSTlIN ³ Autor ³ Inacio Luiz Kolling   ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa os custos insumos da O.S. ( normal )               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGCUSTOS                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function CALCSTLIN(VORDEM,VPLANO)

// Insumos da O. Servi‡o normal  ( STL )

dbSelectArea("STL")
dbSetOrder(01)

dbSeek(xfilial('STL')+VORDEM+VPLANO)

Do While !eof() .and. xFILIAL('STL') == STL->TL_FILIAL;
                .and. VORDEM == STL->TL_ORDEM;
                .and. VPLANO == STL->TL_PLANO

    // CALCULA CUSTO DO INSUMO

   If Tpinsumo <> nil

      if STL->TL_TIPOREG <> Tpinsumo
           dbSelectArea("STL")
           DbSkip()
           loop
      endif

   endif

   PROCINSUMO(STL->TL_TIPOREG,STL->TL_CODIGO,STL->TL_SEQRELA,;
              If(NGCADICBASE('TL_PCTHREX','A','STL',.F.),STL->TL_PCTHREX,Val(STL->TL_HREXTRA)),STL->TL_QUANTID,STL->TL_DTINICI,;
              STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)

   dbSelectArea("STL")
   DbSkip()

End

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CALCSTTIN ³ Autor ³ Inacio Luiz Kolling   ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa os custos insumos da O.S. ( historico)             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGCUSTOS                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CALCSTTIN(VORDEM,VPLANO)

// O. Servi‡o Historico ( STS)

dbSelectArea("STT")
dbSetOrder(01)

dbSeek(xfilial('STT')+VORDEM+VPLANO)

Do While !eof() .and. xFILIAL('STT') == STT->TT_FILIAL;
                .and. VORDEM == STT->TT_ORDEM;
                .and. VPLANO == STT->TT_PLANO

    // CALCULA CUSTO DO INSUMO DO HISTORICO

   If Tpinsumo <> nil

      if STT->TT_TIPOREG <> Tpinsumo
           dbSelectArea("STT")
           DbSkip()
           loop
      endif

   endif

   PROCINSUMO(STT->TT_TIPOREG,STT->TT_CODIGO,STT->TT_SEQRELA,;
              If(NGCADICBASE('TT_PCTHREX','A','STT',.F.),STT->TT_PCTHREX,Val(STT->TT_HREXTRA)),STT->TT_QUANTID,STT->TT_DTINICI,;
              STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)

   dbSelectArea("STT")
   DbSkip()

End

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³PROCINSUMO³ Autor ³ Inacio Luiz Kolling   ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa os custos insumos da O.S. ( GENERICA )             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ NGCUSTOS,MNTC920 ... MNTC980                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function PROCINSUMO(cTPREG,cCOD,cVSEQ,nHREXT,nQTD,dINI,hINI,dFIM,hFIM,cUND)
Local nSEQF := If(valtype(cVSEQ) = "C",Val(cVSEQ),cVSEQ)
cUND    := Trim(cUND)

nCusto := 0.00
nQTD1  := 0.00

If     cTPREG == "P"
       SB1->(DbSeek(xFilial('SB1')+cCOD))
       nCusto := sb1->b1_custd

ElseIf cTPREG == "M"
       cCodigo := Left(cCOD, LEN(ST1->T1_CODFUNC))
       ST1->(DbSeek(xFilial('ST1')+cCodigo))
       nCustof := st1->t1_salario

       dbSelectArea("ST2")
       dbSetOrder(01)
       dbSeek(xFilial('ST2')+st1->t1_codfunc)

       dbSelectArea("ST0")
       dbSetOrder(01)
       dbSeek(xFilial('ST0')+st2->t2_especia)
       nCusto:= st0->t0_salario

       If nCusto == 0
          nCusto:= nCustof
       Endif

       if nHREXT > 0

           nCusto:= nCusto + ( nCusto * If(NGCADICBASE('TT_PCTHREX','A','STT',.F.),STT->TT_PCTHREX,VAL(STT->TT_HREXTRA)) / 100 )

       endif

ElseIf cTPREG == "F"
       cCodigo := Left(cCOD, LEN(SH4->H4_CODIGO))
       SH4->(DbSeek(xFilial('SH4')+cCodigo))
       nCusto := 0.00

ElseIf cTPREG == "T"
       cCodigo := If(FindFunction("NGProdMNT"), NGProdMNT("T")[1], GETMV("MV_PRODTER")) //Ira verificar apenas o primeiro Produto Terceiro do parametro
       SB1->(DbSeek(xFilial('SB1')+cCodigo))
       nCusto:= sb1->b1_custd

ElseIf cTPREG == "E"
       cCodigo := Left(cCOD, LEN(ST0->T0_ESPECIA))
       ST0->(dbSetOrder(1))
       ST0->(DbSeek(xFilial('ST0')+cCodigo))
       nCusto := ST0->T0_SALARIO

Endif

If cTPREG != "P"

   If Alltrim(cUND) == "H"
       nQTD1 := nQTD
   else
      // ESTA FUNCAO ESTµ NO MNTUTIL.PRX

      nQTD1 := QTDHOR(nQTD, dINI, hINI, dFIM, hFIM, cUND, cCALEND)
   endif

   If nSEQF = 0

       hprevist := hprevist + nQTD1

   else

       hrealiza := hrealiza + nQTD1

   endif

else

   nQTD1 := nQTD

endif

If nSEQF = 0

    nprevist := nprevist + (nQTD1 * nCUSTO)

else

    nrealiza := nrealiza + (nQTD1 * nCUSTO)

endif

Return .T.