#INCLUDE "MNTA710.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

Static lFrotas := IIf( FindFunction('MNTFrotas'), MNTFrotas(), GetNewPar('MV_NGMNTFR','N') == 'S' )

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA710
Cadastro de Status de Pneus

@author André Felipe Joriatti
@since 07/04/2014
@version P11
@return Nil
/*/
//---------------------------------------------------------------------
Function MNTA710()

	Local oBrowse

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 19, 95 )

		oBrowse := FWMBrowse():New()
		oBrowse:SetAlias( "TQY" )
		oBrowse:SetMenuDef( "MNTA710" )
		oBrowse:SetDescription( STR0006 ) // "Cadastro de Status do Bem"
		oBrowse:Activate()

	EndIf

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Opções de menu

@author Vitor Emanuel Batista
@since 10/02/2012
@version P11
@return aRotina - Estrutura
	[n,1] Nome a aparecer no cabecalho
	[n,2] Nome da Rotina associada
	[n,3] Reservado
	[n,4] Tipo de Transação a ser efetuada:
		1 - Pesquisa e Posiciona em um Banco de Dados
		2 - Simplesmente Mostra os Campos
		3 - Inclui registros no Bancos de Dados
		4 - Altera o registro corrente
		5 - Remove o registro corrente do Banco de Dados
		6 - Alteração sem inclusão de registros
		7 - Cópia
		8 - Imprimir
	[n,5] Nivel de acesso
	[n,6] Habilita Menu Funcional
/*/
//---------------------------------------------------------------------
Static Function MenuDef()
// Inicializa MenuDef com todas as opções
Return FWMVCMenu( "MNTA710" )

//---------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author André Felipe Joriatti
@since 07/04/2014
@version P11
@return Model
/*/
//---------------------------------------------------------------------
Static Function ModelDef()

	Local oModel
	Local oStructTQY

	oStructTQY := FWFormStruct( 1,"TQY" )

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New( "MNTA710",/*bPre*/,,,/*bCancel*/ )

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields( "MNTA710_TQY",Nil,oStructTQY,/*bPre*/,/*bPost*/,/*bLoad*/ )

	// Descrição do Model
	oModel:SetDescription( STR0006 ) // "Cadastro de Status do Bem"

Return oModel

//---------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author André Felipe Joriatti
@since 07/04/2014
@version P11
@return Nil
/*/
//---------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel( "MNTA710" )
	Local oView  := Nil

	oStruTQY := FWFormStruct( 2,"TQY" )

	oView := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel( oModel )

	// Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA710_TQY",oStruTQY, /*cLinkID*/ )

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER",100,/*cIDOwner*/,/*lFixPixel*/,/*cIDFolder*/,/*cIDSheet*/ )

	// Associa um View a um box
	oView:SetOwnerView( "MNTA710_TQY","MASTER" )

	//Inclusão de itens nas Ações Relacionadas de acordo com O NGRightClick
	NGMVCUserBtn(oView)

Return oView

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT710WHEN
Função para WHEN dos campos da rotina

@param String cField: indica campo para when
@author André Felipe Joriatti
@since 08/04/2014
@version P11
@return Boolean lRet: conforme validações
/*/
//---------------------------------------------------------------------
Function MNT710WHEN( cField )

	Local lRet := .T.

	If cField == "TQY_CATBEM"
		// A partir do release 12.1.33, o parâmetro MV_NGMNTFR será descontinuado
		// O gestão de frotas agora é o novo modulo SIGAGFR(95), indiferente da configuração do parâmetro
		lRet := lFrotas

	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT710VLCP
Validações de campos da rotina

@param String cCampo: indica campo para validar
@author André Felipe Joriatti
@since 08/04/2014
@version P11
@return Boolean lRet: conforme validação
/*/
//---------------------------------------------------------------------
Function MNT710VLCP( cCampo )

	Local lRet := .T.

	If cCampo == "TQY_STATUS"
		lRet := ExistChav( "TQY",M->TQY_STATUS )
	EndIf

Return lRet
