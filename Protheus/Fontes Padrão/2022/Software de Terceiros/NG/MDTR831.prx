#INCLUDE "MDTR831.ch"
#Include "Protheus.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} MDTR831   
Relatório fixo para preenchimento do CAT

@author Guilhrme Freudenburg	
@since 09/09/2014

@return Nil
/*/
//---------------------------------------------------------------------
Function MDTR831()

Local aNGBEGINPRM := NGBEGINPRM()

//---------------------------
//Chama relatório  
//---------------------------  
RIMP831()   

//---------------------------------------------------------------
//  Devolve variaveis armazenadas (NGRIGHTCLICK)                -
//---------------------------------------------------------------
NGRETURNPRM(aNGBEGINPRM)
Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} RIMP831  
Faz a impressão do relatório fixo, para preenchimento da CAT

@author Guilherme Freudenburg	
@since 09/09/2014

@return Nil  
/*/
//---------------------------------------------------------------------
Static Function RIMP831()

Local oPrint	// Objeto de impressão
Local nX := 0	// Variavel de controle
Local aCAT := {}	//Array que recebe os valores fixos

Private lin := 0		//Controle de linha 
Private oFont09, oFont11, oFont14	//Fontes utilizados. 

oFont09	:= TFont():New("Arial",9,9,,.F.,,,,.F.,.F.)
oFont11	:= TFont():New("Arial",11,11,,.T.,,,,.F.,.F.)
oFont14	:= TFont():New("Arial",14,14,,.T.,,,,.F.,.F.)  
   
    
oPrint	:= TMSPrinter():New(OemToAnsi(STR0001)) //"Tabela de dias debitados"

aCAT:= MDT831CAT()  

	//----------------------------------------  
	// Imprime Relatório
	//----------------------------------------
	lin := 100     
	oPrint:Say(lin,940,STR0001,oFont14)//"Tabela de dias debitados"
	Lin+= 120
	oPrint:Line(lin,200,lin,2350)//Pimeira linha superior horizontal
	oPrint:Line(lin,200,lin+100,200) //Primeira linha vertical      
	oPrint:Say(lin+20,220,STR0026,oFont11)//"Natureza"  
	oPrint:Line(lin,1510,lin+100,1510) // Segunda linha vertical
	oPrint:Say(lin+20,1540,STR0027,oFont11)//"Avaliação Percentual"
	oPrint:Line(lin,1990,lin+100,1990) //Tericeira linha vertical 
	oPrint:Say(lin+20,2020,STR0028,oFont11)	//"Dias Debitados"    
	oPrint:Line(lin,2350,lin+100,2350)//Quarta linha vertical    
	oPrint:Line(lin+100,200,lin+100,2350)//Linha inferior horizontal           
	lin:=320     
	For nX := 1 To Len(aCAT)
		oPrint:Line(lin,200,lin+100,200)//Primeira linha vertical		 
		oPrint:Say(lin+30,220,aCAT[nX,1],oFont09)//Natureza      
		oPrint:Line(lin,1510,lin+100,1510)//Segunda linha vertical  
		oPrint:Say(lin+30,1720,aCAT[nX,2],oFont09) //Avaliação Percuentual  
		oPrint:Line(lin,1990,lin+100,1990)//Terceira linha vertical
		oPrint:Say(lin+30,2120,aCAT[nX,3],oFont09)//Dia debitados       
		oPrint:Line(lin,2350,lin+100,2350)//Quarta linha vertical   
		oPrint:Line(lin+100,200,lin+100,2350)//Linha horizontal inferior             
		Lin+=100      
 	Next nX
 		oPrint:Preview()    
Return   

//---------------------------------------------------------------------
/*/{Protheus.doc} MDT831CAT  
Carrega array com os valores FIXOS da tabela de Dias Debitados

@author Guilherme Freudenburg	
@since 09/09/2014
@sample MDTR831 , MDTA640
@return aCAT - Arrya com os valores Fixos da tabela de Dias Debitados
/*/
//---------------------------------------------------------------------
Function MDT831CAT()

Local aCAT //Arra que receberá os valores da tabela Fixa de Dias Debitados

	aCAT := {	{ STR0002, "100"   , "6.000", .F. } ,;	//"Morte"
				{ STR0003, "100"   , "6.000", .F. } ,;	//"Incapacidade total e permanente" 
				{ STR0004, "100"   , "6.000", .F. } ,;	//"Perda de visão de ambos os olhos"
				{ STR0005, "30"	   , "1.800", .F. } ,;	//"Perda da visão de um olho"
				{ STR0006, "75"	   , "4.500", .F. } ,;	//"Perda do braço acima do cotovelo"
				{ STR0007, "60"	   , "3.500", .F. } ,;	//"Perda do braço abaixo do cotovelo" 
				{ STR0008, "50"	   , "3.000", .F. } ,;	//"Perda da mão"
				{ STR0009, "10"	   , "600"	, .F. } ,;	//"Perda do 1º quirodátilo (polegar)"
				{ STR0010, "5"	   , "300"	, .F. } ,;	//"Perda de qualquer outro quirodátilo (dedo)" 
				{ STR0011, "12 1/2", "750"	, .F. } ,;	//"Perda de dois outros quirodátilo (dedos)"
				{ STR0012, "20"	   , "1.200", .F. } ,;	//"Perda de três outros quirodátilo (dedos)"
				{ STR0013, "30"	   , "1.800", .F. } ,;	//"Perda de quatro outros quirodátilo (dedos)"
				{ STR0014, "20"	   , "1.200", .F. } ,;	//"Perda do 1º quirodátilo (polegar) e qualquer outro quirodátilo (dedo)"
				{ STR0015, "25"	   , "1.500", .F. } ,;	//"Perda do 1 quirodátilo (polegar) e dois outros quirodátilo (dedos)" 
				{ STR0016, "33 1/2", "2.000", .F. } ,;	//"Perda do 1º quirodátilo (polegar) e três outros quirodátilo (dedos)"	
				{ STR0017, "40"	   , "2.400", .F. } ,;	//"Perda do 1º quirodátilo (polegar) e quatro outros quirodátilo (dedos)"
				{ STR0018, "75"	   , "4.500", .F. } ,;	//"Perda da perna acima do joelho" 
				{ STR0019, "50"	   , "3.000", .F. } ,;	//"Perda da perna no joelho ou abaixo dele"
				{ STR0020, "40"	   , "2.400", .F. } ,;	//"Perda do pé" 
				{ STR0021, "6"	   , "300"	, .F. } ,;	//"Perda do pododátilo (dedo grande) ou de dois ou mais pododátilos (dedos do pé)"
				{ STR0022, "10"	   , "600"	, .F. } ,;	//"Perda do 1º pododátilo (dedo grande) de ambos os pés" 
				{ STR0023, "0"	   , "0"	, .F. } ,;	//"Perda de qualquer outro pododátilo (dedo do pé)" 	
				{ STR0024, "10"	   , "600"	, .F. } ,;	//"Perda da audição de um ouvido"
				{ STR0025, "50"	   , "3.000", .F. } }	//"Perda da audição de ambos ou ouvidos"
Return aCAT
