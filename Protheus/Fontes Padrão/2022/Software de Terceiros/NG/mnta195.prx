#INCLUDE "MNTA195.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA195
Programa para exportar dados para atualizar posicao de
contador no sistema Bandag

@author Elisangela Costa
@since 25/07/06
@version P12

@return .T.
/*/
//------------------------------------------------------------------------------
Function MNTA195()

	Local lAMBIE   		:= .F.
	Local cIniFile 		:= GetAdv97()
	Private cCodEmp245  := ""
	Private cCodFil245  := ""

	//Abre tabelas necessarias
	If !(Type("oMainWnd")=="O")

		lAMBIE  := .T.
		cCodEmp245 := GetPvProfString("ONSTART","Empresa","",cInIfile)
		cCodFil245 := GetPvProfString("ONSTART","Filial" ,"",cInIfile)

		If cCodEmp245 == '-1' .Or. cCodFil245 == '-1'
			Return .F.
		EndIf

		RPCSetType(3)
		RPCSetEnv(cCodEmp245,cCodFil245,"","","MNT","",{"ST9"})
		MNT195EXP(lAMBIE)
	Else
		MNT195EXP(lAMBIE)
	EndIf
Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ MNT195EXPºAutor  ³ Elisangela Costa   º Data ³  25/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Faz a leitura dos bens para montar o arquivo de acordo     º±±
±±º          ³ com os parametros                                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MNTA195                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT195EXP(xAuto)

	Private aRegs := {}
	Private lAuto := iif(xAuto!=Nil,xAuto,.F.)

	//+----------------------------------------------------------+
	//|Variaveis utilizadas para parametros!                     |
	//| mv_par01     // De Filial                                |
	//| mv_par02     // Ate Filial                               |
	//| mv_par03     // De Familia                               |
	//| mv_par04     // Ate Familia                              |
	//| mv_par05     // De Data Atual.                           |
	//| mv_par06     // Ate Data Atual.                          |
	//| mv_par07     // Nome do Arquivo                          |
	//| mv_par08     // Caminho Gravacao                         |
	//+----------------------------------------------------------+

	Private cPerg := PadR( "MNT195", Len(Posicione("SX1", 1, "MNT195", "X1_GRUPO")) )

	If lAuto

   		mv_par01 := Substr(Posicione("SX1", 1, cPerg+"01", "X1_CNT01"),1,2)
   		mv_par02 := Substr(Posicione("SX1", 1, cPerg+"02", "X1_CNT01"),1,2)
   		mv_par03 := Substr(Posicione("SX1", 1, cPerg+"03", "X1_CNT01"),1,6)
   		mv_par04 := Substr(Posicione("SX1", 1, cPerg+"04", "X1_CNT01"),1,6)
   		mv_par05 := CTOD(Substr(Posicione("SX1", 1, cPerg+"05", "X1_CNT01"),1,10))
   		mv_par06 := CTOD(Substr(Posicione("SX1", 1, cPerg+"06", "X1_CNT01"),1,10))
   		mv_par07 := Substr(Posicione("SX1", 1, cPerg+"07", "X1_CNT01"),1,30)
   		mv_par08 := Substr(Posicione("SX1", 1, cPerg+"08", "X1_CNT01"),1,30)

  		MakeDir(mv_par08)
		MNT195EXPD()
	Else
  		If !Pergunte(cPerg,.T.)
      		Return
   		Endif
   		MakeDir(mv_par08)
   		Processa({ || MNT195EXPD() })
   		MsgInfo(STR0017,STR0018) //"Processo de exportacao realizado com sucesso."#"ATENCAO"
	Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNT195EXPDºAutor  ³ Elisangela Costa   º Data ³  25/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Faz a leitura dos bens para montar o arquivo de acordo      º±±
±±º          ³com os parametros                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MNT195EXP                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MNT195EXPD()

Local nHand1   := 0
Local cArq1    := Alltrim(mv_par08)+Alltrim(mv_par07)
Local cQuery   := "", cLinha := ""
Local cFilHiV  := xFilial("ST9")
Local cCondST9 := "!Empty(ST9->T9_CODBEM) .And. ST9->T9_TEMCONT = 'S' .And. ST9->T9_SITBEM = 'A' .And."
      cCondST9 += "ST9->T9_CODFAMI >= MV_PAR03 .And. ST9->T9_CODFAMI <= MV_PAR04 .And."
      cCondST9 += "ST9->T9_DTULTAC >= MV_PAR05 .And. ST9->T9_DTULTAC <= MV_PAR06"

If NGSX2MODO("ST9") <> "E"
   MV_PAR01 := cFilHiV
   MV_PAR02 := cFilHiV
EndIf

#IFNDEF TOP

   dbSelectArea("ST9")
   dbSetOrder(01)
   dbSeek(MV_PAR01,.T.)
   If !EoF()

	   //Cria arquivo de trabalho
	   If ((nHand1 := fCreate(cArq1)) == -1)
	      Return .T.
	   Endif

	   If (!lAuto)
	      Procregua(1)
	   EndIf
	EndIf

   dbSelectArea("ST9")
   While !EoF() .And. ST9->T9_FILIAL <= MV_PAR02

      If (!lAuto)
	      Incproc(STR0016+Alltrim(ST9->T9_CODBEM)) //"Veiculos..."
	   EndIf

      If &(cCondST9)
         //Dados para serem exportados--> Monta a linha para gravar no arquivo

         If !lAuto
            cLinha := "'"+Alltrim(cEmpAnt)+"',"
         Else
            cLinha := "'"+Alltrim(cCodEmp245)+"',"
         EndIf
  	      cLinha += "'"+Alltrim(ST9->T9_FILIAL)+"',"
	      cLinha += "'"+Substr(DTOS(ST9->T9_DTULTAC),7,2)+Substr(DTOS(ST9->T9_DTULTAC),5,2)+Substr(DTOS(ST9->T9_DTULTAC),3,2)+"',"
	      cLinha += "'"+Alltrim(ST9->T9_CODBEM)+"',"
	      cLinha += "'"+Alltrim(Str(ST9->T9_CONTACU))+"',"
	      cLinha += "'UPD'"
	      cLinha += chr(13)+chr(10)
		   fWrite(nHand1,cLinha,Len(cLinha))
		EndIf
      dbSelectArea("ST9")
      dbSkip()
   End
   fClose(nHand1)
#ELSE

   //Monta a Query para filtrar os veiculos de acordo com os parametros
	cQuery := "SELECT T9_FILIAL,T9_DTULTAC,T9_CODBEM,T9_CONTACU FROM "+RetSqlName("ST9")+" "
	cQuery += "WHERE D_E_L_E_T_ = '' AND RTRIM(T9_CODBEM) <> '' AND T9_FILIAL >= '"+ MV_PAR01 +"'AND T9_FILIAL <='"+ MV_PAR02+"'
	cQuery += "AND T9_CODFAMI >= '"+ MV_PAR03 +"'AND T9_CODFAMI <='"+ MV_PAR04 +"'
	cQuery += "AND T9_DTULTAC >= '"+ Dtos(MV_PAR05) +"'AND T9_DTULTAC <='"+ Dtos(MV_PAR06)+"'
	cQuery += "AND T9_TEMCONT = 'S' AND T9_SITBEM = 'A'"
	cQuery += "ORDER BY T9_FILIAL,T9_CODBEM"
	cQuery := ChangeQuery(cQuery)
	TCQuery cQuery NEW ALIAS "BDGST9"

	dbSelectArea("BDGST9")
	dbGotop()
	If !EoF()

	   //Cria arquivo de trabalho
	   If ((nHand1 := fCreate(cArq1)) == -1)
	      	dbSelectArea("BDGST9")
	    	dbCloseArea()
	  	   	Return .T.
	   EndIf

	   If (!lAuto)
	 	   Procregua(1)
	   EndIf

	   While !EoF()

		   If (!lAuto)
	          Incproc(STR0016+Alltrim(BDGST9->T9_CODBEM)) //"Veiculos..."
		   EndIf

		   //Dados para serem exportados--> Monta a linha para gravar no arquivo
		   If !lAuto
            cLinha := "'"+Alltrim(cEmpAnt)+"',"
         Else
            cLinha := "'"+Alltrim(cCodEmp245)+"',"
         EndIf
  	      cLinha += "'"+Alltrim(BDGST9->T9_FILIAL)+"',"
	      cLinha += "'"+Substr(BDGST9->T9_DTULTAC,7,2)+Substr(BDGST9->T9_DTULTAC,5,2)+Substr(BDGST9->T9_DTULTAC,3,2)+"',"
	      cLinha += "'"+Alltrim(BDGST9->T9_CODBEM)+"',"
	      cLinha += "'"+Alltrim(Str(BDGST9->T9_CONTACU))+"',"
	      cLinha += "'UPD'"
	      cLinha += chr(13)+chr(10)
		   fWrite(nHand1,cLinha,Len(cLinha))

	      dbSelectArea("BDGST9")
		   dbSkip()
	   End
	   fClose(nHand1)
	EndIf
	dbSelectArea("BDGST9")
	dbCloseArea()
#ENDIF

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ M195VAFILºAutor  ³ Elisangela Costa   º Data ³  25/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida o codigo da filial                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MNT195EXP                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M195VAFIL(nOpc)
Local cVERFL
cVERFL := MV_PAR01

If Empty(MV_PAR01)
   Return .t.
Else
   If nOpc == 1
   	lRet := IIf(Empty(MV_PAR01),.T.,ExistCpo("SM0",SM0->M0_CODIGO+MV_PAR01))
	   If !lRet
	      Return .F.
	   EndIf
   EndIf

   If nOpc == 2
      lRet := IIf(ATECODIGO("SM0",SM0->M0_CODIGO+MV_PAR01,SM0->M0_CODIGO+MV_PAR02,02),.T.,.F.)
      If !lRet
         Return .F.
      EndIf
   EndIf
EndIf

Return .T.