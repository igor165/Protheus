#INCLUDE "Mdta920.ch"
#include "Protheus.ch"
#DEFINE _nVERSAO 1 //Versao do fonte
  
/*/


Ŀ
Funo     MDTA920   Autor Denis Hyroshi de Souza  Data  16/02/04 
Ĵ
Descrio  Programa de Cadastro de Licencas Maternidade               
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MDTA920
//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 					      
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)  

Private lIntGpe   := If( SuperGetMv("MV_MDTGPE",.F.,"N") == "S", .t. , .f. )//Verifica se est integrado ao GPE
Private lCpoDura  := If( NGCADICBASE("R8_DURACAO","A","SR8",.F.) .and.;//Verifica se existem os campos
						 NGCADICBASE("R8_DPAGAR" ,"A","SR8",.F.) .and.;
						 NGCADICBASE("R8_DIASEMP","A","SR8",.F.) , .T. , .F. )

Private lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Private bNGGRAVA
Private nSizeCli   := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
Private nSizeLoj   := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))
Private nSizeSI3   := If((TAMSX3("I3_CUSTO")[1]) < 1,9,(TAMSX3("I3_CUSTO")[1]))  //Usado no X3_RELACAO
Private aRotina    := MenuDef()
Private cCadastro  := OemtoAnsi(STR0006) //"Licencas Maternidade"

If FindFunction("MDTRESTRI") .AND. !MDTRESTRI(cPrograma)
	//Ŀ
	// Devolve variaveis armazenadas (NGRIGHTCLICK) 			 			  
	//
	NGRETURNPRM(aNGBEGINPRM)
	Return .F.
Endif

If !lSigaMdtPS
	MDTA920d_()
Else
	dbSelectArea("SA1")
	dbSetOrder(1)
	mBrowse( 6, 1,22,75,"SA1")
Endif

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK) 						  
//
NGRETURNPRM(aNGBEGINPRM)

Return .T.
/*/


Ŀ
Funo     MDTA920d_ Autor  Denis                  Data 21/03/2000
Ĵ
Descrio  Registrar os Licencas Maternidades                         
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MDTA920d_
Local aArea			:= GetArea()
Local aAreaTOF		:= TOF->( GetArea() )
Local aIndexTOF		:= {}
Local aIndexTM0		:= {}
Local oldROTINA     := aCLONE(aROTINA)
Local uRet

Private cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA
Private cAlias    := "TOF"
Private cNUMFIC   := space(09)

Begin Sequence

	aRotina :=	MenuDef( .F. )
	
	If lSigaMdtPS
		cCadastro	:= OemToAnsi(STR0006+STR0011+Alltrim(SA1->A1_NOME)) //" - Cliente: "
		
		/*
		Ŀ
		 Inicializa o filtro da tabela TOF - Licencas                           
		*/
		Private bFiltra920	:= { || NIL }
		cFiltra920 := ChkRh("MDTA920","TOF","1")
		cFiltra920 += IF(!Empty(cFiltra920),' .and. ','')
		cFiltra920 += 'TOF_CLIENT == "'+SA1->A1_COD+'" .And. TOF_LOJA == "'+SA1->A1_LOJA+'"'
		bFiltra920 := { || FilBrowse("TOF",@aIndexTOF,@cFiltra920) }
		Eval( bFiltra920 )
	Else
		/*
		Ŀ
		 Inicializa o filtro da tabela TM0 -Ficha Medica                        
		*/
		Private bFiltra921	:= { || NIL }
		cFiltra921 := 'TM0_SEXO == "2" .AND. Empty(TM0_NUMDEP)'
		bFiltra921 := { || FilBrowse("TM0",@aIndexTM0,@cFiltra921) }
		Eval( bFiltra921 )
	Endif
	
	dbSelectarea("TOF")
	dbSetOrder(1)
	mBrowse( 6, 1,22,75,"TOF")

	/*
	Ŀ
	 Deleta o filtro utilizando a funcao FilBrowse                     	 
	*/
	If lSigaMdtPS
		EndFilBrw( "TOF" , aIndexTOF )
	Else
		EndFilBrw( "TM0" , aIndexTM0 )
	Endif
	
End Sequence

RestArea( aAreaTOF )
RestArea( aArea )
aROTINA := aCLONE(oldROTINA)

Return .T.
/*/


Ŀ
Funo     VAL920FIC Autor Denis Hyroshi de Souza  Data  16/02/04 
Ĵ
Descrio  Valida campo Ficha Medica p/ aceitar sexo feminino         
ٱ


/*/
Function VAL920FIC(cFichaMed)

Dbselectarea("TM0")
Dbsetorder(1)
If Dbseek(xFilial("TM0")+cFichaMed)
	If TM0->TM0_SEXO != "2"
		MsgStop(STR0007,STR0008) //"Somente poder selecionar pessoa do sexo feminino."###"ATENCAO"
		Return .f.
	Endif
Else
	Help(" ",1,"REGNOIS")
	Return .f.
Endif

Return .t.

/*/


Ŀ
Funo     VAL920DTF Autor Denis Hyroshi de Souza  Data  16/02/04 
Ĵ
Descrio  Altera a data de saida e de retorno da licenca maternidade 
ٱ


/*/
Function VAL920DTF(dDataFim)

If !Naovazio(dDataFim)//Verifica se est vazio
	Return .f.
Endif

M->TOF_DTRLIC := dDataFim+120 //Data de Retorno da Licenca Maternidade: 120 dias apos a data de saida
lRefresh := .t.

Return .t.

/*/


Ŀ
Funo     IMPATEST  Autor    Liber de Esteban     Data  20/04/04 
Ĵ
Descrio  Imptime o atestado para licenca maternidade                
ٱ


/*/
Function IMPATEST()

Local cCart, cSerie, cNome 

Private oFont14,oFont16,oFont21
oFont14	:= TFont():New("COURIER NEW",13,13,,.F.,,,,.F.,.F.)
oFont16	:= TFont():New("COURIER NEW",16,16,,.F.,,,,.F.,.F.)
oFont21	:= TFont():New("COURIER NEW",21,21,,.F.,,,,.F.,.F.)

oPrint	:= TMSPrinter():New(OemToAnsi(STR0010)) //"ATESTADO MDICO PARA GESTANTE"
oPrint:Setup()

lin := 450

oPrint:StartPage()
   
	dbselectarea("TM0")
	dbSetorder(01)
	dbSeek(xFilial("TM0")+TOF->TOF_NUMFIC)
	dbselectarea("SRA")
	dbSetorder(01)   
	dbSeek(xFilial("SRA")+TM0->TM0_MAT)                    
	oPrint:Say(lin,510,STR0010,oFont21) //"ATESTADO MDICO PARA GESTANTE"
	lin += 450
    cNome := If(!Empty(SRA->RA_NOME),Alltrim(SRA->RA_NOME),Alltrim(TM0->TM0_NOMFIC))//Verifica se pegar os dados da SRA ou TM0 
    cCart := If(!Empty(SRA->RA_NUMCP),Alltrim(SRA->RA_NUMCP),Alltrim(TM0->TM0_NUMCP))//Verifica se pegar os dados da SRA ou TM0
    cSerie:= If(!Empty(SRA->RA_SERCP),Alltrim(SRA->RA_SERCP),Alltrim(TM0->TM0_SERCP))//Verifica se pegar os dados da SRA ou TM0
    nNome := len(cNome)
    if nNome > 18
   		cLin1 := STR0012 + SUBSTR(cNome,1,22) + STR0013 //"ATESTO QUE A SEGURADA EMPREGADA "###", PORTADORA DA CARTEIRA"
   	ElseiF nNome > 15                                                                                                      
		cLin1 := STR0014 + cNome + STR0013 //"    ATESTO QUE A SEGURADA EMPREGADA "###", PORTADORA DA CARTEIRA"
	Else	
		cLin1 := STR0015 + cNome + STR0013 //"     ATESTO  QUE A SEGURADA EMPREGADA  "###", PORTADORA DA CARTEIRA"
   	End	                                                                                      
   	cLin2 := STR0016 + cCart + STR0017 + cSerie + STR0018 //"DE  TRABALHO  E  PREVIDENCIA  SOCIAL  NMERO "###" , SRIE  "###" ,  DEVER"
   	cLin3 := STR0019           //"AFASTAR-SE DO TRABALHO POR  UM PERODO DE 120 (CENTO E VINTE) DIAS, A PARTIR"
   	cLin4 := STR0020 + DTOC(TOF->TOF_DTSLIC) + STR0021 //"DE  "###" , NOS TERMOS DO ARTIGO 393 DA C.L.T., DEVENDO RETORNAR AO "
   	cLin6 := STR0022 + DTOC(TOF->TOF_DTRLIC) + STR0023 //"TRABALHO NO DIA "###", PASSANDO PELO SERVIO MDICO PARA A REALIZAO DO"
   	cLin7 := STR0024 //"EXAME MDICO DE RETORNO AO TRABALHO."
   		
	oPrint:Say(lin,60,cLin1,oFont14)
	lin += 80  
	oPrint:Say(lin,60,cLin2,oFont14)
	lin += 80
	oPrint:Say(lin,60,cLin3,oFont14)
	lin += 80
	oPrint:Say(lin,60,cLin4,oFont14)
	lin += 80
	oPrint:Say(lin,60,cLin6,oFont14)
	lin += 80
	oPrint:Say(lin,60,cLin7,oFont14)
	
	lin += 500                           
	cData := Alltrim(SM0->M0_CIDCOB)+", "+STRZERO(Day(dDataBase),2)+STR0025+ Upper(MesExtenso(dDataBase))+STR0025+StrZero(Year(dDataBase),4) //" de "###" de "
	oPrint:Say(lin,600,cData,oFont16)
	lin += 450                          
	oPrint:line(lin,650,lin,1650)                      
	oPrint:Say(lin+25,860,STR0026,oFont14) //"CARIMBO E ASSINATURA"
	
oPrint:EndPage()
oPrint:Preview()

Return .t.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMDT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados         
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef( lMdtPs )
Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Local aRotina

Default lMdtPs  := lSigaMdtPS

If lMdtPs
	aRotina := { 	{ STR0001 , "AxPesqui"	, 0 , 1},; //"Pesquisar"
					{ STR0002 , "NGCAD01"	, 0 , 2},; //"Visualizar"
					{ STR0027 , "MDTA920d_"	, 0 , 4}} //"Licenas M."
Else
	aRotina :=	{ 	{ STR0001, "AxPesqui"	, 0 , 1},; //"Pesquisar"
					{ STR0002, "NGCAD01"	, 0 , 2},; //"Visualizar"
					{ STR0003, "MDT920INC"	, 0 , 3},; //"Incluir"
					{ STR0004, "MDT920INC"	, 0 , 4},; //"Alterar"
					{ STR0005, "MDT920INC"	, 0 , 5, 3},; //"Excluir"
					{ STR0009, "ImpAtest"	, 0 , 2}} //"Atestado"
	If !lSigaMdtPS .AND. SuperGetMv("MV_NG2AUDI",.F.,"2") == "1"
		aAdd( aRotina , { STR0037,"MDTA991('TOF')" , 0 , 3 } )//"Hist. Exc."
	Endif
Endif
Return aRotina
/*


ͻ
Programa  MDT920INC Autor  Roger Rodrigues      Data   27/05/10   
͹
Desc.     Tela de incluso de licenca maternidade                     
                                                                      
͹
Uso       MDTA920                                                     
ͼ


*/
Function MDT920INC(cAlias, nRecno, nOpcx)
Local nRet := 0, cSeq := 0
Private nRecSR8 := 0

If lIntGpe .and. (nOpcx == 4 .or. nOpcx ==5)//Verifica se esta integrado ao GPE e alteracao ou inclusao
	dbSelectArea("TM0")
	dbSetOrder(1)
	dbSeek(xFilial("TM0")+TOF->TOF_NUMFIC)
	dbSelectArea("SRA")
	dbSetOrder(1)
	If dbSeek(xFilial("SRA",TM0->TM0_FILFUN)+TM0->TM0_MAT)
		dbSelectArea("SR8")
		dbSetOrder(1)
		If dbSeek(xFilial("SRA",TM0->TM0_FILFUN)+SRA->RA_MAT+DTOS(TOF->TOF_DTSLIC)+"Q")
			nRecSR8 := SR8->(Recno())
		Endif
	Endif
Endif

If NGCADICBASE("TMK_USUARI","A","TMK",.F.) .AND. cValToChar(nOpcx) $ "4/5" .AND. FindFunction("MDTRESTRI") .AND. !MDTRESTUS(MDTDATALO("TOF->TOF_USERGI",.F.))
	bNGGRAVA  := {||}
	Return .F.
ElseIf NGCADICBASE("TMK_USUARI","A","TMK",.F.) .AND. nOpcx == 5 .AND. SuperGetMV("MV_NG2SEG",.F.,"2") == "1" .AND. !(SuperGetMV("MV_MDTPS",.F.,"N") == "S") .AND. ;
		FindFunction("MDTEXCSBI") .AND. !MDTEXCSBI(MDTDATALO("TOF->TOF_USERGI"))
 	bNGGRAVA  := {||}
	Return .F.
Endif

bNGGRAVA  := {|| f920VLAF(nOpcx)} // Verifica se os campos estao preenchidos corretamente
nRet := NGCAD01("TOF", nRecno, nOpcx)//Abre tela de cadastro
bNGGRAVA  := {|| }

If nRet == 1//Se confirmar tela
	dbSelectArea("TM0")
	dbSetOrder(1)
	dbSeek(xFilial("TM0")+TOF->TOF_NUMFIC)
	dbSelectArea("SRA")
	dbSetOrder(1)
	If dbSeek(xFilial("SRA",TM0->TM0_FILFUN)+TM0->TM0_MAT) .and. lIntGpe//Se encontrar funcionrio e for integrado ao GPE
		//Exclui registro de afastamento anterior
		If (nOpcx == 4 .or. nOpcx == 5) .and. nRecSR8 > 0
			dbSelectArea("SR8")
			dbGoTo(nRecSR8)
			If !Eof() .and. SR8->R8_TIPO == "Q"
				RecLock("SR8",.F.)
				dbDelete()
				MsUnlock("SR8")
			Endif
		Endif
		If nOpcx == 3 .or. nOpcx == 4
			If ExistBlock("MDTA9201")
				ExecBlock("MDTA9201",.F.,.F.) 
			Else
				//Verifica ultima sequencia
				dbSelectArea("SR8")
				dbSetOrder(2)
				dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"ZZZ",.T.)
				dbSkip(-1)
				IF !Eof() .AND. SRA->RA_FILIAL+SRA->RA_MAT == SR8->R8_FILIAL+SR8->R8_MAT
					cSeq := SOMA1(SR8->R8_SEQ)
				Else
					cSeq := "001"
				Endif
				//Grava afastamento 
				dbSelectArea("SR8")
				dbSetOrder(1)
				If !dbSeek(SRA->RA_FILIAL+SRA->RA_MAT+DTOS(TOF->TOF_DTSLIC)+"Q")
					Reclock("SR8",.t.)
					SR8->R8_FILIAL  := SRA->RA_FILIAL
					SR8->R8_SEQ     := cSeq
					SR8->R8_MAT     := SRA->RA_MAT 
					SR8->R8_DATA    := dDataBase
					SR8->R8_DATAINI := TOF->TOF_DTSLIC
					SR8->R8_DATAFIM := TOF->TOF_DTRLIC-1
					SR8->R8_TIPO    := "Q"
					SR8->R8_CONTINU := "2"
					If lCpoDura//A empresa dever pagar o salrio integralmente a funcionria
						SR8->R8_DURACAO := 120
						SR8->R8_DIASEMP := 120
						SR8->R8_DPAGAR  := 120
					Endif
					MsUnlock("SR8")
					
					//Monta email comunicando afastamento se inclusao
					If nOpcx == 3
						fMontaMail("018")
					Endif
				Endif
			Endif 
		Endif
	Endif
Endif

Return .T.
/*


ͻ
Programa  f920VLAF  Autor  Roger Rodrigues      Data   28/05/10   
͹
Desc.     Verifica se existe afastamento no perodo informado         
                                                                      
͹
Uso       MDTA920                                                     
ͼ


*/
Static Function f920VLAF(nOpcx)
Local lRet := .T.

If nOpcx == 5
	If nRecSR8 <> 0
		Return MsgYesNo(STR0028; //"Foi registrado afastamento referente a este registro de Licena Maternidade."
					 	 +STR0029, STR0030) //"Confirma a excluso do Afastamento e do registro de Licena Maternidade?"###"Ateno"
	Endif
ElseIf nOpcx == 3 .or. nOpcx == 4
	If lIntGpe
		dbSelectArea("TM0")
		dbSetOrder(1)
		dbSeek(xFilial("TM0")+M->TOF_NUMFIC)
		dbSelectArea("SRA")
		dbSetOrder(1)
		dbSeek(xFilial("SRA",TM0->TM0_FILFUN)+TM0->TM0_MAT)
		dbSelectArea("SR8")
		dbSetOrder(1)
		dbSeek(xFilial("SRA",TM0->TM0_FILFUN)+SRA->RA_MAT)
		While !eof() .and. SR8->R8_FILIAL+SR8->R8_MAT == xFilial("SRA",TM0->TM0_FILFUN)+SRA->RA_MAT
			If (nOpcx <> 3) .and. (nRecSR8 <> 0 .and. SR8->(Recno()) == nRecSR8)
				dbSelectArea("SR8")
				dbSkip()
				Loop
			Endif
			If M->TOF_DTRLIC < SR8->R8_DATAINI
				Exit
			Endif
			If M->TOF_DTSLIC >= SR8->R8_DATAINI .and. (M->TOF_DTSLIC <= SR8->R8_DATAFIM .OR. Empty(SR8->R8_DATAFIM))
				lRet := .F.
			Endif
			If M->TOF_DTRLIC >= SR8->R8_DATAINI .AND. (M->TOF_DTRLIC <= SR8->R8_DATAFIM .OR. Empty(SR8->R8_DATAFIM))
				lRet := .F.
			Endif
			If SR8->R8_DATAINI > M->TOF_DTSLIC .AND. SR8->R8_DATAFIM < M->TOF_DTRLIC
				lRet := .F.
			Endif
			If !lRet
				ShowHelpDlg(STR0030,{STR0031},2) //"Ateno"###"J existe Afastamento cadastrado no perodo desejado."
				Exit
			Endif
			dbSelectArea("SR8")
			dbSkip()
		End
	Endif
Endif

Return lRet

/*---------------------------------------------------------------------
{Protheus.doc} MDT920DATA
Cadastro de Certificado de Aprovao de Instalao

@return Nil

@sample MDT920DATA()

@author Bruno Lobo de Souza
@since 19/12/2013
@version 1.0
---------------------------------------------------------------------*/
Function MDT920DATA(dDtMestrua,dDtParto,dDtSaida,cCampo)

Local lRet := .T.
Local cMat := Posicione("TM0",1,xFilial("TM0")+M->TOF_NUMFIC,"TM0_MAT")
Local dDtAdmis := Posicione("SRA",1,xFilial("SRA")+cMat,"RA_ADMISSA")

If cCampo == "TOF_DTULME"
	If !Empty(dDtMestrua) .And. dDtMestrua < dDtAdmis
		MsgStop(STR0036,STR0030) //"A esta data no pode ser menor que a data de admisso do funcionrio."
		lRet := .F.
	EndIf 
	If !Empty(dDtMestrua) .And. !Empty(dDtParto) .And. dDtMestrua >= dDtParto .And. lRet
		MsgStop(STR0032,STR0030) //"Ateno"###"A data da ultima menstruao deve ser menor que a data do parto."
		lRet := .F.
	ElseIf lRet
		lRet := EXISTCHAV("TOF",M->TOF_NUMFIC+DTOS(dDtMestrua))
	EndIf
EndIf

If cCampo == "TOF_DTPART"
	If !Empty(dDtParto) .And. dDtParto < dDtAdmis
		MsgStop(STR0036,STR0030)
		lRet := .F.
	EndIf 
	If !Empty(dDtMestrua) .And. !Empty(dDtParto) .And. dDtMestrua >= dDtParto .And. lRet
		MsgStop(STR0033,STR0030) //"Ateno"###"A data do parto deve ser maior que a data da ultima menstruao."
		lRet := .F.
	Endif
	If !Empty(dDtParto) .And. !Empty(dDtSaida) .And. dDtSaida > dDtParto .And. lRet
		MsgStop(STR0034,STR0030) //"Ateno"###"A data do parto deve ser maior que a data de saida para licena maternidade."
		lRet := .F.
	Endif
EndIf

If cCampo == "TOF_DTSLIC"
	If !Empty(dDtSaida) .And. dDtSaida < dDtAdmis
		MsgStop(STR0036,STR0030)   
		lRet := .F.
	EndIf 
	If Empty(dDtSaida) .And. lRet //Verifica se est vazio
		lRet := .F.
	ElseIf !Empty(dDtParto) .And. dDtSaida > dDtParto .And. lRet
		MsgStop(STR0035,STR0030) //"Ateno"###"A data de saida para licenca maternidade no pode ser maior que a data do parto."
		lRet := .F.
	Endif
	If lRet
		M->TOF_DTRLIC := dDtSaida+120 //Data de Retorno da Licena Maternidade: 120 dias apos a data de saida
		lRefresh := .T.
	EndIf
EndIf

Return lRet