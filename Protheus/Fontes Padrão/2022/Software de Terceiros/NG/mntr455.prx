#INCLUDE "MNTR455.CH"
#INCLUDE "PROTHEUS.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR455
Analise de durabilidade por marca de pneu     
@author Deivys Joenck 
@since 11/01/01
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTR455()
	
	Local oReport 
	Local aArea := GetArea()

	//Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
	Local aOldMenu
	Local aNGCAD02 := {}
	
	Private asMenu 

	bKeyF9  := SetKey(VK_F9)          
	bKeyF10 := SetKey(VK_F10)
	bKeyF11 := SetKey(VK_F11)
	bKeyF12 := SetKey(VK_F12)
	SetKey( VK_F9, { | | NGVersao( "MNTR455" , 2 ) } )
	SETKEY(VK_F10,Nil)
	SETKEY(VK_F11,Nil)
	SETKEY(VK_F12,Nil)

	aOldMenu := ACLONE(asMenu)
	asMenu   := NGRIGHTCLICK("MNTR455")

	aNGCAD02:={;
		If(Type("aCHOICE"  ) == "A",ACLONE(aCHOICE) ,{}),;
		If(Type("aVARNAO"  ) == "A",ACLONE(aVARNAO) ,{}),;
		If(Type("aGETNAO"  ) == "A",ACLONE(aGETNAO) ,{}),;
		If(Type("cGETWHILE") == "C",cGETWHILE,NIL)	    ,;
		If(Type("cGETMAKE" ) == "C",cGETMAKE ,NIL)	    ,;
		If(Type("cGETKEY"  ) == "C",cGETKEY  ,NIL)	    ,;
		If(Type("cGETALIAS") == "C",cGETALIAS,NIL)	    ,;
		If(Type("cTUDOOK"  ) == "C",cTUDOOK  ,NIL)	    ,;
		If(Type("cLINOK"   ) == "C",cLINOK   ,NIL)	    ,;
		If(Type("aRELAC"   ) == "A",ACLONE(aRELAC)  ,{}),;
		If(Type("aCHKDEL"  ) == "A",ACLONE(aCHKDEL) ,{}),;
		If(Type("bngGRAVA" ) == "A",ACLONE(bngGRAVA),{}),;
		If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}

	Private vVETHORAS := {}

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetPortrait() //Default Retrato  
		oReport:PrintDialog()
	Else
		MNTR455R3()
	EndIf

	//Devolve variaveis armazenadas (NGRIGHTCLICK)
	SETKEY(VK_F9,bKeyF9)
	SETKEY(VK_F10,bKeyF10)
	SETKEY(VK_F11,bKeyF11)
	SETKEY(VK_F12,bKeyF12)

	asMenu  := ACLONE(aOldMenu)
	aCHOICE := ACLONE(aNGCAD02[1])
	aVARNAO := ACLONE(aNGCAD02[2])
	AGETNAO := ACLONE(aNGCAD02[3])
	
	If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
	If(aNGCAD02[5] != NIL,cGETMAKE  := aNGCAD02[5],)
	If(aNGCAD02[6] != NIL,cGETKEY   := aNGCAD02[6],)
	If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
	If(aNGCAD02[8] != NIL,cTUDOOK   := aNGCAD02[8],)
	If(aNGCAD02[9] != NIL,cLINOK    := aNGCAD02[9],)
	
	aRELAC    := ACLONE(aNGCAD02[10])
	aCHKDEL   := ACLONE(aNGCAD02[11])
	bngGRAVA  := ACLONE(aNGCAD02[12])
	aNGBUTTON := ACLONE(aNGCAD02[13])

	RestArea(aArea)

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR455R3
Analise de durabilidade por marca de pneu
@author Ricardo Dal Ponte
@since 30/01/07
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTR455R3()
	
	Local WNREL      := "MNTR455"
	Local LIMITE     := 132 
	Local cDESC1     := STR0001 //"Relatorio de analise de duracao de Bens por marca de pneu."
	Local cDESC2     := " "
	Local cDESC3     := " "
	Local cSTRING    := "ST9"

	Private NOMEPROG := "MNTR455"
	Private TAMANHO  := "M"
	Private aRETURN  := {STR0003 , 1,STR0004, 2, 2, 1, "",1 }  //"Zebrado"###"Administracao"
	Private TITULO   := cDESC1
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private cPERG    := "MNT455"
	Private CABEC1
	Private CABEC2

	//+--------------------------------------------------------------+
	//| Variaveis utilizadas para parametros                         |
	//| mv_par01     | De  Familia de Bem                            |
	//| mv_par02     | Ate Familia de Bem                            |
	//| mv_par03     | De  Fabricante                                |
	//| mv_par04     | Ate Fabricante                                |
	//| mv_par05     | De  Modelo                                    |
	//| mv_par06     | Ate Modelo                                    |
	//| mv_par07     | De  Bem                                       |
	//| mv_par08     | At‚ Bem                                       |
	//| mv_par09     | Data In¡cio                                   |
	//| mv_par10     | Data Fim                                      |
	//| mv_par11     | Considera inativo                             |
	//| mv_par12     | Considera Transferencias                      |
	//| mv_par13     | Tipo de Custo (1-Medio,2-Standard)            |
	//+--------------------------------------------------------------+

	cPERG    := "MNT455"
                  
	Pergunte(cPERG,.F.)

	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")

	If nLASTKEY == 27
		Set Filter To
		dbSelectArea("ST9")
		Return
	EndIf
	
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEnd| R455Imp(@lEnd,wnRel,titulo,tamanho)},titulo)
	DbSelectArea("ST9")

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} R455IMP
Chamada do Relatório  
@author Deivys Joenck   
@since 11/01/2001
@version undefined
@param lEND, logical, descricao
@param WNREL, , descricao
@param TITULO, , descricao
@param TAMANHO, , descricao
@type function
/*/
//---------------------------------------------------------------------
Function R455IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT := ""
	Local nCNTIMPR := 0
	Local CONTADOR := 0
	//Variaveis para controle do cursor de progressao do relatorio 
	Local nTOTREGS := 0 
	Local nMULT    := 1 
	Local nPOSANT  := 4 
	Local nPOSATU  := 4 
	Local nPOSCNT  := 0
	//Variaveis locais exclusivas deste programa                   
	Local cCHAVE     := Space(16)
	Local lCONTINUA  := .T.
	Local lPrint     := .T.
	//Tabela temporaria 
	Local oTmpTbl1		
	//Variaveis tipo Private padrao de todos os relatorios        
	Private  cFICANT := Space(9)
	//Contadores de linha e pagina                                 
	Private li    := 80 
	Private m_pag := 1
	//Alias da Tabela
	Private cTRB	 := GetNextAlias()
	
	//Verifica se deve comprimir ou nao                            
	nTIPO  := IIF(aRETURN[4]==1,15,18)     
	//Monta os Cabecalhos                                         
	CABEC1 := STR0018 //"Bem               Evento      Descricao                   Data Ini.    Data Fim    Cont.Inic.    Cont.Fim         Custo   Vida Util"
	CABEC2 := " "

	/*/
	*************************************************************************************************************************************        
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	*************************************************************************************************************************************
	Bem               Evento      Descricao                   Data Ini.    Data Fim    Cont.Inic.    Cont.Fim           Custo   Vida Util
	*************************************************************************************************************************************

	Fam¡lia - xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Fabricante - xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                                                                  
	Modelo  - xxxxxxxxxx

	xxxxxxxxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx        xx/xx/xx     xx/xx/xx     xxxxxxxxx   xxxxxxxxx      xxx.xxx,xx   x
	xxxxxxxxxxxxxxxx  xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx        xx/xx/xx                                         xxx.xxx.xxx,xx
	PN492             COMPRA                                  01/01/00                                                              ****INFORMACAO DO ST9
	TRANSFER.   PARA FILIAL 03              01/01/00     11/12/01         1.000       1.000            0.00       ****INFORMACAO DO TQ2
	TRANSFER.   DA FILIAL 03                02/01/00     11/12/01       	 1.000       1.000            0.00       ****INFORMACAO DO TQ2
	RODIZIO     CA002\DD                    02/01/00     20/12/01         1.000                                   ****INFORMACAO DO STZ
	CORRETIVA   LUBRIF\01\000001            02/03/01     04/03/01         1.900       2.000          200.00   *   ****INFORMACAO DO STS OU STJ                                                                  
	PRODUCAO    BURACO                      06/04/01     05/06/01         2.200       2.300                       ****INFORMACAO DO STY
	PREVENTIVA  PREVEN\01\000005            07/08/01     10/08/01         1.900       2.000          200.00   *   ****INFORMACAO DO STS OU STJ                                                                  
	PRODUCAO    BURACO                      06/10/01     25/10/01         2.200       2.300                       ****INFORMACAO DO STY
	RODIZIO     CA002\ID                    02/01/01     20/12/01         2.200                                   ****INFORMACAO DO STZ
	CORRETIVA   LUBRIF\01\000020            02/03/02                      2.300       2.400          300.00   *   ****INFORMACAO DO STS OU STJ                                                                  
	INATIVO     0004\FALTA DE TECNICO       10/06/02                                                              ****INFORMACAO DO ST9                                                                  

	PN493             COMPRA                                  01/01/00                                                              ****INFORMACAO DO ST9
	RODIZIO     CA001\DD                    02/01/00     20/12/01         1.000                                   ****INFORMACAO DO STZ
	CORRETIVA   LUBRIF\01\000025            02/03/01     04/03/01         1.900       2.000          200.00   *   ****INFORMACAO DO STS OU STJ                                                                  
	PRODUCAO    BURACO                                                                                                                                                                                       
	/*/  

	//Processa arquivo
	aDBF :={{"FAMIBEM" , "C", 06,0},;
			{"FABRIC"  , "C", 06,0},;
			{"MODELO"  , "C", 10,0},; 
			{"CODBEM"  , "C", 16,0},;
			{"ORDEM"   , "C", 06,0},;
			{"BEMPAI"  , "C", 16,0},;
			{"LOCALI"  , "C", 06,0},;
			{"PRODUC"  , "C", 15,0},;
			{"DTINIC"  , "D", 08,0},;  
			{"DTFIM"   , "D", 08,0},;  
			{"CONTINI" , "N", 09,0},;
			{"CONTFIM" , "N", 09,0},;
			{"CUSTO"   , "N", 09,2},;
			{"SERVICO" , "C", 06,0},;
			{"SEQREL"  , "C", 03,0},;
			{"FILORI"  , "C", 02,0},;
			{"HORATR"  , "C", 05,0},;
			{"FILDES"  , "C", 02,0},;
			{"TIPARQ"  , "C", 03,0}}         

	//Intancia classe FWTemporaryTable
	oTmpTbl1  := FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices
	oTmpTbl1:AddIndex( "Ind01" , {"FAMIBEM","FABRIC","MODELO","CODBEM","DTINIC","DTFIM","CONTINI"}  )
	//Cria a tabela temporaria
	oTmpTbl1:Create()

	Processa({|lEND| MNTR455ST9()},STR0030) //"Processando Arquivo..."

	lPri := .T.  
	DbSelectArea(cTRB)
	DbGoTop()  
	SetRegua(LastRec())
	While !Eof()
		cFAMIBEM  := (cTRB)->FAMIBEM
		cFABRICA  := (cTRB)->FABRIC 
		
		If !Empty(cFAMIBEM)
			If lPri
				NgSomali(58)
				NgSomali(58)                   
				lPri := .F.
			Else
				NgSomali(58)
				NgSomali(58)
				NgSomali(58)
			EndIf   
			@ Li,000 Psay STR0020 //"Familia -"
			@ Li,010 Psay cFAMIBEM
			@ Li,018 Psay NGSEEK('ST6',cFAMIBEM,1,'T6_NOME')

			DbSelectArea(cTRB) 
			@ Li,061 Psay STR0021 //"Fabricante -"
			@ Li,074 Psay (cTRB)->FABRIC
			@ Li,082 Psay NGSEEK('ST7',(cTRB)->FABRIC,1,'T7_NOME')
		EndIf  
		
		DbSelectArea(cTRB)
		While !Eof() .And. (cTRB)->FAMIBEM == cFAMIBEM .And. (cTRB)->FABRIC == cFABRICA 
			cMODELO := (cTRB)->MODELO
			
			NgSomali(58)	
			@ Li,000 Psay STR0022  //"Modelo -" 
			@ Li,010 Psay (cTRB)->MODELO	

			DbSelectArea(cTRB)
			While !Eof() .And. (cTRB)->FAMIBEM == cFAMIBEM   .And.;
			(cTRB)->FABRIC == cFABRICA                       .And.;
			(cTRB)->MODELO == cMODELO

				cCODBEM := (cTRB)->CODBEM
				cKeyTQ2 := ""

				NgSomaLi(58)
				NgSomaLi(58)
				dbSelectArea("TQ2")
				dbSetOrder(2)
				dbSeek(xFilial("TQ2")+cCODBEM+xFilial("ST9"),.T.)
				
				While (TQ2->TQ2_FILIAL == xFILIAL("TQ2") .And. TQ2->TQ2_CODBEM == cCODBEM .And. TQ2->TQ2_FILDES == xFILIAL("ST9") .And. DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR <= DTOS((cTRB)->DTINIC)+(cTRB)->HORATR)
					cKeyTQ2 := TQ2->TQ2_CODBEM+TQ2->TQ2_FILDES+DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR
					dbSkip()
				EndDo

				If !Empty(cKeyTQ2)
					dbSkip(-1)
					@ Li,000 Psay TQ2->TQ2_CODBEM
					@ Li,018 Psay STR0046 				  //"Transfer.
					@ Li,030 Psay STR0047+TQ2->TQ2_FILORI //"da Filial 
					@ Li,058 Psay TQ2->TQ2_DATATR Picture '99/99/99' 
					@ Li,071 Psay TQ2->TQ2_DATATR Picture '99/99/99'
					@ Li,086 Psay TQ2->TQ2_POSCON Picture '@E 999,999'  
					@ Li,098 Psay TQ2->TQ2_POSCON Picture '@E 999,999' 
					@ Li,107 Psay 0 Picture '@E 999,999,999.99'
				Else
					@ Li,000 Psay (cTRB)->CODBEM
					@ Li,018 Psay STR0024 				  //"Compra"
					DbSelectArea("ST9")
					DbSetOrder(1)
					DbSeek(xFILIAL("ST9")+cCODBEM)
					@ Li,058 Psay ST9->T9_DTCOMPR Picture '99/99/99'
					@ Li,107 Psay ST9->T9_VALCPA Picture '@E 999,999,999.99'
				EndIf

				DbSelectArea(cTRB)
				While !Eof() .And. (cTRB)->FAMIBEM == cFAMIBEM   .And.;
				(cTRB)->FABRIC == cFABRICA                       .And.;
				(cTRB)->MODELO == cMODELO                        .And.;
				(cTRB)->CODBEM == cCODBEM                        

					IncRegua()          
					If lPrint
						NgSomaLi(58)
					EndIf

					lPrint := .T.

					If (cTRB)->TIPARQ = "STZ"
						@ Li,018 Psay STR0025     //"Rodizio"
						@ Li,030 Psay Alltrim((cTRB)->BEMPAI)+"\"+Alltrim((cTRB)->LOCALI) 
					ElseIf (cTRB)->TIPARQ = "STS" .Or. (cTRB)->TIPARQ = "STJ" 
						If Alltrim((cTRB)->SEQREL) <> "0"
							@ Li,018 Psay STR0026 //"Preventiva"
						Else
							@ Li,018 Psay STR0027 //"Corretiva"
						EndIf
						@ Li,030 Psay Alltrim((cTRB)->SERVICO)+"\"+Alltrim((cTRB)->SEQREL)+"\"+Alltrim((cTRB)->ORDEM)
					ElseIf (cTRB)->TIPARQ = "STY"
						@ Li,018 Psay STR0028     //"Producao"
						@ Li,030 Psay (cTRB)->PRODUC 
					ElseIf (cTRB)->TIPARQ = "TQ2" .And. cKeyTQ2 != (cTRB)->CODBEM+(cTRB)->FILDES+DTOS((cTRB)->DTINIC)+(cTRB)->HORATR
						@ Li,018 Psay STR0046     //"Transfer."
						@ Li,030 Psay If((cTRB)->FILORI == xFILIAL("ST9"),STR0048+AllTrim((cTRB)->FILDES),STR0047+AllTrim((cTRB)->FILORI)) //"para Filial "##"da Filial "
					Else
						lPrint := .F.
					EndIf

					If lPrint
						@ Li,058 Psay (cTRB)->DTINIC  Picture '99/99/99' 
						@ Li,071 Psay (cTRB)->DTFIM   Picture '99/99/99'
						@ Li,086 Psay (cTRB)->CONTINI Picture '@E 999,999'  
						@ Li,098 Psay (cTRB)->CONTFIM Picture '@E 999,999' 
						@ Li,107 Psay (cTRB)->CUSTO   Picture '@E 999,999,999.99'
						If (cTRB)->TIPARQ = "STS" .Or. (cTRB)->TIPARQ = "STJ"
							DbSelectArea("ST4")
							DbSetOrder(1)
							DbSeeK(xFILIAL("ST4")+(cTRB)->SERVICO)
							If ST4->T4_VIDAUTI == 'S' 
								@ Li,123 Psay "*" 
							EndIf
						EndIf      
					EndIf

					DbSelectArea(cTRB)
					DbSkip()
				End

				DbSelectArea("ST9")
				DbSetOrder(1)
				DbSeek(xFILIAL("ST9")+cCODBEM)
				If ST9->T9_SITBEM = "I"
					If lPrint
						NgSomaLi(58)
					EndIf
					@ Li,018 Psay STR0029 //"Inativo"
					DbSelectArea("TPJ")
					DbSetOrder(1)
					If DbSeek(xFILIAL("TPJ")+ST9->T9_MTBAIXA)
						@ Li,030 Psay ST9->T9_MTBAIXA+"\"+SubStr(TPJ->TPJ_DESMOT,1,20)
					EndIf
					@ Li,058 Psay ST9->T9_DTBAIXA Picture '99/99/99'
				EndIf

				DbSelectArea(cTRB)
				lPrint := .T.

			End
		End                
	End
	RODA(nCNTIMPR,cRODATXT,TAMANHO) 

	//+--------------------------------------------------------------+
	//| Devolve a condicao original do arquivo principal             |
	//+--------------------------------------------------------------+
	RetIndex("ST9") 
	RetIndex("STS")
	RetIndex("STJ")
	RetIndex("STL")
	RetIndex("STT")
	RetIndex("STZ")
	RetIndex("STY")
	RetIndex("TQ2")

	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf

	Ms_Flush()
	oTmpTbl1:Delete()//Deleta arquivo temporario 1

Return .T.   

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR455ST9
Processa os arquivos  
@author Elisangela Costa
@since 29/08/2003
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Static Function MNTR455ST9()

	cCONDST9 := 'ST9->T9_FABRICA >= MV_PAR03 .And. ST9->T9_FABRICA <= MV_PAR04 .And. '
	cCONDST9 := cCONDST9 + 'ST9->T9_MODELO >= MV_PAR05 .And. ST9->T9_MODELO <= MV_PAR06 .And.'
	cCONDST9 := cCONDST9 + 'ST9->T9_CODBEM >= MV_PAR07 .And. ST9->T9_CODBEM <= MV_PAR08'   

	cCONDSTJ := 'STJ->TJ_SITUACA = "L" .And. STJ->TJ_TERMINO = "S" .And.'
	cCONDSTJ := cCONDSTJ + '(STJ->TJ_DTORIGI >= MV_PAR09 .And. STJ->TJ_DTORIGI <= MV_PAR10)'   

	cCONDSTS := 'STS->TS_SITUACA = "L" .And. STS->TS_TERMINO = "S" .And.'
	cCONDSTS := cCONDSTS + '(STS->TS_DTORIGI >= MV_PAR09 .And. STS->TS_DTORIGI <= MV_PAR10)'   

	cFamilia := Space(6)
	cBem     := Space(16)
	
	DbSelectArea("ST9")
	DbSetOrder(4)
	DbSeek(xFILIAL("ST9")+MV_PAR01,.T.)
	ProcRegua(LastRec())
	
	While !Eof()                          .And. ;
	ST9->T9_FILIAL == xFILIAL('ST9')  .And. ;
	ST9->T9_CODFAMI <= MV_PAR02       
		IncProc()
		If &(cCONDST9)
			If ST9->T9_SITBEM == 'I'
				If MV_PAR11 == 1 
					DbSkip()
					Loop 
				EndIf
			EndIf

			//Transferencias entre Filiais
			If MV_PAR12 = 2
				DbSelectArea("TQ2")
				DbSetOrder(1)
				If DbSeek(xFILIAL("TQ2")+ST9->T9_CODBEM)
					While !Eof()                           .And.; 
					TQ2->TQ2_FILIAL == xFILIAL("TQ2")      .And.; 
					TQ2->TQ2_CODBEM == ST9->T9_CODBEM

						If !(TQ2->TQ2_FILORI == xFILIAL("ST9") .OR. TQ2->TQ2_FILDES == xFILIAL("ST9"))
							DbSkip()
							Loop
						EndIf

						If TQ2->TQ2_DATATR < MV_PAR09 .Or. TQ2->TQ2_DATATR > MV_PAR10
							DbSkip()
							Loop
						EndIf

						GRAVAR455(ST9->T9_CODFAMI,ST9->T9_FABRICA,ST9->T9_MODELO,ST9->T9_CODBEM, , , , ,TQ2->TQ2_DATATR,TQ2->TQ2_DATATR,;
						TQ2->TQ2_POSCON,TQ2->TQ2_POSCON, , , , TQ2->TQ2_FILORI, TQ2->TQ2_FILDES, TQ2->TQ2_HORATR, "TQ2") 

						DbSelectArea("TQ2")
						DbSkip()
					End
				EndIf
			EndIf

			//Movimentacao de Bens
			DbSelectArea("STZ")
			DbSetOrder(2)
			If DbSeek(xFILIAL("STZ")+ST9->T9_CODBEM)
				While !Eof()                           .And.; 
				STZ->TZ_FILIAL == xFILIAL("STZ")       .And.; 
				STZ->TZ_CODBEM == ST9->T9_CODBEM

					If STZ->TZ_DATAMOV < MV_PAR09 .Or. STZ->TZ_DATAMOV > MV_PAR10
						DbSkip()
						Loop
					EndIf                                      

					GRAVAR455(ST9->T9_CODFAMI,ST9->T9_FABRICA,ST9->T9_MODELO,ST9->T9_CODBEM, ,STZ->TZ_BEMPAI,STZ->TZ_LOCALIZ, ,;
					STZ->TZ_DATAMOV,STZ->TZ_DATASAI,STZ->TZ_POSCONT,STZ->TZ_CONTSAI, , , , , ,STZ->TZ_HORAENT,"STZ")                     

					DbSelectArea("STZ")                 
					DbSkip()
				End                      
			EndIf

			//Hist. de Retorno de Producao
			DbSelectArea("STY")
			DbSetOrder(2)             
			If DbSeek(xFILIAL("STY")+ST9->T9_CODBEM)
				While !Eof()                             .And.; 
				STY->TY_FILIAL  ==  xFILIAL('STZ')   .And.; 
				STY->TY_CODBEM  ==  ST9->T9_CODBEM

					If STY->TY_DATAINI < MV_PAR09 .OR. STY->TY_DATAINI > MV_PAR10
						DbSkip()
						Loop
					EndIf

					DbSelectArea("STR")
					DbSetOrder(1)
					If DbSeek(xFilial("STR")+ST9->T9_CODFAMI+cBem+STY->TY_PRODUTO)
						nPosFim := STR->TR_FATOR * STY->TY_QUANTI1 +  STY->TY_POSINI1 
					ElseIf DbSeek(xFilial("STR")+cFamilia+ST9->T9_CODBEM+STY->TY_PRODUTO)
						nPosFim := STR->TR_FATOR * STY->TY_QUANTI1 +  STY->TY_POSINI1   
					Else   
						nPosFim := STY->TY_POSINI1 + STY->TY_QUANTI1
					EndIf    

					GRAVAR455(ST9->T9_CODFAMI,ST9->T9_FABRICA,ST9->T9_MODELO,ST9->T9_CODBEM, , , ,STY->TY_PRODUTO,;
					STY->TY_DATAINI,STY->TY_DATAFIM, STY->TY_POSINI1 ,nPosFim, , , , , ,STY->TY_HORAINI,"STY")          

					DbSelectArea("STY")
					DbSkip()
				End
			EndIf       

			//Ordens de Servico de Manutencao
			DbSelectArea("STJ")
			DbSetOrder(2)
			If DbSeek(xFILIAL("STJ")+"B"+ST9->T9_CODBEM)
				While !Eof() .And. STJ->TJ_FILIAL == xFILIAL('STJ') .And.;
				STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM == ST9->T9_CODBEM
					If &(cCONDSTJ)
						nCUSTO := 0.00
						DbSelectArea("STL") 
						DbSetOrder(1)
						If DbSeek(xFILIAL("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO)
							While !Eof()                         .And.; 
							STL->TL_FILIAL == xFILIAL('STL') .And.; 
							STL->TL_ORDEM  == STJ->TJ_ORDEM  .And.;
							STL->TL_PLANO  == STJ->TJ_PLANO  

								If Alltrim(STL->TL_SEQRELA) <> "0"
									If MV_PAR13 = 2 //STANDARD 
										aVETCUST  := NGCUSTSTAN(STL->TL_CODIGO,STL->TL_TIPOREG)
										nVCUSTO   := aVETCUST[1]   //Custo standard 
										vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
										STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
										STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
										nQTDHORAS := vVETHORAS[1]
										nVCUSTO  := nVCUSTO * nQTDHORAS
									Else
										nVCUSTO  := STL->TL_CUSTO 
									EndIf
									nCUSTO += nVCUSTO
								EndIf     
								
								DbSelectArea("STL")
								DbSkip()
							End     
						EndIf 
						
						/*------------------------------------------------------------+
						| Procura a posição do contador inicial com bem + data + hora |
						+------------------------------------------------------------*/
						nCONT1INI := NGACUMEHIS( ST9->T9_CODBEM, STJ->TJ_DTMRINI, STJ->TJ_HOMRINI, 1, 'P' )[1]
						
						/*----------------------------------------------------------+
						| Procura a posição do contador final com bem + data + hora |
						+--------------------------------------------------------- */
						nCONT1FIM := NGACUMEHIS( ST9->T9_CODBEM, STJ->TJ_DTMRFIM, STJ->TJ_HOMRFIM, 1, 'E' )[1]         
						
						/*----------------------------------------------------+
						| Grava as informações na tabela temporária da rotina |
						+--------------------------------------------------- */
						GRAVAR455( ST9->T9_CODFAMI, ST9->T9_FABRICA, ST9->T9_MODELO, ST9->T9_CODBEM, STJ->TJ_ORDEM, , , , STJ->TJ_DTMRINI,;
							STJ->TJ_DTMRFIM, nCONT1INI, nCONT1FIM, nCUSTO, STJ->TJ_SERVICO, STJ->TJ_SEQRELA, , , STJ->TJ_HOMPINI, 'STJ' )

					EndIf
					
					DbSelectArea("STJ")
					DbSkip()

				End 
				  
			EndIf          

			//Hist. de Manutencao
			DbSelectArea("STS")
			DbSetOrder(2)
			If DbSeek(xFILIAL("STS")+"B"+ST9->T9_CODBEM)
				While !Eof() .And. STS->TS_FILIAL == xFILIAL('STS') .And.;
				STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM == ST9->T9_CODBEM
					If &(cCONDSTS)
						nCUSTO := 0.00
						DbSelectArea("STT") 
						DbSetOrder(1)
						
						If DbSeek(xFILIAL("STT")+STS->TS_ORDEM+STS->TS_PLANO)
							While !Eof()                          .And.; 
							STT->TT_FILIAL == xFILIAL('STT') 	  .And.; 
							STT->TT_ORDEM  == STS->TS_ORDEM  	  .And.;
							STT->TT_PLANO  == STS->TS_PLANO  
								
							If Alltrim(STT->TT_SEQRELA) <> "0"
		                        If MV_PAR13 = 2 //STANDARD 
		                           aVETCUST := NGCUSTSTAN(STT->TT_CODIGO,STT->TT_TIPOREG)
			                       nVCUSTO  := aVETCUST[1] //Custo standard 
								   vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
		 											       STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
		   												   STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
								   nQTDHORAS := vVETHORAS[1]
			                       nVCUSTO  := nVCUSTO * nQTDHORAS
			                    Else
			                       nVCUSTO  := STT->TT_CUSTO 
			                    EndIf
			                    nCUSTO += nVCUSTO
			                EndIf    

								DbSelectArea("STT")
								DbSkip()
							End     
						EndIf
						/*------------------------------------------------------------+
						| Procura a posição do contador inicial com bem + data + hora |
						+------------------------------------------------------------*/
						nCONT1INI := NGACUMEHIS( ST9->T9_CODBEM, STS->TS_DTMRINI, STS->TS_HOMRINI, 1, 'P' )[1]
						
						/*----------------------------------------------------------+
						| Procura a posição do contador final com bem + data + hora |
						+--------------------------------------------------------- */
						nCONT1FIM := NGACUMEHIS( ST9->T9_CODBEM, STS->TS_DTMRFIM, STS->TS_HOMRFIM, 1, 'E' )[1]         
						
						/*----------------------------------------------------+
						| Grava as informações na tabela temporária da rotina |
						+--------------------------------------------------- */
						GRAVAR455( ST9->T9_CODFAMI, ST9->T9_FABRICA, ST9->T9_MODELO, ST9->T9_CODBEM, STS->TS_ORDEM, , , , STS->TS_DTMRINI,;
							STS->TS_DTMRFIM, nCONT1INI, nCONT1FIM, nCUSTO, STS->TS_SERVICO, STS->TS_SEQRELA, , , STS->TS_HOMPINI, 'STS' )

					EndIf   

					DbSelectArea("STS")
					DbSkip()
				End   
			EndIf
		EndIf   
		DbSelectArea("ST9")
		DbSkip()
	End

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} GRAVAR455
Grava os campos no Arquivo de Trabalho 
@author Elisangela Costa 
@since 09/07/02
@version undefined
@param VFAMIL,   , descricao
@param VFABRIC,  , descricao
@param VMODELO,  , descricao
@param VCODBEM,  , descricao
@param VORDEM,   , descricao
@param VBEMPAI,  , descricao
@param VLOCAL,   , descricao
@param VPRODUC,  , descricao
@param VDATINI,  , descricao
@param VDATFIM,  , descricao
@param VCONTINI, , descricao
@param VCONTFIM, , descricao
@param VCUSTO,   , descricao
@param VSERV,    , descricao
@param VSEQREL,  , descricao
@param VFILORI,  , descricao
@param VFILDES,  , descricao
@param VHORA,    , descricao
@param VARQ,     , descricao
@type function
/*/
//---------------------------------------------------------------------
Function GRAVAR455(VFAMIL,VFABRIC,VMODELO,VCODBEM,VORDEM,VBEMPAI,VLOCAL,VPRODUC,VDATINI,VDATFIM,;
				   VCONTINI,VCONTFIM,VCUSTO,VSERV,VSEQREL,VFILORI,VFILDES,VHORA,VARQ)

	(cTRB)->(DbAppend())
	(cTRB)->FAMIBEM  := VFAMIL
	(cTRB)->FABRIC   := VFABRIC
	(cTRB)->MODELO   := VMODELO
	(cTRB)->CODBEM   := VCODBEM
	(cTRB)->ORDEM    := VORDEM
	(cTRB)->BEMPAI   := VBEMPAI    
	(cTRB)->LOCALI   := VLOCAL
	(cTRB)->PRODUC   := VPRODUC
	(cTRB)->DTINIC   := VDATINI
	(cTRB)->DTFIM    := VDATFIM
	(cTRB)->CONTINI  := VCONTINI 
	(cTRB)->CONTFIM  := VCONTFIM
	(cTRB)->CUSTO    := VCUSTO
	(cTRB)->SERVICO  := VSERV 
	(cTRB)->SEQREL   := VSEQREL
	(cTRB)->FILORI   := VFILORI
	(cTRB)->FILDES   := VFILDES
	(cTRB)->HORATR   := VHORA     //Utilizada apenas para ordenar transferencias/compra
	(cTRB)->TIPARQ   := VARQ

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} ReportDef
Define as secoes impressas no relatorio  
@author Elisangela Costa
@since 30/01/07
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Static Function ReportDef()
	
	Local oReport 
	Local oCell
	Local oSection1 
	Local oSection2 
	Local oSection3

	Private c1DET01   := ""
	Private c2DET01   := ""
	Private c3CODBEM  := ""
	Private c3EVENTO  := ""
	Private c3DESCRI  := ""
	Private c3DTINIC  := ""
	Private c3DTFIM   := ""
	Private c3CONTINI := ""
	Private c3CONTFIM := ""
	Private c3CUSTO   := ""
	Private c3ASTE    := ""

	//+-------------------------------------------------------------------------+
	//| Criacao do componente de impressao                                      |
	//|                                                                         |
	//| TReport():New                                                           |
	//| ExpC1 : Nome do relatorio                                               |
	//| ExpC2 : Titulo                                                          |
	//| ExpC3 : Pergunte                                                        |
	//| ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  |
	//| ExpC5 : Descricao                                                       |
	//|                                                                         |
	//+-------------------------------------------------------------------------+
	oReport := TReport():New("MNTR455",OemToAnsi(STR0001),"MNT455",{|oReport| ReportPrint(oReport)},STR0001)

	//+---------------------------------------------------------------+
	//|  Variaveis utilizadas para parametros                         |
	//|  mv_par01      - De  Familia de Bem                           |
	//|  mv_par02      - Ate Fam¡lia de Bem                           |
	//|  mv_par03      - De  Fabricante                               |
	//|  mv_par04      - Ate Fabricante                               |
	//|  mv_par05      - De  Modelo                                   |
	//|  mv_par06      - Ate Modelo                                   |
	//|  mv_par07      - De  Bem                                      |
	//|  mv_par08      - At‚ Bem                                      |
	//|  mv_par09      - Data In¡cio                                  |
	//|  mv_par10      - Data Fim                                     |
	//|  mv_par11      - Considera inativo                            |
	//|  mv_par12      - Considera Transferencias                     |
	//|  mv_par13      - Tipo de Custo (1-Medio,2-Standard)           |
	//+---------------------------------------------------------------+
	cPerg := "MNT455"

	Pergunte(oReport:uParam,.F.)

	//+------------------------------------------------------------------------+
	//| Criacao da secao utilizada pelo relatorio                              |
	//|                                                                        |
	//| TRSection():New                                                        |
	//| ExpO1 : Objeto TReport que a secao pertence                            |
	//| ExpC2 : Descricao da seçao                                             |
	//| ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela  |
	//|         sera considerada como principal para a seção.                  |
	//| ExpA4 : Array com as Ordens do relatório                               |
	//| ExpL5 : Carrega campos do SX3 como celulas                             |
	//|         Default : False                                                |
	//| ExpL6 : Carrega ordens do Sindex                                       |
	//|         Default : False                                                |
	//|                                                                        |
	//+------------------------------------------------------------------------+
	//+------------------------------------------------------------------------+
	//| Criacao da celulas da secao do relatorio                               |
	//|                                                                        |
	//| TRCell():New                                                           |
	//| ExpO1 : Objeto TSection que a secao pertence                           |
	//| ExpC2 : Nome da celula do relatório. O SX3 será consultado             |
	//| ExpC3 : Nome da tabela de referencia da celula                         |
	//| ExpC4 : Titulo da celula                                               |
	//|         Default : X3Titulo()                                           |
	//| ExpC5 : Picture                                                        |
	//|         Default : X3_PICTURE                                           |
	//| ExpC6 : Tamanho                                                        |
	//|         Default : X3_TAMANHO                                           |
	//| ExpL7 : Informe se o tamanho esta em pixel                             |
	//|         Default : False                                                |
	//| ExpB8 : Bloco de código para impressao.                                |
	//|         Default : ExpC2                                                |
	//|                                                                        |
	//+------------------------------------------------------------------------+

	oSection1 := TRSection():New(oReport,STR0041+"/"+STR0042,{"ST6", "ST7"})//"Familia"###"Fabricante"
	oCell := TRCell():New(oSection1,"c1DET01","","","@!",60,/*lPixel*/,{||c1DET01})
	TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6") + (cTRB)->FAMIBEM})
	TRPosition():New(oSection1,"ST7",1,{|| xFilial("ST7") + (cTRB)->FABRIC})

	oSection2 := TRSection():New(oReport,STR0043,{"ST4"})//"Modelo"    
	oCell := TRCell():New(oSection2,"c2DET01","","","@!",60,/*lPixel*/,{||c2DET01})
	TRPosition():New(oSection2,"ST4",1,{|| xFilial("ST4") + (cTRB)->SERVICO})

	oSection3 := TRSection():New(oReport,STR0033,{"ST9"})
	oCell := TRCell():New(oSection3,"c3CODBEM"  ,"",STR0032,"@!",16,/*lPixel*/,{||c3CODBEM})//"Bem"
	oCell := TRCell():New(oSection3,"c3EVENTO"  ,"",STR0033,"@!",11,/*lPixel*/,{||c3EVENTO})//"Evento"
	oCell := TRCell():New(oSection3,"c3DESCRI"  ,"",STR0034,"@!",27,/*lPixel*/,{||c3DESCRI})//"Descricao"
	oCell := TRCell():New(oSection3,"c3DTINIC"  ,"",STR0035,"99/99/9999",10,/*lPixel*/,{||c3DTINIC})//"Data Ini."
	oCell := TRCell():New(oSection3,"c3DTFIM"   ,"",STR0036,If(!Empty(c3DTFIM)  ,'99/99/9999', ''),10,/*lPixel*/,{||c3DTFIM})//"Data Fim"
	oCell := TRCell():New(oSection3,"c3CONTINI" ,"",STR0037,If(!Empty(c3CONTINI),'@E 999,999,999,999', ''),12,/*lPixel*/,{||c3CONTINI})//"Cont.Inic."
	oCell := TRCell():New(oSection3,"c3CONTFIM" ,"",STR0038,If(!Empty(c3CONTFIM),'@E 999,999,999,999', ''),12,/*lPixel*/,{||c3CONTFIM})//"Cont.Fim"
	oCell := TRCell():New(oSection3,"c3CUSTO"   ,"",STR0039,"@E 9,999,999,999.99",12,/*lPixel*/,{||c3CUSTO})//"Custo"
	oCell := TRCell():New(oSection3,"c3ASTE"    ,"",STR0040,"@!",9,/*lPixel*/,{||c3ASTE})//"Vida Util"
	oCell := TRPosition():New(oSection3,"ST9",1,{|| xFilial("ST9") + (cTRB)->CODBEM })

	oSection3:Cell("c3CONTINI"):SetHeaderAlign("RIGHT") 
	oSection3:Cell("c3CONTFIM"):SetHeaderAlign("RIGHT") 
	oSection3:Cell("c3CUSTO"):SetHeaderAlign("RIGHT")

Return oReport 

//---------------------------------------------------------------------
/*/{Protheus.doc} ReportPrint
Chamada do Relatório  
@author Elisangela Costa 
@since 21/06/06
@version undefined
@param oReport, object, descricao
@type function
/*/
//---------------------------------------------------------------------
Static Function ReportPrint(oReport)
	
	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local oSection3 := oReport:Section(3)
	Local oTmpTbl2  //Tabela Temporaria
	
	Private lPrint  := .T.
	Private cTRB    := GetNextAlias()
	Private aVETINR := {} 

	//Processa arquivo                          
	aDBF :={{"FAMIBEM" , "C", 06,0},;
			{"FABRIC"  , "C", 06,0},;
			{"MODELO"  , "C", 10,0},; 
			{"CODBEM"  , "C", 16,0},;
			{"ORDEM"   , "C", 06,0},;
			{"BEMPAI"  , "C", 16,0},;
			{"LOCALI"  , "C", 06,0},;
			{"PRODUC"  , "C", 15,0},;
			{"DTINIC"  , "D", 08,0},;  
			{"DTFIM"   , "D", 08,0},;  
			{"CONTINI" , "N", 09,0},;
			{"CONTFIM" , "N", 09,0},;
			{"CUSTO"   , "N", 09,2},;
			{"SERVICO" , "C", 06,0},;
			{"SEQREL"  , "C", 03,0},;
			{"FILORI"  , "C", 02,0},;
			{"HORATR"  , "C", 05,0},;
			{"FILDES"  , "C", 02,0},;
			{"TIPARQ"  , "C", 03,0}}         

	//Intancia classe FWTemporaryTable
	oTmpTbl2:= FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices
	oTmpTbl2:AddIndex( "Ind01" , {"FAMIBEM","FABRIC","MODELO","CODBEM","DTINIC","DTFIM","CONTINI"}  )
	//Cria a tabela temporaria
	oTmpTbl2:Create() 

	Processa({|lEND| MNTR455ST9()},STR0030) //"Processando Arquivo..."

	lPri := .T.
	Dbselectarea(cTRB)
	DbGoTop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()
		cFAMIBEM  := (cTRB)->FAMIBEM
		cFABRICA  := (cTRB)->FABRIC 

		If !Empty(cFAMIBEM)
			oSection1:Init()
			If lPri
				lPri := .F.
			Else
				oReport:SkipLine()
			EndIf   
			c1DET01:= Alltrim(STR0020+" "+cFAMIBEM +" "+NGSEEK('ST6',cFAMIBEM,1,'T6_NOME'))+Space(10)+;
			STR0021+" "+(cTRB)->FABRIC +" "+NGSEEK('ST7',(cTRB)->FABRIC,1,'T7_NOME')
			oSection1:PrintLine()
		EndIf  
		
		DbSelectArea(cTRB)
		While !Eof() .And. !oReport:Cancel();
					 .And. (cTRB)->FAMIBEM == cFAMIBEM .And. (cTRB)->FABRIC == cFABRICA 
			cMODELO  := (cTRB)->MODELO
			oReport:SkipLine()
			oSection2:Init()
			c2DET01  := STR0022+" "+(cTRB)->MODELO
			oSection2:PrintLine()

			DbSelectArea(cTRB)
			While !Eof() .And. !oReport:Cancel() .And.;
			(cTRB)->FAMIBEM == cFAMIBEM 		 .And.;
			(cTRB)->FABRIC  == cFABRICA  		 .And.;
			(cTRB)->MODELO  == cMODELO
				cCODBEM   := (cTRB)->CODBEM
				cKeyTQ2   := ""
				
				oSection3:Init()
				c3CODBEM  := ""
				c3EVENTO  := ""
				c3DESCRI  := ""
				c3DTINIC  := ""
				c3DTFIM   := ""
				c3CONTINI := ""
				c3CONTFIM := ""
				c3CUSTO   := ""
				c3ASTE    := ""

				oReport:SkipLine()

				dbSelectArea("TQ2")
				dbSetOrder(2)
				dbSeek(xFilial("TQ2")+cCODBEM+xFilial("ST9"),.T.)
				While (TQ2->TQ2_FILIAL == xFILIAL("TQ2") .And. TQ2->TQ2_CODBEM == cCODBEM .And. TQ2->TQ2_FILDES == xFILIAL("ST9") .And. DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR <= DTOS((cTRB)->DTINIC)+(cTRB)->HORATR)
					cKeyTQ2 := TQ2->TQ2_CODBEM+TQ2->TQ2_FILDES+DTOS(TQ2->TQ2_DATATR)+TQ2->TQ2_HORATR
					dbSkip()
				EndDo

				If !Empty(cKeyTQ2)
					dbSkip(-1)
					c3CODBEM := TQ2->TQ2_CODBEM
					c3EVENTO := STR0046 				//"Transfer."
					c3DESCRI := STR0047+TQ2->TQ2_FILORI //"da Filial "
					c3DTINIC := TQ2->TQ2_DATATR
					c3DTFIM  := TQ2->TQ2_DATATR
					c3CONTINI:= TQ2->TQ2_POSCON
					c3CONTFIM:= TQ2->TQ2_POSCON
					c3CUSTO  := 0
					c3ASTE   := ""
				Else
					c3CODBEM := (cTRB)->CODBEM
					c3EVENTO := STR0024 				//"Compra"
					DbSelectArea("ST9")
					DbSetOrder(1)
					DbSeek(xFILIAL("ST9")+cCODBEM)
					c3DTINIC := ST9->T9_DTCOMPR
					c3CUSTO  := ST9->T9_VALCPA
				EndIf

				oSection3:PrintLine()

				DbSelectArea(cTRB)
				While !Eof() .And. !oReport:Cancel() .And.;
				(cTRB)->FAMIBEM == cFAMIBEM          .And.;
				(cTRB)->FABRIC == cFABRICA           .And.;
				(cTRB)->MODELO == cMODELO            .And.;
				(cTRB)->CODBEM == cCODBEM                        

					oReport:IncMeter()

					c3CODBEM:= ""
					lPrint := .T.

					If (cTRB)->TIPARQ = "STZ"
						c3EVENTO:= STR0025        //"Rodizio"
						c3DESCRI:= Alltrim((cTRB)->BEMPAI)+"\"+Alltrim((cTRB)->LOCALI)                                                                                     
					ElseIf (cTRB)->TIPARQ = "STS" .or. (cTRB)->TIPARQ = "STJ" 
						If Alltrim((cTRB)->SEQREL) <> "0"
							c3EVENTO:= STR0026    //"Preventiva"
						Else
							c3EVENTO:= STR0027    //"Corretiva"
						EndIf
						c3DESCRI:= Alltrim((cTRB)->SERVICO)+"\"+Alltrim((cTRB)->SEQREL)+"\"+Alltrim((cTRB)->ORDEM)    
					ElseIf (cTRB)->TIPARQ = "STY"
						c3EVENTO:= STR0028        //"Producao"
						c3DESCRI:= (cTRB)->PRODUC 
					ElseIf (cTRB)->TIPARQ = "TQ2" .And. cKeyTQ2 != (cTRB)->CODBEM+(cTRB)->FILDES+DTOS((cTRB)->DTINIC)+(cTRB)->HORATR
						c3EVENTO:= "Transfer."
						c3DESCRI:= If((cTRB)->FILORI == xFILIAL("ST9"),"para Filial "+AllTrim((cTRB)->FILDES),"da Filial "+AllTrim((cTRB)->FILORI))
					Else
						lPrint := .F.
					EndIf

					If lPrint
						c3DTINIC := (cTRB)->DTINIC
						c3DTFIM  := (cTRB)->DTFIM
						c3CONTINI:= (cTRB)->CONTINI
						c3CONTFIM:= (cTRB)->CONTFIM
						c3CUSTO  := (cTRB)->CUSTO
						c3ASTE  := ""

						If c3EVENTO= STR0024
							c3DTFIM  := ""
							c3CONTINI:= ""
							c3CONTFIM:= ""
						EndIf

						If (cTRB)->TIPARQ = "STS" .Or. (cTRB)->TIPARQ = "STJ"
							DbSelectArea("ST4")
							DbSetOrder(1)
							DbSeeK(xFILIAL("ST4")+(cTRB)->SERVICO)
							If ST4->T4_VIDAUTI == 'S' 
								c3ASTE  := "*"
							EndIf
						EndIf      

						oSection3:PrintLine()
					EndIf

					DbSelectArea(cTRB)
					DbSkip()
				End

				DbSelectArea("ST9")
				DbSetOrder(1)
				DbSeek(xFILIAL("ST9")+cCODBEM)
				If ST9->T9_SITBEM = "I"
					c3DTINIC := ""
					c3DTFIM  := ""
					c3CONTINI:= 0
					c3CONTFIM:= 0
					c3CUSTO  := 0
					c3ASTE   := ""
					If !lPrint
						oReport:SkipLine()
					EndIf
					c3EVENTO:= STR0029 //"Inativo"
					DbSelectArea("TPJ")
					DbSetOrder(1)
					If DbSeek(xFILIAL("TPJ")+ST9->T9_MTBAIXA)
						c3DESCRI:= ST9->T9_MTBAIXA+"\"+SubStr(TPJ->TPJ_DESMOT,1,20)
					EndIf
					c3DTINIC := ST9->T9_DTBAIXA
					oSection3:PrintLine()
				EndIf

				DbSelectArea(cTRB)
				lPrint := .T.
			End
			oSection3:Finish()
		End                
		oSection2:Finish()
		oSection1:Finish()
	End

	//+--------------------------------------------------------+
	//| Devolve a condicao original do arquivo principal       |
	//+--------------------------------------------------------+
	RetIndex("ST9")
	RetIndex("STS")
	RetIndex("STJ")
	RetIndex("STL")
	RetIndex("STT")
	RetIndex("STZ")
	RetIndex("STY")
	RetIndex("TQ2")

	Set Filter To
	Set Device To Screen

	oTmpTbl2:Delete() //Deleta Arquivo temporario 2
	
Return .T.
