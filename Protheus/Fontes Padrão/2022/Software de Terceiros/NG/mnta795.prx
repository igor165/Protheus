#INCLUDE "MNTA795.ch"
#include "Protheus.ch"
#Include 'FWMVCDEF.CH'

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA795
Cadastro de Movimentacao dos Pagamentos Efetuados
@author  Vitor Bonet
@since   12/11/2018
@version P12
/*/
//-------------------------------------------------------------------
Function MNTA795()

Local aNGBEGINPRM := NGBEGINPRM() // Armazena as variáveis
Local oBrowse := FWMBrowse():New()

oBrowse:SetAlias("TSG") // Alias da tabela utilizada
oBrowse:SetMenuDef("MNTA795") // Nome do fonte onde esta a função MenuDef
oBrowse:SetDescription(STR0006) // "Cadastro de Histórico de Ações no Processo de Multa"
oBrowse:Activate()

NGRETURNPRM(aNGBEGINPRM) // Devolve as variáveis armazenadas

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Opções de menu padrão.

@author Vitor Bonet
@since 12/11/2018
@version P12
@return aRotina - Estrutura
	[n,1] Nome a aparecer no cabecalho
	[n,2] Nome da Rotina associada
	[n,3] Reservado
	[n,4] Tipo de Transação a ser efetuada:
		1 - Pesquisa e Posiciona em um Banco de Dados
		2 - Simplesmente Mostra os Campos
		3 - Inclui registros no Bancos de Dados
		4 - Altera o registro corrente
		5 - Remove o registro corrente do Banco de Dados
		6 - Alteração sem inclusão de registros
		7 - Cópia
		8 - Imprimir
	[n,5] Nivel de acesso
	[n,6] Habilita Menu Funcional
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

	Local aRotina := {}

	aAdd( aRotina, { STR0001, 'AxPesqui', 0, 1, 0, NIL } ) // Pesquisar
	aAdd( aRotina, { STR0002, 'VIEWDEF.MNTA795', 0, 2, 0, NIL } ) // Visualizar
	aAdd( aRotina, { STR0003, 'VIEWDEF.MNTA795', 0, 3, 0, NIL } ) // Incluir
	aAdd( aRotina, { STR0004, 'VIEWDEF.MNTA795', 0, 4, 0, NIL } ) // Alterar
	aAdd( aRotina, { STR0005, 'VIEWDEF.MNTA795', 0, 5, 0, NIL } ) // Excluir

Return aRotina

//---------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author Vitor Bonet
@since 12/11/2018
@version P12

@return oModel, Objeto, Modelo de dados (MVC)
/*/
//---------------------------------------------------------------------
Static Function ModelDef()
	// Cria a estrutura a ser usada no Modelo de Dados
	Local oStructTSG := FWFormStruct( 1, 'TSG', /*bAvalCampo*/,/*lViewUsado*/ )
	Local oModel

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTA795", /*bPreValid*/, /*bPosValid*/, /*bFormCommit*/, /*bFormCancel*/)

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields('MNTA795_TSG', /*cOwner*/, oStructTSG, /*bPreValidacao*/, /*bPosValidacao*/, /*bCarga*/)

	// Adiciona a descricao do Modelo de Dados
	oModel:SetDescription(STR0006) // "Cadastro de Movimentação dos Pagamentos Efetuados"

	// Adiciona a descricao do Componente do Modelo de Dados
	oModel:GetModel('MNTA795_TSG'):SetDescription(STR0006) // "Cadastro de Movimentação dos Pagamentos Efetuados"

Return oModel

//---------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Definição da View (padrão MVC).

@author Vitor Bonet
@since 12/11/2018
@version P12

@return oView,  Objeto, Objeto da View MVC
/*/
//---------------------------------------------------------------------
Static Function ViewDef()

	// Cria um objeto de Modelo de dados baseado no ModelDef() do fonte informado
	Local oModel := FWLoadModel("MNTA795")

	// Cria a estrutura a ser usada na View
	Local oStruTSG := FWFormStruct(2, "TSG", /*bAvalCampo*/, /*lViewUsado*/)

	// Interface de visualização construída
	Local oView

	// Cria o objeto de View
	oView := FWFormView():New()

	// Define qual o Modelo de dados será utilizado na View
	oView:SetModel(oModel)

	// Adiciona no View um controle do tipo formulário (antiga Enchoice)
	oView:AddField("VIEW_TSG"/*cFormModelID*/, oStruTSG/*oViewStruct*/, "MNTA795_TSG")

	// Cria os componentes "box" horizontais para receberem elementos da View
	oView:CreateHorizontalBox("BOX_TSG"/*cID*/, 100)

	// Relaciona o identificador (ID) da View com o "box" para exibição
	oView:SetOwnerView("VIEW_TSG"/*cFormModelID*/, "BOX_TSG")
	
	//Inclusão de itens nas Ações Relacionadas de acordo com O NGRightClick
	NGMVCUserBtn(oView)

Return oView

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA795MOV
Limpa o campo de Destino Val. quando TipoMov = 1
@author  Marcos Wagner Junior
@since   23/02/07
@version p12
/*/
//-------------------------------------------------------------------
Function MNTA795MOV()

	Local oModel := FWModelActive()  // Copia o Model utilizado.

	If M->TSG_TIPOMO = '1'
		oModel:LoadValue('MNTA795_TSG', 'TSG_DESVAL', Space(Len(TSG->TSG_DESVAL)))
	Endif

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA795VAL
Calcula o Valor Pago
@author  Marcos Wagner Junior
@since   23/02/07
@version p12
/*/
//-------------------------------------------------------------------
Function MNTA795VAL(nOpc)

	Local oModel := FWModelActive()  // Copia o Model utilizado.

	If nOpc == 1
		nValorBol := oModel:GetValue("MNTA795_TSG","TSG_VALORI")
	Else
		aOldArea := GetArea()
		dbSelectArea("TRX")
		dbSetOrder(01)
		dbSeek(xFilial("TRX")+ oModel:GetValue("MNTA795_TSG","TSG_MULTA"))
		nValorBol := TRX->TRX_VALOR
		RestArea(aOldArea)
	Endif

	oModel:LoadValue('MNTA795_TSG', 'TSG_VALPGO', nValorBol - (nValorBol * ( oModel:GetValue("MNTA795_TSG","TSG_DESCON") /100)) )

Return .T.
