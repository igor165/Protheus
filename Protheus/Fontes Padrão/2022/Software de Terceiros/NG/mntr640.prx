#INCLUDE "mntr640.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 2 //Versao do fonte

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR640  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio Onde-Se-Usa Terceiros                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR640

	//---------------------------------------------------------------------
	// Guarda conteudo e declara variaveis padroes
	//---------------------------------------------------------------------
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

	Local cString		:= "STG"
	Local wnrel			:= "MNTR640"
	Local cDesc1		:= STR0001 //"Relatorio das manutencoes em que o terceiro selecionado e planeja-"
	Local cDesc2		:= STR0002 //"do para utilizacao. Atraves da opcao parametros o usuario podera fazer "
	Local cDesc3		:= STR0003 //"selecao por Centro de Custo e Familia de Bens"

	Private Tamanho		:= "M"
	Private aReturn		:= { STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey	:= 0
	Private cPerg		:= "MNT640"
	Private titulo		:= STR0006 //"Onde-Se-Usa Terceiros"
	Private ntipo		:= 0
	Private nomeprog	:= "MNTR640"
	Private cabec1,cabec2

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     Terceiros  a procurar                           ³
	//³ mv_par02     De  Centro de Custos                            ³
	//³ mv_par03     Ate Centro de Custos                            ³
	//³ mv_par04     De  Familia de Bens                             ³
	//³ mv_par05     Ate Familia de Bens                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte(cPerg,.F.)

	//---------------------------------------------------------------------
	// Envia controle para a funcao SETPRINT                        
	//---------------------------------------------------------------------
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

	If nLastKey = 27
		Set Filter To
		Return
	EndIf

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R640Imp(@lEnd,wnRel,titulo,tamanho)},titulo)   

	//---------------------------------------------------------------------
	// Retorna conteudo de variaveis padroes
	//---------------------------------------------------------------------
	NGRETURNPRM(aNGBEGINPRM)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R640Imp  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR640                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R640Imp(lEnd,wnRel,titulo,tamanho)

	Local aINDR640	 := {} //Array p/ Indice Tabela Temporária
	Local cRodaTxt	 := ""
	Local cChave	 := Space(TAMSX3("T9_CODBEM")[1])
	Local cTRB640	 := GetNextAlias() //Alias Tabela Temporária
	Local cCCUSTO
	Local cCENTRAB
	Local cCODFAMI
	Local nCntImpr	 := 0
	Local oARQTR640 //Objeto Tabela Temporária
	
	//---------------------------------------------------------------------
	// Contadores de linha e pagina
	//---------------------------------------------------------------------
	Private li := 80 ,m_pag := 1

	//---------------------------------------------------------------------
	// Verifica se deve comprimir ou nao
	//---------------------------------------------------------------------
	nTipo := IIF(aReturn[4]==1,15,18)

	//---------------------------------------------------------------------
	// Monta os Cabecalhos
	//---------------------------------------------------------------------
	cabec1 := STR0007 //"Bem              Nome do Bem                          Servic Nome                                 Sequencia      Consumo    Unidade"
	cabec2 := STR0008 //"Bem Pai          Descrição                      Tarefa Descricao                            C. Custo             C.Trab     Familia"

	/*/                                                                                                  1         1         1         1
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	______________________________________________________________________________________________________________________________________
	Bem              Descrição                         Serviço Nome                          Sequencia          Consumo         Unidade
	Bem Pai          Descrição                      Tarefa Descricao                            C. Custo             C.Trab     Familia
	_____________________________________________________________________________________________________________________________________
	Especialidade.:  xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	**************************************************************************************************************************************

	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999         xxx    xx:xx   xxx     xxx SUBST
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxx     xxxxxx
	*/

	aDBFR640 := {{"CODBEM" , "C", 16, 0},;
				 {"SERVICO", "C", 06, 0},;
				 {"SEQRELA", "C", 03, 0},;
				 {"SEQUEN" , "C", 03, 0},;
				 {"TAREFA" , "C", 06, 0},;
				 {"CODIGO" , "C", 15, 0},;
				 {"QUANTID", "N", 09, 0},;
				 {"QUANREC", "N", 03, 0},;
				 {"RESERVA", "C", 01, 0},; 
				 {"UNIDADE", "C", 02, 0}}   

	//Indice Tabela Temporária
	aINDR640  := {{"CODIGO","CODBEM","SERVICO","SEQRELA","SEQUEN","TAREFA"}}
	
	//Criação Tabela Temporária
	oARQTR640 := NGFwTmpTbl(cTRB640, aDBFR640, aINDR640) 

	DbSelectArea("STG")
	DbSetOrder(02)
	DbSeek(xFilial("STG")+"T"+MV_PAR01)
	SetRegua(LastRec())
	While !Eof() .And. STG->TG_FILIAL = Xfilial("STG") .And.;
	STG->TG_TIPOREG = "T" .And. Substr(STG->TG_CODIGO,1,TAMSX3("A2_COD")[1]) = MV_PAR01

		dbSelectArea(cTRB640)
		RecLock(cTRB640,.T.) 
		(cTRB640)->CODBEM	:= STG->TG_CODBEM
		(cTRB640)->SERVICO	:= STG->TG_SERVICO
		(cTRB640)->SEQRELA	:= STG->TG_SEQRELA
		(cTRB640)->TAREFA	:= STG->TG_TAREFA
		(cTRB640)->CODIGO	:= STG->TG_CODIGO
		(cTRB640)->QUANTID	:= STG->TG_QUANTID
		(cTRB640)->QUANREC	:= STG->TG_QUANREC
		(cTRB640)->UNIDADE	:= STG->TG_UNIDADE
		(cTRB640)->RESERVA	:= STG->TG_RESERVA
		(cTRB640)->SEQUEN	:= STR(NGSEEK("ST5",STG->TG_CODBEM+STG->TG_SERVICO+STG->TG_SEQRELA+STG->TG_TAREFA,1,"T5_SEQUENC"), TAMSX3("T5_SEQUENC")[1])
		(cTRB640)->(MsUnlock())
		dbSelectarea("STG")
		dbSkip()

	End While

	DbSelectArea(cTRB640)
	DbSetOrder(01)
	DbGoTop()
	SetRegua(LastRec())
	If !EoF()
		While !EoF()

			IncRegua()

			//---------------------------------------------------------------------
			// Selecao dos registros conforme os parametros
			//---------------------------------------------------------------------
			DbSelectArea("ST9")
			DbSetOrder(01)
			If DbSeek(xFilial("ST9")+(cTRB640)->CODBEM)
				If (ST9->T9_CCUSTO >= MV_PAR02 .And. ST9->T9_CCUSTO <= MV_PAR03) .And.;
				(ST9->T9_CODFAMI >= MV_PAR04 .And. ST9->T9_CODFAMI <= MV_PAR05)

					cCCUSTO  := ST9->T9_CCUSTO
					cCENTRAB := ST9->T9_CENTRAB
					cCODFAMI := ST9->T9_CODFAMI

					//---------------------------------------------------------------------
					// Impressao da Primeira Linha da O.S.
					//---------------------------------------------------------------------
					SomaLinha()

					@ Li,000 Psay (cTRB640)->CODBEM  Picture "@!"
					@ Li,017 Psay Substr(ST9->T9_NOME,1,36)
					@ Li,054 Psay (cTRB640)->SERVICO Picture "@!"
					@ Li,062 Psay NGSEEK("ST4",(cTRB640)->SERVICO,1,"Substr(ST4->T4_NOME,1,35)")
					@ Li,098 Psay (cTRB640)->SEQRELA Picture "@!"
					@ Li,107 Psay (cTRB640)->QUANTID Picture "@E 999,999.99"
					@ Li,124 Psay (cTRB640)->UNIDADE Picture "@!"
					SomaLinha()

					//---------------------------------------------------------------------
					// Verifica se o Bem tem Bem Pai
					//---------------------------------------------------------------------
					cChave := NGBEMPAI((cTRB640)->CODBEM)

					If !Empty(cChave)
						@ Li,000 Psay cChave Picture "@!"
						@ Li,017 Psay NGSEEK("ST9",cChave,1,'Substr(ST9->T9_NOME,1,30)')
					Endif

					@ Li,048 Psay (cTRB640)->TAREFA Picture "@!"
					If (cTRB640)->TAREFA = "0     "
						@ Li,055 Psay STR0011 //"Sem Especificacao De Tarefa"
					Else
						@ Li,055 Psay NGSEEK("ST5",(cTRB640)->CODBEM+(cTRB640)->SERVICO+(cTRB640)->SEQRELA+(cTRB640)->TAREFA,;
						1,'Substr(ST5->T5_DESCRIC,1,35)')
					EndIf

					@ Li,092 Psay cCCUSTO  Picture "@!"
					@ Li,113 Psay cCENTRAB Picture "@!"
					@ Li,124 Psay cCODFAMI Picture "@!"

					SomaLinha()
					SomaLinha()
				Endif
			Endif

			DbSelectArea(cTRB640)
			DbSkip()
		End While
	Else
		MsgInfo(STR0012,STR0013) //"Não existem dados para montar o relatório."###"ATENÇÃO"
		//Deleta tabela temporária.
		oARQTR640:Delete()
		Return .F.
	Endif

	Roda(nCntImpr,cRodaTxt,Tamanho)
	
	//Deleta tabela temporária.
	oARQTR640:Delete()

	//---------------------------------------------------------------------
	// Devolve a condicao original do arquivo principal
	//---------------------------------------------------------------------
	RetIndex("STG")

	Set Filter To
	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif

	MS_FLUSH()

Return Nil

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaLinha()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR640                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Somalinha()

	Li++

	If Li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		@ Li,000 Psay STR0010 + " " + Alltrim(STG->TG_CODIGO) + " - " + NGSEEK("SA2",MV_PAR01,1,'A2_NOME')
		SomaLinha()
		@ Li,000 Psay Replicate("*",132)
		SomaLinha()
	EndIf

Return