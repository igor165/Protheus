#INCLUDE "MNTA010.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA010
Cadastro de especialidade

@author Vitor Emanuel Batista
@since 10/02/2012
@version P11
@return Nil
/*/
//---------------------------------------------------------------------
Function MNTA010

	Local oBrowse

	Private tipoins := 'E'

	// Seta visualização de versão para tecla F9 
	SetKey( VK_F9, { | | NGVersao( "MNTA010" , 1 ) } )

	oBrowse := FWMBrowse():New()
		oBrowse:SetAlias("ST0")					// Alias da tabela utilizada
		oBrowse:SetMenuDef("MNTA010")			    // Nome do fonte onde esta a função MenuDef
		oBrowse:SetDescription(STR0006)  	    // Descrição do browse //"Especialidades"

	oBrowse:Activate()

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Opções de menu

@author Vitor Emanuel Batista
@since 10/02/2012
@version P11
@return aRotina - Estrutura
	[n,1] Nome a aparecer no cabecalho
	[n,2] Nome da Rotina associada
	[n,3] Reservado
	[n,4] Tipo de Transação a ser efetuada:
		1 - Pesquisa e Posiciona em um Banco de Dados
		2 - Simplesmente Mostra os Campos
		3 - Inclui registros no Bancos de Dados
		4 - Altera o registro corrente
		5 - Remove o registro corrente do Banco de Dados
		6 - Alteração sem inclusão de registros
		7 - Cópia
		8 - Imprimir
	[n,5] Nivel de acesso
	[n,6] Habilita Menu Funcional
/*/
//---------------------------------------------------------------------
Static Function MenuDef()
//Inicializa MenuDef com todas as opções
Return FWMVCMenu( 'MNTA010' )


//---------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author Vitor Emanuel Batista
@since 10/02/2012
@version P11
@return Nil
/*/
//---------------------------------------------------------------------
Static Function ModelDef()

	Local oModel

	Local oStructST0 := FWFormStruct(1,"ST0")

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTA010", /*bPre*/, /*bPost*/, /*bCommit*/, /*bCancel*/)

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTA010_ST0", Nil, oStructST0,/*bPre*/,/*bPost*/,/*bLoad*/)

	oModel:SetDescription(STR0006)

Return oModel

//---------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author Vitor Emanuel Batista
@since 10/02/2012
@version P11
@return Nil
/*/
//---------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel("MNTA010")
	Local oView  := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA010_ST0" , FWFormStruct(2,"ST0"), /*cLinkID*/ )	//

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100,/*cIDOwner*/,/*lFixPixel*/,/*cIDFolder*/,/*cIDSheet*/ )

	// Associa um View a um box
	oView:SetOwnerView( "MNTA010_ST0" , "MASTER" )
	
	//Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn(oView)

Return oView

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA010DIS
Validação do campo de %Disponivel

@return Nil
@sample
NGVal010()

@author Jean Pytter da costa
@since 19/11/2013
@version 1.0
/*/
//---------------------------------------------------------------------  

Function MNTA010DIS()

Local lRet := .T.

If !Empty(M->T0_DISPONI)
	If (M->T0_DISPONI < 0) .Or. (M->T0_DISPONI > 100)
		Help("",1,"MNTA010DIS")
		lRet := .F.
	EndIf
EndIf

Return lRet 