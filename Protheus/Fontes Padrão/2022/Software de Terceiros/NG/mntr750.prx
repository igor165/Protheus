#INCLUDE "MNTR750.CH"
#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR750  ³ Autor ³ Paulo Pego            ³ Data ³ 10/08/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Custo do Centro de Custo                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³OBSERVACAO³Revisao t‚cnica em 21/10/2002 - In cio Luiz Kolling         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR750()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Guarda conteudo e declara variaveis padroes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM	:= NGBEGINPRM()

	Local vVETHORAS		:= {}
	Local WNREL			:= "MNTR750"
	Local cDESC1		:= STR0001 //"Relatorio de Custos do Centro de Custos."
	Local cDESC2		:= STR0002 //"Este programa seleciona por default OS finalizadas e as"
	Local cDESC3		:= STR0003 //"em aberto conforme parametro selecionado."
	Local cSTRING		:= "STJ"

	Private nTamPro		:= TamSX3("TL_CODIGO")[1] - 15
	Private NOMEPROG	:= "MNTR750"
	Private TAMANHO		:= "G"
	Private aRETURN		:= {STR0004,1,STR0005,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO		:= STR0006 //"Relatorio de Custos Por Centro de Custos"
	Private nLASTKEY	:= 0
	Private cPERG		:= "MNT75R"
	Private aTIPOREG	:= { {"E",STR0082}, {"F",STR0083}, {"P",STR0084}, {"T",STR0085} } //"Espe"###"Ferr"###"Prod"###"Terc"
	Private CABEC1		:= " "
	Private CABEC2		:= CABEC1
	Private cTIPOREG	:= ""
	Private cCODPROD	:= ""
	Private cNOME		:= ""
	Private nCUSTO		:= 0
	Private nEXTRA		:= 0
	Private nSizeFil	:= IIf(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(ST9->ST9_FILIAL))

	cCONDSTL := '(STL->TL_TIPOREG == "P" .And. MV_PAR11 != 2) .Or. '
	cCONDSTL += '(STL->TL_TIPOREG == "M" .And. MV_PAR11 != 3) .Or. '
	cCONDSTL += '(STL->TL_TIPOREG == "F" .And. MV_PAR11 != 4) .Or. '
	cCONDSTL += '(STL->TL_TIPOREG == "T" .And. MV_PAR11 != 5)'

	cCONDSTT := '(STT->TT_TIPOREG == "P" .And. MV_PAR11 != 2) .Or. '
	cCONDSTT += '(STT->TT_TIPOREG == "M" .And. MV_PAR11 != 3) .Or. '
	cCONDSTT += '(STT->TT_TIPOREG == "F" .And. MV_PAR11 != 4) .Or. '
	cCONDSTT += '(STT->TT_TIPOREG == "T" .And. MV_PAR11 != 5)'

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis utilizadas para parametros!                                        ³
	³ MV_PAR01   // De Filial                                                      ³
	³ MV_PAR02   // Ate Filial                                                     ³
	³ MV_PAR03   // De Centro de Custo                                             ³
	³ MV_PAR04   // Ate Centro de Custo                                            ³
	³ MV_PAR05   // De Centro de Trabalho                                          ³
	³ MV_PAR06   // Ate Centro de Trabalho                                         ³
	³ MV_PAR07   // De Familia de Bens                                             ³
	³ MV_PAR08   // Ate Familia de Bens                                            ³
	³ MV_PAR09   // De Servico                                                     ³
	³ MV_PAR10   // Ate Servico                                                    ³
	³ MV_PAR11   // Tipo de Insumo(Todos/Produto/funcionario/ferramentas/Terceiros)³
	³ MV_PAR12   // Considera OS (liberada/pendente/aberta/finalizada)             ³
	³ MV_PAR13   // De data manutencao                                             ³
	³ MV_PAR14   // Ate data manutencao                                            ³
	³ MV_PAR15   // Tipo de Custo          ( Medio/Standard)                       ³
	³ MV_PAR16   // Custo Maquina Parada   ( Sim/Nao )                             ³
	³ MV_PAR17   // Alglutina Mao de Obra  ( Sim/Nao )                             ³
	³ MV_PAR18   // Detalhado ou Resumido  (Detalhado/Resumido )                   ³
	³ MV_PAR19   // Considera Estrutura    ( Nao/Sim )                             ³
	³ MV_PAR20   // Considera Data         (Da O.S\Do Insumo)                      ³
	³ MV_PAR21   // Imprimir Localizacao ?  ( Sim/Nao )                            ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	*/

	PERGUNTE(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL:=SETPRINT(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		SET FILTER TO
		NGRETURNPRM(aNGBEGINPRM)
		RETURN
	Endif
	SETDEFAULT(aRETURN,cSTRING)

	RPTSTATUS({|lEND| R750IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Retorna conteudo de variaveis padroes       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R750Imp  ³ Autor ³ Paulo Pego            ³ Data ³ 21/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function R750IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT := ""
	Local nCNTIMPR := 0
	Local cEstrut  := ""
	Local aEstrut  := {}
	Local nCont    := 0
	Local nMoedarel:= 1
	Local oTmpTbl1
	Local oTmpTbl2
	Local oTmpTbl3
	Local oTmpTbl4
	Local cTamanP := TAMSX3( 'TL_CUSTO' )[1]
	Local cTamanD := TAMSX3( 'TL_CUSTO' )[2]

	//Alias das tabelas temporarias
	Private cTRB	:= GetNextAlias()
	Private cTRB2	:= GetNextAlias()
	Private cTRB3	:= GetNextAlias()
	Private cTRB4	:= GetNextAlias()

	Private li := 80 ,m_pag := 1
	Private cPROD,cTIPO,nCUSTO,cCOD,cCCUSTO,cBEMPAI
	nTIPO := IIf(aRETURN[4]==1,15,18)

	// Caso o update de multi-moeda esteja aplicado e haja a funcao de criacao da pergunta
	If NGCADICBASE("TL_MOEDA","A","STL",.F.) .And. FindFunction("MNTR750MD")
		nMoedarel := MNTR750MD()
	Endif

	//*********************SE MV_PAR19 = NAO E MV_PAR18 = 1

	/*
	          1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	Centro Custo.: 1              TESTE
	*************************************************************************************************************************************
	Tipo Codigo Insumo   Nome Insumo                ........Previsto.........     ........Realizado........
	                                                Quantidade Un.      Custo     Quantidade Un.      Custo      Diferenca        Var.  %
	*************************************************************************************************************************************
	BEM.:xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	O.S.:xxxxxx   PLANO.:xxxxxx   SERVICO.:xxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   DATA....:28/01/01
	xxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  xxx.xxx,xx xxx xxx.xxx,xx     xxx.xxx,xx xxx xxx.xxx,xx     xxx.xxx,xx     xxx.xxx,xx
	     TOTAL DA O.S                           X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.999.999,99

	     TOTAL DO BEM                           X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.999.999,99

	     TOTAL DO CENTRO CUSTO                  X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.X99.999,99

	     TOTAL DO RELATORIO                     X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.X99.999,99

	*********************SE MV_PAR19 = SIM  E MV_PAR18 = 1
	          1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	Centro Custo.: 1              TESTE
	*************************************************************************************************************************************
	Tipo Codigo Insumo   Nome Insumo                ........Previsto.........     ........Realizado........
	                                                Quantidade Un.      Custo     Quantidade Un.      Custo      Diferenca        Var.  %
	*************************************************************************************************************************************
	BEM PAI.:xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	O.S.:xxxxxx   PLANO.:xxxxxx   SERVICO.:xxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   DATA....:28/01/01
	xxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  xxx.xxx,xx xxx xxx.xxx,xx     xxx.xxx,xx xxx xxx.xxx,xx     xxx.xxx,xx     xxx.xxx,xx
	     TOTAL DA O.S                           X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.999.999,99

	     TOTAL DO BEM                           X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.999.999,99
	BEM FILHO.:xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	O.S.:xxxxxx   PLANO.:xxxxxx   SERVICO.:xxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   DATA....:28/01/01
	xxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  xxx.xxx,xx xxx xxx.xxx,xx     xxx.xxx,xx xxx xxx.xxx,xx     xxx.xxx,xx     xxx.xxx,xx
	xxxx xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx  xxx.xxx,xx xxx xxx.xxx,xx     xxx.xxx,xx xxx xxx.xxx,xx     xxx.xxx,xx     xxx.xxx,xx
	     TOTAL DA O.S                           X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.999.999,99

	     TOTAL DO BEM FILHO                     X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.999.999,99

	     TOTAL DO GERAL DO PAI                  X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.999.999,99

	     TOTAL DO CENTRO CUSTO                  X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.999.999,99

	     TOTAL DO RELATORIO                     X99.999.999,99 X99.999.999,99 X99,999.999,99 X99.999.999,99 X99.999.999,99 999.999.999,99

	*********************SE MV_PAR19 = SIM E MV_PAR18 = 2
	          1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	Centro Custo.: 1              TESTE
	*************************************************************************************************************************************
	Bem              Descricao                                 Custo Previsto   Custo Realizado         Diferenca            Var. %
	*************************************************************************************************************************************
	BEM PAI.....:
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxx.xxx,xx        xxx.xxx,xx        xxx.xxx,xx        xxx.xxx,xx

	BENS FILHOS.:
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxx.xxx,xx        xxx.xxx,xx        xxx.xxx,xx        xxx.xxx,xx
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxx.xxx,xx        xxx.xxx,xx        xxx.xxx,xx        xxx.xxx,xx

	     TOTAL DO BEM PAI                                      X99.999.999,99    X99.999.999,99    X99.999.999,99    X99.999.999,99

	     TOTAL DO CENTRO CUSTO                                 X99.999.999,99    X99.999.999,99    X99.999.999,99    X99.999.999,99

	     TOTAL DO RELATORIO                                    X99.999.999,99    X99.999.999,99    X99.999,999,99    X99.999.999,99

	*********************SE MV_PAR19 = NAO E MV_PAR18 = 2
	          1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	Centro Custo.: 1              TESTE
	*************************************************************************************************************************************
	Bem              Descricao                                 Custo Previsto   Custo Realizado         Diferenca            Var. %
	*************************************************************************************************************************************
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      xxx.xxx,xx        xxx.xxx,xx        xxx.xxx,xx        xxx.xxx,xx

	     TOTAL DO CENTRO CUSTO                                 X99.999.999,99    X99.999.999,99    X99.999,999,99    X99.999.999,99

	     TOTAL DO RELATORIO                                    X99.999.999,99    X99.999.999,99    X99.999,999,99    X99.999.999,99
	/*/

	aCAMPOS := {}
	AADD(aCAMPOS,{"PLANO"    ,"C",06,0})
	AADD(aCAMPOS,{"CCUSTO"   ,"C",Len(stj->tj_ccusto),0})
	AADD(aCAMPOS,{"CTRAB"    ,"C",Len(stj->tj_centrab),0})
	AADD(aCAMPOS,{"CODBEM"   ,"C",16,0})
	AADD(aCAMPOS,{"SERVICO"  ,"C",06,0})
	AADD(aCAMPOS,{"ORDEM"    ,"C",06,0})
	AADD(aCAMPOS,{"TIPOREG"  ,"C",01,0})
	AADD(aCAMPOS,{"PRODUTO"  ,"C",TamSX3("TL_CODIGO")[1],0})
	AADD(aCAMPOS,{"NOMPROD"  ,"C",40,0})
	AADD(aCAMPOS,{"TERMINO"  ,"C",01,0})
	AADD(aCAMPOS,{"QUANTPRE" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"UNIDPRE"  ,"C",03,0})
	AADD(aCAMPOS,{"VALORPRE" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"QUANTREA" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"UNIDREA"  ,"C",03,0})
	AADD(aCAMPOS,{"VALORREA" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"ARQUIVO"  ,"C",03,0})
	AADD(aCAMPOS,{"ORIGI"    ,"D",08,0})

	//Intancia classe FWTemporaryTable (TABELA 1)
	oTmpTbl1  := FWTemporaryTable():New( cTRB, aCAMPOS )
	//Cria indices
	oTmpTbl1:AddIndex( "Ind01" , {"CCUSTO","CODBEM","ORDEM","PLANO","TIPOREG","PRODUTO"}   )
	oTmpTbl1:AddIndex( "Ind02" , {"CCUSTO","CTRAB","CODBEM"}  )
	//Cria a tabela temporaria
	oTmpTbl1:Create()

	aCAMPOS  := {}
	AADD(aCAMPOS,{"PLANO"    ,"C",06,0})
	AADD(aCAMPOS,{"ORDEM"    ,"C",06,0})
	AADD(aCAMPOS,{"QUANTPRE" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"VALORPRE" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"QUANTREA" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"VALORREA" ,"N",cTamanP,cTamanD})

	oTmpTbl2  := FWTemporaryTable():New( cTRB2, aCAMPOS )
	//Cria indices
	oTmpTbl2:AddIndex( "Ind01" , {"ORDEM","PLANO"}   )
	//Cria a tabela temporaria
	oTmpTbl2:Create()

	//ARQUIVO DE TRABALHO DE GRAVACAO DOS FILHOS DO PAI DA ESTRUTURA
	aCAMPOS := {}
	AADD(aCAMPOS,{"BEMPAI"   ,"C",16,0})
	AADD(aCAMPOS,{"CODBEM"   ,"C",16,0})
	AADD(aCAMPOS,{"ORDEM"    ,"C",06,0})
	AADD(aCAMPOS,{"PLANO"    ,"C",06,0})
	AADD(aCAMPOS,{"SERVICO"  ,"C",06,0})
	AADD(aCAMPOS,{"TIPOREG"  ,"C",01,0})
	AADD(aCAMPOS,{"PRODUTO"  ,"C",TamSX3("TL_CODIGO")[1],0})
	AADD(aCAMPOS,{"NOMPROD"  ,"C",40,0})
	AADD(aCAMPOS,{"TERMINO"  ,"C",01,0})
	AADD(aCAMPOS,{"QUANTPRE" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"UNIDPRE"  ,"C",03,0})
	AADD(aCAMPOS,{"VALORPRE" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"QUANTREA" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"UNIDREA"  ,"C",03,0})
	AADD(aCAMPOS,{"VALORREA" ,"N",cTamanP,cTamanD})
	AADD(aCAMPOS,{"ARQUIVO"  ,"C",03,0})
	AADD(aCAMPOS,{"ORIGI"    ,"D",08,0})

	oTmpTbl3  := FWTemporaryTable():New( cTRB3, aCAMPOS )
	//Cria indices
	oTmpTbl3:AddIndex( "Ind01" , {"BEMPAI","CODBEM","ORDEM","PLANO","TIPOREG","PRODUTO"}   )
	//Cria a tabela temporaria
	oTmpTbl3:Create()

	//ARQUIVO DE TRABALHO PARA VERIFICAR SE A O.S JA FOI LIDA
	aCAMPOS := {}
	AADD(aCAMPOS,{"CODBEM" ,"C",16,0})
	AADD(aCAMPOS,{"ORDEM"  ,"C",06,0})
	AADD(aCAMPOS,{"PLANO"  ,"C",06,0})

	oTmpTbl4  := FWTemporaryTable():New( cTRB4, aCAMPOS )
	//Cria indices
	oTmpTbl4:AddIndex( "Ind01" , {"CODBEM","ORDEM","PLANO"}   )
	//Cria a tabela temporaria
	oTmpTbl4:Create()

	//Query referente a STJ
	cAliQrySTJ	:= GetNextAlias()

	cQuery := "SELECT ST9.T9_FILIAL, ST9.T9_CODFAMI, ST9.T9_CODBEM,  ST9.T9_NOME,"
	cQuery += " STJ.TJ_FILIAL,  STJ.TJ_ORDEM,   STJ.TJ_TIPOOS, STJ.TJ_PLANO,   STJ.TJ_SITUACA, STJ.TJ_TERMINO,"
	cQuery += " STJ.TJ_DTMRINI, STJ.TJ_DTMPINI, STJ.TJ_CCUSTO, STJ.TJ_CENTRAB, STJ.TJ_SERVICO, STJ.TJ_DTORIGI,"
	cQuery += " STJ.TJ_SEQRELA, STJ.TJ_HOMPINI, STJ.TJ_DTPPINI, STJ.TJ_HOPPINI, STJ.TJ_DTPPFIM, STJ.TJ_HOPPFIM,"
	cQuery += " STJ.TJ_DTMPFIM, STJ.TJ_HOMPFIM, STJ.TJ_DTPRINI, STJ.TJ_HOPRINI, STJ.TJ_HOMRINI, STJ.TJ_DTPRFIM,"
	cQuery += " STJ.TJ_HOPRFIM, STJ.TJ_DTMRFIM, STJ.TJ_HOMRFIM"
	cQuery += " FROM " + RetSqlName("ST9") + " ST9"
	cQuery += " INNER JOIN " + RetSqlName("STJ") + " STJ"
	cQuery += " ON STJ.TJ_CODBEM = ST9.T9_CODBEM AND " + NGMODCOMP("ST9","STJ","=")
	cQuery += " WHERE STJ.TJ_FILIAL  >= " + ValToSql(MV_PAR01) + " AND STJ.TJ_FILIAL <= " + ValToSql(MV_PAR02)
	cQuery += " AND STJ.TJ_CCUSTO  >= " + ValToSql(MV_PAR03) + " AND STJ.TJ_CCUSTO  <= " + ValToSql(MV_PAR04)
	cQuery += " AND STJ.TJ_CENTRAB >= " + ValToSql(MV_PAR05) + " AND STJ.TJ_CENTRAB <= " + ValToSql(MV_PAR06)
	cQuery += " AND STJ.TJ_SERVICO >= " + ValToSql(MV_PAR09) + " AND STJ.TJ_SERVICO <= " + ValToSql(MV_PAR10)
	cQuery += " AND ST9.T9_CODFAMI >= " + ValToSql(MV_PAR07) + " AND ST9.T9_CODFAMI <= " + ValToSql(MV_PAR08)
	cQuery += " AND STJ.D_E_L_E_T_ <> '*'"
	cQuery += " AND ST9.D_E_L_E_T_ <> '*'"


	If MV_PAR12 == 1  //Liberadas
		cQuery += " AND TJ_SITUACA = 'L' "
	ElseIf MV_PAR12 == 2 //Finalizadas(L/S) e Pendentes(P/N)
		cQuery += " AND ( ( TJ_SITUACA <> 'L' AND TJ_TERMINO <> 'N' ) OR TJ_SITUACA <> 'C' )
	ElseIf MV_PAR12 == 3 //Finalizadas(L/S) e Ambas (Pendentes e Liberadas - L/N e P/N))
		cQuery += " AND TJ_SITUACA <> 'C' "
	ElseIf MV_PAR12 == 4 //Finalizadas(L/S)
		cQuery += " AND TJ_SITUACA = 'L' AND TJ_TERMINO = 'S' "
	EndIf

	// Considera Data ( Da O.S. )
	If MV_PAR20 == 1

		If MV_PAR12 <> 4

			cQuery += " AND TJ_DTMPINI >= '" +Dtos(MV_PAR13)+ "' AND TJ_DTMPINI <= '" +DtoS(MV_PAR14)+ "' "

		Else

			// Considera se a data real fim da O.S. está contida no range definido por parâmetro.
			cQuery += ' AND TJ_DTMRFIM >= ' + ValToSQL( DToS( MV_PAR13 ) ) + ' AND TJ_DTMRFIM <= ' +;
				ValToSQL( DToS( MV_PAR14 ) )

		EndIf

	EndIf

	dbUseArea( .T., "TOPCONN", TCGenQry(,,cQuery), cAliQrySTJ, .F., .T. )

	//Query referente a STS
	cAliQrySTS	:= GetNextAlias()

	cQuery := "SELECT ST9.T9_FILIAL, ST9.T9_CODFAMI, ST9.T9_CODBEM,  ST9.T9_NOME,"
	cQuery += " STS.TS_FILIAL,  STS.TS_ORDEM,   STS.TS_TIPOOS, STS.TS_PLANO,   STS.TS_SITUACA, STS.TS_TERMINO,"
	cQuery += " STS.TS_DTMRINI, STS.TS_DTMPINI, STS.TS_CCUSTO, STS.TS_CENTRAB, STS.TS_SERVICO, STS.TS_DTORIGI,"
	cQuery += " STS.TS_SEQRELA, STS.TS_HOMPINI, STS.TS_DTPPINI, STS.TS_HOPPINI, STS.TS_DTPPFIM, STS.TS_HOPPFIM,"
	cQuery += " STS.TS_DTMPFIM, STS.TS_HOMPFIM, STS.TS_DTPRINI, STS.TS_HOPRINI, STS.TS_HOMRINI, STS.TS_DTPRFIM,"
	cQuery += " STS.TS_HOPRFIM, STS.TS_DTMRFIM, STS.TS_HOMRFIM"
	cQuery += " FROM " + RetSqlName("ST9") + " ST9"
	cQuery += " INNER JOIN " + RetSqlName("STS") + " STS"
	cQuery += " ON STS.TS_CODBEM = ST9.T9_CODBEM AND " + NGMODCOMP("ST9","STS","=")
	cQuery += " WHERE STS.TS_FILIAL  >= '" + MV_PAR01 + "' AND STS.TS_FILIAL <= '" + MV_PAR02 + "'"
	cQuery += " AND STS.TS_CCUSTO  >= '" + MV_PAR03 + "' AND STS.TS_CCUSTO  <= '" + MV_PAR04 + "'"
	cQuery += ' AND STS.TS_DTMRFIM >= ' + ValToSQL( DToS( MV_PAR13 ) ) + ' AND STS.TS_DTMRFIM <= ' + ValToSQL( DToS( MV_PAR14 ) )
	cQuery += " AND STS.TS_CENTRAB >= '" + MV_PAR05 + "' AND STS.TS_CENTRAB <= '" + MV_PAR06 + "'"
	cQuery += " AND ST9.T9_CODFAMI >= '" + MV_PAR07 + "' AND ST9.T9_CODFAMI <= '" + MV_PAR08 + "' "
	cQuery += " AND STS.TS_SERVICO >= " + ValToSql(MV_PAR09) + " AND STS.TS_SERVICO <= " + ValToSql(MV_PAR10)
	cQuery += " AND STS.D_E_L_E_T_ <> '*'"
	cQuery += " AND ST9.D_E_L_E_T_ <> '*'"

	dbUseArea( .T., "TOPCONN", TCGenQry(,,cQuery), cAliQrySTS, .F., .T. )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Possiciona os arquivos para processamento                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Dbselectarea("STL")
	Dbsetorder(1)
	Dbselectarea("STT")
	Dbsetorder(1)

	If MV_PAR19 = 1
		Dbselectarea("STJ")
		Dbsetorder(07)
		Dbseek(xFILIAL("STJ")+MV_PAR03,.T.)
		nREG := LASTREC()

		Dbselectarea("STS")
		Dbsetorder(04)
		Dbseek(xFILIAL("STS")+MV_PAR03,.T.)
		nREG := nREG+LASTREC()

		SETREGUA(nREG)

		DbSelectArea(cAliQrySTJ)

		While !Eof()

			INCREGUA()

			NGCUST1STJ()

			Dbselectarea(cAliQrySTJ)
			Dbskip()
		End

		DbSelectArea(cAliQrySTS)

		While !Eof()

			INCREGUA()

			If !NGTRCSTSOS(	MV_PAR13, MV_PAR14, MV_PAR20, (cAliQrySTS)->TS_SITUACA, (cAliQrySTS)->TS_TERMINO,;
					SToD( (cAliQrySTS)->TS_DTMRFIM ) )
				DbSkip()
				Loop
			EndIf

			NGCUST1STS()

			Dbselectarea(cAliQrySTS)
			Dbskip()
		End

	Else
		cCONDTPN := '(tpn->tpn_ccusto >= MV_PAR03 .And. tpn->tpn_ccusto <= MV_PAR04) .And. '
		cCONDTPN += '(tpn->tpn_ctrab >= MV_PAR05 .And. tpn->tpn_ctrab <= MV_PAR06)'

		Dbselectarea("ST9")
		Dbsetorder(04)
		Dbseek((cAliQrySTJ)->T9_FILIAL+MV_PAR07,.T.)
		SETREGUA(LASTREC())
		While !Eof() .And. ST9->T9_FILIAL == (cAliQrySTJ)->T9_FILIAL .And.;
				ST9->T9_CODFAMI <= MV_PAR08

			Dbselectarea("TPN")
			Dbsetorder(02)
			If Dbseek((cAliQrySTJ)->T9_FILIAL+ST9->T9_CODBEM)
				lPRIMBEM := .t.
				While !Eof() .And. TPN->TPN_FILIAL == (cAliQrySTJ)->T9_FILIAL .And.;
						TPN->TPN_CODBEM == ST9->T9_CODBEM

					If &(cCONDTPN)
						Dbselectarea(cTRB)
						Dbsetorder(02)
						If !Dbseek(TPN->TPN_CCUSTO+TPN->TPN_CTRAB+TPN->TPN_CODBEM)
							//Calcula o Custo da O.S do Pai da Estrutura(STJ)
							NGCUST3STJ(TPN->TPN_CODBEM,TPN->TPN_CCUSTO,TPN->TPN_CTRAB)//CALCULA O CUSTO DO PAI STJ
							//Calcula o Custo da O.S do Pai da Estrutura(STS)
							NGCUST3STS(TPN->TPN_CODBEM,TPN->TPN_CCUSTO,TPN->TPN_CTRAB)//CALCULA O CUSTO DO PAI STS

							If lPRIMBEM
								aRet := { }
								lPai := .F.
								aRet := NGBEMPER(TPN->TPN_CODBEM,MV_PAR13,MV_PAR14)
								lPRIMBEM := .f.
							Endif

							If !EMPTY(aRet)
								//Calcula o Custo da O.S dos Filhos do Pai da  Estrutura(STJ)
								NGCUST2STJ(aRet,TPN->TPN_CODBEM,TPN->TPN_CCUSTO,TPN->TPN_CTRAB)//CALCULA O CUSTO DOS FILHOS STJ
								//Calcula o Custo das O.S dos Filhos do Pai da  Estrutura(STS)
								NGCUST2STS(aRet,TPN->TPN_CODBEM,TPN->TPN_CCUSTO,TPN->TPN_CTRAB)//CALCULA O CUSTO DOS FILHOS STS
							Endif
						Endif
					Endif
					Dbselectarea("TPN")
					Dbskip()
				End
			Endif
			Dbselectarea("ST9")
			Dbskip()
		End
	Endif
	//*********************** INICIO DA IMPRESSAO

	If MV_PAR18 = 1 //***********************RELATORIO DO TIPO DETALHADO

		If MV_PAR19 = 2 //CONSIDERANDO O CUSTO DA ESTRUTURA
			Dbselectarea(cTRB)
			Dbsetorder(1)
			Dbgotop()
			SETREGUA(LASTREC())
			Store 0.00 To nTOTP,nTOTR,nHTOTP,nHTOTR

			While !Eof()
				cCCUSTO := (cTRB)->CCUSTO
				Li := 100
				SOMALIN()
				Store 0.00 To nCUSTP,nCUSTR,nHCUSTP,nHCUSTR

				Dbselectarea(cTRB)
				While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO
					cBEMPAI := (cTRB)->CODBEM
					Store 0.00 To nBEMP,nBEMR,nHBEMP,nHBEMR,;
						nCBEMPAIP,nCBEMPAIR,nHBEMPAIP,nHBEMPAIR
					SOMALIN()
					@ Li,000 PSAY STR0052+cBEMPAI+"   "+NGSEEK('ST9',cBEMPAI,1,'T9_NOME') //"BEM PAI.: "
					SOMALIN()

					If MV_PAR21 == 1
						cEstrut := NGLocComp(cBEMPAI,"1")
						aEstrut := {}
						If Len(cEstrut) > 100
							aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
							cEstrut := AllTrim(SubStr(cEstrut,101))
							While Len(cEstrut) > 100
								aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
								cEstrut := AllTrim(SubStr(cEstrut,101))
							End
							aAdd(aEstrut,AllTrim(cEstrut))
						EndIf
						If Len(aEstrut) > 0
							For nCont := 1 To Len(aEstrut)
								If nCont == 1
									@ Li,000 PSay STR0070 + aEstrut[nCont] //"Localização.: "
								Else
									@ Li,000 PSay Space(Len(STR0070)) + aEstrut[nCont] //"Localização.: "
								EndIf
								SOMALIN()
							Next nCont
						Else
							If AllTrim(cBEMPAI) <> AllTrim(cEstrut)
								@ Li,000 Psay STR0070 + cEstrut //"Localização.: "
							Else
								@ Li,000 Psay STR0070 + STR0071 //"Localização.: "###"Não está relacionado a uma Estrutura."
							EndIf
							SOMALIN()
						EndIf
					Endif
					SOMALIN()

					nCONT := 0
					Dbselectarea(cTRB)
					While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO .And. (cTRB)->CODBEM == cBEMPAI

						If !EMPTY((cTRB)->ORDEM)
							cSERV := (cTRB)->SERVICO
							@ Li,000 PSAY STR0038+(cTRB)->ORDEM+"   "+STR0039+(cTRB)->PLANO+"   "+; //"PLANO.:"
							STR0040+cSERV+"   "+NGSEEK('ST4',cSERV,1,'T4_NOME')+"   "+STR0041+DTOC((cTRB)->ORIGI) //"DATA....:"

							Dbselectarea(cTRB)
							SOMALIN()

							If !Empty((cTRB)->ARQUIVO)
								MNTR750MEM((cTRB)->ARQUIVO,(cTRB)->ORDEM,(cTRB)->PLANO)
							Endif

							Store 0.00 To nOSP,nOSR,nHOSP,nHOSR
							cNUM   := (cTRB)->ORDEM
							cPLANO := (cTRB)->PLANO

							While !Eof() .And.(cTRB)->CCUSTO == cCCUSTO .And.;
							(cTRB)->CODBEM == cBEMPAI .And. (cTRB)->ORDEM == cNUM .And. ;
							(cTRB)->PLANO  == cPLANO

								INCREGUA()
								MNTR750IP1((cTRB)->TIPOREG,(cTRB)->VALORREA,(cTRB)->VALORPRE,;
								(cTRB)->QUANTPRE,(cTRB)->QUANTREA,(cTRB)->PRODUTO,;
								(cTRB)->NOMPROD,(cTRB)->UNIDPRE,(cTRB)->UNIDREA)
								nCONT := nCONT + 1
								Dbselectarea(cTRB)
								Dbskip()
							End
							If MV_PAR16 == 1
								Dbselectarea(cTRB2)
								If (cTRB2)->(Dbseek(cNUM+cPLANO))
									MNTR750IP3(STR0043,(cTRB2)->QUANTPRE,(cTRB2)->VALORPRE,;
									(cTRB2)->QUANTREA,(cTRB2)->VALORREA)
								Endif
							Endif
							Dbselectarea(cTRB)
							nBEMP  += nOSP
							nBEMR  += nOSR

							MNTR750IP2(STR0044,nHOSP,nOSP,nHOSR,nOSR)
						Else
							Dbselectarea(cTRB)
							Dbskip()
						Endif
					End

					nCBEMPAIP += nBEMP //SOMA O CUSTO GERAL DO BEM PAI
					nCBEMPAIR += nBEMR
					If nCONT > 0
						MNTR750IP2(STR0054,nHBEMP,nBEMP,nHBEMR,nBEMR)
					Endif

					//IMPRESSAO DAS O.S DOS FILHOS DA ESTRUTURA*****************************************
					Dbselectarea(cTRB3)
					If Dbseek(cBEMPAI)
						While !Eof() .And. (cTRB3)->BEMPAI == cBEMPAI
							cBEMFILHO := (cTRB3)->CODBEM
							Store 0.00 To nBEMFP,nBEMFR,nHBEMFP,nHBEMFR
							SOMALIN()
							@ Li,000 PSAY STR0053+cBEMFILHO+"   "+NGSEEK('ST9',(cTRB3)->CODBEM,1,'T9_NOME') //"BEM FILHO.: "
							SOMALIN()

							Dbselectarea(cTRB3)
							While !Eof() .And. (cTRB3)->BEMPAI == cBEMPAI .And. (cTRB3)->CODBEM == cBEMFILHO
								cSERV := (cTRB)->SERVICO
								@ Li,000 PSAY STR0038+(cTRB3)->ORDEM+"   "+STR0039+(cTRB3)->PLANO+"   "+; //"PLANO.:"
								STR0040+cSERV+"   "+NGSEEK('ST4',cSERV,1,'T4_NOME')+"   "+STR0041+DTOC((cTRB3)->ORIGI) //"DATA....:"
								Dbselectarea(cTRB3)
								SOMALIN()

								If !Empty((cTRB3)->ARQUIVO)
									MNTR750MEM((cTRB3)->ARQUIVO,(cTRB3)->ORDEM,(cTRB3)->PLANO)
								Endif

								Store 0.00 To nOSP,nOSR,nHOSP,nHOSR
								cNUM   := (cTRB3)->ORDEM
								cPLANO := (cTRB3)->PLANO

								While !Eof() .And. (cTRB3)->BEMPAI == cBEMPAI .And. ;
								(cTRB3)->CODBEM == cBEMFILHO .And. (cTRB3)->ORDEM == cNUM .And. ;
								(cTRB3)->PLANO  == cPLANO

									INCREGUA()
									MNTR750IP1((cTRB3)->TIPOREG,(cTRB3)->VALORREA,(cTRB3)->VALORPRE,;
									(cTRB3)->QUANTPRE,(cTRB3)->QUANTREA,(cTRB3)->PRODUTO,;
									(cTRB3)->NOMPROD,(cTRB3)->UNIDPRE,(cTRB3)->UNIDREA)
									Dbselectarea(cTRB3)
									Dbskip()
								End
								If MV_PAR16 == 1
									Dbselectarea(cTRB2)
									If (cTRB2)->(Dbseek(cNUM+cPLANO))
										MNTR750IP3(STR0043,(cTRB2)->QUANTPRE,(cTRB2)->VALORPRE,;
										(cTRB2)->QUANTREA,(cTRB2)->VALORREA)
									Endif
								Endif
								Dbselectarea(cTRB3)
								MNTR750IP2(STR0044,nHOSP,nOSP,nHOSR,nOSR)
								nBEMFP += nOSP //SOMA O CUSTO DO BEM FILHO
								nBEMFR += nOSR
							End

							MNTR750IP2(STR0055,nHBEMFP,nBEMFP,nHBEMFR,nBEMFR)
							nCBEMPAIP += nBEMFP //SOMA O CUSTO DO BEM PAI
							nCBEMPAIR += nBEMFR
						End

						MNTR750IP2(STR0056,nHBEMPAIP,nCBEMPAIP,nHBEMPAIR,nCBEMPAIR)
					Endif
					nCUSTP += nCBEMPAIP
					nCUSTR += nCBEMPAIR
					Dbselectarea(cTRB)
				End
				Dbselectarea(cTRB)

				MNTR750IP2(STR0046,nHCUSTP,nCUSTP,nHCUSTR,nCUSTR)

				nTOTP += nCUSTP
				nTOTR += nCUSTR
			End
			If nTOTP > 0 .Or. nTOTR  > 0
				MNTR750IP2(STR0047,nHTOTP,nTOTP,nHTOTR,nTOTR)
			Endif
		Else //************************NAO CONSIDERANDO O CUSTO DA ESTRUTURA
			Dbselectarea(cTRB)
			Dbsetorder(1)
			Dbgotop()
			SETREGUA(LASTREC())
			Store 0.00 To nTOTP,nTOTR,nHTOTP,nHTOTR
			While !Eof()

				cCCUSTO := (cTRB)->CCUSTO
				Li := 100
				SOMALIN()
				Store 0.00 To nCUSTP,nCUSTR,nHCUSTP,nHCUSTR
				Dbselectarea(cTRB)
				While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO

					cCODBEM := (cTRB)->CODBEM
					Store 0.00 To nBEMP,nBEMR,nHBEMP,nHBEMR
					SOMALIN()
					@ Li,000 PSAY STR0027+cCODBEM+"   "+NGSEEK('ST9',cCODBEM,1,'T9_NOME') //"BEM.:"
					SOMALIN()

					If MV_PAR21 == 1
						cEstrut := NGLocComp(cCODBEM,"1")
						aEstrut := {}
						If Len(cEstrut) > 100
							aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
							cEstrut := AllTrim(SubStr(cEstrut,101))
							While Len(cEstrut) > 100
								aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
								cEstrut := AllTrim(SubStr(cEstrut,101))
							End
							aAdd(aEstrut,AllTrim(cEstrut))
						EndIf
						If Len(aEstrut) > 0
							For nCont := 1 To Len(aEstrut)
								If nCont == 1
									@ Li,000 PSay STR0070 + aEstrut[nCont] //"Localização.: "
								Else
									@ Li,000 PSay Space(Len(STR0070)) + aEstrut[nCont] //"Localização.: "
								EndIf
								SOMALIN()
							Next nCont
						Else
							If AllTrim(cCODBEM) <> AllTrim(cEstrut)
								@ Li,000 Psay STR0070 + cEstrut //"Localização.: "
							Else
								@ Li,000 Psay STR0070 + STR0071 //"Localização.: "###"Não está relacionado a uma Estrutura."
							EndIf
							SOMALIN()
						EndIf
					Endif
					SOMALIN()

					Dbselectarea(cTRB)
					While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO .And. (cTRB)->CODBEM == cCODBEM
						cSERV := (cTRB)->SERVICO
						@ Li,000 PSAY STR0038+(cTRB)->ORDEM+"   "+STR0039+(cTRB)->PLANO+"   "+; //"PLANO.:"
						STR0040+cSERV+"   "+NGSEEK('ST4',cSERV,1,'T4_NOME')+"   "+STR0041+DTOC((cTRB)->ORIGI) //"DATA....:"

						Dbselectarea(cTRB)
						SOMALIN()
						If !Empty((cTRB)->ARQUIVO)
							MNTR750MEM((cTRB)->ARQUIVO,(cTRB)->ORDEM,(cTRB)->PLANO)
						Endif

						Store 0.00 To nOSP,nOSR,nHOSP,nHOSR
						cNUM   := (cTRB)->ORDEM
						cPLANO := (cTRB)->PLANO

						While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO  .And. ;
						(cTRB)->CODBEM == cCODBEM .And. (cTRB)->ORDEM == cNUM .And. ;
						(cTRB)->PLANO == cPLANO

							INCREGUA()
							MNTR750IP1((cTRB)->TIPOREG,(cTRB)->VALORREA,(cTRB)->VALORPRE,;
							(cTRB)->QUANTPRE,(cTRB)->QUANTREA,(cTRB)->PRODUTO,;
							(cTRB)->NOMPROD,(cTRB)->UNIDPRE,(cTRB)->UNIDREA)
							Dbselectarea(cTRB)
							Dbskip()
						End
						If MV_PAR16 == 1
							Dbselectarea(cTRB2)
							If (cTRB2)->(Dbseek(cNUM+cPLANO))
								MNTR750IP3(STR0043,(cTRB2)->QUANTPRE,(cTRB2)->VALORPRE,;
								(cTRB2)->QUANTREA,(cTRB2)->VALORREA)
							Endif
						Endif
						Dbselectarea(cTRB)

						MNTR750IP2(STR0044,nHOSP,nOSP,nHOSR,nOSR)

						nBEMP += nOSP
						nBEMR += nOSR
					End

					MNTR750IP2(STR0045,nHBEMP,nBEMP,nHBEMR,nBEMR)
					@Li,000 PSAY REPLICATE("_",157+nTamPro)
					SOMALIN()
					nCUSTP += nBEMP
					nCUSTR += nBEMR
				End
				SOMALIN()
				MNTR750IP2(STR0046,nHCUSTP,nCUSTP,nHCUSTR,nCUSTR)

				nTOTP += nCUSTP
				nTOTR += nCUSTR
			End
			If nTOTP > 0 .Or. nTOTR  > 0
				MNTR750IP2(STR0047,nHTOTP,nTOTP,nHTOTR,nTOTR)
			Endif
		Endif
	Else //*******************************RELATORIO DO TIPO RESUMIDO
		If MV_PAR19 = 2  //CONSIDERANDO CUSTO DA ESTRUTURA
			Dbselectarea(cTRB)
			Dbsetorder(1)
			Dbgotop()
			SETREGUA(LASTREC())
			Store 0.00 To nTOTP,nTOTR
			While !Eof()

				cCCUSTO := (cTRB)->CCUSTO
				Li := 100
				SOMALIN()
				Store 0.00 To nCUSTP,nCUSTR
				Dbselectarea(cTRB)
				While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO

					cBEMPAI := (cTRB)->CODBEM
					Store 0.00 To nBEMP,nBEMR,nCBEMPAIP,nCBEMPAIR

					SOMALIN()
					@ Li,000 PSAY STR0057 //"BEM PAI......:"
					Dbselectarea(cTRB)
					While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO .And. (cTRB)->CODBEM == cBEMPAI
						Store 0.00 To nOSP,nOSR
						cNUM   := (cTRB)->ORDEM
						cPLANO := (cTRB)->PLANO
						While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO  .And. ;
						(cTRB)->CODBEM == cBEMPAI .And. (cTRB)->ORDEM == cNUM .And. ;
						(cTRB)->PLANO  == cPLANO

							INCREGUA()
							nOSP += (cTRB)->VALORPRE
							nOSR += (cTRB)->VALORREA
							Dbselectarea(cTRB)
							Dbskip()
						End
						If MV_PAR16 == 1
							Dbselectarea(cTRB2)
							If (cTRB2)->(Dbseek(cNUM+cPLANO))
								nOSP += (cTRB2)->VALORPRE
								nOSR += (cTRB2)->VALORREA
							Endif
						Endif
						Dbselectarea(cTRB)
						nBEMP += nOSP
						nBEMR += nOSR
					End

					MNTR750IP4(cBEMPAI,nBEMP,nBEMR)
					nCBEMPAIP += nBEMP //SOMA O CUSTO DO BEM PAI
					nCBEMPAIR += nBEMR

					//IMPRESSAO DAS O.S DOS FILHOS DA ESTRUTURA*****************************************
					Dbselectarea(cTRB3)
					If Dbseek(cBEMPAI)

						LPRIFI := .T.
						While !Eof() .And. (cTRB3)->BEMPAI == cBEMPAI
							cBEMFILHO := (cTRB3)->CODBEM
							Store 0.00 To nBEMFP,nBEMFR
							If LPRIFI
								SOMALIN()
								SOMALIN()
								@ Li,000 PSAY STR0058 //"BENS FILHOS..:"
								LPRIFI := .F.
							Endif
							Dbselectarea(cTRB3)
							cNUM   := (cTRB)->ORDEM
							cPLANO := (cTRB)->PLANO
							While !Eof() .And. (cTRB3)->BEMPAI == cBEMPAI .And. (cTRB3)->CODBEM == cBEMFILHO
								Store 0.00 To nOSP,nOSR
								cNUM   := (cTRB3)->ORDEM
								cPLANO := (cTRB3)->PLANO
								While !Eof() .And. (cTRB3)->BEMPAI == cBEMPAI .And. ;
								(cTRB3)->CODBEM == cBEMFILHO .And. (cTRB3)->ORDEM == cNUM .And. ;
								(cTRB3)->PLANO  == cPLANO

									INCREGUA()
									nOSP += (cTRB3)->VALORPRE
									nOSR += (cTRB3)->VALORREA

									Dbselectarea(cTRB3)
									Dbskip()
								End
								If MV_PAR16 == 1
									Dbselectarea(cTRB2)
									If (cTRB2)->(Dbseek(cNUM+cPLANO))
										nOSP += (cTRB2)->VALORPRE
										nOSR += (cTRB2)->VALORREA
									Endif
								Endif
								Dbselectarea(cTRB3)
								nBEMFP += nOSP //SOMA O CUSTO DO BEM FILHO
								nBEMFR += nOSR
							End

							MNTR750IP4(cBEMFILHO,nBEMFP,nBEMFR)
							nCBEMPAIP += nBEMFP //SOMA O CUSTO DO BEM PAI
							nCBEMPAIR += nBEMFR
						End
					Endif
					SOMALIN()
					SOMALIN()
					MNTR750IP5(STR0054,nCBEMPAIP,nCBEMPAIR)
					nCUSTP += nCBEMPAIP
					nCUSTR += nCBEMPAIR

					Dbselectarea(cTRB)
				End
				Dbselectarea(cTRB)

				MNTR750IP5(STR0046,nCUSTP,nCUSTR)
				nTOTP += nCUSTP
				nTOTR += nCUSTR
			End
			If nTOTP > 0 .Or. nTOTR > 0
				MNTR750IP5(STR0047,nTOTP,nTOTR)
			Endif
		Else //NAO CONSIDERANDO CUSTO DA ESTRUTURA
			Dbselectarea(cTRB)
			Dbsetorder(1)
			Dbgotop()
			SETREGUA(LASTREC())
			Store 0.00 To nTOTP,nTOTR

			While !Eof()
				cCCUSTO := (cTRB)->CCUSTO

				Li := 100
				SOMALIN()
				Store 0.00 To nCUSTP,nCUSTR
				Dbselectarea(cTRB)
				While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO
					cCODBEM := (cTRB)->CODBEM
					Store 0.00 To nBEMP,nBEMR
					While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO .And. (cTRB)->CODBEM == cCODBEM
						Store 0.00 To nOSP,nOSR
						cNUM   := (cTRB)->ORDEM
						cPLANO := (cTRB)->PLANO
						While !Eof() .And. (cTRB)->CCUSTO == cCCUSTO .And. ;
						(cTRB)->CODBEM == cCODBEM .And. (cTRB)->ORDEM == cNUM .And. ;
						(cTRB)->PLANO  == cPLANO

							INCREGUA()
							nOSP += (cTRB)->VALORPRE
							nOSR += (cTRB)->VALORREA
							Dbselectarea(cTRB)
							Dbskip()
						End
						If MV_PAR16 == 1
							Dbselectarea(cTRB2)
							If (cTRB2)->(Dbseek(cNUM+cPLANO))
								nOSP += (cTRB2)->VALORPRE
								nOSR += (cTRB2)->VALORREA
							Endif
						Endif
						Dbselectarea(cTRB)
						nBEMP += nOSP
						nBEMR += nOSR
					End

					MNTR750IP4(cCODBEM,nBEMP,nBEMR)
					nCUSTP += nBEMP
					nCUSTR += nBEMR

				End
				If nCUSTP > 0 .Or. nCUSTR > 0
					SOMALIN()
					SOMALIN()
					MNTR750IP5(STR0046,nCUSTP,nCUSTR)
					nTOTP += nCUSTP
					nTOTR += nCUSTR
				Endif
			End
			If nTOTP > 0 .Or. nTOTR > 0
				SOMALIN()
				SOMALIN()
				MNTR750IP5(STR0047,nTOTP,nTOTR)
			Endif
		Endif
	Endif
	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	oTmpTbl1:Delete()
	oTmpTbl2:Delete()
	oTmpTbl3:Delete()
	oTmpTbl4:Delete()

	RETINDEX("ST9")
	SET FILTER TO
	SET DEVICE TO SCREEN
	If aRETURN[5] = 1
		SET PRINTER TO
		dbCommitAll()
		OURSPOOL(WNREL)
	Endif
	MS_FLUSH()

	(cAliQrySTJ)->( dbCloseArea())
	(cAliQrySTS)->( dbCloseArea())

Return NIL

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SOMALIN  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 21/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function SOMALIN()

	Li++
	If Li > 58
		CABEC(TITULO,CABEC1,CABEC2,NOMEPROG,TAMANHO,nTIPO)
		@Li,000 PSAY REPLICATE("*",157+nTamPro)
		SOMALIN()
		@LI,000 PSAY STR0048 //"Centro Custo.:"
		@LI,015 PSAY Alltrim(cCCUSTO) +" "+ NGSEEK("CTT",cCCUSTO,1,"CTT_DESC01")
		SOMALIN()
		@Li,000 PSAY REPLICATE("*",157+nTamPro)
		SOMALIN()
		If MV_PAR18 = 1

			@Li,000 PSAY STR0080 + Space(3) + ; 	//"Tipo Codigo Insumo"
			Space(nTamPro) + STR0081 			//"Nome Insumo"

			@Li,049+nTamPro PSAY STR0050 //".........Previsto........ ........Realizado........"
			SOMALIN()
			@Li,049+nTamPro PSAY STR0051 //"Quantid. Uni        Custo Quantid. Uni        Custo Diferenca   Var. %"
		Else
			@Li,000 PSAY STR0059 //"Bem              Descricao                                 Custo Previsto   Custo Realizado      Diferenca        Var. %"
		Endif
		SOMALIN()
		@Li,000 PSAY REPLICATE("*",157+nTamPro)
		Li := prow() + 1
		Dbselectarea(cTRB)
	Endif

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NGCUST1STJ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 21/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Calcula o Custo da O.S do arquivo STJ                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function NGCUST1STJ()

	Local nValueCad := 0
	Local aAreaSTJ  := STJ->( GetArea() )

	Private lMMoeda := NGCADICBASE("TL_MOEDA","A","STL",.F.) // Multi-Moeda

	Dbselectarea("STL")
	Dbsetorder(01)
	If Dbseek((cAliQrySTJ)->TJ_FILIAL+(cAliQrySTJ)->TJ_ORDEM+(cAliQrySTJ)->TJ_PLANO)

		While !Eof() .And.	STL->TL_FILIAL == (cAliQrySTJ)->TJ_FILIAL .And.;
							STL->TL_ORDEM  == (cAliQrySTJ)->TJ_ORDEM .And.;
							STL->TL_PLANO  == (cAliQrySTJ)->TJ_PLANO

			//Calcula o custo do Insumo
			MNTA901STL()

			Dbselectarea("STL")
			If MV_PAR11 != 1
				If &(cCONDSTL)
					Dbselectarea("STL")
					Dbskip()
					Loop
				Endif
			Endif

			If !NGTRDTINSU(MV_PAR20,MV_PAR13,MV_PAR14,STL->TL_DTINICI)
				dbSkip()
				Loop
			Endif

			MNTR750AUX(STL->TL_TIPOREG,STL->TL_CODIGO,IIf(NGCADICBASE('TL_PCTHREX','A','STL',.F.),STL->TL_PCTHREX,VAL(STL->TL_HREXTRA)))
			//Verifica o Tipo do Insumo
			MNTR750TIN(STL->TL_TIPOREG,STL->TL_CODIGO)

			dbSelectArea( 'STJ' )
			dbSetOrder( 1 )
			dbSeek( (cAliQrySTJ)->TJ_FILIAL + (cAliQrySTJ)->TJ_ORDEM + (cAliQrySTJ)->TJ_PLANO )
			cOBSERVA := IIf(FieldPos('TJ_MMSYP') > 0,NGMEMOSYP(STJ->TJ_MMSYP),IIf(FieldPos('TJ_OBSERVA')>0,STJ->TJ_OBSERVA," "))

			vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
				STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
				STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
			nQTDHORAS := vVETHORAS[1]

			nValueCad := IIf(lMMoeda, xMoeda(STL->TL_CUSTO, Val(STL->TL_MOEDA), nMoedarel, STL->TL_DTINICI, 2), STL->TL_CUSTO)

			MNTR750TRB(	(cAliQrySTJ)->TJ_CCUSTO, (cAliQrySTJ)->T9_CODBEM,;
						(cAliQrySTJ)->TJ_ORDEM,  (cAliQrySTJ)->TJ_PLANO,;
						(cAliQrySTJ)->TJ_CENTRAB,(cAliQrySTJ)->TJ_SERVICO,;
						cTIPOREG,cCODPROD,cNOME,STL->TL_REPFIM,cOBSERVA,;
						STOD((cAliQrySTJ)->TJ_DTORIGI),nValueCad,nEXTRA,;
						nQTDHORAS,STL->TL_UNIDADE,'STJ',Val(STL->TL_SEQRELA))
			Dbselectarea("STL")
			Dbskip()

		End
	EndIf

	Dbselectarea("ST9")
	Dbsetorder(01)
	Dbseek((cAliQrySTJ)->T9_FILIAL+(cAliQrySTJ)->T9_CODBEM)

	If !EMPTY(ST9->T9_CUSTOHO)
		If MV_PAR16 == 1
			MNTR750PAR(	(cAliQrySTJ)->T9_CODBEM, (cAliQrySTJ)->TJ_SERVICO,;
						(cAliQrySTJ)->TJ_SEQRELA,(cAliQrySTJ)->TJ_ORDEM,;
						(cAliQrySTJ)->TJ_PLANO,  STOD((cAliQrySTJ)->TJ_DTPPINI),;
						(cAliQrySTJ)->TJ_HOPPINI,STOD((cAliQrySTJ)->TJ_DTMPINI),;
						(cAliQrySTJ)->TJ_HOMPINI,STOD((cAliQrySTJ)->TJ_DTPPFIM),;
						(cAliQrySTJ)->TJ_HOPPFIM,STOD((cAliQrySTJ)->TJ_DTMPFIM),;
						(cAliQrySTJ)->TJ_HOMPFIM,STOD((cAliQrySTJ)->TJ_DTPRINI),;
						(cAliQrySTJ)->TJ_HOPRINI,STOD((cAliQrySTJ)->TJ_DTMRINI),;
						(cAliQrySTJ)->TJ_HOMRINI,STOD((cAliQrySTJ)->TJ_DTPRFIM),;
						(cAliQrySTJ)->TJ_HOPRFIM,STOD((cAliQrySTJ)->TJ_DTMRFIM),;
						(cAliQrySTJ)->TJ_HOMRFIM,'STJ')
		Endif
	Endif

	RestArea( aAreaSTJ )

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NGCUST2STJ³ Autor ³                       ³ Data ³ 21/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Calcula o Custo da O.S do arquivo STJ  dos Filhos da Estr. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function NGCUST2STJ(aEstrutura,nBEMPAI,cCCUSTOSTJ,cCENTRA)

	Local i       := 0

	cCENTRTJ  := cCENTRA
	cCONDSTJ2 := '(cAliQrySTJ)->tj_centrab = cCENTRTJ'
	cCONDSTJ2 += ' .And. ((cAliQrySTJ)->tj_servico >= MV_PAR09 .And. (cAliQrySTJ)->tj_servico <= MV_PAR10)'
	lPRI      := .T.

	For i := 1 to len(aEstrutura)
		Dbselectarea("STJ")
		Dbsetorder(13)
		Dbseek((cAliQrySTJ)->TJ_FILIAL+"B"+aEstrutura[i][1]+cCCUSTOSTJ)
		SETREGUA(LASTREC())
		While !Eof() .And. (cAliQrySTJ)->TJ_FILIAL == STJ->TJ_FILIAL .And.;
				(cAliQrySTJ)->TJ_TIPOOS == "B" .And. (cAliQrySTJ)->T9_CODBEM == aEstrutura[i][1] .And.;
				(cAliQrySTJ)->TJ_CCUSTO == cCCUSTOSTJ

			INCREGUA()

			If &(cCONDSTJ2)

				If !NGTRCSTJOS(	aEstrutura[i,3], aEstrutura[i,4], MV_PAR12, MV_PAR20, (cAliQrySTJ)->TJ_SITUACA,;
					(cAliQrySTJ)->TJ_TERMINO, SToD( (cAliQrySTJ)->TJ_DTMRFIM ), SToD((cAliQrySTJ)->TJ_DTMPINI) )
					DbSkip()
					Loop
				EndIf

				Dbselectarea(cTRB4)
				If !Dbseek((cAliQrySTJ)->T9_CODBEM+(cAliQrySTJ)->TJ_ORDEM+(cAliQrySTJ)->TJ_PLANO)
					Dbselectarea("STL")
					Dbsetorder(01)
					Dbseek((cAliQrySTJ)->TJ_FILIAL+(cAliQrySTJ)->TJ_ORDEM+(cAliQrySTJ)->TJ_PLANO)
					While !Eof() .And. STL->TL_FILIAL == (cAliQrySTJ)->TJ_FILIAL .And.;
							STL->TL_ORDEM == (cAliQrySTJ)->TJ_ORDEM .And. STL->TL_PLANO == (cAliQrySTJ)->TJ_PLANO

						//Calcula o custo do Insumo
						MNTA901STL()
						Dbselectarea("STL")
						If MV_PAR11 != 1
							If &(cCONDSTL)
								Dbselectarea("STL")
								Dbskip()
								Loop
							Endif
						Endif

						If !NGTRDTINSU(MV_PAR20,MV_PAR13,MV_PAR14,STL->TL_DTINICI)
							dbSkip()
							Loop
						Endif

						If lPRI
							MNTA750TRL((cAliQrySTJ)->TJ_CCUSTO,(cAliQrySTJ)->TJ_CENTRAB,nBEMPAI)
							lPRI := .F.
						Endif

						MNTR750AUX(STL->TL_TIPOREG,STL->TL_CODIGO,IIf(NGCADICBASE('TL_PCTHREX','A','STL',.F.),STL->TL_PCTHREX,VAL(STL->TL_HREXTRA)))
						//Verifica o Tipo do Insumo
						MNTR750TIN(STL->TL_TIPOREG,STL->TL_CODIGO)

						cOBSERVA := IIf(FieldPos('TJ_MMSYP') > 0,NGMEMOSYP(STJ->TJ_MMSYP),IIf(FieldPos('TJ_OBSERVA')>0,STJ->TJ_OBSERVA," "))

						vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
							STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
							STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
						nQTDHORAS := vVETHORAS[1]

						MNTR750TR3(nBEMPAI,(cAliQrySTJ)->T9_CODBEM,(cAliQrySTJ)->TJ_ORDEM,(cAliQrySTJ)->TJ_PLANO,cTIPOREG,;
							cCODPROD,(cAliQrySTJ)->TJ_SERVICO,cNOME,STL->TL_REPFIM,cOBSERVA,;
							(cAliQrySTJ)->TJ_DTORIGI,STL->TL_CUSTO,nQTDHORAS,STL->TL_UNIDADE,;
							'STJ',Val(STL->TL_SEQRELA))
						Dbselectarea("STL")
						Dbskip()
					End

					Dbselectarea("ST9")
					Dbsetorder(01)
					Dbseek((cAliQrySTJ)->T9_FILIAL+(cAliQrySTJ)->T9_CODBEM)
					If !EMPTY(ST9->T9_CUSTOHO)

						If MV_PAR16 == 1
							MNTR750PAR(	(cAliQrySTJ)->T9_CODBEM, (cAliQrySTJ)->TJ_SERVICO,;
										(cAliQrySTJ)->TJ_SEQRELA,(cAliQrySTJ)->TJ_ORDEM,;
										(cAliQrySTJ)->TJ_PLANO,  STOD((cAliQrySTJ)->TJ_DTPPINI),;
										(cAliQrySTJ)->TJ_HOPPINI,STOD((cAliQrySTJ)->TJ_DTMPINI),;
										(cAliQrySTJ)->TJ_HOMPINI,STOD((cAliQrySTJ)->TJ_DTPPFIM),;
										(cAliQrySTJ)->TJ_HOPPFIM,STOD((cAliQrySTJ)->TJ_DTMPFIM),;
										(cAliQrySTJ)->TJ_HOMPFIM,STOD((cAliQrySTJ)->TJ_DTPRINI),;
										(cAliQrySTJ)->TJ_HOPRINI,STOD((cAliQrySTJ)->TJ_DTMRINI),;
										(cAliQrySTJ)->TJ_HOMRINI,STOD((cAliQrySTJ)->TJ_DTPRFIM),;
										(cAliQrySTJ)->TJ_HOPRFIM,STOD((cAliQrySTJ)->TJ_DTMRFIM),;
										(cAliQrySTJ)->TJ_HOMRFIM,'STJ')
						Endif
					Endif
				Endif
			Endif
			Dbselectarea("STJ")
			Dbskip()
		End
	Next i

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NGCUST3STJ³ Autor ³                       ³ Data ³ 21/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Calcula o Custo da O.S do PAI DA ESTRUTURA(STJ)            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function NGCUST3STJ(nBEMPAI,cCCUSTO3,cTRAB3)

	cTRABA3   := cTRAB3
	cCONDSTJ3 := '(cAliQrySTJ)->tj_centrab = cTRABA3'
	cCONDSTJ3 += ' .And. ((cAliQrySTJ)->tj_servico >= MV_PAR09 .And. (cAliQrySTJ)->tj_servico <= MV_PAR10)'

	Dbselectarea("STJ")
	Dbsetorder(13)
	Dbseek((cAliQrySTJ)->TJ_FILIAL+"B"+nBEMPAI+cCCUSTO3)
	SETREGUA(LASTREC())
	While !Eof() .And. (cAliQrySTJ)->TJ_FILIAL == STJ->TJ_FILIAL .And.;
			(cAliQrySTJ)->TJ_TIPOOS == "B" .And. (cAliQrySTJ)->T9_CODBEM == nBEMPAI .And.;
			(cAliQrySTJ)->TJ_CCUSTO == cCCUSTO3
		INCREGUA()
		If &(cCONDSTJ3)

			If !NGTRCSTJOS(	MV_PAR13, MV_PAR14, MV_PAR12, MV_PAR20, (cAliQrySTJ)->TJ_SITUACA, (cAliQrySTJ)->TJ_TERMINO,;
					SToD( (cAliQrySTJ)->TJ_DTMRFIM ), SToD((cAliQrySTJ)->TJ_DTMPINI ) )
				DbSkip()
				Loop
			EndIf

			Dbselectarea(cTRB4)
			If !Dbseek((cAliQrySTJ)->T9_CODBEM+(cAliQrySTJ)->TJ_ORDEM+(cAliQrySTJ)->TJ_PLANO)
         //Calcula o Custo da O.S do Pai da Estrutura
				NGCUST1STJ()
			Endif
		Endif
		Dbselectarea("STJ")
		Dbskip()
	End

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NGCUST1STS³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 21/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Calcula o Custo da O.S do arquivo STS                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function NGCUST1STS()

	Local aAreaSTS := STS->( GetArea() )

	Private lMMoeda := NGCADICBASE("TL_MOEDA","A","STL",.F.) // Multi-Moeda

	Dbselectarea("STT")
	Dbseek((cAliQrySTS)->TS_FILIAL+(cAliQrySTS)->TS_ORDEM+(cAliQrySTS)->TS_PLANO)
	While !Eof() .And. STT->TT_FILIAL == (cAliQrySTS)->TS_FILIAL .And.;
			STT->TT_ORDEM == (cAliQrySTS)->TS_ORDEM .And. STT->TT_PLANO == (cAliQrySTS)->TS_PLANO

		//Calcula o custo do Insumo
		MNTA901STT()
		Dbselectarea("STT")
		If MV_PAR11 != 1
			If &(cCONDSTT)
				Dbselectarea("STT")
				Dbskip()
				Loop
			Endif
		Endif

		If !NGTRDTINSU(MV_PAR20,MV_PAR13,MV_PAR14,STT->TT_DTINICI)
			dbSkip()
			Loop
		Endif

		MNTR750AUX(STL->TL_TIPOREG,STL->TL_CODIGO,IIf(NGCADICBASE('TT_PCTHREX','A','STT',.F.),STT->TT_PCTHREX,VAL(STT->TT_HREXTRA)))
		//Verifica o Tipo do Insumo
		MNTR750TIN(STT->TT_TIPOREG,STT->TT_CODIGO)

		cOBSERVA := IIf(FieldPos('TS_MMSYP') > 0,NGMEMOSYP(STS->TS_MMSYP),IIf(FieldPos('TS_OBSERVA')>0,STS->TS_OBSERVA," "))

		vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
			STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
			STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
		nQTDHORAS := vVETHORAS[1]

		nValueCad := IIf(lMMoeda, xMoeda(STT->TT_CUSTO, Val(STT->TT_MOEDA), nMoedarel, STT->TT_DTINICI, 2), STT->TT_CUSTO)

		MNTR750TRB(	(cAliQrySTS)->TS_CCUSTO,(cAliQrySTS)->T9_CODBEM,;
			(cAliQrySTS)->TS_ORDEM,(cAliQrySTS)->TS_PLANO,;
			(cAliQrySTS)->TS_CENTRAB,(cAliQrySTS)->TS_SERVICO,;
			cTIPOREG,cCODPROD,cNOME,STT->TT_REPFIM,cOBSERVA,;
			STOD((cAliQrySTS)->TS_DTORIGI),nValueCad,nEXTRA,;
			nQTDHORAS,STT->TT_UNIDADE,'STS',Val(STT->TT_SEQRELA))

		Dbselectarea("STT")
		Dbskip()
	End
	Dbselectarea("ST9")
	If !EMPTY(ST9->T9_CUSTOHO)

		If MV_PAR16 == 1
			MNTR750PAR(	(cAliQrySTS)->T9_CODBEM, (cAliQrySTS)->TS_SERVICO,;
						(cAliQrySTS)->TS_SEQRELA,(cAliQrySTS)->TS_ORDEM,;
						(cAliQrySTS)->TS_PLANO,  STOD((cAliQrySTS)->TS_DTPPINI),;
						(cAliQrySTS)->TS_HOPPINI,STOD((cAliQrySTS)->TS_DTMPINI),;
						(cAliQrySTS)->TS_HOMPINI,STOD((cAliQrySTS)->TS_DTPPFIM),;
						(cAliQrySTS)->TS_HOPPFIM,STOD((cAliQrySTS)->TS_DTMPFIM),;
						(cAliQrySTS)->TS_HOMPFIM,STOD((cAliQrySTS)->TS_DTPRINI),;
						(cAliQrySTS)->TS_HOPRINI,STOD((cAliQrySTS)->TS_DTMRINI),;
						(cAliQrySTS)->TS_HOMRINI,STOD((cAliQrySTS)->TS_DTPRFIM),;
						(cAliQrySTS)->TS_HOPRFIM,STOD((cAliQrySTS)->TS_DTMRFIM),;
						(cAliQrySTS)->TS_HOMRFIM,'STS')
		Endif
	Endif

	RestArea( aAreaSTS )

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NGCUST2STS³ Autor ³                       ³ Data ³ 21/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Calcula o Custo da O.S do arquivo STS  dos Filhos da Estr. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function  NGCUST2STS(aEstrutura,nBEMPAI,cCCUSTOSTS,cCENTRA)

	Local i       := 0

	cCENTRTS  := cCENTRA
	cCONDSTS2 := '(cAliQrySTS)->TS_SITUACA = "L" .And. (cAliQrySTS)->TS_TERMINO = "S" .And. (cAliQrySTS)->TS_CENTRAB = cCENTRTS'
	cCONDSTS2 += ' .And. ((cAliQrySTS)->TS_SERVICO >= MV_PAR09 .And. (cAliQrySTS)->TS_SERVICO <= MV_PAR10)'
	lPRI := .T.

	For i := 1 to len(aEstrutura)
		Dbselectarea("STS")
		Dbsetorder(09)
		Dbseek((cAliQrySTS)->TS_FILIAL+"B"+aEstrutura[i][1]+cCCUSTOSTS)
		SETREGUA(LASTREC())
		While !Eof() .And. (cAliQrySTS)->TS_FILIAL == STS->TS_FILIAL .And.;
				(cAliQrySTS)->TS_TIPOOS == "B" .And. (cAliQrySTS)->T9_CODBEM == aEstrutura[i][1] .And.;
				(cAliQrySTS)->TS_CCUSTO == cCCUSTOSTS

			INCREGUA()

			If &(cCONDSTS2)

				If !NGTRCSTSOS(	aEstrutura[i,3], aEstrutura[i,4], MV_PAR20, (cAliQrySTS)->TS_SITUACA,;
					(cAliQrySTS)->TS_TERMINO, SToD( (cAliQrySTS)->TS_DTMRFIM ) )
					DbSkip()
					Loop
				EndIf

				Dbselectarea(cTRB4)
				If !Dbseek((cAliQrySTS)->T9_CODBEM+(cAliQrySTS)->TS_ORDEM+(cAliQrySTS)->TS_PLANO)

					Dbselectarea("STT")
					Dbsetorder(01)
					Dbseek((cAliQrySTS)->TS_FILIAL+(cAliQrySTS)->TS_ORDEM+(cAliQrySTS)->TS_PLANO)
					While !Eof() .And. STT->TT_FILIAL == (cAliQrySTS)->TS_FILIAL .And.;
							STT->TT_ORDEM == (cAliQrySTS)->TS_ORDEM .And. STT->TT_PLANO == (cAliQrySTS)->TS_PLANO

						//Calcula o custo do Insumo
						MNTA901STT()
						Dbselectarea("STT")
						If MV_PAR11 != 1
							If &(cCONDSTT)
								Dbselectarea("STT")
								Dbskip()
								Loop
							Endif
						Endif

						If !NGTRDTINSU(MV_PAR20,MV_PAR13,MV_PAR14,STT->TT_DTINICI)
							dbSkip()
							Loop
						Endif

						If lPRI
							MNTA750TRL((cAliQrySTS)->TS_CCUSTO,(cAliQrySTS)->TS_CENTRAB,nBEMPAI)
							lPRI := .F.
						Endif

						MNTR750AUX(STL->TL_TIPOREG,STL->TL_CODIGO,IIf(NGCADICBASE('TT_PCTHREX','A','STT',.F.),STT->TT_PCTHREX,VAL(STT->TT_HREXTRA)))
						//Verifica o Tipo de Insumo
						MNTR750TIN(STT->TT_TIPOREG,STT->TT_CODIGO)

						cOBSERVA := IIf(FieldPos('TS_MMSYP') > 0,NGMEMOSYP(STS->TS_MMSYP),IIf(FieldPos('TS_OBSERVA')>0,STS->TS_OBSERVA," "))

						vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
							STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
							STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
						nQTDHORAS := vVETHORAS[1]

						MNTR750TR3(nBEMPAI,(cAliQrySTS)->T9_CODBEM,(cAliQrySTS)->TS_ORDEM,(cAliQrySTS)->TS_PLANO,cTIPOREG,;
							cCODPROD,(cAliQrySTS)->TS_SERVICO,cNOME,STT->TT_REPFIM,cOBSERVA,;
							STOD((cAliQrySTS)->TS_DTORIGI),STT->TT_CUSTO,nQTDHORAS,STT->TT_UNIDADE,;
							'STS',Val(STT->TT_SEQRELA))
						Dbselectarea("STT")
						Dbskip()
					End
					Dbselectarea("ST9")
					Dbsetorder(01)
					Dbseek((cAliQrySTS)->T9_FILIAL+(cAliQrySTS)->T9_CODBEM)
					If !EMPTY(ST9->T9_CUSTOHO)

						If MV_PAR16 == 1
							MNTR750PAR(	(cAliQrySTS)->T9_CODBEM, (cAliQrySTS)->TS_SERVICO,;
										(cAliQrySTS)->TS_SEQRELA,(cAliQrySTS)->TS_ORDEM,;
										(cAliQrySTS)->TS_PLANO,  STOD((cAliQrySTS)->TS_DTPPINI),;
										(cAliQrySTS)->TS_HOPPINI,STOD((cAliQrySTS)->TS_DTMPINI),;
										(cAliQrySTS)->TS_HOMPINI,STOD((cAliQrySTS)->TS_DTPPFIM),;
										(cAliQrySTS)->TS_HOPPFIM,STOD((cAliQrySTS)->TS_DTMPFIM),;
										(cAliQrySTS)->TS_HOMPFIM,STOD((cAliQrySTS)->TS_DTPRINI),;
										(cAliQrySTS)->TS_HOPRINI,STOD((cAliQrySTS)->TS_DTMRINI),;
										(cAliQrySTS)->TS_HOMRINI,STOD((cAliQrySTS)->TS_DTPRFIM),;
										(cAliQrySTS)->TS_HOPRFIM,STOD((cAliQrySTS)->TS_DTMRFIM),;
										(cAliQrySTS)->TS_HOMRFIM,'STS')
						Endif
					Endif
				Endif
			Endif
			Dbselectarea("STS")
			Dbskip()
		End
	Next i

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NGCUST3STS³ Autor ³                       ³ Data ³ 21/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Calcula o Custo da O.S do PAI DA ESTRUTURA(STS)            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function NGCUST3STS(nBEMPAI,cCCUSTO3,cTRAB3)

	cTRABA3 := cTRAB3
	cCONDSTS3 := '(cAliQrySTS)->TS_SITUACA = "L" .And. (cAliQrySTS)->TS_TERMINO = "S" .And. (cAliQrySTS)->TS_CENTRAB = cTRABA3'
	cCONDSTS3 += ' .And. ((cAliQrySTS)->TS_SERVICO >= MV_PAR09 .And. (cAliQrySTS)->TS_SERVICO <= MV_PAR10)'

	Dbselectarea("STS")
	Dbsetorder(09)
	Dbseek((cAliQrySTS)->TS_FILIAL+"B"+nBEMPAI+cCCUSTO3)
	SETREGUA(LASTREC())
	While !Eof() .And. (cAliQrySTS)->TS_FILIAL == STS->TS_FILIAL .And.;
			(cAliQrySTS)->TS_TIPOOS = "B" .And.;
			(cAliQrySTS)->T9_CODBEM == nBEMPAI .And.;
			(cAliQrySTS)->TS_CCUSTO == cCCUSTO3

		INCREGUA()
		If &(cCONDSTS3)
			If !NGTRCSTSOS(	MV_PAR13, MV_PAR14, MV_PAR20, (cAliQrySTS)->TS_SITUACA, (cAliQrySTS)->TS_TERMINO,;
					SToD( (cAliQrySTS)->TS_DTMRFIM ) )
				DbSkip()
				Loop
			EndIf
			Dbselectarea(cTRB4)
			If !Dbseek((cAliQrySTS)->T9_CODBEM+(cAliQrySTS)->TS_ORDEM+(cAliQrySTS)->TS_PLANO)
         //Calcula o Custo da O.S do Pai da Estrutura
				NGCUST1STS()
			Endif
		Endif
		Dbselectarea("STS")
		Dbskip()
	End

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NGT()     ³ Autor ³ Elisangela Costa      ³ Data ³ 12/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Testa o parametro da Familia se Custo da Estrutura for sim ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ RCBM007IMP                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function NGT()

	Local LRET := .T.
	If MV_PAR19 == 2
		If !EMPTY(MV_PAR08)
			If MV_PAR08 <> MV_PAR07
				MSGSTOP(STR0060)//"O Parametro de De/Ate Familia deve ser igual para quando considerar custo da estrutura"
				LRET := .F.
			Endif
		Endif
	Endif

Return LRET

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750TRB³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Grava arquivo temporario (TRB)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr7507                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750TRB(cCUST,cBEM,cOROS,cPLANO,cCTRAB,cSERV,cTIP,cCODPR,;
		cNOM,cREPF,cOBSM,dDTORI,nCUST,nEXTRA,nQTD,cUNID,cARQ,nSEQREL)

	Dbselectarea(cTRB)
	Dbsetorder(1)
	If !(cTRB)->(Dbseek(cCUST+cBEM+cOROS+cPLANO+cTIP+cCODPR))
		(cTRB)->(DbAppend())
		(cTRB)->PLANO   := cPLANO
		(cTRB)->CCUSTO  := cCUST
		(cTRB)->CTRAB   := cCTRAB
		(cTRB)->CODBEM  := cBEM
		(cTRB)->SERVICO := cSERV
		(cTRB)->ORDEM   := cOROS
		(cTRB)->TIPOREG := cTIP
		(cTRB)->PRODUTO := cCODPR
		(cTRB)->NOMPROD := cNOM
		(cTRB)->TERMINO := cREPF
		(cTRB)->ARQUIVO := If(!Empty(cOBSM),cARQ,Space(Len((cTRB)->ARQUIVO)))
		(cTRB)->ORIGI   := dDTORI
		If MV_PAR17 = 2
			Dbselectarea(cTRB4)
			(cTRB4)->(DbAppend())
			(cTRB4)->CODBEM := cBEM
			(cTRB4)->ORDEM  := cOROS
			(cTRB4)->PLANO  := cPLANO
		Endif
	Endif

	nCUSTO := IIf(MV_PAR15 = 1,nCUST * nEXTRA,nCUSTO * nQTD)

	If nSEQREL == 0
		(cTRB)->QUANTPRE += nQTD
		(cTRB)->UNIDPRE  := cUNID
		(cTRB)->VALORPRE += nCUSTO
	Else
		(cTRB)->QUANTREA += nQTD
		(cTRB)->UNIDREA  := cUNID
		(cTRB)->VALORREA += nCUSTO
	Endif

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750TIN³ Autor ³ Inacio Luiz Kolling   ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Verifica o tipo do insumo                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR750TIN(cTIP,cCOD)

	Local cCODX := cCOD
	Local lMMoeda := NGCADICBASE("TL_MOEDA","A","STL",.F.) // Multi-Moeda

	If cTIP <> "P"
		If cTIP = "E"
			cCODX := Substr(cCOD,1,3)
		ElseIf cTIP = "T"
			cCODX := Substr(cCOD,1,Len(sa2->a2_cod))
		Else
			cCODX := Substr(cCOD,1,6)
		Endif
	Endif

	If cTIP == "M"
		cNOMTIPO := STR0087 //"FUN."
		Dbselectarea("ST1")
		Dbseek(xFilial("ST1", (cAliQrySTJ)->TJ_FILIAL) + cCODX)
		cNOME  := ST1->T1_NOME
		nCUSTO := IIf(lMMoeda, xMoeda(ST1->T1_SALARIO, 1, nMoedarel, dDataBase, 2), ST1->T1_SALARIO)
		nCUSTO := (nCUSTO * nEXTRA)

		If MV_PAR17 == 1
			cNOMTIPO := STR0088 //"ESP."
			Dbselectarea("ST2")
			If Dbseek(xFilial("ST2", (cAliQrySTJ)->TJ_FILIAL) + cCODX)
				cTIPOREG := "E"
				cCODPROD := ST2->T2_ESPECIA
				Dbselectarea("ST0")
				Dbseek(xFilial("ST0", (cAliQrySTJ)->TJ_FILIAL) + cCODPROD)
				cNOME  := ST0->T0_NOME
				nCUSTO := IIf(EMPTY(nCUSTO), IIf(lMMoeda, xMoeda(ST0->T0_SALARIO, 1, nMoedarel, dDataBase, 2), ST0->T0_SALARIO), nCUSTO)
				nCUSTO := nCUSTO * nEXTRA
			Endif
		Endif

	Elseif cTIP == "P"
		cNOMTIPO := STR0089 //"PRO."
		Dbselectarea("SB1")
		Dbseek(xFilial( "SB1", (cAliQrySTJ)->TJ_FILIAL) + cCODX)
		cNOME  := SB1->B1_DESC
		nCUSTO := SB1->B1_CUSTD
		nCUSTO := IIf(lMMoeda, xMoeda(nCUSTO, Val(SB1->B1_MCUSTD), nMoedarel, dDataBase, 2), nCUSTO)
	Elseif cTIP == "T"
		cNOMTIPO := STR0090 //"TER."
		DbSelectArea("SA2")
		DbSetOrder(01)
		DbSeek(xFilial("SA2", (cAliQrySTJ)->TJ_FILIAL) + cCODX)
		cNOME := SA2->A2_NOME
		DbSelectArea("TPO")
		DbSetOrder(01)
		If DbSeek(xFilial("TPO", (cAliQrySTJ)->TJ_FILIAL) + cCODX)
			nCUSTO := TPO->TPO_CUSTO
			nCUSTO := IIf(lMMoeda, xMoeda(nCusto, 1, nMoedarel, dDataBase, 2), nCusto)
		EndIf
	Elseif cTIP == "F"
		cNOMTIPO := STR0091 //"FER."
		Dbselectarea("SH4")
		Dbseek(xFilial("SH4", (cAliQrySTJ)->TJ_FILIAL) + cCODX)
		cNOME  := SH4->H4_DESCRI
		nCUSTO := SH4->H4_CUSTOH
		nCUSTO := IIf(lMMoeda, xMoeda(nCusto, 1, nMoedarel, dDataBase, 2), nCusto)
	Else
		cNOMTIPO := STR0088 //"ESP."
		Dbselectarea("ST0")
		Dbseek(xFilial("ST0", (cAliQrySTJ)->TJ_FILIAL) + cCODX)
		cNOME  := ST0->T0_NOME
		nCUSTO := ST0->T0_SALARIO
		nCUSTO := IIf(lMMoeda, xMoeda(nCusto, 1, nMoedarel, dDataBase, 2), nCusto)
		nCUSTO := nCUSTO * nEXTRA
	Endif

Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750PAR³ Autor ³ Inacio Luiz Kolling   ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Verifica as horas de paradas                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR750                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750PAR(cBEM,cSERV,nSEQ,cORDEM,cPLANO,dDTPPINI,cHOPPINI,;
		dDTMPINI,cHOMPINI,dDTPPFIM,cHOPPFIM,dDTMPFIM,;
		cHOMPFIM,dDTPRINI,cHOPRINI,dDTMRINI,cHOMRINI,;
		dDTPRFIM,cHOPRFIM,dDTMRFIM,cHOMRFIM,cARQ)

	Private lMMoeda := NGCADICBASE("TL_MOEDA","A","STL",.F.) // Multi-Moeda

	Dbselectarea("STF")
	Dbsetorder(01)
	Dbseek((cAliQrySTJ)->TJ_FILIAL+cBEM+cSERV+nSEQ)
	If STF->TF_PARADA == "S" .Or. cPLANO == "000000"
		dINI := IIf(!EMPTY(dDTPPINI),dDTPPINI,dDTMPINI)
		hINI := IIf(!EMPTY(dDTPPINI),cHOPPINI,cHOMPINI)
		dFIM := IIf(!EMPTY(dDTPPFIM),dDTPPFIM,dDTMPFIM)
		hFIM := IIf(!EMPTY(dDTPPFIM),cHOPPFIM,cHOMPFIM)

		nPRE := NGCALEBEM(dINI,hINI,dFIM,hFIM,cBEM)
		nPRE := HtoM(nPRE)/60
		nPRE := IIf(nPRE < 0.00,0.00,nPRE)

		dINI := IIf(!EMPTY(dDTPRINI),dDTPRINI,dDTMRINI)
		hINI := IIf(!EMPTY(dDTPRINI),cHOPRINI,cHOMRINI)
		dFIM := IIf(!EMPTY(dDTPRFIM),dDTPRFIM,dDTMRFIM)
		hFIM := IIf(!EMPTY(dDTPRFIM),cHOPRFIM,cHOMRFIM)

		nREA    := NGCALEBEM(dINI,hINI,dFIM,hFIM,cBEM)
		nREA    := HTOM(nREA)/60
		nREA    := IIf(nREA < 0.00,0.00,nREA)
		lPARADA := .T.

		If cARQ = 'STJ'
			If STJ->(FIELDPOS("TJ_PARADA")) > 0
				If STJ->TJ_PARADA == "N"
					lPARADA := .F.
				Endif
			Endif
		Else
			If STS->(FIELDPOS("TS_PARADA")) > 0
				If STS->TS_PARADA == "N"
					lPARADA := .F.
				Endif
			Endif
		Endif
		If lPARADA
			Dbselectarea(cTRB2)
			If !Dbseek(cORDEM+cPLANO)
				DbAppend()
				(cTRB2)->ORDEM := cORDEM
				(cTRB2)->PLANO := cPLANO
			Endif
			(cTRB2)->QUANTPRE += nPRE
			(cTRB2)->QUANTREA += nREA

			nCstHrST9 := IIf(lMMoeda, xMoeda(ST9->T9_CUSTOHO, Val(ST9->T9_MOEDA), nMoedarel, dDatabase, 2), ST9->T9_CUSTOHO)

			(cTRB2)->VALORPRE += (nPRE * nCstHrST9)
			(cTRB2)->VALORREA += (nREA * nCstHrST9)
		Endif
	Endif

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750TRL³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Grava arquivo temporario (TRB)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTA750TRL(cvCUS,cvCENT,cBEMPAI)
	Dbselectarea(cTRB)
	Dbsetorder(02)
	If !Dbseek(cvCUS+cvCENT+cBEMPAI)
		(cTRB)->(DbAppend())
		(cTRB)->CCUSTO := cvCUS
		(cTRB)->CTRAB  := cvCENT
		(cTRB)->CODBEM := cBEMPAI
	Endif

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750AUX³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Alimenta variaveis                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750AUX(ctipo,ccodi,nhre)

	cTIPOREG := ctipo
	cCODPROD := ccodi
	cNOME    := SPACE(40)
	nCUSTO   := 0.00
	nEXTRA   := IIf(MV_PAR15 == 2, 1+(nhre/100), 1)
			//So aplica Hr.Extra no Custo Standard

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750TR3³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Grava arquivo temporario (TRB3/TRB4)                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750TR3(cBEMP,cBEM,cORD,cPLANO,cTIPR,cPROD,cSERV,cNOM,cREP,;
	cOBS,dDTORI,nCUST,nQTD,nUNID,cARQ,cSEQ)
	Dbselectarea(cTRB3)
	If !(cTRB3)->(Dbseek(cBEMP+cBEM+cORD+cPLANO+cTIPR+cPROD))
		(cTRB3)->(DbAppend())
		(cTRB3)->BEMPAI  := cBEMP
		(cTRB3)->CODBEM  := cBEM
		(cTRB3)->ORDEM   := cORD
		(cTRB3)->PLANO   := cPLANO
		(cTRB3)->SERVICO := cSERV
		(cTRB3)->TIPOREG := cTIPR
		(cTRB3)->PRODUTO := cPROD
		(cTRB3)->NOMPROD := cNOM
		(cTRB3)->TERMINO := cREP
		(cTRB3)->ARQUIVO := If(!Empty(cOBS),cARQ,Space(Len((cTRB3)->ARQUIVO)))
		(cTRB3)->ORIGI   := dDTORI

		Dbselectarea(cTRB4)
		(cTRB4)->(DbAppend())
		(cTRB4)->CODBEM := cBEM
		(cTRB4)->ORDEM  := cORD
		(cTRB4)->PLANO  := cPLANO
	Endif
	nCUSTO := IIf(MV_PAR15 = 1,nCUST * nEXTRA,nCUSTO * nQTD)
	If cSEQ == 0
		(cTRB3)->QUANTPRE += nQTD
		(cTRB3)->UNIDPRE  := nUNID
		(cTRB3)->VALORPRE += nCUSTO
	Else
		(cTRB3)->QUANTREA += nQTD
		(cTRB3)->UNIDREA  := nUNID
		(cTRB3)->VALORREA += nCUSTO
	Endif

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750MEM³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Imprime campo memo                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750MEM(cTMEMO,cORDEM,cPLANO)

	Dbselectarea(cTMEMO)
	Dbsetorder(01)
	Dbseek(xFILIAL(cTMEMO)+cORDEM+cPLANO)
	cMEMO := IIf(cTMEMO = 'STJ',IIf(FieldPos('TJ_MMSYP') > 0,NGMEMOSYP(STJ->TJ_MMSYP),IIf(FieldPos('TJ_OBSERVA')>0,STJ->TJ_OBSERVA," ")),;
		IIf(FieldPos('TS_MMSYP') > 0,NGMEMOSYP(STS->TS_MMSYP),IIf(FieldPos('TS_OBSERVA')>0,STS->TS_OBSERVA," ")))
	NG2IMPMEMO(cMEMO,100,0,STR0042,.F.,.F.,"SOMALIN()")

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750IP1³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Imprime insumos                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750IP1(cTIPOR,nVLREA,nVLPRE,nQTDPRE,nQTDREA,cPROD,;
		cNOMPR,cUNIPRE,cUNIREA)

	Local cPictuCus  := X3Picture( 'TL_CUSTO' )

	cTIPOREG := cTIPOR
	nPOS     := aSCAN(aTIPOREG,{|x| (x[1]) == cTIPOREG})
	cTIPO    := IIf(nPOS > 0, aTIPOREG[nPOS][2], STR0086) //"MDO."
	nDIF     := nVLREA - nVLPRE

	If nVLPRE > nVLREA
		nPER := -( Round( 100 - ( ( nVLREA * 100 ) / nVLPRE ), 2 ) )
	ElseIf nVLPRE < nVLREA
		nPER := Round( 100 - ( nVLPRE * 100 ) / nVLREA, 2 )
	Else
		nPER := 0
	EndIf

	nOSP     += nVLPRE
	nOSR     += nVLREA
	@ Li,000 PSAY cTIPO
	@ Li,005 PSAY cPROD
	@ Li,021+nTamPro PSAY Left(cNOMPR,25)
	If cTIPOR <> "P"
		@ Li,043+nTamPro PSAY NGRHODSEXN(nQTDPRE, "D") Picture cPictuCus
	Else
		@ Li,043+nTamPro PSAY nQTDPRE Picture cPictuCus
	EndIf
	@ Li,067+nTamPro PSAY cUNIPRE Picture "@!"
	@ Li,067+nTamPro PSAY nVLPRE  Picture cPictuCus
	If cTIPOR <> "P"
		@ Li,088+nTamPro PSAY NGRHODSEXN(nQTDREA, "D") Picture cPictuCus
	Else
		@ Li,088+nTamPro PSAY nQTDREA Picture cPictuCus
	EndIf
	@ Li,111+nTamPro PSAY cUNIREA Picture "@!"
	@ Li,111+nTamPro PSAY nVLREA  Picture cPictuCus
	@ Li,127+nTamPro PSAY nDIF    Picture cPictuCus
	@ Li,141+nTamPro PSAY nPER    Picture cPictuCus
	SOMALIN()

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750IP2³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Imprime Totais                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750IP2(cDESC,nVHOSP,nVOSP,nVHOSR,nVOSR)

	Local nDIF      := nVOSR - nVOSP
	Local nPER      := 0
	Local cPictuCus := X3Picture( 'TL_CUSTO' )

	If nVOSP > nVOSR
		nPER := -( Round( 100 - ( ( nVOSR * 100 ) / nVOSP ), 2 ) )
	ElseIf nVOSP < nVOSR
		nPER := Round( 100 - ( nVOSP * 100 ) / nVOSR, 2 )
	Else
		nPER := 0
	EndIf

	@ Li,005 PSAY cDESC
	@ Li,066+nTamPro PSAY nVOSP  Picture cPictuCus
	@ Li,110+nTamPro PSAY nVOSR  Picture cPictuCus
	@ Li,126.2+nTamPro PSAY nDIF   Picture cPictuCus
	@ Li,141+nTamPro PSAY nPER   Picture cPictuCus
	SOMALIN()
	SOMALIN()

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750IP3³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Imprime Totais                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750IP3(cDESC,nQTAPRE,nVLRPRE,nQTDREA,nVLRREA)

	Local cPictuCus  := X3Picture( 'TL_CUSTO' )

	@ Li,005 PSAY cDESC
	@ Li,043+nTamPro PSAY NGRHODSEXN(nQTAPRE, "D") Picture cPictuCus
	@ Li,059+nTamPro PSAY nVLRPRE Picture cPictuCus
	@ Li,073+nTamPro PSAY NGRHODSEXN(nQTDREA, "D") Picture cPictuCus
	@ Li,089+nTamPro PSAY nVLRREA Picture cPictuCus
	nOSP += (cTRB2)->VALORPRE
	nOSR += (cTRB2)->VALORREA
	SOMALIN()
	SOMALIN()

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750IP4³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Imprime Totais                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750IP4(cBEM,nVBEMP,nVBEMR)

	Local nDIF 		 := nVBEMR - nVBEMP
	Local nPER 		 := 0
	Local cPictuCus  := X3Picture( 'TL_CUSTO' )

	If nVBEMP > nVBEMR
		nPER := -( Round( 100 - ( ( nVBEMR * 100 ) / nVBEMP ), 2 ) )
	ElseIf nVBEMP < nVBEMR
		nPER := Round( 100 - ( nVBEMP * 100 ) / nVBEMR, 2 )
	Else
		nPER := 0
	EndIf

	SOMALIN()
	@ Li,000 PSAY cBEM
	@ Li,017 PSAY NGSEEK('ST9',cBEM,1,'T9_NOME')
	@ Li,063+nTamPro PSAY nVBEMP  Picture cPictuCus
	@ Li,081+nTamPro PSAY nVBEMR  Picture cPictuCus
	@ Li,099+nTamPro PSAY nDIF    Picture cPictuCus
	@ Li,117+nTamPro PSAY nPER    Picture cPictuCus

Return .t.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR750IP5³ Autor ³In cio Luiz Kolling    ³ Data ³21/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Imprime Totais                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³mntr750                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MNTR750IP5(cDESC,nVCBEMPAI,nVCBEMPAR)

	Local nDIF 		 := nVCBEMPAR - nVCBEMPAI
	Local nPER 		 := 0
	Local cPictuCus  := X3Picture( 'TL_CUSTO' )

	If nVCBEMPAI > nVCBEMPAR
		nPER := -( Round( 100 - ( ( nVCBEMPAR * 100 ) / nVCBEMPAI ), 2 ) )
	ElseIf nVCBEMPAI < nVCBEMPAR
		nPER := Round( 100 - ( nVCBEMPAI * 100 ) / nVCBEMPAR, 2 )
	Else
		nPER := 0
	EndIf

	@ Li,000 PSAY cDESC
	@ Li,059+nTamPro PSAY nVCBEMPAI Picture cPictuCus
	@ Li,077+nTamPro PSAY nVCBEMPAR Picture cPictuCus
	@ Li,095+nTamPro PSAY nDIF      Picture cPictuCus
	@ Li,113+nTamPro PSAY nPER      Picture cPictuCus
	SOMALIN()
	SOMALIN()

Return .t.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR750MD()

@author Pedro Henrique Soares de Souza
@since 16/05/2013
@version MP11
@return nMoeda variável contendo a opção da moeda para que o relatório seja impresso.

/*/
//---------------------------------------------------------------------

Function MNTR750MD()

	Local aItems	:= {'1=' + STR0078+ ' 1', '2=' + STR0078+ ' 2', '3=' + STR0078+ ' 3',;
									'4=' +STR0078+ ' 4', '5=' +STR0078+ ' 5'}
	Local nMoeda  := 1
	Local oCombo, oDlg

	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0092) FROM 000,000 TO 130,300 PIXEL

	@ 009,010 Say STR0093 OF oDlg Pixel

	oCombo := TComboBox():New(020,009,{|u|IIf(PCount()>0,nMoeda:=u,nMoeda)},aItems, 65,20,oDlg,,,,,,.T.,,,,,,,,,'nMoeda')

	TButton():New(048, 098, STR0092, oDlg, {||oDlg:End()}, 40 , 10 ,, /*oFont*/,,.T.,,,,/* bWhen*/,,)

	ACTIVATE MSDIALOG oDlg CENTERED

Return nMoeda
