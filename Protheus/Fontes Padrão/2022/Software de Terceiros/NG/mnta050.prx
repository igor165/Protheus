#INCLUDE "MNTA050.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA050
Cadastro de Familia de Bens.
@type function

@author NG Informática Ltda.
@since 01/01/2015

@param
@return
/*/
//------------------------------------------------------------------------------
Function MNTA050()
	
	Local aNGBeginPrm := {}
	Local oBrowse
	
	Private cCadastro := ''
	Private aRotina	  := {}
	
	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 19, 88, 35, 56 )

		aNGBeginPrm := NGBeginPrm()

		cCadastro   := NgSX2Nome( 'ST6' ) // Familias de Bens
		aRotina	    := MenuDef()	
	
		oBrowse := FWMBrowse():New()
		oBrowse:SetAlias( 'ST6' )
		oBrowse:SetDescription( cCadastro )
		oBrowse:Activate()

		NGReturnPrm( aNGBeginPrm )

	EndIf
	
Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Interface da rotina

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function MenuDef()

	// Inicializa MenuDef com todas as opções
	aRotina := {} 

	ADD OPTION aRotina TITLE STR0001 ACTION 'PesqBrw'           OPERATION 1  ACCESS 0 // 'Pesquisar'
    ADD OPTION aRotina TITLE STR0002 ACTION 'VIEWDEF.MNTA050'   OPERATION 2  ACCESS 0 // 'Visualizar'
    ADD OPTION aRotina TITLE STR0003 ACTION 'VIEWDEF.MNTA050'   OPERATION 3  ACCESS 0 // 'Incluir'
    ADD OPTION aRotina TITLE STR0004 ACTION 'VIEWDEF.MNTA050'   OPERATION 4  ACCESS 0 // 'Alterar'
    ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.MNTA050'   OPERATION 5  ACCESS 0 // 'Excluir'
    ADD OPTION aRotina TITLE STR0011 ACTION 'VIEWDEF.MNTA050'   OPERATION 8  ACCESS 0 // 'Imprimir'

	If Type("__lPyme") == "U" .Or. !__lPyme
		ADD OPTION aRotina Title STR0010 Action 'MsDocument' OPERATION 4 ACCESS 0 //"Conhecimento"
	EndIf
	
Return aRotina

//------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ModelDef()

	Local oModel
	Local oStructST6 := FWFormStruct(1,"ST6")
	Local bCommit	 := { |oModel| CommitInfo( oModel ) }
	
	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTA050", , , bCommit )

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTA050_ST6", Nil, oStructST6 )

	oModel:SetDescription( NgSX2Nome( "ST6" ) )

Return oModel

//------------------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel("MNTA050")
	Local oView  := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA050_ST6" , FWFormStruct(2,"ST6") )

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100 )

	// Associa um View a um box
	oView:SetOwnerView( "MNTA050_ST6" , "MASTER" )

	// Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn( oView )

Return oView

//------------------------------------------------------------------------------
/*/{Protheus.doc} CommitInfo
Grava todas as informações

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function CommitInfo( oModel )

	Local nOpc := oModel:GetOperation()

	//--------------------------------------------------------------------------
	// Grava informações do Model
	//--------------------------------------------------------------------------
	FwFormCommit( oModel )
	
	//--------------------------------------------------------------------------
	// Integração com PIMS
	//--------------------------------------------------------------------------
	If SuperGetMV("MV_PIMSINT",.F.,.F.) .And. FindFunction("NGIntPIMS")
		NGIntPIMS( "ST6" , ( "ST6" )->( RecNo() ) , nOpc )
	EndIf

Return .T.
