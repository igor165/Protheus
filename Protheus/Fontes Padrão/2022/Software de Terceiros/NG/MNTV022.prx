#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV022    ³ Autor ³ Marcos Wagner Junior  ³ Data ³01/03/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Custo total das multas no periodo   									 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data    - Data inicio                                     ³±±
±±³          ³ Ate_Data   - Ate data                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nQtdLt  - Litros total                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV022(De_Data,Ate_Data,De_Familia,Ate_Familia,Tip_Mul)
Local aAreaOLD   := GetArea(),nValMulta := 0
Local De_FamiliaL := If(De_Familia = Nil,Space(NGSEEKDIC("SX3","T9_CODFAMI",2,"X3_TAMANHO")),De_Familia) 

// Variáveis de Histórico de Indicadores
Local lMV_HIST := NGI6MVHIST()
Local aParams := {}
Local cCodIndic := "MNTV022"
Local nResult := 0

Default Tip_Mul := "3"

If ValType(Tip_Mul) <> "C"
	Tip_Mul := cValToChar(Tip_Mul)
Endif

// Armazena os Parâmetros
If lMV_HIST
	aParams := {}
	aAdd(aParams, {"DE_DATA"    , De_Data})
	aAdd(aParams, {"ATE_DATA"   , Ate_Data})
	aAdd(aParams, {"DE_FAMILIA" , De_Familia})
	aAdd(aParams, {"ATE_FAMILIA", Ate_Familia})
	aAdd(aParams, {"TIP_MUL"    , Tip_Mul})
	NGI6PREPPA(aParams, cCodIndic)
EndIf

If ValType(De_Data) != "D" .or. ValType(Ate_Data) != "D"
	NGI6PREPVA(cCodIndic, nResult)
	Return nResult
Endif

cAliasQry := GetNextAlias()
// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT TRX.TRX_MULTA, TRX.TRX_VALOR, TRX_VALPAG "
EndIf
cQuery += " FROM " + RetSqlName("TRX") + " TRX "     
cQuery += " INNER JOIN "+RetSqlName("ST9")+" ST9 ON"
cQuery += " ( ST9.T9_PLACA = TRX.TRX_PLACA "              
If ValType(De_FamiliaL) == "C" .and. ValType(Ate_Familia) == "C"
	cQuery += " AND ST9.T9_CODFAMI BETWEEN '"+De_FamiliaL+"' AND '"+Ate_Familia+"'"
EndIf	          
cQuery += " AND ST9.D_E_L_E_T_ <> '*' )"

cQuery += " WHERE TRX.TRX_FILIAL = '"+xFilial("TRX")+"' "
If ValType(De_Data) == "D"                                    
	cQuery += " AND TRX.TRX_DTINFR >= '"+Dtos(De_Data)+"' "
Endif
If ValType(Ate_Data) == "D"
	cQuery += " AND TRX.TRX_DTINFR <= '"+Dtos(Ate_Data)+"' "
Endif
If Tip_Mul <> "3"
	cQuery += " AND TRX_INDFOB = '"+Tip_Mul+"'"
Endif
cQuery += " AND TRX.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

dbSelectArea(cAliasQry)
dbGoTop()
While !Eof()
	If !Empty((cAliasQry)->TRX_VALPAG)
		nValMulta += (cAliasQry)->TRX_VALPAG
	Else
		nValMulta += (cAliasQry)->TRX_VALOR
	Endif
   dbSkip()
End
(cAliasQry)->(dbCloseArea())

// RESULTADO
nResult := nValMulta
NGI6PREPVA(cCodIndic, nResult)

RestArea(aAreaOLD)
Return nResult