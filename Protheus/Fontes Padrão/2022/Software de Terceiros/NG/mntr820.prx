#INCLUDE "mntr820.ch"
#INCLUDE "PROTHEUS.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR820
Relatorio Funcionario X Especialidade

@author Deivys Joenck
@since 10/10/2000
@return Nil
/*/
//---------------------------------------------------------------------
Function MNTR820

	Local cString := "ST1"
	Local wnrel   :="MNTR820"
	Local cDesc1  := STR0001 // "Relatorio das especialidades do funcionario"
	Local cDesc2  := STR0002 // "Atraves da opcao parametros o usuario podera fazer "
	Local cDesc3  := STR0003 // "selecao o funcionario que desejar"

	Private Tamanho  := "M"
	Private aReturn  := { STR0004, 1,STR0005, 2, 2, 1, "",1 } // "Zebrado"###"Administracao"
	Private nLastKey := 0
	Private cPerg    := "MNT82R"
	Private titulo   := STR0006 // "Especialidades do Funcionario"
	Private cabec1,cabec2
	Private ntipo    := 0
	Private nomeprog := "MNTR820"

	//----------------------------------------------------------------
	// Variaveis utilizadas para parametros
	// mv_par01     // De Funcionario
	// mv_par02     // Ate Funcionario
	//----------------------------------------------------------------
	pergunte(cPerg,.F.)

	//----------------------------------------------------------------
	// Envia controle para a funcao SETPRINT
	//----------------------------------------------------------------
	wnrel := SetPrint(cString, wnrel, cPerg, titulo, cDesc1, cDesc2, cDesc3, .F., "")
	If nLastKey = 27
		Set Filter To
		Return
	Endif

	SetDefault(aReturn,cString)

	If nLastKey = 27
	Set Filter To
	Return
	Endif

	RptStatus({|lEnd| R820Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return NIL

//---------------------------------------------------------------------
/*/{Protheus.doc} R820Imp
Chamada do Relatório

@author Inacio Luiz Kolling
@since 01/06/1997
@param lEnd, Lógico, controle da régua de progressão
@param wnRel, Caractere, Nome do relatório
@param titulo, Caractere, Título do relatório
@param tamanho, Caractere, tamanho do rodapé
@return Nil
/*/
//---------------------------------------------------------------------
Static Function R820Imp(lEnd, wnRel, titulo, tamanho)

	Local cRodaTxt  := ""
	Local nCntImpr  := 0
	Local lContinua := .T.

	// [LGPD] Caso o usuário não possua acesso ao(s) campo(s), deve-se ofuscá-lo(s)
	Local lOfuscar := FindFunction( 'FWPDCanUse' ) .And. FwPdCanUse( .T. );
				.And. Len( FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'T1_NOME' } ) ) == 0

	Private li := 80
	Private m_pag := 1

	nTipo := IIf(aReturn[4]==1, 15, 18)

	//----------------------------------------------------------------
	// Monta os Cabeçalhos
	//----------------------------------------------------------------

	cabec1 := "Func.  Nome do Funcionario                       Especil.  Nome da Especialidade"
	cabec2 := ""

	/*/
			1         2         3         4         5         6         7         8         9         0         1         2         3
	123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	Func.  Nome do Funcionario                       Especil.  Nome da Especialidade
	xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   xxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
													xxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
													xxxxx     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	/*/

	dbSelectArea("ST2")
	dbSetOrder(01)
	dbSeek(xFilial('ST2')+MV_PAR01,.t.)
	SetRegua(LastRec())

	While lContinua .And. !Eof() .And. ST2->T2_FILIAL == xFilial('ST2') .And. ST2->T2_CODFUNC <= MV_PAR02

		cCODFUNC := ST2->T2_CODFUNC

		NGSOMALI(58)
		@ Li,000 PSay cCODFUNC
		dbSelectArea("ST1")
		dbSetOrder(01)
		dbSeek(xFilial('ST1')+cCODFUNC)
		@ Li,007 PSay IIf(lOfuscar, FwProtectedDataUtil():ValueAsteriskToAnonymize( ST1->T1_NOME ), ST1->T1_NOME)

		While lContinua .And. !Eof() .And. ST2->T2_FILIAL == xFilial('ST2') .And. ST2->T2_CODFUNC == cCODFUNC

			IncRegua()
			@ Li,049 PSay ST2->T2_ESPECIA
			dbSelectArea("ST0")
			dbSetOrder(01)
			dbSeek(xFilial('ST0')+ST2->T2_ESPECIA)
			@ Li,059 PSay ST0->T0_NOME
			dbSelectArea("ST2")
			dbSkip()
			NGSOMALI(58)

		End

		NGSOMALI(58)

	End

	Roda(nCntImpr,cRodaTxt,Tamanho) // Cria o rodapé do relatório

	//----------------------------------------------------------------
	// Devolve a condição original do arquivo principal
	//----------------------------------------------------------------
	RetIndex("ST2")

	Set Filter To
	Set device to Screen

	If aReturn[5] = 1

		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)

	Endif

	MS_FLUSH()

Return Nil
