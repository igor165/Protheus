#INCLUDE "MNTA365.ch"
#INCLUDE "Protheus.ch"

Static lHasMNTCM := SuperGetMV( 'MV_NGMNTCM', .F., 'N' ) == 'S'

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA365

Substituicao de O.S. PADRAO.

@author	Thiago Olis Machado
@version MP12
@since 15/09/2008
@source  Generico
@return .T.
/*/
//---------------------------------------------------------------------
Function MNTA365()

	Local aNGBeginPrm := {}

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 19, 95 ) .And. fVersion()

		aNGBeginPrm := NGBeginPrm()

		Private aRotina   := MenuDef()
		Private cUsaIntEs := AllTrim(GetMV("MV_NGMNTES"))
		Private lGeraSA   := .F.
		Private aRetSA    := {}
		Private aInf_STJ  := {}
		Private aVETINR   := {}
		Private lTemOSc   := .F.
		Private aSIM 	  := {}
		Private nCONTROGD := 1 //Variavel utilizada na função NG120LINOK #NÃO APAGAR

		If NGCADICBASE('TL_NUMSA','A','STL',.F.) .And. FindFunction("NGGERASA")
			If GetNewPar("MV_NGGERSA","N") == "S" .And. cUsaIntEs == "S"
				lGeraSA := .T.
			EndIf
		EndIf

		//------------------------------------------------------
		//Define o cabecalho da tela de atualizacoes
		//------------------------------------------------------
		cCadastro := STR0003 //"Conf.Plano Manut."

		LCORRET   := .T.
		lCONV     := .F.

		//------------------------------------------------------
		//Endereca a funcao de BROWSE
		//------------------------------------------------------
		dbSelectArea("STI")
		Set Filter To STI->TI_FILIAL == xFilial("STI") .And. STI->TI_SITUACA == "P" .And.  STI->TI_TERMINO == "N"   .And. ;
						(STI->TI_LUBRIFI == "N" .Or. Empty(STI->TI_LUBRIFI))

		dbSelectArea("STI")
		dbSeek(xFilial("STI"))
		If Eof()
			Help("",1,"ARQVAZIO")
		Else
			mBrowse( 6, 1,22,75,"STI")
		EndIf

		//------------------------------------------------------
		//Devolve a condicao original do arquivo principal
		//------------------------------------------------------
		dbSelectArea("STI")
		Set Filter To
		dbSetOrder(1)

		dbSelectArea("STI")
		dbSetOrder(1)
		dbSeek(xFilial("STI"))

		NGRETURNPRM( aNGBeginPrm )

	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT365Kill ³ Autor ³ Thiago Olis Machado   ³ Data ³ 15/09/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz a substituicao de O.S. conforme campo TF_SUBSTIT        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function MNT365Kill
Private cSeqRela := ''
Private aPaginas := {}
Private nPagina  := 1
Private nQtdObj  := 0, nSomaBotao, nDivPanel
Private lVisCanc := .T.

// Ponto de Entrada destinado a possibilitar a confirmação do plano
If ExistBlock("MNTA365B")
	If !ExecBlock("MNTA365B",.F.,.F.)
		Return .F.
	Endif
Endif

If ExistBlock("MNTA365C")
	lVisCanc := ExecBlock("MNTA365C",.F.,.F.)
	If ValType(lVisCanc) != "L"
		lVisCanc := .T.
	EndIf
EndIf

aInf_STJ := {} //Informações temporarias do STJ

dbSelectArea("STJ")
dbSetOrder(3)
If !dbSeek(xFilial("STJ")+STI->TI_PLANO)
	Help("",1,"ARQVAZIO")
	Return .F.
EndIf

MsgRun( "Processando informações..." , STR0014 , { || ProssKill() })//"Aguarde"

NGGanttOS()

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ProssKill  ³ Autor ³ Thiago Olis Machado   ³ Data ³ 15/09/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz a substituicao de O.S. conforme campo TF_SUBSTIT        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ProssKill()

Local cCodBem   := space(16)
Local cOrdem    := space(6)
Local cServico  := space(6)
Local i,nPosXX,nXXX
Local lDtMorta  := .F.
Local aDados    := {}
Local lVerSTJ 	:= SuperGetMV("MV_NG1SUBS", .F., "1" ) == "2"

cAliasQry := GetNextAlias()
cQuery := " SELECT STJ.TJ_ORDEM, STJ.TJ_PLANO, STJ.TJ_CODBEM, STJ.TJ_SERVICO, STJ.TJ_DTMPINI, STJ.TJ_SEQRELA FROM "+RetSQLName("STJ")+" STJ"
cQuery += " INNER JOIN "+RetSQLName("STF")+" STF ON STF.TF_CODBEM = STJ.TJ_CODBEM AND STF.TF_SERVICO = STJ.TJ_SERVICO AND"
cQuery += 										  " STF.TF_SEQRELA = STJ.TJ_SEQRELA AND STF.TF_ATIVO != 'N' AND"
cQuery += 										  " STF.D_E_L_E_T_ != '*' AND STF.TF_FILIAL = '"+xFilial("STF")+"'"
cQuery += " WHERE STJ.TJ_PLANO = '"+STI->TI_PLANO+"' AND STJ.D_E_L_E_T_<>'*' AND STJ.TJ_FILIAL = '"+xFilial("STJ")+"'"
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

lDtMorta := .f.
dbSelectArea(cAliasQry)
dbGotop()
While !Eof()
	aAdd( aDados , { (cAliasQry)->TJ_ORDEM, (cAliasQry)->TJ_PLANO, (cAliasQry)->TJ_CODBEM, ;
					 (cAliasQry)->TJ_SERVICO, StoD((cAliasQry)->TJ_DTMPINI), (cAliasQry)->TJ_SEQRELA } )
	dbSkip()
End
(cAliasQry)->(dbCloseArea())

aSort(aDados,,,{|x,y| x[3]+x[4]+DtoS(x[5])+x[6] < y[3]+y[4]+DtoS(y[5])+y[6] })

NGIFDBSEEK('STJ',STI->TI_PLANO,9,.F.)
While !eof() .And. STJ->TJ_FILIAL = xFilial("STJ") .And. STJ->TJ_PLANO == STI->TI_PLANO

	cCodBem := STJ->TJ_CODBEM
	While !eof() .and. xFilial("STJ") == STJ->TJ_FILIAL .and.;
		STJ->TJ_PLANO == STI->TI_PLANO .and.;
		STJ->TJ_TIPOOS = "B"            .and.;
		STJ->TJ_CODBEM = cCodBem

		aArea := GetArea()
		akill := {}

		NGIFDBSEEK('STF',STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA,1,.F.)
		If Empty(STF->TF_SUBSTIT) .or. STF->TF_ATIVO == "N"
           NGDBSELSKIP("STJ")
           Loop
		EndIf

		//Verifica nova função genérica criada no MNTUTIL_OS
		If FindFunction( "MntSeqSub") //FindFunction remover na release GetRPORelease() >= '12.1.027'
			akill := MntSeqSub(STJ->TJ_CODBEM,STJ->TJ_SERVICO,STJ->TJ_SEQRELA,STJ->TJ_PLANO, .T.)
		Else
			akill := MNTSepSeq(IIf( lVerSTJ, STF->TF_SUBSTIT, STJ->TJ_SUBSTIT))
		EndIf

		lFirst := .t.
  		NGIFDBSEEK("ST9",STJ->TJ_CODBEM,1,.F.)
    	NGIFDBSEEK("TPE",STJ->TJ_CODBEM,1,.F.)

		For i := 1 To Len(aKill)

			cKill   := PadR(Alltrim(aKill[i]),3)
   			NGIFDBSEEK('STF',STJ->TJ_CODBEM+STJ->TJ_SERVICO+PadR(cKill,3),1,.f.)
			If STF->TF_ATIVO == "N"
				Loop
			Endif

			If lFirst
				dDtMata := STJ->TJ_DTMPINI
				lFirst := .f.
			EndIf
            nContAc := If(STF->TF_TIPACOM = "S",TPE->TPE_CONTAC,ST9->T9_CONTACU)
            nVardiC := If(STF->TF_TIPACOM = "S",TPE->TPE_VARDIA,ST9->T9_VARDIA)
            dDTUltA := If(STF->TF_TIPACOM = "S",TPE->TPE_DTULTA,ST9->T9_DTULTAC)

            nIncMan := STF->TF_INENMAN/nVardiC
			If STF->TF_TIPACOM $ "A/T"
               If STF->TF_UNENMAN == "H"
                  nIncMan := STF->TF_TEENMAN/24
               ElseIf STF->TF_UNENMAN == "D"
                  nIncMan := STF->TF_TEENMAN
               ElseIf STF->TF_UNENMAN == "S"
                  nIncMan := STF->TF_TEENMAN * 7
               Else
                  nIncMan := STF->TF_TEENMAN * 30
               EndIf

               If STF->TF_TIPACOM = "A"
                  dDtCom := dDTUltA + ((STF->TF_CONMANU + STF->TF_INENMAN) - nContAc) / nVardiC
                  dDtTem := STF->TF_DTULTMA + nIncMan
                  If dDtCom < dDtTem
                     nIncMan := STF->TF_INENMAN/nVardiC
                  EndIf
               EndIf
			Endif
            nIncMan  := If( nIncMan/2 < 1,0,Round(nIncMan/2,0))
            cCodBem  := STJ->TJ_CODBEM
            cPlano   := STJ->TJ_PLANO
            cOsKill  := STJ->TJ_ORDEM
            cSqKill  := STJ->TJ_SEQRELA
            cServico := STJ->TJ_SERVICO
				lDtMorta := .f.

				nPosXX := aScan( aDados, {|x| x[3] == cCodBem .And. x[6] == cKill .And. x[4] == cServico} )
				If nPosXX > 0
					For nXXX := nPosXX To Len(aDados)
						If aDados[nXXX,3] == cCodBem .And. aDados[nXXX,6] == cKill .And. aDados[nXXX,4] == cServico
							If aDados[nXXX,5] >= dDtMata-nIncMan .and. aDados[nXXX,5] <= dDtMata+nIncMan
								cOrdem   := aDados[nXXX,1]
								cPlano   := aDados[nXXX,2]
								cSeqRela := aDados[nXXX,6]
								lDtMorta := .T.

								dDtMorta := STJ->TJ_DTMPINI
								If NGIFDBSEEK('STJ',cOrdem+cPlano+"B"+cCodBem+cServico+cSeqRela,1,.f.)
									//Define a data da sequencia menor
									dDtOtSTJ := STJ->TJ_DTMPINI
									nPos001 := aScan( aInf_STJ, {|x| x[1] == 2 .and. x[2] == cOrdem } )
									If nPos001 > 0
										lDtMorta := .f.
										If aInf_STJ[nPos001,6] >= dDtMata-nIncMan .and. aInf_STJ[nPos001,6] <= dDtMata+nIncMan
											lDtMorta := .t.
											dDtOtSTJ := aInf_STJ[nPos001,6]
										Endif
									Endif

                                    If aScan( aInf_STJ, {|x| x[1] == 1 .and. x[2] == cOrdem } ) = 0
                                       //Se a data da sequencia menor for anterior ao STJ salvo, o STJ recebe a menor data entre as duas
                                       If dDtOtSTJ < dDtMorta
                                          dDtMorta := dDtOtSTJ
                                          dDtMata  := dDtMorta
                                       Else
                                          dDtOtSTJ := dDtMorta
                                       Endif
                                       If lDtMorta
                                          //Esta OS será cancelada posteriormente
                                          aAdd( aInf_STJ , { 1 , cOrdem , cPlano , "C" , ;
                                          STR0004+DtoC(STJ->TJ_DTMPINI)+STR0005+cOsKill+STR0006+AllTrim(cSqKill)+".",; //"Essa O.S. prevista para a data "###", foi substituida pela O.S. "###" de sequencia "
                                          dDtOtSTJ,cOsKill + cPlano,cKill } )
                                       Endif
                                    EndIf
								EndIf

								RestArea(aArea)
								If lDtMorta
									//Esta OS terá a data inicio previsto modificada posteriormente
									nPos001 := aScan( aInf_STJ, {|x| x[1] == 2 .and. x[2] == STJ->TJ_ORDEM } )
									If nPos001 > 0
										If dDtMorta < aInf_STJ[nPos001,6]
											aInf_STJ[nPos001,6] := dDtMorta
										Endif
									Else
										aAdd( aInf_STJ , { 2 , STJ->TJ_ORDEM , STJ->TJ_PLANO , "" , "", dDtMorta } )
									Endif
								Endif
							Endif
						EndIF
              Next nXXX
            Endif
		Next i
        NGDBAREAORDE("STJ",9)
		dbSkip()
	EndDo
EndDo

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NGGanttOS ³ Autor ³ Thiago Olis Machado   ³ Data ³ 18/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta a dialog e as Manutencoes na tela.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NGGanttOS(lChama)
	Local nX, nNgMes,xDay,lGetIn := .T.,g,h
	Local aNgButton  := {}
	Local lOk := .f.
	Local aSize := MsAdvSize()
	Local nLeft := 0
	Private nInicial := 0,nFinal := 0
	Private nLin,nCol,oFont10,oFont12,oPanel,oScrool,oPanel2
	Private aMeses
	Private oSay1,oDlg
	Private LCORRET   := .F.
	Private nOPCAO  := 2
	Private aIndSTF := {}, cFiltro
	Private lMan := .T.
	Private lReproc
	Private lExitDlg := .F.
	Private aMesCorr := {}, nTimes := 0
	Private oBmpCim1, oBmpBai1
	Private lOpenedDlg := .F. //Indica que a Dialog ja esta criada na tela

	If !(Alltrim(GetTheme()) == "FLAT") .And. !SetMdiChild()
		aSize[7] := aSize[7]-50
		aSize[6] := aSize[6]-30
		aSize[5] := aSize[5]-14
		nLeft := 5
		nSomaBotao := 0
	Else
		nSomaBotao := -26
	EndIf

	If Type("aInf_STJ") <> "A"
		aInf_STJ := {}
	Endif

	if lChama = Nil
		lChama := .t.
		Private aGantt   := {}
		Private nCntMan  := 0
		Private nCntBem  := 0
	endIf

	Aadd(aNgButton,{"PROJETPMS",{|| IF(MontaAlt(),oDlg:End(),.t.)},STR0007,STR0008 })  //"Alterar Data"###"Alterar"
	Aadd(aNgButton,{"SDUSETDEL",{||MontaLeg()},STR0009,STR0009 })  //"Legenda"###"Legenda"

	If !getOs(lChama)
		MsgStop(STR0010) //"Não existem dados para montar a consulta do plano"
		Return .f.
	endIf

	Define MsDialog oDlg From aSize[7],nLeft to aSize[6],aSize[5] Title STR0011 Pixel
	oDlg:lMaximized := .t.
	oPanel2 := TPanel():New(0,0,,oDlg,,,,,RGB(57,59,74),12,12,.f.,.f.)
	oPanel2:Align := CONTROL_ALIGN_LEFT
	oPanel2:nWidth := 34

	oPanel := TPanel():New(0,0,,oDlg,,,,,,(GetScreenRes()[1]),(GetScreenRes()[2]),.F.,.F.)
	oPanel:Align := CONTROL_ALIGN_ALLCLIENT

	oFont12 := TFont():New("Verdana",12,12,,.F.,,,,.T.,.F.)
	oFont10 := TFont():New("Tahoma",08,12,,.F.,,,,.T.,.F.)

	oPanel := TScrollBox():new(oDlg,00,00,(100),(100),.T.,.T.,.T.)
	oPanel:Align := CONTROL_ALIGN_ALLCLIENT

	ContaGantt()

	If Len(aPaginas) > 0
		nInicial := aPaginas[1][2]
		nFinal   := aPaginas[1][3]
	Endif

	MontaLeft(nInicial,nFinal)

	If lExitDlg
		Activate Dialog oDlg On Init (oDlg:End()) Centered
	Else
		Activate Dialog oDlg On Init (lOpenedDlg := .T.,EnchoiceBar(oDlg,{|| lOk:=.T.,oDlg:End()},{|| lOk:= .F.,oDlg:End()},,aNgButton)) Centered
	Endif

	If lOk
		
		Processa( { || lOk := GantGrava() }, STR0014, STR0016 ) //"Aguarde"###"Gravando Atualizações..."
		
		If lOk

			dbSelectArea( 'STI' )

			RecLock( 'STI', .F. )
				STI->TI_SITUACA := 'L'
			MsUnLock()

		EndIf
		
	Else
		If Type("lReproc") != 'U'
			NGGanttOS(.f.)
		Endif
	EndIf

	aInf_STJ := {} //Limpa variavel temporaria da STJ

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ getOS     ³ Autor ³ Thiago Olis Machado   ³ Data ³ 18/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Busca as Ordens de Servico para o Plano selecionado         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function getOS(lChama)
Local i
Local aCodBem := {}
Local aManut  := {}

If Type("aInf_STJ") <> "A"
	aInf_STJ := {}
Endif

If lChama
	cAliasQry := GetNextAlias()

	cQuery := " SELECT STJ.TJ_CODBEM, STJ.TJ_SERVICO, STJ.TJ_SEQRELA, STJ.TJ_DTMPINI, STJ.TJ_SITUACA, STJ.TJ_ORDEM"
	cQuery += " FROM "+RetSQLName("STJ")+" STJ"
	cQuery += " INNER JOIN "+RetSQLName("STF")+" STF ON STF.TF_CODBEM = STJ.TJ_CODBEM AND STF.TF_SERVICO = STJ.TJ_SERVICO AND"
	cQuery += 										  " STF.TF_SEQRELA = STJ.TJ_SEQRELA AND STF.TF_ATIVO != 'N' AND"
	cQuery += 										  " STF.D_E_L_E_T_ != '*' AND STF.TF_FILIAL = '"+xFilial("STF")+"'"
	cQuery += " WHERE STJ.TJ_PLANO = '"+STI->TI_PLANO+"' AND STJ.D_E_L_E_T_<>'*' AND STJ.TJ_FILIAL  = '"+xFilial("STJ")+"'"
	cQuery += " ORDER BY STJ.TJ_CODBEM,STJ.TJ_SERVICO,STJ.TJ_SEQRELA"

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	cSeqRela := ''
	cCodBem  := ''
	cServico := ''
	DbSelectArea(cAliasQry)
	DbGotop()
	While !Eof()

		If cCodBem <> (cAliasQry)->TJ_CODBEM
			nCntBem++
		EndIf

		If cCodBem <> (cAliasQry)->TJ_CODBEM .OR. cServico <> (cAliasQry)->TJ_SERVICO .OR. cSeqRela <> (cAliasQry)->TJ_SEQRELA

			nCntMan++
			cSeqRela := (cAliasQry)->TJ_SEQRELA
			cCodBem  := (cAliasQry)->TJ_CODBEM
			cServico := (cAliasQry)->TJ_SERVICO
		EndIf

		dDataTmp := StoD((cAliasQry)->TJ_DTMPINI)
		cSituTmp := (cAliasQry)->TJ_SITUACA

		nPos001 := aScan( aInf_STJ, {|x| x[2] == (cAliasQry)->TJ_ORDEM } )
		If nPos001 > 0
			dDataTmp := aInf_STJ[nPos001,6]
		Endif
		nPos001 := aScan( aInf_STJ, {|x| x[1] == 1 .and. x[2] == (cAliasQry)->TJ_ORDEM } )
		If nPos001 > 0
			cSituTmp := "C"
		Endif

		aAdd(aGantt,{ dDataTmp ,;
		             (cAliasQry)->TJ_ORDEM ,;
		             StrZero( Month( StoD( (cAliasQry)->TJ_DTMPINI ) ),2 ),;
						 (cAliasQry)->TJ_CODBEM+(cAliasQry)->TJ_SERVICO+(cAliasQry)->TJ_SEQRELA,;
						 cSituTmp,;
				 		 (cAliasQry)->TJ_CODBEM+(cAliasQry)->TJ_SERVICO+(cAliasQry)->TJ_SEQRELA+(cAliasQry)->TJ_DTMPINI+StrZero( Month( StoD( (cAliasQry)->TJ_DTMPINI ) ),2 ),;
				 		 (cAliasQry)->TJ_CODBEM,;
				 		 (cAliasQry)->TJ_SEQRELA,;
				 		 (cAliasQry)->TJ_CODBEM+(cAliasQry)->TJ_SERVICO+(cAliasQry)->TJ_SEQRELA+(cAliasQry)->TJ_DTMPINI,;
				 		  nCntBem,;
				 		  nCntMan,;
				 		 (cAliasQry)->TJ_SERVICO,;
				 		 StoD((cAliasQry)->TJ_DTMPINI) } ) //Nunca devera ser alterada esta posicao, mesmo que posicao 1 seja alterada

		DbSkip()
	EndDo
	(cAliasQry)->(dbCloseArea())
Else
	If Len(aGantt) == 0
		Return .f.
	EndIf
EndIf

aSort(aGantt,,,{|x,y| x[6] < y[6] })

aMeses := {}

For i:= 1 To Len(aGantt)
	aAdd(aMeses,{ aGantt[i][10],aGantt[i][11],Month(aGantt[i][1]),aGantt[i][9],aGantt[i][1],aGantt[i][5],aGantt[i][4]})
Next i

aSort(aGantt,,,{|x,y| x[4] < y[4] })

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MontaCabec ³ Autor ³ Thiago Olis Machado   ³ Data ³ 18/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta o cabecalho de meses.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MontaCabec()
Local i, j
Local nPosScan := 0, nScan2 := 0
Local nColuna  := 0
Local nSomaCol := 55, nMaior := 0
Local aMesCol  := {}
Local aOldGantt := {}
Local nPosCol := nCol+1
Private lPrim1   := .t.
Private lPrim2   := .t.
Private lPrim3   := .t.
Private lPrim4   := .t.
Private lPrim5   := .t.
Private lPrim6   := .t.
Private lPrim7   := .t.
Private lPrim8   := .t.
Private lPrim9   := .t.
Private lPrim10  := .t.
Private lPrim11  := .t.
Private lPrim12  := .t.

If Len(aGantt) == 0
	Return .f.
EndIf

aMesCorr := {}
aOldGantt := aClone(aGantt)
aSort(aGantt,,,{|x,y| DTOS(x[1]) < DTOS(y[1]) })

//Verifica OS's por mes
For i := nInicial To nFinal
	//Verifica no caso de cancelamento
	If (lVisCanc .or. (!lVisCanc .and. aMeses[i][6] != "C"))
		If (nPosScan := aScan( aMesCol, {|x| x[1]+x[2] == AllTrim(Str(Month(aMeses[i][5])))+AllTrim(Str(Year(aMeses[i][5]))) } )) == 0
			aAdd(aMesCol, {AllTrim(Str(Month(aMeses[i][5]))),AllTrim(Str(Year(aMeses[i][5]))),{{aMeses[i][7],1}} })
		Else
			If (nScan2 := aScan( aMesCol[nPosScan][3], {|x| x[1] == aMeses[i][7] } )) == 0
				aAdd(aMesCol[nPosScan][3], {aMeses[i][7],1} )
			Else
				aMesCol[nPosScan][3][nScan2][2] += 1
			Endif
		Endif
	Endif
Next i

//Monta Cabecalho
For i := 1 To Len(aGantt)

	If aScan( aMesCorr, {|x| x[1]+x[4] == AllTrim(Str(Month(aGantt[i][1])))+AllTrim(Str(Year(aGantt[i][1]))) } ) == 0
		nSomaCol := 55
		If (nPosScan := aScan( aMesCol, {|x| x[1]+x[2] == AllTrim(Str(Month(aGantt[i][1])))+AllTrim(Str(Year(aGantt[i][1]))) } )) > 0
			nMaior := 0
			For j:=1 to Len(aMesCol[nPosScan][3])//Verifica que servico tem mais OS
				If aMesCol[nPosScan][3][j][2] > nMaior
					nMaior := aMesCol[nPosScan][3][j][2]
				Endif
			Next j
			nSomaCol := nMaior*7.1
			nSomaCol := If(nSomaCol < 55, 55, nSomaCol)
		Endif
		If StrZero(Month(aGantt[i][1]),2) = '01'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0017+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"JAN/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim1 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '02'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0018+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"FEV/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim2 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '03'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0019+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"MAR/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim3 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '04'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0020+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"ABR/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim4 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '05'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0021+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"MAI/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim5 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '06'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0022+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"JUN/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim6 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '07'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0023+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"JUL/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim7 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '08'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0024+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"AGO/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim8 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '09'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0025+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"SET/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim9 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '10'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0026+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"OUT/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim10 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '11'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0027+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"NOV/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim11 := .f.
			nCol += nSomaCol
			nColuna++
		ElseIf StrZero(Month(aGantt[i][1]),2) = '12'
			If !ContaObjet(2)
				Return .F.
			EndIf
			@ nLin,nCol To nLin+26,nCol+nSomaCol Label "" Of oPanel PIXEL
			cCaption := STR0028+AllTrim( Str( Year( aGantt[i][1] ) ) ) //"DEZ/"
			cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"
			oSay     := TSay():New(030,nCol+1,&cBlkSay1,oPanel,,oFont12, .F., .F., .F., .T.,CLR_BLUE,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			lPrim12 := .f.
			nCol += nSomaCol
			nColuna++
		EndIf
		aAdd( aMesCorr, { AllTrim(Str(Month(aGantt[i][1]))),nColuna, nPosCol ,AllTrim(Str(Year(aGantt[i][1]))), nSomaCol })
		nPosCol += nSomaCol
	Endif
Next i

aGantt := aClone(aOldGantt)

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MontaGant ³ Autor ³ Thiago Olis Machado   ³ Data ³ 18/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta os valores na linha do tempo.                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nFinLin -> Parametro que define o tamanho maximo da linna do³±±
±±³          ³            rodape.                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MontaGant(nFinLin,nPagina,lNovoGantt)
Local nI
Local nDias := 0
Local nBem := 0, nLinha := 0
Default lNovoGantt := .f., nPagina := 1
If nPagina == 1
	nLinTit := 44
Else
	nLinTit := 0
Endif
nMes    := 0
nAno    := 0

lMes	  := .f.

If Len(aMeses) > 0
	ProcRegua(Len(aMeses))
	For nI := nInicial To nFinal

		IncProc()
		lMes := .t.
		cCaption := StrZero(Day(aMeses[nI][5]),2)
		cBlKSay1 := "{|| OemToAnsi('"+cCaption+"')}"

		If nMes <> Month(aMeses[nI][5]) .OR. nAno <> Year(aMeses[nI][5]) .OR. nLinha <> aMeses[nI][2] .OR. nBem <> aMeses[nI][1]
			nMes := 	Month(aMeses[nI][5])
			nAno := 	Year(aMeses[nI][5])
			nPos 		:= aScan( aMesCorr, {|x| x[1]+x[4] == AllTrim(Str(nMes))+AllTrim(Str(nAno)) } )
			nColDay  := aMesCorr[nPos][3]
			nDias := 0
		Else
			If nBem == aMeses[nI][1] .AND. nLinha == aMeses[nI][2]
				nColDay += 7
				nDias++
			Endif
		EndIf

  		If nBem != aMeses[nI][1]
	  		nLinTit  += 14
	  		nBem := aMeses[nI][1]
	  	Else
	  		If nLinha != aMeses[nI][2]
			  	nLinTit  += 7
			Endif
  		Endif
  		nLinha := aMeses[nI][2]

		If !ContaObjet(1)
			lExitDlg := .T.
			Return .F.
		Endif

		If aMeses[nI][6] == 'C'
			If lVisCanc
				oSay1     := TSay():New(nLinTit,nColDay,&cBlkSay1,oPanel,,, .F., .F., .F., .T.,CLR_RED,,,, .F., .F., .F., .F., .F. )
			Else
				nColDay -= 7
			EndIf
		Else
		   oSay1     := TSay():New(nLinTit,nColDay,&cBlkSay1,oPanel,,, .F., .F., .F., .T.,CLR_GREEN,,,, .F., .F., .F., .F., .F. )
		EndIf

	Next nI
Else
	ShowHelpDlg(STR0052,{STR0055},3) //"ATENÇÃO"###"Não existe nenhuma O.S. gerada pelo plano."
	lExitDlg := .T.
	Return .F.
Endif

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MontaAlt  ³ Autor ³ Thiago Olis Machado   ³ Data ³ 20/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta a tela de alteracao das datas                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nFinLin -> Parametro que define o tamanho maximo da linna do³±±
±±³          ³            rodape.                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MontaAlt()
Local oDlgAlt
Local oCancel
Local oShowOS
Local oShowMN
Local cCadastro := STR0029 //"Alteração da Data da Ordem de Servico"
Local cCodBem   := Space(Len(ST9->T9_CODBEM))
Local cSequenc  := Space(Len(STF->TF_SEQRELA))
Local cServico  := Space(Len(ST4->T4_SERVICO))
Local dDtAtu    := CTOD("  /  /  ")
Local dDtNew    := CTOD("  /  /  ")
Local lCancel := .f.
Local lShowOS := .f.
Local lShowMN := .f.

nOpca := 0
oDlgAlt    := MSDialog():New( 000,000,180,485,cCadastro,,,.F.,,,,,,.T.,,,.T. )

oPnlPai := TPanel():New(00,00,,oDlgAlt,,,,,,480,150,.F.,.F.)
	oPnlPai:Align := CONTROL_ALIGN_ALLCLIENT

@ 005 ,008  Say OemToAnsi(STR0030) Color CLR_HBLUE of oPnlPai Pixel //"Codigo do Bem"
@ 005 ,060  MsGet cCodBem  Picture "@!" Valid ExistCpo("ST9",cCodBem) SIZE 60,10 F3 "ST9" of oPnlPai HASBUTTON Pixel
@ 005 ,140  Say OemToAnsi(STR0031) Color CLR_HBLUE of oPnlPai Pixel //"Servico"
@ 005 ,192  MsGet cServico Picture "@!" Valid ExistCpo("ST4",cServico) SIZE 40,10 F3 "ST3" of oPnlPai HASBUTTON Pixel

@ 020 ,008  Say OemToAnsi(STR0032) Color CLR_HBLUE of oPnlPai Pixel //"Sequencia"
@ 020 ,060  MsGet cSequenc Picture "@!" SIZE 40,10 of oPnlPai Pixel
@ 020 ,140  Say OemToAnsi(STR0033) Color CLR_HBLUE of oPnlPai Pixel //"Data Atual"
@ 020 ,192  MsGet dDtAtu Picture "99/99/99" Valid ChkCancel(cCodBem,cServico,cSequenc,dDtAtu,oCancel,@lCancel) SIZE 40,10 of oPnlPai HASBUTTON Pixel

@ 035 ,008  Say OemToAnsi(STR0034) of oPnlPai Pixel //"Nova Data"
@ 035 ,060  MsGet dDtNew Picture "99/99/99" Valid ChkDtNew(dDtNew) SIZE 40,10 of oPnlPai HASBUTTON Pixel

@ 050 ,008 CheckBox oCancel Var lCancel Prompt OemToAnsi(STR0035) Size 102,10 of oPnlPai Pixel //"O.S. Cancelada"
@ 050 ,100 CheckBox oShowOS Var lShowOS Prompt OemToAnsi(STR0036) On Change ShowOS(cCodBem,cServico,cSequenc,dDtAtu,dDtNew,@lShowOS,oShowOS) Size 102,10 of oPnlPai Pixel  //"Mostrar O.S."
@ 050 ,180 CheckBox oShowMN Var lShowMN Prompt OemToAnsi(STR0037) On Change ShowMN(cCodBem,cServico,cSequenc,@lShowMN,oShowMN) Size 102,10 of oPnlPai Pixel  //"Mostrar Manutenção"

Activate MsDialog oDlgAlt On Init EnchoiceBar(oDlgAlt,{|| IF(ChkMan(cCodBem,cServico,cSequenc,dDtAtu,dDtNew,@oDlgAlt,lCancel),(nOpca:=1,oDlgAlt:End()),nOpca:=0) },{||oDlgAlt:End()}) Centered

If nOpca == 0
	Return .f.
Endif

lReproc := .t.

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ChkMan    ³ Autor ³ Thiago Olis Machado   ³ Data ³ 20/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Checa se existe manutencoes e remonta a tela caso exista.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ChkMan(cCodBem,cServico,cSequenc,dDtAtu,dDtNew,oDlgAlt,lCancel)
Local nPos := 0

If Empty(cCodBem) .or. Empty(cSequenc) .or. Empty(dDtAtu) .or. Empty(cServico)
	Help(" ",1,"OBRIGAT")
	Return .f.
EndIf

If aScan( aGantt, {|x| x[9] == cCodBem+cServico+cSequenc+DTOS(dDtNew) } ) > 0 .AND. dDtNew != dDtAtu
	If !MsgYesNo(STR0050+CHR(13); //"Já existe manutenção para:"
					+STR0039+allTrim(cCodBem)+CHR(13); //"Bem           : "
					+STR0040+allTrim(cServico)+CHR(13); //"Servico      : "
					+STR0041+allTrim(cSequenc)+CHR(13); //"Sequencia : "
			  		+STR0042+DtoC(dDtNew)+CHR(13)+CHR(13); //"Data           : "
			  		+STR0051,STR0052) //"Deseja confirmar?"###"Atenção"
		Return .f.
	Endif
Endif

nPos 		:= aScan( aGantt, {|x| x[9] == cCodBem+cServico+cSequenc+DTOS(dDtAtu) } )
If nPos > 0

	If ExistBlock("MNTA365A")
		If !ExecBlock("MNTA365A",.F.,.F.,{lCancel,nPos,dDtNew,dDtAtu})
			Return .f.
		EndIf
	EndIf

	If Empty(dDtNew)
		dDtNew := dDtAtu
	EndIf
	aGantt[nPos][1] := dDtNew
	aGantt[nPos][3] := StrZero( Month( dDtNew ), 2 )
	aGantt[nPos][5] := If(lCancel,"C","P")
	aGantt[nPos][6] := subStr( aGantt[nPos][6], 1, 25 ) + DtoS( dDtNew )+StrZero( Month( dDtNew ), 2 )
	aGantt[nPos][9] := subStr( aGantt[nPos][9], 1, 25 ) + DtoS( dDtNew )
	Return .t.
Else
	MsgStop(STR0038+CHR(10)+CHR(13); //"Não existem manutenções para :"
		     +STR0039+allTrim(cCodBem)+CHR(13); //"Bem           : "
		 	  +STR0040+allTrim(cServico)+CHR(13); //"Servico      : "
			  +STR0041+allTrim(cSequenc)+CHR(13); //"Sequencia : "
			  +STR0042+DtoC(dDtAtu)) //"Data           : "
	Return .f.
EndIf

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ShowOS    ³ Autor ³ Thiago Olis Machado   ³ Data ³ 20/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mostra a Ordem de Servico selecionada.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ShowOS(cCodBem,cServico,cSequenc,dDtAtu,dDtNew,lShowOS,oShowOS)
Local nPos := 0
Local aArea := GetArea()
Local aOldRot := aClone(aRotina)

If !lShowOS
	Return
Endif

TIPOACOM  := .F.
TIPOACOM2 := .F.
lSITUACA  := .F.

If Empty(cCodBem) .or. Empty(cSequenc) .or. Empty(dDtAtu) .or. Empty(cServico)
	Help(" ",1,"OBRIGAT")
	lShowOS := .f.
	oShowOS:Refresh()
	Return .f.
EndIf

nPos 		:= aScan( aGantt, {|x| x[9] == cCodBem+cServico+cSequenc+DTOS(dDtAtu) } )
If nPos > 0
   dbSelectArea("STJ")
   dbSetOrder(1)
   dbSeek(xFilial("STJ")+aGantt[nPos][2])
	aRotina := {{ STR0001 , 'AxPesqui' , 0 , 1},; //"Pesquisar"
					{ STR0002 , 'MNT365Kill', 0 , 2, 0}} //"Confirmar"
	NGCAD01(Alias(),Recno(),2)
	aRotina := aClone(aOldRot)
Else
	MsgStop(STR0038+CHR(10)+CHR(13); //"Não existem manutenções para :"
		     +STR0039+allTrim(cCodBem)+CHR(13); //"Bem           : "
		 	  +STR0040+allTrim(cServico)+CHR(13); //"Servico      : "
			  +STR0041+allTrim(cSequenc)+CHR(13); //"Sequencia : "
			  +STR0042+DtoC(dDtAtu)) //"Data           : "
	lShowOS := .f.
	oShowOS:Refresh()
	Return .f.
EndIf

lShowOS := .f.
oShowOS:Refresh()
RestArea(aArea)
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ShowOS    ³ Autor ³ Thiago Olis Machado   ³ Data ³ 20/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mostra a Ordem de Servico selecionada.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ShowMN(cCodBem,cServico,cSequenc,lShowMN,oShowMN)
Local nPos := 0
Local aArea := GetArea()
Local aOldRot := aClone(aRotina)

If !lShowMN
	Return
Endif

If Empty(cCodBem) .or. Empty(cServico) .or. Empty(cSequenc)
	Help(" ",1,"OBRIGAT")
	lShowMN := .f.
	oShowMN:Refresh()
	Return .f.
EndIf

nPos 		:= aScan( aGantt, {|x| x[9] = cCodBem+cServico+cSequenc } )
If nPos > 0
   dbSelectArea("STF")
   dbSetOrder(1)
   dbSeek(xFilial("STF")+cCodBem+cServico+cSequenc)
	aRotina := {{ STR0001 , 'AxPesqui' , 0 , 1},; //"Pesquisar"
					{ STR0002 , 'MNT365Kill', 0 , 2, 0}} //"Confirmar"
	NG120FOLD("STF",Recno(),2)
	aRotina := aClone(aOldRot)
Else
	MsgStop(STR0038+CHR(10)+CHR(13); //"Não existem manutenções para :"
		     +STR0039+allTrim(cCodBem)+CHR(13); //"Bem           : "
		 	  +STR0040+allTrim(cServico)+CHR(13); //"Servico      : "
			  +STR0041+allTrim(cSequenc)) //"Sequencia : "
	lShowMN := .f.
	oShowMN:Refresh()
	Return .f.
EndIf

lShowMN := .f.
oShowMN:Refresh()
RestArea(aArea)
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MontaLeg  ³ Autor ³ Thiago Olis Machado   ³ Data ³ 21/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta a tela de Legenda.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MontaLeg()

Local cCadastro := STR0043 //"Gráfico das Ordens de Serviço do Plano"
BrwLegenda(cCadastro,STR0009,{{"ENABLE"     ,STR0044},;  //"Legenda"
										  {"BR_VERMELHO",STR0045}}) //###"Cancelada"

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GantGrava ³ Autor ³ Thiago Olis Machado   ³ Data ³ 21/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava as Ordens de Servico que foram modificadas            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function GantGrava()
	
	Local i, nDifData := 0, aOldArea := {}
	Local aWorkfOS := {}
	Local aAreaStj := {}
	Local lOk      := .T.

	Private M->TI_PLANO := Space(Len(STI->TI_PLANO))
	Private M->TI_DATAPLA := CtoD("  /  /  ")
	Private dPOS, hPOS

	If Type("aInf_STJ") <> "A"
		aInf_STJ := {}
	Endif

	ProcRegua( Len( aGantt ) )

	BEGIN TRANSACTION

		For i:= 1 To Len( aGantt )
			IncProc()

			dbSelectArea("STJ")
			dbSetOrder(1)
			If dbSeek(xFilial("STJ")+aGantt[i][2]+STI->TI_PLANO)
				nRecSTJ := Recno()

				nDifData := 0
				nDifData := aGantt[i][1] - STJ->TJ_DTMPINI
				If nDifData != 0
					aOldArea := GetArea()
					M265BLOQBR(STJ->TJ_CODBEM,STJ->TJ_ORDEM,STJ->TJ_PLANO,STJ->TJ_SERVICO,;
							If(NgVerify("STJ"),STJ->TJ_SEQRELA,STJ->TJ_SEQUENC),;
							STJ->TJ_CCUSTO,STJ->TJ_DTMPINI,STJ->TJ_HOMPINI,STJ->TJ_DTPPINI,;
							STJ->TJ_HOPPINI,STJ->TJ_DTMPINI+nDifData,STJ->TJ_HOMPINI,STJ->TJ_DTMPFIM+nDifData,STJ->TJ_HOMPFIM,;
							STJ->TJ_DTPPINI,STJ->TJ_HOPPINI,STJ->TJ_DTPPFIM,STJ->TJ_HOPPFIM)
					RestArea(aOldArea)
				Endif

				dbSelectArea("STJ")
				dbGoto(nRecSTJ)
				RecLock("STJ",.f.)
				STJ->TJ_DTMPINI := STJ->TJ_DTMPINI + nDifData
				STJ->TJ_DTMPFIM := STJ->TJ_DTMPFIM + nDifData
				If STJ->TJ_SITUACA == 'C' .AND. aGantt[i][5] == 'P'

					If NGCADICBASE("TJ_MMSYP","A","STJ",.F.)
							MsMM(,80,,'',1,,,"STJ","TJ_MMSYP")
					Else
							STJ->TJ_OBSERVA := ' '
					EndIf
				Endif
				STJ->TJ_SITUACA := aGantt[i][5]
				MsUnLock("STJ")
				If aGantt[i][5] != 'C'
					M->TI_PLANO   := STI->TI_PLANO
					M->TI_DATAPLA := STI->TI_DATAPLA
					aSIM := {}
					Aadd(aSIM,{STJ->TJ_ORDEM,STJ->TJ_CODBEM,STJ->TJ_CCUSTO,STJ->TJ_DTMPINI,nDifData,0,"cORDEM",;
									STJ->TJ_DTMPFIM,STJ->TJ_SERVICO,STJ->TJ_SEQRELA,STJ->TJ_DTORIGI," "})
					
					A340ASIM( aSIM, @lOk )

					If STJ->TJ_SITUACA == 'L'
						AADD(aWorkfOS,STJ->TJ_ORDEM)
					Endif

					//INICIO - Recalcula data de parada prevista fim da manutencao
					If nDifData != 0
						aOldArea := GetArea()
						dbSelectArea("STJ")
						dbGoto(nRecSTJ)
						If !Empty(STJ->TJ_DTPPFIM)
							Reclock("STJ",.F.)
							STJ->TJ_DTPPINI := NGSEEK('ST3',STJ->TJ_ORDEM+STJ->TJ_PLANO,2,'T3_DTINI')
							STJ->TJ_HOPPINI := NGSEEK('ST3',STJ->TJ_ORDEM+STJ->TJ_PLANO,2,'T3_HRINI')
							STJ->TJ_DTPPFIM := NGSEEK('ST3',STJ->TJ_ORDEM+STJ->TJ_PLANO,2,'T3_DTFIM')
							STJ->TJ_HOPPFIM := NGSEEK('ST3',STJ->TJ_ORDEM+STJ->TJ_PLANO,2,'T3_HRFIM')
							STJ->(MsUnlock())
						EndIf
						RestArea(aOldArea)
					Endif
					//FIM - Recalcula data de parada prevista fim da manutencao
				Else
					If NGDELETOS(STJ->TJ_ORDEM,STJ->TJ_PLANO)
						nPos001 := aScan( aInf_STJ, {|x| x[1] == 1 .and. x[2] == STJ->TJ_ORDEM } )
						If nPos001 > 0
							RecLock("STJ",.f.)
							If NGCADICBASE("TJ_MMSYP","A","STJ",.F.)
								MsMM(,80,,aInf_STJ[nPos001,5],1,,,"STJ","TJ_MMSYP")
							Else
								STJ->TJ_OBSERVA := aInf_STJ[nPos001,5]
							EndIf
							MsUnLock("STJ")

							//Grava na O.S. pai qual sequencia ele substituiu
							aAreaStj := GetArea()
							dbSelectArea("STJ")
							dbSetOrder(1)
							If dbSeek( xFilial("STJ") + aInf_STJ[nPos001,7] )
								RecLock("STJ",.f.)
								If !Empty(STJ->TJ_SUBSTIT)
									STJ->TJ_SUBSTIT := AllTrim(STJ->TJ_SUBSTIT) + ", " + aInf_STJ[nPos001,8]
								Else
									STJ->TJ_SUBSTIT := aInf_STJ[nPos001,8]
								EndIf
								MsUnLock()
							EndIf
							RestArea(aAreaStj)

						Endif
					EndIf

				Endif

			EndIf
		Next i

	END TRANSACTION

	If lOk
	
		If Len( aWorkfOS ) > 0
			MNTW215(,aWorkfOS)
		Endif

		dbSelectArea("STI")
		a340CONFP("STI", Recno(), 2)

	EndIf

Return lOk

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ChkCancel ³ Autor ³ Thiago Olis Machado   ³ Data ³ 21/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Checa o status da O.S.                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function ChkCancel(cCodBem,cServico,cSequenc,dDtAtu,oCancel,lCancel)
Local nPos := 0

If Empty(cCodBem) .or. Empty(cServico) .or. Empty(cSequenc) .or. Empty(dDtAtu)
	Help(" ",1,"OBRIGAT")
	Return .f.
EndIf

nPos 		:= aScan( aGantt, {|x| x[9] == cCodBem+cServico+cSequenc+DTOS(dDtAtu) } )
If nPos > 0
	lCancel := If(aGantt[nPos][5]="C",.T.,.F.)
	oCancel:Refresh()
EndIf

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ChkDtNew  ³ Autor ³ Thiago Olis Machado   ³ Data ³ 21/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Checa o periodo de Data                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Booleano                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function ChkDtNew(dDtNew)
Local nPos := 0
Local nMes := 	Month(dDtNew)

nPos 		:= aScan( aMesCorr, {|x| x[1] == AllTrim(Str(nMes)) } )

If nPos <> 0
	If AllTrim(Str(Year(dDtNew))) <> aMesCorr[nPos][4]
		MsgStop(STR0046+DTOC(dDtNew)+STR0047+CHR(13); //"Essa O.S. será transferida para a data "###", porém não será mostrada"
		        +STR0048+CHR(13); //"no gráfico, pois a mesma ultrapassou o período máximo de 12 meses a partir"
		        +STR0049)	 //"do primeiro mês do plano."
	EndIf
EndIf

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ricardo Dal Ponte     ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
Local aRotina := { { STR0001 , 'AxPesqui' , 0 , 1},; //"Pesquisar"
      		       { STR0002 , 'MNT365Kill', 0 , 4, 0}} //"Confirmar"
Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MNTSepSeq ³ Autor ³ Thiago Olis Machado   ³ Data ³18/09/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Separa as sequencias para substituicao                     ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com as sequencias.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function MNTSepSeq(cSubstit)
Local aKill := {}
Local nInd  := 1
Local lSair := .f.
Local cSubs := ""

Default cSubstit := STF->TF_SUBSTIT

While !lSair .and. nInd < Len(cSubstit)

   If subStr(cSubstit,nInd,1) <> ','
      cSubs += subStr(cSubstit,nInd,1)
   Else
   	If !Empty(cSubs)
   		aAdd( aKill,AllTrim(cSubs)+Space(Len(STF->TF_SEQRELA)-Len(AllTrim(cSubs))) )
   		cSubs := ""
   	Else
   		lSair := .t.
   		Exit
   	EndIf
   EndIf
   nInd ++
End

If !Empty(cSubs)
	aAdd( aKill,AllTrim(cSubs)+Space(Len(STF->TF_SEQRELA)-Len(AllTrim(cSubs))) )
EndIf

Return aKill

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ContaGantt³ Autor ³ Marcos Wagner Junior  ³ Data ³ 28/04/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula a quantidade de paginas.                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ContaGantt()
Local nI, i, nLinAtu := 0, nUltCont := 0
Local nBem := 0, nLinha := 0
nDivPanel := 0
nLinTit := 44
nMes    := 0

lMes	  := .f.
aPaginas := {}

If Len(aMeses) > 0
	Aadd(aPaginas,{1,1,0}) //Numero da Pagina, nI(inicial), nI(final)
	For nI := 1 To Len(aMeses)

		If nLinAtu != aMeses[nI][2]
			nLinAtu := aMeses[nI][2]

			For i:=nI To Len(aMeses)
				If nLinAtu != aMeses[i][2]
					Exit
				Endif
			Next i
			If (i-nUltCont) > 850 .or. nI == 1
				nUltCont := nI
				nDivPanel ++
			Endif
		Endif

		If nLinha != aMeses[nI][2]
			nLinTit  += 7
		Endif

		If (aScan(aPaginas,{|x| x[1] == nDivPanel }) != 0)
			If nBem != aMeses[nI][1]
				nLinTit  += 14
				nBem := aMeses[nI][1]
			Else
				If nLinha != aMeses[nI][2]
					nLinTit  += 7
				Endif
			Endif
			nLinha := aMeses[nI][2]
		Else
			aPaginas[Len(aPaginas)][3] := nI-1
			Aadd(aPaginas,{Len(aPaginas)+1,nI,0})
		Endif
	Next nI
	aPaginas[Len(aPaginas)][3] := nI-1
Endif

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MontaLeft ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 29/04/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta o cabecalho da esquerda.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MontaLeft(nInicial,nFinal)
Local nX, xDay
nLin	  := 20
nCol    := 178
nColSay := 175
lOk     := .f.
nUltCol := 158 //Posicao da ultima coluna (Utilizado pelo ponto de entrada MNTA365I)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ponto de Entrada para adicionar colunas no Gantt³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAddCol := {}
If ExistBlock("MNTA365I")
	aAddCol := ExecBlock("MNTA365I",.F.,.F.)
	nCol += aAddCol[1]
EndIf

If !ContaObjet(5)
	Return .F.
EndIf
@ 010,023 Say STI->TI_PLANO Font oFont12 of oPanel Pixel
@ 010,068 Say STI->TI_DESCRIC Font oFont12 of oPanel Pixel
@ 020,010 To nLin+26,nCol Label "" Of oPanel PIXEL
@ 038,038 Say OemToAnsi(STR0012) Color CLR_BLACK Font oFont10 of oPanel Pixel //"Serviço"
@ 038,158 Say OemToAnsi(STR0013) Color CLR_BLACK Font oFont10 of oPanel Pixel //"Seq."

If Len(aAddCol) > 0
	For nX := 1 To Len(aAddCol[2])
		If !ContaObjet(1)
			Return .F.
		EndIf
		TSay():New(038,nUltCol+aAddCol[2][nX][2],&("{|| '"+ aAddCol[2][nX][1] +"' }"),oPanel,,oFont10, .F., .F., .F., .T.,CLR_BLACK,,,, .F., .F., .F., .F., .F. )
	Next nX
EndIf

cQuebra  := ''
cQuebra2 := ''
nLinTit  := 51
nColDay  := 175
nTimes   := 0

MontaCabec()

For xDay := nInicial To nFinal

	If cQuebra2 <> aGantt[xDay][7]
		dbSelectArea("ST9")
		dbSetOrder(1)
		dbSeek(xFilial("ST9")+aGantt[xDay][7])

		cCaption := ST9->T9_CODBEM+" "+subStr(ST9->T9_NOME,1,26)
		cBlKSay1 := '{|| OemToAnsi("'+cCaption+'")}'
		If !ContaObjet(1)
			Return .F.
		EndIf
		oSay     := TSay():New(nLinTit,028,&cBlkSay1,oPanel,,, .F., .F., .F., .T.,CLR_BLACK,,,, .F., .F., .F., .F., .F. )
		nLargSay := GetTextWidth(0,cCaption) / 1.8
		cCaption := oSay:cCaption
		nLinTit  += 7
		cQuebra2  := aGantt[xDay][7]
		nTimes++
	EndIf

	If cQuebra <> aGantt[xDay][4]

		dbSelectArea("ST4")
		dbSetOrder(1)
		dbSeek(xFilial("ST4")+aGantt[xDay][12])

		dbSelectArea("STF")
		dbSetOrder(1)
		dbSeek(xFilial("STF")+aGantt[xDay][4])

		//Servico
		cCaption := aGantt[xDay][12] + ' - ' + SubStr(ST4->T4_NOME,1,28)
		cBlKSay1 := '{|| OemToAnsi("'+cCaption+'")}'
		If !ContaObjet(1)
			Return .F.
		EndIf
		oSay     := TSay():New(nLinTit,038,&cBlkSay1,oPanel,,, .F., .F., .F., .T.,CLR_BLACK,,,, .F., .F., .F., .F., .F. )
		nLargSay := GetTextWidth(0,cCaption) / 1.8
		cCaption := oSay:cCaption
		cQuebra  := aGantt[xDay][4]

		//Sequencia
		cCaption := AllTrim(STF->TF_SEQRELA)
		cBlKSay1 := '{|| OemToAnsi("'+cCaption+'")}'
		If !ContaObjet(1)
			Return .F.
		EndIf
		oSay     := TSay():New(nLinTit,nUltCol,&cBlkSay1,oPanel,,, .F., .F., .F., .T.,CLR_BLACK,,,, .F., .F., .F., .F., .F. )
		nLargSay := GetTextWidth(0,cCaption) / 1.8
		cCaption := oSay:cCaption

		//Adiciona valores para as colunas do ponto de entrada MNTA365I
		If Len(aAddCol) > 0
			For nX := 1 To Len(aAddCol[2])
				cCaption := Eval(aAddCol[2][nX][3])
				cBlKSay1 := "{|| '"+cCaption+"'}"
				If !ContaObjet(1)
					Return .F.
				EndIf
				oSay     := TSay():New(nLinTit,nUltCol+aAddCol[2][nX][2],&cBlkSay1,oPanel,,, .F., .F., .F., .T.,CLR_BLACK,,,, .F., .F., .F., .F., .F. )
				nLargSay := GetTextWidth(0,cCaption) / 1.8
				cCaption := oSay:cCaption
			Next nX
		EndIf
		nTimes++
   	nLinTit  += 7
	Endif
Next xDay

//Monta Contorno
nFinLin := nTimes*7
If !ContaObjet(1)
	Return .F.
EndIf
@ 46,10  To 59+nFinLin,nCol Label "" Of oPanel PIXEL

//Monta Colunas
For nX := 1 to Len(aMesCorr)
	If !ContaObjet(1)
		Return .F.
	EndIf
	@ 46,aMesCorr[nX][3]-1 To 59+nFinLin,aMesCorr[nX][3]+aMesCorr[nX][5]-1 Label "" Of oPanel PIXEL
Next

nFinLin := nTimes*7
If Type("oBmpCim1") != "O"
	If !ContaObjet(2)
		Return .F.
	EndIf
	@ 010,005 BTNBMP oBmpCim1 Resource "ng_seta_cima" Size 24,21 Pixel Of oPanel2 Noborder Action GanttUp()
	@ oPanel2:nClientHeight-55+nSomaBotao,005 BTNBMP oBmpBai1 Resource "ng_seta_baixo" Size 24,21 Pixel Of oPanel2 Noborder Action GanttDown()
	oBmpCim1:Disable()
Endif

Processa( {|lEnd| MontaGant(nFinLin)},STR0014,STR0015 ) //"Aguarde"###"Atualizando o gráfico..."

//Fecha Dialog caso o Bem+Servico+Seq ter mais de 7 O.S para o mesmo mes
If lExitDlg .And. lOpenedDlg
	oDlg:End()
EndIf
nQtdObj := 0

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GanttDown ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 29/04/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pula para a proxima pagina		                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function GanttDown()

If nPagina != Len(aPaginas)
	nPagina++

	MsFreeObj(oPanel,.t.)

	nInicial := aPaginas[nPagina][2]
	nFinal   := aPaginas[nPagina][3]

	MontaLeft(nInicial,nFinal)
Endif

If nPagina != 1
	oBmpCim1:Enable()
Endif

If nPagina == Len(aPaginas)
	oBmpBai1:Disable()
Endif

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  GanttUp  ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 29/04/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pula para a proxima pagina		                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function GanttUp()
If nPagina != 1
	nPagina--

	MsFreeObj(oPanel,.t.)

	nInicial := aPaginas[nPagina][2]
	nFinal   := aPaginas[nPagina][3]

	MontaLeft(nInicial,nFinal)
Endif

If nPagina == 1
	oBmpCim1:Disable()
Else
	oBmpCim1:Enable()
Endif

If Len(aPaginas) > 1
	oBmpBai1:Enable()
Endif
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ContaObjet ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 29/04/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Conta a quantidade de objetos na tela                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAMNT - Manutencao de Ativos                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ContaObjet(nSoma)
nQtdObj += nSoma
If nQtdObj > 990
	MsgStop(STR0056) //"O plano superou a quantidade máxima de meses permitidos para o gráfico!"
	Return .f.
Endif
Return .t.

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} fVersion
Valida a versão de fontes relacionado, consistindo se é necessário atualizações.
@type function

@author Alexandre Santos
@since 12/09/2022

@return boolean, Define se as rotina de integração estão atualizadas
/*/
//-----------------------------------------------------------------------------------
Static Function fVersion()

	Local aVersion := {}
	Local lRet     := .T.

	If lHasMNTCM

		aVersion := {	{ 'MNTA340.prx'   , SToD( '20220914' ), '08:00' },;
						{ 'MNTUTIL.prx'   , SToD( '20220914' ), '08:00' },;
						{ 'MNTUTIL_OS.prw', SToD( '20220914' ), '08:00' } }
		
		If !( lRet := NgVldRpo( aVersion ) )

			NGMSGMEMO( STR0052,; // Atenção
				STR0057 ) // Para a correta utilização da integração com o módulo Compras deve-se realizar a aplicação do pacote acumulado de atualizações.

		EndIf

	EndIf
	
Return lRet
