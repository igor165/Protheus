#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV028    ³ Autor ³ Thiago Olis Machado   ³ Data ³01/03/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Quantidade total de litros utilizado em relacao aos parametros³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data    - Data inicio                                     ³±±
±±³          ³ Ate_Data   - Ate data                                        ³±±
±±³          ³ De_Ccusto  - De centro de custo                              ³±±
±±³          ³ Ate_Ccusto - Ate centro de custo                             ³±±
±±³          ³ De_CenTra  - De centro de trabalho                           ³±±
±±³          ³ Ate_CenTra - Ate sentro de trabalho                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nQtdLt  - Litros total                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV028(De_Data,Ate_Data,De_Ccusto,Ate_Ccusto,De_CenTra,Ate_CenTra,De_Familia,Ate_Familia)
Local aAreaOLD    := GetArea(),nQtdLt := 0
Local De_CcustoL  := If(De_Ccusto = Nil,Space(NGSEEKDIC("SX3","TQN_CCUSTO",2,"X3_TAMANHO")),De_Ccusto)
Local De_CenTraL  := If(De_CenTra = Nil,Space(NGSEEKDIC("SX3","TQN_CENTRA",2,"X3_TAMANHO")),De_CenTra) 
Local De_FamiliaL := If(De_Familia = Nil,Space(NGSEEKDIC("SX3","T9_CODFAMI",2,"X3_TAMANHO")),De_Familia) 

// Variáveis de Histórico de Indicadores
Local lMV_HIST := NGI6MVHIST()
Local aParams := {}
Local cCodIndic := "MNTV028"
Local nResult := 0

// Armazena os Parâmetros
If lMV_HIST
	aParams := {}
	aAdd(aParams, {"DE_DATA"    , De_Data})
	aAdd(aParams, {"ATE_DATA"   , Ate_Data})
	aAdd(aParams, {"DE_CCUSTO"  , De_Ccusto})
	aAdd(aParams, {"ATE_CCUSTO" , Ate_Ccusto})
	aAdd(aParams, {"DE_CENTRA"  , De_CenTra})
	aAdd(aParams, {"ATE_CENTRA" , Ate_CenTra})
	aAdd(aParams, {"DE_FAMILIA" , De_Familia})
	aAdd(aParams, {"ATE_FAMILIA", Ate_Familia})
	NGI6PREPPA(aParams, cCodIndic)
EndIf

If ValType(De_Data) != "D" .or. ValType(Ate_Data) != "D"
	NGI6PREPVA(cCodIndic, nResult)
	Return nResult
Endif

cAliasQry := GetNextAlias()
// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT SUM(TQN.TQN_QUANT) TQN_QUANT "
EndIf
cQuery += " FROM "+RetSqlName("TQN")+" TQN "
cQuery += " INNER JOIN "+RetSqlName("ST9")+" ST9 ON"
cQuery += " ( ST9.T9_CODBEM = TQN.TQN_FROTA "              
If ValType(De_FamiliaL) == "C" .and. ValType(Ate_Familia) == "C"
	cQuery += " AND ST9.T9_CODFAMI BETWEEN '"+De_FamiliaL+"' AND '"+Ate_Familia+"'"
EndIf	          
cQuery += " AND ST9.D_E_L_E_T_ <> '*' )"
cQuery += " WHERE TQN.TQN_FILIAL = '"+xFilial("TQN")+"' AND "
If ValType(De_CcustoL) == "C" .and. ValType(Ate_Ccusto) == "C"
	cQuery += " TQN.TQN_CCUSTO >= '"+De_CcustoL+"' AND TQN.TQN_CCUSTO <= '"+Ate_Ccusto+"' AND "
Endif                                                         
If ValType(De_CenTraL) == "C" .and. ValType(Ate_CenTra) == "C"
	cQuery += " TQN.TQN_CENTRA >= '"+De_CenTraL+"' AND TQN.TQN_CENTRA <= '"+Ate_CenTra+"' AND "
Endif                                                         
If ValType(De_Data) == "D"                                    
	cQuery += " TQN.TQN_DTABAS >= '"+Dtos(De_Data)+"' AND "
Endif
If ValType(Ate_Data) == "D"
	cQuery += " TQN.TQN_DTABAS <= '"+Dtos(Ate_Data)+"' AND "
Endif
cQuery += " TQN.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

dbSelectArea(cAliasQry)
dbGoTop()
While !Eof()
   nQtdLt += (cAliasQry)->(TQN_QUANT)
   DbSkip()
End
(cAliasQry)->(dbCloseArea())

// RESULTADO
nResult := nQtdLt
NGI6PREPVA(cCodIndic, nResult)

RestArea(aAreaOLD)
Return nResult