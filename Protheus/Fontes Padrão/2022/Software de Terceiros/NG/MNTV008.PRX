/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV008    ³ Autor ³ Inacio Luiz Kolling   ³ Data ³18/11/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Quantidade horas trabalhadas em manutencao O.S. FINALIZADAS  ³±±
±±³          ³ em relacao aos parametros  ( Homem hora trabalhada em manu-  ³±±
±±³          ³ tencao - HHNM )                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data  - Data inicio                                       ³±±
±±³          ³ Ate_Data - Ate data                                          ³±±
±±³          ³ De_Area  - De area inicio                                    ³±±
±±³          ³ Ate_Area - Ate area fim                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nQtdho   - Quantidade de horas disponivel                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV008(De_Data,Ate_Data,De_Area,Ate_Area)
Local aAreaOLD := GetArea(),nQtdho := 0
Local De_AreaL := If(De_Area = Nil,Space(NGSEEKDIC("SX3","TJ_CODAREA",2,"X3_TAMANHO")),De_Area)

// Variáveis de Histórico de Indicadores
Local lMV_HIST := NGI6MVHIST()
Local aParams := {}
Local cCodIndic := "MNTV008"
Local nResult := 0

// Armazena os Parâmetros
If lMV_HIST
	aParams := {}
	aAdd(aParams, {"DE_DATA" , De_Data})
	aAdd(aParams, {"ATE_DATA", Ate_Data})
	aAdd(aParams, {"DE_AREA" , De_Area})
	aAdd(aParams, {"ATE_AREA", Ate_Area})
	NGI6PREPPA(aParams, cCodIndic)
EndIf

If ValType(De_Data) != "D" .or. ValType(Ate_Data) != "D"
	NGI6PREPVA(cCodIndic, nResult)
	Return nResult
Endif

cAliasQry := GetNextAlias()
// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT STL.TL_QUANTID,STL.TL_TIPOHOR "
EndIf
cQuery += " FROM "+RetSqlName("STL")+" STL "
cQuery += " INNER JOIN "+RetSqlName("STJ")+" STJ "
cQuery += " ON STJ.TJ_ORDEM = STL.TL_ORDEM AND STJ.TJ_PLANO = STL.TL_PLANO AND"
If ValType(De_AreaL) == "C" .and. ValType(Ate_Area) == "C"
	cQuery += " STJ.TJ_CODAREA >= '"+De_AreaL+"' AND STJ.TJ_CODAREA <= '"+Ate_Area+"' AND "
Endif
cQuery += " STJ.TJ_SITUACA = 'L' AND STJ.TJ_TERMINO = 'S' AND "
cQuery += " STJ.TJ_FILIAL = '"+xFilial("STJ")+"' AND STJ.D_E_L_E_T_ <> '*' "
cQuery += " WHERE STL.TL_FILIAL = '"+xFilial("STL")+"' AND "
cQuery += " STL.TL_SEQRELA <> '0  ' AND STL.TL_TIPOREG = 'M' AND "
If ValType(De_Data) == "D"
	cQuery += " STL.TL_DTINICI >= '"+Dtos(De_Data)+"' AND "
Endif
If ValType(Ate_Data) == "D"
	cQuery += " STL.TL_DTINICI <= '"+Dtos(Ate_Data)+"' AND "
Endif
cQuery += " STL.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

dbSelectArea(cAliasQry)
dbGoTop()
While !Eof()
   nQtdho += If((cAliasQry)->TL_TIPOHOR <> "D",NGCONVERHORA((cAliasQry)->TL_QUANTID,;
                      "S","D"),(cAliasQry)->TL_QUANTID)
   DbSkip()
End
(cAliasQry)->(dbCloseArea())

cAliasQry := GetNextAlias()
// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT STT.TT_QUANTID,STT.TT_TIPOHOR "
EndIf
cQuery += " FROM "+RetSqlName("STT")+" STT "
cQuery += " INNER JOIN "+RetSqlName("STS")+" STS "
cQuery += " ON STS.TS_ORDEM = STT.TT_ORDEM AND STS.TS_PLANO = STT.TT_PLANO AND"
If ValType(De_AreaL) == "C" .and. ValType(Ate_Area) == "C"
	cQuery += " STS.TS_CODAREA >= '"+De_AreaL+"' AND STS.TS_CODAREA <= '"+Ate_Area+"' AND "
Endif
cQuery += " STS.TS_SITUACA = 'L' AND STS.TS_TERMINO = 'S' AND "
cQuery += " STS.TS_FILIAL = '"+xFilial("STS")+"' AND STS.D_E_L_E_T_ <> '*' "
cQuery += " WHERE STT.TT_FILIAL = '"+xFilial("STT")+"' AND "
cQuery += " STT.TT_SEQRELA <> '0  ' AND STT.TT_TIPOREG = 'M' AND "
If ValType(De_Data) == "D"
	cQuery += " STT.TT_DTINICI >= '"+Dtos(De_Data)+"' AND "
Endif
If ValType(Ate_Data) == "D"
	cQuery += " STT.TT_DTINICI <= '"+Dtos(Ate_Data)+"' AND "
Endif
cQuery += " STT.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

dbSelectArea(cAliasQry)
dbGoTop()
While !Eof()
   nQtdho += If((cAliasQry)->TT_TIPOHOR <> "D",NGCONVERHORA((cAliasQry)->TT_QUANTID,;
                      "S","D"),(cAliasQry)->TT_QUANTID)
   DbSkip()
End
(cAliasQry)->(dbCloseArea())

// RESULTADO
nResult := nQtdho
NGI6PREPVA(cCodIndic, nResult)

RestArea(aAreaOLD)
Return nResult