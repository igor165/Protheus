#INCLUDE "MNTR105.ch"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR105  ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 10/04/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Historico dos Retornos de Contador de um Determinado Bem.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR105()
	
	Local cString    := "STP"
	Local cDesc1     := STR0001 //"Emissao de relatorio de apresentacao do historico dos retornos de contador de um determinado Bem. "
	Local cDesc2     := ""
	Local cDesc3     := ""
	Local wnrel      := "MNTR105"
	Local lPrimeiro  := .T.//Variável de controle do SX1
	
	Private aReturn  := { "Zebrado", 1,"Administracao", 1, 2, 1, "",1 }
	Private nLastKey := 0
	Private cPerg    := "MNT105"
	Private Titulo   := STR0002 //"Historico do Contador"
	Private Tamanho  := "M"

	SetKey( VK_F9, { | | NGVersao( "MNTR105" , 1 ) } )

	//+-------------------------------------+
	//| Verifica as perguntas selecionadas  |
	//+-------------------------------------+
	//+-------------------------------------+
	//| Variaveis utilizadas para parametros|
	//| mv_par01     // Codigo do Bem       |
	//| mv_par02     // Data Inicio         |
	//| mv_par03     // Data Final          |
	//+-------------------------------------+

	Pergunte(cPerg,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		DbSelectArea("STP")
		Return
	EndIf

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R105Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R105Imp  ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 13.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR600                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R105Imp(lEnd,wnRel,titulo,tamanho)

	Local cRodaTxt := ""
	Local nCntImpr := 0
	Local oArqTrb //Obj. Tab. Temporária

	Private li := 80 ,m_pag := 1
	Private Cabec1   := STR0007 //"Data       Contador  Var.Dia  Viradas  Tipo Lancamento        Data       Contador  Var.Dia  Viradas  Tipo Lancamento"
	Private Cabec2   := " "
	Private nomeprog := "MNTR105"
	Private cTRB     := GetNextAlias()
	Private aVETINR := {}
	nTipo := IIF(aReturn[4]==1,15,18)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta os Cabecalhos                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//          1         2         3         4         5         6         7         8         9         0         1         2         3
	//0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	//-------------------------------------------------------------------------------------------------------------------------------------
	//Data       Contador  Var.Dia  Viradas  Tipo Lancamento        Data       Contador  Var.Dia  Viradas  Tipo Lancamento
	//-------------------------------------------------------------------------------------------------------------------------------------
	//Bem..: xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	//
	//Contador 1
	//xx/xx/xx  xxxxxxxxx   xxxxxx      xxx  xxxxxxxxxxxxxxx        xx/xx/xx  xxxxxxxxx   xxxxxx      xxx  xxxxxxxxxxxxxxx
	//xx/xx/xx  xxxxxxxxx   xxxxxx      xxx  xxxxxxxxxxxxxxx        xx/xx/xx  xxxxxxxxx   xxxxxx      xxx  xxxxxxxxxxxxxxx
	//
	//Contador 2
	//xx/xx/xx  xxxxxxxxx   xxxxxx      xxx  xxxxxxxxxxxxxxx        xx/xx/xx  xxxxxxxxx   xxxxxx      xxx  xxxxxxxxxxxxxxx
	//xx/xx/xx  xxxxxxxxx   xxxxxx      xxx  xxxxxxxxxxxxxxx        xx/xx/xx  xxxxxxxxx   xxxxxx      xxx  xxxxxxxxxxxxxxx
	aCampos  := {}
		AAdd(aCAMPOS,{"CODBEM"   ,"C",16,0})
		AAdd(aCAMPOS,{"DTLEITU"  ,"D",08,0})
		AAdd(aCAMPOS,{"POSCONT"  ,"N",09,0})
		AAdd(aCAMPOS,{"VARDIA"   ,"N",06,0})
		AAdd(aCAMPOS,{"TIPO"     ,"C",01,0})
		AAdd(aCAMPOS,{"VIRADAS"  ,"N",03,0})
		AAdd(aCAMPOS,{"TIPOLAN"  ,"C",15,0})

	//Cria Tabela Temporária
	oArqTrb := NGFwTmpTbl(cTRB,aCAMPOS,{{"CODBEM","TIPO","DTLEITU"}})

	//----------------------------------------------------------------
	SetRegua(LastRec())
	DbSelectArea("ST9")
	DbSetOrder(01)
	If DbSeek(xFilial("ST9")+MV_PAR01)
		If ST9->T9_TEMCONT <> "N"
			DbSelectArea("STP")
			DbSetOrder(05)
			If DbSeek(xFILIAL("STP")+ST9->T9_CODBEM)
				While !Eof() .And. xFilial("STP") = STP->TP_FILIAL .And. STP->TP_CODBEM = ST9->T9_CODBEM
					If STP->TP_DTLEITU >= MV_PAR02 .And. STP->TP_DTLEITU <= MV_PAR03 .And. STP->TP_SITUACA = "L"
						NGMR105TRB(STP->TP_CODBEM,STP->TP_DTLEITU,STP->TP_POSCONT,STP->TP_VARDIA,"1",STP->TP_VIRACON,STP->TP_TIPOLAN)
					EndIf
					DbSelectArea("STP")
					DbSkip()
				End
			EndIf

			DbSelectArea("TPP")
			DbSetOrder(05)
			If DbSeek(xFILIAL("TPP")+ST9->T9_CODBEM)
				While !Eof() .And. xFilial("TPP") = TPP->TPP_FILIAL .And. TPP->TPP_CODBEM = ST9->T9_CODBEM
					If TPP->TPP_DTLEIT >= MV_PAR02 .And. TPP->TPP_DTLEIT <= MV_PAR03 .And. TPP->TPP_SITUAC = "L"
						NGMR105TRB(TPP->TPP_CODBEM,TPP->TPP_DTLEIT,TPP->TPP_POSCON,TPP->TPP_VARDIA,"2",TPP->TPP_VIRACO,TPP->TPP_TIPOLA)
					EndIf
					DbSelectArea("TPP")
					DbSkip()
				End
			EndIf
		EndIf
	EndIf

	DbSelectArea(cTRB)
	DbGoTop()
	If (cTRB)->(Eof())
		//Deleta o arquivo temporario fisicamente
		//NGDELETRB(cTRB,cArqTrb)
		oArqTrb:Delete()
		MsgStop(STR0019, STR0018) //"Não existem dados para montar o relatório."###"Atenção"
		Return .F.
	Endif
	lPRINT      := .T.
	While !Eof()
		nCONTREGI := 1
		lPRINT1   := .T.
		lPRINT2   := .T.
		cBEM  	 := (cTRB)->CODBEM
		cTIPO 	 := (cTRB)->TIPO

		While !Eof() .AND. (cTRB)->CODBEM =  cBEM .AND. (cTRB)->TIPO = cTIPO

			If lPRINT
				Somalinha()
				@ Li,000 Psay STR0006 //"Bem..:"
				@ Li,007 Psay (cTRB)->CODBEM
				DbselectArea("ST9")
				Dbsetorder(1)
				If DbSeek(xFilial("ST9")+(cTRB)->CODBEM)
					@ Li,024 Psay ST9->T9_NOME
				Endif
				lPRINT := .f.
			Endif

			If lPRINT1 .AND. cTIPO = '1'
				Somalinha()
				Somalinha()
				@ Li,000 Psay STR0008  //"Contador: 1"
				lPRINT1 := .f.
			EndIf
			If lPRINT2 .AND. cTIPO = '2'
				Somalinha()
				Somalinha()
				@ Li,000 Psay STR0009  //"Contador: 2"
				lPRINT2 := .f.
			EndIf

			If nCONTREGI = 1
				Somalinha()
				@ Li,000 Psay (cTRB)->DTLEITU
				@ Li,010 Psay (cTRB)->POSCONT Picture '999999999'
				@ Li,022 Psay (cTRB)->VARDIA  Picture '999999'
				@ Li,034 Psay PADL(Transform((cTRB)->VIRADAS,"@E 999"),3)
				@ Li,039 Psay (cTRB)->TIPOLAN Picture "@!"
			EndIf
			If nCONTREGI = 2
				@ Li,062 Psay (cTRB)->DTLEITU
				@ Li,072 Psay (cTRB)->POSCONT Picture '999999999'
				@ Li,084 Psay (cTRB)->VARDIA  Picture '999999'
				@ Li,096 Psay PADL(Transform((cTRB)->VIRADAS,"@E 999"),3)
				@ Li,101 Psay (cTRB)->TIPOLAN Picture "@!"
				nCONTREGI := 0
			EndIf
			nCONTREGI := nCONTREGI + 1
			DbSelectArea(cTRB)
			DbSkip()
		End
	End
	Roda(nCntImpr,cRodaTxt,Tamanho)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principal             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex("STP")
	RetIndex("TPP")
	RetIndex("STW")
	RetIndex("TPW")

	//NGDELETRB(cTRB,cArqTrb)
	oArqTrb:Delete()

	Set Filter To
	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf
	Ms_Flush()

Return Nil
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³NGMR105TRB³ Autor ³ Denis Hyroshi de Souza³ Data ³ 15/05/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Cria arquivo de trabalho                                   |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ NGMR105TRB()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ mntr105                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function NGMR105TRB(cBEM,dDTLEITU,nPOSCONT,nVARDIA,cTIPO,nVIRADAS,cTPLANC)

	DbSelectArea(cTRB)
	DbSetOrder(1)
	(cTRB)->(DbAppend())
	(cTRB)->CODBEM  := cBEM
	(cTRB)->DTLEITU := dDTLEITU
	(cTRB)->POSCONT := nPOSCONT
	(cTRB)->VARDIA  := nVARDIA
	(cTRB)->TIPO    := cTIPO
	(cTRB)->VIRADAS := nVIRADAS
	If cTPLANC == 'I'
		cTPLANC := STR0011 //"Inclusão"
	Elseif cTPLANC == 'C'
		cTPLANC := STR0012 //"Contador"
	Elseif cTPLANC == 'P'
		cTPLANC := STR0013 //"Produção"
	Elseif cTPLANC == 'A'
		cTPLANC := STR0014 //"Abastecimento"
	Elseif cTPLANC == 'V'
		cTPLANC := STR0015 //"Virada"
	Elseif cTPLANC == 'M'
		cTPLANC := STR0016 //"Movimentação"
	Elseif cTPLANC == 'Q'
		cTPLANC := STR0017 //"Quebra"
	Endif
	(cTRB)->TIPOLAN := cTPLANC
Return .t.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³ NG Informatica Ltda   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaLinha()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ DOCR500                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static  Function Somalinha()
	Li++
	If !Eof() .AND. Li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	EndIf
Return .T.