#INCLUDE "mntr525.ch"
#INCLUDE "PROTHEUS.CH"

/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥MNTR525   ≥ Autor ≥ Evaldo Cevinscki Jr.  ≥ Data ≥ 31/07/07 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥Relatorio de Previsao de Retirada de Pneus                  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥Generico                                                    ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function MNTR525()

	Local oARQTR525 //Obj. Tabela Tempor·ria
	Local nSizeFil := FwSizeFilial()

	WNREL      := "MNTR525"
	LIMITE     := 132
	cDESC1     := STR0001 //"O relatÛrio apresentar· uma previs„o da"
	cDESC2     := STR0002 //"retirada mensal de pneus, por motivo de"
	cDESC3     := STR0003 //"desgaste do sulco."
	cSTRING    := "TQS"

	Private lImpRel  := .T.
	Private cTRB 	 := GetNextAlias() //Alias Tabela Tempor·ria
	Private NOMEPROG := "MNTR525"
	Private TAMANHO  := "G"
	Private aRETURN  := {"Zebrado",1,"Administracao",1,2,1,"",1}
	Private TITULO   := STR0004 //"RelatÛrio de Previs„o de Retirada de Pneus"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
	Private aVETINR  := {}
	Private cPERG 	 := "MNR525"
	Private aPerg 	 := {}
	Private aImprime := {}
	Private aINDR525 := {}
	Private aDados 	 := {}
	SetKey( VK_F9, { | | NGVersao( "MNTR525" , 1 ) } )

	Pergunte(cPERG,.F.)

	aPos1      := {15,1,95,315 }

	aDBFR525 := {}
		Aadd(aDBFR525,{"CODHUB"  ,"C", 02,0})
		Aadd(aDBFR525,{"FILIAL"  ,"C", nSizeFil,0})
		Aadd(aDBFR525,{"FAMILIA" ,"C", 06,0})
		Aadd(aDBFR525,{"MEDIDA"  ,"C", 06,0})
		Aadd(aDBFR525,{"MODELO"  ,"C", 10,0})
		Aadd(aDBFR525,{"PNEU"    ,"C", 16,0})
		Aadd(aDBFR525,{"NUMFOG"  ,"C", 10,0})
		Aadd(aDBFR525,{"DOT"     ,"C", 04,0})
		Aadd(aDBFR525,{"VIDA"    ,"C", 02,0})
		Aadd(aDBFR525,{"SULCO"   ,"N", 06,2})
		Aadd(aDBFR525,{"RENDOR"  ,"N", 05,0})
		Aadd(aDBFR525,{"RENDREF" ,"N", 05,0})
		Aadd(aDBFR525,{"PREVROD" ,"N", 06,0})
		Aadd(aDBFR525,{"VARDIA"  ,"N", 06,0})
		Aadd(aDBFR525,{"QTDDUR"  ,"N", 09,2})
		Aadd(aDBFR525,{"QTDARR"  ,"N", 04,0})
		Aadd(aDBFR525,{"CUSTOTR" ,"N", 09,2})
		Aadd(aDBFR525,{"DTTROCA" ,"D", 08,0})
		Aadd(aDBFR525,{"MES"     ,"C", 08,0})
		Aadd(aDBFR525,{"ANO"     ,"C", 04,0})
		Aadd(aDBFR525,{"DESMED"  ,"C", 20,0})
		Aadd(aDBFR525,{"DESMOD"  ,"C", 20,0})

	TITULO   := STR0004 //"RelatÛrio de Previs„o de Retirada de Pneus"

	//Indice Tabela Tempor·ria
	aINDR525 := {{"CODHUB","FILIAL","DTTROCA","FAMILIA","MEDIDA","MODELO"},{"CODHUB","DTTROCA"}}
	//CriaÁ„o Tabela Tempor·ria
	oARQTR525 := NGFwTmpTbl(cTRB, aDBFR525, aINDR525)

	//+-----------------------------------------+
	//| Envia controle para a funcao SETPRINT   |
	//+-----------------------------------------+
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		DbSelectArea("TQS")
		Return
	EndIf

	SetDefault(aReturn,cSTRING)

	Processa({ |lEnd| MNR525PROC(@lImpRel)}, STR0017,STR0018 )  //"Aguarde..."###"Processando Registros..."
	If lImpRel
		RptStatus({|lEND| MNR525IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0017,STR0018)  //"Aguarde..."###"Processando Registros..."
	EndIf
	Dbselectarea(cTRB)
	oARQTR525:Delete()

Return lImpRel
/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    |MNR525PROC| Autor ≥ Evaldo Cevinscki Jr.  ≥ Data ≥31/07/2007≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÑo ≥Processa registros                                          ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥MNTR525                                                     ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
/*/
Function MNR525PROC()
	Local nCusto 	:= 0
	Local cNGSEREF	:= GetMv("MV_NGSEREF")

	nAnoAtu := Val(Substr(DtoS(dDataBase),1,4))
	dDtMax := CtoD('31/12/'+AllTrim(Str(nAnoAtu + 2)))
	nProxMes := Month(dDataBase)+1
	nAno := nAnoAtu
	If nProxMes > 12
		nProxMes := nProxMes - 12
		nAno := nAno + 1
	EndIf
	dDtMin  := CtoD('01/'+StrZero(nProxMes,2)+'/'+AllTrim(Str(nAno)))

	cAliasQry := GetNextAlias()

	cQuery := " SELECT tqs.tqs_filial,tqs.tqs_codbem,tqs.tqs_medida,tqs.tqs_desenh,tqs.tqs_numfog,tqs.tqs_sulcat,"
	cQuery += " tqs.tqs_bandaa,tqs.tqs_kmor,tqs.tqs_kmr1,tqs.tqs_kmr2,tqs.tqs_kmr3,tqs.tqs_kmr4,tqs.tqs_dot,"
	cQuery += " tqs.tqs_eixo,tqs.tqs_tipeix,tqs.tqs_posic,tqs.tqs_eixo,st9.t9_codesto,st9.t9_valcpa,st9.t9_codfami,"
	cQuery += " (SELECT TQT_DESMED FROM "+RetSqlName("TQT")+" TQT WHERE TQT.TQT_MEDIDA = TQS.TQS_MEDIDA AND " + NGMODCOMP("TQT","TQS") + "AND TQT.d_e_l_e_t_ <> '*')DESMED,"
	cQuery += " st9.t9_tipmod,(SELECT TQR_DESMOD FROM "+RetSqlName("TQR")+" TQR WHERE TQR.TQR_TIPMOD = ST9.T9_TIPMOD AND " + NGMODCOMP("TQR","ST9") + " AND TQR.d_e_l_e_t_ <> '*')DESMOD,"
	cQuery += " st9.t9_fabrica,st9.t9_nome,st9.t9_dtultac,st9.t9_poscont,st9.t9_contacu,st9.t9_vardia,"
	cQuery += " tqx.tqx_kmespo,tqx.tqx_sulcoo,tqx.tqx_kmespo,tqu.tqu_sulco,tqu.tqu_kmesp1,tqu.tqu_kmesp2,tqu.tqu_kmesp3,tqu.tqu_kmesp4 "
	cQuery += " FROM "+RetSqlName("TQS")+" TQS,"+RetSqlName("ST9")+" ST9,"
	cQuery +=   RetSqlName("TQX")+" TQX,"+RetSqlName("TQU")+" TQU "
	cQuery += " WHERE tqs.tqs_codbem = st9.t9_codbem "
	cQuery += " AND st9.t9_sitbem = 'A' "
	cQuery += " AND st9.t9_status = '"+GetMv("MV_NGSTAPL")+"' "
	cQuery += " AND st9.t9_codfami BETWEEN '"+MV_PAR01+"' AND '"+MV_PAR02+"' "
	cQuery += " AND st9.t9_tipmod BETWEEN '"+MV_PAR03+"' AND '"+MV_PAR04+"' "
	cQuery += " AND tqs.tqs_medida BETWEEN '"+MV_PAR05+"' AND '"+MV_PAR06+"' "
	cQuery += " AND tqs.tqs_bandaa <= '"+AllTrim(Str(MV_PAR08))+"'  "
	cQuery += " AND tqs.tqs_medida = tqx.tqx_medida "
	cQuery += " AND st9.t9_tipmod = tqx.tqx_tipmod "
	cQuery += " AND tqs.tqs_desenh = tqu.tqu_desenh "
	cQuery += " AND tqs.d_e_l_e_t_ <> '*' AND st9.d_e_l_e_t_ <> '*' "
	cQuery += " AND tqx.d_e_l_e_t_ <> '*' AND tqu.d_e_l_e_t_ <> '*' "
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
	/*
	LEFT JOIN SZ1010 SZ1 ON sz1.z1_films=tqs.tqs_filial and sz1.d_e_l_e_t_<>'*'
	LEFT JOIN zzh010 zzh ON zzh.zzh_hub=sz1.z1_hub and zzh.d_e_l_e_t_<>'*',
	*/
	ProcRegua(LastRec())

	While !Eof()
		IncProc(STR0017,STR0018)       //"Aguarde..."###"Processando Registros..."

		DbSelectArea(cTRB)
		RecLock((cTRB), .T.)
		(cTRB)->FILIAL  := (cAliasQry)->TQS_FILIAL
		DbSelectArea("TSL")
		DbSetOrder(1)
		If DbSeek(xFilial("TSL")+(cTRB)->FILIAL)
			(cTRB)->CODHUB := TSL->TSL_HUB
		EndIf
		(cTRB)->FAMILIA := (cAliasQry)->T9_CODFAMI
		(cTRB)->MODELO  := (cAliasQry)->T9_TIPMOD
		(cTRB)->DESMOD  := (cAliasQry)->DESMOD
		(cTRB)->MEDIDA  := (cAliasQry)->TQS_MEDIDA
		(cTRB)->DESMED  := (cAliasQry)->DESMED
		(cTRB)->PNEU    := (cAliasQry)->TQS_CODBEM
		(cTRB)->NUMFOG  := (cAliasQry)->TQS_NUMFOG
		(cTRB)->DOT     := (cAliasQry)->TQS_DOT
		(cTRB)->SULCO   := (cAliasQry)->TQS_SULCAT
		(cTRB)->RENDOR  := (cAliasQry)->TQX_KMESPO / (cAliasQry)->TQX_SULCOO
		If (cAliasQry)->TQS_BANDAA = '1'
			(cTRB)->RENDREF := 0
			(cTRB)->VIDA   := "OR"
		ElseIf (cAliasQry)->TQS_BANDAA = '2'
			(cTRB)->RENDREF := ((cAliasQry)->TQX_KMESPO * ((cAliasQry)->TQU_KMESP1/100)) /	(cAliasQry)->TQU_SULCO
			(cTRB)->VIDA   := "R1"
		ElseIf (cAliasQry)->TQS_BANDAA = '3'
			(cTRB)->RENDREF := ((cAliasQry)->TQX_KMESPO * ((cAliasQry)->TQU_KMESP2/100)) /	(cAliasQry)->TQU_SULCO
			(cTRB)->VIDA   := "R2"
		ElseIf (cAliasQry)->TQS_BANDAA = '4'
			(cTRB)->RENDREF := ((cAliasQry)->TQX_KMESPO * ((cAliasQry)->TQU_KMESP3/100)) /	(cAliasQry)->TQU_SULCO
			(cTRB)->VIDA   := "R3"
		ElseIf (cAliasQry)->TQS_BANDAA = '5'
			(cTRB)->RENDREF := ((cAliasQry)->TQX_KMESPO * ((cAliasQry)->TQU_KMESP4/100)) /	(cAliasQry)->TQU_SULCO
			(cTRB)->VIDA   := "R4"
		EndIf
		(cTRB)->PREVROD := ((cAliasQry)->TQS_SULCAT - MV_PAR07) * If((cAliasQry)->TQS_BANDAA = '1',(cTRB)->RENDOR,(cTRB)->RENDREF)
		(cTRB)->VARDIA  := (cAliasQry)->T9_VARDIA
		(cTRB)->QTDDUR  := Round((((cTRB)->PREVROD / (cTRB)->VARDIA) / 30) ,2)
		(cTRB)->QTDARR  := If((cTRB)->QTDDUR < 1 .and. (cTRB)->QTDDUR > 0 ,1,Round(((cTRB)->QTDDUR),0))
		(cTRB)->DTTROCA := dDataBase + (30 * (cTRB)->QTDARR)
		(cTRB)->ANO := SubStr(DtoS((cTRB)->DTTROCA),1,4)
		cMES := SubStr(DtoS((cTRB)->DTTROCA),5,2)

		Do Case
			Case cMes = '01'
				(cTRB)->MES := STR0019 //"Janeiro"
			Case cMes = '02'
				(cTRB)->MES := STR0020 //"Fevereiro"
			Case cMes = '03'
				(cTRB)->MES := STR0021 //"MarÁo"
			Case cMes = '04'
				(cTRB)->MES := STR0022 //"Abril"
			Case cMes = '05'
				(cTRB)->MES := STR0023 //"Maio"
			Case cMes = '06'
				(cTRB)->MES := STR0024 //"Junho"
			Case cMes = '07'
				(cTRB)->MES := STR0025 //"Julho"
			Case cMes = '08'
				(cTRB)->MES := STR0026 //"Agosto"
			Case cMes = '09'
				(cTRB)->MES := STR0027 //"Setembro"
			Case cMes = '10'
				(cTRB)->MES := STR0028	//"Outubro"
			Case cMes = '11'
				(cTRB)->MES := STR0029 //"Novembro"
			Case cMes = '12'
				(cTRB)->MES := STR0030 //"Dezembro"
		End Do

		If ((cAliasQry)->TQS_BANDAA == '1' .OR. (cAliasQry)->TQS_BANDAA == AllTrim(STR(MV_PAR08))) .AND. (cAliasQry)->TQS_EIXO == '1'
			If !Empty((cAliasQry)->T9_CODESTO)
				DbSelectArea("SB2")
				DbSetOrder(1)
				If DbSeek(xFilial("SB2")+(cAliasQry)->T9_CODESTO)
					nCusto := SB2->B2_CM1
				EndIf
			Else
				nCusto := (cAliasQry)->T9_VALCPA
			EndIf
		Else
			cAliasQry2 := GetNextAlias()
			cQuery2 := " SELECT * FROM "+RetSqlName("TRA")
			cQuery2 += " WHERE TRA_SERVIC = '"+cNGSEREF+"' "
			cQuery2 += " AND TRA_MEDIDA = '"+(cAliasQry)->TQS_MEDIDA+"' AND "
			cQuery2 += " D_E_L_E_T_ <> '*' "
			cQuery2 += " ORDER BY TRA_BANDA "
			cQuery2 := ChangeQuery(cQuery2)
			dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery2),cAliasQry2, .F., .T.)

			lCusto := .f.
			While !Eof()
				If Val((cAliasQry)->TQS_BANDAA) + 1 <= Val((cAliasQry2)->TRA_BANDA) .And. !lCusto
					nCusto := (cAliasQry2)->TRA_CUSTO
					lCusto := .T.
				EndIf
				If !lCusto
					nCusto := (cAliasQry2)->TRA_CUSTO
				EndIf
				DbSelectArea(cAliasQry2)
				DbSkip()
			End
			(cAliasQry2)->(DbCloseArea())
		EndIf
		(cTRB)->CUSTOTR := nCusto
		nCusto := 0
		(cTRB)->(MsUnLock())

		//deleta as trocas superiores ao final dos 2 proximos anos
		If (cTRB)->DTTROCA > dDtMax .Or. (cTRB)->DTTROCA < dDtMin
			RecLock((cTRB),.F.)
			dbDelete()
			(cTRB)->(MsUnLock())
		EndIf
		DbSelectArea(cAliasQry)
		DbSkip()
	End
	(cALIASQRY)->(dbCloseArea())

	DbSelectArea(cTRB)
	DbGoTop()
	If Eof()
		MsgInfo(STR0031,"ATEN«√O")  //"N„o existe dados para montar o relatÛrio."
		lImpRel := .F.
	EndIf

Return lImpRel
/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    |MNR525IMP | Autor ≥ Evaldo Cevinscki Jr.  ≥ Data ≥31/07/2007≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÑo ≥Chamada do Relat¢rio                                        ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥MNTR525                                                     ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
/*/
Function MNR525IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cFil := " ",cHUB := " ",cMes := " ",cAno := " "
	Local i,nTotAno1 := 0,nTotAno2 := 0,nTotGeral := 0
	Private aTot := {},aTot2 := {}
	Private cRODATXT := ""
	Private nCNTIMPR := 0
	Private li := 80 ,m_pag := 1
	Store 0 To nQtdPneusM,nCustoM,nQtdPneusF,nCustoF,nQtdPneusH,nCustoH


	nTIPO  := IIf(aReturn[4]==1,15,18)
	If MV_PAR09 = 1
		CABEC1 := "                                                                                                                               Rend.     Rend.      Previs„o  Var.   Qtd.MÍs   Qtd.MÍs       Custo"
		CABEC2 := STR0032 //"FamÌlia  Medida  Des.Medida              Modelo      Des.Modelo              Cod.Pneu           Num.Fogo    DOT   Vida  Sulco  OR        Reformas   Rodagem   Dia    DuraÁ„o   Arred.        Troca"
	ElseIf MV_PAR09 = 2 .or. MV_PAR09 = 3
		CABEC1 := "HUB"+Space(14)+MNT525CAB()
		CABEC2 := " "
	EndIf


	/*
	MV_PAR09 = 1 (DETALHADO)
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234
	***************************************************************************************************************************************************************************************************
	RelatÛrio de Previsao de Retirada de Pneus
	***************************************************************************************************************************************************************************************************
	Rend.     Rend.      Previs„o  Var.   Qtd.MÍs   Qtd.MÍs       Custo
	FamÌlia  Medida  Des.Medida              Modelo      Des.Modelo              Cod.Pneu           Num.Fogo    DOT   Vida  Sulco  OR        Reformas   Rodagem   Dia    DuraÁ„o   Arred.        Troca
	___________________________________________________________________________________________________________________________________________________________________________________________________
	Mes: xxxxxxxx  Ano: 9999     HUB: xx            Filial: xx - xxxxxxxxxxxxxxxxxxxx

	xxxxxx   xxxxxx  xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxx   9999999999  9999  xx    99.99  99999     99999      999999    99999  99,999.99 9999          99,999.99
	xxxxxx   xxxxxx  xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxx   9999999999  9999  xx    99.99  99999     99999      999999    99999  99,999.99 9999          99,999.99
	xxxxxx   xxxxxx  xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxx   9999999999  9999  xx    99.99  99999     99999      999999    99999  99,999.99 9999          99,999.99
	___________________________________________________________________________________________________________________________________________________________________________________________________

	MV_PAR09 = 2 (RESUMIDO)
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234
	***************************************************************************************************************************************************************************************************
	RelatÛrio de Previsao de Retirada de Pneus - Resumido Qtd.
	***************************************************************************************************************************************************************************************************
	HUB              JAN     FEV     MAR     ABR     MAI     JUN     JUL     AGO     SET     OUT     NOV     DEZ     ANO1    ANO2    Total HUB
	___________________________________________________________________________________________________________________________________________________________________________________________________
	xxxxxxxxxxxxxxx  99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999
	99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999
	99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999
	99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999
	___________________________________________________________________________________________________________________________________________________________________________________________________
	MV_PAR09 = 3 (RESUMIDO)
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234
	***************************************************************************************************************************************************************************************************
	RelatÛrio de Previsao de Retirada de Pneus - Resumido Custo
	***************************************************************************************************************************************************************************************************
	HUB              JAN        FEV        MAR        ABR        MAI        JUN        JUL        AGO        SET        OUT        NOV        DEZ        ANO1       ANO2    Total HUB
	___________________________________________________________________________________________________________________________________________________________________________________________________
	xxxxxxxxxxxxxxx  99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999
	99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999
	99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999
	99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999   99999
	___________________________________________________________________________________________________________________________________________________________________________________________________

	*/

	DbSelectArea(cTRB)
	DbSetOrder(1)
	DbGotop()
	ProcRegua(RecCount())

	While !Eof()

		IncProc()

		If lEnd
			@ Prow()+1,001 PSay STR0033  //"CANCELADO PELO OPERADOR"
			Exit
		EndIf
		If MV_PAR09 = 1
			If cMes <> (cTRB)->MES .OR. cAno <> (cTRB)->ANO .OR. cHUB <> (cTRB)->CODHUB .OR. cFil <> (cTRB)->FILIAL
				cMes := (cTRB)->MES
				cAno := (cTRB)->ANO
				NgSomaLi(58)
				@ Li,000 PSay STR0034+(cTRB)->MES //"MÍs: "
				@ Li,015 PSay STR0035+(cTRB)->ANO //"Ano: "

				cHUB := (cTRB)->CODHUB
				DbSelectArea("TRW")
				TRW->(DbSetOrder(1))
				If DBSeek(xFilial("TRW")+(cTRB)->CODHUB)
					@ Li,029 PSay "HUB: "+SUBSTR(TRW->TRW_DESHUB,1,15)
				EndIf

				cFil := (cTRB)->CODHUB
				DbSelectArea("SM0")
				SM0->(DbSetOrder(1))
				If DBSeek(SM0->M0_CODIGO+(cTRB)->FILIAL)
					@ Li,058 PSay STR0036+(cTRB)->FILIAL+" - "+SUBSTR(SM0->M0_FILIAL,1,20) //"Filial: "
				EndIf
				NgSomaLi(58)
				NgSomaLi(58)
			EndIf

			@ Li,000 PSay (cTRB)->FAMILIA
			@ Li,009 PSay (cTRB)->MEDIDA
			@ Li,017 PSay (cTRB)->DESMED
			@ Li,041 PSay (cTRB)->MODELO
			@ Li,053 PSay (cTRB)->DESMOD
			@ Li,077 PSay (cTRB)->PNEU
			@ Li,096 PSay (cTRB)->NUMFOG
			@ Li,108 PSay (cTRB)->DOT
			@ Li,114 PSay (cTRB)->VIDA
			@ Li,120 PSay (cTRB)->SULCO
			@ Li,127 PSay (cTRB)->RENDOR
			@ Li,137 PSay (cTRB)->RENDREF
			@ Li,148 PSay (cTRB)->PREVROD
			@ Li,158 PSay (cTRB)->VARDIA
			@ Li,162 PSay (cTRB)->QTDDUR Picture "@E 999,999.99"
			@ Li,175 PSay (cTRB)->QTDARR
			@ Li,184 PSay (cTRB)->CUSTOTR Picture "@E 999,999.99"

			++nQtdPneusM
			nCustoM += (cTRB)->CUSTOTR
			++nQtdPneusF
			nCustoF += (cTRB)->CUSTOTR
			++nQtdPneusH
			nCustoH += (cTRB)->CUSTOTR
			NgSomaLi(58)

			DbSelectArea(cTRB)
			DbSetOrder(1)
			DbSkip()

			If cMes <> (cTRB)->MES .OR. cAno <> (cTRB)->ANO
				NgSomaLi(58)
				@ Li,000 PSay STR0037+AllTrim(STR(nQtdPneusM)) //"Total MÍs......:                                                                                                                                               Qtd.Pneus.....: "
				@ Li,184 PSay nCustoM Picture "@E 999,999.99"
				NgSomaLi(58)
				nQtdPneusM := 0
				nCustoM    := 0
			EndIf

			If cFil <> (cTRB)->FILIAL
				NgSomaLi(58)
				@ Li,000 PSay STR0038+AllTrim(STR(nQtdPneusF)) //"Total Filial...:                                                                                                                                               Qtd.Pneus.....: "
				@ Li,184 PSay nCustoF Picture "@E 999,999.99"
				NgSomaLi(58)
				nQtdPneusF := 0
				nCustoF    := 0
			EndIf

			If cHUB <> (cTRB)->CODHUB
				NgSomaLi(58)
				@ Li,000 PSay STR0039+AllTrim(STR(nQtdPneusH)) //"Total HUB......:                                                                                                                                               Qtd.Pneus.....: "
				@ Li,184 PSay nCustoH Picture "@E 999,999.99"
				NgSomaLi(58)
				nQtdPneusH := 0
				nCustoH    := 0
			EndIf
		ElseIf MV_PAR09 = 2  //Resumido - Qtd
			If cHUB <> (cTRB)->CODHUB
				cHUB := (cTRB)->CODHUB
				NgSomaLi(58)
				DbSelectArea("TRW")

				TRW->(DbSetOrder(1))
				If DBSeek(xFilial("TRW")+(cTRB)->CODHUB)
					@ Li,000 PSay SUBSTR(TRW->TRW_DESHUB,1,15)
				EndIf

				MNT525RES((cTRB)->CODHUB)
				nAno1 := 0
				nAno2 := 0
				nTotHub := 0

				For i := 1 to Len(aDados)    //imprime a qtd de pneus nos respectivos meses de troca
					If aDados[i][5] <= 12
						ny := 9 + (aDados[i][5] * 8)
						@ Li,ny PSay aDados[i][3]
						nTotHub += aDados[i][3]
						Aadd(aTot,{aDados[i][5],aDados[i][3],aDados[i][6],aDados[i][7]})
					EndIf
					If !Empty(aDados[i][6])
						If aDados[i][5] > 12
							nTotHub += aDados[i][3]
						EndIf
						nAno1 += aDados[i][6]
					EndIf
					If !Empty(aDados[i][7])
						nAno2 += aDados[i][7]
					EndIf
				Next i
				nTotAno1 += nAno1
				nTotAno2 += nAno2
				nTotHub  += nAno2
				nTotGeral += nTotHub
				@ Li,113 PSay nAno1
				@ Li,121 PSay nAno2
				@ Li,129 PSay nTotHub
				NgSomaLi(58)
			EndIf

			DbSelectArea(cTRB)
			DbSetOrder(1)
			DbSkip()

			If Eof()
				MNT525TOTR()
				NgSomaLi(58)
				@ Li,000 PSay STR0040 //"Total Geral....:"
				For i:=1 To Len(aTot2)
					ny := 9 + (aTot2[i][1] * 8)
					@ Li,ny PSay aTot2[i][2]
				Next i
				@ Li,113 PSay nTotAno1
				@ Li,121 PSay nTotAno2
				@ Li,129 PSay nTotGeral
			EndIf

		ElseIf MV_PAR09 = 3
			If cHUB <> (cTRB)->CODHUB
				cHUB := (cTRB)->CODHUB
				NgSomaLi(58)
				DbSelectArea("TRW")
				TRW->(DbSetOrder(1))
				If DBSeek(xFilial("TRW")+(cTRB)->CODHUB)
					@ Li,000 PSay SUBSTR(TRW->TRW_DESHUB,1,15)
				EndIf
				MNT525RES((cTRB)->CODHUB)
				nAno1 := 0
				nAno2 := 0
				nTotHub := 0

				For i := 1 to Len(aDados)    //imprime o custo dos pneus nos respectivos meses de troca
					If aDados[i][5] <= 12 //!Empty(aDados[i][5])
						ny := 6 + (aDados[i][5] * 11)
						@ Li,ny PSay aDados[i][4]
						nTotHub += aDados[i][4]
						Aadd(aTot,{aDados[i][5],aDados[i][4],aDados[i][6],aDados[i][7]})
					EndIf
					If !Empty(aDados[i][6])
						If aDados[i][5] > 12
							nTotHub += aDados[i][4]
						EndIf
						nAno1 += aDados[i][6]
					EndIf
					If !Empty(aDados[i][7])
						nAno2 += aDados[i][7]
					EndIf
				Next i
				nTotAno1 += nAno1
				nTotAno2 += nAno2
				nTotHub  += nAno2
				nTotGeral += nTotHub
				@ Li,149 PSay nAno1
				@ Li,160 PSay nAno2
				@ Li,171 PSay nTotHub
				NgSomaLi(58)
			EndIf

			DbSelectArea(cTRB)
			DbSetOrder(1)
			DbSkip()

			If Eof()
				MNT525TOTR()
				NgSomaLi(58)
				@ Li,000 PSay STR0040 //"Total Geral....:"
				For i:=1 To Len(aTot2)
					ny := 6 + (aTot2[i][1] * 11)
					@ Li,ny PSay aTot2[i][2]
				Next i
				@ Li,149 PSay nTotAno1
				@ Li,160 PSay nTotAno2
				@ Li,171 PSay nTotGeral
			EndIf
		EndIf
	End
	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	//≥ Devolve a condicao original do arquivo principal             ≥
	//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
	RetIndex('TQS')
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil
/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    |MNT525RES | Autor ≥ Evaldo Cevinscki Jr.  ≥ Data ≥31/07/2007≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÑo ≥Monta matriz com os dados a serem impressos                 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥MNTR525                                                     ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
/*/
Function MNT525RES(cHub)
	Local cMes := " ", cAno := " "
	Local nx := 0
	Local aArea := GetArea()

	aDados := {}
	DbSelectArea(cTRB)
	DbSetOrder(2)
	DbSeek(cHub)
	While !EoF() .And. (cTRB)->CODHUB == cHub
		nMes := Month((cTRB)->DTTROCA)

		nAnoAtu := Val(Substr(DtoS(dDataBase),1,4))
		nAno1 := nAnoAtu + 1
		nAno2 := nAnoAtu + 2

		If (cTRB)->ANO <= AllTrim(Str(nAno2))
			If cMes <> (cTRB)->MES .Or. cAno <> (cTRB)->ANO
				cMes := (cTRB)->MES
				cAno := (cTRB)->ANO
				++nx
				Aadd(aDados,{(cTRB)->MES,(cTRB)->ANO,0,0,0,0,0})
			EndIf
			If cMes == (cTRB)->MES .And. cAno == (cTRB)->ANO
				++aDados[nx][3]
				aDados[nx][4] += (cTRB)->CUSTOTR
			EndIf
		EndIf

		If (cTRB)->ANO == AllTrim(Str(nAno2))
			If     nMes = 1 ; aDados[nx][5] := 14
			ElseIf nMes = 2 ; aDados[nx][5] := 14
			ElseIf nMes = 3 ; aDados[nx][5] := 14
			ElseIf nMes = 4 ; aDados[nx][5] := 14
			ElseIf nMes = 5 ; aDados[nx][5] := 14
			ElseIf nMes = 6 ; aDados[nx][5] := 14
			ElseIf nMes = 7 ; aDados[nx][5] := 14
			ElseIf nMes = 8 ; aDados[nx][5] := 14
			ElseIf nMes = 9 ; aDados[nx][5] := 14
			ElseIf nMes = 10 ; aDados[nx][5] := 14
			ElseIf nMes = 11 ; aDados[nx][5] := 14
			ElseIf nMes = 12 ; aDados[nx][5] := 14
			EndIf

			If MV_PAR09 == 2
				aDados[nx][7] := aDados[nx][3]
			Else
				aDados[nx][7] := aDados[nx][4]
			EndIf
		Else
			If ((cTRB)->ANO == AllTrim(Str(nAno1))) .or. ((cTRB)->ANO = AllTrim(Str(nAnoAtu)))
				If     nMes = 1 ; aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 2 ; aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 3 ; aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 4 ; aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 5 ; aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 6 ; aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 7 ; aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 8 ; aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 9 ; aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 10 ;aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 11 ;aDados[nx][5] := (cTRB)->QTDARR //nPos
				ElseIf nMes = 12 ;aDados[nx][5] := (cTRB)->QTDARR //nPos
				EndIf
				If (cTRB)->ANO == AllTrim(Str(nAno1))
					If MV_PAR09 == 2
						aDados[nx][6] := aDados[nx][3]  //total qtd
					Else
						aDados[nx][6] := aDados[nx][4]  //total custo
					EndIf
				EndIf
			EndIf
		EndIf

		DbSelectArea(cTRB)
		DbSetOrder(2)
		DbSkip()
	End
	RestArea(aArea)
Return .t.
/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    |MNT525CAB | Autor ≥ Evaldo Cevinscki Jr.  ≥ Data ≥31/07/2007≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÑo ≥Monta cabecalho                                             ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥MNTR525                                                     ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
/*/
Function MNT525CAB()
	Local i,cCab := ""
	nMes := Month(dDataBase) + 1
	If nMes > 12
		nMes := nMes - 12
	EndIf
	cAno := Substr(DtoS(dDataBase),1,4)

	If MV_PAR09 == 2
		n := 5
	ElseIf MV_PAR09 == 3
		n := 8
	EndIf

	For i := 1 to 12
		If     nMes = 1 ; cCab += STR0041+Space(n) //"JAN"
		ElseIf nMes = 2 ; cCab += STR0042+Space(n) //"FEV"
		ElseIf nMes = 3 ; cCab += STR0043+Space(n) //"MAR"
		ElseIf nMes = 4 ; cCab += STR0044+Space(n) //"ABR"
		ElseIf nMes = 5 ; cCab += STR0045+Space(n) //"MAI"
		ElseIf nMes = 6 ; cCab += STR0046+Space(n) //"JUN"
		ElseIf nMes = 7 ; cCab += STR0047+Space(n) //"JUL"
		ElseIf nMes = 8 ; cCab += STR0048+Space(n) //"AGO"
		ElseIf nMes = 9 ; cCab += STR0049+Space(n) //"SET"
		ElseIf nMes = 10 ; cCab += STR0050+Space(n)			 //"OUT"
		ElseIf nMes = 11 ; cCab += STR0051+Space(n) //"NOV"
		ElseIf nMes = 12 ; cCab += STR0052+Space(n) //"DEZ"
		EndIf
		++nMes
		If nMes > 12
			nMes := nMes - 12
		EndIf
	Next i
	cCab += AllTrim(Str(Val(cAno) + 1))+Space(n-1)
	cCab += AllTrim(Str(Val(cAno) + 2))+Space(n-1)
	cCab += STR0053 //"Total HUB"
Return cCab
/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    |MNT525TOTR| Autor ≥ Evaldo Cevinscki Jr.  ≥ Data ≥31/07/2007≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÑo ≥Monta matriz com totais                                     ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥MNTR525                                                     ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
/*/
Function MNT525TOTR()
	Local i
	Store 0 To nAno1,nAno2

	If Len(aTot) > 0
		aTot := aSort( aTot,,, { |x,y| x[1] < y[1] } )
		nMes := 0
		nTotal := 0
		For i:= 1 To Len(aTot)
			If aTot[i][1] = nMes
				nTotal += aTot[i][2]
				nPos := aScan(aTot2,{|x| x[1] = nMes } )
				If nPos = 0
					aAdd(aTot2 ,{nMes,nTotal})
				Else
					aTot2[nPos][2] := nTotal
				EndIf
			Else
				nMes   := aTot[i][1]
				nTotal := aTot[i][2]
				aAdd(aTot2 ,{nMes,nTotal})
			EndIf
			nAno1 += aTot[i][3]
		Next
	EndIf

Return .t.
