#Include "MDTC340.ch"
#Include "Protheus.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} MDTC340
Consulta Geral de Questionario E Gráfico

@type    function
@author  Inacio Luiz Kolling
@since   11/04/2000
@sample  MDTC340()

@return  Lógico, Sempre verdadeiro
/*/
//-------------------------------------------------------------------
Function MDTC340()

	// Guarda conteudo e declara variaveis padroes
	Local aNGBEGINPRM := NGBEGINPRM( )

	Private nSizeTMH := (TAMSX3("TMH_PERGUN")[1])

	nSizeTMH := If( nSizeTMH > 0 , nSizeTMH , 60 )

	lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .T. , .F. )

	Private aRotina	:= MenuDef()
	Private cPerg	:= Padr( "MDT340", 10 )

	If FindFunction("MDTRESTRI") .And. !MDTRESTRI(cPrograma)
		//  Devolve variaveis armazenadas (NGRIGHTCLICK)
		NGRETURNPRM(aNGBEGINPRM)
		Return .F.
	EndIf

	If lSigaMdtps
		Private cCadastro := OemtoAnsi(STR0027) //"Clientes"

		DbSelectArea("SA1")
		DbSetOrder(1)

		mBrowse( 6, 1,22,75,"SA1")
	Else

		cCadastro := OemToAnsi(STR0004) //"Questionario"
		//  Endereca a funcao de BROWSE
		mBrowse( 6, 1,22,75,"TMG")

		//  Recupera a Ordem Original do arquivo principal
		dbSelectArea("TMG")
		dbSetOrder(1)

	EndIf

	// Retorna conteudo de variaveis padroes
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MDC340QUE
Monta um browse com as perguntas do questionário

@type    function
@author  Inacio Luiz Kolling
@since   11/04/2000
@sample  MDC340QUE()
@return  Nil, Sempre nulo
/*/
//-------------------------------------------------------------------
Function MDC340QUE()

	Local OldRot
	Local oTempTRBH
	Local nIndTMH
	Local cSeekTMH
	Local cCondTMH
	Local aRespostas := {}

	Private aVetinr := {}

	MV_PAR01 := ctod(' /  /  ')
	MV_PAR02 := ctod(' /  /  ')

	OldRot := aCLONE(aROTINA)

	aRotina := {  { STR0002  ,"NgC340VIS"   , 0 , 2}, ; // "Visualizar"
				  { STR0035  ,"MDT340RES"   , 0 , 3}  }  // "Resultado"

	lInverte := .F.
	cMARCA   := GetMark()
	lQuery   := .T.

	dbSelectArea("TMH")

	aDBF := {}
	AADD(aDBF,{ "TMH_OK"    , "C", 02      , 0 })
	AADD(aDBF,{ "TMH_QUESTI", "C", 06      , 0 })
	AADD(aDBF,{ "TMH_NOMQUE", "C", 40      , 0 })
	AADD(aDBF,{ "TMH_QUESTA", "C", 03      , 0 })
	AADD(aDBF,{ "TMH_PERGUN", "C", nSizeTMH, 0 })

	aTRBH := {}

	AADD(aTRBH,{ "TMH_OK"    , Nil, " "      , })
	AADD(aTRBH,{ "TMH_QUESTA", Nil, STR0005  , }) // "Pergunta"
	AADD(aTRBH,{ "TMH_PERGUN", Nil, STR0006  , }) // "Descricao Pergunta"

	//Cria TRB
	oTempTRBH := FWTemporaryTable():New( "TRBH", aDBF )
	oTempTRBH:AddIndex( "1", {"TMH_QUESTI","TMH_QUESTA"} )
	oTempTRBH:Create()

	If lSigaMdtps
		nIndTMH := 2
		cSeekTMH := xFilial("TMH")+cCliMdtps+TMG->TMG_QUESTI
		cCondTMH := "TMH->TMH_FILIAL+TMH->TMH_CLIENT+TMH->TMH_LOJA+TMH->TMH_QUESTI"
	Else
		nIndTMH := 1
		cSeekTMH := xFilial("TMH")+TMG->TMG_QUESTI
		cCondTMH := "TMH->TMH_FILIAL+TMH->TMH_QUESTI"
	EndIf

	dbSelectArea("TMH")
	dbsetorder(nIndTMH)
	dbseek(cSeekTMH)
	While !EoF() .And. cSeekTMH == &(cCondTMH)

		aRespostas := StrTokArr( FwNoAccent(TMH->TMH_RESPOS), ';')
		If Len(aRespostas) == 2 .And. Upper(aRespostas[1]) == '1=SIM' .And. Upper(aRespostas[2]) == '2=NAO'

			TRBH->(DbAppend())
			TRBH->TMH_OK      := If(Empty(TRBH->TMH_OK), "  ", cMARCA)
			TRBH->TMH_QUESTI  := TMH->TMH_QUESTI
			TRBH->TMH_NOMQUE  := TMG->TMG_NOMQUE
			TRBH->TMH_QUESTA  := TMH->TMH_QUESTA
			TRBH->TMH_PERGUN  := TMH->TMH_PERGUN

		EndIf

		dbSelectArea("TMH")
		dbSkip()
	End

	dbSelectArea("TRBH")
	dbGoTop()
	MARKBROW("TRBH","TMH_OK",,aTRBH,lInverte, cMarca, "C340Invert()")

	oTempTRBH:Delete()
	aROTINA := aCLONE(OldRot)

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} MDC340GRA
Monta o Gráfico das perguntas

@type    function
@author  Inacio Luiz Kolling
@since   11/04/2000
@sample  MDC340GRA()
@return  Lógico, Sempre verdadeiro
/*/
//-------------------------------------------------------------------
Function MDC340GRA()

	Local oTempTRBV

	DbselectArea('TRB')
	Dbgotop()

	If reccount() == 0
		NDEL := Help(" ",1,"NGTDADGRAF") // "Nao existem dados para montar o grafico..."###"Atencao"
		return .T.
	EndIf

	aDBF3 := { {"CODIGO" , "C", 03,0} ,;
			   {"NOMECOD", "C", If(nSizeTMH>110,110,nSizeTMH) ,0} ,; // O Grafico nao suporta mais do que 110 caracteres
			   {"VALOR1" , "N", 12,2} ,;
			   {"VALOR2" , "N", 12,2}}

	oTempTRBV := FWTemporaryTable():New( "TRBV", aDBF3 )
	oTempTRBV:AddIndex( "1", {"CODIGO"} )
	oTempTRBV:Create()

	DbselectArea('TRB')
	Dbgotop()

	Do While !EoF()

		TRBV->(DbAppend())
		TRBV->CODIGO  := TRB->QUESTA
		TRBV->NOMECOD := TRB->PERGUN
		TRBV->VALOR1  := TRB->SIM
		TRBV->VALOR2  := TRB->NAO

		DbselectArea('TRB')
		dbSkip()

	EndDo

	// 1 linha titulo do grafico (janela)
	// 2 linha titulo da direita do grafico
	// 3 linha titulo superior do grafico
	// 4 linha titulo da direita do grafico
	// 5 linha titulo da inferior do grafico
	// 6 linha series do grafico
	// 7 leitura ("A" - Arquivo temporario,"M" - Matriz)
	// 8 alias doa arquivo temporario com os dados /ou
	// 9 matriz com os dados

	vCRIGTXT := NGGRAFICO(" "+STR0004," ",STR0004," "," ",;
						{STR0029,STR0030},"A","TRBV")  //"Sim"  //"Não"

	oTempTRBV:Delete()

	DbselectArea('TRB')
	Dbgotop()

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} NGC340VIS
Posiciona no arquivo correspondente

@type    function
@author  Inacio Luiz Kolling
@since   11/04/2000
@sample  NGC340VIS()
@return  Lógico, Sempre verdadeiro
/*/
//-------------------------------------------------------------------
Function NgC340VIS()

	dbSelectArea("TMH")
	If lSigaMdtps
		dbsetorder(2)
		dbseek(xFilial("TMH")+cCliMdtps+TRBH->TMH_QUESTI+TRBH->TMH_QUESTA)
	Else
		dbsetorder(1)
		dbseek(xFilial("TMH")+TRBH->TMH_QUESTI+TRBH->TMH_QUESTA)
	EndIf

	NGCAD01('TMH', RECNO(),1)

	dbSelectArea("TRBH")

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MDC340IMP
Impressao do Questionário médico do período

@type    function
@author  Inacio Luiz Kolling
@since   11/04/2000
@sample  MDC340IMP()
@return  Nil, Sempre nulo
/*/
//-------------------------------------------------------------------
Function MDC340IMP()

	Local cString    := "TRB"
	Local cDesc2     := ""
	Local cDesc3     := ""
	Local wnrel      := "MDTC340"

	Private cDesc1   := cTitulo
	Private aReturn  := { STR0019, 1,STR0020, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private Titulo   := cDesc1
	Private Tamanho  := "M"

	If nSizeTMH > 100
		Tamanho  := "G"
	EndIf

	If reccount() = 0
		NDEL := Help(" ",1,"NGTDADIMP") //"Nao existem dados para imprimir..."###"Atencao"
		Return .T.
	EndIf

	//  Envia controle para a funcao SETPRINT
	wnrel:=SetPrint(cString,wnrel,,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		Return
	EndIf

	SetDefault(aReturn,cString)

	If nLastKey = 27
	Set Filter To
	Return
	EndIf

	RptStatus({|lEnd| C340Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} C340Imp
Chamada do Relatório

@type    function
@author  Inacio Luiz Kolling
@since   11/04/2000
@sample  C340Imp( @lEnd, "MDTR435", "Título", "M" )

@param   lEnd, Lógico, Indica o fim da impressão
@param   wnRel, Caractere, Programa utilizado
@param   titulo, Caractere, Título do relatório
@param   tamanho, Caractere, Tamanho do relatório

@return  Nil, Sempre nulo
/*/
//-------------------------------------------------------------------
Static Function C340Imp( lEnd, wnRel, titulo, tamanho )

	//  Define Variaveis
	Local cRodaTxt := ""
	Local nCntImpr := 0

	//  Variaveis para controle do cursor de progressao do relatorio
	//  Variaveis locais exclusivas deste programa
	//  Contadores de linha e pagina
	Private li := 80

	m_pag := 1

	//  Verifica se deve comprimir ou nao
	nTipo  := IIF(aReturn[4]==1,15,18)

	Private Cabec1       := STR0031 //"Perg. Descrição da Pergunta                                                                                       Sim       Não"
	Private Cabec2       := ''
	Private nomeprog     := "MDTC340"

	If nSizeTMH > 100
		Cabec1   := STR0032 //"Pergunta   Descrição da Pergunta                                                                                                                                                                       Sim       Não"
	EndIf

	//  Define o indice de leitura do arquivo de Bens
	DbSelectArea("TRB")
	DbGoTop()
	SetRegua(LastRec())

	nSIM := 0
	nNAO := 0

	Do While !TRB->(EoF())
		IncRegua()

	If lEnd
		@ PROW()+1,001 PSay STR0022 //"CANCELADO PELO OPERADOR"
		Exit
	EndIf

	/*
	TAMANHO M
	_____________________________________________________________________________________________________________________________________
			1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	_____________________________________________________________________________________________________________________________________
	Perg. Descrição da Pergunta                                                                                       Sim       Não
	_____________________________________________________________________________________________________________________________________
	Questionário:  XXXXXX - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXX   1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890  9.999.999 9.999.999
																									TOTAL    9.999.999 9.999.999

	TAMANHO G
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________
			1         2         3         4         5         6         7         8         9       100       110       120       130       140       150       160       170       180       190       200       210       220
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________
	Pergunta   Descrição da Pergunta                                                                                                                                                                       Sim       Não
	_____________________________________________________________________________________________________________________________________________________________________________________________________________________________
	Questionário:  XXXXXX - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXX        123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890  9.999.999 9.999.999
																																															TOTAL    9.999.999 9.999.999
	*/

		SomaLi340()
		@li,000 Psay TRB->QUESTA PICTURE '@!'
		If nSizeTMH <= 100
		@li,006 Psay SubStr(TRB->PERGUN,1,100) PICTURE '@!'
		@li,108 Psay TRB->SIM    PICTURE '@E 9,999,999'
		@li,118 Psay TRB->NAO    PICTURE '@E 9,999,999'
		Else
		@li,011 Psay SubStr(TRB->PERGUN,1,180) PICTURE '@!'
		@li,193 Psay TRB->SIM    PICTURE '@E 9,999,999'
		@li,203 Psay TRB->NAO    PICTURE '@E 9,999,999'
		EndIf

		nSIM := nSIM + TRB->SIM
		nNAO := nNAO + TRB->NAO

		TRB->(dbSkip())
	EndDo

	If nSIM > 0 .Or. nNAO > 0

		SomaLi340()
		SomaLi340()

		If nSizeTMH <= 100
			@li,099 Psay STR0023 //'TOTAL'
			@li,108 Psay nSIM    PICTURE '@E 9,999,999'
			@li,118 Psay nNAO    PICTURE '@E 9,999,999'
		Else
			@li,184 Psay STR0023 //'TOTAL'
			@li,193 Psay nSIM    PICTURE '@E 9,999,999'
			@li,203 Psay nNAO    PICTURE '@E 9,999,999'
		EndIf

	EndIf

	Roda(nCntImpr,cRodaTxt,Tamanho)
	Set Filter To
	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf
	MS_FLUSH()
	DBgotop()

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} Somali340
Incrementa Linha e Controla Salto de Pagina

@type    function
@author  Inacio Luiz Kolling
@since   01/06/19997
@sample  Somali340()
@return  Nil, Sempre nulo
/*/
//-------------------------------------------------------------------
Static Function Somali340()

	Li++
	If Li > 58
		Cabec(cTitulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	EndIf

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} c340INVERT
Inverte marcações

@type    function
@author  Paulo Pego
@since   09/11/1999
@sample  c340INVERT()
@return  Nil, Sempre nulo
/*/
//-------------------------------------------------------------------
Function c340INVERT()

	Local nREG := TRBH->(RECNO())

	DBSELECTAREA("TRBH")
	DBGOTOP()
	DO WHILE !EoF()
		TRBH->TMH_OK := IIF(TMH_OK == "  ",cMARCA,"  ")
		dbSkip()
	EndDo

	TRBH->(DBGOTO(nREG))
	lREFRESH := .T.

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de Menu Funcional

@type    function
@author  Rafael Diogo Richter
@since   29/11/2006
@sample  MenuDef()
@return  aRotina, Array, Opções de menu
@obs     Parametros do array a Rotina:
			1. Nome a aparecer no cabecalho
			2. Nome da Rotina associada
			3. Reservado
			4. Tipo de Transação a ser efetuada:
				1 - Pesquisa e Posiciona em um Banco de Dados
				2 - Simplesmente Mostra os Campos
				3 - Inclui registros no Bancos de Dados
				4 - Altera o registro corrente
				5 - Remove o registro corrente do Banco de Dados
			5. Nivel de acesso
			6. Habilita Menu Funcional
/*/
//-------------------------------------------------------------------
Static Function MenuDef()

	Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .T. , .F. )
	Local aRotina

	If lSigaMdtps
		aRotina := { { STR0001, "AxPesqui" , 0, 1},; //"Pesquisar"
					 { STR0002, "NGCAD01"  , 0, 2},; //"Visualizar"
					 { STR0028, "MDTC340QU", 0, 4} } //"Questionários"
	Else
		aRotina := { { STR0001, "AxPesqui" , 0, 1 },; //"Pesquisar"
					 { STR0002, "NGCAD01"  , 0, 2 },; //"Visualizar"
					 { STR0003, "MDC340QUE", 0, 3 } } //"Questoes"
	EndIf

Return aRotina

//-------------------------------------------------------------------
/*/{Protheus.doc} MDTC340QU
Monta um browse com os questionarios do cliente

@type    function
@author  Andre Perez Alvarez
@since   20/11/2007
@sample  MDTC340QU()
@return  Nil, Sempre nulo
/*/
//-------------------------------------------------------------------
Function MDTC340QU()

	Local aArea	:= GetArea()
	Local oldROTINA := aCLONE(aROTINA)
	Local oldCad := cCadastro
	cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

	aRotina := { { STR0001, "AxPesqui"  , 0 , 1 }, ; //"Pesquisar"
			 	 { STR0002, "NGCAD01"   , 0 , 2 }, ; //"Visualizar"
				 { STR0003, "MDC340QUE" , 0 , 3 }  } //"Questoes"

	cCadastro := OemToAnsi(STR0004) //"Questionario"

	//  Endereca a funcao de BROWSE
	dbSelectArea("TMG")
	Set Filter To TMG->(TMG_CLIENT+TMG_LOJA) == cCliMdtps
	dbSetOrder(2)

	mBrowse( 6, 1,22,75,"TMG")

	//  Recupera a Ordem Original do arquivo principal
	dbSelectArea("TMG")
	Set Filter To

	aROTINA := aCLONE(oldROTINA)
	RestArea(aArea)
	cCadastro := oldCad

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} MDT340RES
Função para mostrar o resultado.

@type    function
@author  Jean Pytter da Costa
@since   04/04/2014
@sample  MDT340RES()
@return  lRet, Lógico, Verdadeiro se possuir alguma pergunta marcada
/*/
//---------------------------------------------------------------------
Function MDT340RES()

	Local lRet := .T. // Variável para mostrar o resultado, caso possua alguma pergunta marcada.
	Local nPer := 0   // Variável para verificar se possui pergunta marcada no MarkBrowse.
	Local nCan := 0   // Variável para verificar se pergunta foi cancelada.
	Local oTempTRB

	lTEMMARK := .F.
	dbSelectArea("TRBH")
	DbGoTop()

	Do While TRBH->(!EoF()) //Verifica se algum item foi marcado no MarkBrwose.
		If !Empty(TRBH->TMH_OK)
			lTEMMARK := .T.
			nPer := 1 //Caso alguma pergunta estiver marcada.
			Exit
		EndIf
		DbSelectArea("TRBH")
		dbSkip(1)
	End

	If !lTEMMARK //Caso o MarkBrowse não possuir marcações.
		dbSelectArea("TRBH")
		dbGoTop()
		lRet := .F.
	EndIf

	//  Variaveis utilizadas para parametros
	//  mv_ch1     // De  Data
	//  mv_ch2     // Ate Data
	If !Pergunte("MDT340", lRet)//Se possuir algum item marcado no MarkBrowse Pergunta.
		dbSelectArea("TRBH")
		lRet := .F.
		nCan := 1 //Se pergunta foi cancelada.
	EndIf

	If lRet
		aDBF := { { "QUESTA"  , "C", 03      , 0 } ,;
			      { "PERGUN"  , "C", nSizeTMH, 0 } ,;
				  { "SIM"     , "N", 10      , 0 } ,;
				  { "NAO"     , "N", 10      , 0 } }

		oTempTRB := FWTemporaryTable():New( "TRB", aDBF )
		oTempTRB:AddIndex( "1", {"QUESTA"} )
		oTempTRB:Create()

		DbSelectArea("TRBH")
		DbGoTop()

		Do While !EoF()

			If !Empty(TRBH->TMH_OK)

				If lSigaMdtps   // Prestador

					DbSelectArea("TMI")
					Dbsetorder(2)
					If Dbseek(xFilial('TMI')+TRBH->TMH_QUESTI)

						Do While !EoF() .And. TMI->TMI_FILIAL == xFilial("TMI")  .And.;
							TMI->TMI_QUESTI == TRBH->TMH_QUESTI

							dbSelectArea("TM0")
							dbSetOrder(1)
							dbSeek(xFilial("TM0")+TMI->TMI_NUMFIC)
							If TM0->(TM0_CLIENT+TM0_LOJA) <> cCliMdtps
								DbSelectArea("TMI")
								dbSkip(1)
								Loop
							EndIf

							If TMI->TMI_DTREAL >= MV_PAR01 .And. TMI->TMI_DTREAL <= MV_PAR02;
									.And.TMI->TMI_QUESTA = TRBH->TMH_QUESTA

								dbSelectArea("TRB")
								if !dbSeek(TMI->TMI_QUESTA)
									TRB->(DbAppend())
									Replace TRB->QUESTA with TMI->TMI_QUESTA
									Replace TRB->PERGUN with TRBH->TMH_PERGUN
								EndIf

								If TMI->TMI_RESPOS = '1'
									Replace TRB->SIM with TRB->SIM + 1
								else
									Replace TRB->NAO with TRB->NAO + 1
								EndIf

							EndIf

							DbSelectArea("TMI")
							dbSkip(1)
						End

					EndIf

				Else // Não-prestador

					DbSelectArea("TMI")
					Dbsetorder(2)
					If Dbseek(xFilial('TMI')+TRBH->TMH_QUESTI)

						Do While !EoF() .And. TMI->TMI_FILIAL == xFilial("TMI")  .And.;
							TMI->TMI_QUESTI == TRBH->TMH_QUESTI

							If TMI->TMI_DTREAL >= MV_PAR01 .And. TMI->TMI_DTREAL <= MV_PAR02;
									.And.TMI->TMI_QUESTA = TRBH->TMH_QUESTA

								dbSelectArea("TRB")
								If !dbSeek(TMI->TMI_QUESTA)
									TRB->(DbAppend())
									Replace TRB->QUESTA with TMI->TMI_QUESTA
									Replace TRB->PERGUN with TRBH->TMH_PERGUN
								EndIf

								If TMI->TMI_RESPOS = '1'
									Replace TRB->SIM with TRB->SIM + 1
								ElseIf TMI->TMI_RESPOS = '2'
									Replace TRB->NAO with TRB->NAO + 1
								EndIf

							EndIf
							DbSelectArea("TMI")
							dbSkip(1)

						End

					EndIf

				EndIf

			EndIf

			DbSelectArea("TRBH")
			dbSkip(1)

		End

		DbselectArea('TRB')
		Dbgotop()
		cTitulo := STR0007+Alltrim(TMG->TMG_NOMQUE)+' ( '+dtoc(mv_par01)+' a '+dtoc(mv_par02)+' )' //'Questionario - '

		DEFINE DIALOG oDlg1 FROM 6,0 TO 34,100 TITLE cTitulo

		@ 04,020 BUTTON STR0008 OF oDlg1 SIZE 40,09 PIXEL ACTION (MDC340GRA()) //"&Grafico"
		@ 04,060 BUTTON STR0009 OF oDlg1 SIZE 40,09 PIXEL ACTION (MDC340IMP()) //"&Imprimir"

		@ 01,0.3 LISTBOX FIELDS QUESTA                                ,;
								PERGUN                                ,;
								PADL(Transform(SIM,"@E 999,999"),12)  ,;
								PADL(Transform(NAO,"@E 999,999"),12)   ;
							FieldSizes 40,200,70,70                    ;
							Size 388,195                               ;
							HEADERS STR0005                           ,; //"Pergunta"
									STR0010                           ,; //"Nome Pergunta"
									STR0011                           ,; //"Sim"
									STR0012                              //"Nao"
		ACTIVATE DIALOG oDlg1
	Else
		If nPer == 0 //Se não possuir pergunta marcada.
			ShowHelpDlg(STR0036,{STR0037},1,{STR0038},1)//"ATENÇÃO"##"Não possui pergunta marcada."##"Favor selecionar no minímo uma pergunta para verificar o resultado."
		EndIf
	EndIf

	If nCan == 0 .And. nPer == 1 //Se não foi cancelado###Se foi marcado alguma pergunta
		oTempTRB:Delete()
	EndIf
	DbSelectArea("TRBH")
	dbgotop()

Return lRet
