#INCLUDE "MNTR250.CH"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR250  ³ Autor ³ Deivys Joenck         ³ Data ³ 25/09/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mapa de Despesas Anuais                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR250()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK)			  		  	  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := NGBEGINPRM()

	Local WNREL      := "MNTR250"
	Local LIMITE     := 132
	Local cDESC1     := STR0001 //"Relatorio Anual de Despesas de Manutencao."
	Local cDESC2     := STR0002 //"O usuario pode selecionar quais os campos que deverao ser mostrados,"
	Local cDESC3     := STR0003 //"bem como informar parametros de selecao para a impressao."
	Local cSTRING    := "ST9"

	Private NOMEPROG := "MNTR250"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1}   //"Zebrado"###"Administracao"
	Private TITULO   := STR0001 //"Relatorio Anual de Despesas de Manutencao."
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private vVETHORAS := {}
	Private CABEC1,CABEC2
	Private cPERG := "MNT25R"

	//+--------------------------------------------------------------+
	//| Verifica as perguntas selecionadas                           |
	//+--------------------------------------------------------------+
	//+--------------------------------------------------------------+
	//| Variaveis utilizadas para parametros                         |
	//| mv_par01      | Ano                                          |
	//| mv_par02      | De  Centro de Custo                          |
	//| mv_par03      | Ate Centro de Custo                          |
	//| mv_par04      | De  Centro de Trabalho                       |
	//| mv_par05      | Ate Centro de Trabalho                       |
	//| mv_par06      | De  Familia                                  |
	//| mv_par07      | Ate Familia                                  |
	//| mv_par08      | De  Bem                                      |
	//| mv_par09      | Ate Bem                                      |
	//| mv_par10      | Considerar Estrutura S/N                     |
	//| mv_par11      | Tipo de Custo (1-Medio, 2-Standard)          |
	//| mv_par12      | Considerar O.S.(1-Abertas, 2-Finalizadas     |
	//|                 e 3-Ambas)                                   |
	//| mv_par13      | Imprimir Localizacao ?  Sim/Nao              |
	//+--------------------------------------------------------------+
	Pergunte(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("ST9")
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		NGRETURNPRM(aNGBEGINPRM)
		Return
	EndIf
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND| MNTR250IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR250IMP³ Autor ³ Deivys Joenck         ³ Data ³ 25/09/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR250                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR250IMP(lEND,WNREL,TITULO,TAMANHO)
	Local cRODATXT := ""
	Local nCNTIMPR := 0,i,nVALOR
	Local cEstrut  := ""
	Local aEstrut  := {}
	Local nCont    := 0

	Local lMMoeda  := NGCADICBASE("TL_MOEDA","A","STL",.F.) // Multi-Moeda
	Local nCustoAux := 0
	Local nMoedarel:= 1
	Local oTempTable	//Obj. Tab. Temp.
	Local cPictuCus  := X3Picture( 'TL_CUSTO' )

	//³ Contadores de linha e pagina                                 ³
	Private li := 80 ,m_pag := 1

	Private cTRB	 := GetNextAlias() //Alias Tab. Temp.

	// Caso o update de multi-moeda esteja aplicado e haja a funcao de criacao da pergunta
	If lMMoeda .And. FindFunction("MNTR750MD")
		nMoedarel := MNTR750MD()
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se deve comprimir ou nao                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTIPO  := IIF(aRETURN[4]==1,15,18)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta os Cabecalhos                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


	CABEC1 := STR0006 //"   Familia Descricao                          Janeiro  Fevereiro     Marco       Abril       Maio      Junho         Total"
	CABEC2 := STR0007 //"    Bem              Descricao                  Julho     Agosto  Setembro     Outubro   Novembro   Dezembro"

	//          1         2         3         4         5         6         7         8         9        10       110       120
	//0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123
	//xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	//     Bem              Descricao                Janeiro  Fevereiro     Marco       Abril       Maio      Junho         Total
	//                                                 Julho     Agosto  Setembro     Outubro   Novembro   Dezembro
	//xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	//Centro de Custo..: xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	//  Centro de Trabalho..: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	//    Familia..: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	//     xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99 999.999,99 999.999,99 999.999,99 999.999,99 999.999,99
	//                                            999.999,99 999.999,99 999.999,99 999.999,99 999.999,99 999.999,99  9.999.999,99
	//
	//     xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx  999.999,99 999.999,99 999.999,99 999.999,99 999.999,99 999.999,99
	//                                            999.999,99 999.999,99 999.999,99 999.999,99 999.999,99 999.999,99  9.999.999,99

	aDBF := {{"CUSTO" ,"C",Len(ST9->T9_CCUSTO),0},;
	{"CENTRA","C",06,0},;
	{"CODBEM","C",16,0},;
	{"FAMILI","C",06,0},;
	{"NOME"  ,"C",40,0},;
	{"M01"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M02"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M03"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M04"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M05"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M06"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M07"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M08"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M09"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M10"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M11"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]},;
	{"M12"   ,"N",TAMSX3( 'TL_CUSTO' )[1],TAMSX3( 'TL_CUSTO' )[2]}}

	//Instancia classe FWTemporaryTable
	oTempTable := FWTemporaryTable():New( cTRB, aDBF )

	//Cria indices
	oTempTable:AddIndex( "Ind01" , {"CUSTO","CENTRA","FAMILI","CODBEM"} )

	//Cria a tabela temporaria
	oTempTable:Create()

	cANO := If(MV_PAR01 < 100,StrZero(MV_PAR01,02),StrZero(MV_PAR01,04))
	dINI := CTOD("01/01/" + cANO)
	dFIM := CTOD("31/12/" + cANO)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define o indice de leitura do arquivo de Bens            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


	cCodST9 := '(ST9->T9_CCUSTO >= MV_PAR02 .And. ST9->T9_CCUSTO <= MV_PAR03)'
	cCodST9 += ' .And. (ST9->T9_CENTRAB >= MV_PAR04 .And. ST9->T9_CENTRAB <= MV_PAR05)'
	cCodST9 += ' .And. (ST9->T9_CODBEM >= MV_PAR08 .And. ST9->T9_CODBEM <= MV_PAR09)'
	vVetOS  := {}
	lExiste := .f.

	DbSelectArea("ST9")
	DbSetOrder(4)
	DbSeek(xFILIAL("ST9")+MV_PAR06,.T.)
	SetRegua(LastRec())
	While !Eof() .And. ST9->T9_FILIAL == xFILIAL("ST9") .And.;
	ST9->T9_CODFAMI <= MV_PAR07

		If lEND
			@ PROW()+1,001 Psay STR0008 //"CANCELADO PELO OPERADOR"
			Exit
		EndIf

		IncRegua()

		If !&cCodST9
			DbSkip()
			Loop
		Endif

		aRet := {}

		If mv_par10 = 1
			lpai := .t.
			aRet := NGBEM250(ST9->T9_CODBEM,dINI,dFIM)
			If Len(aRet) = 1
				DbSelectArea("STC")
				DbSetOrder(3)
				If DbSeek(xFilial("STC")+aRet[1][1])
					DbSelectArea("ST9")
					DbSkip()
					Loop
				EndIf
			EndIf
		Else
			Aadd(aRet,{ST9->T9_CODBEM," ",dINI,dFIM,0,0})
		EndIf

		For i := 1 to Len(aRet)
			DbSelectArea("STS")
			DbSetOrder(2)
			DbSeek(xFILIAL("STS")+"B"+aRet[i][1])
			While !Eof() .And. STS->TS_FILIAL == xFILIAL("STS") .And.;
			STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM == aRet[i][1]

				If Ascan(vVetOS,STS->TS_ORDEM) = 0
					If STS->TS_SITUACA <> "L"
						DbSkip()
						Loop
					EndIf

					If (Mv_par12 == 1 .and. STS->TS_TERMINO <> 'N') .or.; // Se opção for abertas e OS esta finalizada
					(Mv_par12 == 2 .and. STS->TS_TERMINO <> 'S') .or.; // Se opção for finalizadas e OS esta aberta
					(Mv_par12 == 3 .and. !(STS->TS_TERMINO $ 'S#N') )  // Se opção for ambas e OS nao esta aberta ou finalizada
						DbSkip()
						Loop
					Endif

					If STS->TS_TERMINO == 'N'
						If STS->TS_DTMPFIM < aRet[i][3] .Or. STS->TS_DTMPFIM > aRet[i][4]
							DbSkip()
							Loop
						EndIf
					Else
						If STS->TS_DTMRFIM < aRet[i][3] .Or. STS->TS_DTMRFIM > aRet[i][4]
							DbSkip()
							Loop
						EndIf
					Endif

					nVALOR  := 0.00
					lExiste := .t.

					DbSelectArea("STT")
					DbSetOrder(01)
					If DbSeek(xFilial("STT") + STS->TS_ORDEM + STS->TS_PLANO)
						While !Eof() .And. STT->TT_FILIAL == xFILIAL("STT") .And. ;
						STT->TT_ORDEM == STS->TS_ORDEM .And. STT->TT_PLANO == STS->TS_PLANO

							//Calcula o custo do Insumo
							MNTA901STT()

							If Alltrim(STT->TT_SEQRELA) <> "0"

								vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
								STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
								STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
								nQTDHORAS := vVETHORAS[1]

								If MV_PAR11 = 2 //STANDARD
									aVETCUST := NGCUSTSTAN(STT->TT_CODIGO,STT->TT_TIPOREG,,,cValToChar(nMoedarel))
									nVCUSTO  := aVETCUST[1] //Custo standard
									nVCUSTO  := nVCUSTO * nQTDHORAS
								Else
									nVCUSTO := If(lMMoeda, xMoeda(STT->TT_CUSTO,Val(STT->TT_MOEDA),nMoedarel,STT->TT_DTINICI,2), STT->TT_CUSTO)
								EndIf

								nVALOR += nVCUSTO

							EndIf
							DbSelectArea("STT")
							DbSkip()
						End
					EndIf

					DbSelectArea(cTRB)
					DbSetOrder(1)
					If !DbSeek(ST9->T9_CCUSTO+ST9->T9_CENTRAB+ST9->T9_CODFAMI+ST9->T9_CODBEM)
						(cTRB)->(DbAppend())
						(cTRB)->CUSTO  := ST9->T9_CCUSTO
						(cTRB)->CENTRA := ST9->T9_CENTRAB
						(cTRB)->FAMILI := ST9->T9_CODFAMI
						(cTRB)->CODBEM := ST9->T9_CODBEM
						(cTRB)->NOME   := ST9->T9_NOME
					EndIf
					Aadd(vVetOS,STS->TS_ORDEM)
					xx := Month(If(STS->TS_TERMINO == 'N',STS->TS_DTMPFIM,STS->TS_DTMRFIM))
					If     xx == 1 ; (cTRB)->M01 := (cTRB)->M01 + nVALOR
					ElseIf xx == 2 ; (cTRB)->M02 := (cTRB)->M02 + nVALOR
					ElseIf xx == 3 ; (cTRB)->M03 := (cTRB)->M03 + nVALOR
					ElseIf xx == 4 ; (cTRB)->M04 := (cTRB)->M04 + nVALOR
					ElseIf xx == 5 ; (cTRB)->M05 := (cTRB)->M05 + nVALOR
					ElseIf xx == 6 ; (cTRB)->M06 := (cTRB)->M06 + nVALOR
					ElseIf xx == 7 ; (cTRB)->M07 := (cTRB)->M07 + nVALOR
					ElseIf xx == 8 ; (cTRB)->M08 := (cTRB)->M08 + nVALOR
					ElseIf xx == 9 ; (cTRB)->M09 := (cTRB)->M09 + nVALOR
					ElseIf xx == 10; (cTRB)->M10 := (cTRB)->M10 + nVALOR
					ElseIf xx == 11; (cTRB)->M11 := (cTRB)->M11 + nVALOR
					ElseIf xx == 12; (cTRB)->M12 := (cTRB)->M12 + nVALOR
					EndIf
				EndIf
				DbSelectArea("STS")
				DbSkip()
			End

			DbSelectArea("STJ")
			DbSetOrder(2)
			DbSeek(xFILIAL("STJ")+"B"+aRet[i][1])
			While !Eof() .And. STJ->TJ_FILIAL == xFILIAL("STJ") .And.;
			STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM == aRet[i][1]
				If Ascan(vVetOS,STJ->TJ_ORDEM) = 0
					If STJ->TJ_SITUACA <> "L"
						DbSkip()
						Loop
					EndIf

					If (Mv_par12 == 1 .and. STJ->TJ_TERMINO <> 'N') .or.; // Se opção for abertas e OS esta finalizada
					(Mv_par12 == 2 .and. STJ->TJ_TERMINO <> 'S') .or.; // Se opção for finalizadas e OS esta aberta
					(Mv_par12 == 3 .and. !(STJ->TJ_TERMINO $ 'S#N') )  // Se opção for ambas e OS nao esta aberta ou finalizada
						DbSkip()
						Loop
					Endif

					If STJ->TJ_TERMINO == 'N'
						If STJ->TJ_DTMPFIM < aRet[i][3] .Or. STJ->TJ_DTMPFIM > aRet[i][4]
							DbSkip()
							Loop
						EndIf
					Else
						If STJ->TJ_DTMRFIM < aRet[i][3] .Or. STJ->TJ_DTMRFIM > aRet[i][4]
							DbSkip()
							Loop
						EndIf
					Endif

					lExiste := .T.
					nVALOR  := 0.00
					DbSelectArea("STL")
					DbSetOrder(01)
					If DbSeek(xFilial("STL") + STJ->TJ_ORDEM + STJ->TJ_PLANO)
						While !Eof() .And. STL->TL_FILIAL == xFILIAL("STL") .And. ;
						STL->TL_ORDEM == STJ->TJ_ORDEM .And. STL->TL_PLANO == STJ->TJ_PLANO

							//Calcula o custo do Insumo
							MNTA901STL()

							If Alltrim(STL->TL_SEQRELA) <> "0"

								vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
								STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
								STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
								nQTDHORAS := vVETHORAS[1]

								If MV_PAR11 = 2 //STANDARD
									aVETCUST := NGCUSTSTAN(STL->TL_CODIGO,STL->TL_TIPOREG,,,cValToChar(nMoedarel))
									nVCUSTO  := aVETCUST[1] //Custo standard
									nVCUSTO  := nVCUSTO * nQTDHORAS
								Else
									nVCUSTO := If(lMMoeda, xMoeda(STL->TL_CUSTO,Val(STL->TL_MOEDA),nMoedarel,STL->TL_DTINICI,2), STL->TL_CUSTO)
								EndIf

								nVALOR += nVCUSTO

							EndIf
							DbSelectArea("STL")
							DbSkip()
						End
					EndIf

					DbSelectArea(cTRB)
					If !DbSeek(ST9->T9_CCUSTO+ST9->T9_CENTRAB+ST9->T9_CODFAMI+ST9->T9_CODBEM)
						(cTRB)->(DbAppend())
						(cTRB)->CUSTO  := ST9->T9_CCUSTO
						(cTRB)->CENTRA := ST9->T9_CENTRAB
						(cTRB)->FAMILI := ST9->T9_CODFAMI
						(cTRB)->CODBEM := ST9->T9_CODBEM
						(cTRB)->NOME   := ST9->T9_NOME
					EndIf
					Aadd(vVetOS,STJ->TJ_ORDEM)
					xx := Month(If(STJ->TJ_TERMINO == 'N',STJ->TJ_DTMPFIM,STJ->TJ_DTMRFIM))
					If     xx == 1 ; (cTRB)->M01 := (cTRB)->M01 + nVALOR
					ElseIf xx == 2 ; (cTRB)->M02 := (cTRB)->M02 + nVALOR
					ElseIf xx == 3 ; (cTRB)->M03 := (cTRB)->M03 + nVALOR
					ElseIf xx == 4 ; (cTRB)->M04 := (cTRB)->M04 + nVALOR
					ElseIf xx == 5 ; (cTRB)->M05 := (cTRB)->M05 + nVALOR
					ElseIf xx == 6 ; (cTRB)->M06 := (cTRB)->M06 + nVALOR
					ElseIf xx == 7 ; (cTRB)->M07 := (cTRB)->M07 + nVALOR
					ElseIf xx == 8 ; (cTRB)->M08 := (cTRB)->M08 + nVALOR
					ElseIf xx == 9 ; (cTRB)->M09 := (cTRB)->M09 + nVALOR
					ElseIf xx == 10; (cTRB)->M10 := (cTRB)->M10 + nVALOR
					ElseIf xx == 11; (cTRB)->M11 := (cTRB)->M11 + nVALOR
					ElseIf xx == 12; (cTRB)->M12 := (cTRB)->M12 + nVALOR
					EndIf
				EndIf
				DbSelectArea("STJ")
				DbSkip()
			End
		Next i
		DbSelectArea("ST9")
		DbSkip()
	End

	//IMPRESSAO DO RELATORIO

	DbSelectArea(cTRB)
	DbGotop()
	If Reccount() > 0
		nM01 := 0.00; nM02 := 0.00; nM03 := 0.00; nM04 := 0.00
		nM05 := 0.00; nM06 := 0.00; nM07 := 0.00; nM08 := 0.00
		nM09 := 0.00; nM10 := 0.00; nM11 := 0.00; nM12 := 0.00
		nTOTGER := 0.00
		NGSOMALI(58)
		While !Eof()
			cCUSTO  := (cTRB)->CUSTO
			nTOTCUS := 0.00
			NGSOMALI(58)
			@LI,000 Psay STR0009 //"Centro de Custo..: "
			@LI,019 Psay Alltrim((cTRB)->CUSTO)+"  "+NGSEEK("SI3",(cTRB)->CUSTO,1,"I3_DESC")

			DbSelectArea(cTRB)
			While !Eof() .And. (cTRB)->CUSTO == cCUSTO
				cCENTRA := (cTRB)->CENTRA
				nTOTCEN := 0.00
				NGSOMALI(58)
				@LI,002 Psay STR0010 //"Centro de Trabalho..: "
				@LI,024 Psay Alltrim((cTRB)->CENTRA)+"   "+NGSEEK("SHB",(cTRB)->CENTRA,1,"Substr(SHB->HB_NOME,1,30)")
				NGSOMALI(58)

				DbSelectArea(cTRB)
				While !Eof() .And. (cTRB)->CUSTO == cCUSTO .And. (cTRB)->CENTRA == cCENTRA

					cCODFAMI := (cTRB)->FAMILI
					nTOTFAM  := 0.00

					@ Li,004 Psay STR0011
					@ Li,015 Psay Alltrim((cTRB)->FAMILI)+"   "+NGSEEK("ST6",(cTRB)->FAMILI,1,"Substr(ST6->T6_NOME,1,25)")
					NGSOMALI(58)

					DbSelectArea(cTRB)
					While !Eof() .And. (cTRB)->CUSTO == cCUSTO .And.;
					(cTRB)->CENTRA == cCENTRA .And. (cTRB)->FAMILI == cCODFAMI

						cCODBEM := (cTRB)->CODBEM
						While !Eof() .And. (cTRB)->CUSTO == cCUSTO .And.;
						(cTRB)->CENTRA == cCENTRA .And. (cTRB)->FAMILI == cCODFAMI .And.;
						(cTRB)->CODBEM == cCODBEM

							nLIN := (cTRB)->M01+(cTRB)->M02+(cTRB)->M03+(cTRB)->M04+(cTRB)->M05+(cTRB)->M06+;
							(cTRB)->M07+(cTRB)->M08+(cTRB)->M09+(cTRB)->M10+(cTRB)->M11+(cTRB)->M12

							@ LI,005 Psay (cTRB)->CODBEM
							@ LI,022 Psay Substr((cTRB)->NOME,1,40)
							NGSOMALI(58)

							If MV_PAR13 == 1
								cEstrut := NGLocComp((cTRB)->CODBEM,"1")
								aEstrut := {}
								If Len(cEstrut) > 100
									aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
									cEstrut := AllTrim(SubStr(cEstrut,101))
									While Len(cEstrut) > 100
										aAdd(aEstrut,AllTrim(SubStr(cEstrut,1,100)))
										cEstrut := AllTrim(SubStr(cEstrut,101))
									End
									aAdd(aEstrut,AllTrim(cEstrut))
								EndIf

								If Len(aEstrut) > 0
									For nCont := 1 To Len(aEstrut)
										If nCont == 1
											@ LI,005 Psay STR0017 + aEstrut[nCont] //"Localização.: "
										Else
											@ LI,005 Psay Space(Len(STR0017)) + aEstrut[nCont] //"Localização.: "
										EndIf
										NGSOMALI(58)
									Next nCont
								Else
									If AllTrim(cEstrut) <> AllTrim((cTRB)->CODBEM)
										@ LI,005 Psay STR0017 + cEstrut //"Localização.: "
									Else
										@ LI,005 Psay STR0017 + STR0018 //"Localização.: "###"Não está relacionado a uma Estrutura."
									EndIf
									NGSOMALI(58)
								EndIf
							Endif

							NGSOMALI(58)
							@ LI,034 Psay (cTRB)->M01 Picture cPictuCus //"@E 999,999.99"
							@ LI,054 Psay (cTRB)->M02 Picture cPictuCus //"@E 999,999.99"
							@ LI,074 Psay (cTRB)->M03 Picture cPictuCus //"@E 999,999.99"
							@ LI,094 Psay (cTRB)->M04 Picture cPictuCus //"@E 999,999.99"
							@ LI,114 Psay (cTRB)->M05 Picture cPictuCus //"@E 999,999.99"
							@ LI,134 Psay (cTRB)->M06 Picture cPictuCus //"@E 999,999.99"
							NGSOMALI(58)
							@ LI,034 Psay (cTRB)->M07 Picture cPictuCus //"@E 999,999.99"
							@ LI,054 Psay (cTRB)->M08 Picture cPictuCus //"@E 999,999.99"
							@ LI,074 Psay (cTRB)->M09 Picture cPictuCus //"@E 999,999.99"
							@ LI,094 Psay (cTRB)->M10 Picture cPictuCus //"@E 999,999.99"
							@ LI,114 Psay (cTRB)->M11 Picture cPictuCus //"@E 999,999.99"
							@ LI,134 Psay (cTRB)->M12 Picture cPictuCus //"@E 999,999.99"
							@ LI,154 Psay nLIN     Picture cPictuCus

							nTOTFAM += nLIN
							NGSOMALI(58)
							NGSOMALI(58)
							DbSelectArea(cTRB)
							DbSkip()
						End
					End
					@ LI,112 Psay STR0012
					@ LI,154 Psay nTOTFAM Picture cPictuCus
					NGSOMALI(58)
					nTOTCEN += nTOTFAM
				End
				@ LI,112 Psay STR0014
				@ LI,154 Psay nTOTCEN Picture cPictuCus
				NGSOMALI(58)
				nTOTCUS += nTOTCEN
			End
			@ LI,112 Psay STR0013
			@ LI,154 Psay nTOTCUS Picture cPictuCus
			NGSOMALI(58)
			NGSOMALI(58)
			nTOTGER += nTOTCUS
		End
		@ LI,112 Psay STR0015
		@ LI,154 Psay nTOTGER Picture cPictuCus
	EndIf

	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	RetIndex("ST9")
	Set Filter To
	Set Device To Screen
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

	oTempTable:Delete()//Deleta tabela temporária

Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NGBEM250 ³ Autor ³Denis Hyroshi de Souza ³ Data ³26/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Custo da Manutencao c/ Estrutura               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cCodBem   -> Codigo do Bem Pai                              ³±±
±±³          ³dDataIni  -> Data Inicio    				                  ³±±
±±³	         ³dDataFim  -> Data Final.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Retorna a Estrutura no periodo informado                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR250                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NGBEM250(cCodBem,dDataIni,dDataFim)
	Local afilhos:={},aRet := {},nReg, i

	If lPai
		Aadd(aFilhos,{cCodBem  , ;
		" "   , ;
		dDataIni , ;
		dDataFim , ;
		0 , ;
		0  })
		lPai := .F.
	EndIf

	DbSelectArea("STZ")
	DbSetOrder(3)
	DbSeek(xFilial("STZ")+cCodBem)
	While !Eof() .And. STZ->TZ_FILIAL == xFilial("STZ") .And.;
	STZ->TZ_BEMPAI == cCodBem

		nReg := Recno()
		dDataSai := STZ->TZ_DATASAI
		If Empty(dDataSai)
			dDataSai := dDataFim
		EndIf

		If STZ->TZ_DATAMOV <= dDataFim
			If dDataSai >= dDataIni

				dDataEnt := STZ->TZ_DATAMOV
				If dDataEnt < dDataIni
					dDataEnt := DDataIni
				EndIf

				If dDataSai > dDataFim
					dDataSai := DDataFim
				EndIf

				Aadd(aFilhos,{STZ->TZ_CODBEM ,;
				STZ->TZ_LOCALIZ ,;
				dDataEnt        ,;
				dDataSai        ,;
				STZ->TZ_POSCONT ,;
				STZ->TZ_CONTSAI})

				aRet := NGBEM250(STZ->TZ_CODBEM,dDataIni,dDataFim)

				For i := 1 To Len(aRET)
					Aadd(aFilhos,{aRet[i][1],;
					aRet[i][2],;
					aRet[i][3],;
					aRet[i][4],;
					aRet[i][5],;
					aRet[i][6]})
				Next i
			EndIf
		EndIf
		DbGoto(nReg)
		DbSetOrder(3)
		DbSkip()
	End

Return aFilhos