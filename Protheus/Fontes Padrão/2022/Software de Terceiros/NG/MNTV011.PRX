#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV011    ³ Autor ³ Inacio Luiz Kolling   ³ Data ³20/11/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula a quantidade de horas disponivel                     ³±±
±±³          ³ ( Homem hora disponivel no periodo - HHDI )                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data  - Data inicio                                       ³±±
±±³          ³ Ate_Data - Ate data                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nQtdho    - Quantidade de horas disponivel                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV011(De_Data,Ate_Data,De_Area,Ate_Area,De_Funci,Ate_Funci)

	Local aAreaOLD  := GetArea(),nQtdho := 0, nTab := 0, nX := 0
	Local aCodFunc  := {}//Salva os funcionarios para busca na TTL
	Local De_AreaL  := If(De_Area = Nil,Space(NGSEEKDIC("SX3","TJ_CODAREA",2,"X3_TAMANHO")),De_Area)
	Local De_FuncL  := If(De_Funci = Nil,Space(NGSEEKDIC("SX3","T1_CODFUNC",2,"X3_TAMANHO")),De_Funci)
	Local Ate_FuncL := If(Ate_Funci = Nil,Space(NGSEEKDIC("SX3","T1_CODFUNC",2,"X3_TAMANHO")),Ate_Funci)

	// Variáveis de Histórico de Indicadores
	Local lMV_HIST := NGI6MVHIST()
	Local aParams := {}
	Local cCodIndic := "MNTV011"
	Local nResult := 0
	
	// Armazena os Parâmetros
	If lMV_HIST
		aParams := {}
		aAdd(aParams, {"DE_DATA"   , De_Data})
		aAdd(aParams, {"ATE_DATA"  , Ate_Data})
		aAdd(aParams, {"DE_AREA"   , De_Area})
		aAdd(aParams, {"ATE_AREA"  , Ate_Area})
		aAdd(aParams, {"DE_FUNCI"  , De_Funci})
		aAdd(aParams, {"ATE_FUNCI" , Ate_Funci})
		NGI6PREPPA(aParams, cCodIndic)
	EndIf
    
	If ValType(De_Data) == "D" .And. ValType(Ate_Data) == "D"
		dbSelectArea("ST1")
		dbSetOrder(1)
		dbSeek(xFilial("ST1")+De_FuncL,.T.)
		While !Eof() .AND. ST1->T1_FILIAL == xFilial("ST1") .AND. ST1->T1_CODFUNC <= Ate_FuncL				
			nQtdho += NGCALEHDIS(ST1->T1_TURNO, De_Data, Ate_Data, "N")
			dbSelectArea("ST1")
			dbSkip()
		End
	EndIf
	
	RestArea(aAreaOLD)
	
	// RESULTADO
	nResult := nQtdho
	NGI6PREPVA(cCodIndic, nResult)

Return nResult
