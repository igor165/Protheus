#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV005    ³ Autor ³ Inacio Luiz Kolling   ³ Data ³10/11/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tempo de operacao do(s) bem(s) das O.S. Preventiva TERMINADAS³±±
±±³          ³ em relacao aos parametros                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data    - Data inicio                                     ³±±
±±³          ³ Ate_Data   - Ate data                                        ³±±
±±³          ³ De_Bem     - De bem inicio                                   ³±±
±±³          ³ Ate_Bem    - Ate bem fim                                     ³±±
±±³          ³ De_Ccusto  - De centro de custo                              ³±±
±±³          ³ Ate_Ccusto - Ate centro de custo                             ³±±
±±³          ³ De_CenTra  - De centro de trabalho                           ³±±
±±³          ³ Ate_CenTra - Ate sentro de trabalho                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nQtdho  - Quantidade de horas disponivel                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV005(De_Data,Ate_Data,De_Bem,Ate_Bem,De_Ccusto,Ate_Ccusto,;
                 De_CenTra,Ate_CenTra)
Local aAreaOLD   := GetArea(),nQtdhO := 0,vVetBem := {},nF := 0,nQtdhP := 0
Local De_BemL    := If(De_Bem = Nil,Space(NGSEEKDIC("SX3","TJ_CODBEM",2,"X3_TAMANHO")),De_Bem)
Local De_CcustoL := If(De_Ccusto = Nil,Space(NGSEEKDIC("SX3","TJ_CCUSTO",2,"X3_TAMANHO")),De_Ccusto)
Local De_CenTraL := If(De_CenTra = Nil,Space(NGSEEKDIC("SX3","TJ_CENTRAB",2,"X3_TAMANHO")),De_CenTra)

// Variáveis de Histórico de Indicadores
Local lMV_HIST := NGI6MVHIST()
Local aParams := {}
Local cCodIndic := "MNTV005"
Local nResult := 0

cAlia1 := "        "
cAlia2 := "        "

// Armazena os Parâmetros
If lMV_HIST
	aParams := {}
	aAdd(aParams, {"DE_DATA"   , De_Data})
	aAdd(aParams, {"ATE_DATA"  , Ate_Data})
	aAdd(aParams, {"DE_BEM"    , De_Bem})
	aAdd(aParams, {"ATE_BEM"   , Ate_Bem})
	aAdd(aParams, {"DE_CCUSTO" , De_Ccusto})
	aAdd(aParams, {"ATE_CCUSTO", Ate_Ccusto})
	aAdd(aParams, {"DE_CENTRA" , De_CenTra})
	aAdd(aParams, {"ATE_CENTRA", Ate_CenTra})
	NGI6PREPPA(aParams, cCodIndic)
EndIf

If ValType(De_Data) != "D" .or. ValType(Ate_Data) != "D"
	NGI6PREPVA(cCodIndic, nResult)
	Return nResult
Endif

// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT TJ_CODBEM,TJ_ORDEM,TJ_PLANO,TJ_DTPRINI,TJ_HOPRINI,TJ_DTPRFIM,TJ_HOPRFIM "
EndIf
cQuery += "FROM "+RetSqlName("STJ")+" WHERE "
cQuery += "TJ_FILIAL='"+xFilial("STJ")+"' AND "
If ValType(De_BemL) == "C"
	cQuery += "TJ_CODBEM >= '"+De_BemL+"' AND "
Endif
If ValType(Ate_Bem) == "C"
	cQuery += "TJ_CODBEM <= '"+Ate_Bem+"' AND "
Endif
If ValType(De_CcustoL) == "C"
	cQuery += "TJ_CCUSTO >= '"+De_CcustoL+"' AND "
Endif
If ValType(Ate_Ccusto) == "C"
	cQuery += "TJ_CCUSTO <= '"+Ate_Ccusto+"' AND "
Endif
If ValType(De_CenTraL) == "C"
	cQuery += "TJ_CENTRAB >= '"+De_CenTraL+"' AND "
Endif
If ValType(Ate_CenTra) == "C"
	cQuery += "TJ_CENTRAB <= '"+Ate_CenTra+"' AND "
Endif
cQuery += "TJ_TIPOOS = 'B'  AND "
cQuery += "TJ_TERMINO = 'S' AND TJ_SITUACA = 'L' AND "
cQuery += "TJ_PLANO <>'000000' AND "
If ValType(De_Data) == "D"
	cQuery += "TJ_DTMRINI >= '"+Dtos(De_Data)+"' AND "
Endif
If ValType(Ate_Data) == "D"
	cQuery += "TJ_DTMRFIM <= '"+Dtos(Ate_Data)+"' AND "
Endif
cQuery += "D_E_L_E_T_<>'*'"
cQuery := ChangeQuery(cQuery)
cAlia1 := "__BXCAN1"
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlia1,.T.,.T.)
NGI6PREPDA(cAlia1, cCodIndic)

// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT TS_CODBEM,TS_ORDEM,TS_PLANO,TS_DTPRINI,TS_HOPRINI,TS_DTPRFIM,TS_HOPRFIM "
EndIf
cQuery += "FROM "+RetSqlName("STS")+" WHERE "
cQuery += "TS_FILIAL='"+xFilial("STS")+"' AND "
If ValType(De_BemL) == "C"
	cQuery += "TS_CODBEM >= '"+De_BemL+"' AND "
Endif
If ValType(Ate_Bem) == "C"
	cQuery += "TS_CODBEM <= '"+Ate_Bem+"' AND "
Endif
If ValType(De_CcustoL) == "C"
	cQuery += "TS_CCUSTO >= '"+De_CcustoL+"' AND "
Endif
If ValType(Ate_Ccusto) == "C"
	cQuery += "TS_CCUSTO <= '"+Ate_Ccusto+"' AND "
Endif
If ValType(De_CenTraL) == "C"
	cQuery += "TS_CENTRAB >= '"+De_CenTraL+"' AND "
Endif
If ValType(Ate_CenTra) == "C"
	cQuery += "TS_CENTRAB <= '"+Ate_CenTra+"' AND "
Endif
cQuery += "TS_TIPOOS = 'B' AND "
cQuery += "TS_TERMINO = 'S' AND TS_SITUACA = 'L' AND "
cQuery += "TS_PLANO <> '000000' AND "
If ValType(De_Data) == "D"
	cQuery += "TS_DTMRINI >= '"+Dtos(De_Data)+"' AND "
Endif
If ValType(Ate_Data) == "D"
	cQuery += "TS_DTMRFIM <= '"+Dtos(Ate_Data)+"' AND "
Endif
cQuery += "D_E_L_E_T_<>'*'"
cQuery := ChangeQuery(cQuery)
cAlia2 := "__BXCAN2"
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlia2,.T.,.T.)
NGI6PREPDA(cAlia2, cCodIndic)

dbSelectArea(cAlia1)
dbGotop()
While !Eof()
   If Ascan(vVetBem,(cAlia1)->TJ_CODBEM) == 0
      Aadd(vVetBem,(cAlia1)->TJ_CODBEM)
   Endif
   If !Empty((cAlia1)->TJ_DTPRINI)
      nQtdhP += NGCALCH100(Stod((cAlia1)->TJ_DTPRINI),(cAlia1)->TJ_HOPRINI,;
                           Stod((cAlia1)->TJ_DTPRFIM),(cAlia1)->TJ_HOPRFIM)
   Endif
   Dbskip()
End
dbSelectArea(cAlia1)
dbCloseArea()

dbSelectArea(cAlia2)
dbGotop()
While !Eof()
   If Ascan(vVetBem,(cAlia2)->TS_CODBEM) == 0
      Aadd(vVetBem,(cAlia2)->TS_CODBEM)
   Endif
   If !Empty((cAlia2)->TS_DTPRINI)
      nQtdhP += NGCALCH100(Stod((cAlia2)->TS_DTPRINI),(cAlia2)->TS_HOPRINI,;
                           Stod((cAlia2)->TS_DTPRFIM),(cAlia2)->TS_HOPRFIM)
   Endif
   Dbskip()
End
dbSelectArea(cAlia2)
dbCloseArea()

For nF := 1 To Len(vVetBem)
	If ValType(De_Data) == "D" .And. ValType(Ate_Data) == "D"
		nQtdhO += NGCALEHDIS(NGSEEK("ST9",vVetBem[nF],1,"T9_CALENDA"),De_Data,Ate_Data,"N")
	EndIF
Next nF

// RESULTADO
nResult := nQtdhO-nQtdhP
NGI6PREPVA(cCodIndic, nResult)

RestArea(aAreaOLD)
Return nResult
