#INCLUDE "MNTA555.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"

#DEFINE _nVERSAO 2 //Versao do fonte

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA555
Cadastro de Documentos

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Function MNTA555()
	
	Local aNGBeginPrm := NGBeginPrm( _nVERSAO )
	Local oBrowse
	
	Private cCadastro := NgSX2Nome( "TRU" ) // "Documentos"
	Private aRotina	:= MenuDef()
	
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias( "TRU" )
	oBrowse:SetDescription( cCadastro )
	oBrowse:Activate()
	
	NGReturnPrm( aNGBeginPrm )
	
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Interface da rotina

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function MenuDef()
// Inicializa MenuDef com todas as opções
Return FWMVCMenu( "MNTA555" )

//------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Regras de Modelagem da gravacao

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ModelDef()

	Local oModel
	Local oStructTRU := FWFormStruct(1,"TRU")

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New("MNTA555",,{|oModel| PosValida(oModel)} )

	// Adiciona ao modelo uma estrutura de formulário de edição por campo
	oModel:AddFields("MNTA555_TRU", Nil, oStructTRU )

	oModel:SetDescription( NgSX2Nome( "TRU" ) )

Return oModel

//------------------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Regras de Interface com o Usuario

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
Static Function ViewDef()

	Local oModel := FWLoadModel("MNTA555")
	Local oView  := FWFormView():New()

	// Objeto do model a se associar a view.
	oView:SetModel(oModel)

	//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
	oView:AddField( "MNTA555_TRU" , FWFormStruct(2,"TRU") )

	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( "MASTER" , 100 )

	// Associa um View a um box
	oView:SetOwnerView( "MNTA555_TRU" , "MASTER" )
	
	// Inclusão de itens no Ações Relacionadas de acordo com o NGRightClick
	NGMVCUserBtn( oView )

Return oView

//------------------------------------------------------------------------------
/*/{Protheus.doc} PosValida
Pos validação para exclusão de registro

@author NG Informática Ltda.
@since 01/01/2015
/*/
//------------------------------------------------------------------------------
 Static Function PosValida( oModel )
	
	Local aArea := GetArea()
	Local cQuery := ""
	Local cAliasQry1 := ""
	
	If oModel:GetOperation() == 5 // Excluir
		cAliasQry1 := GetNextAlias()
		cQuery := " SELECT 1 "
		cQuery += "   FROM " + RetSqlName("TRH")+" TRH, " + RetSqlName("TRI")+" TRI "
		cQuery += "  WHERE TRI.TRI_FILIAL  = '"+xFilial("TRI")+"'"
		cQuery += "    AND TRI.TRI_FILIAL  = TRH.TRH_FILIAL"
		cQuery += "    AND TRI.TRI_NUMSIN  = TRH.TRH_NUMSIN "
		cQuery += "    AND TRH.TRH_EVENTO  = '"+TRU->TRU_TIPEVE+"'"
		cQuery += "    AND TRI.TRI_CODDOC  = '"+TRU->TRU_CODDOC+"'"
		cQuery += "    AND TRH.D_E_L_E_T_ <> '*' "
		cQuery += "    AND TRI.D_E_L_E_T_ <> '*' "
		cQuery := ChangeQuery(cQuery)
		dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry1, .F., .T.) 
		dbGoTop()
		
		If !Eof()
			(cAliasQry1)->(dbCloseArea())
			Help(" ",1,STR0007,,STR0008,4,5) //"Atenção"###"Exclusão não permitida, o documento está relacionado a um Sinistro!"
			Return .F.
		EndIf
	EndIf
	
	RestArea(aArea)
	
Return .T.