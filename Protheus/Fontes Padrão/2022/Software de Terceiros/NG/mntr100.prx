#INCLUDE "mntr100.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 1 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR100  ³ Autor ³ Giorgio Fortunato     ³ Data ³ 10/04/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Historico da localizacao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Obs:      ³ Tem problema de conceito 14/09/2010 - ( comite )    Kli    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR100()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Local oReport 
Local aArea := GetArea()

If FindFunction("TRepInUse") .And. TRepInUse()
   //-- Interface de impressao
   oReport := ReportDef()
   oReport:SetLandscape() //Default Paisagem  
   oReport:PrintDialog()
Else
   MNTR100R3()
EndIf   
RestArea(aArea) 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM) 
Return  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ReportDef³ Autor ³ Elisangela Costa      ³ Data ³ 16/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR100                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()
Local oReport 
Local oSection1 
Local oSection2 
Local oCell  

//LAYOUT
/*
          1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7
012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123
______________________________________________________________________________________________________________________________________________________________________________
                                                   Historico da Localizacao                    
______________________________________________________________________________________________________________________________________________________________________________

Bem              Descricao                                   Local         Descricao 
______________________________________________________________________________________________________________________________________________________________________________
XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX    XXXXXX        XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

Dt.Inicio   Hora Inicio   Dt.Fim      Hora Fim   Cont1.Inicio   Cont1.Fim   Dif. Cont1.  Cont2.Inicio  Cont2.Fim   Dif. Cont2.   Componente          Nome
______________________________________________________________________________________________________________________________________________________________________________
XX/XX/XX     99:99     XX/XX/XX    99:99     999999999   999999999    999999999   999999999  999999999    999999999    XXXXXXXXXXXXXXXX    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MNTR100",OemToAnsi(STR0001),"MNT100",{|oReport| ReportPrint(oReport)},STR0001+".")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01 // Bem                                              ³ 
//³ mv_par02 // Posicao                                          ³ 
//³ mv_par03 // Data Inicial                                     ³ 
//³ mv_par04 // Data Fim                                         ³ 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

oSection1 := TRSection():New(oReport,STR0026,{"STZ","ST9","TPS"}) //"Bem pai"
TRCell():New(oSection1,"TZ_BEMPAI" ,"STZ",STR0009,/*Picture*/,/*Tamanho*/) //"Bem"
TRCell():New(oSection1,"T9_NOME"   ,"ST9",STR0010,/*Picture*/,/*Tamanho*/) //"Descrição"
TRCell():New(oSection1,"TZ_LOCALIZ","STZ",STR0011,/*Picture*/,/*Tamanho*/) //"Local"               
TRCell():New(oSection1,"TPS_NOME"  ,"TPS",STR0010,/*Picture*/,/*Tamanho*/) //"Descrição"
TRPosition():New(oSection1,"ST9",1,{|| xFilial() + STZ->TZ_BEMPAI})
TRPosition():New(oSection1,"TPS",1,{|| xFilial() + STZ->TZ_LOCALIZ})

oSection2 := TRSection():New(oSection1,STR0027,{"STZ","ST9"}) //"Movimentacoes"
oCell := TRCell():New(oSection2,"TZ_DATAMOV","STZ",STR0012,"99/99/9999"    ,12,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Dt.Inicio"
oCell := TRCell():New(oSection2,"TZ_HORAENT","STZ",STR0020,/*Picture*/     ,12,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Hora Inicio"
oCell := TRCell():New(oSection2,"TZ_DATASAI","STZ",STR0013,"99/99/9999"    ,12,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Dt.Fim"
oCell := TRCell():New(oSection2,"TZ_HORASAI","STZ",STR0021,/*Picture*/     ,10,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Hora Fim"
oCell := TRCell():New(oSection2,"TZ_POSCONT","STZ",STR0014,/*Picture*/     ,14,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Cont1. Inicio"
oCell := TRCell():New(oSection2,"TZ_CONTSAI","STZ",STR0015,/*Picture*/     ,11,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Cont1. Fim"
oCell := TRCell():New(oSection2,"DIFCONT1"  ,     ,STR0022,"@E 999,999,999",13,.T./*lPixel*/,{||NGRETVAL(1)})  				  //"Dif. Cont.1"
oCell := TRCell():New(oSection2,"TZ_POSCON2","STZ",STR0016,/*Picture*/     ,14,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Cont2. Inicio"
oCell := TRCell():New(oSection2,"TZ_CONTSA2","STZ",STR0017,/*Picture*/     ,10,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Cont2. Fim"
oCell := TRCell():New(oSection2,"DIFCONT2"  ,     ,STR0023,"@E 999,999,999",12,.T./*lPixel*/,{||NGRETVAL(2)})				      //"Dif. Cont.2"
oCell := TRCell():New(oSection2,"TZ_CODBEM" ,"STZ",STR0018,/*Picture*/     ,  ,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Componente"
oCell := TRCell():New(oSection2,"T9_NOME"   ,"ST9",STR0019,/*Picture*/     ,  ,.T./*lPixel*/,/*{|| code-block de impressao }*/) //"Nome"    

//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a
//direita
oSection2:Cell("TZ_POSCONT"):SetHeaderAlign("RIGHT") 
oSection2:Cell("TZ_CONTSAI"):SetHeaderAlign("RIGHT") 
oSection2:Cell("TZ_POSCON2"):SetHeaderAlign("RIGHT") 
oSection2:Cell("TZ_CONTSA2"):SetHeaderAlign("RIGHT") 
oSection2:Cell("DIFCONT1"):SetHeaderAlign("RIGHT")
oSection2:Cell("DIFCONT2"):SetHeaderAlign("RIGHT") 
TRPosition():New(oSection2,"ST9",1,{|| xFilial() + STZ->TZ_CODBEM})

Return oReport 
 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR100R3³ Autor ³ Giorgio Fortunato     ³ Data ³ 10/04/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Historico da localizacao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR100                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR100R3()
Local cString    := "STZ"
Local cDesc1     := STR0001 //"Historico da Localizacao"
Local cDesc2     := ""
Local cDesc3     := ""
Local wnrel      := "MNTR100"
Private aReturn  := {STR0024, 1,STR0025, 1, 2, 1, "",1 } //"Zebrado"# "Administracao"
Private nLastKey := 0
Private cPerg    := "MNT100"
Private Titulo   := STR0001 //"Historico da Localizacao"
Private Tamanho  := "G"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01 // Bem                                              ³ 
//³ mv_par02 // Posicao                                          ³ 
//³ mv_par03 // Data Inicial                                     ³ 
//³ mv_par04 // Data Fim                                         ³ 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)

wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

If nLastKey = 27
   Set Filter To
   dbSelectArea("STZ")
   Return
Endif
SetDefault(aReturn,cString)
RptStatus({|lEnd| RF100Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

dbSelectArea("STZ")
Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ RF100Imp ³ Autor ³ Giorgio Fortunato     ³ Data ³ 22/03/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR100                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function RF100Imp(lEnd,wnRel,titulo,tamanho)
Local cRodaTxt    := ""
Local nCntImpr    := 0 
Local nDifcont1   := 0
Local nDifcont2   := 0 
Private li        := 80 ,m_pag := 1
Private CABEC1    := STR0008 //"Dt.Inicio   Hora Inicio   Dt.Fim      Hora Fim   Cont.1 Inicio   Cont.1 Fim   Dif. Cont.1  Cont.2 Inicio  Cont.2 Fim   Dif. Cont.2   Componente          Nome"
Private Cabec2    := ""
Private nomeprog  := "MNTR100"
nTipo             := IIF(aReturn[4]==1,15,18)

/*
          1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6          7
0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345647890123
_______________________________________________________________________________________________________________________________________________________________________________
Dt.Inicio   Hora Inicio   Dt.Fim      Hora Fim   Cont.1 Inicio   Cont.1 Fim   Dif. Cont.1  Cont.2 Inicio  Cont.2 Fim   Dif. Cont.2   Componente          Nome                 *
_______________________________________________________________________________________________________________________________________________________________________________
Bem..: XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
Local: XXXXXX           XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

XX/XX/XX    99:99         XX/XX/XX    99:99        999,999,999  999,999,999   999,999,999    999,999,999 999,999,999   999,999,999   XXXXXXXXXXXXXXXX    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX               
*/

If Empty(mv_par01) .Or. Empty(mv_par02) .Or. Empty(mv_par03)
   RetIndex("STZ")
   Set Filter To
   Set device to Screen
	
   If aReturn[5] = 1
      Set Printer To
      dbCommitAll()
      OurSpool(wnrel)
   Endif
   MS_FLUSH()
EndIf        

DbSelectArea("STZ")
DbSetOrder(03)
DbSeek(xFilial("STZ")+mv_par01+mv_par02+dtos(mv_par03),.T.)
SetRegua(LastRec())
While !Eof() .and. STZ->TZ_FILIAL = XFILIAL("STZ")            .And. ;
   STZ->TZ_BEMPAI = mv_par01 .And. STZ->TZ_LOCALIZ = mv_par02 .And. ;
   STZ->TZ_DATAMOV <= mv_par04
   
   nDifcont1 := 0
   nDifcont2 := 0
   If !Empty(STZ->TZ_CONTSAI)
      vVetHisEn := NGACUMEHIS(STZ->TZ_CODBEM,STZ->TZ_DATAMOV,STZ->TZ_HORAENT,1,"E")
      vVetHisSa := NGACUMEHIS(STZ->TZ_CODBEM,STZ->TZ_DATASAI,STZ->TZ_HORASAI,1,"E")
      nDifcont1 := vVetHisSa[2] - vVetHisEn[2]
   EndIf

   If !Empty(STZ->TZ_CONTSA2)
      vVetHisEn := NGACUMEHIS(STZ->TZ_CODBEM,STZ->TZ_DATAMOV,STZ->TZ_HORAENT,2,"E")
      vVetHisSa := NGACUMEHIS(STZ->TZ_CODBEM,STZ->TZ_DATASAI,STZ->TZ_HORASAI,2,"E")
      nDifcont2 := vVetHisSa[2] - vVetHisEn[2]
   EndIf

   IncRegua()

   If li > 58
      NGSOMALI(58)
      @ li,000 Psay STR0006 //"Bem..:"
      @ li,007 Psay MV_PAR01
      @ li,024 Psay NGSEEK('ST9',MV_PAR01,1,'ST9->T9_NOME') Picture '@!'

      NGSOMALI(58)
      @ li,000 Psay STR0007 //"Local:"
      @ li,007 Psay MV_PAR02
      @ li,024 Psay NGSEEK('TPS',MV_PAR02,1,'TPS->TPS_NOME') Picture '@!'
      NGSOMALI(58)
   Endif   

   NGSOMALI(58)
   @ li,000 Psay STZ->TZ_DATAMOV Picture '99/99/9999'
   @ li,012 Psay STZ->TZ_HORAENT Picture '99:99'
   @ li,026 Psay STZ->TZ_DATASAI Picture '99/99/9999'
   @ li,038 Psay STZ->TZ_HORASAI Picture '99:99'
   @ li,051 - Len(Alltrim(STZ->TZ_POSCONT)) Psay STZ->TZ_POSCONT picture '@E 999,999,999'
   @ li,064 - Len(Alltrim(STZ->TZ_CONTSAI)) Psay STZ->TZ_CONTSAI picture '@E 999,999,999'
   @ li,078 - Len(Alltrim(nDifcont1))       Psay nDifcont1       Picture '@E 999,999,999'
   @ li,093 - Len(Alltrim(STZ->TZ_POSCON2)) Psay STZ->TZ_POSCON2 picture '@E 999,999,999'
   @ li,105 - Len(Alltrim(STZ->TZ_CONTSA2)) Psay STZ->TZ_CONTSA2 picture '@E 999,999,999'
   @ li,119 - Len(Alltrim(nDifcont2))       Psay nDifcont2       Picture '@E 999,999,999'
   @ li,133 Psay STZ->TZ_CODBEM
   @ li,153 Psay Substr(NGSEEK('ST9',STZ->TZ_CODBEM,1,'ST9->T9_NOME'),1,35)
   DbSelectArea("STZ")
   DbSkip()                                       
   nDifcont1:= 0
   nDifcont2:= 0  
End
Roda(nCntImpr,cRodaTxt,Tamanho)
RetIndex("STZ")
Set Filter To
Set device to Screen

If aReturn[5] = 1
   Set Printer To
   dbCommitAll()
   OurSpool(wnrel)
Endif
MS_FLUSH()

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³ Elisangela Cosa       ³ Data ³ 16/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ReportDef                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)
Local oSection1 := oReport:Section(1)
Local cFiltro   := "" 

If Empty(mv_par01) .Or. Empty(mv_par02) .Or. Empty(mv_par03) .Or. Empty(mv_par04)
   Return
EndIf        

#IFDEF TOP
	cFiltro := "@TZ_FILIAL = '"+XFILIAL("STZ")+"' And "
	cFiltro += "TZ_BEMPAI = '" + mv_par01 + "' And TZ_LOCALIZ = '" + mv_par02 + "' And "
   cFiltro += "TZ_DATAMOV >= '" + dtoS(mv_par03) + "' AND TZ_DATAMOV <= '" + dtoS(mv_par04) + "'"
#ELSE
	cFiltro := 'STZ->TZ_FILIAL = "'+XFILIAL("STZ")+'" .And. '
	cFiltro += 'STZ->TZ_BEMPAI = "' + mv_par01 + '" .And. STZ->TZ_LOCALIZ = "' + mv_par02 + '" .And. '
   cFiltro += 'Dtos(STZ->TZ_DATAMOV) >= "' + dtoS(mv_par03) + '" .And. Dtos(STZ->TZ_DATAMOV) <= "' + dtoS(mv_par04) + '"'
#ENDIF

oSection1:SetIdxOrder(3)
oSection1:SetFilter(cFiltro)
oSection1:Print()
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³NGRETVAL  ³ Autor ³ Soraia de carvalho    ³ Data ³ 29/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o valor dos campos da secao 1 dos relatorio         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³REPORTDEF                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function NGRETVAL(nCAM)
Local nVALORCAM := 0

If nCAM = 1
   If !Empty(STZ->TZ_CONTSAI)
      vVetHisEn := NGACUMEHIS(STZ->TZ_CODBEM,STZ->TZ_DATAMOV,STZ->TZ_HORAENT,1,"E")
      vVetHisSa := NGACUMEHIS(STZ->TZ_CODBEM,STZ->TZ_DATASAI,STZ->TZ_HORASAI,1,"E")
      nVALORCAM := vVetHisSa[2] - vVetHisEn[2]
   EndIf
Else
   If !Empty(STZ->TZ_CONTSA2)
      vVetHisEn := NGACUMEHIS(STZ->TZ_CODBEM,STZ->TZ_DATAMOV,STZ->TZ_HORAENT,2,"E")
      vVetHisSa := NGACUMEHIS(STZ->TZ_CODBEM,STZ->TZ_DATASAI,STZ->TZ_HORASAI,2,"E")
      nVALORCAM := vVetHisSa[2] - vVetHisEn[2]
   EndIf
EndIf
Return nVALORCAM