/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV019    ³ Autor ³ Inacio Luiz Kolling   ³ Data ³26/11/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula o custo total das hora das manutencoes PREVENTIVAS em³±±
±±³          ³ relacao aos parametros (Custo Homem hora manut. Pr - CHMP )  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data  - Data inicio                                       ³±±
±±³          ³ Ate_Data - Ate data                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nCusto   - Custo das horas de manutencao preventivas         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV019(De_Data,Ate_Data,De_Ccusto,Ate_Ccusto)
Local aAreaOLD   := GetArea(),nCusto := 0.00
Local De_CcustoL := If(De_Ccusto = Nil,Space(NGSEEKDIC("SX3","TJ_CCUSTO",2,"X3_TAMANHO")),De_Ccusto)

// Variáveis de Histórico de Indicadores
Local lMV_HIST := NGI6MVHIST()
Local aParams := {}
Local cCodIndic := "MNTV019"
Local nResult := 0

// Armazena os Parâmetros
If lMV_HIST
	aParams := {}
	aAdd(aParams, {"DE_DATA"   , De_Data})
	aAdd(aParams, {"ATE_DATA"  , Ate_Data})
	aAdd(aParams, {"DE_CCUSTO" , De_Ccusto})
	aAdd(aParams, {"ATE_CCUSTO", Ate_Ccusto})
	NGI6PREPPA(aParams, cCodIndic)
EndIf

cAliasQry := GetNextAlias()
// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT SUM(STL.TL_CUSTO) AS TL_CUSTO "
EndIf
cQuery += " FROM "+RetSqlName("STL")+" STL "
cQuery += " INNER JOIN "+RetSqlName("STJ")+" STJ "
cQuery += " ON STJ.TJ_ORDEM = STL.TL_ORDEM AND STJ.TJ_PLANO = STL.TL_PLANO AND"
If ValType(De_CcustoL) == "C" .and. ValType(Ate_Ccusto) == "C"
	cQuery += " STJ.TJ_CCUSTO >= '"+De_CcustoL+"' AND STJ.TJ_CCUSTO <= '"+Ate_Ccusto+"' AND "
Endif
cQuery += " STJ.TJ_PLANO  <> '000000' AND "
cQuery += " STJ.TJ_SITUACA = 'L' AND "
cQuery += " STJ.TJ_FILIAL = '"+xFilial("STJ")+"' AND STJ.D_E_L_E_T_ <> '*' "
cQuery += " WHERE STL.TL_FILIAL = '"+xFilial("STL")+"' AND "
cQuery += " STL.TL_SEQRELA <> '0  ' AND STL.TL_TIPOREG = 'M' AND "
If ValType(De_Data) == "D"
	cQuery += " STL.TL_DTINICI >= '"+Dtos(De_Data)+"' AND "
Endif
If ValType(Ate_Data) == "D"
	cQuery += " STL.TL_DTINICI <= '"+Dtos(Ate_Data)+"' AND "
Endif
cQuery += " STL.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

dbSelectArea(cAliasQry)
dbGoTop()
While !Eof()
   nCusto += (cAliasQry)->(TL_CUSTO)
   DbSkip()
End
(cAliasQry)->(dbCloseArea())

cAliasQry := GetNextAlias()
// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT SUM(STT.TT_CUSTO) AS TT_CUSTO "
EndIf
cQuery += " FROM "+RetSqlName("STT")+" STT "
cQuery += " INNER JOIN "+RetSqlName("STS")+" STS "
cQuery += " ON STS.TS_ORDEM = STT.TT_ORDEM AND STS.TS_PLANO = STT.TT_PLANO AND"
If ValType(De_CcustoL) == "C" .and. ValType(Ate_Ccusto) == "C"
	cQuery += " STS.TS_CCUSTO >= '"+De_CcustoL+"' AND STS.TS_CCUSTO <= '"+Ate_Ccusto+"' AND "
Endif
cQuery += " STS.TS_PLANO  <> '000000' AND "
cQuery += " STS.TS_SITUACA = 'L' AND "
cQuery += " STS.TS_FILIAL = '"+xFilial("STS")+"' AND STS.D_E_L_E_T_ <> '*' "
cQuery += " WHERE STT.TT_FILIAL = '"+xFilial("STT")+"' AND "
cQuery += " STT.TT_SEQRELA <> '0  ' AND STT.TT_TIPOREG = 'M' AND "
If ValType(De_Data) == "D"
	cQuery += " STT.TT_DTINICI >= '"+Dtos(De_Data)+"' AND "
Endif
If ValType(Ate_Data) == "D"
	cQuery += " STT.TT_DTINICI <= '"+Dtos(Ate_Data)+"' AND "
Endif
cQuery += " STT.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

dbSelectArea(cAliasQry)
dbGoTop()
While !Eof()
   nCusto += (cAliasQry)->(TT_CUSTO)
   DbSkip()
End
(cAliasQry)->(dbCloseArea())

// RESULTADO
nResult := nCusto
NGI6PREPVA(cCodIndic, nResult)

RestArea(aAreaOLD)
Return nResult