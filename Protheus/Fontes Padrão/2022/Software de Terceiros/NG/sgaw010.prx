#INCLUDE "SGAW010.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ SGAW010  ºAutor  ³ Thiago Olis Machado º Data ³  20/10/07  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Workflow de aviso inclusao de metas ambientais             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAMNT                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function SGAW010(cCodPla)

Default cCodPla := ""
Private cIniFile := GetAdv97()
Private cCodEmp  := ""
Private cCodFil  := ""

//Se via schedule
If !(Type("oMainWnd")=="O")
	//Define Empresa e Filial
	cCodEmp := GetPvProfString("ONSTART",STR0001,"",cInIfile) //"Empresa"
	cCodFil := GetPvProfString("ONSTART",STR0002,"",cInIfile) //"Filial"

	If cCodEmp == '-1' .Or. cCodFil == '-1'
		Return .f.
	Endif

	//Nao consome licensas
	RPCSetType(3)

	//Abre empresa/filial/modulo/arquivos
	RPCSetEnv(cCodEmp,cCodFil,"","","SGA","",{"TAA","TC9","QAA","SRA"})

	SGAW010F()//Processa Workflow
Else
	Processa({ || SGAW010F(cCodPla)})//Processa Workflow
EndIf

Return  .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ SGAW010F ºAutor  ³ Thiago Olis Machadoº Data ³  20/10/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Envio do Workflow                                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGASGA                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function SGAW010F(cCodPla)
Local oProcess
Local cArquivo := "SGAW010.htm"
Local cDir := AllTrim(GetMV("MV_WFDIR"))
Local aEmails := {}, cEmail := ""//Variaveis de email
Local cCodProcesso := "WSGA010"//Codigo do processo
Local aArea := GetArea()
Local cTblRes	:= "QAA"
Local cMail		:= ""
Default cCodPla := ""

//Coloco a barra no final do parametro do diretorio
If Substr(cDir,Len(cDir),1) != "\"
	cDir += "\"
Endif

//Verifico se existe o arquivo de workflow
If !File(cDir+cArquivo)
	MsgInfo(">>> "+STR0008+" "+cDir+cArquivo) //"Nao foi encontrado o arquivo"
	Return .F.
Endif

// Arquivo html template utilizado para montagem da aprovação
cHtmlModelo := cDir+cArquivo

// Assunto da mensagem
cAssunto := STR0009 //"Aviso sobre inclusão de Plano de Ação"

// Registre o nome do usuário corrente que esta criando o processo:
cUsuarioProtheus:= SubStr(cUsuario,7,15)

dbSelectArea("TAA")
dbSetOrder(1)
dbSeek(xFilial("TAA")+cCodPla)
While !eof() .and. xFilial('TAA') == TAA->TAA_FILIAL
	aEmails := {}
	cEmails := ""
	If Empty(cCodPla)
		If TAA->TAA_DTIMPL <> dDataBase
			dbSelectArea("TAA")
			dbSkip()
			Loop
		Endif
	Endif
	// Inicialize a classe TWFProcess e assinale a variável objeto oProcess:
	oProcess := TWFProcess():New(cCodProcesso, cAssunto)

	// Crie uma tarefa.
	oProcess:NewTask(cAssunto, cHtmlModelo)

	// Repasse o texto do assunto criado para a propriedade especifica do processo.
	oProcess:cSubject := dtoc(MsDate())+" - "+cAssunto

	// Utilizada a variável __CUSERID para obter o codigo do usuario protheus.
	oProcess:UserSiga := __CUSERID

	// Informe o nome da função de retorno a ser executada quando a mensagem de
	// respostas retornarem ao Workflow:
	oProcess:bReturn := ""

	aAdd(oProcess:oHtml:ValByName("IT1.PLANO"),  TAA->TAA_CODPLA)
	aAdd(oProcess:oHtml:ValByName("IT1.DTIMP"),  TAA->TAA_DTIMPL)
	aAdd(oProcess:oHtml:ValByName("IT1.CCUSTO"), TAA->TAA_CCUSTO)
	aAdd(oProcess:oHtml:ValByName("IT1.DTINI"),  TAA->TAA_DTINPR)
	aAdd(oProcess:oHtml:ValByName("IT1.DTFIM"),  TAA->TAA_DTFIPR)
	aAdd(oProcess:oHtml:ValByName("IT2.DESCRI"), TAA->TAA_NOME)
	aAdd(oProcess:oHtml:ValByName("IT3.SGAOQUE"),   AllTrim(TAA->TAA_OQUE))
	aAdd(oProcess:oHtml:ValByName("IT4.SGAONDE"),   AllTrim(TAA->TAA_ONDE))
	aAdd(oProcess:oHtml:ValByName("IT5.SGAPORQUE") ,AllTrim(TAA->TAA_PORQUE))
	aAdd(oProcess:oHtml:ValByName("IT6.SGACOMO"),   AllTrim(TAA->TAA_COMO))
	If NGCADICBASE('TAA_TPMETA','D','TAA',.F.)
		aAdd(oProcess:oHtml:ValByName("IT7.CABEC"),;
		'<td style="background-color: #C0C0C0" align="center"><font face="Arial" size="2"><b>Tipo de Meta</b></font></td>')
	Else
		aAdd(oProcess:oHtml:ValByName("IT7.CABEC"),"")
	Endif
	aAdd(oProcess:oHtml:ValByName("IT8.CPREV"),  Transform(TAA->TAA_CUSTOP, PesqPict("TAA","TAA_CUSTOP")))
	aAdd(oProcess:oHtml:ValByName("IT8.QTDAT"),  Transform(TAA->TAA_QTDATU, PesqPict("TAA","TAA_QTDATU")))
	aAdd(oProcess:oHtml:ValByName("IT8.META"),   Transform(TAA->TAA_META, 	PesqPict("TAA","TAA_META")))
	If NGCADICBASE('TAA_TPMETA','D','TAA',.F.)
		aAdd(oProcess:oHtml:ValByName("IT8.TPMETA"),;
		'<td style="background-color: #F3F3F3" align="center"><font face="Arial" size="1">'+NGRETSX3BOX("TAA_TPMETA",TAA->TAA_TPMETA)+'</font></td>')
	Else
		aAdd(oProcess:oHtml:ValByName("IT8.TPMETA"),"")
	Endif
	dbSelectArea("TC9")
	dbSetOrder(1)
	dbSeek(xFilial("TC9")+TAA->TAA_CODPLA)
	While !eof() .and. xFilial("TC9")+TAA->TAA_CODPLA == TC9->TC9_FILIAL+TC9->TC9_CODPLA

		cMail 	:= ""
		cTblRes := If( TC9->TC9_TIPRES == "1" , "QAA" , "SRA" )

		//Grava Emails dos responsaveis
		dbSelectArea( cTblRes )
		dbSetOrder(1)
		If dbSeek( xFilial( cTblRes ) + TC9->TC9_CODRES )
			cMail := If( TC9->TC9_TIPRES == "1" , QAA->QAA_EMAIL , SRA->RA_EMAIL )
		Endif

		If !Empty(cMail) .and. aScan(aEmails,{|x| Trim(Upper(x[1])) == Trim(Upper(cMail))}) == 0
			cEmails += Alltrim(cMail) + ";"
			aAdd(aEmails,{AllTrim(cMail)})
		Endif

		dbSelectArea("TC9")
		dbSkip()
	End

	If !Empty(cEmails)
		// Grava os endereços eletrônicos dos destinatários
		oProcess:cTo := cEmails
		oProcess:Start()//Manda email
		MsgInfo(STR0011) //"Aviso sobre inclusão de Plano de Ação enviado com sucesso!"
		oProcess:Finish()
	Endif
	If !Empty(cCodPla)
		Exit
	Else
		dbSelectArea("TAA")
		dbSkip()
	Endif
End
RestArea(aArea)
Return .T.