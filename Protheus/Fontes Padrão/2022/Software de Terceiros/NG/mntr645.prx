#INCLUDE "mntr645.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 2 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR645  ³ Autor ³ NG Informatica        ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relacao Das Ordens de Servico de Acompanhamento             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR645()
	//+-------------------------------------------------+
	//| Armazena variaveis p/ devolucao (NGRIGHTCLICK) 	|
	//+-------------------------------------------------+
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

	Local   cString  := "STP"
	Local   wnrel    :="MNTR645"
	Local   cDesc1   := STR0001 //"Este programa ira emitir um relatorio com as informacoes referente"
	Local   cDesc2   := STR0002 //"as Ordens de Servico de Acompanhamento, para a obtencao da posicao"
	Local   cDesc3   := STR0003 //"atualizada do contador dos Bens do plano."

	Private Tamanho  := "G"
	Private aReturn  := {STR0004,1,STR0005,1,2,1," ",1} //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private cPerg    := "MNT645"
	Private titulo   := STR0006 //"O.S. de Acompanhamento"
	Private ntipo    := 0
	Private nomeprog := "MNTR645"
	Private cabec1,cabec2

	//+--------------------------------------------------------------+
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     // Numero do Plano de Acompanhamento            ³
	//³ mv_par02     // De  Centro de Custo                          ³
	//³ mv_par03     // Ate Centro de Custos                         ³
	//³ mv_par04     // De  Centro de Trabalho                       ³
	//³ mv_par05     // Ate Centro de Trabalho                       ³
	//³ mv_par06     // De  Data de Acompanhamento                   ³
	//³ mv_par07     // Ate Data de Acompanhamento                   ³
	//³ mv_par08     // Salta Pagina para CC 1- Nao 2- Sim           ³
	//³ mv_par09     // Tipo de lista 1-todas 2-so confirmadas       ³
	//+--------------------------------------------------------------+
	Pergunte(cPERG,.F.)
	//+-------------------------------------------+
	//| Envia controle para a funcao SETPRINT     |
	//+-------------------------------------------+
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey = 27
		Set Filter To
		//+-----------------------------------------------+
		//| Devolve variaveis armazenadas (NGRIGHTCLICK)  |
		//+-----------------------------------------------+
		NGRETURNPRM(aNGBEGINPRM)
		Return
	EndIf
	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R645Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

	//+-----------------------------------------------+
	//| Devolve variaveis armazenadas (NGRIGHTCLICK)  |
	//+-----------------------------------------------+
	NGRETURNPRM(aNGBEGINPRM)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³NGCHPLANOA³ Autor ³ NG Informatica Ltda.  ³ Data ³12/09/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Consistencia da plano de companhamento                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NGCHPLANOA()
	Local lRETPL := .f.
	DbSelectArea("STO")
	DbSetOrder(01)
	If DbSeek(xFILIAL("STO")+MV_PAR01)
		If sto->to_situaca <> "C"
			lRETPL := .t.
		Endif
	Endif
Return lRETPL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R645Imp  ³ Autor ³ NG Informatica Ltda.  ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR645                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R645Imp(lEnd,wnRel,titulo,tamanho)

	// Define Variaveis
	Local cRodaTxt := ""
	Local nCntImpr := 0
	
	// Variaveis para controle do cursor de progressao do relatorio
	Local nTotRegs := 0 ,nMult := 1 ,nPosAnt := 4 ,nPosAtu := 4 ,nPosCnt := 0
	Local oArqTRB //Objeto Tabela Temporária
	
	// Contadores de linha e pagina
	Private li := 80 ,m_pag := 1
	Private cAliasTRB := GetNextAlias()
	Private aVetInr   := {}

	// Verifica se deve comprimir ou nao
	nTipo  := IIF(aReturn[4]==1,15,18)
	
	//+-----------------------+
	//| Monta os Cabecalhos   |
	//+-----------------------+
	//cabec1 := STR0007 //"Bem              Descricao               Ult.Cont.  Ult.Leit.  Dt.Prev.  Situacao  O.S.    Dt.Leitura   Contador 1  Contador 2"  
	cabec1 := STR0017 //" Bem              Descricao               Ult.Cont.  Ult.Leit.    Ult.Cont.2  Ult.Leit.2   Dt.Real.    Situacao  O.S.    Dt.Leitura   Contador 1  Contador 2"  
	cabec2 := " " 
	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567
	______________________________________________________________________________________________________________________________________________________________
	Bem              Descricao               Ult.Cont.  Ult.Leit.    Ult.Cont.2  Ult.Leit.2   Dt.Real.    Situacao  O.S.    Dt.Leitura   Contador 1  Contador 2 
	______________________________________________________________________________________________________________________________________________________________

	Centro de Custos - xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	Centro de Trabalho - xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   

	XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXX  999.999.999  XX/XX/XXXX  999.999.999  XX/XX/XXXX   XX/XX/XXXX  xxxxxxxxx xxxxxx  ___/___/___  __________  __________
	Pai..: XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXX

	*/   
	aDBF := {{"CTCUSTO" , "C", 20, 0},;
			 {"CTRAB"   , "C", 06, 0},;
			 {"BEMTQA"  , "C", 16, 0},;
			 {"ORDEMTQA", "C", 06, 0},;
			 {"DTREALAC", "D", 08, 0},; 
			 {"DTLEI1"  , "D", 08, 0},; 
			 {"POSCON"  , "N", 09, 0},; 
			 {"POSCO2"  , "N", 09, 0},; 
			 {"SITUACA" , "C", 01, 0}}
	
	//Criação Tabela Temporária
	oArqTRB := NGFwTmpTbl(cAliasTRB, aDBF, {{"CTCUSTO","CTRAB","BEMTQA"}}) 
	

	Processa({|lEND| MNTR645TQA()},STR0015) //"Processando Arquivo..."

	lPriR465 := .T.
	DbSelectArea(cAliasTRB)
	DbGotop()
	SetRegua(LastRec())
	While !Eof()

		cCCTRB1 := (cAliasTRB)->CTCUSTO    
		If MV_PAR08 = 2
			Li = 80
			NGSOMALI(58)
		Else
			If lPriR465
				lPriR465 := .F.
				NGSOMALI(58)
			Else
				NGSOMALI(58)
				NGSOMALI(58)
			EndIf   
		EndIf
		@ Li,000 Psay STR0013 //"Centro de Custos"
		@ Li,018 Psay " - " + cCCTRB1 +" "+ NGSEEK("SI3",cCCTRB1,1,"I3_DESC")


		DbSelectArea(cAliasTRB)
		While !Eof() .And. (cAliasTRB)->CTCUSTO = cCCTRB1

			cCENTTRB1 := (cAliasTRB)->CTRAB

			If !Empty((cAliasTRB)->CTRAB)
				NGSOMALI(58)
				@ Li,000 Psay STR0009 //"Centro de Trabalho"
				@ Li,018 Psay " - " + cCENTTRB1 +" "+  NGSEEK("SHB",cCENTTRB1,1,"HB_NOME")  
			EndIf
			NGSOMALI(58)

			DbSelectArea(cAliasTRB)
			While !Eof() .And. (cAliasTRB)->CTCUSTO = cCCTRB1 .And. (cAliasTRB)->CTRAB =  cCENTTRB1

				cBEM := (cAliasTRB)->BEMTQA

				DbSelectArea("ST9")
				DbSetOrder(01)
				DbSeek(xFilial("ST9")+cBEM)
				NGSOMALI(58)
				@ Li,001 Psay cBEM
				@ Li,018 Psay Substr(ST9->T9_NOME,1,20)
				@ Li,040 Psay ST9->T9_POSCONT Picture "@E 999,999,999"
				@ Li,053 Psay ST9->T9_DTULTAC Picture "99/99/9999"
				dbSelectArea("TPE")
				dbSetOrder()
				dbSeek(xFilial("TPE")+cBEM)
				@ Li,065 Psay TPE->TPE_POSCON Picture "@E 999,999,999"
				@ Li,078 Psay TPE->TPE_DTULTA  Picture "99/99/9999"

				DbSelectArea(cAliasTRB)
				While !Eof() .And. (cAliasTRB)->CTCUSTO = cCCTRB1 .And. (cAliasTRB)->CTRAB =  cCENTTRB1 ;
				.And. (cAliasTRB)->BEMTQA = cBEM 

					IncRegua()
					@ Li,091 Psay (cAliasTRB)->DTREALAC Picture "99/99/9999"
					If (cAliasTRB)->SITUACA = "P"
						@ Li,103 Psay STR0010   //"Pendente"
					ElseIf (cAliasTRB)->SITUACA = "L"
						@ Li,103 Psay STR0011   //"Liberada"
					Else
						@ Li,103 Psay STR0014   //"Cancelada"
					EndIf 
					@ Li,113 Psay (cAliasTRB)->ORDEMTQA

					IF !Empty((cAliasTRB)->DTLEI1)
						@ Li,121 Psay (cAliasTRB)->DTLEI1 Picture "99/99/9999"
						@ Li,132 Psay (cAliasTRB)->POSCON Picture "@E 999,999,999"
						@ Li,145 Psay (cAliasTRB)->POSCO2 Picture "@E 999,999,999"
					Else
						@ Li,121 Psay "___/___/___  __________  __________"
					EndIf

					//            @ Li,121 Psay "___/___/___  __________  __________"
					NGSOMALI(58) 

					DbSelectArea(cAliasTRB)
					DbSkip()
				End
				//+--------------------------------+
				//| Verifica se o Bem tem Bem Pai  |
				//+--------------------------------+
				cPAITQA := NGBEMPAI(cBEM)
				//+---------------------------------------------+
				//| Impressao da Segunda  Linha da O.S. Bem Pai |
				//+---------------------------------------------+
				If !Empty(cPAITQA) 
					@ Li,000 Psay STR0012 //"Pai.:"
					@ Li,007 Psay cPAITQA //cChave
					@ Li,024 Psay NGSEEK("ST9",cPAITQA,1,"Substr(T9_NOME,1,20)")
				EndIf
			End   
		End   
	End
	Roda(nCntImpr,cRodaTxt,Tamanho)

	//+--------------------------------------------------+
	//| Devolve a condicao original do arquivo principal |
	//+--------------------------------------------------+
	RetIndex("TQA")
	RetIndex("ST9")
	RetIndex("SI3")
	RetIndex("SHB")
	RetIndex("STO")

	Set Filter To
	Set device to Screen
	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf
	MS_FLUSH()

	//Deleta Tabela Temporária
	oArqTrb:Delete()

Return Nil     
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR645TQA³ Autor ³Elisangela Costa       ³ Data ³03/09/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa os arquivos                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR645                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR645TQA()

	cCONDTQA := 'TQA->TQA_CCUSTO >= MV_PAR02 .And. TQA->TQA_CCUSTO <= MV_PAR03 .And. '
	cCONDTQA := cCONDTQA + 'TQA->TQA_CENTRA >= MV_PAR04 .And. TQA->TQA_CENTRA <= MV_PAR05 .And.' 
	cCONDTQA := cCONDTQA + '(TQA->TQA_DTREAL >= MV_PAR06 .And. TQA->TQA_DTREAL <= MV_PAR07)'    

	DbSelectArea("TQA")
	DbSetOrder(03)
	DbSeek(xFilial("TQA")+MV_PAR01)
	Procregua(LastRec())
	While !Eof() .And. TQA->TQA_FILIAL = xFilial("TQA")  .And.;
					   TQA->TQA_PLANO  = MV_PAR01

		IncProc() 
		If &(cCONDTQA)
			If MV_PAR09 = 2
				If TQA->TQA_SITUAC = "P" .Or. TQA->TQA_SITUAC = "C"
					Dbskip()
					Loop
				EndIf   
			EndIf

			DbSelectArea(cAliasTRB)
			(cAliasTRB)->(DbAppend())
			(cAliasTRB)->CTCUSTO  := TQA->TQA_CCUSTO
			(cAliasTRB)->CTRAB    := TQA->TQA_CENTRA 
			(cAliasTRB)->BEMTQA   := TQA->TQA_CODBEM
			(cAliasTRB)->ORDEMTQA := TQA->TQA_ORDEM  
			(cAliasTRB)->DTREALAC := TQA->TQA_DTREAL
			(cAliasTRB)->SITUACA  := TQA->TQA_SITUAC  
			(cAliasTRB)->DTLEI1   := TQA->TQA_DTLEI1  
			(cAliasTRB)->POSCON   := TQA->TQA_POSCON  
			(cAliasTRB)->POSCO2   := TQA->TQA_POSCO2  
		EndIf      
		DbSelectArea("TQA")
		DbSkip()
	End
Return .T.