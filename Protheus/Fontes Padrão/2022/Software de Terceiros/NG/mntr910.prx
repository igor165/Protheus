#INCLUDE "mntr910.ch"
#INCLUDE "PROTHEUS.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR910
RESUMO DO PLANO DE MANUTENCAO POR BEM/SERVICO/TAREFAS 
@author NG Informatica
@since 05/05/98
@version undefined
@type function
@obs Uso Generico
/*/
//---------------------------------------------------------------------
Function MNTR910

	cString		:= "STJ"
	cDesc1		:= STR0001 //"RESUMO DO PLANO DE MANUTENCAO POR BEM, SERVICO , TAREFAS"
	cDesc2		:= STR0002 //"ESTABELECIDO PELO USUµRIO. PODERŽO SEREM FEITAS SELE€™ES DO PLANO "
	cDesc3		:= STR0003 //"BEM INICIO BEM FIM E TIPO ( ANALITICO/SINTETICO )"
	cDesc4		:= ""
	wnrel		:= "MNTR910"
	aReturn		:= { STR0004, 1,STR0005, 1, 2, 1, "",1 } //"Zebrado"###"Administracao"
	nLastKey	:= 0
	cPerg		:= "MNT91R"
	Titulo		:= STR0006 //"Resumo do Plano de Manutencao"
	Tamanho		:= "M"
	lEnd		:= .F.

	Private lUSATAR := If(FindFunction("NGUSATARPAD"),NGUSATARPAD(),.F.)
	//TABELA TEMPORARIA	
	Private cTRB	:= GetNextAlias()
	Private oTempTable		//Objeto Tabela Temporaria
	

	SetKey( VK_F9, { | | NGVersao( "MNTR910" , 2 ) } )//Versão do Fonte

	//----------------------------------------------------------------
	//| Variaveis utilizadas para parametros                         |
	//| MV_PAR01     // Plano                                        |
	//| MV_PAR02     // Do Bem                                       |
	//| MV_PAR03     // Ate o Bem                                    |
	//| MV_PAR04     // Tipo ( Analitico/Sintetico )                 |
	//----------------------------------------------------------------
	Pergunte(cPerg,.F.)

	//----------------------------------------------------------------
	//| Envia controle para a funcao SETPRINT                        |
	//----------------------------------------------------------------
	wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	If nLastKey == 27
		Set Filter To
		Return
	EndIf

	SetDefault(aReturn,cString)

	If nLastKey == 27
		Set Filter To
		Return
	EndIf

	Titulo := If(MV_PAR04 = 1, titulo+STR0013,titulo+STR0014) //" ( ANALITICO )"###" ( SINTETICO )"

	RptStatus({|lEnd| R910Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

	dbSelectArea("STI")
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} R910Imp
Chamada do Relatorio 
@author NG Informatica
@since 05/05/98
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Static Function R910Imp()

	Local cSeq  := 0
	cRodaTxt	:= ""
	nCntImpr	:= 0
	nTotRegs	:= 0
	Cabec1		:= STR0015 //"     Tarefa                                               Obs"
	Cabec2		:= ""
	nomeprog	:= "MNTR910"
	nTipo		:= If(aReturn[4]==1,15,18)

	//----------------------------------------------------------------
	//| Monta os Cabecalhos                                          |
	//----------------------------------------------------------------

	/*
	SINTETICO
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	Plano..:XXXXXX - XXXXXXXXXXXXX40XXXXXXXXXXXXXXXXXXXXXXXX   Data Inicio.:99/99/99  Data Fim.:99/99/99

	Bem....:XXXXXX - XXXXXXXXXXXXX40XXXXXXXXXXXXXXXXXXXXXXXX

	Tarefa                                        Obs
	XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   ___________________________________________________

	Servico..:999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	--------------------------------------------------------------------------------------
	Servico..:999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX


	ANALITICO
	Plano..:XXXXXX - XXXXXXXXXXXXX40XXXXXXXXXXXXXXXXXXXXXXXX   Data Inicio.:99/99/99  Data Fim.:99/99/99

	Bem....:XXXXXX - XXXXXXXXXXXXX40XXXXXXXXXXXXXXXXXXXXXXXX

	Tarefa                                        Obs
	XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   ___________________________________________________

	Servico..:999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	Funcionario..: 999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Produto..: 999999999999999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	999999999999999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Ferramenta...: 9999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	9999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Terceiro...: 999999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	999999 xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	--------------------------------------------------------------------------------------

	Servico..:999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	999999 - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

	*/

	//------------------------------------------------------------
	//| Monta o arquivo de trabalho para o relatorio             |
	//------------------------------------------------------------
	
	

	If MV_PAR04 = 2

		aDBF := {{"BEM"     , "C", 16,0},;
				 {"TAREFA"  , "C", 06,0},;
				 {"SERVICO" , "C", 06,0},;
				 {"SEQSERV" , "C", 03,0},;
				 {"NOMTARE" , "C", 30,0},;
				 {"SEQUENC" , "C", 03,0}}

		If lUSATAR
			Aadd(aDBF,{"SEQTARE"  , "C", 6,0})
		EndIf
		
		//Instancia classe FWTemporaryTable
		oTempTable  := FWTemporaryTable():New( cTRB, aDBF )

		If !lUSATAR
			oTempTable:AddIndex( "Ind01" , {"BEM","SERVICO","SEQSERV","TAREFA"} )
		Else
			oTempTable:AddIndex( "Ind02" , {"BEM","SERVICO","SEQSERV","TAREFA","SEQTARE"} )
		EndIf

		//Cria a tabela temporaria
		oTempTable:Create()

	Else

		aDBF := {{"BEM"     , "C", 16,0},;
 				 {"TAREFA"	, "C", 06,0},;
				 {"SERVICO"	, "C", 06,0},;
				 {"SEQSERV"	, "C", 03,0},;
				 {"TIPO"	, "C", 01,0},;
				 {"CODIGO"	, "C", 15,0},;
				 {"NOMTARE"	, "C", 30,0},;
				 {"SEQUENC"	, "C", 03,0}}
		If lUSATAR
			aAdd(aDBF,{"SEQTARE" , "C", 6,0})
		EndIf
		
		//Instancia classe FWTemporaryTable
		oTempTable  := FWTemporaryTable():New( cTRB, aDBF )
		
		If !lUSATAR
			oTempTable:AddIndex( "Ind03" , {"BEM","SERVICO","SEQSERV","SEQUENC","TAREFA","TIPO","CODIGO"} )
		Else
			oTempTable:AddIndex( "Ind04" , {"BEM","SERVICO","SEQSERV","SEQUENC","TAREFA","TIPO","CODIGO","SEQTARE"}  )
		EndIf

		oTempTable:Create()// Deleta Arquivo Temporario

	EndIf

	dbSelectArea( "STJ" )
	dbSetOrder(3)
	dbSeek(xfilial('STJ')+MV_PAR01)

	SetRegua(LastRec())

	While !Eof() .And. STJ->TJ_FILIAL == XFILIAL('STJ') .And. STJ->TJ_PLANO == MV_PAR01

		IncRegua()

		If (STJ->TJ_CODBEM >= MV_PAR02 .And. STJ->TJ_CODBEM <= MV_PAR03) .And. STJ->TJ_TIPOOS = "B"
			dbSelectArea("STL" )
			dbSetOrder(3)
			If dbSeek(xFilial("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO+"0  ")

				While !Eof() .And. STL->TL_FILIAL == xFilial('STL') .And. STL->TL_ORDEM == STJ->TJ_ORDEM .And.;
				STL->TL_PLANO  == STJ->TJ_PLANO  .And. Alltrim(STL->TL_SEQRELA) == "0"

					cSeq := NGSEEK("ST5",STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA+STL->TL_TAREFA,1,"T5_SEQUENC")
					If Empty(cSeq)
						cSeq := 0
					EndIf

					dbSelectArea( cTRB )
					If MV_PAR04 = 2

						cChaveAc := STJ->TJ_CODBEM+STJ->TJ_SERVICO+STL->TL_TAREFA
						If lUSATAR
							cChaveAc += STL->TL_SEQTARE
						EndIf

						If !dbSeek(cChaveAc)
							(cTRB)->(DbAppend())
							(cTRB)->BEM	 := STJ->TJ_CODBEM
							(cTRB)->SERVICO := STJ->TJ_SERVICO
							(cTRB)->SEQSERV := STJ->TJ_SEQRELA
							(cTRB)->TAREFA	 := STL->TL_TAREFA
							(cTRB)->SEQUENC := cValToChar(STRZERO(cSeq,3))
							MNTR910TA(STJ->TJ_PLANO,STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA,STL->TL_TAREFA)
						EndIf
					Else
						cChaveAc := STJ->TJ_CODBEM+STJ->TJ_SERVICO+STL->TL_TAREFA+STL->TL_TIPOREG+STL->TL_CODIGO
						If lUSATAR
							cChaveAc += STL->TL_SEQTARE
						EndIf

						If !dbSeek(cChaveAc)
							(cTRB)->(DbAppend())
							(cTRB)->BEM	 := STJ->TJ_CODBEM
							(cTRB)->SERVICO := STJ->TJ_SERVICO
							(cTRB)->SEQSERV := STJ->TJ_SEQRELA
							(cTRB)->TAREFA	 := STL->TL_TAREFA
							(cTRB)->TIPO	 := STL->TL_TIPOREG
							(cTRB)->CODIGO	 := STL->TL_CODIGO
							(cTRB)->SEQUENC := cValToChar(STRZERO(cSeq,3))
							MNTR910TA(STJ->TJ_PLANO,STJ->TJ_CODBEM+STJ->TJ_SERVICO+STJ->TJ_SEQRELA,STL->TL_TAREFA)
						EndIf
					EndIf

					dbSelectArea("STL" )
					dbSkip()
				End
			EndIf
		EndIf
		dbSelectArea("STJ" )
		dbSkip()
	End

	dbSelectArea(cTRB)
	dbGoTop()
	SetRegua (LastRec())

	li      := 80
	m_pag   := 1
	nPrinpl := .T.

	While !Eof()

		IncRegua()

		If nPrinpl
			NGSOMALI(58)
			@li,000 PSAY STR0017 //"Plano.:"
			@li,007 PSAY MV_PAR01

			dbSelectArea("STI")
			dbSetOrder(1)
			dbSeek(xfilial('STI')+MV_PAR01)
			@li,027 PSAY STI->TI_DESCRIC
			@li,088 PSAY STR0018 //"Data Inicio.:"
			@li,102 PSAY STI->TI_DATAINI PICTURE '99/99/99'
			@li,113 PSAY STR0019 //"Data Fim.:"
			@li,124 PSAY STI->TI_DATAFIM PICTURE '99/99/99'

			nPrinpl := .f.

			NGSOMALI(58)

		EndIf

		nBem := (cTRB)->BEM
		NGSOMALI(58)
		@li,000 PSAY STR0020 //"Bem...:"
		@li,007 PSAY nBem
		//@li,025 PSAY '-'

		@li,027 PSAY NGSEEK("ST9",nBem,1,"T9_NOME")
		dbSelectArea(cTRB)

		While !Eof() .And. (cTRB)->BEM == nBem

			nServico := (cTRB)->SERVICO

			NGSOMALI(58)
			NGSOMALI(58)
			@li,000 PSAY STR0021 //"Servico..:"
			@li,011 PSAY nServico
			@li,018 PSAY '-'

			@li,020 PSAY NGSEEK("ST4",nServico,1,"T4_NOME")

			dbSelectArea(cTRB)

			While !Eof() .And. (cTRB)->BEM == nBem .And. (cTRB)->SERVICO == nServico

				nSeqServ := (cTRB)->SEQSERV

				NGSOMALI(58)
				@li,002 PSAY "Sequência do Serviço: "
				@li,025 PSAY (cTRB)->SEQSERV PICTURE "@!"

				While !Eof() .And. (cTRB)->BEM == nBem .And. (cTRB)->SERVICO == nServico .And. (cTRB)->SEQSERV == nSeqServ

					nTarefa := (cTRB)->TAREFA

					If MV_PAR04 = 1   // ANALITICO

						NGSOMALI(58)
						@li,004 PSAY (cTRB)->TAREFA
						@li,011 PSAY '-'
						@li,013 PSAY (cTRB)->NOMTARE PICTURE "@!"
						@li,058 PSAY replicate("-",74)
						NGSOMALI(58)
						nTarefa := (cTRB)->TAREFA

						dbSelectArea(cTRB)

						While !Eof() .And. (cTRB)->BEM == nBem .And. (cTRB)->SERVICO == nServico .And. (cTRB)->SEQSERV == nSeqServ .And. (cTRB)->TAREFA == nTarefa

							nInsumo := (cTRB)->TIPO

							nPrin := .T.
							While !Eof() .And. (cTRB)->BEM == nBem .And. (cTRB)->SERVICO == nServico .And. (cTRB)->TAREFA == nTarefa .And. (cTRB)->TIPO == nInsumo

								vNomeI := NGNOMINSUM((cTRB)->TIPO,(cTRB)->CODIGO,30)

								If nPrin
									@li,013 PSAY vNomeI[1,1]
									@li,027 PSAY (cTRB)->CODIGO
									@li,044 PSAY vNomeI[1,2]
									NGSOMALI(58)
									nPrin := .F.
								EndIf


								dbSelectArea(cTRB)
								dbSkip()
								IncRegua()
							End
						End
					Else
						NGSOMALI(58)
						@li,004 PSAY (cTRB)->TAREFA
						@li,011 PSAY '-'
						@li,013 PSAY (cTRB)->NOMTARE PICTURE "@!"
						@li,058 PSAY replicate("-",74)
						While !Eof() .And. (cTRB)->BEM == nBem .And. (cTRB)->SERVICO == nServico .And. (cTRB)->SEQSERV == nSeqServ .And. (cTRB)->TAREFA == nTarefa
							dbSelectArea(cTRB)
							dbSkip()
							IncRegua()
						End
					EndIf
				End
			End
			NGSOMALI(58)
			@li,000 PSAY replicate('-',132)
		End
	End

	Roda(nCntImpr,cRodaTxt,Tamanho)

	//----------------------------------------------------------------
	//| Devolve a condicao original do arquivo principal             |
	//----------------------------------------------------------------
	
	oTempTable:Delete()// Deleta tabela temporaria
	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	EndIf

	MS_FLUSH()

Return NIL
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR910TA
Busca e grava o nome da tarefa 
@author Inacio Luiz Kolling
@since 03/11/2008
@version undefined
@param cplano, characters
@param cPreC, characters
@param cTar, characters
@type function
/*/
//---------------------------------------------------------------------
Function MNTR910TA(cplano,cPreC,cTar)

	If Alltrim(cTar) = "0"
		(cTRB)->NOMTARE := STR0022
	Else
		If lUSATAR
			(cTRB)->SEQTARE := STL->TL_SEQTARE
			lCORRET := If(Val(cplano) = 0,.T.,.f.)
			(cTRB)->NOMTARE := NGNOMETAR(cPreC,cTar)
		Else
			(cTRB)->NOMTARE := NGSEEK("ST5",cPreC+cTar,1,"T5_DESCRIC")
		EndIf
	EndIf

Return .T.