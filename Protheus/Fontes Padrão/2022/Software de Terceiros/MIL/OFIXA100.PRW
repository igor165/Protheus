#include "Protheus.ch"
#include "OFIXA100.ch"

/*
===============================================================================
###############################################################################
##+----------+------------+-------+-----------------------+------+----------+##
##|Funcao    | OFIXA100   | Autor |  Takahashi            | Data | 11/10/10 |##
##+----------+------------+-------+-----------------------+------+----------+##
##|Descricao | Fechamento de OS                                             |##
##+----------+--------------------------------------------------------------+##
##|Uso       | Oficina                                                      |##
##+----------+--------------------------------------------------------------+##
###############################################################################
===============================================================================
*/
Function OFIXA100(lNoMBrowse)

Default lNoMBrowse := .f.

Private cCadastro := STR0001
Private aRotina := MenuDef()

/////////////////////////////////////////////////////////////////////////////////////////////////////
// Valida se a empresa tem autorizacao para utilizar o modulo de Oficina		          //
/////////////////////////////////////////////////////////////////////////////////////////////////////
If !AMIIn(14) .or. !FMX_AMIIn({"OFIXA100","OFIOM030","OFIOM010"})
    Return()
EndIf

// AADD(aRegs,{STR0011,STR0011,STR0011,'MV_CH1','N',1,0,1,'C','','MV_PAR01',STR0015,STR0015,STR0015,'','',STR0016,STR0016,STR0016,'','','','','','','','','','','','','','','','','','','','','',{STR0012,STR0013,STR0014},{},{}})
// AADD(aRegs,{STR0018,STR0018,STR0018,'MV_CH2','C',3,0,0,'G','Vazio().or.ExistCpo("SE4")','MV_PAR02','','','','','','','','','','','','','','','','','','','','','','','','','SE4','','','','',{STR0019,STR0020,STR0021},{STR0019,STR0020,STR0021},{STR0019,STR0020,STR0021}})
// AADD(aRegs,{STR0022,STR0022,STR0022,'MV_CH3','N',1,0,0,'C','','MV_PAR03',STR0023,STR0023,STR0023,'','',STR0024,STR0024,STR0024,'','','','','','','','','','','','','','','','','','','','','',{ STR0025 },{ STR0025 },{ STR0025 }})

Pergunte("OXA100",.f.,,,,.f.)

//
If lNoMBrowse
	dbSelectArea("VO1")
	If ( nOpc <> 0 ) .And. !Deleted()		
		bBlockOXA100 := &( "{ |a,b,c,d,e| " + aRotina[ nOpc,2 ] + "(a,b,c,d,e) }" )
		Eval( bBlockOXA100, Alias(), (Alias())->(Recno()),nOpc)
	EndIf
Else
	SetKey(VK_F12,{ || Pergunte( "OXA100" , .T. ,,,,.f.)})
	//
	DBSelectArea("VAI")
	DBSetOrder(4)
	DBSeek(xFilial("VAI")+__cUserId)
	cFilFAbe := ""
	if VAI->VAI_TIPTEC == "4"
		cFilFAbe := "VO1_FUNABE='"+VAI->VAI_CODTEC+"'"
	endif
	//
	dbSelectArea("VO1")
	dbSetOrder(1)
	mBrowse( 6, 1,22,75,"VO1",,,,,,OXA100LEG(),,,,,,,,cFilFAbe)
	SetKey(VK_F12,Nil)
EndIf
//

Return


/*
===============================================================================
###############################################################################
##+----------+------------+-------+-----------------------+------+----------+##
##|Funcao    | OXA100F    | Autor | Takahashi             | Data | 11/10/10 |##
##+----------+------------+-------+-----------------------+------+----------+##
##|Descricao | Chama Funcao de Fechamento de O.S.                           |##
##+----------+--------------------------------------------------------------+##
##|Uso       | Oficina                                                      |##
##+----------+--------------------------------------------------------------+##
###############################################################################
===============================================================================
*/
Function OXA100F(cAlias,nReg,nOpc)

VAI->(dbSetOrder(4))
If !VAI->(dbSeek(xFilial("VAI")+__cUserID))
	MsgAlert(STR0009) // "Tecnico não encontrado"
	Return .f.
EndIf

OFIXX100(nOpc)

MsUnlockAll()
//
SA1->(MsUnlock()) // Nao remover, pois quando integrado com o Venda Direta o registro permanecia bloqueado
//

Return

/*
===============================================================================
###############################################################################
##+----------+------------+-------+-----------------------+------+----------+##
##|Funcao    | MenuDef    | Autor | Takahashi             | Data | 11/10/10 |##
##+----------+------------+-------+-----------------------+------+----------+##
##|Descricao | Definicao de Menu                                            |##
##+----------+--------------------------------------------------------------+##
##|Uso       | Oficina                                                      |##
##+----------+--------------------------------------------------------------+##
###############################################################################
===============================================================================
*/
Static Function MenuDef()
	Local aRotina := {;
		{STR0002, "PesqBrw",    0, 1},; 			// Pesquisar
		{STR0003, "OXA100F",    0, 3},; 			// Fechar 
		{STR0004, "OXA100LEG",  0, 4, 2, .F.},; 	// Legenda
		{STR0017, "OXA100PESQ", 0, 1}; 				// Pesquisa Avancada 
	}

	If (ExistBlock("OXA100BT"))
		aRotina := ExecBlock("OXA100BT", .F., .F., aRotina)
	EndIf
Return aRotina

/*
===============================================================================
###############################################################################
##+----------+------------+-------+-----------------------+------+----------+##
##|Funcao    | OXA100LEG  | Autor | Takahashi             | Data | 11/10/10 |##
##+----------+------------+-------+-----------------------+------+----------+##
##|Descricao | Legenda                                                      |##
##+----------+--------------------------------------------------------------+##
##|Uso       | Oficina                                                      |##
##+----------+--------------------------------------------------------------+##
###############################################################################
===============================================================================
*/
Function OXA100LEG(nReg)

Local uRetorno  := .t.
Local aLegenda  := {{ 'BR_VERDE'   , STR0005 } ,; // Aberta
					{ 'BR_AZUL'    , STR0006 } ,; // Liberada
					{ 'BR_VERMELHO', STR0007 } ,; // Fechado
					{ 'BR_PRETO'   , STR0008}}    // Cancelado

If ( ExistBlock("OX100LEG") )
	aLegenda := ExecBlock("OX100LEG",.f.,.f.,{aLegenda})
EndIf

If nReg == NIL 	// Chamada direta da funcao onde nao passa, via menu Recno eh passado
	uRetorno := {}                 

	AADD(uRetorno , { 'VO1->VO1_STATUS == "A"', aLegenda[1,1] , aLegenda[1,2]} ) // Aberta
	AADD(uRetorno , { 'VO1->VO1_STATUS == "D"', aLegenda[2,1] , aLegenda[2,2]} ) // Liberada
	AADD(uRetorno , { 'VO1->VO1_STATUS == "F"', aLegenda[3,1] , aLegenda[3,2]} ) // Fechado
	AADD(uRetorno , { 'VO1->VO1_STATUS == "C"', aLegenda[4,1] , aLegenda[4,2]} ) // Cancelado

 	If ( ExistBlock("OX100COR") )
		uRetorno := ExecBlock("OX100COR",.f.,.f.,{uRetorno,aLegenda})
	EndIf

Else
	BrwLegenda(cCadastro,STR0004,aLegenda) //Legenda
EndIf

Return uRetorno

/*
===============================================================================
###############################################################################
##+----------+------------+-------+-----------------------+------+----------+##
##|Funcao    | OXA100PESQ | Autor | Takahashi             | Data | 13/02/13 |##
##+----------+------------+-------+-----------------------+------+----------+##
##|Descricao | Pesquisa Avancada                                            |##
##+----------+--------------------------------------------------------------+##
##|Uso       | Oficina                                                      |##
##+----------+--------------------------------------------------------------+##
###############################################################################
===============================================================================
*/
Function OXA100PESQ()
nOpc := 1
OFIOC450()
Return(.t.)
