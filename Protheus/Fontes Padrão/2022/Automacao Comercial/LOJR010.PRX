#INCLUDE "LOJR010.CH"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo	  LOJR010   Autor  Hanna Caroline         Data  18.05.06 
Ĵ
Descrio  Vendas Mes a Mes por Produto								  
Ĵ
Sintaxe	  LOJR010(void)											  
Ĵ
Parametros 															  
Ĵ
 Uso		  Relatorio Personalizavel									  
ٱ


/*/
Function Lojr010()
Local oReport
Local aArea := GetArea()

MsgAlert(STR0040) // Antes de emitir o relatrio,  recomendada a execuo da rotina Acumulados Mensais, para gerar um arquivo(SL3) de acumulados das vendas de todos os produtos no ms.

If !FindFunction("TRepInUse") .OR. !TRepInUse()

	//Ŀ
	//Executa versao anterior do fonte
	//
	Lojr010R3()
Else
	//Ŀ
	// Variaveis utilizadas para parametros 
	// mv_par01			// Produto de 	 
	// mv_par02			// Produto Ate	 
	// mv_par03			// Grupo de	 	 
	// mv_par04			// Grupo Ate	 
	//
	Pergunte("LJR010",.F.)

	//Ŀ
	//Interface de impressao
	//
	oReport := Ljr010RptDef()
	oReport:PrintDialog()
EndIf

RestArea( aArea )
Return

/*/


Ŀ
Funo	  LOJR010R3 Autor  Elaine F. T. Beraldo   Data  10.08.94 
Ĵ
Descrio  Vendas Mes a Mes por Produto								  
Ĵ
Sintaxe	  LOJR010(void)											  
Ĵ
Parametros 															  
Ĵ
 Uso		  Generico 												  
ٱ


/*/
Function Lojr010R3()
//Ŀ
// Define Variaveis 														  
//
LOCAL cDesc1  := STR0001  // Este programa tem como objetivo relacionar, mes a mes por produto,
LOCAL cDesc2  := STR0002  // as quantidades e valores vendidos em moeda forte. Antes de confima-lo
LOCAL cDesc3  := STR0003  // execute a rotina de acumulados mensais para gerar o arquivo.
LOCAL cString := "SB1"
Local cArea   := Alias()

PRIVATE titulo   := STR0004  // RELATORIO DE VENDAS MES A MES POR PRODUTO
PRIVATE wnRel
PRIVATE tamanho  := "G"
PRIVATE cPerg	  := "LJR010"
// Zebrado / Administracao
PRIVATE aReturn  := { STR0005, 1, STR0006, 1, 2, 1, "",1 }
PRIVATE nomeprog := "LOJR010",nLastKey := 0

//Ŀ
// Variaveis utilizadas para Imporessao do Cabecalho e Rodape   
//
wnrel := "LOJR010"
//Ŀ
// Verifica as perguntas selecionadas 								  
//
pergunte("LJR010",.F.)
//Ŀ
// Variaveis utilizadas para parametros								  
// mv_par01				 // Produto de 								  
// mv_par02				 // Produto Ate								  
// mv_par03				 // Grupo de									  
// mv_par04				 // Grupo Ate									  
//
//Ŀ
// Envia controle para a funcao SETPRINT 							  
//
wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,"")

If nLastKey == 27
	Return
Endif

SetDefault(aReturn,cString)

If  nLastKey == 27
	Return
Endif

//Ŀ
// Abre o SL3 para emitir o relatrio 
//
ChkFile("SL3")                       

RptStatus({|lEnd| LJR010Imp(@lEnd,wnRel,cString)},Titulo)

//Ŀ
// Fecha o SL3 para liberar os FILES do DOS 
//
dbSelectArea("SL3")
dbCloseArea()
dbSelectArea(cArea)

Return

/*/


Ŀ
Funo	  LJR010Imp Autor  Mario Angelo			 Data  08.05.96 
Ĵ
Descrio  Relatorio de Vendas Mes a Mes por Produto 				  
Ĵ
Sintaxe	  LOJR010(void)											  
ٱ


/*/
Function LJR010Imp(lEnd,wnRel,cString)
LOCAL CbCont,cabec1,cabec2,CbTxt

// | Codigo Produto  | Qtd.Mes 01 | Valor Item Mes 01 | Qtd.Mes 02 | Valor Item Mes 02 | Qtd.Mes 03 | Valor Item Mes 03 | Qtd.Mes 04 | Valor Item Mes 04 | Qtd.Mes 05 | Valor Item Mes 05 | Qtd.Mes 06 | Valor Item Mes 06    |
cabec1:= STR0007
// | Descr. Produto  | Qtd.Mes 07 | Valor Item Mes 07 | Qtd.Mes 08 | Valor Item Mes 08 | Qtd.Mes 09 | Valor Item Mes 09 | Qtd.Mes 10 | Valor Item Mes 10 | Qtd.Mes 11 | Valor Item Mes 11 | Qtd.Mes 12 | Valor Item Mes 12    |
cabec2:= STR0008
cbtxt := SPACE(10)
cbcont:= 00
Li 	:= 80
m_pag := 01
dbSelectArea("SL3")
dbSetOrder(1)
dbSeek(cFilial+mv_par01,.t.)

SetRegua(RecCount())

IF  Eof()
	MsgAlert(STR0010) // "No existem informaes a serem visualizadas."
	Return(Nil)
Endif

While !Eof() .and. L3_FILIAL+L3_PRODUTO <= cFilial+mv_par02
	 
		// Incrementa Regua
		IncRegua()

		IF  lEnd
			 @PROW()+1,001 PSAY STR0009  // CANCELADO PELO OPERADOR
			 Exit
		End

		dbSelectArea( "SB1" )                                                       
		dbSetOrder( 1 )
		dbSeek( cFilial+SL3->L3_PRODUTO )														 
		If Eof() .Or. B1_GRUPO<mv_par03 .Or. B1_GRUPO>mv_par04							 
			 dbSelectArea( "SL3" )                                                   
			 dbSkip( )																					 
			 Loop 																						 
		End																								 
																											 
		dbSelectArea( "SL3" )                                                       
		If Li > 60																						 
			 cabec(titulo,cabec1,cabec2,nomeprog,tamanho,GetMV("MV_COMP"))           
		Endif 																							 
																						 
		@ Li,000 PSay "|"                                                            
		@ Li,002 PSay L3_PRODUTO																	  
		@ Li,018 PSay "|"                                                            
		@ Li,020 PSay L3_QUANT01 picture tm(L3_QUANT01,10) 								  
		@ Li,031 PSay "|"                                                            
		@ Li,033 PSay L3_VLRIT01 picture tm(L3_VLRIT01,17) 								  
		@ Li,051 PSay "|"
		@ Li,053 PSay L3_QUANT02 picture tm(L3_QUANT02,10) 								  
		@ Li,064 PSay "|"                                                            
		@ Li,066 PSay L3_VLRIT02 picture tm(L3_VLRIT02,17) 								  
		@ Li,084 PSay "|"                                                            
		@ Li,086 PSay L3_QUANT03 picture tm(L3_QUANT03,10) 								  
		@ Li,097 PSay "|"                                                            
		@ Li,099 PSay L3_VLRIT03 picture tm(L3_VLRIT03,17) 								  
		@ Li,117 PSay "|"                                                            
		@ Li,119 PSay L3_QUANT04 picture tm(L3_QUANT04,10) 								  
		@ Li,130 PSay "|"                                                            
		@ Li,132 PSay L3_VLRIT04 picture tm(L3_VLRIT04,17) 								  
		@ Li,150 PSay "|"                                                            
		@ Li,152 PSay L3_QUANT05 picture tm(L3_QUANT05,10) 								  
		@ Li,163 PSay "|"                                                            
		@ Li,165 PSay L3_VLRIT05 picture tm(L3_VLRIT05,17) 								  
		@ Li,183 PSay "|"                                                            
		@ Li,185 PSay L3_QUANT06 picture tm(L3_QUANT06,10) 								  
		@ Li,196 PSay "|"                                                            
		@ Li,198 PSay L3_VLRIT06 picture tm(L3_VLRIT06,17) 								  
		@ Li,219 PSay "|"
		Li++																								 
		@ Li,000 PSay "|"                                                            
	    dbSelectArea( "SB1" ) 
 		@ Li,002 PSay Left(B1_DESC,14) 								  																	  
		@ Li,018 PSay "|"                                                            
		dbSelectArea( "SL3" )                                                       
		@ Li,020 PSay L3_QUANT07 picture tm(L3_QUANT07,10) 								  
		@ Li,031 PSay "|"                                                            
		@ Li,033 PSay L3_VLRIT07 picture tm(L3_VLRIT07,17) 								  
		@ Li,051 PSay "|"                                                            
		@ Li,053 PSay L3_QUANT08 picture tm(L3_QUANT08,10) 								  
		@ Li,064 PSay "|"                                                            
		@ Li,066 PSay L3_VLRIT08 picture tm(L3_VLRIT08,17) 								  
		@ Li,084 PSay "|"                                                            
		@ Li,086 PSay L3_QUANT09 picture tm(L3_QUANT09,10) 								  
		@ Li,097 PSay "|"                                                            
		@ Li,099 PSay L3_VLRIT09 picture tm(L3_VLRIT09,17) 								  
		@ Li,117 PSay "|"                                                            
		@ Li,119 PSay L3_QUANT10 picture tm(L3_QUANT10,10) 								  
		@ Li,130 PSay "|"                                                            
		@ Li,132 PSay L3_VLRIT10 picture tm(L3_VLRIT10,17) 								  
		@ Li,150 PSay "|"                                                            
		@ Li,152 PSay L3_QUANT11 picture tm(L3_QUANT11,10)
		@ Li,163 PSay "|"                                                            
		@ Li,165 PSay L3_VLRIT11 picture tm(L3_VLRIT11,17) 								            
		@ Li,183 PSay "|"                                                            
		@ Li,185 PSay L3_QUANT12 picture tm(L3_QUANT12,10) 								  
		@ Li,196 PSay "|"                                                            
		@ Li,198 PSay L3_VLRIT12 picture tm(L3_VLRIT12,17) 								  
		@ Li,219 PSay "|"                                                            
		Li++																								 
																											 
		dbSelectArea("SL3")                                                         
		dbSkip() 																						 

End

If Li != 80
	@ Li,000 PSay Replicate('*',220)
	Li++
	roda(CbCont,cbtxt,tamanho)
EnDif

//Ŀ
// Restaura a Integridade dos dados									  
//
Set Device To Screen
dbSelectArea("SL3")
dbSetOrder(1)
Set Filter To

dbSelectArea("SB1")
dbSetOrder(1)
Set Filter To

If aReturn[5] == 1
	Set Printer To 
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()
Return

/*


ͻ
Programa  Ljr010RptDef Autor  Hanna / FabioMr      Data   18/05/06   
͹
Desc.     Funcao para informar as celulas que serao utilizadas no rela-  
          latorio                                                        
͹
Uso        LOJA010                                                       
ͼ


*/
Static Function Ljr010RptDef()
Local oReport			// Objeto do relatorio
Local oSection1			// Objeto da secao 1
Local oSection2			// Objeto da secao 2
Local oSection3			// Objeto da secao 3
Local nSize				// Tamanho de cada celula

//Ŀ
// Abre o SL3 para emitir o relatrio 
//
ChkFile("SL3")                       

//Ŀ
//Definimos o tamanho de cada celula a ser impressa
//
nSize := IIf( Len(Transform( "1" , PesqPict("SL3","L3_QUANT01"))) >= Len(Transform( "1" , PesqPict("SL3","L3_VLRIT01"))),;
	Len(Transform( "1" , PesqPict("SL3","L3_QUANT01"))),;
	Len(Transform( "1" , PesqPict("SL3","L3_VLRIT01"))) )

//Ŀ
//Gera a tela com os dados para a confirmao da geracao do relatorio
//

// RELATORIO DE VENDAS MES A MES POR PRODUTO
// Este programa tem como objetivo relacionar, mes a mes por produto, as quantidades e valores 
//vendidos em moeda forte. Antes de confima-lo execute a rotina de acumulados mensais para gerar o arquivo.
oReport := TReport():New("LOJR010",STR0004,"LJR010",{|oReport| Ljr010PrtRpt(oReport)},STR0001 + STR0002 + STR0003 )

//Ŀ
//Define a secao1 do relatorio, informando que o arquivo principal utlizado eh o SL3 e o complementar o SB1
//
oSection1 := TRSection():New(oReport,STR0025,{"SL3","SB1"})	// ACUMULADO DE VENDAS

//Ŀ
//Define as celulas que irao aparecer na secao1
//
TRCell():New(oSection1,"L3_PRODUTO","SL3")
TRCell():New(oSection1,"B1_DESC","SB1")
TRCell():New(oSection1,"L3_FILIAL","SL3")

//Ŀ
//Define a secao2 (que sera filha da secao1)  e o arquivo principal utilizado na secao 
//
oSection2 := TRSection():New(oSection1,STR0026,"SL3")			// QUANTIDADE - ACUMULADO DE VENDAS

//Ŀ
//Define as celulas que irao aparecer na secao2
//
TRCell():New(oSection2,"QUANT"		,"   "," ",/*cPicture*/      ,10         ,/*lPixel*/,/*{|| code-block de impressao }*/) // "Total Geral"
TRCell():New(oSection2,"L3_QUANT01"	,"SL3",STR0013,,nSize)
TRCell():New(oSection2,"L3_QUANT02"	,"SL3",STR0014,,nSize)
TRCell():New(oSection2,"L3_QUANT03"	,"SL3",STR0015,,nSize)
TRCell():New(oSection2,"L3_QUANT04"	,"SL3",STR0016,,nSize)
TRCell():New(oSection2,"L3_QUANT05"	,"SL3",STR0017,,nSize)
TRCell():New(oSection2,"L3_QUANT06"	,"SL3",STR0018,,nSize)
TRCell():New(oSection2,"L3_QUANT07"	,"SL3",STR0019,,nSize)
TRCell():New(oSection2,"L3_QUANT08"	,"SL3",STR0020,,nSize)
TRCell():New(oSection2,"L3_QUANT09"	,"SL3",STR0021,,nSize)
TRCell():New(oSection2,"L3_QUANT10"	,"SL3",STR0022,,nSize)
TRCell():New(oSection2,"L3_QUANT11"	,"SL3",STR0023,,nSize)
TRCell():New(oSection2,"L3_QUANT12"	,"SL3",STR0024,,nSize)

oSection3 := TRSection():New(oSection1,STR0027,"SL3")			// VALOR - ACUMULADO DE VENDAS
TRCell():New(oSection3,"VALOR"		,"   "," ",/*cPicture*/      ,10         ,/*lPixel*/,/*{|| code-block de impressao }*/) // "Total Geral"
TRCell():New(oSection3,"L3_VLRIT01","SL3",STR0028,,nSize)			//"Valor item Mes 01"
TRCell():New(oSection3,"L3_VLRIT02","SL3",STR0029,,nSize)			//"Valor item Mes 02"
TRCell():New(oSection3,"L3_VLRIT03","SL3",STR0030,,nSize)			//"Valor item Mes 03"
TRCell():New(oSection3,"L3_VLRIT04","SL3",STR0031,,nSize)			//"Valor item Mes 04"
TRCell():New(oSection3,"L3_VLRIT05","SL3",STR0032,,nSize)			//"Valor item Mes 05"
TRCell():New(oSection3,"L3_VLRIT06","SL3",STR0033,,nSize)			//"Valor item Mes 06"
TRCell():New(oSection3,"L3_VLRIT07","SL3",STR0034,,nSize)			//"Valor item Mes 07"
TRCell():New(oSection3,"L3_VLRIT08","SL3",STR0035,,nSize)			//"Valor item Mes 08"
TRCell():New(oSection3,"L3_VLRIT09","SL3",STR0036,,nSize)			//"Valor item Mes 09"
TRCell():New(oSection3,"L3_VLRIT10","SL3",STR0037,,nSize)			//"Valor item Mes 10"
TRCell():New(oSection3,"L3_VLRIT11","SL3",STR0038,,nSize)			//"Valor item Mes 11"
TRCell():New(oSection3,"L3_VLRIT12","SL3",STR0039,,nSize)			//"Valor item Mes 12"

Return oReport

/*


ͻ
Programa  Ljr010PrtRptAutor  Hanna / FabioMr      Data   17/05/06   
͹
Desc.     Funcao para impressao do relatorio personalizavel             
͹
Uso        LOJR010                                                      
ͼ


*/
Static Function Ljr010PrtRpt( oReport )
Local oSection1 := oReport:Section(1)				// Define a secao 1 do relatorio
Local oSection2 := oSection1:Section(1)				// Define que a secao 2 serah filha da secao 1
Local oSection3 := oSection1:Section(2)				// Define que a secao 3 tambem serah filha da secao 1
Local cAlias1	:= GetNextAlias()					// Pega o proximo Alias Disponivel
Local cAlias2	:= GetNextAlias()					// Pega o proximo Alias Disponivel
Local cAlias3	:= GetNextAlias()					// Pega o proximo Alias Disponivel

#IFDEF TOP
	DbSelectArea("SL3")
	DbSetOrder(1)
	//Ŀ
	//Transforma parametros do tipo Range em expressao SQL para ser utilizada na query 
	//Exemplo Word, nao intervalos pre definidos (mais utilizado pelo RH - Ferias)     
	//
	MakeSqlExpr("LJR010")

	//Ŀ
	//Inicializa a secao 1
	//
	BEGIN REPORT QUERY oSection1

	//
	//Query da secao 1
	//
	BeginSql alias cAlias1
		SELECT	B1_DESC,L3_PRODUTO,L3_FILIAL
				
		FROM %table:SB1% SB1,%table:SL3% SL3        
		WHERE	B1_COD		=	L3_PRODUTO		AND 
				L3_FILIAL	=	%xfilial:SL3%	AND
				B1_FILIAL	=	%xfilial:SB1%	AND
				L3_PRODUTO	>=	%exp:mv_par01%	AND
				L3_PRODUTO	<=	%exp:mv_par02%	AND
				B1_GRUPO	>=	%exp:mv_par03%	AND
				B1_GRUPO	<=	%exp:mv_par04%	AND
				SL3.%notDel%					AND
				SB1.%notDel%

		ORDER BY %Order:SL3%
	EndSql
	
	//Ŀ
	//Indica o fim da query, caso utilize range, deve-se informar como parametro 
	//{MV_?,MV_?} para que seja incluido no select automaticamente               
	//
	END REPORT QUERY oSection1 //PARAM mv_par01,mv_par02
	
	//Ŀ
	//Define a descricao para a celula QUANT, que ja esta definida na secao 2
	//
	oSection2:Cell("QUANT"):SetValue(STR0011)			// "Quantidade"

	//Ŀ
	//Inicializa a secao 2
	//
	BEGIN REPORT QUERY oSection2

	//Ŀ
	//Query da secao 2, para impressao dos dados das celulas apenas da secao 2
	//
	BeginSql alias cAlias2
		SELECT	L3_QUANT01,	L3_QUANT02,	L3_QUANT03,	L3_QUANT04,	L3_QUANT05,
				L3_QUANT06,	L3_QUANT07,	L3_QUANT08,	L3_QUANT09,	L3_QUANT10,	L3_QUANT11,	L3_QUANT12
				
		FROM %table:SL3% SL3        
		WHERE	L3_PRODUTO	=	%report_param: (cAlias1)->L3_PRODUTO%
		AND 	L3_FILIAL	=	%xfilial:SL3%
		ORDER BY %Order:SL3%
	EndSql

	END REPORT QUERY oSection2

	//Ŀ
	//Define a descricao para a celula VALOR, que ja esta definida na secao 3
	//
	oSection3:Cell("VALOR"):SetValue( STR0012 )				// "Valor"
	
	//Ŀ
	//Esconde o cabecalho, pois nao eh necessario ser impresso neste caso (secao 3)
	//
	oSection3:SetHeaderSection(.F.)

	//Ŀ
	//Inicializa a secao 3
	//
	BEGIN REPORT QUERY oSection3

	//Ŀ
	//Query da secao 3, para impressao dos dados das celulas apenas da secao 3
	//
	BeginSql alias cAlias3
		SELECT	L3_VLRIT01,	L3_VLRIT02,	L3_VLRIT03,	L3_VLRIT04,	L3_VLRIT05,	L3_VLRIT06,	L3_VLRIT07,
				L3_VLRIT08,	L3_VLRIT09,	L3_VLRIT10,	L3_VLRIT11,	L3_VLRIT12
				
		FROM %table:SL3% SL3        
		WHERE	L3_PRODUTO	=	%report_param: (cAlias1)->L3_PRODUTO%
		AND 	L3_FILIAL	=	%xfilial:SL3%
		ORDER BY %Order:SL3%
	EndSql

	END REPORT QUERY oSection3

#ELSE

	//Ŀ
	//Utilizar a funcao MakeAdvlExpr, somente quando for utilizar o range de parametros
	//
	MakeAdvplExpr("LJR010")

	DbSelectArea("SL3")
	DbSetOrder(1)

	cFiltro := "L3_FILIAL = '" + xFilial("SL3") + "'"
	cFiltro += " .AND. L3_PRODUTO >= '"+mv_par01+"' .and. L3_PRODUTO <= '"+mv_par02+"'"
	
	//
	//Efetua o filtro de acordo com a expressao (somente do SL3), pois o SB1 nao esta sendo utilizado ainda 
	//
	oSection1:SetFilter( cFiltro )

	//
	//Efetua a condicao do codeblock dentro do filtro jah efetuado, efetua o filtro do B1_GRUPO do pergunte 
	//
	oSection1:SetLineCondition({|| ChkB1_GRUPO()})

	//Ŀ
	//Executa a secao2, com o mesmo filtro da secao1.                                   
	//A condicao no codeblock, informa que o laco da secao sera enqt o produto for igual
	//
	oSection2:SetParentFilter( { | cParam | SL3->L3_PRODUTO == cParam},{|| SL3->L3_PRODUTO } )
	oSection2:Cell("QUANT"):SetValue( STR0011 )			// "Quantidade"
	
	//Ŀ
	//Devido a secao 2 dar o skip na tabela SL3, eh necessario dar o DbSkip(-1) para o novo laco do filtro
	//
	oSection3:SetParentFilter( { | cParam | SL3->L3_PRODUTO == cParam},{|| SL3->(DbSkip(-1)),SL3->L3_PRODUTO } )
	oSection3:Cell("VALOR"):SetValue( STR0012 )				// "Valor"

	//
	//Esconde o cabecalho da secao 3, que nao eh necessaria ser apresentada
	//
	oSection3:SetHeaderSection(.F.)
#ENDIF	

//Ŀ
//Executa a impressao dos dados, de acordo com o filtro ou query
//
oSection1:Print()
Return

/*


ͻ
Programa  ChkB1_GRUPOAutor  Hanna / Fabiomr      Data   18/05/06   
͹
Desc.     Funcao para considerar o grupo de produtos, informado no pa- 
          rametro no filtro em Codebase                                
͹
Uso        LOJR010                                                     
ͼ


*/
Static Function ChkB1_GRUPO()

DbSelectArea( "SB1" )                                                       
DbSetOrder( 1 )
Return MsSeek( xFilial( "SL3" ) + SL3->L3_PRODUTO ) .AND. SB1->B1_GRUPO >= MV_PAR03 .AND. SB1->B1_GRUPO <= MV_PAR04