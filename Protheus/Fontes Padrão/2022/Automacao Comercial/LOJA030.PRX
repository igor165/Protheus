#INCLUDE "LOJA030.CH"
#INCLUDE "PROTHEUS.Ch"
#INCLUDE "FWEVENTVIEWCONSTS.CH"                            
#INCLUDE "FWADAPTEREAI.CH"
#INCLUDE "FWMVCDEF.CH"  

Static lR7 := GetRpoRelease("R7")

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ LojA030  ³ Autor ³ Wagner Xavier         ³ Data ³ 06.06.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualiza‡„o de Pre‡os.                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaLoja                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Loja030()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabe‡alho da tela de atualiza‡”es                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemToAnsi(STR0007) //"Atualiza‡„o de Pre‡os de Venda"

If lR7
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('SB0')
	oBrowse:SetDescription(OemToAnsi(STR0007))
	oBrowse:Activate()
Else
	mBrowse( 6, 1, 22, 75, "SB0", NIL, NIL, 20)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recupera a Integridade dos dados                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MsUnlockALL( )

Return (NIL)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MenuDef   ³ Autor ³ Fernando Amorim       ³ Data ³08/12/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de definição do aRotina                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina   retorna a array com lista de aRotina             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATMK                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
  
Local nX                                                      // Controle de loop
Local lLJ030ROT := ExistBlock("LJ030ROT")                // Verifica se existe o ponto de entrada LJ030ROT
Local aRotAdic	:= {}  
Local aRotina	:= {}   // Array com rotinas adicionais

If lR7
	ADD OPTION aRotina TITLE STR0001 ACTION "PesqBrw"                                          OPERATION 0                                                                                                     ACCESS 0 //"Pesquisar"
	ADD OPTION aRotina TITLE STR0002 ACTION "VIEWDEF.LOJA030"     	OPERATION MODEL_OPERATION_VIEW     	ACCESS 0 //"Visualizar"
	ADD OPTION aRotina TITLE STR0003 ACTION "VIEWDEF.LOJA030"     	OPERATION MODEL_OPERATION_INSERT	ACCESS 0 //"Incluir"
	ADD OPTION aRotina TITLE STR0004 ACTION "VIEWDEF.LOJA030"     	OPERATION MODEL_OPERATION_UPDATE   	ACCESS 0 //"Alterar"
	ADD OPTION aRotina TITLE STR0005 ACTION "VIEWDEF.LOJA030"     	OPERATION MODEL_OPERATION_DELETE   	ACCESS 0 //"Excluir"
	ADD OPTION aRotina TITLE STR0006 ACTION "lj030Rec"				OPERATION MODEL_OPERATION_DELETE	ACCESS 0 //"Recalcular"

Else
	aRotina:= { 	 { STR0001,"AxPesqui", 0 , 1 , , .F.},; //"Pesquisar"
                   { STR0002,"AxVisual", 0 , 2 , , .T.},; //"Visualizar"
                   { STR0003,"LJ030Inc", 0 , 3 , , .T.},; //"Incluir"
                   { STR0004,"LJ030Alt", 0 , 4 , , .T.},; //"Alterar"
                   { STR0005,"lj030Del" ,0 , 5 , , .T.},; //"Excluir"
                   { STR0006,"lj030Rec" ,0 , 5 , , .T.} } //"Recalcular"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada - Adiciona rotinas ao aRotina       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLJ030ROT
	aRotAdic :=   ExecBlock("LJ030ROT",.F.,.F.)
	If ValType(aRotAdic) == "A"
		For nX := 1 to LEN(aRotAdic)
			If LEN(aRotAdic[nX]) == 4
				AADD(aRotina,aRotAdic[nX])
			Endif
		Next nX
	Endif
Endif

Return(ARotina)

//-------------------------------------------------------------------
/* {Protheus.doc} ModelDef
Definicao do Modelo de dados.

@author Vendas & CRM
@since 14/08/2012
@version 11
@return  oModel - Retorna o model com todo o conteudo dos campos preenchido

*/
//-------------------------------------------------------------------
Static Function ModelDef()
Local oStructSB0 	:= FWFormStruct(1,"SB0") // Estrutura da tabela SB0
Local oModel 		:= Nil						// Objeto do modelo de dados

//-----------------------------------------
//Monta o modelo do formulário 
//-----------------------------------------
oModel:= MPFormModel():New("LOJA030",/*Pre-Validacao*/,/*Pos-Validacao*/,{|oModel| LJ030Grava(oModel)}/*Commit*/,{|oModel| LJ030Cancel(oModel)}/*Cancel*/)
oModel:AddFields("SB0MASTER", Nil/*cOwner*/, oStructSB0 ,/*Pre-Validacao*/,/*Pos-Validacao*/,/*Carga*/)
oModel:GetModel("SB0MASTER"):SetDescription(OemToAnsi(STR0007)) //"Atualiza‡„o de Pre‡cos de Venda"

Return oModel

//-------------------------------------------------------------------
/* {Protheus.doc} ViewDef
Definicao da Interface do programa.

@author		Vendas & CRM
@version	11
@since 		14/08/2012
@return		oView - Retorna o objeto que representa a interface do programa

*/
//-------------------------------------------------------------------
Static Function ViewDef()
Local oView      := Nil						// Objeto da interface
Local oModel     := FWLoadModel("LOJA030") // Objeto do modelo de dados
Local oStructSB0 := FWFormStruct(2,"SB0") // Estrutura da tabela SB0

//-----------------------------------------
//Monta o modelo da interface do formulário
//-----------------------------------------
oView := FWFormView():New()
oView:SetModel(oModel)  
oView:SetDescription(STR0007) //"Atualização de Preços de Venda"
oView:EnableControlBar(.T.)  
oView:AddField( "VIEW_SB0" , oStructSB0,"SB0MASTER" )
oView:CreateHorizontalBox( "HEADER" , 100 )
oView:SetOwnerView( "VIEW_SB0" , "HEADER" )
                
Return oView

//-------------------------------------------------------------------
/*{Protheus.doc} LJ030Grava
Realiza a gravacao dos dados

@param		oModel - Objeto do modelo de dados
@author 	Vendas & CRM
@since 		14/08/2012
@version 	11
@return		lRet - Retorna .T. se a gravacao foi concluida com sucesso / .F. se houver qualquer tipo de falha
*/
//-------------------------------------------------------------------
Static Function LJ030Grava(oModel)
Local aArea			:= GetArea()				// Salva a area corrente
Local lRet 			:= .T.						// Retorno da funcao
Local nOpc 			:= oModel:GetOperation()	// Numero da operacao (1: Visualizacao, 3: Inclusao, 4: Alteracao, 5: Exclusao)
Local lLj030Excl	:= ExistBlock("Lj030Excl")	// Ponto de entrada LJ030EXCL
Local cB0FILIAL		:= oModel:GetValue("SB0MASTER", "B0_FILIAL")
Local cB0COD		:= oModel:GetValue("SB0MASTER", "B0_COD")

//A cada inclusão/alteração/exclusão do produto grava data e hora  
If nOpc == 5
	If SB0->(ColumnPos("B0_DTHRALT") > 0) 
		DbSelectArea("SB0")
		SB0->(DbSetOrder(1)) // B0_FILIAL+B0_COD
		If SB0->(DbSeek(cB0FILIAL + cB0COD))
			RecLock("SB0",.F.)
			REPLACE SB0->B0_DTHRALT WITH DTos(Date()) + Left(Time(),8)
			SB0->(MsUnLock())
		EndIf	
	EndIf
Else
	If oModel:GetIdField("B0_DTHRALT") > 0
		oModel:SetValue("SB0MASTER", "B0_DTHRALT", DTos(Date()) + Left(Time(),8))
	EndIf
EndIf

lRet := FwFormCommit(oModel)

If lRet

	LJ030AltOk()
	
	If nOpc == 5
		If lLj030Excl
			U_Lj030Excl(.T.)
		Endif
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gravacao no Log de Alteracoes. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SB1->(DbSeek(xFilial("SB1") + SB0->B0_COD))
	
	If nOpc == 3
		FRTGeraSLH("SB1", "I")
		
		If ExistBlock("LJ030INC")
		   ExecBlock("LJ030INC",.F.,.F.,{1})
		EndIf  
	ElseIf nOpc == 4
		FRTGeraSLH("SB1", "A")
		
		If ExistBlock("LJ030ALT")
		   ExecBlock("LJ030ALT",.F.,.F.,{1})
		EndIf 
	EndIf 

EndIf

RestArea(aArea)

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} LJ030Cancel
Funcao executada ao cancelar uma operacao. Nas operacoes de inclusao e alteracao, eh chamado um ponto de entrada ao cancelar.

@param		oModel - Objeto do modelo de dados
@author 	Vendas & CRM
@since 		14/08/2012
@version 	11
@return		.T. - Indica que a operacao foi cancelada com sucesso

*/
//-------------------------------------------------------------------
Static Function LJ030Cancel(oModel)
Local nOpc := oModel:GetOperation() // Numero da operacao (1: Visualizacao, 3: Inclusao, 4: Alteracao, 5: Exclusao)

If nOpc == 3
	If ExistBlock("LJ030INC")
	   ExecBlock("LJ030INC",.F.,.F.,{2})
	EndIf 
ElseIf nOpc == 4
	If ExistBlock("LJ030ALT")
	   ExecBlock("LJ030ALT",.F.,.F.,{2})
	EndIf
EndIf

Return .T.


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³LJ030Inc  ³ Autor ³ Cesar Valadao         ³ Data ³16/05/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de inclusao de Pre‡os de Venda.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³LJ030Inc(ExpC1,ExpN1,ExpN2)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1 = Alias do arquivo                                    ³±±
±±³          ³ExpN1 = Numero do registro                                  ³±±
±±³          ³ExpN2 = Numero da opcao selecionada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³SigaLoja                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJ030Inc( cAlias, nReg, nOpc )

Local nRet		//Retorno do Ponto de Entrada				
Local aParam	:= {{|| .T.}, {|| .T.}, {|| .T.}, {||LJ030AltOk()}}	//Bloco de codigo executado apos a transacao da inclusao do preco

nRet  := AxInclui(cAlias, nReg, nOpc,Nil,Nil,Nil,Nil,Nil,Nil,Nil,aParam,Nil)

If nRet == 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gravacao no Log de Alteracoes. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SB1->(DbSeek(xFilial("SB1") + SB0->B0_COD))
	FRTGeraSLH("SB1", "I")
EndIf
If ExistBlock("LJ030INC")
   ExecBlock("LJ030INC",.F.,.F.,{nRet})
EndIf   

Return (NIL)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³LJ030Alt  ³ Autor ³ Cesar Valadao         ³ Data ³16/05/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de alteracao de Pre‡os de Venda.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³LJ030Alt(ExpC1,ExpN1,ExpN2)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1 = Alias do arquivo                                    ³±±
±±³          ³ExpN1 = Numero do registro                                  ³±±
±±³          ³ExpN2 = Numero da opcao selecionada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³SigaLoja                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJ030Alt( cAlias, nReg, nOpc )

Local nRet		//Retorno do Ponto de Entrada
Local aParam	:= {{|| .T.}, {|| .T.}, {|| .T.}, {||LJ030AltOk()}}	//Bloco de codigo executado apos a transacao da alteracao do preco

nRet := AxAltera(cAlias,nReg,nOpc,/*aAcho*/,/*aCpos*/,/*nColMens*/,/*cMensagem*/,/*iif(!lAcativo, "MA030TudOk(nOpc)", "MA030TudOk(nOpc) .And. AC700ALTALU()")*/,/*cTransact*/,/*cFunc*/,/*aButtons*/,aParam,/*aRotAuto*/,/*lVirtual*/)

If nRet == 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gravacao no Log de Alteracoes. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SB1->(DbSeek(xFilial("SB1") + SB0->B0_COD))
	FRTGeraSLH("SB1", "A")
EndIf

If ExistBlock("LJ030ALT")
   ExecBlock("LJ030ALT",.F.,.F.,{nRet})
EndIf   

Return(NIL)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³lj030Del  ³ Autor ³ Wagner Xavier         ³ Data ³ 05/06/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de exclus„o de Pre‡os de Venda.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³lj030Del(ExpC1,ExpN1,ExpN2)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1 = Alias do arquivo                                    ³±±
±±³          ³ExpN1 = Numero do registro                                  ³±±
±±³          ³ExpN2 = Numero da opcao selecionada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³SigaLoja                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj030Del( cAlias,nReg,nOpc )

Local nOpcA
Local lDeleta    := .T.
Local oDlg
Local lLj030Excl := FindFunction("U_Lj030Excl")     // Ponto de entrada LJ030EXCL

Local lR5:= GetRpoRelease("R5")    						// Indica se o release e 11.5
Local aSizeAut 	:= MsAdvSize()							// Tamanho da tela. 
Local aObjects 	:={}									// Posicao da Tela
Local aInfo  	:={}									// Posicao da Tela
Local aPosObj 	:={}									// Posicao da Tela

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aTELA[0][0],aGETS[0]

While .T.
	nOpcA  := 0
	dbSelectArea(cAlias)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Release 11.5³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If lR5	
		// Calcula as posicoes da tela
		aObjects := {}
		AADD(aObjects,{100,100,.T.,.T.})
		AADD(aObjects,{315,035,.T.,.T.})

		aInfo 	:= {aSizeAut[1],aSizeAut[2],aSizeAut[3],aSizeAut[4],3,3}
		aPosObj := MsObjSize(aInfo,aObjects,.T.)        
		
		DEFINE MSDIALOG oDlg TITLE cCadastro From aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5]  OF oMainWnd PIXEL	
		EnChoice( cAlias, nReg, nOpc, ,"AC",OemToAnsi(STR0012),,aPosObj[1],,,,,,oDlg) //"Quanto … exclus„o?" 		
	Else
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM 9,0 TO 28,80 OF oMainWnd
		EnChoice( cAlias, nReg, nOpc, ,"AC",OemToAnsi(STR0012) ) //"Quanto … exclus„o?" 		
	Endif                                                                                
	
	nOpca := 1	
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})

	If nOpcA == 2
		dbSelectArea( "SB2" )
		If MsSeek( cFilial + SB0->B0_COD )
			While !Eof( ) .AND. SB2->B2_COD == SB0->B0_COD
				If SB2->B2_QATU <> 0
					Help( " ", 1, "A030ESTOQ" )
					lDeleta := .F.
					Exit
				Endif
				dbSkip()
			End
		Endif
		If lDeleta
			dbSelectArea( cAlias )
			RecLock( cAlias ,.F.,.T.)
			dbDelete()			
			MsUnlock()
			LJ030AltOk()
		Endif
		If lLj030Excl
			U_Lj030Excl(lDeleta)
		Endif
	Endif
	Exit
End
dbSelectArea( cAlias )

Return (NIL)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³lj030Rec  ³ Autor ³ Wagner Xavier         ³ Data ³ 05/06/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de Atualiza‡„o de Pre‡os.                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³lj030Rec(ExpC1,ExpN1,ExpN2)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1 = Alias do arquivo                                    ³±±
±±³          ³ExpN1 = Numero do registro                                  ³±±
±±³          ³ExpN2 = Numero da opcao selecionada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³SigaLoja                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj030Rec( cAlias, nReg, nOpc )

Local nOpca := 0
Local oDlg
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01 - A partir do Codigo                                ³
//³ mv_par02 - Ate' o codigo                                     ³
//³ mv_par03 - Do Grupo                                          ³
//³ mv_par04 - Ate o Grupo                                       ³
//³ mv_par05 - Percentual de Reajuste                            ³
//³ mv_par06 - Redu‡„o ou Aumento                                ³
//³ mv_par07 - Tabelas                                           ³
//³ mv_par08 - Produto x Fornecedor                              ³
//³ mv_par09 - Do Fornecedor                                     ³
//³ mv_par10 - Ate Fornecedor                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("LJA030",.F.)

DEFINE MSDIALOG oDlg FROM 100,100 TO 350,510 TITLE cCadastro PIXEL OF oMainWnd

	@ 03,05 TO 100,200 PIXEL
	@ 10,10 SAY OemToAnsi(STR0013) SIZE 180,08 OF oDlg PIXEL //"  Este programa tem como objetivo recalcular as tabelas de pre‡os  dos"
	@ 20,10 SAY OemToAnsi(STR0014) SIZE 180,08 OF oDlg PIXEL //"produtos. Dever  ser informado o intervalo de  produto,  de  grupos, o"
	@ 30,10 SAY OemToAnsi(STR0015) SIZE 180,08 OF oDlg PIXEL //"percentual  de  reajuste,  se  esta  percentagem  ser  de  redu‡Æo  ou"
	@ 40,10 SAY OemToAnsi(STR0016) SIZE 180,08 OF oDlg PIXEL //"aumento, e as tabelas a serem atualizadas, assim como o  intervalo  de"
	@ 50,10 SAY OemToAnsi(STR0017) SIZE 180,08 OF oDlg PIXEL //"fornecedores caso necesite."

	DEFINE SBUTTON FROM 107,115 TYPE 5 ACTION (Pergunte("LJA030",.t.)) ENABLE OF oDlg
	DEFINE SBUTTON FROM 107,144 TYPE 1 ACTION (nOpca := 1,IF(LojaOK(STR0018),oDlg:End(),nOpca:=0)) ENABLE OF oDlg //"Confirma Parƒmetros?"
	DEFINE SBUTTON FROM 107,173 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg

ACTIVATE MSDIALOG oDlg CENTERED

If nOpca == 1
	Processa({|lEnd| La030Proc()})
Endif

Return (NIL)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³La030Proc ³ Autor ³ Wagner Xavier         ³ Data ³ 01.12.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Reprocessamento - recalcula os saldos do Plano de Contas   ³±±
±±³          ³ de acordo com os lan‡amentos contabeis                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ La030Proc(void)                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±   
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function La030Proc()

Local nOrdem
Local nI 	   := 0       		// variavel de looping
Local cCodDe   := mv_par01		// armazena codigo inicial
Local cCodAte  := mv_par02		// armazena codigo final
Local cGrupoDe := mv_par03		// armazena grupo inicial
Local cGrupoAte:= mv_par04		// armazena codigo final
Local nPercent := mv_par05		// armazena percentual do reajuste
Local cTabelas := mv_par07		// armazena as tabelas de precos alteradas
Local nProdxFor:= mv_par08		// armazena produto x fornecedor
Local cFornDe  := mv_par09		// armazena fornecedor inicial
Local cFornAte := mv_par10		// armazena forncedor final                           

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Variaveis do ponto de entrada LJ030REC³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aAntes   := {}		//Array que armazena os valores das tabelas de um produto antes da alteracao
Local aDepois  := {}		//Array que armazena os valores das tabelas de um produto depois da alteracao

ProcRegua(SB0->(RecCount())+SB1->(RecCount())+SB2->(RecCount()))

dbSelectArea( "SB0" )
nOrdem := IndexOrd( )
dbSetOrder( 1 )
MsSeek( cFilial + cCodDe , .T. )

While SB0->( !Eof() ) .AND. SB0->B0_COD <= cCodAte .AND. SB0->B0_FILIAL == cFilial
	IncProc()
	dbSelectArea( "SB1" )
	MsSeek( cFilial+SB0->B0_COD )
	dbSelectArea( "SB0" )

	If SB1->B1_GRUPO < cGrupoDe .OR. SB1->B1_GRUPO > cGrupoAte
		dbSkip( )
		Loop
	End
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se Fornecedor Atende ao Intervalo - Caso Tenha      ³
	//³ Selecionado esta opcao - 12/12/95                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nProdxFor == 1
		If SB1->B1_PROC < cFornDe .OR. SB1->B1_PROC > cFornAte
			dbSelectArea("SB0")
			SB0->( dbSkip() )
			Loop
		Endif
	Endif

	For nI := 1 To 9
		cTab := Str(nI,1)
		If cTab $ cTabelas
		   If mv_par06 == 1
			  nPreco&cTab := B0_PRV&cTab - (B0_PRV&cTab*nPercent/100)
           Else
			  nPreco&cTab := B0_PRV&cTab + (B0_PRV&cTab*nPercent/100)           
           Endif
        Endif
	Next nI  
	
	aAntes 	:= {}
	aDepois := {}
	
	RecLock( "SB0" ) 	
	For nI := 1 To 9
		If Str(nI,1) $ cTabelas
			cTab := Str(nI,1)
			AAdd(aAntes,&('SB0->B0_PRV'+cTab) )
			AAdd(aDepois,nPreco&cTab)
			If cTab $ cTabelas
				&('SB0->B0_PRV'+cTab) := nPreco&cTab
			Endif
						
			//A cada alteração do produto grava data e hora  
			If SB0->(ColumnPos("B0_DTHRALT") > 0) 
				REPLACE SB0->B0_DTHRALT WITH DTos(Date()) + Left(Time(),8)
			EndIf
        Endif
	Next nI   
	MsUnlock()
	
	LJ030AltOk(.T.) //Responsavel em enviar os dados do preco para integracao
	
	dbSelectArea( "SB0" )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de entrada LJ030REC³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("LJ030REC")
		ExecBlock("LJ030REC",.F.,.F.,{aAntes,aDepois})
	EndIf   
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gravacao no Log de Alteracoes. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FRTGeraSLH("SB1", "A")

	SB0->( dbSkip() )
End

Return (NIL)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³LJ030AltOk³ Autor ³ Nunzio Autorino Junior³ Data ³ 31/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Responsavel em enviar os dados do preco para integracao    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Cadastro de precos                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function LJ030AltOk(lRecalcula)

	Local cTipo 		:= ""				   							//Como os dados serao integrados no processo offline    
	Local oProcessOff 	:= Nil				   							//Objeto do tipo LJCProcessoOffLine
	Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)			//Identifica se o ambiente esta operando em offline
	
	Default lRecalcula  := .F. 											//Parametro para indicar que foi escolhida a opcao "Recalcular" no menu da rotina
	
	//Verifica se o ambiente esta em off-line
	If lAmbOffLn
		//Instancia o objeto LJCProcessoOffLine
		oProcessOff := LJCProcessoOffLine():New("010")
		
		//Determina o tipo de operacao 
		If INCLUI
			cTipo := "INSERT"
		ElseIf ALTERA .Or. lRecalcula
			cTipo := "UPDATE"
		Else
			cTipo := "DELETE"
			
			//Considera os registros deletados
			SET DELETED OFF
		EndIf
			    
		If !Empty(cTipo)
			//Insere os dados do processo (registro da tabela)
			oProcessOff:Inserir("SB0", xFilial("SB0") + SB0->B0_COD, 1, cTipo)	
		
			//Processa os dados 
			oProcessOff:Processar()	
		EndIf
		
		//Desconsidera os registros deletados
		SET DELETED ON
	EndIf
	
Return Nil
