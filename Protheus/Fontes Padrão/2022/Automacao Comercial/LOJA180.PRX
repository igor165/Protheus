#INCLUDE "LOJA180.CH"
#Include "FiveWin.CH"

Static cReducIni := space(4)			// Armazena o numero da reducao inicial
Static cReducFim := space(4)			// Armazena o numero da reducao final

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ LojA180	³ Autor ³ Aline C. Vale 	    ³ Data ³ 23.10.97  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime o Leitura da Mem¢ria Fiscal 		   				   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ SigaLoja 	                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Alteracoes³Guilherme ³05/09/06³Alteracao na inicializacao da variavel   ³±±
±±³          ³Santos    ³	     ³nOpcao.                                  ³±±
±±³          ³Mauro S.  ³22/11/06³Melhoria para download da MFD - 113921   ³±±
±±³          ³Conrado Q.³05/04/07³Bops 122711: Alterada a utilização da ch ³±±
±±³          ³          ³        ³amada SubStr(cUsuario,7,15) por cUserName³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Loja180()

Local aArea 		:= GetArea()		// Armazena a area corrente
Local dDataIni 		:= MsDate()			// Aramazena a data inicial
Local dDataFim 		:= MsDate()			// Aramazena a data final
Local cDataIni 		:= ""				// Data Inicial
Local cDataFim 		:= ""             	// Data Final
Local oFont								// Fonte dos objetos da interface
Local oDlgMF							// Instaciacao do objeto de montagem da tela
Local nOpca 		:= 0				// Armazena a opção na selecao do tipo do relatorio
Local oDataIni							// Objeto da data inicial
Local oDataFim							// Objeto da data final
Local oReducIni							// Objeto da reducao inicial
Local oReducFim							// objeto da reducao final
Local oCheck1							// Objeto do check (impressora / disco )
Local nCheck 							// Armazena a opcao o check
Local cLock 		:= cUserName+cEstacao		// 
Local cImpressora						// Armazena o modelo da impressora fiscal 
Local oTipoRel							// Objeto do tipo de relatorio memoria fiscal ou MFD
Local nTipoRel 		:= 1				// Armazena a opcao do tipo de relatorio
Local nRet 	  		:= -1				// Retorno da IFDownMFD
Local cTipo	  		:= ""				// Tipo do download da mfd
Local cInicio		:= ""				// Parametro inicial para o download da mfd
Local cFinal 		:= ""   			// Parametro final para o download da mfd
Local lCont  		:= .T.				// Flag de execução
Local lTouch		:= If( LJGetStation("TIPTELA") == "2", .T., .F. )	//Verifica a Interface de Venda
Local oKeyb                       		// Objeto do teclado
Local lIsMDI 		:= Iif(ExistFunc("LjIsMDI"),LjIsMDI(),SetMDIChild(0)) //Verifica se acessou via SIGAMDI

If cModulo $ "FAT,FIS,OMS"
	cImpressora := SuperGetMv("MV_IMPFIS")
Else
	cImpressora := LJGetStation("IMPFISC")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Abre a impressora fiscal - FAT/OMS/FIS                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cModulo $ "OMS,FAT,FIS"
	If Type("nHdlECF") == "U" .OR. nHdlEcf == -1
		Public nHdlEcf := IFAbrir(SuperGetMv("MV_IMPFIS"),SuperGetMv("MV_PORTFIS"))
		Public lFiscal := nHdlECF >= 0
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz o controle via LockByName para evitar que um usuário acesse       ³
//³ 2 vezes uma rotina que use os periféricos de automação, evitando assim³
//³ a concorrência dos mesmos.                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lIsMDI .AND. !LockByName( cLock )
	Return Nil
Endif

// Verifica se o Caixa e de Cupom Fiscal
If !lFiscal
	// Para emitir o relat¢rio da Mem¢ria Fiscal da impres
	// deve-se estar cadastrado como usu rio fiscal e com os parƒmetros da impressora corretos.
	// Aten‡„o
	If (cModulo $"FAT, FIS, OMS") 
		MsgStop(STR0017,STR0004) 	// "Falha na comunicão com a impressora fiscal. Verifique os parametros MV_IMPFIS, MV_PORTFIS e a conexão física com a impressora fiscal.", "Objetivo do Programa"
	Else
		MsgStop(STR0001 + STR0002,STR0003)		// "Para emitir o relatório da Memória Fiscal da impressora " + "deve-se estar cadastrado como usuário fiscal e com os parâmetros da impressora corretos. ", "Atençäo" 
	EndIf
	Return .F.
EndIf

// Leitura de Mem¢ria Fiscal
If lTouch
	DEFINE MSDIALOG oDlgMF FROM 00,00 TO 470,480 TITLE STR0004 PIXEL OF oMainWnd STYLE DS_MODALFRAME
Else
	DEFINE MSDIALOG oDlgMF FROM 39,85 TO 450,340 TITLE STR0004 PIXEL OF oMainWnd
EndIf
DEFINE FONT oFont NAME "Ms Sans Serif" BOLD
// Definindo o Objeto Teclado
If lTouch
	oKeyb := TKeyboard():New( 135, 05, 2, oDlgMF )     
EndIf
@ 7, 4 TO 60, 121 LABEL STR0005 OF oDlgMF  PIXEL // Objetivo do Programa

//               Este programa tem como objetivo
// efetuar  a  impress„o   da leitura  de   mem¢ria    fiscal   da
// impressora   fiscal
@ 19, 15 SAY (STR0006 + STR0007 + STR0008 + cImpressora + ".") SIZE 100, 40 OF oDlgMF PIXEL FONT oFont   
@ 63,4 TO 90,121 LABEL STR0018 PIXEL OF oDlgMF // "Selecione Relatório"
@ 70,7 Radio oTipoRel Var nTipoRel Items STR0019,STR0020 3D Size 85,10 ON CHANGE ( LjValid180(nTipoRel,@oReducIni,@oReducFim,@oCheck1) ) PIXEL OF oDlgMF // Memoria Fiscal, MFD (Memoria de Fita Detalhe)
        
@ 93,4 TO 118,121 LABEL STR0012 OF oDlgMF  PIXEL // 'Leitura por data'
@ 103,10  SAY STR0009 SIZE 48, 7 OF oDlgMF PIXEL  // Inicial:
@ 103,70  SAY STR0010 SIZE 48, 7 OF oDlgMF PIXEL  // Final:

If lTouch
	@ 103, 30 MSGET oDataIni Var cDataIni SIZE 32, 8 OF oDlgMF PIXEL
	@ 103, 85 MSGET oDataFim Var cDataFim SIZE 32, 8 OF oDlgMF PIXEL
Else                                                               
	@ 103, 30 MSGET oDataIni Var dDataIni SIZE 32, 8 OF oDlgMF PIXEL
	@ 103, 85 MSGET oDataFim Var dDataFim SIZE 32, 8 OF oDlgMF PIXEL 
Endif

@ 122,4 TO 150, 121 LABEL STR0021 OF oDlgMF  PIXEL // Leitura por: Reducao / COO (MFD) 

@ 132,10  SAY STR0009 SIZE 48, 7 OF oDlgMF PIXEL  // Inicial:
@ 132,70  SAY STR0010 SIZE 48, 7 OF oDlgMF PIXEL  // Final:

@ 132, 30 MSGET oReducIni Var cReducIni WHEN empty(dDataIni).AND.empty(dDataFim)  VALID !Empty(cReducIni) .AND. Val(cReducIni)>0 SIZE 30, 8 OF oDlgMF PIXEL
@ 132, 85 MSGET oReducFim Var cReducFim WHEN empty(dDataIni).AND.empty(dDataFim)  VALID !Empty(cReducFim) .AND. Val(cReducFim)>0 SIZE 30, 8 OF oDlgMF PIXEL

@ 153,4 TO 185,121 LABEL STR0014 OF oDlgMF  PIXEL //'Saida da Leitura'
@ 160,10 RADIO oCheck1 VAR nCheck 3D SIZE 60,10 PROMPT STR0015, STR0016 OF oDlgMF PIXEL //'Impressora'/'Disco'
        
If lTouch
	// Definindo as Acoes do teclado quando o foco for para cada Get
	// Definindo a Acao da tecla ENTER do Teclado oKeyb
	// Definindo onde o Foco deve iniciar na Dialog
	oDataIni:bGotFocus  := {|| oKeyb:SetVars(oDataIni,6)}  
	oDataFim:bGotFocus  := {|| oKeyb:SetVars(oDataFim,6)}  
	oReducIni:bGotFocus  := {|| oKeyb:SetVars(oReducIni,4)}  
	oReducFim:bGotFocus  := {|| oKeyb:SetVars(oReducFim,4)}  
	oKeyb:SetEnter({|| (nOpca := 1,IF(MsgYesNo(STR0011,STR0003),oDlgMF:End(),nOpca:=0))}) //"Impressora OK?","Atencao"
	oKeyb:bEsc:={|| nOpca:=2,oDlgMF:End()}
	oDataIni:SetFocus()
Else
	DEFINE SBUTTON FROM 190, 65 TYPE 1;		// OK
	ACTION (nOpca := 1,IF(MsgYesNo(STR0011,STR0003),oDlgMF:End(),nOpca:=0)) ENABLE OF oDlgMF  // "Impressora Ok?", "Atençäo" 
	DEFINE SBUTTON FROM 190, 94 TYPE 2;
	ACTION ( oDlgMF:End(), lCont := .F. ) ENABLE OF oDlgMF
EndIf

ACTIVATE MSDIALOG oDlgMF CENTERED

If lTouch   
	If nOpca == 1
		cDataIni := Substr(cDataIni,1,2)+ "/" + Substr(cDataIni,3,2)+ "/" + Substr(cDataIni,5,2)
		cDataFim := Substr(cDataFim,1,2)+ "/" + Substr(cDataFim,3,2)+ "/" + Substr(cDataFim,5,2)
		Processa({|lEnd| IFMemFisc( nHdlECF, cDataIni, cDataFim,cReducIni,cReducFim,If(nCheck=1,'I','D') )})
	Endif
Else
	If lCont
		If nTipoRel == 1
			If nOpca == 1
				Processa({|lEnd| IFMemFisc( nHdlECF, DTOC(dDataIni), DTOC(dDataFim),cReducIni,cReducFim,If(nCheck=1,'I','D') )})
			Endif 
		Else  
			If Empty( dDataIni )
				cTipo	:= "2"
				cInicio	:= cReducIni
				cFinal	:= cReducFim
			Else
				cTipo	:= "1"     
				cInicio	:= DTOC( dDataIni )
				cFinal	:= DTOC( dDataFim ) 
			EndIf		
			Processa( {|lEnd| nRet := IFDownMFD( nHdlECF, cTipo, cInicio, cFinal ) } )     	  
		Endif
		
	Endif	
Endif	
oFont:End()
RestArea(aArea)
Return Nil

Static Function LjValid180(nTipoRel, oReducIni, oReducFim, oCheck1)
	cReducIni := space(6)
	cReducFim := space(6)

	If nTipoRel == 1 
		oReducIni:Picture	:= '@E 9999'
		oReducFim:Picture	:= '@E 9999'
		oCheck1:Enable()
	ElseIf nTipoRel == 2
		oReducIni:Picture	:= '@E 999999'
		oReducFim:Picture	:= '@E 999999'
		oCheck1:Disable()
	EndIf

	oReducIni:Refresh()
	oReducFim:Refresh()
	oCheck1:Refresh()
Return .T.
