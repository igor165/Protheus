#INCLUDE "PROTHEUS.CH"                                              
#INCLUDE "APWIZARD.CH" 
#INCLUDE "LOJA801.CH"

Static oCodPro 					   												// Codigo do Produto
Static cCodPro 			:= CriaVar("B1_COD",.F.)								// Codigo do Produto 
Static cNomePro			:= CriaVar("B1_DESC",.F.)								// Nome do Produto
Static oNomePro		   															// Nome do Produto 
Static cAliasTRB		:="TRB"                                              
Static cMarca  			:= GetMark()  
Static nQtdeVend        :=0 
Static oChkData         														//permite alteração de data
Static lChkData	    														    // permite alteracao da data
Static oAtencao
Static cAtencao 
Static aProdCad 											 					//Array contendo os produtos já cadastrados
Static oDlg             														// Tela dos produtos já cadastrados
Static oMark                                                                   
Static oWizard
Static oDBTree

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³LOJA801   ³ Autor ³ Vendas Cliente        ³ Data ³ 08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Rotina que efetua atraves do wizard a sugestao de vendas    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ SIGALOJA - VENDA ASSISTIDA                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LOJA801()  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Declaracao de variaveis locais³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nTpProc		:= 1      													// Opcao selecionada. 1-Produto especifico 2-Quantidade vendida
Local aGrid 		:= {}       												// Campos da tabela SL2 que serao exibidos na MsSelect                                                  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Declaracao dos Objetos ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cSugestao 	:=CriaVar("ACU_DESC",.F.)   								// Nome da Sugestao da Venda
Local dDataIni		:= dDataBase										        // Data Inicial para filtrar as vendas efetuadas         
Local dDataFim		:= dDataBase										        // Data Final para filtrar as vendas efetuadas  
Local oDataIniP		:= NIL															// Data Inicial para filtro do produto específico
Local oDataFimP     := NIL   											           	// Data Final para filtro do produto específico
Local aStruTRB 		:={}                                                        // array de estrutura dos arquivos temporarios
Local aNomeTMP		:= {}                                                      //  Array tamporario
Local lQtdeVen		:=.F.														//verifica se o grid esta em quantuidade vendida   
Local nTpSubCat		:= 0	  						                                //Verifica se havera sub categoria       

If !LJ801aVlUs()
	Return(Nil)  
EndIf         

Lj801aGetS(@aGrid,@aStruTRB,@aNomeTMP)   

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem dos paineis do WIZARD , cada funcao representa um painel |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P1() 
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do segundo painel  |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P2(@nTpProc,@oDataIniP,@oDataFimP)
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do Terceiro painel |                   
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P3(@nTpProc ,@lQtdeVen,@dDataIni,@dDataFim,@oDataIniP,@oDataFimP)
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do Quarto painel   |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P4(@nTpProc,@lQtdeVen)   
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do Quinto painel   |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P5(@nTpProc,@oDataIniP,@oDataFimP,@lQtdeVen,@aGrid)
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do Sexto painel    |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P6(@nTpSubCat)   
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do Setimo painel   |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P7(@nTpSubCat,@cSugestao)
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do Oitavo painel   |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P8(@nTpProc,@aGrid,@cCodPro)
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do Nono painel     |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P9(@nTpProc) 
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Montagem do Decimo painel   |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
Lja801P10()
                        
Return(Nil)  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P1	³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Definicao e primeiro painel do Wizard                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P1()
	//ÚÄÄÄÄÄÄÄ¿
	//³Panel 1³
	//ÀÄÄÄÄÄÄÄÙ           
	
	DEFINE WIZARD oWizard TITLE OemToAnsi(STR0001) HEADER OemToAnsi(STR0002) MESSAGE " " ;      //Assistente de sugestão de Venda
	TEXT OemToAnsi(STR0003)+OemToAnsi(STR0004) PANEL NEXT {|| .T.} FINISH {|| .F.}	//Processo de sugestão de Vendas do Sistema loja
	/*Este assistente ira ajuda-lo a relacionar produtos que normalmente são vendidos em conjunto (Sugestão de Vendas)."
	 Clique em avancar para iniciar o assistente"*/

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P2	³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Segundo Painel do Wizard, nesse painel o usuario seleciona ³±±
±±³          ³o tipo de  consulta que ira fazer, se eh por 1-produto      ³±±
±±³          ³especifico ou por 2-quantidade vendida, indicados pela      ³±±
±±³          ³variavel nTpProc                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±³          ³ nTpProc: Tipo escolhido                                    ³±±
±±³				       [1] Produto especifico                             ³±±
±±³          ³         [2] quantidade vendida                             ³±±
±±³          ³ oDataIniP: Objeto Data inicial                             ³±±
±±³          ³ oDataFimP: Objeto Data Final                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P2(nTpProc,oDataIniP,oDataFimP)
//Os objetos de data inicial e data final foram incluidos nessa funcao pois eles serao habilitados ou desabilitados
// de acordo com a opcao selecionada
Local   oTpProc			:= NIL						//objeto tipo do processo 1 prod especifico 2 quantidade vendida
Default oWizard 		:= NIL                     // objeto Wizard
Default nTpProc 		:= 0                       // variavel de tipo de processo
Default oDataIniP 		:= Nil                    //  objeto de pesquisa para data inicial
Default oDataFimP 		:= Nil                    //  objeto de pesquisa para data final

	/*Selecao do processo"
	 Deseja relacionar os produtos por qual critério? */ 
	CREATE PANEL oWizard  HEADER STR0005  MESSAGE OemToAnsi(STR0006) ;
	BACK {|| .T. } ;
	NEXT {||  Lj801aNe2(@nTpProc,1,@oCodPro,@oDataIniP,@oDataFimP)} ;	
	FINISH {||  .F. } PANEL 
	@ 001,01 TO 139,300 
	@ 01,01  TO 139,300 LABEL STR0004		OF oWizard:GetPanel(2) PIXEL //Clique em avancar para iniciar o assistente
	@ 20,20 RADIO oTpProc  		VAR nTpProc ITEMS STR0008,STR0007  	SIZE 70,10 PIXEL OF oWizard:GetPanel(2) ;       
	   
Return 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P3	³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Terceiro Painel do Wizard, nesse painel o usuario seleciona³±±
±±³          ³o produto que deseja consultar, a data inicial e final e    ³±±
±±³          ³porcentagem inicial e final, sendo que eh necessario informar±±
±±³          ³o produto e data inicial e final, com isso sera executada a ³±±
±±³          ³funcao Lj801aNe3  que efetuara filtro e exibira os produtos ³±±
±±³          ³no grid do painel 5 - cinco                                 ³±±
±±³          ³a variavel lQtdeVen representa que sera por produto especif.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±³          ³ nTpProc: Tipo escolhido                                    ³±±
±±³				       [1] Produto especifico                             ³±±
±±³          ³         [2] quantidade vendida                             ³±±
±±³          ³ oDataIniP: Objeto Data inicial                             ³±±
±±³          ³ oDataFimP: Objeto Data Final                               ³±±
±±³          ³ dDataIniP:        Data inicial                             ³±±
±±³          ³ dDataFimP:        Data Final                               ³±±
±±³          ³ lQtdeVen: qtde vendida = .F. , Prod. Especifico :T         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P3(nTpProc,	lQtdeVen,	dDataIni,	dDataFim,;
                            oDataIniP,	oDataFimP)        
                            
Local nPerceIni	   		:= 0												//Percentual inicial
Local oPerceIni         := NIL												//Objeto Percentual inicial
Local nPerceFim			:= 0												//Percentual Final
Local oPerceFim         := NIL												//Objeto Percentual Final
Default oWizard 		:= Nil
Default nTpProc 		:=0
Default lQtdeVen 		:=.F.
Default dDataIni 		:=dDataBase
Default dDataFim 		:=dDataBase
Default oDataIniP 		:=Nil   
Default oDataFimP 		:=Nil    
Default cCodPro     	:=""  

	/*Selecao do processo
	Nesse painel iremos informar o produto que será pesquisado, intervalo de Data e/ou porcentagem, lembrando que a porcentagem diz 
	respeito a quantidade vendida	*/
	CREATE PANEL oWizard HEADER OemToAnsi(STR0005) MESSAGE OemToAnsi(STR0041+CHR(10)+CHR(13)+(STR0039));
	 PANEL BACK {|| Lj801aNe3(@nTpProc  ,2, @cCodPro, dDataINI,dDataFIM,nPerceIni,nPerceFim )};
	 NEXT   {|| Lj801aNe3(@nTpProc  ,1, @cCodPro, dDataINI,dDataFIM,nPerceIni,nPerceFim ,@lQtdeVen)};
                FINISH {|| .F.} PANEL

	oWizard:GetPanel(3)

	@ 01,01 TO 139,300 LABEL 	OF oWizard:GetPanel(3) PIXEL
	@ 10,8 TO 40,292 LABEL STR0010	OF oWizard:GetPanel(3) PIXEL //"Informações sobre o Produto
	@ 22,16  SAY  STR0011     		   	OF oWizard:GetPanel(3) PIXEL SIZE 50,9 //"Produto:"
	@ 20,50  MSGET 	oCodPro  	VAR cCodPro  	SIZE 40,10 	Picture "@!" F3 "SB1" 	OF  oWizard:GetPanel(3) ;
	         VALID (If(!EMPTY(cCodPro), cNomePro:=Lj801aDescP(cCodPro),oNomePro:Refresh())) ;
	         PIXEL
	@ 20,110 SAY 	oNomePro 	Var cNomePro 	OF oWizard:GetPanel(3) COLOR CLR_RED PIXEL SIZE 210,9
	@ 40,8   TO 100,148 LABEL STR0015	OF oWizard:GetPanel(3) PIXEL //"Intervalo de Datas :       
	@ 40,152   TO 100,292 LABEL STR0009	OF oWizard:GetPanel(3) PIXEL //"Porcentagem : 
	@ 53,30  SAY  STR0012 		OF oWizard:GetPanel(3) 	SIZE 50,9 PIXEL //"Data Inicial
	@ 50,65  MSGET oDataIniP 	VAR dDataIni   			SIZE 50,10 	OF  oWizard:GetPanel(3) VALID( If(nTpProc == 1,!EMPTY(dDataIni),.T.) , IIf(!Empty(dDataFim),dDataFim >= dDataIni,.T.)) PIXEL 
	@ 73,30 SAY  STR0013 		OF oWizard:GetPanel(3) 	SIZE 50,9 PIXEL //"Data Final
	@ 70,65 MSGET	oDataFimP  	VAR dDataFim   			SIZE 50,10 	OF  oWizard:GetPanel(3) VALID( If(nTpProc == 1,!EMPTY(dDataFim),.T.) ,  dDataFim >= dDataIni) PIXEL
	@ 53,155  SAY  STR0036  		OF oWizard:GetPanel(3) 	SIZE 50,15 PIXEL //Porcentagem inicial
	@ 52,210  MSGET	oPerceIni  	VAR nPerceIni   			SIZE 40,10 Picture "@E 99.99"	OF  oWizard:GetPanel(3) VALID( If(nTpProc == 1,!EMPTY(nPerceIni),.T.)) PIXEL
	@ 73,155  SAY  STR0037  		OF oWizard:GetPanel(3) 	SIZE 50,15 PIXEL //Porcentagem final
	@ 72,210  MSGET	oPerceFim  	VAR nPerceFim   			SIZE 40,10 Picture "@R 999.99" OF  oWizard:GetPanel(3) VALID( LjVldPorc(nTpProc,nPerceFim) ) PIXEL

	lChkData := .F.
	oChkData := TCheckBox():New(88,100,"Alterar Data",,oWizard:GetPanel(3), 150,400,,,,,,,,.T.,,,)
	oChkData:Disable()
	oChkData:Refresh()	
	
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P4	³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Quarto Painel do Wizard, nesse painel o usuario seleciona  ³±±
±±³          ³a data inicial e data final alem da quantidade vendida inici³±±
±±³          ³al com o intuido de buscar na base de dados todos os produtos±±
±±³          ³que satisfizerem as condicoes da consulta, sera executada a ³±±
±±³          ³funcao Lj801aNe4 que efetuara filtro e exibira os produtos  ³±±
±±³          ³no grid do painel 5 - cinco                                 ³±±
±±³          ³a variavel lQtdeVen representa que sera por qtade vendida   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±³          ³ nTpProc: Tipo escolhido                                    ³±±
±±³				       [1] Produto especifico                             ³±±
±±³          ³         [2] quantidade vendida                             ³±±
±±³          ³ lQtdeVen: qtde vendida = .F. , Prod. Especifico :T         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P4(nTpProc,lQtdeVen)

Local cQuantIni			:=CriaVar("B1_COD",.F.)                             //Quantidade Inicial
Local oQuantIni			:= NIL													//Quantidade Inicial
Local cQuantFim			:=CriaVar("B1_COD",.F.)    							//Quantidade Final
Local oQuantFim 		:= NIL													//Quantidade Final    
Local oDataIni			:= NIL											    	// Data Inicial para filtro da quantidade vendida
Local oDataFim        	:= NIL										           	// Data Final para filtro das Notas Fiscais de Saida
Local dDataIni			:= dDataBase									    // Data Inicial para filtrar as vendas efetuadas         
Local dDataFim			:= dDataBase									    // Data Final para filtrar as vendas efetuadas  
Default oWizard 		:= Nil
Default nTpProc 		:=0
Default lQtdeVen 		:=.F.

	CREATE PANEL oWizard HEADER OemToAnsi(STR0005) MESSAGE "";   //Seleção do processo
	PANEL BACK {||  Lj801aNe4( nTpProc,2 ,dDataINI,dDataFIM  ,cQuantIni, cQuantFim,@lQtdeVen)};
	NEXT {||  Lj801aNe4( nTpProc,1 ,dDataINI,dDataFIM  ,cQuantIni, cQuantFim,@lQtdeVen)} ;	
    FINISH {|| .F.} 
   	@ 40,8   TO 100,148 LABEL STR0015 	OF oWizard:GetPanel(4) PIXEL //"Data       
	@ 40,152   TO 100,292 LABEL STR0047	OF oWizard:GetPanel(4) PIXEL //Quantidade Vendida
	
	@ 53,30  SAY  STR0012 		OF oWizard:GetPanel(4) 	SIZE 50,9 PIXEL //"Data Inicial
	@ 50,64  MSGET oDataIni 	VAR dDataIni   			SIZE 50,10 	OF  oWizard:GetPanel(4) VALID( If(nTpProc == 1,!EMPTY(dDataIni),.T.) , IIf(!Empty(dDataFim),dDataFim >= dDataIni,.T.)) PIXEL 
	@ 73,30 SAY  STR0013 		OF oWizard:GetPanel(4) 	SIZE 50,9 PIXEL //"Data Final 
	@ 70,64 MSGET	oDataFim  	VAR dDataFim   			SIZE 50,10 	OF  oWizard:GetPanel(4) VALID( If(nTpProc == 1,!EMPTY(dDataFim),.T.) ,  dDataFim >= dDataIni) PIXEL 	 
 	@ 53,180  SAY  STR0022 		OF oWizard:GetPanel(4) 	SIZE 50,9 PIXEL // Quantidade vendida de 
	@ 50,210  MSGET oQuantIni 	VAR cQuantIni   			SIZE 40,10 Picture "@E 999999999"	OF  oWizard:GetPanel(4)  PIXEL 				
	@ 73,180  SAY  STR0023 		OF oWizard:GetPanel(4) 	SIZE 50,9 PIXEL //ate
	@ 70,210  MSGET	oQuantFim 	VAR cQuantFim   			SIZE 40,10 Picture "@E 999999999"	OF  oWizard:GetPanel(4)  PIXEL 		

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P5	³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Quinto Painel do Wizard, nesse painel sera exibido o grid  ³±±
±±³          ³com as informacoes passadas pelo painel 3 ( prod especifico)³±±
±±³          ³ou 4 ( quantidade vendida ) caso seja painel 3 sera efetuada|±±
±±³          ³validacao de escolha de pelo menos um item do grid, se for  ³±±
±±³          ³pelo painel 4, sera possivel apenas a escolha de UM produto ³±±
±±³          ³pois sera retornado ao painel 3 com o produto selecionado no|±±
±±³          ³grid para uma nova busca na base com o produto sel no grid  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±³          ³ nTpProc: Tipo escolhido                                    ³±±
±±³				       [1] Produto especifico                             ³±±
±±³          ³         [2] quantidade vendida                             ³±±
±±³          ³ oDataIniP: Objeto Data inicial                             ³±±
±±³          ³ oDataFimP: Objeto Data Final                               ³±±
±±³          ³ lQtdeVen: qtde vendida = .F. , Prod. Especifico :T         ³±±
±±³          ³ aGrid   : Grid dos produtos encontrados                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P5(nTpProc,oDataIniP,oDataFimP,lQtdeVen,;
				  aGrid)
Default oWizard 	:= Nil
Default nTpProc 	:= 0
Default lQtdeVen 	:= .F.
Default oDataIniP 	:= Nil   
Default oDataFimP 	:= Nil
Default aGrid 		:= {}

// caso seja quantidade vendida, apos selecionar apenas um produto a rotina voltara para o
// painel 3 , com o codigo do produto e as datas desabilitadas 

		//Neste painel são exibidos os produtos que foram vendidos no período selecionado, juntamente com o produto escolhido anteriormente."
	CREATE PANEL oWizard  HEADER STR0001  MESSAGE OemToAnsi(STR0014+CHR(10)+CHR(13)+(STR0038)) ; 
	BACK {|| Lj801aNe5(nTpProc   ,2) }; 
	NEXT {|| Lj801aNe5(@nTpProc   ,1,@cCodPro,@oCodPro,@cNomePro,@oNomePro,@oDataIniP,@oDataFimP,lQtdeVen)} ;							
	FINISH {|| .F.} PANEL 

	oWizard:GetPanel(5)

	@ 03,02 SAY ""  OF oWizard:GetPanel(5) SIZE 120,8 PIXEL 
  	@ 10,10 SAY STR0018	OF oWizard:GetPanel(5) PIXEL SIZE 801,801  //Selecione o produto para relacionar na sugestão de vendas
	@ 125,30  SAY 	oAtencao 	Var cAtencao 	OF oWizard:GetPanel(5) 	SIZE 200,9 PIXEL //"Data Inicial        
	
    
    oMark := MsSelect():New(cAliasTRB,"L2_OK",,aGrid,.F.,@cMarca,{05,02,115,300},"SD2->(DbGotop())","SD2->(DbGoBottom())",oWizard:GetPanel(5))
	oMark:oBrowse:lhasMark    := .T.
	oMark:oBrowse:lCanAllmark := .F.
    
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P6	³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Sexto Painel do Wizard, nesse painel o usuario ira decidir ³±±
±±³          ³se o produto selecionado inicialmente vai ser ou nao "pai"  ³±±
±±³          ³do(s) produto(s) escolhidos posteriormente                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±³          ³ nTpSubCat: Se o produto sera pai ou nao                    ³±±
±±³				       [1] Sim                                            ³±±
±±³          ³         [2] nao                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P6(nTpSubCat)
Local oTpSubCat		:= NIL							//Verifica se havera sub categoria
Default oWizard 	:= NIL                          //Objeto do wizard

Default nTpSubCat 	:=0
		/*Selecao do processo"
	 Definir o produto selecionado como produto 'Pai'?"*/ 
	CREATE PANEL oWizard  HEADER STR0055 MESSAGE OemtoAnsi (STR0056+CHR(10)+CHR(13)+(STR0040)) ;
	BACK {|| .T. } ;
	NEXT {|| .T.} ;	
	FINISH {||  .F. } PANEL          
	@ 001,01 TO 139,300 LABEL STR0050 OF oWizard:GetPanel(6) PIXEL 
	@ 01,01  TO 139,300 LABEL	      OF oWizard:GetPanel(6) PIXEL 
	@ 20,20 RADIO oTpSubCat  		VAR nTpSubCat ITEMS STR0026,STR0027  	SIZE 70,10 PIXEL OF oWizard:GetPanel(6) ;    

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P7	³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Setimo Painel do Wizard, nesse painel o usuario ira digitar³±±
±±³          ³o nome da categoria que sera cadastrada apos selecao dos prod±±
±±³          ³dutos, o parametro nTpSubCat determina se o produto sera pai³±±
±±³          ³ou nao dos produtos selecionados no grid                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±³          ³ nTpSubCat: Se o produto sera pai ou nao                    ³±±
±±³				       [1] Sim                                            ³±±
±±³          ³         [2] nao                                            ³±±
±±³          ³ cSugestao: Nome da sugestao que sera cadastrada            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P7(nTpSubCat,cSugestao)
Local oSugestao		:= NIL														//Nome da Sugestao da Venda    
Default oWizard 	:= Nil
Default nTpSubCat 	:=0
Default cSugestao 	:=""

		//Finalização de Processo"
        //Informe o nome da Sugestão de vendas que acabou de criar.
	CREATE PANEL oWizard  HEADER STR0057  MESSAGE OemToAnsi(STR0032) ; 
	BACK {||  , .T. }; 
	NEXT {|| Lj801aNe7( 1,cSugestao,nTpSubCat)} ;							
	FINISH {|| .F.} PANEL
	
	@ 001,01 TO 139,300 LABEL 		OF oWizard:GetPanel(7) PIXEL 
   	@ 30,16  SAY  STR0058     		    	OF oWizard:GetPanel(7) PIXEL SIZE 200,80 //"Nome da Sugestão:
	@ 40,16  MSGET	oSugestao  	VAR cSugestao   SIZE 80,10 	Picture "@!" 	OF  oWizard:GetPanel(7)  PIXEL 				

	oWizard:GetPanel(7) 
	
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P8	³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Oitavo Painel do Wizard, esse painel eh parecido com o     ³±±
±±³          ³painel 5, ele eh acionado quando o usuario seleciona um     |±±
±±³          ³produto que ja tem sugestao cadastrada, que alem de ter o   ³±±
±±³          ³grid como o painel 5 tem o botao detalhes que exibe todos os³±±
±±³          ³produtos que estao associados a essa sugestao, com isso depois±
±±³          ³de selecionar um produto, sera verificado se na sugestao cadas±
±±³          ³trada existe um produto pai, caso nao exista, sera acrescenta±±
±±³          ³do o novo produto, caso tenha, sera envidado ao proximo painel±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±³          ³ nTpProc: Tipo escolhido                                    ³±±
±±³			 |	       [1] Produto especifico                             ³±±
±±³          ³         [2] quantidade vendida                             ³±±
±±³          | oDBTree: Objeto de Exibicao em forma hierarquica "arvore"  ³±±
±±³          ³ aGrid   : Grid dos produtos encontrados                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P8(nTpProc,aGrid,cCodPro)
Local oButton  := NIL                               // Objeto para Botao
Local cCateg := AllTrim(LJ801aRetC(cCodPro))// Retorna a categoria do respectivo produto 
Default oWizard 	:= Nil
Default nTpProc 	:=0
Default aGrid 		:={}
  
	//Neste painel são exibidos os produtos que foram vendidos no período selecionado, juntamente com o produto escolhido no painel anterior"
	CREATE PANEL oWizard  HEADER STR0001  MESSAGE OemToAnsi(STR0014+CHR(10)+CHR(13)+(STR0038)) ; 
	BACK {|| Lj801aNe3(@nTpProc  ,2,@cCodPro)};
	NEXT {|| Lj801aNe8(@nTpProc  ,1,@cCodPro)} ;
	FINISH {|| .F.} PANEL 

	oWizard:GetPanel(8)
	@ 03,02 SAY ""  OF oWizard:GetPanel(8) SIZE 120,8 PIXEL 
  	@ 10,10 SAY ""	OF oWizard:GetPanel(8) PIXEL SIZE 801,801  // Selecione o produto para relacionar na sugestão de vendas
	@ 125,30  SAY 	oAtencao 	Var cAtencao 	OF oWizard:GetPanel(8) 	SIZE 200,9 PIXEL //"Data Inicial

	aAdd(aGrid,{"L2_PORCENT"	,,STR0009," "})							 	//Porcentagem
	oMark := MsSelect():New(cAliasTRB,"L2_OK",,aGrid,.F.,@cMarca,{05,02,115,300},"SD2->(DbGotop())","SD2->(DbGoBottom())",oWizard:GetPanel(8))
	oMark:oBrowse:lhasMark    := .T.
	oMark:oBrowse:lCanAllmark := .F.

		oButton:=tButton():New(120,260,STR0045,oWizard:GetPanel(8),{||oWizard:GetPanel(8):End()},30,12,,,,.T.)  //Detalhes
		oButton:bAction := {|| Ljc801ExGr()}       
     
Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P9	³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Nono  Painel do Wizard, esse painel exibe um DbTree com a  ³±±
±±³          ³estrutura da sugestao ja existente, perguntando ao usuario  |±±
±±³          ³em que hierarquia deseja inserir o produto selecionado, se  ³±±
±±³          ³eh na mesma estrutura do produto pai ou na sub sugestao dos ³±±
±±³          ³produtos filhos, a resposta sera obtida pelo retorno da     ³±±
±±³          ³funcao GetCargo() do DbTree. No DbTree serao exibidas tanto as±
±±³          ³sugestoes como o produtos que fazem parte delas             |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±³          ³ nTpProc: Tipo escolhido                                    ³±±
±±³			 |	       [1] Produto especifico                             ³±±
±±³          ³         [2] quantidade vendida                             ³±±
±±³          | oDBTree: Objeto de Exibicao em forma hierarquica "arvore"  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P9(nTpProc) 
Default oWizard 	:= Nil
Default nTpProc 	:=0

	//Neste painel são exibidos os produtos que foram vendidos no período selecionado, juntamente com o produto escolhido no painel anterior"
	CREATE PANEL oWizard  HEADER STR0001  MESSAGE OemToAnsi(STR0053) ; //Selecione em qual categoria o produto selecionado no grid deve ser inserido
	BACK {|| Lj801aNe8(nTpProc  ,2,@cCodPro)};
	NEXT {|| Lj801aNe9(nTpProc ,3,,oDBTree:GetCargo())} ;										
	FINISH {|| .F.} PANEL 
	oWizard:GetPanel(9)
	@ 03,02 SAY ""  OF oWizard:GetPanel(9) SIZE 120,8 PIXEL 
  	@ 10,10 SAY ""	OF oWizard:GetPanel(9) PIXEL SIZE 801,801  
	@ 125,30  SAY 	oAtencao 	Var cAtencao 	OF oWizard:GetPanel(9) 	SIZE 200,9 PIXEL   

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801P10 |Autor  ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Decimo Painel do Wizard, indica a finalizacao do wizard    ³±±
±±³          ³estrutura da sugestao ja existente, perguntando ao usuario  |±±
±±³          ³em que hierarquia deseja inserir o produto selecionado, se  ³±±
±±³          ³eh na mesma estrutura do produto pai ou na sub sugestao dos ³±±
±±³          ³produtos filhos, a resposta sera obtida pelo retorno da     ³±±
±±³          ³funcao GetCargo() do DbTree. No DbTree serao exibidas tanto as±
±±³          ³sugestoes como o produtos que fazem parte delas             |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ oWizard: Objeto do Wizard                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lja801P10()
Default oWizard 	:= Nil 

	CREATE PANEL oWizard HEADER STR0025 MESSAGE OemToAnsi(STR0059) ; 
	BACK {|| .F. } ; 
	NEXT {||  .F. } ;	 
	FINISH {|| .T.} PANEL
     	@ 45,16  SAY  STR0024 		OF oWizard:GetPanel(10) 	SIZE 150,60 PIXEL //"	Os produtos foram gravados com sucesso
	ACTIVATE WIZARD oWizard CENTERED  WHEN {||.T.} VALID {||.T.}

Return
 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801ExCa³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Exibte a categoria selecionada ao clicar no dbtree         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ cCargo: String retornada ao clicar no dbtree               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/         
Function Lja801ExCa(cCargo)  
Local cArea := "LJVC" 	//Alias temporario                
                        
cQuery:= "SELECT  ACU_DESC, ACU_CODPAI, ACU_COD FROM " +   RetSQLName("ACU") + " WHERE  D_E_L_E_T_ = ' '  AND ACU_COD IN( "
cQuery+= "SELECT ACV_CATEGO FROM "+   RetSQLName("ACV") + " WHERE ACV_CATEGO = '" + cCargo + "' AND D_E_L_E_T_ = ' ' )"
LJa801ExQu(cArea,@cQuery)
cCondPai := AllTrim((cArea)->ACU_DESC)     
cCodProP := Alltrim((cArea)->ACU_COD)
cDescP   := Alltrim((cArea)->ACU_DESC)

If cCondPai <> '' 
	cAtencao := STR0051 + cCodProp + " - " + cDescP //"A categoria seleccionada e : "
Else
	cAtencao:=""
EndIf
oAtencao:Refresh()

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Ljc801ExGr³ Autor ³ Vendas Cliente        ³ Data ³08.11.10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Exbibe grid de produtos que ja foram relacionados ao produto±±
±±³          ³ selecionado no painel 3                                     ±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Parametros³ cProduto: Produto necessario para desc sugestao de Vendas  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ljc801ExGr(cProduto) 
Local oOK 	:= LoadBitmap(GetResources(),'br_verde')  //Botao verde somente para exibicao

DEFINE MSDIALOG oDlg FROM 0,0 TO 310,402 PIXEL TITLE STR0046      //Produtos Relacionados 

oBrowse := TWBrowse():New( 5 , 5, 195,  130,,;
		{'',STR0028,STR0029},{20,40,40}, oDlg, ,,,,;
		{||},,,,,,,.F.,,.T.,,.F.,,, )
  
oBrowse:SetArray(aProdCad)    
oBrowse:bLine := {||{;
If(aProdCad[oBrowse:nAt,01],oOK,oOK),;
	aProdCad[oBrowse:nAt,02],;
	aProdCad[oBrowse:nAt,03]} }

ACTIVATE MSDIALOG oDlg CENTERED
oDlg:=Nil
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |Lja801Desc³ Autor ³ Vendas Cliente        ³ Data ³27/10/10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna a descricao da categoria em que o produto passado  ³±±
±±³          ³ como parametro eh pai                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cProd: Produto selecionado                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJa801Desc(cProd)
Local cCateg	:=""		//variavel de retorno     
Local cQuery	:=""       // Variavel de consulta
Local cArea 	:="LJVC" 	//Alias temporario

cQuery:= "SELECT  ACU_DESC FROM " +   RetSQLName("ACU") + " WHERE  D_E_L_E_T_ = ' '  AND ACU_COD IN( "
cQuery+= "SELECT ACV_CATEGO FROM "+   RetSQLName("ACV") + " WHERE ACV_CODPRO = '" + cProd + "' AND D_E_L_E_T_ = ' ')"
LJa801ExQu(cArea,@cQuery)
cCateg := AllTrim((cArea)->ACU_DESC)
(cArea)->(DbCloseArea())

Return cCateg
 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |LJa801ExQu³ Autor ³ Vendas Cliente        ³ Data ³27/110/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao que executa querys                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cArea   : Arquivo temporario                               ³±±
±±³          ³ cQuery  : Query que vai ser executada                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJa801ExQu(cArea,cQuery) 
Default cArea 	:="LJVC"
Default cQuery	:=""
If Select(cArea) > 0
	(cArea)->(DbCloseArea())
EndIf
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cArea,.F.,.T.)

Return                                                                                 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |LJa801HDat³ Autor ³ Vendas Cliente        ³ Data ³27/10/10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Habilita ou desabilita as datas na sugestão de vendas      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oDataini: Data inicial                                     ³±±
±±³          ³ oDataFim: Data final                                       ³±±
±±³          ³ lHabilita: Verifica se irá habilitar ou desabilitar as datas±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJa801HDat(oDataini,oDataFim,lHabilita)
Default oDataini 	:=Nil	
Default oDataFim 	:=Nil   
Default  lHabilita 	:=.F. 
If lHabilita                            
	oDataini:Enable()
	oDataFim:Enable()
Else
	oDataini:Disable()
	oDataFim:Disable()
EndIf

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |LJa801GRAV³ Autor ³ Vendas Cliente        ³ Data ³22/10/10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de Gravacao da sugestao de vendas                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet, .T. para gravado com sucesso                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cCodPro: Codigo do Produto                                 ³±±
±±³          ³ cSugestao : Nome da sugestao de Vendas que será cadastrada ³±±
±±³          ³ nTipo  : Deseja considerar o produto selecionado no painel ³±±
±±³          ³ 3 como produto pai ?                                       ³±±
±±³			 |	       [1] Sim                                            ³±±
±±³          ³         [2] Nao                                            ³±± 
±±³          ³ cCategoria : Caso seja inclusao de um produto a uma sugestao±±
±±³          ³ existente, caso o array aProdCad tiver informacao indica que±±
±±³          ³ sera inclusao de um prod. em uma categoria existente, caso |±±
±±³          ³ contrario ser de um prod. em uma categoria existente, caso |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJa801GRAV(cCodPro,cSugestao,nTipo,cCategoria)
Local lRet 			:= .T.       				//Variavel de Retorno
Local cCateg        := ""                           	//Categoria
Local cAlias 	    := cAliasTrb               // Alias temporario
Local lIntPOS 		:= (SuperGetMV("MV_LJSYNT",,"0") == "1")
Local lACV_POSFLG	:= ACV->(FieldPos("ACV_POSFLG")) > 0
Local lACU_POSFLG	:= ACU->(FieldPos("ACU_POSFLG")) > 0
Default	cCodPro 	:=""
Default cSugestao 	:=""
Default nTipo		:=0
Default cCategoria	:=""

//Caso não tenha informacoes no aProdCad sera inclusao caso contrario sera alteracao
If Len(aProdCad)==0       // Somente quando for inclusao de item que nao tem ja sugestao cadastrada esse array estara zerado
	//Inclusao
	DbSelectArea("ACU")
	cCateg := AllTrim(LJ801aRetC())       // Retorna a primeira categoria disponivel
	Reclock("ACU",.T.)
    
	REPLACE	ACU->ACU_FILIAL	WITH  xFilial("ACU")
	REPLACE	ACU->ACU_COD 	WITH cCateg
	REPLACE	ACU->ACU_DESC	WITH AllTrim(cSugestao)
	REPLACE	ACU->ACU_MSBLQL	WITH "2"
	If lIntPOS .AND. lACU_POSFLG
		REPLACE ACU->ACU_POSFLG WITH "1"
	EndIf
	ACU->(MsUnlock())
	
	Reclock("ACV",.T.)

	REPLACE	ACV->ACV_FILIAL		WITH  xFilial("ACU")
	REPLACE	ACV->ACV_CATEGO 	WITH  cCateg
	REPLACE	ACV->ACV_CODPRO		WITH  AllTrim(cCodPro)
	REPLACE	ACV->ACV_SUVEND		WITH  "1"
	If lIntPOS .AND. lACV_POSFLG
		REPLACE ACV->ACV_POSFLG WITH "1"
	EndIf
	ACV->(MsUnlock())

	If nTipo == 1    
		Reclock("ACU",.T.)        
	    
		REPLACE	ACU->ACU_FILIAL	WITH xFilial("ACU")
		REPLACE	ACU->ACU_CODPAI	WITH cCateg    
		cCateg := Soma1(cCateg)
		REPLACE	ACU->ACU_COD   	WITH AllTRIM(cCateg)
		REPLACE	ACU->ACU_DESC	WITH "Filho " +  AllTrim(cSugestao)
		REPLACE	ACU->ACU_MSBLQL	WITH "2" 
		If lIntPOS .AND. lACU_POSFLG
			REPLACE ACU->ACU_POSFLG WITH "1"
		EndIf
		ACU->(MsUnlock())
	EndIf	

Else
	// ALTERACAO
	cCateg := AllTrim(LJ801aRetC(cCodPro))// Retorna a categoria do respectivo produto
	
EndIf

DbSelectArea(cAlias)
(cAlias)->(DbGoTop())

While (cAlias)->( !Eof() )
   If !Empty(AllTrim((cAlias)->L2_OK))
		Reclock("ACV",.T.)
   		REPLACE	ACV->ACV_FILIAL		WITH xFilial("ACU")
   		REPLACE	ACV->ACV_CATEGO 	WITH AllTrim(cCateg)
		REPLACE	ACV->ACV_CODPRO		WITH (cAlias)->L2_PRODUTO
		REPLACE	ACV->ACV_SUVEND		WITH "1"
		If lIntPOS .AND. lACV_POSFLG
			REPLACE ACV->ACV_POSFLG WITH "1"
		EndIf
		ACV->(MsUnlock())
   EndIf
   (cAlias)->(DbSkip())
End

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  |Lj801aDescP ºAutor  ³ Vendas Cliente     º Data ³  26/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Devolve o nome do produto baseado no codigo que foi        º±±
±±º          ³ digitado.                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cCodPro - Codigo do Produto                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aDescP(cCodPro)
Local cDesc 		:= CRIAVAR("B1_DESC",.F.)                //Nome do Produto
Local aArea			:= GetArea()                             //Area atual para restaurar no final da funcao
Default cCodpro 	:=""                             			

If !Empty(cCodPro) 
	DbSelectArea("SB1")
	If SB1->(DbSeek(xFilial("SB1")+cCodPro))
		cDesc := SB1->B1_DESC
	Endif	
Endif

RestArea(aArea)

Return cDesc

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aNe2    ºAutor  ³ Vendas Cliente    º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao executada quando eh clicado no botao avancar do pai- º±±
±±º          ³nel 2 o parametro principal eh nTpProc que vai determinar   º±±
±±º          ³o proximo painel                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ oWizard: Wizard atual                                      º±±
±±º          ³ nTpProc: tipo processo 1 prod especifico 2 quant vendida   º±±
±±º          ³ nAvanc: Verifica se está avancando ou voltando             º±±
±±º          ³ oCodPro: Codigo do produto                                 º±±
±±º          ³ oDataIni: Data inicial                                     º±±
±±º          ³ oDataFim: Data final                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   |  Retorna Verdadeiro caso efetuado com sucesso              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Lj801aNe2(nTpProc, nAvanc,oCodPro,oDataIni,;
                   oDataFim) 
Local lRet 			:= .T.     // Variavel de retorno
Default oWizard 	:=Nil
Default nTpProc 	:=0 
Default nAvanc 		:=0  
Default oCodPro 	:=Nil
Default oDataIni 	:=Nil   
Default oDataIni 	:=Nil		
If nAvanc  ==1
	If nTpProc ==1       // Produto especifico, habilita os campos de data para alteracao 
 		oWizard:SetPanel(2)
	   	oDataIni:Enable()
		oDataFim:Enable()
		oCodPro:Enable()
		oCodPro:Refresh()
		oDataIni:Refresh()
		oDataFim:Refresh()
		oChkData:bSetGet 	:= {|| .T. }
		oChkData:Disable()
		oChkData:Refresh()	
 	Else  //Quantidade vendida, pula um painel
     	oWizard:SetPanel(3)    		
    EndIf
EndIf

Return lRet 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aNe3    ºAutor  ³ Vendas Cliente    º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao que retorna os os produtos de acordo com as informacoes±
±±º          ³passadas no painel 3                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ oWizard: Wizard atual                                      º±±
±±º          ³ nTpProc: tipo processo 1 prod especifico 2 quant vendida   º±±
±±º          ³ nAvanc: Verifica se está avancando ou voltando             º±±
±±º          ³ cCodProd: Codigo do produto                                º±±
±±º          ³ dDataIni: Data inicial                                     º±±
±±º          ³ dDataFIM: Data final                                       º±±
±±º          ³ nPorceIni: Porcentagem inicial                             º±±
±±º          ³ lQtdeVen : Indifica se eh qtde vendida ou prod. especifico º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aNe3(nTpProc	, nAvanc    ,  cCodProd,	dDataIni,;
                   dDataFIM , nPorceIni , nPorceFim,	lQtdeVen ) 
Local lRet 			:= .T.       //Variavel de retorno
Default oWizard 	:=Nil
Default nTpProc 	:=0 
Default nAvanc 		:=0
Default cCodProd 	:=""
Default dDataini 	:=""
Default dDataFIM	:=""
Default nPorceIni 	:=0
Default nPorceFim 	:=0
Default lQtdeVen	:=.T.
lQtdeVen:=.T.                

If nAvanc  ==1
	If Empty(cCodProd) 
		Alert(STR0016)//"O codigo do produto deve ser informado
		lRet:= .F.
	Else
 		DbSelectArea("SB1")
        SB1->(DbSetOrder(1))
  	    If !SB1->(DbSeek(xFilial("SB1")+cCodProd))
	      //"O Produto nao existe
	      	Alert(STR0016)
	    	lRet  := .F.       
	    ElseIf Lj801cFilP(@cCodProd,DTOS(dDataIni),DTOS(dDataFIM),1, ,,nPorceIni, nPorceFim )  // efetua filtro de acordo com os dados informados
     	   	cAtencao := OemToAnsi(STR0028) + AllTrim(@cCodPro) + OemToAnsi(STR0042);// O produto x foi vendido y vezes no periodo
     	   	 +  AllTrim(STR(@nQtdeVend)) + OemToAnsi(STR0043)   
			oAtencao:Refresh()
			If Len(aProdCad)>0   // Caso esse array tenha informações indica que ja existe sugestao de vendas para esse produto, ou seja, sera atleracao
			  	MsgAlert(STR0044)//Este produto já tem sugestão de vendas relacionada, para mais informarções pressione o botão Detalhes "
				oWizard:SetPanel(7)
			Else
			 	oWizard:SetPanel(4)
			EndIf
		Else
			MsgAlert(STR0030) //Não foi encontrado nenhum produto com a seleção informada!
			lRet := .F.
			oWizard:SetPanel(3)				   
   		EndIf
	Endif
Else
	oWizard:SetPanel(3)	

	
EndIf
Return lRet 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aNe4    ºAutor  ³ Vendas Cliente    º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Esse painel filtra os produtos de acordo com a quantidade   º±±
±±º          ³vendida                                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ oWizard: Wizard atual                                      º±±
±±º          ³ nTpProc: tipo processo 1 prod especifico 2 quant vendida   º±±
±±º          ³ nAvanc: Verifica se está avancando ou voltando             º±±
±±º          ³ dDataIni: Data inicial                                     º±±
±±º          ³ dDataFIM: Data final                                       º±±
±±º          ³ nQuantIni: Quantidade inicial                              º±±
±±º          ³ nQuantFin: Quantidade Final                                º±±
±±º          ³ lQtdeVen : Indifica se eh qtde vendida ou prod. especifico º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aNe4( nTpProc  , nAvanc    ,dDataIni	,dDataFIM,;
                    nQuantIni, nQuantFin ,lQtdeVen ) 
Local lRet 			:= .T.   //Variavel de retorno
Default oWizard 	:=Nil
Default nTpProc 	:=0 
Default nAvanc 		:=0
Default dDataini 	:= dDataBase
Default dDataFIM	:= dDataBase
Default nQuantIni 	:=0
Default nQuantFin 	:=0
Default lQtdeVen	:=.F.
lQtdeVen:=.F.
If nAvanc  ==1
	If Empty(nQuantIni)  .OR.  Empty(nQuantFin) 
	 	MsgAlert(STR0017)		//Informe a quantidade vendida
		lRet := .F.
	ElseIf Lj801cFilP(cCodPro,DTOS(dDataIni),DTOS(dDataFIM),2,nQuantIni,nQuantFin)   // efetua o filtro por quantidade vendida
	    cAtencao:=""
	    oAtencao:Refresh()
	Else
		MsgAlert(STR0030)    //Não foi encontrado nenhum produto com a seleção informada!
		lRet := .F.
	EndIf   
Else
oWizard:SetPanel(3)  
  
EndIf
Return lRet 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aNe5    ºAutor  ³ Vendas Cliente    º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao que valida se o produto do grid  foi selecionado ou nao±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ oWizard: Wizard atual                                      º±±
±±º          ³ nTpProc: tipo processo 1 prod especifico 2 quant vendida   º±±
±±º          ³ nAvanc: Verifica se está avancando ou voltando             º±±
±±º          ³ cCodProd: Codigo do produto                                º±±
±±º          ³ oCodPro:Objeto codigo do produto                           º±±
±±º          ³ cNomePro: Nome do produto                                  º±±
±±º          ³ oNomePro: Objeto nome do produto                           º±±
±±º          ³ oDataIni: Objeto data inicial                              º±±
±±º          ³ oDataFim: Objeto data Final                                º±±
±±º          ³ cAliasTrb: Alias arquivo temporario                        º±±
±±º          ³ lQtdeVen: Verifica se o grid ant. foi o qtde vendida       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aNe5(nTpProc	 , 	nAvanc	 ,cCodPro ,oCodPro,;
                   cNomePro  ,	oNomePro ,oDataIni,oDataFim,;
                   lQtdeVen )                  
                   
Local lRet           := .F.    		//Variavel de retorno
Local lChkData    := .F.		//Define se o componente ochkData estara marcado ou nao 
Local nTipo          := 0           //Variavel de controle de Tipo
Default oWizard   :=Nil
Default nTpProc   :=0 
Default nAvanc     :=0
Default cCodPro   :=""
Default oCodPro   := Nil
Default cNomePro :=""
Default oNomePro := Nil
Default oDataIni     :=Nil
Default oDataFim   :=Nil
Default lQtdeVen   :=.T. 

Do Case
    Case nAvanc  ==1     				// Indica que veio do painel numero 3, e que vai validar se o usuario selecionou ou nao o produto do grid
    	If lQtdeVen
       		nTipo :=1	  
    	Else
       		nTipo :=2    		
    	EndIf
		If Lj801aSelPr(nTipo,@cCodPro) // valida se foi selecionado o produto
			If nTipo ==2       					// caso seja  por quantidade vendida ira desabilitar alguns campos
			  	cNomePro:=Lj801aDescP(cCodPro)
				oNomePro:Refresh()
				oDataIni:Disable()
				oDataFim:Disable()
				oCodPro:Disable()
				oCodPro:Refresh()
				oDataIni:Refresh()
				oDataFim:Refresh()
			  	oWizard:SetPanel(2)
				oChkData:Enable()
				oChkData:bSetGet 	:= {|| lChkData }
				oChkData:bLClicked	:= {|| lChkData:=!lChkData,LJa801HDat(oDataIni,oDataFim,lChkData) }
				oChkData:Refresh()                     
			Else                                                         
			
				oCodPro:Refresh()  
				// fazer novo painel
		
			EndIf
			lRet :=.T.
		EndIf
	Case nAvanc  ==2 //está voltando e deve ir ao painel de selecionar por produto especifico ou quantidade vendida
		 oWizard:SetPanel(2)

Endcase

Return lRet 
 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aNe7    ºAutor  ³ Vendas Cliente    º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida o nome da sugestao de vendas e chama funcao de gravacao±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ oWizard: Wizard atual                                      º±±
±±º          ³ nAvanc: Verifica se está avancando ou voltando             º±±
±±º          ³ cSugestao:Nome da sugestao de vendas que sera cadastrada   º±±
±±º          ³ nTipoCateg: Verifica se vai usuar sub categoria ou nao     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aNe7(nAvanc,cSugestao,nTipoCateg ) 
Local lRet 			:= .T. 		//Variavel de Retorno

Default oWizard 	:=Nil
Default nAvanc 		:=0 
Default cSugestao 	:= ""
Default nTipoCateg 	:=0        

If nAvanc  ==1
	If Empty(cSugestao)
	 	MsgAlert(STR0031)		//Informe a Sugestão
		lRet := .F.		
	ElseIf LJa801GRAV(cCodPro,cSugestao,nTipoCateg)  // Funcao de gravacao a sugestao de vendas
		oWizard:SetPanel(9)
	Else
		lRet := .F.			
	EndIf
EndIf

Return lRet

/*/                        
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aNe8    ºAutor  ³ Vendas Cliente    º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao que valida se o produto do grid  foi selecionado ou nao±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ oWizard: Wizard atual                                      º±±
±±º          ³ nTpProc: tipo processo 1 prod especifico 2 quant vendida   º±±
±±º          ³ nAvanc: Verifica se está avancando ou voltando             º±±
±±º          ³ cCodProd: Codigo do produto                                º±±
±±º          ³ oDBTree: Objeto arvore                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aNe8(nTpProc, nAvanc,cCodPro)
Local cCateg := AllTrim(LJ801aRetC(cCodPro))// Retorna a categoria do respectivo produto 
Local lRet := .F.    		//Variavel de retorno 
Default oWizard	:=Nil
Default nTpProc		:=0
Default nAvanc		:=0
Default cCodPro		:=""

If nAvanc  ==1  //Caso seja inclusao de um produto em uma sugestao de vendas existente, sera considerado alteracao e irá para o ultimo painel
	If Lj801aSelPr(nTpProc,@cCodPro)    
		If Lj801aSuCa(cCateg)
				cAtencao:=""
			    oAtencao:Refresh()
		Else
			If LJa801GRAV(cCodPro, , ,AllTrim(LJ801aRetC(cCodPro))) 
				oWizard:SetPanel(9)
			EndIf
	  	EndIf
	  	lRet :=.T.
	EndIf
Else  //Caso seja retorno ao Painel, exibe novamente Grid com os produtos que podem ser associados ao Principal
	Lj801aSelPr(nTpProc,@cCodPro)
  	oWizard:SetPanel(8)   	      	 
  	
	oDBTree:Reset()

	cAtencao := OemToAnsi(STR0028) + AllTrim(cCodPro) + OemToAnsi(STR0042);// O produto x foi vendido y vezes no periodo
     	   	 +  AllTrim(STR(@nQtdeVend)) + OemToAnsi(STR0043)   
	oAtencao:Refresh()
	
EndIf

Return lRet 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aNe9    ºAutor  ³ Vendas Cliente    º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica se a categoria foi selecionada                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ oWizard: Wizard atual                                      º±±
±±º          ³ nTpProc: tipo processo 1 prod especifico 2 quant vendida   º±±
±±º          ³ nAvanc: Verifica se está avancando ou voltando             º±±
±±º          ³ cCodProd: Codigo do produto                                º±±
±±º          ³ cCateg: Sugestao  nao formatada                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aNe9(nTpProc, nAvanc,cCodPro,cCateg) 
Local lRet 				:=.F.		//Variavel de retorno
Local cQuery         := ""                //Variavel de consulta
Local cArea	        :="LJVC"    //variavel de Area temporaria para consulta           
Default oWizard		:=Nil
Default nTpProc		:=0
Default nAvanc		:=0
Default cCodPro		:=""       

cQuery := "SELECT V.ACV_CODPRO, U.ACU_CODPAI FROM "+ RetSqlName("ACU") +" U LEFT JOIN "+ RetSqlName("ACV") +" V "
cQuery += "ON V.ACV_CATEGO = U.ACU_COD WHERE U.ACU_COD = '"+ cCateg +"' AND V.ACV_SUVEND = '1' "
cQuery += "AND V.D_E_L_E_T_ = ' ' AND U.D_E_L_E_T_ = ' '"
LJa801ExQu(cArea,@cQuery)                        

cCodPro    := Alltrim((cArea)->ACV_CODPRO)    

If cAtencao = ''                                                                             
   	MsgAlert(STR0060)
Else 
  	lRet:=	LJa801GRAV(cCodPro,,,cCateg)
EndIf
                     
Return lRet 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aNe10   ºAutor  ³ Vendas Cliente    º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Responsavel por limpar todos os campos do wizard funcao eh  º±±
±±º          ³executada no ultimo painel                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ oWizard: Wizard atual                                      º±±
±±º          ³ cCodProd: Codigo do produto                                º±±
±±º          ³ oCodPro:Objeto codigo do produto                           º±±
±±º          ³ oQtdeIni: Objeto Quantidade inicial                        º±±
±±º          ³ oQtdeFim: Objeto Quantidade Final                          º±±
±±º          ³ dDataIni: Data inicial                                     º±±
±±º          ³ dDataFIM: Data final                                       º±±
±±º          ³ nQtdeIni: Quantidade inicial                               º±±
±±º          ³ nQtdeFim: Quantidade Final                                 º±±
±±º          ³ cSugestao:Nome da sugestao de vendas que sera cadastrada   º±±
±±º          ³ oSugestao:Objeto  sugestao de vendas que sera cadastrada   º±± 
±±º          ³ oDataIni: Objeto data inicial                              º±±
±±º          ³ oDataFim: Objeto data Final                                º±±
±±º          ³ nPercentIni:Nome da sugestao de vendas que sera cadastrada º±±
±±º          ³ oPercentIni:Objeto  sugestao de vendas que sera cadastrada º±± 
±±º          ³ nPercentFim: Objeto data inicial                           º±±
±±º          ³ oPercentFim: Objeto data Final                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aNe10() 
Local lRet := .T.        //Variavel de Retorno                                                                                                             

oWizard:SetPanel(1)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aSuCa   ºAutor  ³ Vendas Cliente    º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica se o produto selecionado tem ou nao sub categorias º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cCateg: Categoria do produto                               º±±
±±º          ³ oDBTree: Objeto de de exibicao das categorias em forma     º±±
±±º          ³ hierarquica                                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aSuCa(cCateg)  
Local cQuery        := ""				//Query que sera executada
Local lRet 			:=.F.			// variavel de retorno
Local cCat          := ""        		// Categoria que sera inserida no dbtree
Local cArea 		:= "LJVC"		// tabela temporaria
Local cBmp1 		:= "PMSEDT3"    // icone das categorias
Local cBmp2 		:= "PMSDOC"     // icone dos produtos
Default cCateg 		:=""                                                                                          

oDBTree := dbTree():New(10,10,95,300,oWizard:GetPanel(9),{||Lja801ExCa(oDBTree:GetCargo())},,.T.)	                     
oDBTree:PTRefresh()
  
cQuery:= "SELECT ACU_COD, ACU_DESC, ACU_CODPAI FROM "+ RetSqlName("ACU") + " WHERE ACU_CODPAI ='"+cCateg+"'  OR ACU_COD ='" +cCateg + "' "
cQuery+= " AND D_E_L_E_T_ = ' '"
LJa801ExQu(cArea,@cQuery)

While !(cArea) ->(EOF())
	cCat  := Alltrim((cArea)->ACU_COD) + " - " + (cArea)->ACU_DESC
   	If(AllTrim(cCateg)==AllTrim((cArea)->ACU_COD)) 
		oDBTree:AddTree(cCat ,.T.,cBmp1,cBmp1,,,(cArea)->ACU_COD)
		cQuery :=" SELECT DISTINCT C.ACV_CATEGO,C.ACV_CODPRO,P.B1_DESC FROM  "+ RetSqlName("ACV") + " C LEFT JOIN "+ RetSqlName("SB1") + " P "
		cQuery += "ON P.B1_COD = C.ACV_CODPRO WHERE C.ACV_CATEGO ='"+cCateg+"' "
		cQuery += "AND C.D_E_L_E_T_ = ' ' AND P.D_E_L_E_T_ = ' '"
		LJa801ExQu("LJPAI",@cQuery)

		While !LJPAI->(EOF())
			cCat:= AllTrim(LJPAI->ACV_CODPRO) + " - "+ (LJPAI->B1_DESC)
			oDBTree:AddItem(cCat,(LJPAI->ACV_CODPRO),cBmp2,,,2)
			LJPAI ->(DBSKIP())
		End
		LJPAI->(DbCloseArea())    
		
	Else
		oDBTree:AddTree(cCat ,.F.,cBmp1,cBmp1,,,(cArea)->ACU_COD)
		cQuery :=" SELECT DISTINCT C.ACV_CATEGO,C.ACV_CODPRO,P.B1_DESC FROM "+ RetSqlName("ACV") + " C LEFT JOIN "+ RetSqlName("SB1") + " P "
		cQuery += "ON P.B1_COD = C.ACV_CODPRO WHERE C.ACV_CATEGO ='"+(cArea)->ACU_COD+"' "  
		cQuery += "AND C.D_E_L_E_T_ = ' ' AND P.D_E_L_E_T_ = ' '"
		LJa801ExQu("LJPAI",@cQuery)                                                                                  
		
		While !LJPAI->(EOF())
			cCat:= Alltrim((LJPAI->ACV_CODPRO)) + " - "+ (LJPAI->B1_DESC)
  			oDBTree:AddTreeItem(cCat, cBmp2,,(LJPAI->ACV_CODPRO))
		
			LJPAI ->(DBSKIP())
		End
		
		lRet:=.T.	
		LJPAI->(DbCloseArea())  
	EndIf 
	(cArea) ->(DBSKIP()) 
End
oDBTree:EndTree()
oDBTree:PTRefresh()
(cArea)->(DbCloseArea())

Return  lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801SelPr  ºAutor  ³ Vendas Cliente     º Data ³ 28/10/2010º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica os produtos selecionados pela quantidade vendida  º±±
±±º          ³ ou produto especifico                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³nTipo - por prod especifico ou qtdade vendida               º±±
±±º          ³cprodSel - retorna o produto selecionado                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aSelPr(nTipo,cProdSel)

Local lRet 			:= .F.			//Variavel de retorno  
    
Default nTipo 		:=0
Default cProdSel 	:=""

DbSelectArea(cAliasTRB)
(cAliasTRB)->(DbGoTop())

While (cAliasTRB)->( !Eof() )
    If !Empty(AllTrim((cAliasTRB)->L2_OK))
    	If nTipo ==1         // caso seja por produto especifico sera necessario selecionar pelo menos um produto
    		lRet :=.T.
    		Exit
    	Else// caso seja por quantidade vendida sera necessario selecionar APENAS  um produto
    		If lRet
	    		lRet :=.F.
	    		Exit		    		     
    		EndIf
    		lRet :=.T.
    		cProdSel := (cAliasTRB)->L2_PRODUTO
    	Endif
    EndIf
	(cAliasTRB)->(DbSkip())
End
If !lRet
	If nTipo ==1
		MsgAlert(STR0033)
	Else
		MsgAlert(STR0034)
	EndIf

	/*/
É necessário informar pelo menos um produto.
Selecione apenas um produto.
/*/

EndIf
(cAliasTRB)->(DbGoTop())
Return lRet              

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801cFilP  ºAutor  ³ Vendas Cliente     º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Filtra os produtos de acordo com a selecao informada ao    º±±
±±º          ³ usuario                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cCodProduto - Codigo do produto                             º±±
±±º          ³cDataInicial - Data inicial da consulta                     º±±
±±º          |cDataFinal - Data final da consulta                         º±±
±±º          |nTipo - Tipo - se e por produto especifico ou qtde vendida  º±±
±±º          ³nQuantIni - Quantidade inicial                              º±±
±±º          ³nQuantFim - Quantidade Final                                º±±
±±º          ³nPercenIni - Porcentagem inicial                            º±±
±±º          ³nPercenFim - Porcentagem final                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801cFilP (cCodProduto, cDataInicial, cDataFinal, nTipo,;
                     nQuantIni  , nQuantFim    , nPercenIni, nPercenFim)   

Local cOrcament 					:=  RetSQLName("SL2")			//Verificar qual eh a tabela de orcamento
Local cArea							:= "LJVC"  						//Alias temporario
Local lRet						    :=.F.							//Variável de retorno
Local cQuerySel   					:=""								//Query
Local nPos                          :=0                              //variavel de controle de posicao
Default cCodProduto 				:=""
Default cDataInicial 				:="20100101"
Default cDataFinal	 				:="20101231"
Default nTipo 						:= 0
Default	nPercenIni 					:=0
Default	nPercenFim 					:=0
Default nQuantIni 					:=0
Default nQuantFim 					:=0


If Empty(nPercenIni)
	nPercenIni = 00.01
Endif
If Empty(nPercenFim)
	nPercenFim = 100
Endif

aProdCad:={}
If ntipo == 1     // caso seja produto especifico
	Lj801cVlPr(cCodProduto)     // verifica se o produto informado ja tem sugestao cadastrada
	nQtdeVend:=	Lj801aLocQ(cCodProduto, cDataInicial, cDataFinal)   // verifica a quantidade vendida do produto no periodo
	cQuerySel := "SELECT DISTINCT L2_PRODUTO, SUM(L2_QUANT) L2_QUANT, '.F.' L2_OK,   	L2_DESCRI FROM "  +  cOrcament
	cQuerySel += " WHERE  L2_NUM IN (  SELECT L2_NUM FROM " + cOrcament + " 	WHERE D_E_L_E_T_ = ' '  AND L2_PRODUTO = '" +  cCodProduto + "' )"
  	cQuerySel += " AND  L2_PRODUTO <> '" + AllTrim(cCodProduto) + "' AND L2_EMISSAO BETWEEN '" + cDataInicial  + "' AND '" + cDataFinal + "' "  
  	cQuerySel += " AND D_E_L_E_T_ = ' ' AND (L2_DOC <> '" + Space(TamSx3("L2_DOC")[1]) + "' OR L2_PEDRES <> '" + Space(TamSx3("L2_PEDRES")[1]) +  "') "
	cQuerySel += " GROUP BY L2_PRODUTO,  L2_DESCRI"
	cQuerySel += " HAVING SUM(L2_QUANT)"
  	cQuerySel += " BETWEEN "
  	cQuerySel += "( (SELECT SUM (L2_QUANT) FROM " + cOrcament + " WHERE  D_E_L_E_T_ = ' '  AND L2_PRODUTO = '" +  cCodProduto + "')*"  + STR(nPercenIni) + ") /100 AND "
  	cQuerySel += "( (SELECT SUM (L2_QUANT) FROM " + cOrcament + " WHERE  D_E_L_E_T_ = ' '  AND L2_PRODUTO = '" +  cCodProduto + "')*"  + STR(nPercenFim) + ") /100     "
Else
    nQtdeVend:=0
	cQuerySel := "SELECT DISTINCT L2_PRODUTO, SUM(L2_QUANT) AS L2_QUANT,	L2_DESCRI FROM "  +  cOrcament
	cQuerySel += " WHERE L2_EMISSAO 	BETWEEN '" + cDataInicial  + "' AND '" + cDataFinal + "'
	cQuerySel += " AND D_E_L_E_T_ = ' '  AND (L2_DOC <> '" + Space(TamSx3("L2_DOC")[1]) + "' OR L2_PEDRES <> '" + Space(TamSx3("L2_PEDRES")[1]) +  "')"
	cQuerySel += " GROUP BY L2_PRODUTO, L2_DESCRI HAVING  SUM(L2_QUANT)"
	cQuerySel += " BETWEEN " + nQuantIni + " AND "  +  nQuantfim
Endif

cQuerySel += " ORDER BY L2_QUANT DESC "
LJa801ExQu(cArea,@cQuerySel)
DbSelectArea(cAliasTRB)
(cAliasTRB)->(__dbZap())
DbSelectArea(cArea)   
While !(cArea) ->(EOF())  // passa da tabela temporaria  LJVC para a TRB que ira preencher o grid
    nPos := aScan(aProdCad, {|c| c[2] == AllTrim((cArea)->L2_PRODUTO)} )     // verifica se os produtos retornados ja estao cadastrados
    If	nPos  == 0      // retorna zero quando o produto retornado nao esta cadastrado
		Reclock("TRB",.T.)
		(cAliasTRB)->L2_PRODUTO:=(cArea)->L2_PRODUTO                       // codigo do produto
		(cAliasTRB)->L2_DESCRI:=(cArea)->L2_DESCRI                         // Descricao do produto
		(cAliasTRB)->L2_QUANT:=(cArea)->L2_QUANT                           // quantidade vendida no periodo
		(cAliasTRB)->L2_PORCENT:= ((cArea)->L2_QUANT * 100)/ nQtdeVend     // porcentagem 
		(cAliasTRB)->L2_OK:=" "
	    lRet :=.T.
    EndIf
	(cArea) ->(DBSKIP())
End

(cAliasTRB)->(DbGoTop())  

Return lRet 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801cVlPr  ºAutor  ³ Vendas Cliente     º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se já existe o produto na sugestão de vendas      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cCodPro - Codigo do produto                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801cVlPr(cCodPro) 

Local cArea	    := "LJVC"  						//Alias temporario	
Local cQuery 	:= ""                               //Variavel de Consulta
Default cCodPro :=""

cQuery:="SELECT DISTINCT C.ACV_CATEGO,C.ACV_CODPRO,P.B1_DESC FROM " + RetSqlName("ACV") +" C LEFT JOIN "+ RetSqlName("SB1") +" P " 
cQuery+="ON P.B1_COD = C.ACV_CODPRO WHERE  C.ACV_CATEGO IN ("
cQuery+="SELECT ACU_COD FROM  " + RetSqlName("ACU") +" WHERE ACU_CODPAI IN" 
cQuery+="(SELECT ACV_CATEGO FROM  " + RetSqlName("ACV") +" WHERE ACV_CODPRO ='" + (cCodPro) + "' AND D_E_L_E_T_ = ' ')) OR C.ACV_CATEGO IN("
cQuery+="(SELECT ACV_CATEGO FROM  " + RetSqlName("ACV") +" WHERE ACV_CODPRO ='" + (cCodPro) + "' AND D_E_L_E_T_ = ' ')) "
cQuery+="AND C.ACV_CODPRO <> '" + (cCodPro) + "' "
cQuery+="AND C.D_E_L_E_T_ =' '"   

LJa801ExQu(cArea,@cQuery)
While !(cArea) ->(EOF())
	AAdd(aProdCad, { .T., AllTrim((cArea)->ACV_CODPRO),(cArea)->B1_DESC} )  // preenche no array os produtos encontrados que ja estao cadastrados 
	(cArea) ->(DBSKIP())														//na sugestao de vendas
End	
(cArea)->(DbCloseArea())

Return 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aGetS  ºAutor  ³ Vendas Cliente     º Data ³  28/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Efetua a estrutura do grid e da tabela temporaria de produ º±±
±±º          ³ tos                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³aGrid - array de Campos para o grid de produtos             º±±
±±º          ³aStruTRB - Array com estrutura da tabela temporaria         º±±
±±º          ³aNomeTMP - Array com os arquviso temporarios                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aGetS(aGrid,aStruTRB,	aNomeTMP)
Local aTamD2_ITEM		:= TamSx3("L2_ITEM")		// Tamanho do campo D2_ITEM
Local aTamD2_COD		:= TamSx3("L2_PRODUTO")		// Tamanho do campo D2_COD
Local aTamB1_DESC		:= TamSx3("L2_DESCRI")		// Tamanho do campo B1_DESC
Local aTamD2_QTD		:= TamSx3("L2_QUANT")		// Tamanho do campo D2_QUANT
Local oTempTable		:= Nil 						// Objeto tabela temporaria

Default aGrid 			:= {}
Default aStruTrB 		:= {}
Default aNomeTMP 		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se estiver utilizando rastreablidade, mostra os campos de ³
//³controle de lote.                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   
AADD(aStruTRB,{"L2_OK"		,"C",aTamD2_ITEM[1]			,aTamD2_ITEM[2]		}) 
AADD(aStruTRB,{"L2_PRODUTO"	,"C",aTamD2_COD[1]			,aTamD2_COD[2]		})
AADD(aStruTRB,{"L2_DESCRI" 	,"C",aTamB1_DESC[1]			,aTamB1_DESC[2]		})
AADD(aStruTRB,{"L2_QUANT" 	,"N",aTamD2_QTD[1]			,aTamD2_QTD[2]		})
AADD(aStruTRB,{"L2_RECNO"   ,"C",10						,0					})
AADD(aStruTRB,{"L2_PORCENT"	,"N",aTamD2_QTD[1]			,aTamD2_QTD[2]		})

aAdd(aGrid,{"L2_OK"		,," "	 ," "})		
aAdd(aGrid,{"L2_PRODUTO",,STR0011," "}) 		//"Produto		
aAdd(aGrid,{"L2_DESCRI"	,,STR0029," "})			//"Descricao		
aAdd(aGrid,{"L2_QUANT"	,,STR0021," "})			//Quantidade

If Select(cAliasTRB) > 0
	If( ValType(oTempTable) == "O")
	  oTempTable:Delete()
	  FreeObj(oTempTable)
	  oTempTable := Nil
	EndIf
EndIf

//Cria tabela temporaria
oTempTable := LjCrTmpTbl(cAliasTRB, aStruTRB, {"L2_RECNO","L2_OK"})

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Lj801aLocQ  ºAutor  ³ Vendas Cliente     º Data ³  20/10/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica a quantidade vendida de um determinado produto em º±±
±±º          ³ um intervalo de tempo                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cCodProduto - Codigo do produto                             º±±
±±º          ³cDataInicial - Data inicial da consulta                     º±±
±±º          |cDataFinal - Data final da consulta                         º±±  
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Retorna a quantidade vendida do produto                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA - VENDA ASSISTIDA                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj801aLocQ (cCodProduto, cDataInicial, cDataFinal)     
Local nQuant 		:= 0				 	//Variavel de retorno
Local cOrcament 	:= RetSQLName("SL2")	//Verificar qual eh a tabela de orcamento
Local cArea			:= "LJVC"    			//Alias temporario 
Local cQuery       	:= ""                   //Variavel de Consulta
Default cCodProduto := ""
Default cDataInicial:= ""
Default cDataFinal 	:= ""

cQuery := " SELECT SUM(L2_QUANT) AS L2_QUANT FROM "  +  cOrcament
cQuery += " WHERE  L2_NUM IN (  SELECT L2_NUM FROM " + cOrcament + " 	WHERE D_E_L_E_T_ = ' '  AND L2_PRODUTO = '" +  cCodProduto + "')"
cQuery += " AND  L2_PRODUTO = '" + AllTrim(cCodProduto) + "' AND L2_EMISSAO BETWEEN '" + cDataInicial  + "' AND '" + cDataFinal + "' "
cQuery += " AND D_E_L_E_T_ = ' '  AND (L2_DOC <> '" + Space(TamSx3("L2_DOC")[1]) + "' OR L2_PEDRES <> '" + Space(TamSx3("L2_PEDRES")[1]) +  "') "
cQuery += " GROUP BY L2_PRODUTO,  L2_DESCRI"
cQuery += " ORDER BY L2_QUANT DESC "
LJa801ExQu(cArea,@cQuery)

nQuant := LJVC->L2_QUANT

(cArea)->(DbCloseArea())

Return nQuant
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    |LJ801aRetC  ºAutor ³Vendas               º Data ³ 28/10/10  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Retorna ultima Categora cadastrada                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Parametros³cCodPro = codigo do produto                	              ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/    	
Function LJ801aRetC(cCodPro) 
Local cCateg  := "" 	// Variavel de Retorno
Local cQuery  := ""  // Query
Local cACU	  :=  RetSQLName("ACU")  //Define a tabela a ser utilizada em consulta
Local cArea	  := "LJVC"             //Area temporaria para consulta

Default	cCodPro	:=""

If !Empty(AllTrim(cCodPro)) // Caso tenha produto sera efetuada busca do produto para alteracao
	cQuery :="SELECT ACV_CATEGO FROM " + RetSQLName("ACV") + " WHERE ACV_CODPRO ='" + cCodPro + "' AND ACV_SUVEND = '1' AND D_E_L_E_T_ = ' '"
	LJa801ExQu(cArea,@cQuery)
	DbSelectArea(cArea) 
	cCateg := LJVC->ACV_CATEGO  //campo é tipo varchar
Else      
	cQuery :="SELECT MAX(ACU_COD) COD FROM " + cAcu +" WHERE D_E_L_E_T_ = ' '"
 	LJa801ExQu(cArea,@cQuery)
	DbSelectArea(cArea) 
	cCateg := Soma1(LJVC->COD)		//campo alfanumerico
EndIf
(cArea)->(DbCloseArea())

Return cCateg

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³LJ801aVlUs³ Autor ³ RAFAEL MARQUES        ³ Data ³25/10/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida se o usuario esta habilitado para usar a rotina     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LOJA801()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³                                                                        ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJ801aVlUs()
Local lCat	:= SuperGetMV("MV_LJCATPR",,.F.) 	// Verifica se o usuario esta com o parametro setado como True  
Local lRet  := .F.                       // Variavel de retorno

#IFDEF TOP 
	lRet := .T.
#ELSE	
	MsgStop(STR0065,STR0064) //"Rotina disponivel apenas para ambiente TOPCONECT."."###"Atenção !"
#ENDIF                                                

If lRet
	If !lCat  
		MsgStop(STR0066,STR0064) //"O parametro MV_LJCATPR deve estar habilitado."."###"Atenção !"
		lRet := .F.	
	EndIf	
EndIf 

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} LjVldPorc
Usado para validação do valor informado no campo de porcentagem final

@param   nTpProc - Tipo escolhido (1-Produto especifico;2-quantidade vendida
@param   nPerceFim - Valor de porcentagem informado 
@author  Varejo
@version P11
@since   21/10/2014
@return  lRet - booleana com o retorno de sucesso (.T.) ou problema (.F.) 
/*/
//-------------------------------------------------------------------
Static Function LjVldPorc(nTpProc, nPerceFim)
Local lRet := .T.

If nTpProc == 1 .And. nPerceFim > 100	//Produto especifico
	lRet := .F.
	MsgInfo(STR0063, STR0064) //#"A porcentagem final não pode ser supeior a 100%." //##"Atenção"
EndIf

Return lRet
