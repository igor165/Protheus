#INCLUDE "LOJC040.CH"
#INCLUDE "TOTVS.CH"

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³ DATA   ³ BOPS ³Prograd.³ALTERACAO                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³11.08.99³PROTH ³Julio W.³ Convers„o de Macros (&) para Protheus        ³±±
±±³03.10.02³xxxxxx³Edilson ³ Implmentacao para leitura de campos virtuais ³±±
±±³        ³      ³        ³ na tabela SX3.                               ³±±
±±³13/12/06|9.12  ³Fernando|Bops 115313 Alterado a array aRotina  para    ³±±
±±³        ³      |        ³ criação do menu funcional            	      ³±±
±±³19/02/07|9.12  ³Kotaki  |Conversao dos Bitmaps para o padrao da 9.12   ³±±
±±³        ³      |        ³Ajuste da notacao do fonte conforme MDM/S	  ³±±
±±³07/04/07³9.12  |Fernando³Bops-121727 alteração feita na Query e na     ³±±
±±³        ³      |        ³Fillgetdados para corrigir a consulta vendas  ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³ LOJC040	³ Autor ³ Microsiga        	    ³ Data ³ 19/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta de Vendas										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ SigaLoja 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Conversao ³ Fernando Godoy  - FiveWin								  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LojC040()
// Campo verificador se está sendo chamado pelo Robo
Local lAutomato	:= If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

// Pesq. / Consulta
Private aRotina	:= {}

// Consulta Vendas
Private cCadastro := STR0003 // "Pesq."

//Endereça a função de BROWSE
If !lAutomato
	aRotina	:= MenuDef()
	mBrowse( 6, 1,22,75,"SL1" )
EndIf

Return NIL

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    |MenuDef	³ Autor ³ Fernando Amorim       ³ Data ³13/12/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de definição do aRotina                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina   retorna a array com lista de aRotina             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MenuDef()

Local aRotina:= {		{ STR0001, "AxPesqui"   , 0 , 1 , , .F.},;		//Pesquisar
						{ STR0002, "Lj040Con"   , 0 , 2 , , .T.} }     //Consultar

Return ARotina

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡…o	 ³ Lj040Con ³ Autor ³ Microsiga     		³ Data ³ 19/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Consulta de Vendas								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Lj040Con(ExpC1,ExpN1,ExpN2)								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo									  ³±±
±±³			 ³ ExpN1 = Numero do registro 								  ³±±
±±³			 ³ ExpN2 = Opcao selecionada								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Lj040Con(cAlias,nReg,nOpcx)
LOCAL nCnt			:= 0
LOCAL nDesconto 	:= 0
LOCAL nTotMerc		:= 0
Local oDlg
Local oGet
Local nOpca			:= 1
Local nDescItem		:= 0
Local nCredito		:= 0
Local M->L1_NUM		:= SL1->L1_NUM
Local lContinua		:= .T.

Local cSeek			:= Nil
Local cWhile		:= Nil
Local aNoFields		:= {"L2_NUM"}
Local cQuery		:= ""
LOCAL n				:= 1
Local lCondNeg      := .F.                // Controla se a venda foi paga com Condicao Negociada

Local aSizeAut 	:= MsAdvSize()							// Tamanho da tela. 
Local aObjects 	:={}									// Posicao da Tela
Local aInfo  	:={}									// Posicao da Tela
Local aPosObj 	:={}									// Posicao da Tela
Local aButtons := {}									// Array com os botoes acrescentados no EnchoiceBar

// Campo verificador se está sendo chamado pelo Robo
Local lAutomato	:= If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

// Calcula as posicoes da tela
AADD(aObjects,{100,100,.T.,.T.})
AADD(aObjects,{315,035,.T.,.T.})

aInfo 	:= {aSizeAut[1],aSizeAut[2],aSizeAut[3],aSizeAut[4],3,3}
aPosObj := MsObjSize(aInfo,aObjects,.T.)        

//*** Query da secao 1 ***
cQuery		:= "SELECT * "
cQuery		+= "FROM "  + RetSqlName("SL2") + " SL2 "
cQuery		+= "WHERE "
cQuery		+= "SL2.L2_FILIAL = '" + xFilial("SL2") + "' AND "
cQuery		+= "SL2.L2_NUM = '" + M->L1_NUM + "' AND "
cQuery		+= "SL2.D_E_L_E_T_ <> '*' "
cQuery		:= ChangeQuery( cQuery )

cSeek  := xFilial("SL2")+SL1->L1_NUM
cWhile := "SL2->L2_FILIAL + SL2->L2_NUM"          
   
FillGetDados(	nOpcx 	  , "SL2"	, 1	, cSeek ,; 
			{||&(cWhile)},{ || SL2->L2_VENDIDO = 'S' } , aNoFields,; 
	   		Nil, Nil,cQuery, Nil, IIf(nOpcx<>3,.F.,.T.),; 
			Nil, Nil,{|| Lj040After( aCols, n)} , Nil,;
			Nil, Nil) 

// Posiciona o ponteiro nos registros corretos das tabelas relacionadas
DbSelectArea("SA1")
DbSetOrder(1)
If DbSeek(xFilial("SA1")+SL1->L1_CLIENTE+SL1->L1_LOJA)

	DbSelectArea("SA3")
	DbSetOrder(1)
	If DbSeek(xFilial("SA3")+SL1->L1_VEND)
	    
	    lCondNeg := .F. 
		If Alltrim(SL1->L1_CONDPG) == "CN"
		   lCondNeg := .T.
		EndIf
		
		DbSelectArea("SE4")
		DbSetOrder(1)
		If lCondNeg .OR. DbSeek(xFilial("SE4")+SL1->L1_CONDPG)
		
			DbSelectArea( "SL2" )
			DbSetOrder(1)
			If DbSeek( xFilial("SL2")+SL1->L1_NUM )
				nRegistro := RecNo()
				
				While !Eof( ) .And. L2_FILIAL+L2_NUM==SL1->L1_FILIAL+SL1->L1_NUM
					
					If SL2->L2_VENDIDO != "S"
						DbSkip()
						Loop
					Endif
					
					nDesconto+= SL2->L2_DESCPRO + SL2->L2_VALDESC
					nDescItem+= SL2->L2_VALDESC
					nTotMerc += SL2->L2_VLRITEM
					
					nCnt++
					DbSkip()
				End
				
				If nCnt == 0
					Help(" ",1,"Lj20NOVENDA" )
					lContinua := .F.
				Endif
				
				If lContinua					
					nCredito := SL1->L1_CREDITO
					
					If !lCondNeg .AND. SE4->E4_DESCFIN > 0
						nDesconto := nTotMerc - SL1->L1_VLRLIQ
					Endif
					
					DEFINE MSDIALOG oDlg TITLE STR0003 From aSizeAut[7],0 TO ;
							aSizeAut[6],aSizeAut[5]  OF oMainWnd PIXEL //"Consulta Vendas"			
					
					@ 1.4 , .8  SAY STR0004  // Or‡amento:
					@ 1.4 , 5 MSGET SL1->L1_NUM SIZE 050,8	When .F.
					
					If cPaisLoc != "BRA"
						@ 1.4, 15 	SAY STR0030  //Moeda:
						@ 1.4, 18 	MSGET SL1->L1_MOEDA	 PICTURE PesqPict("SL1","L1_MOEDA") 	SIZE 010, 8 WHEN .F.
						@ 1.4, 25  	SAY STR0031  //Taxa Moeda:
						@ 1.4, 30 	MSGET SL1->L1_TXMOEDA PICTURE PesqPict("SL1","L1_TXMOEDA")  SIZE 050, 8 WHEN .F.
					Endif
										
					AADD( aButtons, {NIL,{|| Agenda() }, STR0034,STR0034,{|| .T.}}) 	// "Agenda"				
					AADD( aButtons, {NIL,{|| Lj040Tela(nTotMerc,nDesconto,nDescItem,nCredito)}, STR0037,STR0037,{|| .T.}}) // "Detalhes" 			
					If !lAutomato // variavel de controle do Robo
						oGet := MSGetDados():New(aPosObj[1,1]+30,aPosObj[1,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AllWaysTrue","AllWaysTrue","",.F.)													
						ACTIVATE MSDIALOG oDlg ON INIT (EnchoiceBar(oDlg,{||nopca:=1,oDlg:End()},{||oDlg:End()},,@aButtons))
					EndIf
				EndIf
			Endif
		Endif
	Endif	
Endif

If lContinua
	SetKey(20,	{ || AllWaysTrue() })
	SetKEY(15,	{ || AllWaysTrue() })
	SetKEY(24,	{ || AllWaysTrue() })
EndIf

Return NIL

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡ao	 ³Lj040Tela ³ Autor ³ Microsiga       		³ Data ³ 19/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mostra a Tela de Detalhes da venda (Cliente, ...)		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Lj040Tela(nTotMerc,nDesconto,nDescItem,nCredito)
Local oDlg
Local cLoja   	:= SL1->L1_LOJA
Local cCodClie	:= SA1->A1_COD+"-"+cLoja
Local cCliente	:= Substr(SA1->A1_NOME,1,20)
Local cCodVend	:= SA3->A3_COD
Local cVend   	:= Substr(SA3->A3_NOME,1,20)
Local cCodCond	:= SL1->L1_CONDPG
Local cCondPag	:= Space(20)
Local nTotal  	:= nTotMerc
Local nTotGer	:= 0
Local nTotAbat	:= 0
Local nDinheir	:= SL1->L1_DINHEIR
Local nCheques	:= SL1->L1_CHEQUES
Local nCartao 	:= SL1->L1_CARTAO
Local nConveni	:= SL1->L1_CONVENI
Local nVales  	:= SL1->L1_VALES
Local nFinanc 	:= SL1->L1_FINANC
Local nDebito 	:= SL1->L1_VLRDEBI
Local nOutros 	:= SL1->L1_OUTROS
// Campo verificador se está sendo chamado pelo Robo
Local lAutomato	:= If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

Default nCredito:= 0

If Alltrim(SL1->L1_CONDPG) == "CN"
   cCondPag	:= STR0040                           //"Cond.Negociada"		
Else 
   cCondPag	:= SubStr(SE4->E4_DESCRI,1,20)
EndIf

// Detalhes da Venda
DEFINE MSDIALOG oDlg TITLE STR0005 From 16,1 To 400,439 PIXEL OF oMainWnd

	@ -2, 3 TO 041, 187 OF oDlg  PIXEL
	@ 08, 5 SAY STR0006 SIZE 53, 7 OF oDlg PIXEL  // Cliente-Loja
	@ 05, 64 MSGET cCodClie SIZE 29, 09 OF oDlg PIXEL When .F.
	@ 05, 96 MSGET cCliente SIZE 90, 09 OF oDlg PIXEL When .F.

	@ 20, 5 SAY STR0007 SIZE 53, 7 OF oDlg PIXEL  // Vendedor
	@ 17, 64 MSGET cCodVend SIZE 29, 09 OF oDlg PIXEL When .F.
	@ 17, 96 MSGET cVend 	SIZE 90, 09 OF oDlg PIXEL When .F.
	
	@ 42, 3 TO 084, 187 OF oDlg  PIXEL
	@ 33, 5 SAY STR0008 SIZE 60, 7 OF oDlg PIXEL  // Condicoes de Pagto.
	
	@ 29, 64 MSGET cCodCond SIZE 29, 09 OF oDlg PIXEL When .F.
	@ 29, 96 MSGET cCondPag SIZE 90, 09 OF oDlg PIXEL When .F.

	@ 050, 5 SAY STR0009 SIZE 76, 7 OF oDlg PIXEL  // Total das Mercadorias
	nTotGer := (nTotMerc + nDesconto)
	@ 050, 113 MSGET nTotGer  Picture PesqPict("SL2","L2_PRCTAB",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT
	
	If SL1->L1_CREDITO > 0
		If SL1->L1_DESCONT > 0 .OR. nDescItem > 0
			@ 061, 5 SAY STR0010 SIZE 53, 7 OF oDlg PIXEL  // Credito/Desconto
		Else
			@ 061, 5 SAY STR0011 SIZE 53, 7 OF oDlg PIXEL  // Credito
		Endif
	Else
		@ 061, 5 SAY STR0012 SIZE 53, 7 OF oDlg PIXEL  // Desconto
	Endif
	nTotAbat := nDesconto + nCredito
	@ 061, 113 MSGET nTotAbat	Picture PesqPict("SL2","L2_VALDESC",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT

	@ 073, 5 SAY STR0013       SIZE 67, 7 OF oDlg PIXEL  // Total da Venda - Descontos
	@ 072, 113 MSGET nTotal 	Picture PesqPict("SL2","L2_VALDESC",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT

	@ 86, 3 TO 185, 187 OF oDlg  PIXEL
	@ 096, 6 SAY STR0014       SIZE 53, 7 OF oDlg PIXEL  // Dinheiro
	@ 094, 113 MSGET nDinheir	Picture PesqPict("SL1","L1_DINHEIR",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT

	@ 107, 6 SAY STR0015       SIZE 53, 7 OF oDlg PIXEL  // Cheque
	@ 105, 113 MSGET nCheques	Picture PesqPict("SL1","L1_CHEQUES",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT

	@ 117, 6 SAY STR0016       SIZE 53, 7 OF oDlg PIXEL  // CartÆo
	@ 116, 113 MSGET nCartao	Picture PesqPict("SL1","L1_CARTAO",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT

	@ 127, 6 SAY STR0017       SIZE 53, 7 OF oDlg PIXEL  // Convˆnio
	@ 127, 113 MSGET nConveni	Picture PesqPict("SL1","L1_CONVENI",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT

	@ 138, 7 SAY STR0018       SIZE 53, 7 OF oDlg PIXEL  // Vale
	@ 138, 113 MSGET nVales 	Picture PesqPict("SL1","L1_VALES",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT

	@ 149, 6 SAY STR0019       SIZE 53, 7 OF oDlg PIXEL  // Financiado
	@ 149, 113 MSGET nFinanc	Picture PesqPict("SL1","L1_FINANC",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT

	@ 160, 6 SAY STR0029       SIZE 53, 7 OF oDlg PIXEL  // Cart„o D‚bito
	@ 160, 113 MSGET nDebito    Picture PesqPict("SL1","L1_VLRDEBI",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT

	@ 171, 6 SAY STR0039       SIZE 53, 7 OF oDlg PIXEL  // Outros
	@ 171, 113 MSGET nOutros    Picture PesqPict("SL1","L1_OUTROS",16) SIZE 73, 09 OF oDlg PIXEL When .F. RIGHT
	DEFINE SBUTTON oBtn FROM 002,190 TYPE 1 ACTION oDlg:End() Enable OF oDlg
If !lAutomato	
	ACTIVATE MSDIALOG oDlg
EndIf

Return NIL

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡…o	 ³Lj040After³ Autor ³ Microsiga        		³ Data ³ 19/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Implementa tratamento na aCols para que os registros		  ³±±
±±³          ³ dos produtos que tiveram devolucao, sejam mostrados na tela³±±
±±³          ³ como deletados                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ SIGALOJA 											      ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Lj040After(aCols, n)
Local nPosStatus	:= Ascan( aHeader,{ |x| Alltrim(x[2]) == "L2_STATUS" } )
Local nLenAcols		:= Len(acols)

If nLenAcols > 0 .AND. nPosStatus > 0 .AND. aCols[n][nPosStatus] == "D"
	aCols[nLenAcols][ Len(aCols[nLenAcols]) ] := .T.
Endif

Return .T.
