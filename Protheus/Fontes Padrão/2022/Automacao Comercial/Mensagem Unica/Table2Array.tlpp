#include "TOTVS.CH"
#include "msobject.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} Table2Array
Classe de suporte para auxiliar na implementação de interfaces

@type    class
@since   02/08/22
@version 12.1.33
/*/
//-------------------------------------------------------------------
Class Table2Array

    Private Data Alias As Character
    Private Data Struct As Array
    Private Data FieldPosition as Object

    Public Method New(cAlias As Character) As Object
    Public Method GetFirstPosition(Field as Character, Value as Variant ) As Numeric
    Public Method GetValue(Field as Character, PositionOfValue as Numeric ) As Variant
    Public Method SumUp(Field As Character, ReturnedField As Character) as Array

    Private Method GetStruct()
    Private Method SetData()
    Private Method GetFieldPosition(Field As Character) As Numeric

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
 Metodo New para instanciar todos as propriedades
@since 02/08/2022	
@version 12.1.33
@param	cAlias	 - Alias da tabela temporaria 
@return Self	 - Objeto com todas propriedades
/*/
//-------------------------------------------------------------------
Method New(cAlias As Character)  As Object class Table2Array
 
    TRY
        Self:Alias := cAlias
        Self:FieldPosition := THashMap():New()
        Self:GetStruct()
        Self:SetData()
    CATCH oError
        ConOut( oError:description )
    ENDTRY

Return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} GetStruct
 Metodo para criação da propriedade, contendo a estruruta da tabela
@since 02/08/2022	
@version 12.1.33
/*/
//-------------------------------------------------------------------
Method GetStruct() class Table2Array

    Self:Struct :=  DBStruct( Self:Alias )

Return  

//-------------------------------------------------------------------
/*/{Protheus.doc} SetData
 Metodo para montar estrutura da tabela temporaria
@since 02/08/2022	
@version 12.1.33
/*/
//-------------------------------------------------------------------
Method SetData() class Table2Array

	Local nLenStruct,nX AS Numeric

	If !(Self:Alias)->( Eof() ) // -- Se não for fim de arquivo

		nLenStruct := Len(Self:Struct[1])

		While !(Self:Alias)->( Eof() ) // -- Enquanto não for fim de arquivo
			
			For nX := 1 To Len(Self:Struct)
				
				If Len(Self:Struct[nX]) == nLenStruct
					aAdd(Self:Struct[nX],{})
				EndIf 

				aAdd(Self:Struct[nX][nLenStruct + 1],&((Self:Alias)->(Self:Struct[nX][1])))

			Next nX

			(Self:Alias)->(DbSkip())

		Enddo
	EndIf 

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetFirstPosition
 Metodo encontrar a posição na estrutura
@since 02/08/2022	
@version 12.1.33
@param	Field	 - campo da tabela temporaria 
@return value	 - valor da tabela temporaria
/*/
//-------------------------------------------------------------------
Method GetFirstPosition(Field as Character, Value as Variant ) As Numeric class Table2Array
    
    Local PositionOfField As Numeric 
    Local PositionOfValue As Numeric 
    Local StructureSize As Numeric 
    Local VarError := ErrorClass():New() As Object

    TRY 
        PositionOfField := Self:GetFieldPosition(Field)
        
        StructureSize := Len(Self:Struct[positionOfField])
        PositionOfValue := AScan(Self:Struct[positionOfField][structureSize], Value)

    CATCH oError
        ConOut( oError:description )
    ENDTRY

Return PositionOfValue 

//-------------------------------------------------------------------
/*/{Protheus.doc} GetValue
 Metodo para encontrar o dado na estrutura
@since 02/08/2022	
@version 12.1.33
@param	Field	 - campo da estrutura
@param	PositionOfValue	 - campo da estrutura
@return 
/*/
//-------------------------------------------------------------------
Method GetValue(Field as Character, PositionOfValue as Numeric ) As Variant class Table2Array
Return Self:Struct[Self:GetFieldPosition(Field)][Len(Self:Struct[Self:GetFieldPosition(Field)])][PositionOfValue]

//-------------------------------------------------------------------
/*/{Protheus.doc} GetFieldPosition
 Metodo para obter a posição do campo 
@since 02/08/2022	
@version 12.1.33
@param	Field	 - campo da estrutura
@return PositionOfField	 - posicao na estrutura
/*/
//-------------------------------------------------------------------
Method GetFieldPosition(Field As Character) As Numeric class Table2Array
    Local PositionOfField As Numeric
    Local VarError := ErrorClass():New() As Object
    
    If !Self:FieldPosition:Get(Field,PositionOfField)
        
        PositionOfField := AScan(Self:Struct,{ |x| x[1] = Field})
        
        If PositionOfField == 0
            VarError:GenCode := 1
            VarError:Description := "Campo [" + Field + "] não encontrado"
            Throw varError
        EndIf 

        Self:FieldPosition:Set(Field,PositionOfField)

    EndIf 

Return PositionOfField

//-------------------------------------------------------------------
/*/{Protheus.doc} SumUp
 Metodo para tratar quantas reservas serão criadas
@since 02/08/2022	
@version 12.1.33
@param	Field	 - campo da estrutura
@param	Stop	 - valor para controle de parada 
@return reservas
/*/
//-------------------------------------------------------------------
Method SumUp(Field As Character, Stop As Numeric) as Array class Table2Array
    Local PositionOfField As Numeric     
    Local StructureSize As Numeric 
    Local Sum := 0 as numeric
    Local Num as Numeric
    Local ItensSum := {} As Array 
    
    Local VarError := ErrorClass():New() As Object

    TRY 
        PositionOfField := Self:GetFieldPosition(Field)

        If PositionOfField == 0
            VarError:GenCode := 1
            VarError:Description := "Valor [" + cValTochar(Value) + "] não encontrado no campo [" + Field + "]"
            Throw varError
        EndIf 

        StructureSize := Len(Self:Struct[positionOfField])
        
        For Num := 1 To Len(Self:Struct[positionOfField][StructureSize])
            If Sum + Self:Struct[positionOfField][StructureSize][Num] <= Stop
                Sum += Self:Struct[positionOfField][StructureSize][Num] 
                
                AAdd(ItensSum,{Self:Struct[1][StructureSize][Num],;
                               Self:Struct[2][StructureSize][Num],;
                               Self:Struct[3][StructureSize][Num],;
                               Self:Struct[4][StructureSize][Num],;
                               Self:Struct[9][StructureSize][Num],;
                               Self:Struct[5][StructureSize][Num],;
                               Self:Struct[6][StructureSize][Num],;
                               Self:Struct[7][StructureSize][Num],;
                               Self:Struct[8][StructureSize][Num]})
            EndIf 

            If Sum >= Stop
                Exit
            EndIf 

        Next 

    CATCH oError
        ConOut( oError:description )
    ENDTRY

Return ItensSum

