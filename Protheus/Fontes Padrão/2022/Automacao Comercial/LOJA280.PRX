#INCLUDE "LOJA280.CH"
#INCLUDE "PROTHEUS.CH"

Static lCenVenda := SuperGetMv("MV_LJCNVDA",,.F.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³  DATA  ³ BOPS ³Program.³ ALTERACAO                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³18/06/03³064846³Edilson ³Implementada o posicinamento de registro na    ³±±
±±³        ³      ³        ³tabela SB1 para a geracao dos arquivos para a  ³±±
±±³        ³      ³        ³balanca.                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Loja280  ³ Autor ³ Adriano Sacomani      ³ Data ³ 29.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Transferencia de dados para balancas                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Loja280(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Sigaloja                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Loja280()

Local nOpca     := 0
Local oFont
Local oDlgTrans
Local oCheck1
Local oCheck2
Local lCheck1	 := .T.
Local lCheck2	 := .T.

Private nFile	     := 0

Private oMeter
Private oAlias
Private oRegistros

Private nRegistros := 0
Private cAlias     := " "

Private oDlgRegua
Private lAbort      := .T.
Private lEntrei     := .F.
Private cDirBalanca := LjGetStation("DIRBAL")
Private cBalanca    := LjGetStation("BALANCA")
Private lLoja280    := ExistBlock("LOJA280")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para montagem da Regua de Processamento - 26/12/95 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private nTotRegs

SetKey(VK_F12, { || pergunte("LJA280",.T.)})

// Transferˆncia para Balancas
DEFINE MSDIALOG oDlgTrans FROM 39,85 TO 190,550 TITLE ;
OemToAnsi(STR0009) PIXEL OF oMainWnd
DEFINE FONT oFont NAME "Ms Sans Serif" BOLD

@ 7, 130 TO 50, 226 LABEL STR0010 OF oDlgTrans PIXEL   // Arquivos
@ 7,   4 TO 68, 121 LABEL STR0011 OF oDlgTrans  PIXEL  // Objetivo do Programa

@	19, 132 CHECKBOX oCheck1  VAR lCheck1  PROMPT OemToAnsi(STR0012) SIZE 53,10 OF oDlgTrans PIXEL  // Produtos
@	31, 132 CHECKBOX oCheck2  VAR lCheck2  PROMPT OemToAnsi(STR0013) SIZE 53,10 OF oDlgTrans PIXEL  // Setores

@ 19, 15 SAY OemToAnsi(STR0014 )+;
OemtoAnsi(STR0015)+cBalanca ;
SIZE 100, 20 OF oDlgTrans PIXEL FONT oFont

@ 40, 15 SAY OemToAnsi(STR0016) SIZE 100, 20 OF oDlgTrans PIXEL;
FONT oFont

DEFINE SBUTTON FROM 60,155 TYPE 1 ENABLE OF oDlgTrans;
ACTION ( nOpcA := 1 , Regua280(lCheck1,lCheck2,oDlgTrans) ,;
oDlgTrans:End() )
DEFINE SBUTTON FROM 60,185 TYPE 2 ENABLE OF oDlgTrans;
ACTION ( nOpcA := 2 , oDlgTrans:End() )
ACTIVATE MSDIALOG oDlgTrans CENTERED
oFont:End()

If lLoja280
	ExecBlock("LOJA280",.F.,.F.)
EndIf

SetKey(VK_F12,Nil)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Lj280SB1 ³ Autor ³ Adriano Sacomani      ³ Data ³ 29.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Grava no arquivo CADTXT.TXT                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ LOJA280                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function Lj280SB1(lCheck2)
Local nRegAtual  	:= 0
Local nTotalRegs 	:= 1
Local aCampos
Local nPreco     	:= 0
Local lFirst     	:= .T.
Local oTempTable	:= Nil		//Objeto da tabela temporária

aCampos:={ {"PRODUTO"   ,"C",6 ,0},;
{"TIPO"      ,"C",1,0},;
{"DESCRICAO" ,"C",22,0},;
{"UNIT"      ,"C",7,0},;
{"VALIDADE"  ,"C", 3,0}}

pergunte("LJA280",.f.)

//Cria tabela temporária
oTempTable := LjCrTmpTbl("cArqB1", aCampos)

dbSelectArea("SB1")
cAlias:=Alias()
nTotRegs  := RecCount()
dbSetOrder(1)
MsSeek(xFilial("SB1"))
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desenha cursor para movimenta‡„o SB1								  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Lj280Regua(.F.)

Count To nTotalRegs For (SB1->B1_BALANCA) $ "123" .and. ! Alltrim(B1_COD) == "000000"

SB1->( DBSeek( xFilial("SB1") ) )

While !Eof() .And. SB1->B1_FILIAL == xFilial("SB1")
	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³ Monta Regua de processamento ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Lj280Regua(.T.,nTotalRegs,nRegAtual)
	
	//Produto tem que estar como Balanca no cadastro
	If ! SB1->B1_Balanca $ "123" .or. Alltrim(B1_COD) == "000000"
		dbSkip()
		Loop
	Endif
	
	//Codigo do Produto nao pode ser <> que 6 se Balanca FILIZOLA BCSA, CS, PLATINA
	If Left(cBalanca,8) == "FILIZOLA" .and. Len(Alltrim(SB1->B1_COD)) <> 6
		dbSkip()
		Loop
	Endif
	
	//Se tiver vazio o setor, entao desconsiderar
	If Empty(SB1->B1_SETOR)
		If !lCheck2
			MsgInfo(STR0017+Alltrim(SB1->B1_COD)+STR0018 +;
			STR0019)
		Endif
		dbSkip()
		Loop
	Endif
	
	//Setor deve estar dentro do range da pergunte
	IF !Empty(mv_par01) .and. !Empty(mv_par02)
		IF B1_Setor < mv_par01 .or. B1_Setor > mv_par02
			dbSkip()
			Loop
		Endif
	Endif
	
	nPreco:=0
	If lCenVenda
		LjxeValPre(@nPreco, SB1->B1_COD, /*cCliente*/,/*cLoja*/)
	Else
		dbSelectArea("SB0")
		dbSetOrder(1)
		If dbSeek(xFilial("SB0")+SB1->B1_COD)
			nPreco:=B0_Prv1
		Endif
	EndIf
	
	lEntrei:=.T.
	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³ Gera Registro no arquivo Transferencia ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lFirst
		//Sugestao da propria "Filizola" para que o codigo DIVERSOS esteja como coringa
		//na balanca
		lFirst:=.F.
		RecLock("cArqB1",.T.)
		Replace Produto   With "000000"
		Replace Descricao With PADR(STR0026,22)
		Replace Tipo      With "P"
		Replace Unit      With "0000000"
		Replace Validade  With "000"
		MsUnLock()
	Endif
	
	RecLock("cArqB1",.T.)
	Replace Produto   With AllTrim(SB1->B1_Cod)
	Replace Descricao With Left(SB1->B1_Desc,22)
	If SB1->B1_BALANCA $ "12"
		Replace Tipo      With "P"
	Else
		Replace Tipo      With "U"
	Endif
	Replace Unit      With "0"+Left(StrZero(nPreco,7,2),4)+Right(StrZero(nPreco,7,2),2)
	Replace Validade  With StrZero(SB1->B1_PRValid,3)
	MsUnLock()
	
	nRegAtual++
	
	dbSelectArea("SB1")
	dbSkip()
EndDo

dbSelectArea("SB1")
dbSetOrder(1)
MsSeek(xFilial("SB1"))
dbSelectArea("cArqB1")
If lEntrei
	Copy To &(cDirBalanca+"CADTXT.TXT") SDF
Endif

If( ValType(oTempTable) == "O")
  oTempTable:Delete()
  FreeObj(oTempTable)
  oTempTable := Nil
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ LJ280SETO³ Autor ³ Adriano Sacomani      ³ Data ³ 29.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Grava no arquivo SETORTXT.TXT                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±³ÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ LOJA280                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function LJ280ST1()
Local nRegAtual  	:= 0
Local nTotalRegs 	:= 1
Local aCampos
Local nIndice    	:= 1
Local aIndSetor  	:= {}
Local nAscan     	:= 0
Local oTempTable	:= Nil		//Objeto da tabela temporária

lEntrei:=.F.

// Cria o arquivo Transferencia p/ SB1

dbSelectArea("SB1")
dbSetOrder(1)
cAlias :=Alias()
nTotRegs:=RecCount()
aCampos:={ {"SETOR"   ,"C",12 ,0},;
{"PRODUTO" ,"C",6  ,0},;
{"INDICE"  ,"C",4,0},;
{"TECLA"   ,"C",3,0}}

pergunte("LJA280",.f.)

//Cria tabela temporária
oTempTable := LjCrTmpTbl("cArqB1", aCampos)

dbSelectArea("SB1")
dbSetOrder(1)
MsSeek(xFilial("SB1"))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desenha cursor para movimenta‡„o SB1								  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Lj280Regua(.F.)

Count To nTotalRegs For (SB1->B1_BALANCA) $ "123" .and. ! Alltrim(B1_COD) == "000000"

dbSeek(xFilial())
nTotRegs  := RecCount()
While !Eof() .And. SB1->B1_FILIAL == xFilial("SB1")
	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³ Monta Regua de processamento ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Lj280Regua(.T.,nTotalRegs,nRegAtual)
	
	//Produto tem que estar como Balanca no cadastro
	If ! SB1->B1_Balanca $ "123" .or. Alltrim(SB1->B1_COD) == "000000"
		dbSkip()
		Loop
	Endif
	
	//Codigo do Produto nao pode ser <> de 6 se Balanca FILIZOLA BCSA, CS, PLATINA
	
	If Left(cBalanca,8) == "FILIZOLA" .and. Len(Alltrim(SB1->B1_COD)) <> 6
		dbSkip()
		Loop
	Endif
	
	
	//Se tiver vazio o setor, entao desconsiderar
	If Empty(SB1->B1_SETOR)
		MsgInfo(STR0017+Alltrim(SB1->B1_COD)+STR0018 +;
		STR0019)
		dbSkip()
		Loop
	Endif
	
	//Setor deve estar dentro do range da pergunte
	IF !Empty(mv_par01) .and. !Empty(mv_par02)
		IF B1_Setor < mv_par01 .or. B1_Setor > mv_par02
			dbSkip()
			Loop
		Endif
	Endif
	
	dbSelectArea("SX5")
	dbSetOrder(1)
	dbSeek( cFilial+"72"+SB1->B1_SETOR)
	cSetor:=UPPER(Left(Alltrim(X5Descri()),12))
	cTecla:=SB1->B1_Tecla
	If Empty(cTecla)
		cTecla:="0"
	Endif
	
	If Empty(cSetor)
		MsgInfo(STR0017+Alltrim(SB1->B1_COD)+STR0018 +;
		STR0019)
		dbSkip()
		Loop
	Endif
	
	//Procurando indice do produto para balancas naquele Setor
	If ( nAscan := ASCAN( aIndSetor,{ |X| X[1] == SB1->B1_SETOR}) ) > 0
		nIndice := aIndSetor[nAscan][2] + 1
		aIndSetor[nAscan][2]:=nIndice
	Else
		nIndice:=0
		AADD(aIndSetor,{SB1->B1_SETOR,0})
	Endif
	
	lEntrei:=.T.
	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³ Gera Registro no arquivo Transferencia ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RecLock("cArqB1",.T.)
	Replace Produto   With AllTrim(SB1->B1_Cod)
	Replace Setor     With cSetor
	Replace Indice    With StrZero(nIndice,4)
	Replace Tecla     With StrZero(Val(cTecla),3)
	MsUnLock()
	
	nRegAtual++
	
	dbSelectArea("SB1")
	dbSkip()
EndDo

dbSelectArea("SB1")
dbSetOrder(1)
MsSeek(xFilial("SB1"))

dbSelectArea("cArqB1")
If lEntrei
	Copy To &(cDirBalanca+"SETORTXT.TXT") SDF
Endif

If( ValType(oTempTable) == "O")
  oTempTable:Delete()
  FreeObj(oTempTable)
  oTempTable := Nil
EndIf

lAbort:=.f.
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj280Regua³ Autor ³ Mario Angelo          ³ Data ³26.12.95  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Faz a Montagem da Regua de processamento                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³LOJA280                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Lj280Regua(lProcessa,nTotalRegs,nRegAtual)

If lProcessa

	If nTotalRegs > 0
		nFile := (nRegAtual / nTotalRegs) * 100
	Else
		nFile := 0
	EndIf

	If ! nRegAtual > 0

		oAlias:cCaption := AllTrim(Alias())
		oAlias:Refresh()

	EndIf

	oRegistros:cCaption := AllTrim(Str(nRegAtual))
	oRegistros:Refresh()

	oMeter:Set(nFile)
	oMeter:Refresh()

	SysRefresh()

EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Regua280  ³ Autor ³ Aline / Fernando      ³ Data ³ 15/10/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Desenha a Regua de processamento.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³LOJA280                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Regua280(lCheck1,lCheck2,oDlgTrans)

Local oBtnParar
If Left(cBalanca,8) == "FILIZOLA"
	MsgInfo(OemtoAnsi(STR0020)+OemtoAnsi(STR0021))
Endif
DEFINE DIALOG oDlgRegua ;
   FROM 0,0 TO 7, 40 ;
   STYLE nOr( DS_MODALFRAME, WS_POPUP )

	@ 1.9, 0.5 METER oMeter VAR nFile TOTAL 100 SIZE 150, 10 OF oDlgRegua;
	           BARCOLOR GetSysColor(13),GetSysColor()

	@	2,   1 SAY STR0022    OF oDlgRegua PIXEL  // Transferindo
	@	2,  35 Say oAlias     VAR AllTrim(cAlias) SIZE 20, 20 OF oDlgRegua PIXEL
	@	2,	80 SAY STR0023    OF oDlgRegua PIXEL  // Registro
	@   2, 115 Say oRegistros Var AllTrim(Str(nRegistros)) SIZE 100, 20 OF oDlgRegua PIXEL

DEFINE SBUTTON oBtnParar FROM 40,127 TYPE 2 ENABLE OF oDlgRegua;
   ACTION ( lAbort:=.f. , oMeter:End(),oDlgRegua:End() )

SetKEY(27,oBtnParar:bAction)

nWidth := oDlgRegua:nRight - oDlgRegua:nLeft

oDlgRegua:bStart := {|| CursorWait(),LjChamaFun(lCheck1,lCheck2,oDlgTrans) ;
,CursorArrow(),oMeter:End(),oDlgRegua:End()}
ACTIVATE DIALOG oDlgRegua CENTER
Return nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³LjChamaFun³ Autor ³ Fernando / Aline      ³ Data ³ 29/09/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chama as funcoes de transferencia .                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³LjChamaFun()                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function LjChamaFun(lCheck1,lCheck2,oDlgTrans)

If lCheck1
	Lj280SB1(lCheck2)
EndIf

If lCheck2
	Lj280ST1()
EndIf

If lEntrei
	MsgInfo(OemToAnsi(STR0024))  // Transferˆncia Conclu¡da
Else
	MsgInfo(OemToAnsi(STR0025))
EndIf
Return NIL
