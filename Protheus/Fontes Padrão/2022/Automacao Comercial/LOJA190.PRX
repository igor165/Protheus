#include "LOJA190.ch"
//#Include "inkey.ch"
#include "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±            	
±±³Fun‡„o    ³ LOJA190  ³ Autor ³ Adriano Sacomani      ³ Data ³ 28/09/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Controle de cheques devolvidos                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LOJA190()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Edilson     ³05/04/02³014449³Efetua a validacao da data de reapresenta-³±±
±±³            ³        ³      ³cao de cheques devolvidos, tornando o cam-³±±
±±³            ³        ³      ³po obrigatorio somente nos casos em que o ³±±
±±³            ³        ³      ³codigo da Alinea for igual a 11.          ³±±
±±³A.Veiga     ³03/11/05³088317³Nao estava gravando o campo A1_DTULCHQ na ³±±
±±³            ³        ³      ³primeira devolucao do cheque.             ³±±
±±³Fernando    ³12/12/06³9.12  ³Bops 115280 Alterado a array aRotina      ³±±
±±³            ³        |      ³para criação do menu funcional            ³±±
±±³Dolis       ³30/05/07³125773³na devolucao avando o campo A1_DTULCHQ na ³±±
±±³            ³        ³      ³primeira devolucao do cheque.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function LOJA190(nPosArotina)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Array contendo as Rotinas a executar do programa      ³
	//³ ----------- Elementos contidos por dimens„o ------------     ³
	//³ 1. Nome a aparecer no cabe‡alho                              ³
	//³ 2. Nome da Rotina associada                                  ³
	//³ 3. Usado pela rotina                                         ³
	//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
	//³    1 - Pesquisa e Posiciona em uma Cota‡„o                   ³
	//³    4 - Analisa e/ou encerra uma cota‡„o                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	PRIVATE aRotina := MenuDef()

	Private cCadastro := STR0004 //"Cadastro de Cheques"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carrega fun‡„o Pergunte                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//SetKey (VK_F12,{|a,b| AcessaPerg("LOJ190",.T.)})
	// desabilitada a função pergunte pois não há parametros para essa função
	// bem como esta também não existe


	dbSelectArea("SEF")

	DEFAULT nPosArotina := 0
	If nPosArotina > 0 // Sera executada uma opcao diretamento de aRotina, sem passar pela mBrowse	
		bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nPosArotina,2 ] + "(a,b,c,d,e) }" )
		Eval( bBlock, Alias(), (Alias())->(Recno()),nPosArotina)
	Else
		mBrowse( 6, 1,22,75,"SEF",,,,,,Lj190Leg())
	Endif	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Recupera a Integridade dos dados                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( "SEF" )
	dbSetOrder( 1 )
	Set Key VK_F12 To 

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MenuDef	³ Autor ³ Fernando Amorim       ³ Data ³12/12/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de definição do aRotina                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina   retorna a array com lista de aRotina             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef() 

	Local lLj7190AdBt := ExistBlock("LJ7190ADBT") 	// Verifica se existe o PE LJ7190ADBT
	Local nCont       := 0                          // Contador de for
	Local aNewRotina								// Retorno do PE para adicionar no aRotina
	Local aRotina:= {  		{ STR0001 , "AxPesqui"  , 0 , 1 , , .F.}, ;  //"Pesquisar"
	{ STR0002 , "AxVisual"  , 0 , 2 , , .T.}, ;  //"Visualiza"
	{ STR0003 , "Lj190Dev"  , 0 , 4 , , .T.}, ;  //"Devolver"
	{ STR0038 , "Lj190Est"  , 0 , 5 , , .T.}, ;  //"Estorno"
	{ STR0040 , "Lj190Leg"  , 0 , 5 , , .T.}}    //"Legenda"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de entrada para acrescentar mais opcoes no aRotina ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   
	If lLj7190AdBt
		aNewRotina	:= ExecBlock("LJ7190ADBT")
		If ValType(aNewRotina) == "A"
			For nCont := 1 To Len(aNewRotina)
				If Len(aNewRotina[nCont]) == 6	// Somente adiciona se o tamanho for compativel	a estrutura do aRotina
					aAdd(aRotina ,aNewRotina[nCont])
				EndIf
			Next nCont		  	
		EndIf
	EndIf

Return(ARotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Lj190Dev ³ Autor ³ Adriano Sacomani      ³ Data ³ 28/09/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Devolve o cheque                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj190Dev (ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Op‡„o selecionada no menu                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ LOJA190                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Hanna C.    ³22/07/05³084638³Se o cliente for padrao nao atualiza o    ³±±
±±³            ³        ³      ³campo A1_CHQDEVO e A1_DTULCHQ             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Lj190Dev(cAlias,nReg,nOpcx)
	Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.) 
	Local aArea      := { Alias() , IndexOrd() , Recno() }
	Local oDlg
	Local cBanco190   := SEF->EF_BANCO
	Local cAgencia190 := SEF->EF_AGENCIA
	Local cConta190   := SEF->EF_CONTA
	Local cCheque190  := SEF->EF_NUM
	Local dEmissao    := SEF->EF_DATA
	Local cTitulo190  := SEF->EF_PREFIXO+"-"+SEF->EF_TITULO
	Local cCliente190 := Space(TamSx3("A1_COD")[1])
	Local cNome190    := Space(40)
	Local cFone190    := SEF->EF_TEL
	Local cHistCh190  := SEF->EF_HIST
	Local cHDev190    := SEF->EF_HISTD
	Local cAlinea1190  :=SEF->EF_ALINEA1
	Local cAlinea2190  :=SEF->EF_ALINEA2
	Local dAlinea1190  :=SEF->EF_DTALIN1
	Local dAlinea2190  :=SEF->EF_DTALIN2
	Local dRep190      :=SEF->EF_DTREPRE
	Local lAlin1:=.T.
	Local lAlin2:=.F.
	Local aAreaSef
	Local lDtComp		:= !Empty( SEF->( FieldPos( "EF_DTCOMP" ) ) )

	Local lAutomato := If(Type("lAutomatoX")<>"L",.F.,lAutomatoX) // variavel que trata se foi chamado pelo Robo de Testes

	Private cAge190:= CriaVar("A6_AGENCIA"),cCon190:=CriaVar("A6_NUMCON")
	Private cBco190  := CriaVar("A6_COD")
	Private cArquivo,nTotal:=0,nHdlPrv:=0
	Private cLote

	// quando for chamado da rotina do Robo, as variaveis  irao vir preenchidas
	If lAutomato
		cAlinea1190 := If(Type("xcAlinea1190") == "C", xcAlinea1190, cAlinea1190) // codigo da Alinea - tabela G0 do sx5
		cAlinea2190 := If(Type("xcAlinea2190") == "C", xcAlinea2190, cAlinea2190) // codigo da Alinea - tabela G0 do sx5
		dAlinea1190 := If(Type("xdAlinea1190") == "D", xdAlinea1190, dAlinea1190) // data da primeira devolução
		dAlinea2190 := If(Type("xdAlinea2190") == "D", xdAlinea2190, dAlinea2190) // data da segunda devolução
		dRep190     := If(Type("xdRep190") == "D", xdRep190, dRep190)  // data da reapresentaçao
	EndIf

	If dAlinea1190 > Ctod("  /  /  ")
		lAlin1:=.F.
	Endif	

	If dAlinea2190 > Ctod("  /  /  ")
		lAlin2:=.F.
	Endif	

	If dAlinea1190 > Ctod("  /  /  ") .and. dAlinea2190 == Ctod("  /  /  ")
		lAlin2:=.T.
	Endif	

	If SEF->EF_CART != "R"
		Help( " ", 1, "LJ190CART" )
		Return .T.
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se ja foi devolvido pela segunda vez, entao nao pode alterar  ³
	//³ dados                                   								³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cAlinea2190) .And. !lAutomato
		Help( " ", 1, "2.DEVOLU" )
		Return .T.
	Endif

	If Eof() .and. Bof()
		Help( " ", 1, "ARQVAZIO" )
		Return .T.
	Endif

	If lDtComp .and. !Empty(SEF->EF_DTCOMP)
		Help("", 1, "CHQCOMP",,STR0047,1,0)  //"Este cheque foi compensado. Para devolvê-lo é necessário estornar a compensação do mesmo antes deste procedimento" 
		Return .T.
	EndIf

	AjustaHelp()
	dbSelectArea("SE1")
	dbSetOrder(1)
	dbSeek(xFilial("SE1")+SEF->EF_PREFIXO+SEF->EF_TITULO+SEF->EF_PARCELA+SEF->EF_TIPO)
	IF ! SE1->E1_TIPO $ MVRECANT .And. Empty(SE1->E1_BAIXA) // Exige que o titulo esteja baixado, exceto se for 
		Help(" ",1,"TITNAOBXDO")										 // Recebimento Antecipado
		Return .T.
	Endif

	If Empty(dRep190)
		dRep190:=DataValida(ddatabase+1)
	Endif

	//Identificando Cliente
	dbSelectArea("SA1")
	dbSetOrder(1)
	dbSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA)
	cCliente190:=SA1->A1_COD+"/"+SA1->A1_LOJA
	cNome190:= SA1->A1_NOME
	DbSelectArea("SEF")

	While .t.

		If !lAutomato // nao foi chamado pelo Robo de Testes

			nOpca := 0
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Recebe dados a serem digitados                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			aSize := MSADVSIZE()
			If lPanelFin //Chamado pelo Painel Financeiro					
				oPanelDados := FinWindow:GetVisPanel()
				oPanelDados:FreeChildren()
				aDim := DLGinPANEL(oPanelDados)
				DEFINE MSDIALOG oDlg OF oPanelDados:oWnd FROM 0,0 To 0,0 PIXEL STYLE nOR( WS_VISIBLE, WS_POPUP )							
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Observacao Importante quanto as coordenadas calculadas abaixo: ³ 
				//³ -------------------------------------------------------------- ³ 		
				//³ a funcao DlgWidthPanel() retorna o dobro do valor da area do	 ³
				//³ painel, sendo assim este deve ser dividido por 2 antes da sub- ³
				//³ tracao e redivisao por 2 para a centralizacao. 					 ³		
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
				nEspLarg := ((DlgWidthPanel(oPanelDados)/2) - 210) /2		 					
				nEspLin  := -3		

			Else   
				nEspLarg := 2 
				nEspLin  := 2	
				DEFINE MSDIALOG oDlg FROM  38,16 TO 357,526 TITLE Alltrim(STR0006) PIXEL
			Endif	
			oDlg:lMaximized := .F.
			oPanel := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,20,20)
			oPanel:Align := CONTROL_ALIGN_ALLCLIENT    			


			@ 003+nEspLin, 004+nEspLarg TO 070+nEspLin, 214+nEspLarg OF oPanel  PIXEL LABEL STR0007
			@ 071+nEspLin, 004+nEspLarg TO 110+nEspLin, 214+nEspLarg OF oPanel  PIXEL LABEL STR0021

			// Primeira Linha
			@ 011+nEspLin, 011+nEspLarg SAY STR0011            SIZE 21, 7 OF oPanel PIXEL  //"Banco"
			@ 019+nEspLin, 011+nEspLarg MSGET cBanco190         When .F.	SIZE 17, 07 OF oPanel PIXEL
			@ 011+nEspLin, 038+nEspLarg SAY STR0012            SIZE 28, 7 OF oPanel PIXEL  //"Agˆncia"
			@ 019+nEspLin, 038+nEspLarg MSGET cAgencia190       When .F.  SIZE 30, 07 OF oPanel PIXEL
			@ 011+nEspLin, 078+nEspLarg SAY STR0013            SIZE 25, 7 OF oPanel PIXEL  //"Conta"
			@ 019+nEspLin, 078+nEspLarg MSGET cConta190         When .F.  SIZE 37, 07 OF oPanel PIXEL
			@ 011+nEspLin, 125+nEspLarg SAY STR0014            SIZE 46, 7 OF oPanel PIXEL  //"N£mero Cheque"
			@ 019+nEspLin, 125+nEspLarg MSGET cCheque190        When .F.  SIZE 40, 07 OF oPanel PIXEL
			@ 011+nEspLin, 175+nEspLarg SAY STR0015            SIZE 32, 7 OF oPanel PIXEL  //"Dt Cheque"
			@ 019+nEspLin, 175+nEspLarg MSGET dEmissao          When .F.	SIZE 32, 07 OF oPanel PIXEL

			// Segunda Linha
			@ 030+nEspLin, 011+nEspLarg SAY STR0016            SIZE 60, 7 OF oPanel PIXEL  //Titulo
			@ 038+nEspLin, 011+nEspLarg MSGET cTitulo190 When .F.  SIZE 30, 07 OF oPanel PIXEL
			@ 030+nEspLin, 051+nEspLarg SAY STR0017            SIZE 60, 7 OF oPanel PIXEL  //Cliente
			@ 038+nEspLin, 051+nEspLarg MSGET cCliente190 When .F.  SIZE 30, 07 OF oPanel PIXEL
			@ 030+nEspLin, 091+nEspLarg SAY STR0018            SIZE 25, 7 OF oPanel PIXEL  //Nome
			@ 038+nEspLin, 091+nEspLarg MSGET cNome190    When .F.  SIZE 117, 07 OF oPanel PIXEL

			//Terceira Linha
			@ 049+nEspLin, 011+nEspLarg SAY STR0019            SIZE 46, 7 OF oPanel PIXEL  //"N£mero Cheque"
			@ 057+nEspLin, 011+nEspLarg MSGET cFone190       When .F.  SIZE 40, 07 OF oPanel PIXEL

			@ 049+nEspLin, 061+nEspLarg SAY STR0020         SIZE 66, 7 OF oPanel PIXEL  //"Historico do Cheque"
			@ 057+nEspLin, 061+nEspLarg MSGET cHistCh190     When .F.  SIZE 147, 07 OF oPanel PIXEL

			// Primeira Linha do Segundo Groupbox
			@ 079+nEspLin, 011+nEspLarg SAY STR0030       SIZE 21, 7 OF  oPanel PIXEL  //"Alinea"
			@ 086+nEspLin, 011+nEspLarg MSGET cAlinea1190   F3 "G0"    When Empty(cAlinea2190) .and. lAlin1 SIZE 13, 07 OF oPanel PIXEL Hasbutton 
			@ 079+nEspLin, 032+nEspLarg SAY STR0031     SIZE 40, 7 OF oPanel PIXEL  //"Data"
			@ 086+nEspLin, 036+nEspLarg MSGET odAlinea1190 VAR dAlinea1190 When(Empty(cAlinea2190) .and. lAlin1) Valid If(nOpcA <> 0,Lj190Vdat(1,dAlinea1190,dAlinea2190),.T.) SIZE 32, 07 OF oPanel PIXEL Hasbutton
			odAlinea1190:lReadOnly := (!Empty(cAlinea1190)	.and. lAlin1)	

			@ 079+nEspLin, 087+nEspLarg SAY   STR0030  SIZE 21, 7 OF  oPanel PIXEL  //"Alinea"
			@ 086+nEspLin, 087+nEspLarg MSGET cAlinea2190   F3 "G0"    When !Empty(cAlinea1190) .and. lAlin2  	SIZE 13, 07 OF oPanel PIXEL Hasbutton

			@ 079+nEspLin, 107+nEspLarg SAY STR0032    SIZE 40, 7 OF oPanel PIXEL  //"Data"
			@ 086+nEspLin, 112+nEspLarg MSGET odAlinea2190 Var dAlinea2190 When!Empty(cAlinea1190) .and. lAlin2;
			Valid If(nOpcA <> 0,Lj190VDat(2,dAlinea1190,dAlinea2190,dRep190),.T.) SIZE 32, 07 OF oPanel PIXEL Hasbutton
			odAlinea2190:lReadOnly := (!Empty(cAlinea1190) .and. !Empty(cAlinea2190) .and. lAlin2)

			@ 079+nEspLin, 162+nEspLarg SAY STR0033   SIZE 70, 7 OF oPanel PIXEL  //"Reapresentar em"
			@ 086+nEspLin, 167+nEspLarg MSGET odRep190 VAR dRep190;
			SIZE 32, 07 Valid If(nOpcA <> 0,Lj190Dat(dalinea1190,lAlin1,dRep190,cAlinea1190),.T.) OF oPanel PIXEL Hasbutton
			odRep190:lReadOnly := (!Empty(cAlinea1190) .and.  !lAlin2)

			@ 097+nEspLin, 011+nEspLarg SAY STR0034     SIZE 200, 7 OF oPanel PIXEL
			@ 097+nEspLin, 011+nEspLarg MSGET cHDev190    Picture "@S40" When ((lAlin1 .and. !Empty(cAlinea1190)) .or. ;
			( lAlin2 .and. !Empty(cAlinea2190)))   Valid !Empty(cHDev190) SIZE 197, 07 OF oPanel PIXEL

			If lPanelFin //Chamado pelo Painel Financeiro					
				oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1])			
				ACTIVATE MSDIALOG oDlg ON INIT FaMyBar(oDlg,;
				{||(nOpca:=1,IF(fa190Ok(),oDlg:End(),nOpca := 0))},;
				{||(nOpca:=0,oDlg:End())})

			Else	
				DEFINE SBUTTON FROM 7, 221 TYPE 1 ACTION (nOpca:=1,IF(fa190Ok(),        ;
				oDlg:End(),nOpca := 0))       ;
				ENABLE OF oDlg

				DEFINE SBUTTON FROM 20, 221 TYPE 2 ACTION (nOpca:=0,oDlg:End()) ENABLE OF oDlg

				ACTIVATE MSDIALOG oDlg  CENTERED
			Endif


			If nOpca == 0
				Exit
			EndIf

			//Se nao fez nada, entao o programa vai agir como se tivesse clicado no cancelar
			If (lAlin2 .and. Empty(cAlinea2190)) .or. (lAlin1 .and. Empty(cAlinea1190))
				nOpca:=0
			Endif

			If nOpcA == 2	//REDIGITA
				Loop
			Elseif nOpca == 3		//ABANDONA
				Exit
			Endif

		EndIf // fim do If do teste do Robo

		If nOpcA == 1  // CONFIRMA
			Begin Transaction
				// Marca as datas de devolucao nos cheques de todos os titulos
				DbSelectArea("SEF")
				aAreaSef := SEF->(GetArea())
				DbSetOrder(1)
				MsSeek(xFilial("SEF")+cBanco190+cAgencia190+cConta190+cCheque190)
				While SEF->(!Eof()) 							.And.;
				SEF->EF_FILIAL == xFilial("SEF")	.And.;
				SEF->(EF_BANCO+EF_AGENCIA+EF_CONTA+EF_NUM) == cBanco190+cAgencia190+cConta190+cCheque190
					//Atualizando dados do cheque
					Reclock("SEF",.F.)
					Replace EF_ALINEA1 with	cAlinea1190
					Replace EF_ALINEA2 with	cAlinea2190
					Replace EF_DTALIN1 with  dAlinea1190
					Replace EF_DTALIN2 with  dAlinea2190
					Replace EF_CHDEVOL with  "S"
					Replace EF_DTREPRE with  dRep190
					Replace EF_HISTD   with  cHDev190
					MsUnlock()
					DbSkip()
				End
				SEF->(RestArea(aAreaSef))

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Grava o cheque devolvido somente se o cliente nao for padrao³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Reclock("SA1",.F.)
				If SA1->(!Eof()) .AND. GetMV("MV_CLIPAD") + GetMV("MV_LOJAPAD") <> SA1->A1_COD + SA1->A1_LOJA
					Replace A1_ChqDevo with A1_ChqDevo + 1
					Replace A1_DtUlChq with IIF( dAlinea2190 <= ddatabase .And. !Empty(dAlinea2190) , dAlinea2190, dAlinea1190 )
				Endif
				MsUnLock()
			End Transaction


			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³CHEQUES DEVOLVIDOS - DEVOLVER                                           ³
			//³Pto de entrada para disponibilizar a gravacao de informacoes dos cheques³
			//³devolvidos nos arquivos de historicos                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			If ExistBlock("LJ190DCH")
				ExecBlock("LJ190DCH")
			Endif
		Endif
		Exit
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura a integridade da janela                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Set Key VK_F12 to
	dbSelectArea(aArea[1])
	dbSetOrder(aArea[2])
	dbGoTo(aArea[3])

	If lPanelFin //Chamado pelo Painel Financeiro					
		dbSelectArea(FinWindow:cAliasFile)					
		FinVisual(FinWindow:cAliasFile,FinWindow,(FinWindow:cAliasFile)->(Recno()),.T.)
	Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FA190Ok   ³ Autor ³ Pilar S. Albaladejo   ³ Data ³ 07/11/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Exibe mensagem de OK para dados digitados de resgate       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FA190OK()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ LOJA190                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Fa190Ok()    
	Local lLJ190VLD := ExistBlock("LJ190VLD")
	Local lRet := .T.

	If !lLJ190VLD
		lRet := (MsgYesNo(STR0036,STR0037))//"Confirma Dados?"###"Aten‡„o"
	Else
		lRet := ExecBlock("LJ190VLD",.F.,.F.)
		If ValType(lRet) != "L"
			lRet := .T.
		EndIf
	EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Lj190Dat  ³ Autor ³ Viviani F. Barcellos  ³ Data ³ 29/03/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a  consistencia da data de reapresentacao na devolu-³±±
±±³          ³ cao dos cheques                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj190Dat                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ LOJA190                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj190Dat(dalinea1190,lAlin1,dRep190,cAlinea1190)

	Local lRet := .T.
	DEFAULT cAlinea1190 := ""

	If lAlin1
		If cAlinea1190 == "11"
			If dRep190 <= dalinea1190
				Help(" " ,1, "Lj190Dat" )
				lRet:= .F.
			EndIf
		EndIf
	EndIf
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Lj190VDat ³ Autor ³Marcel Borges Ferreira ³ Data ³ 26/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a  consistencia da data de devolucao dos cheques    ³±±
±±³          ³ 				                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj190VDat                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ LOJA190                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj190VDat(nDat,dAlinea1190,dalinea2190,dRep190)
	Local lRet := .F.
	Do case 
		Case nDat == 1
		If (dAlinea1190 <=(ddatabase) .And. !Empty(dAlinea1190) .And. dAlinea1190 > SEF->EF_DATA)
			lRet := .T.
		Else
			MsgInfo(STR0045)
		EndIf
		Case nDat == 2
		If (dAlinea2190 <=ddatabase .and. dAlinea2190 > dAlinea1190 .and. dalinea2190 > dRep190)
			lRet := .T.
		Else
			MsgInfo(STR0046)
		EndIf	
	EndCase	
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Lj190Est ³ Autor ³ Viviani F. Barcellos  ³ Data ³ 22/04/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancela  a devolucao do cheque por engano                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj190Est (ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Op‡„o selecionada no menu                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ LOJA190                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Lj190Est(cAlias,nReg,nOpcx)
	Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.) 
	Local aArea      := { Alias() , IndexOrd() , Recno() }
	Local oDlg
	Local cBanco190   := SEF->EF_BANCO
	Local cAgencia190 := SEF->EF_AGENCIA
	Local cConta190   := SEF->EF_CONTA
	Local cCheque190  := SEF->EF_NUM
	Local dEmissao    := SEF->EF_DATA
	Local cTitulo190  := SEF->EF_TITULO+"/"+SEF->EF_PARCELA
	Local cCliente190 := Space(TamSx3("A1_COD")[1])
	Local cNome190    := Space(40)
	Local cFone190    := SEF->EF_TEL
	Local cHistCh190  := SEF->EF_HIST
	Local cHDev190    := SEF->EF_HISTD
	Local cAlinea1190  :=SEF->EF_ALINEA1
	Local cAlinea2190  :=SEF->EF_ALINEA2
	Local dAlinea1190  :=SEF->EF_DTALIN1
	Local dAlinea2190  :=SEF->EF_DTALIN2
	Local dRep190      :=SEF->EF_DTREPRE
	Local lAlin1:=.T.
	Local lAlin2:=.F.
	Local aAreaSef
	Local lLj7EstDv    := ExistBlock("LJ7ESTDV")                // Verifica a existencia do ponto de entrada LJ7ESTDV
	Local xRet													// Variavel de retormo do ponto de entrada
	Local lRet 		   := .T.                                   // Retorno da funcao
	Local cQuery	   := ""
	Local lExistSE1	   := .F.
	Local oPrepared    := Nil

	Local lAutomato := If(Type("lAutomatoX")<>"L",.F.,lAutomatoX) // variavel que trata se foi chamado pelo Robo de Testes

	Private cAge190:= CriaVar("A6_AGENCIA"),cCon190:=CriaVar("A6_NUMCON")
	Private cBco190  := CriaVar("A6_COD")
	Private cArquivo,nTotal:=0,nHdlPrv:=0
	Private cLote

	cQuery := "SELECT COUNT(E1_NUM) TITULO FROM " + RetSqlName("SE1") + " WHERE "
	cQuery += "E1_FILIAL = ? "
	cQuery += "AND E1_PREFIXO = ? "
	cQuery += "AND E1_NUM =  ? "
	cQuery += "AND E1_PARCELA = ? "
	cQuery += "AND E1_TIPO = ? "
	cQuery += "AND E1_SALDO = 0 "
	cQuery += "AND D_E_L_E_T_ = ' ' "

	cQuery := ChangeQuery(cQuery)

	oPrepared:=FWPreparedStatement():New(cQuery)
	oPrepared:SetString(1, xFilial('SE1'))
	oPrepared:SetString(2, SEF->EF_PREFIXO)
	oPrepared:SetString(3, SEF->EF_TITULO)
	oPrepared:SetString(4, SEF->EF_PARCELA)
	oPrepared:SetString(5, SEF->EF_TIPO)
	
	cQuery:=oPrepared:GetFixQuery()

	lExistSE1 := !Empty(MpSysExecScalar(cQuery,"TITULO"))

	If oPrepared != NIL
		oPrepared:Destroy()
		oPrepared := NIL
	Endif

	If lExistSE1
		Help(" ",1,"LJ190EXISCH",,STR0048, 1, 0)//"Este cheque está vinculado a titulo já baixado. Não poderá haver estorno da devolução"
		Return .T.
	Endif

	If dAlinea1190 > Ctod("  /  /  ")
		lAlin1:=.F.
	Endif	

	If dAlinea2190 > Ctod("  /  /  ")
		lAlin2:=.F.
	Endif	

	If dAlinea1190 > Ctod("  /  /  ") .and. dAlinea2190 == Ctod("  /  /  ")
		lAlin2:=.T.
	Endif	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se houve realmente devolucao para que possa cancelar ³         								³
	//³ a mesma                                 								³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Eof() .and. Bof()
		Help( " ", 1, "ARQVAZIO" )
		nOpca:= 0
		Return .T.
	Endif

	If Empty(SEF->EF_ALINEA1) .and. Empty(SEF->EF_ALINEA2)
		Help("", 1, "SEMDEVOL")
		Return .T.
	EndIf

	AjustaHelp()
	dbSelectArea("SE1")
	dbSetOrder(1)
	dbSeek(xFilial("SE1")+SEF->EF_PREFIXO+SEF->EF_TITULO+SEF->EF_PARCELA+SEF->EF_TIPO) //posiciona no título conforme dados do cheque

	If Empty(dRep190)
		dRep190:=DataValida(ddatabase+1)
	Endif

	//Identificando Cliente
	dbSelectArea("SA1")
	dbSetOrder(1)
	dbSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA)
	cCliente190:=SA1->A1_COD+"/"+SA1->A1_LOJA
	cNome190:= SA1->A1_NOME
	DbSelectArea("SEF")

	While .t.

		If !lAutomato // nao foi chamado pelo Robo de Testes
			nOpca := 0
			aSize := MSADVSIZE()
			If lPanelFin //Chamado pelo Painel Financeiro					
				oPanelDados := FinWindow:GetVisPanel()
				oPanelDados:FreeChildren()
				aDim := DLGinPANEL(oPanelDados)
				DEFINE MSDIALOG oDlg OF oPanelDados:oWnd FROM 0,0 To 0,0 PIXEL STYLE nOR( WS_VISIBLE, WS_POPUP )							
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Observacao Importante quanto as coordenadas calculadas abaixo: ³ 
				//³ -------------------------------------------------------------- ³ 		
				//³ a funcao DlgWidthPanel() retorna o dobro do valor da area do	 ³
				//³ painel, sendo assim este deve ser dividido por 2 antes da sub- ³
				//³ tracao e redivisao por 2 para a centralizacao. 					 ³		
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
				nEspLarg := ((DlgWidthPanel(oPanelDados)/2) - 210) /2		 					
				nEspLin  := -3		

			Else   
				nEspLarg := 2 
				nEspLin  := 2	
				DEFINE MSDIALOG oDlg FROM  38,16 TO 357,526 TITLE Alltrim(STR0006) PIXEL
			Endif	
			oDlg:lMaximized := .F.
			oPanel := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,20,20)
			oPanel:Align := CONTROL_ALIGN_ALLCLIENT    			


			@ 003+nEspLin, 004+nEspLarg TO 070+nEspLin, 214+nEspLarg OF oPanel  PIXEL LABEL STR0007
			@ 071+nEspLin, 004+nEspLarg TO 110+nEspLin, 214+nEspLarg OF oPanel  PIXEL LABEL STR0021

			// Primeira Linha
			@ 011+nEspLin, 011+nEspLarg SAY STR0011            SIZE 21, 7 OF oPanel PIXEL  //"Banco"
			@ 019+nEspLin, 011+nEspLarg MSGET cBanco190         When .F.	SIZE 17, 07 OF oPanel PIXEL
			@ 011+nEspLin, 038+nEspLarg SAY STR0012            SIZE 28, 7 OF oPanel PIXEL  //"Agˆncia"
			@ 019+nEspLin, 038+nEspLarg MSGET cAgencia190       When .F.  SIZE 30, 07 OF oPanel PIXEL
			@ 011+nEspLin, 078+nEspLarg SAY STR0013            SIZE 25, 7 OF oPanel PIXEL  //"Conta"
			@ 019+nEspLin, 078+nEspLarg MSGET cConta190         When .F.  SIZE 37, 07 OF oPanel PIXEL
			@ 011+nEspLin, 125+nEspLarg SAY STR0014            SIZE 46, 7 OF oPanel PIXEL  //"N£mero Cheque"
			@ 019+nEspLin, 125+nEspLarg MSGET cCheque190        When .F.  SIZE 40, 07 OF oPanel PIXEL
			@ 011+nEspLin, 175+nEspLarg SAY STR0015            SIZE 32, 7 OF oPanel PIXEL  //"Dt Cheque"
			@ 019+nEspLin, 175+nEspLarg MSGET dEmissao          When .F.	SIZE 32, 07 OF oPanel PIXEL

			// Segunda Linha
			@ 030+nEspLin, 011+nEspLarg SAY STR0016            SIZE 60, 7 OF oPanel PIXEL  //Titulo
			@ 038+nEspLin, 011+nEspLarg MSGET cTitulo190 When .F.  SIZE 30, 07 OF oPanel PIXEL
			@ 030+nEspLin, 051+nEspLarg SAY STR0017            SIZE 60, 7 OF oPanel PIXEL  //Cliente
			@ 038+nEspLin, 051+nEspLarg MSGET cCliente190 When .F.  SIZE 30, 07 OF oPanel PIXEL
			@ 030+nEspLin, 091+nEspLarg SAY STR0018            SIZE 25, 7 OF oPanel PIXEL  //Nome
			@ 038+nEspLin, 091+nEspLarg MSGET cNome190    When .F.  SIZE 117, 07 OF oPanel PIXEL

			//Terceira Linha
			@ 049+nEspLin, 011+nEspLarg SAY STR0019            SIZE 46, 7 OF oPanel PIXEL  //"N£mero Cheque"
			@ 057+nEspLin, 011+nEspLarg MSGET cFone190       When .F.  SIZE 40, 07 OF oPanel PIXEL

			@ 049+nEspLin, 061+nEspLarg SAY STR0020         SIZE 66, 7 OF oPanel PIXEL  //"Historico do Cheque"
			@ 057+nEspLin, 061+nEspLarg MSGET cHistCh190     When .F.  SIZE 147, 07 OF oPanel PIXEL

			// Primeira Linha do Segundo Groupbox
			@ 079+nEspLin, 011+nEspLarg SAY STR0030       SIZE 21, 7 OF  oPanel PIXEL  //"Alinea"
			@ 086+nEspLin, 011+nEspLarg MSGET cAlinea1190   When .F. SIZE 13, 07 OF oPanel PIXEL Hasbutton 
			@ 079+nEspLin, 032+nEspLarg SAY STR0031     SIZE 40, 7 OF oPanel PIXEL  //"Data"
			@ 086+nEspLin, 036+nEspLarg MSGET odAlinea1190 VAR dAlinea1190 When .F. SIZE 32, 07 OF oPanel PIXEL Hasbutton	

			@ 079+nEspLin, 087+nEspLarg SAY   STR0030  SIZE 21, 7 OF  oPanel PIXEL  //"Alinea"
			@ 086+nEspLin, 087+nEspLarg MSGET cAlinea2190  When .F. 	SIZE 13, 07 OF oPanel PIXEL Hasbutton

			@ 079+nEspLin, 107+nEspLarg SAY STR0032    SIZE 40, 7 OF oPanel PIXEL  //"Data"
			@ 086+nEspLin, 112+nEspLarg MSGET odAlinea2190 Var dAlinea2190;
			When .F. SIZE 32, 07 OF oPanel PIXEL Hasbutton

			@ 079+nEspLin, 162+nEspLarg SAY STR0033   SIZE 70, 7 OF oPanel PIXEL  //"Reapresentar em"
			@ 086+nEspLin, 167+nEspLarg MSGET odRep190 VAR dRep190;
			When .F. SIZE 32, 7 OF oPanel PIXEL Hasbutton	

			@ 097+nEspLin, 011+nEspLarg SAY STR0034     SIZE 200, 7 OF oPanel PIXEL
			@ 097+nEspLin, 011+nEspLarg MSGET cHDev190    Picture "@S40" When .F. ;
			SIZE 197, 07 OF oPanel PIXEL

			If lPanelFin //Chamado pelo Painel Financeiro					
				oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1])			
				ACTIVATE MSDIALOG oDlg ON INIT FaMyBar(oDlg,;
				{||(nOpca:=1,	oDlg:End())},;
				{||(nOpca:=0,oDlg:End())})

			Else	
				DEFINE SBUTTON FROM 7, 221 TYPE 1 ACTION (nOpca:=1, ;
				oDlg:End())       ;
				ENABLE OF oDlg

				DEFINE SBUTTON FROM 20, 221 TYPE 2 ACTION (nOpca := 0, oDlg:End()) ENABLE OF oDlg

				ACTIVATE MSDIALOG oDlg  CENTERED
			Endif   

			// Ponto de entrada que permite customizacoes antes de confirmar o estorno da devolucao
			If lLj7EstDv
				xRet := ExecBlock("LJ7ESTDV",.F.,.F.,{nOpca})
				If ValType(xRet) == "L"
					lRet:= xRet 
				EndIf
			EndIf

			If nOpca == 0
				Exit
			EndIf

			If nOpcA == 2	//REDIGITA
				Loop
			Elseif nOpca == 3		//ABANDONA
				Exit
			Endif
		EndIf // fim do If do teste do Robo

		If nOpcA == 1 .AND. lRet	//CONFIRMA
			Begin Transaction
				// Marca as datas de devolucao nos cheques de todos os titulos
				DbSelectArea("SEF")
				aAreaSef := SEF->(GetArea())
				DbSetOrder(1)
				MsSeek(xFilial("SEF")+cBanco190+cAgencia190+cConta190+cCheque190)
				While SEF->(!Eof()) 							.And.;
				SEF->EF_FILIAL == xFilial("SEF")	.And.;
				SEF->(EF_BANCO+EF_AGENCIA+EF_CONTA+EF_NUM) == cBanco190+cAgencia190+cConta190+cCheque190
					//Cancela a devolucao
					//atualizando os dados do cheque
					Reclock("SEF",.F.)
					If !Empty(cAlinea2190) .and. !Empty(cAlinea1190)
						Replace SEF->EF_ALINEA2 with	""
						Replace SEF->EF_DTALIN2 with  Ctod("  /  /  ")
						Replace SEF->EF_HISTD   with  ""
						MsUnlock()
					Else
						Replace SEF->EF_ALINEA1 with	""
						Replace SEF->EF_DTALIN1 with  Ctod("  /  /  ")
						Replace SEF->EF_DTREPRE with  Ctod("  /  /  ")
						Replace SEF->EF_CHDEVOL with  ""
						Replace SEF->EF_HISTD   with  ""
						MsUnlock()
					EndIf
					DbSkip()
				End
				SEF->(RestArea(aAreaSef))
				Reclock("SA1",.F.)
				If SA1->(!Eof())
					Replace A1_ChqDevo with A1_ChqDevo - 1
					Replace A1_DtUlChq with IIF(Empty(cAlinea1190),Ctod("  /  /  "),dAlinea2190)
				Endif
				MsUnlock()
			End Transaction

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³CHEQUES DEVOLVIDOS - ESTORNAR                                           ³
			//³Pto de entrada para disponibilizar a gravacao de informacoes dos cheques³
			//³devolvidos nos arquivos de historicos                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ExistBlock("LJ190ECH")
				ExecBlock("LJ190ECH")
			Endif
		Endif
		Exit
	EndDO

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura a integridade da janela                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Set Key VK_F12 to
	dbSelectArea(aArea[1])
	dbSetOrder(aArea[2])
	dbGoTo(aArea[3])

	If lPanelFin //Chamado pelo Painel Financeiro					
		dbSelectArea(FinWindow:cAliasFile)					
		FinVisual(FinWindow:cAliasFile,FinWindow,(FinWindow:cAliasFile)->(Recno()),.T.)
	Endif

Return .T.


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Lj190Leg    ³ Autor ³ Mauricio Pequim Jr. ³ Data ³ 13.11.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria uma janela contendo a legenda da mBrowse ou retorna a ³±±
±±³          ³ para o BROWSE                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Loja190                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj190Leg(nReg)

	Local aLegenda := { 	{"BR_AZUL", STR0044 },; //"Cheque da Carteira de Pagamentos"
	{"BR_VERDE", STR0041 },; //"Cheque não devolvido"
	{"BR_AMARELO", STR0042 },; //"Cheque com uma devolucao"
	{"BR_VERMELHO", STR0043 } } //"Cheque com duas devolucoeses"
	Local uRetorno := .T.

	If nReg = Nil	// Chamada direta da funcao onde nao passa, via menu Recno eh passado
		uRetorno := {}
		Aadd(uRetorno, { ' EF_CART <> "R"'										, aLegenda[1][1] } )
		Aadd(uRetorno, { ' Empty(EF_ALINEA1) .and.  Empty(EF_ALINEA2)'	, aLegenda[2][1] } )
		Aadd(uRetorno, { '!Empty(EF_ALINEA1) .and.  Empty(EF_ALINEA2)'	, aLegenda[3][1] } )
		Aadd(uRetorno, { '!Empty(EF_ALINEA1) .and. !Empty(EF_ALINEA2)'	, aLegenda[4][1] } )
	Else
		BrwLegenda(cCadastro, STR0040,aLegenda) //"Legenda"
	Endif

Return uRetorno                                                                              


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Loja190T   ³ Autor ³ Marcelo Celi Marques ³ Data ³ 04.04.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada semi-automatica utilizado pelo gestor financeiro   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ LOJA190                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LOJA190T(aParam)
	ReCreateBrow("SEF",FinWindow)      	
	cRotinaExec := "LOJA190"
	Loja190(aParam[1])
	ReCreateBrow("SEF",FinWindow)      	

	dbSelectArea("SEF")

	INCLUI := .F.
	ALTERA := .F.

Return .T.	


Static Function AjustaHelp()

	Local aHelpPor := {}
	Local aHelpSpa := {}
	Local aHelpEng := {}


	aHelpPor :={"Este título não sofreu baixa"} 
	aHelpSpa :={"Este título no se dio de baja"}
	aHelpEng :={"This bill did not undergo any posting"}   
	PutHelp("PTITNAOBXDO",aHelpPor,aHelpSpa,aHelpEng,.T.)

	aHelpPor :={"Verifique se a operação selecionada","é realmente a que deve ser executada,","ou selecione um outro título. "} 
	aHelpSpa :={"Verifique si la operacion seleccionada","es realmente la que debe ejecutarse","o seleccione otro titulo."}
	aHelpEng :={"Check if the operation selected must","actually be executed or select","another bill."}   
	PutHelp("STITNAOBXDO",aHelpPor,aHelpSpa,aHelpEng,.T.)

Return()
