#include "TOTVS.ch"
#include "tlpp-core.th"
#include "msobject.ch"

User Function MessegeTelegram()
Local teste := MessegeTelegram():new("TESTE","000001")
teste:SendMessage("Marquinhos")
Return

/*/{Protheus.doc} MessegeTelegram
    Classe para construção dos metedos para o telegram
    @author Lucas Novais (lnovais)
    @since 29/06/2020
    @version 1.0
/*/

Class MessegeTelegram From MessageApp
    PUBLIC METHOD New(cfunctionality,cIdUser) CONSTRUCTOR
    PUBLIC METHOD SendMessage(cMessage) AS Logical
EndClass

/*/{Protheus.doc} New
    Metodo construtor da classe
    @author Lucas Novais (lnovais)
    @since 26/06/2020
    @version 1.0
    @param cfunctionality, character, Funcionalidade (MHT_FUNC)
    @param cIdUser       , character, Id do Usuarios (MHV_IDUSER)
    @return Self         , Object   , Objeto da classes
/*/

Method New(cfunctionality,cIdUser) Class MessegeTelegram
    _Super:NEW(cfunctionality,cIdUser)
Return Self
    
/*/{Protheus.doc} SendMessage
    Metodo responsavel pelo envio de mensagem para o telegram
    @author Lucas Novais (lnovais)
    @since 29/06/2020
    @version 1.0
    @param cMessage, Character, Mensagem que será enviada
    @return lRetorno, Logical, Retorna se obteve sucesso ao consumir o metodo
/*/

Method SendMessage(cMessage) Class MessegeTelegram AS Logical
    Local oRestClient := Nil                AS Object
    Local aChat       := Self:AData[2][1]   AS Array
    Local aHeadStr    := {}                 AS Array
    Local lRetorno    := .F.                AS Logical
    Local nX          := 0                  AS Numeric

    For nX := 1 To Len(aChat) 
        oRestClient := FWRest():New(AllTrim(aChat[nX][Self:aPositions[03]][02]))
        oRestClient:setPath("bot" + AllTrim(aChat[nX][Self:aPositions[04]][02]) + "/SendMessage")
       
        cParams := "chat_id="   + AllTrim(aChat[nX][Self:aPositions[05]][02])
        cParams += "&text="     + cMessage

        If lRetorno := oRestClient:Get(aHeadStr,cParams)
            cteste := oRestClient:GetResult()
        Else
            Self:oLog:Message("Retorno: " + oRestClient:GetLastError())
        EndIf 
        FreeObj(oRestClient)
    Next
Return lRetorno 