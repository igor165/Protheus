#INCLUDE "LOJA100.CH"
//#Include "FiveWin.ch"
#Include "Totvs.ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ LOJA100  ³ Autor ³ Elaine F. T. Beraldo  ³ Data ³ 15/08/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Acumulados Mensais                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
±±ÚÄÄÄÄÄÄÄÄÂÄÁÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³  DATA  ³ BOPS ³Program.³    ALTERACAO                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³24/01/03³xxxxxx³Edilson ³Implementacao na rotina para geracao somente  ³±±
±±³        ³      ³        ³dos produtos aos quais tiveram movimentacao   ³±±
±±³        ³      ³        ³dentro do mes definido no parametro.          ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LOJA100
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis locais												  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL oDlg
LOCAL cCadastro := OemToAnsi(STR0001)
LOCAL nOpca := 0
PRIVATE cMes

While .T.

	Pergunte("LJA100",.F.)

	DEFINE MSDIALOG oDlg FROM 100,100 TO 350,510 TITLE cCadastro PIXEL;
	OF oMainWnd

	// Este gera o arquivo de Acumulados Mensais por produto, do mes
	// selecionado nos parametros.
	// Devera ser utilizado na rotina de fechamento Mensal.

	@ 03,05 TO 100,200 PIXEL
	@ 10,10 Say OemToAnsi(STR0002) SIZE 180,08 OF oDlg PIXEL
	@ 20,10 Say OemToAnsi(STR0003) SIZE 180,08 OF oDlg PIXEL
	@ 30,10 Say OemToAnsi(STR0004) SIZE 180,08 OF oDlg PIXEL

	DEFINE SBUTTON FROM 107,100 TYPE 5 ACTION (Pergunte("LJA100",.T.)) ENABLE OF oDlg
	DEFINE SBUTTON FROM 107,135 TYPE 1 ACTION (nOpca := 1,oDlg:End())  ENABLE OF oDlg
	DEFINE SBUTTON FROM 107,170 TYPE 2 ACTION (nOpca := 2,oDlg:End())  ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTERED
  Exit
Enddo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Abre o arquivo SL3 para Processar a Rotina ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Select("SL3") == 0
	ChkFile("SL3")
Endif

If nOpcA == 1
	Processa({|lEnd| lj100Processa()})	// Chamada da funcao de calculos
EndIf

dbSelectArea("SL3")

Return( Nil )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ LJ100Proc³ Autor ³ Elaine F. T. Beraldo  ³ Data ³ 15/08/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Executa Acumulados Mensais                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJ100Processa()
LOCAL nMes,cProduto,dDataIni,dDataFim,nTotQtd,nTotVlr

nMes:=nTotQtd:=nTotVlr:=0
nMes		:= MV_par01
cMes		:= StrZero(nMes,2)
dDataIni := CtoD("01/"+cMes+"/"+SubStr(Strzero(Year(dDataBase),4),3,2),"ddmmyy")
dDataFim := LastDay( dDataIni )

dbSelectArea("SL2")
dbSetOrder( 2 )
dbSeek( xFilial('SL2') )

ProcRegua(RecCount())

While SL2->( !Eof() ) .And. SL2->L2_FILIAL == xFilial('SL2')
	
	cProduto := SL2->L2_PRODUTO
	nTotQtd  := 0
	nTotVlr  := 0
	
	While SL2->( !Eof() ) .And. SL2->L2_FILIAL == xFilial('SL2') .And. SL2->L2_PRODUTO == cProduto
		
		IncProc()
		
		IF SL2->L2_VENDIDO == "S"
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se TES gera Duplicata - 16/01/96 			 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DBSelectArea("SF4")
			DBSeek(xFilial()+SL2->L2_TES)
			If SF4->F4_DUPLIC == "S"
				
				DBSelectArea("SL1")
				SL1->( DBSetOrder( 1 ) )
				SL1->( DBSeek( xFilial('SL1') + SL2->L2_NUM ) )
				IF SL1->L1_EMISSAO >= dDataIni .And. SL1->L1_EMISSAO <= dDataFim
					DBSelectArea("SL2")
					nTotQtd+=L2_QUANT
					nTotVlr+= SL2->L2_VLRITEM + SL2->L2_VALIPI
				ENDIF
				
			ENDIF
			
		ENDIF
		
		SL2->( DBSkip() )
		
	ENDDO
	
	IF nTotQtd > 0
		
		DBSelectArea("SL3")
		DBSetOrder( 1 )
		DBSeek( xFilial('SL3')+cProduto )
		IF !Found()
			RecLock("SL3",.T.)
			REPLACE L3_FILIAL  With xFilial('SL3')
			REPLACE L3_PRODUTO With cProduto
		Else
			RecLock("SL3")
		EndIF
		Replace L3_QUANT&cMes With nTotQtd
		REPLACE L3_VLRIT&cMes With nTotVlr
		MSUnlock()
		
	ENDIF
	
	DBSelectArea("SL2")
	
ENDDO

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recupera a Integridade dos dados									  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea( "SL2" )
dbSetOrder( 1 )
MsUnlockAll()
