#Include "MATA800.CH"
#Include "fivewin.ch"

Static _lNewMRP := Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Mata800  ³ Autor ³ Rosane Luciane Chene  ³ Data ³ 09.02.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa que sacramenta/desacramenta as op's               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void Mata800(void)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Rodrigo Sart.³17/08/98³15462A³Incluir parametro De  Data Ajus. Inicio ³±±
±±³              ³        ³      ³                  Ate Data Ajus. Inicio ³±±
±±³Rodrigo Sart  ³01/10/98³xxxxxx³ Acerto qdo marca tudo para sacramentar ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Mata800()
PRIVATE aPos:= {  8,  4, 11, 74 }
PRIVATE cSacram:="",cMarca := GetMark()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa  ³
//³ ----------- Elementos contidos por dimensao ------------ ³
//³ 1. Nome a aparecer no cabecalho                          ³
//³ 2. Nome da Rotina associada                              ³
//³ 3. Usado pela rotina                                     ³
//³ 4. Tipo de Transa‡„o a ser efetuada                      ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados         ³
//³    2 - Simplesmente Mostra os Campos                     ³
//³    3 - Inclui registros no Bancos de Dados               ³
//³    4 - Altera o registro corrente                        ³
//³    5 - Remove o registro corrente do Banco de Dados      ³
//³    6 - Altera determinados campos sem incluir novos Regs ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

PRIVATE  cNomeInd

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemToAnsi(STR0001)	//"Sacramento de OP's"
//VERIFICA PONTO DE ENTRADA DE MANIPULAÇÃO DA SHD
PRIVATE lM800SHD := ExistBlock("M800SHD")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01            // Sacramentar     Dessacramentar        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If pergunte("MTA801",.T.)
	cSacram := IIf(mv_par01 == 1,"S","N")
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01            // Produto de                            ³
	//³ mv_par02            // Produto ate                           ³
	//³ mv_par03            // Op de                                 ³
	//³ mv_par04            // Op ate                                ³
	//³ mv_par05            // Data prazo entrega de                 ³
	//³ mv_par06            // Data prazo entrega ate                ³
	//³ mv_par07            // De  Dt Ajustada Inicial               ³
	//³ mv_par08            // Ate Dt Ajustada Inicial               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	PRIVATE aRotina

	If pergunte("MTA800",.T.)
		If cSacram == "S"
			aRotina := {	{STR0002,"A800Param" , 0 , 1},;	//"Parƒmetro"
			{STR0003,"A800Sacram", 0 , 2} }	//"Sacramentar"
		ElseIf cSacram == "N"
			aRotina := {	{STR0002,"A800Param" , 0 , 1},;	//"Parƒmetro"
			{STR0004,"A800Desfaz", 0 , 2} }	//"Dessacramentar"
		Endif

		dbSelectArea("SC2")

		If A690CheckSC2() .And. cSacram == "S"
			Help(" ",1,"A800SACRAM")
		ElseIf A690ChkFlag(2) == 0
			Help(" ",1,"A800SACRA2")
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Filtra o arquivo conforme perguntas antes de mostrar     ³
			//³ o browse                                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			A800Filtra()
			MarkBrow("SC2","C2_OK",,,,cMarca)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Retorna indices do SC2                                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RetIndex("SC2")
			cNomeInd += OrdBagExt()
			FErase(cNomeInd)
			dbSetOrder(1)
		EndIf
	EndIf
EndIf
RETURN


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A800Sacram³ Autor ³ Rosane Luciane Chene  ³ Data ³ 09.02.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Efetua o sacramento das OP's marcadas                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata500                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a800Sacram(cALias,cCampo,nOpcE,cMarca,lInverte)
Local cDirPCP    := "",cDirOCR := ""     
Local cNameCarga := ""        
Local cEmp690 	 := Alltrim(STR(a690FilNum(FwCodFil())))
Default lAutoMacao := .F.

cNameCarga := "CARGA"+cEmp690

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variavel que indica que e' para sacramentar as op's          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSacram  := "S"

IF !lAutoMacao
	If MsgYesNo(OemToAnsi(STR0005))	//" Sacramenta as OPs marcadas ?"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o arquivo SH8 ja' esta' em uso                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If OpenSemSH8()

			If !TCCanOpen(cNameCarga+"OPE")
				A690CheckSC2(.F.)
			Else
				dbUseArea(.T.,"TOPCONN",cNameCarga+"OPE","CARGA",.F.,.F.)
				dbSetIndex(cNameCarga+"OPE"+"1")
				dbSetIndex(cNameCarga+"OPE"+"2")
				dbSetIndex(cNameCarga+"OPE"+"3")
				dbSetIndex(cNameCarga+"OPE"+"4")
				dbSetIndex(cNameCarga+"OPE"+"5")
				dbSetIndex(cNameCarga+"OPE"+"6")
				dbGotop()
			EndIf
			cDirPCP := Alltrim(GetMV("MV_DIRPCP"))
			If Empty(cDirPCP)
				HELP(' ',1,STR0008 ,,STR0009,2,0,,,,,,{STR0010})
				//"Configuração de Parametros" - "Parametro não configurado" - "Definir as informações dos parametros MV_DIRPCP, pelo configurador" 
			else
				cDirOcr := cDirPCP
				cDirPCP += IIf( Right(cDirPCP,1) # "\" , "\" , "" )
			EndIf
			
			// Efetua o sacramento das OP's
			dbSelectArea("SC2")
			dbSetOrder(1)
			Processa({|lEnd| a800Proces(cAlias,cCampo,nOpcE,cMarca,lInverte,cDirOcr,cDirPCP)})
			CloseBrowse()
			
			//-- Fecha semaforo do SH8 disponibilizando para uso
			ClosSemSH8()
		EndIf
	Endif
ENDIF

Return

/*/{Protheus.doc} A800Proces
Efetua o sacramento das OP's marcadas

@type  Function
@author Rosane Luciane Chene
@since 09/02/1996
@uso Mata500
@return .T.
/*/
Function a800Proces(cAlias,cCampo,nOpcE,cMarca,lInverte,cDirOCR,cDirPCP)

	Local i
	Local cIndice   := IndexKey()
	Local nIndex    := 0
	Local cArq      := CriaTrab(,.F.)
	Local nIndexOrd := IndexOrd()
	Local nRecno    := Recno()
	Local cOpSacr
	Local aOPSacram := {}
	Local x
	Local aM800SHD  := {}

	Local cSacramCond	:=""
	Local dData     	:= cTod("  /  /  ")
	Local lIntNewMRP := Iif(_lNewMRP  == Nil, FindFunction("Ma650MrpOn") .AND. Ma650MrpOn(@_lNewMRP),_lNewMRP)
	Local aMRPxJson  := Iif(lIntNewMRP, {{}, JsonObject():New()}, Nil) //{aDados para commit, JsonObject() com RECNOS} - Integracao Novo MRP - APONTAMENTOS

	Private lMarcatudo:=lInverte
	

	dbSelectArea("SC2")

	cSacramCond := 'C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD>="'+mv_par03+'".And.C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD<="'+mv_par04+'".And.'
	cSacramCond += 'dTos(C2_DATPRF)>="'+dTos(mv_par05)+'".And.dTos(C2_DATPRF)<="'+dTos(mv_par06)+'".And.'
	cSacramCond += 'C2_FILIAL=="'+xFilial("SC2")+'".And.dTos(C2_DATAJI)!="'+dTos(dData)+'".And.'
	cSacramCond += 'dTos(C2_DATAJF)!="'+dTos(dData)+'".And.C2_HORAJF != "'+"     "+'".And.'
	cSacramCond += 'dTos(C2_DATRF)=="'+dTos(dData)+'".And.C2_PRODUTO>="'+mv_par01+'".And.'
	cSacramCond += 'C2_PRODUTO<="'+mv_par02+'".And.dTos(C2_DATAJI)>="'+dTos(mv_par07)+'".And.'
	cSacramCond += 'dTos(C2_DATAJI)<= "'+dTos(mv_par08)+'".And.'
	If cSacram == "S"
		cSacramCond += 'C2_STATUS!="S"'
	ElseIf cSacram == "N"
		cSacramCond += 'C2_STATUS=="S"'
	EndIf
	If !lMarcaTudo
		cSacramCond+= '.And.C2_OK == "'+cMarca+'"'
	EndIf
	IndRegua("SC2",cArq,cIndice,,cSacramCond,STR0006)	//"Selecionando Registros..."

	nIndex := RetIndex("SC2")

	dbSetOrder(nIndex+1)
	dbGoTop()

	While !Eof()
		If Ascan( aOPSacram , C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD ) == 0
			Aadd( aOPSacram , C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD )
		EndIf
		dbSkip()
	End

	//Retorna indices do SC2
	RetIndex("SC2")
	cNomeInd += OrdBagExt()
	fErase(cNomeInd)
	dbSelectArea("SC2")
	dbSetOrder(1)
	dbClearFilter()

	ProcRegua(Len(aOPSacram),21,4)

	For i := 1 to Len(aOPSacram)
		cOpSacr := aOPSacram[i]
		// Apagando OP Sacramentada (caso cSacram = "N" - "dessacramentando" uma OP, esta
		// rotina ‚ executada como medide de precau‡„o).
		dbSelectArea("SHD")
		dbSetOrder(1)
		dbSeek(xFilial()+cOpSacr)
		While !Eof() .And. xFilial()+cOpSacr == HD_FILIAL+HD_OP
			RecLock("SHD",.F.,.T.)
			dbDelete()
			MsUnLock()
			
			If lM800SHD .And. cSacram = 'N'
				Aadd(aM800SHD,{SHD->HD_OP,SHD->HD_OPER})
			EndIf
			
			dbSkip()
		End
		// Apagando Ferramentas Sacramentada (caso cSacram = "N" - "dessacramentando" uma OP,
		// esta rotina ‚ executada como medide de precau‡„o).
		dbSelectArea("SHE")
		dbSeek(xFilial()+cOpSacr)
		While !Eof() .And. xFilial()+cOpSacr == HE_FILIAL+HE_OP
			RecLock("SHE",.F.,.T.)
			dbDelete()
			MsUnLock()
			dbSkip()
		End
		If cSacram == "S"
			dbSelectArea("SC2")
			dbSeek(xFilial()+cOpSacr)
			While !Eof() .And. xFilial()+cOpSacr == C2_FILIAL+C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD
				RecLock("SC2",.F.)
				Replace C2_STATUS With "S"

				//Integração de ordem de produção com o novo MRP.
				//Operação de INCLUSÃO/ALTERAÇÃO.
				//Integração de OPs filhas com o novo MRP. Carrega os dados para enviar
				If lIntNewMRP
					A650AddJIn(@aMRPxJson) //Inclui dados no array para integração com o novo MRP
				EndIf

				MsUnLock()
				dbSkip()
			End
			dbSelectArea("SH8")
			dbSetOrder(1)
			dbSeek(xFilial()+cOpSacr)
			While !Eof() .And. xFilial()+cOpSacr == H8_FILIAL+H8_OP
				If xFilial("SC2")+H8_OP # SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD
					dbSelectArea("SC2")
					dbSeek(xFilial()+SH8->H8_OP)
					If !Found()
						Help(" ",1,"A690NAOOP",,SH8->H8_OP,01,21)
						Exit
					EndIf
				EndIf
				RecLock("SHD",.T.)
				For x := 1 to SH8->(fCount())
					If (nField := SHD->(FieldPos(StrTran(SH8->(FieldName(x)), "H8_", "HD_")))) > 0
						SHD->(FieldPut(nField, SH8->(FieldGet(x))))
					Endif
				Next
				SHD->HD_FILIAL := xFilial("SHD")
				MsUnLock()
				
				If lM800SHD
					Aadd(aM800SHD,{SHD->HD_OP,SHD->HD_OPER})
				EndIf
				
				/*If Select("TMP") > 0
					dbSelectArea("TMP")
					dbSeek(xFilial("SHE")+SH8->H8_OP)
					nRecFer := Recno()
					While !Eof() .And. xFilial("SHE")+SH8->H8_OP == HE_FILIAL+HE_OP
						dbSelectArea("SHE")
						RecLock("SHE",.T.)
						Replace  HE_FILIAL With xFilial(),;
						HE_PRODUTO With TMP->HE_PRODUTO,;
						HE_CODIGO With TMP->HE_CODIGO,;
						HE_OPERAC With TMP->HE_OPERAC,;
						HE_FERRAM With TMP->HE_FERRAM,;
						HE_DTINI With TMP->HE_DTINI,;
						HE_DTFIM With TMP->HE_DTFIM,;
						HE_HRINI With TMP->HE_HRINI,;
						HE_HRFIM With TMP->HE_HRFIM,;
						HE_OP With TMP->HE_OP
						MsUnLock()
						dbSelectArea("TMP")
						dbSkip()
					End
					dbGoto(nRecFer)
					While !Eof() .And. xFilial("SHE")+SH8->H8_OP == HE_FILIAL+HE_OP
						dbDelete()
						dbSkip()
					End
				EndIf*/
				dbSelectArea("SH8")
				RecLock('SH8', .F.)
				Replace H8_STATUS With "S"
				MsUnlock()
				dbSkip()
			End
			If Select("CARGA") > 0
				dbSelectArea("CARGA")
				dbSetOrder(1)
				dbSeek(xFilial()+cOpSacr)
				While !Eof()
					If H8_OP # cOpSacr
						Exit
					EndIf
					RecLock('CARGA', .F.)
					Replace H8_STATUS With "S"
					MsUnlock()
					dbSkip()
				End
			EndIf
		Else
			dbSelectArea("SC2")
			dbSeek(xFilial()+cOpSacr)
			While !Eof() .And. xFilial()+cOpSacr == C2_FILIAL+C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD
				RecLock("SC2",.F.)
				Replace C2_STATUS With "N"

				//Integração de ordem de produção com o novo MRP.
				//Operação de INCLUSÃO/ALTERAÇÃO.
				//Integração de OPs filhas com o novo MRP. Carrega os dados para enviar
				If lIntNewMRP
					A650AddJIn(@aMRPxJson) //Inclui dados no array para integração com o novo MRP
				EndIf

				MsUnLock()
				dbSkip()
			End
			dbSelectArea("SH8")
			dbSetOrder(1)
			dbSeek(xFilial()+cOpSacr)
			While !Eof() .And. xFilial()+cOpSacr == H8_FILIAL+H8_OP
				Reclock("SH8",.F.)
				Replace H8_STATUS With " "
				MsUnlock()
				dbSkip()
			End
			If Select("CARGA") > 0
				dbSelectArea("CARGA")
				dbSetOrder(1)
				dbSeek(xFilial()+cOpSacr)
				While !Eof()
					If H8_OP # cOpSacr
						Exit
					EndIf
					Reclock("CARGA",.F.)
					Replace H8_STATUS With " "
					MsUnlock()
					dbSkip()
				End
			EndIf
		EndIf
	Next i

	//Integração com MRP
	If lIntNewMRP
		enviaOpMrp("INSERT", @aMRPxJson)
	EndIf

	If lM800SHD
		ExecBlock("M800SHD",.F.,.F.,{aM800SHD,cSacram})
	EndIf

	dbSelectArea("SH8")
	dbCloseArea()
	/*If Select("TMP") > 0
		dbSelectArea("TMP")
		dbCloseArea()
	EndIf*/
	If Select("CARGA") > 0
		dbSelectArea("CARGA")
		dbCloseArea()
	EndIf
	dbSelectArea(cAlias)
	dbSetOrder(nIndexOrd)
	dbGoto(nRecno)
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A800Filtra³ Autor ³ Rosane Luciane Chene  ³ Data ³ 09/02/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Indice condicional conforme as perguntas                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata800                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A800Filtra()
	Local cIndice,nInd
	Local cSacramCond	:=""
	Local dData     	:= cTod("  /  /  ")

	cNomeInd:=CriaTrab(NIL,.F.)
	
	//Gera index de trabalho do SC6
	dbSelectArea("SC2")
	dbSetOrder(1)
	cIndice   := Indexkey()

	cSacramCond := 'C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD>="'+mv_par03+'".And.C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD<="'+mv_par04+'".And.'
	cSacramCond += 'dTos(C2_DATPRF)>="'+dTos(mv_par05)+'".And.dTos(C2_DATPRF)<="'+dTos(mv_par06)+'".And.'
	cSacramCond += 'C2_FILIAL=="'+xFilial("SC2")+'".And.dTos(C2_DATAJI)!="'+dTos(dData)+'".And.'
	cSacramCond += 'dTos(C2_DATAJF)!="'+dTos(dData)+'".And.C2_HORAJF != "'+"     "+'".And.'
	cSacramCond += 'dTos(C2_DATRF)=="'+dTos(dData)+'".And.C2_PRODUTO>="'+mv_par01+'".And.'
	cSacramCond += 'C2_PRODUTO<="'+mv_par02+'".And.dTos(C2_DATAJI)>="'+dTos(mv_par07)+'".And.'
	cSacramCond += 'dTos(C2_DATAJI)<= "'+dTos(mv_par08)+'".And.'
	
	If cSacram == "S"
		cSacramCond += 'C2_STATUS!="S"'
	ElseIf cSacram == "N"
		cSacramCond += 'C2_STATUS=="S"'
	EndIf
			
	//Ponto de Entrada para alterar ou complementar o filtro na SC2
	If ExistBlock("MA800FIL")
		cSacramCond := ExecBlock("MA800FIL",.F.,.F.,{cSacramCond})
	Endif
		
	IndRegua("SC2",cNomeInd,cIndice,,cSacramCond,STR0006)	//"Selecionando Registros..."

	nInd := RetIndex("SC2")
	dbSetOrder(nInd+1)
	dbGoTop()
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A800Param³ Autor ³ Rosane Luciane Chene  ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para parametrizacao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA800                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A800Param(cAlias,nReg,nOpc)
Default lAutoMacao := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTA800",.T.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01            // Produto de                            ³
//³ mv_par02            // Produto ate                           ³
//³ mv_par03            // Op de                                 ³
//³ mv_par04            // Op ate                                ³
//³ mv_par05            // Data prazo entrega de                 ³
//³ mv_par06            // Data prazo entrega ate                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

A800Filtra()
// Pendˆncia:
IF !lAutoMacao
	MarkBRefresh()
ENDIF

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A800Desfaz³ Autor ³ Rosane Luciane Chene  ³ Data ³ 09.02.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Elimina a marca de op's sacramentada                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata800                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a800Desfaz(cALias,cCampo,nOpcE,cMarca,lInverte)
Local cNameCarga   := "CARGA"+cNumEmp //Nome do arquivo de Carga
Default lAutoMacao := .F.

cSacram := "N"

dbSelectArea("SC2")

IF !lAutoMacao
	If MsgYesNo(OemToAnsi(STR0007))	//" Dessacramenta as OPs marcadas ?"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o arquivo SH8 ja' esta' em uso                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If OpenSemSH8()

			If !TCCanOpen(cNameCarga+"OPE")
				A690CheckSC2(.F.)
			Else
				dbUseArea(.T.,"TOPCONN",cNameCarga+"OPE","CARGA",.F.,.F.)
				dbSetIndex(cNameCarga+"OPE"+"1")
				dbSetIndex(cNameCarga+"OPE"+"2")
				dbSetIndex(cNameCarga+"OPE"+"3")
				dbSetIndex(cNameCarga+"OPE"+"4")
				dbSetIndex(cNameCarga+"OPE"+"5")
				dbSetIndex(cNameCarga+"OPE"+"6")
				dbGotop()
			EndIf

			cDirPCP := Alltrim(GetMV("MV_DIRPCP"))
			If Empty(cDirPCP)
				HELP(' ',1,STR0008 ,,STR0009,2,0,,,,,,{STR0010})
				//"Configuração de Parametros" - "Parametro não configurado" - "Definir as informações dos parametros MV_DIRPCP, pelo configurador" 
			else
				cDirOcr := cDirPCP
				cDirPCP += IIf( Right(cDirPCP,1) # "\" , "\" , "" )
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Efetua o sacramento das OP's                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SC2")
			dbSetOrder(1)
			Processa({|lEnd| a800Proces(cAlias,cCampo,nOpcE,cMarca,lInverte,cDirOcr,cDirPCP)})
			CloseBrowse()

			//-- Fecha semaforo para SH8
			ClosSemSH8()
		EndIf
	Endif
ENDIF
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A800Sel1  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 01/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Seleciona os itens validos no SC2 pois montando cSacramCond³±±
±±³          ³ a String e' maior que 246 caracteres (da erro Corruption)  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata800                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a800Sel1()
Local dData     	:= cTod("  /  /  ")
Local lRet:=.T.
If C2_FILIAL == xFilial("SC2") .And. ;
	(C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD >= mv_par03) .And. (C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD <= mv_par04) .And. ;
	(dTos(C2_DATPRF) >= dTos(mv_par05)) .And. (dTos(C2_DATPRF) <= dTos(mv_par06)) .And. ;
	(dTos(C2_DATAJI) != dTos(dData)) .And. (dTos(C2_DATAJF) != dTos(dData)) .And. ;
	(C2_HORAJF != "     ") .And. (dTos(C2_DATRF) == dTos(dData)) .And.	(C2_PRODUTO >= mv_par01) .And. ;
	(C2_PRODUTO <= mv_par02) .And. dTos(C2_DATAJI) >= dTos(mv_par07) .And. ;
	dTos(C2_DATAJI)<= dTos(mv_par08) .And. If(!lMarcaTudo,(C2_OK == cMarca),.T.)
	If cSacram == "S" .And. C2_STATUS == "S"
		lRet:=.F.
	ElseIf cSacram == "N" .And. C2_STATUS != "S"
		lRet:=.F.
	Else
		lRet:=.T.
	EndIf
Else
	lRet:=.F.
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A800Sel2  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 01/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Seleciona os itens validos no SC2 pois montando cSacramCond³±±
±±³          ³ a String e' maior que 246 caracteres (da erro Corruption)  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata800                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a800Sel2()
Local dData     	:= cTod("  /  /  ")
Local lRet:=.T.
If C2_FILIAL == xFilial("SC2") .And. ;
	(C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD >= mv_par03) .And. (C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD <= mv_par04) .And. ;
	(dTos(C2_DATPRF) >= dTos(mv_par05)) .And. (dTos(C2_DATPRF) <= dTos(mv_par06)) .And. ;
	(dTos(C2_DATAJI) != dTos(dData)) .And. (dTos(C2_DATAJF) != dTos(dData)) .And. ;
	(C2_HORAJF != "     ") .And. (dTos(C2_DATRF) == dTos(dData)) .And.	(C2_PRODUTO >= mv_par01) .And. ;
	(C2_PRODUTO <= mv_par02) .And. dTos(C2_DATAJI) >= dTos(mv_par07) .And. ;
	dTos(C2_DATAJI)<= dTos(mv_par08)
	If cSacram == "S" .And. C2_STATUS == "S"
		lRet:=.F.
	ElseIf cSacram == "N" .And. C2_STATUS != "S"
		lRet:=.F.
	Else
		lRet:=.T.
	EndIf
Else
	lRet:=.F.
EndIf
Return lRet



/*/{Protheus.doc} enviaOpMrp
Envia os empenhos para o MRP

@type  Static Function
@author brunno.costa
@since 23/07/2019
@version P12.1.27
@param cOperac    , Character, Operação em execução (INSERT/DELETE)
@param aMRPxJson  , Array    , Array com os dados para enviar - APONTAMENTOS.
@param lDelTBMRP  , logico   , indica se deve excluir a tabela temporaria
@return Nil
/*/
Static Function enviaOpMrp(cOperac, aMRPxJson, lDelTBMRP)
	Local aAreaAtu   := GetArea()
	Default lAutoMacao := .F.

	//Integra os dados com a API - APONTAMENTOS
	If _lNewMRP .and. aMRPxJson != Nil .and. Len(aMRPxJson[1]) > 0
		IF !lAutoMacao
			MATA650INT(cOperac, aMRPxJson[1])
			aSize(aMRPxJson[1], 0)
			FreeObj(aMRPxJson[2])
			aMRPxJson[2] := Nil
		ENDIF
	EndIf

	//Inicializa variável de controle da integração com o novo mrp.
	If lDelTBMRP
		_lNewMRP := Nil
	EndIf

	RestArea(aAreaAtu)
Return Nil
