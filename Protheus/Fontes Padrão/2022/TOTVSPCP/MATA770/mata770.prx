#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "MATA770.CH"

/*/{Protheus.doc} MATA770
Programa de atualizacao do cadastro de Centro de Trabalho (MVC)
@author lucas.franca
@since 18/06/2018

@param aRotAuto	- Array dos campos para rotina automática
@param nOpcAuto	- Número da opção selecionada para rotina automática
@return Nil
/*/
Function MATA770(aRotAuto,nOpcAuto)
	Local aArea    := GetArea()
	Local lRotAuto := aRotAuto <> Nil
	Local oBrowse
	Private aRotina := {}
	
	If !lRotAuto
		oBrowse := BrowseDef()	
		oBrowse:Activate()
	Else
		aRotina := MenuDef()
		FWMVCRotAuto(ModelDef(), "SHB", nOpcAuto, {{"SHBMASTER", aRotAuto}})
	EndIf
	
	RestArea(aArea)
Return NIL

/*/{Protheus.doc} BrowseDef
Definição do BROWSE do cadastro de centro de trabalho.
@author lucas.franca
@since 18/06/2018
@version 1.0
@return oBrowse	- Objeto FWMBrowse.
/*/
Static Function BrowseDef()
	Local oBrowse := FWMBrowse():New()
	
	oBrowse:SetAlias("SHB")
	oBrowse:SetDescription( STR0006 ) //Centro de trabalho
Return oBrowse

/*/{Protheus.doc} ModelDef
Definição do modelo de dados para o cadastro de centro de trabalho
@author lucas.franca
@since 18/06/2018
@version 1.0
@return oModel	- Modelo de dados do programa.
/*/
Static Function ModelDef()
	Local oStruSHB := FWFormStruct( 1, "SHB")
	Local oEvent   := MATA770EVDEF():New()
	Local oModel
	
	oModel := MPFormModel():New("MATA770")
	oModel:AddFields("SHBMASTER", /*cOwner*/, oStruSHB )
	oModel:SetDescription( STR0006 ) //Centro de trabalho
	If Empty(FWX2Unico("SHB"))
		oModel:SetPrimaryKey({"HB_FILIAL","HB_COD"})
	EndIf
	
	//Instala o evento padrão do programa.
	oModel:InstallEvent("MATA770EVDEF",/*cOwner*/,oEvent)
	
Return oModel

/*/{Protheus.doc} ViewDef
Definição da tela para o cadastro de centro de trabalho
@author lucas.franca
@since 18/06/2018
@version 1.0
@return oView	- Objeto View para a tela
/*/
Static Function ViewDef()
	Local oModel   := FWLoadModel( 'MATA770' )
	Local oStruSHB := FWFormStruct( 2, 'SHB' )
	Local oView
	
	oView := FWFormView():New()
	oView:SetModel(oModel)
	oView:AddField("VIEW_SHB", oStruSHB, "SHBMASTER" )
	oView:CreateHorizontalBox("TELA",100)
	oView:SetOwnerView("VIEW_SHB","TELA")
Return oView

/*/{Protheus.doc} MenuDef
Função para definir as opções de MENU do programa
@author lucas.franca
@since 18/06/2018
@version 1.0
@return aRotina
/*/
Static Function MenuDef()
	Local aRotina := {}
	Local aAdic := {}
	ADD OPTION aRotina Title STR0002 Action "VIEWDEF.MATA770" OPERATION OP_VISUALIZAR ACCESS 0 //Visualizar
	ADD OPTION aRotina Title STR0003 Action "VIEWDEF.MATA770" OPERATION OP_INCLUIR    ACCESS 0 //Incluir
	ADD OPTION aRotina Title STR0004 Action "VIEWDEF.MATA770" OPERATION OP_ALTERAR    ACCESS 0 //Alterar
	ADD OPTION aRotina Title STR0005 Action "VIEWDEF.MATA770" OPERATION OP_EXCLUIR    ACCESS 0 //Excluir
	
	//Ponto de entrada utilizado para inserir novas opcoes no array aRotina
	If ExistBlock("MTA770MNU")
		aAdic := ExecBlock("MTA770MNU",.F.,.F.)
		 If ValType( aAdic ) == 'A'
           aEval( aAdic, { |X| aAdd( aRotina, X ) } )
     	EndIf
	EndIf
	
Return aRotina

/*/{Protheus.doc} A770Inclui
Função para tratar a consulta padrão da SHB do legado
@author ricardo.prandi	
@since 11/12/2018
@version 1.0
@return nil
/*/
Function A770Inclui()
	
	FWExecView(STR0003, "MATA770", MODEL_OPERATION_INSERT,,,,,,,,,)
	
Return 