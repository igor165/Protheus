#INCLUDE "MATA682.CH"
#INCLUDE "FIVEWIN.CH"

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё MATA682  Ё Autor Ё Rodrigo de A. Sartorio Ё Data Ё 08/01/97 Ё╠╠
╠╠цддддддддддеддддддддддадддддддаддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Apontamento de Horas Improdutivas                           Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void MATA682(void)                                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function MATA682(aRotAuto,nOpcx)
/*
   VariАvel nRegSH6 И utilizada para posicionar no registro correto, quando for realizar o estorno.
   Quando И executado o estorno atravИs de rotina automАtica, a funГЦo MsRotAuto pode desposicionar
   do registro da SH6. Isso ocorre quando existem 2 apontamentos para mesma OP/PRODUTO/OPERACAO/SEQ/DATAINI/HORAINI/DATAFIM/HORAFIM,
   pois dentro da funГЦo MsRotAuto И realizado um seek na tabela SH6, que posiciona na primeira ocorrencia deste indice.
*/
Local lReposic   := Iif(Type("nRegSH6")=="N" .And. nRegSH6 > 0, SH6->(dbGoTo(nRegSH6)), .F.)
Local lA682Cpos:=ExistBlock("A682CPO")
Local aBackAcho:={}

Local nPos    := 0
Local lFiltro := .F.

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas para indicar qual apontamento esta sendo utilizado Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE l680:=.F.,l681:=.F.,l682:=.T.,l240:=.F.,l241:=.F.
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInicializa variavel que sera usada no processo de rotina automaticaЁ
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Private l682Auto := (valtype(aRotAuto) == "A")
Private l680Auto := (valtype(aRotAuto) == "A")

PRIVATE aTrocaF3 :={}      // Para PE trocar consulta F3 de SH8 para SC2

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Array com os campos que devem aparecer na Enchoice                      Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aAcho:={}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё lPerdInf: Indica se a perda e informativa ou nao    		 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE lPerdInf := .f.

//здддддддддддддддддддддддддддддддддддддддддддддд©
//ЁArray para o controle contАbil de correlativosЁ
//юдддддддддддддддддддддддддддддддддддддддддддддды
Private aCtbDia	 := {}

AADD(aAcho,"H6_RECURSO")
AADD(aAcho,"H6_FERRAM")
AADD(aAcho,"H6_DATAINI")
AADD(aAcho,"H6_HORAINI")
AADD(aAcho,"H6_DATAFIN")
AADD(aAcho,"H6_HORAFIN")
AADD(aAcho,"H6_DTAPONT")
AADD(aAcho,"H6_TEMPO")
AADD(aAcho,"H6_MOTIVO")
AADD(aAcho,"H6_DESCRI")
AADD(aAcho,"H6_OBSERVA")
AADD(aAcho,"H6_OPERADO")

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa P.E. para selecionar campos que devem aparecer na Enchoice      Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lA682Cpos
	aBackAcho:=ACLONE(aAcho)
	aAcho:=ExecBlock("A682CPO",.F.,.F.,aAcho)
	If Valtype(aAcho) != "A"
		aAcho:=ACLONE(aBackAcho)
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Array contendo as Rotinas a executar do programa  Ё
//Ё ----------- Elementos contidos por dimensao -----------  Ё
//Ё 1. Nome a aparecer no cabecalho                          Ё
//Ё 2. Nome da Rotina associada                              Ё
//Ё 3. Usado pela rotina                                     Ё
//Ё 4. Tipo de Transa┤└o a ser efetuada                      Ё
//Ё    1 - Pesquisa e Posiciona em um Banco de Dados         Ё
//Ё    2 - Simplesmente Mostra os Campos                     Ё
//Ё    3 - Inclui registros no Bancos de Dados               Ё
//Ё    4 - Altera o registro corrente                        Ё
//Ё    5 - Remove o registro corrente do Banco de Dados      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aRotina := MenuDef()

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define o cabecalho da tela de atualizacoes               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE cCadastro := OemToAnsi(STR0005)	//"Horas Improdutivas"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Pega a variavel que identifica se o calculo do custo e' :    Ё
//Ё               O = On-Line                                    Ё
//Ё               M = Mensal                                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE cCusMed := GetMv("mv_CusMed")

If cCusMed == "O"
	PRIVATE nHdlPrv // Endereco do arquivo de contra prova dos lanctos cont.
	PRIVATE lCriaHeader := .T. // Para criar o header do arquivo Contra Prova
	PRIVATE cLoteEst  // Numero do lote para lancamentos do estoque
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Posiciona numero do Lote para Lancamentos do Faturamento     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SX5")
	dbSeek(xFilial()+"09EST")
	cLoteEst:=IIF(Found(),Trim(X5Descri()),"EST ")
	PRIVATE nTotal := 0  // Total dos lancamentos contabeis
	PRIVATE cArquivo  // Nome do arquivo contra prova
Endif

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Abaixo checa se executado procedimento "TimeH6x" para ajuste de campos hora do SH6  Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ! A680ChkH6Time()
	Return
Endif

ASHICalen(,,, .T.)

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inclui pergunta no SX1                                        Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
pergunte("MTA680",.F.)
If !l682Auto
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Endereca a funcao de BROWSE                              Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	mBrowse( 6, 1,22,75,"SH6")
Else
	If nOpcx == 5
		nPos := 0
		nPos := aScan(aRotAuto,{|x| x[1] == 'AUTRECNO'})
		If nPos > 0
			SH6->(dbGoTo(aRotAuto[nPos,2]))
			lFiltro := .T.
		Else
			nPos := 0
			nPos := aScan(aRotAuto,{|x| x[1] == 'INDEX'})
			If nPos > 0
				EasySeekAuto("SH6",aRotAuto,aRotAuto[nPos,2])
				lFiltro := .T.
			EndIf
		EndIf
		If lFiltro
			MBrowseAuto(nOpcx,aRotAuto,"SH6",.F.)
			Return
		EndIf				
	EndIf

	MsRotAuto(nOpcx,aRotAuto,"SH6",aAcho)
EndIf



/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA682VisualЁ Autor Ё Rodrigo de A. SartorioЁ Data Ё 08/01/97 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa para visualizar apontamento de horas improdutivas Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A682Visual(ExpC1,ExpN1,ExpN2)                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA240                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A682Visual(cAlias,nReg,nOpc)
Local aButtons   := {}
Local aUsButtons := {}
Local lAtuCYN  := .F.

CYN->(dbSelectArea("CYN"))
If CYN->(FieldPos("CYN_LGMOD")) > 0
	lAtuCYN := .T.
EndIf

dbSelectArea(cAlias)
If H6_TIPO != "I"
	Help(" ",1,"A682NAO")
	Return
EndIf
RegToMemory("SH6", .F., .T.)
If !lAtuCYN
	M->H6_DESCRI := Posicione("SX5", 1, xFilial("SX5") + "44" + M->H6_MOTIVO, "X5DESCRI()")
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada p/ adicionar botoes de usuario na EnchoiceBarЁ
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock( "MA682BUT" )
	If ValType( aUsButtons := ExecBlock( "MA682BUT", .F., .F.,{nOpc} ) ) == "A"
		AEval( aUsButtons, { |x| AAdd( aButtons, x ) } )
	EndIf
EndIf

AxVisual(cAlias,nReg,nOpc,aAcho,,,"A680MEMVAR",aButtons)

Return Nil
/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA682IncluiЁ Autor Ё Rodrigo de A. Sartorio Ё Data Ё 08/01/97 Ё╠╠
╠╠цддддддддддеддддддддддадддддддаддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de inclusao do apontamento de horas improdutivas   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A680Inclui(cExp1,nExp1,nExp2)                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ cExp1 = Alias do Arquivo                                    Ё╠╠
╠╠Ё          Ё nExp1 = Numero do registro                                  Ё╠╠
╠╠Ё          Ё cExp1 = Opcao escolhida                                     Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA682                                                     Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A682Inclui(cAlias,nReg,nOpc)
Static cNaoMOD   := NIL
Local nOpca,cIdent
Local lGerouMod  := .T.
Local lRet		 := .T.
Local lIntSFC	 := IntegraSFC() .And. !IsInCallStack("AUTO682")						
Local cProdMod   := ""
Local aButtons   := {}
Local aUsButtons := {}
Local nX := 0
Local lGeraMod
Local lAtuCYN  := .F.

CYN->(dbSelectArea("CYN"))
If CYN->(FieldPos("CYN_LGMOD")) > 0
	lAtuCYN := .T.
EndIf

PRIVATE nRegD3

Private lModZero := .T.

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁImpede inclusao de horas improdutivas devido integraГЦo com Chao de FabricaЁ
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRet .And. lIntSFC
	Aviso("AtenГЦo"," O apontamento de horas improdutivas deverА ser feito atrАves do mСdulo ChЦo de FАbrica.",{"OK"})
	lRet:= .F. 
EndIf

If !lAtuCYN
	cNaoMOD:= If(ValType(cNaoMOD) # "C",GetMV("MV_MOTNMOD"),cNaoMOD)
EndIf


//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada p/ adicionar botoes de usuario na EnchoiceBarЁ
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRet .And. ExistBlock( "MA682BUT" )
	If ValType( aUsButtons := ExecBlock( "MA682BUT", .F., .F.,{nOpc} ) ) == "A"
		AEval( aUsButtons, { |x| AAdd( aButtons, x ) } )
	EndIf
EndIf

If lRet .And. (Type("l682Auto") != "L" .or. !l682Auto)
	nOpca := AxInclui(cAlias,nReg,nOpc,aAcho,,,"A680TudoOk()",,,aButtons)
ElseIf lRet
	nOpca := AxIncluiAuto(cAlias,"A680TudoOk()")
EndIf

If lAtuCYN
	If PADR(Nil,tamSX3('H6_MOTIVO')[1]) == (cAlias)->H6_MOTIVO
		lGeraMod := .T.
	Else
		lGeraMod := POSICIONE( "CYN", 1, XFILIAL( "CYN" ) + (cAlias)->H6_MOTIVO, "CYN_LGMOD")
	EndIf
Else
	lGeraMod := !(SH6->H6_MOTIVO $ cNaoMOD)	
EndIf

If lRet .And. nOpca == 1
	Begin Transaction
		//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Envia p/func. de atualizacoes (SD3,SB2,SB3,SC2,..) Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
		cIdent:=ProxNum()
		If lGeraMod
			If (lGerouMod := A680GeraD3("MOD",cIdent))
				A240Atu()
				//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Solucao para corrigir identificacao errada da RE.  Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("SD3")
				dbGoto(nRegD3)
				RecLock("SD3",.F.)
				Replace D3_CF With "RE1"
			Endif
		EndIf
		If lModZero 
			dbSelectArea("SH6")
			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Solucao para gravar perda de MOD no SH6.           Ё
			//Ё O tipo de apontamento deve ser "I" de Improdutiva, Ё
			//Ё o produto a ser gravado ┌ a MOD e a quantidade ┌   Ё
			//Ё gravada no campo de quantidade perdida.            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			
				RecLock("SH6")
				If lGeraMod
					If lGerouMod
						Replace H6_QTDPERD	With SD3->D3_QUANT
					Endif
					Replace H6_PRODUTO	With SD3->D3_COD
				EndIf
			
			If !lGerouMod .And. !IsProdMod(H6_PRODUTO)
				//здддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Estorna Movimentacao da Mao de Obra.             Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддды
				cProdMod:=APrModRec(SH1->H1_CODIGO)
				Replace H6_PRODUTO with cProdMod 
			Endif

			Replace H6_IDENT	With cIdent
			Replace H6_TIPO		With "I"
			Replace H6_TIPOTEM with mv_par03

		Endif
	End Transaction
ElseIf lRet
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se o custo medio e' calculado On-Line               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If cCusMed == "O"
		If !lCriaHeader
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Inicializa perguntas deste programa                          Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё mv_par01 - Se mostra e permite digitar lancamentos contabeis   Ё
			//Ё mv_par02 - Se deve aglutinar os lancamentos contabeis          Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			pergunte("MTA680",.F.)
			lDigita   := Iif(mv_par01 == 1,.T.,.F.)
			lAglutina := Iif(mv_par02 == 1,.T.,.F.)
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Se ele criou o arquivo de prova ele deve gravar o rodape'    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			RodaProva(nHdlPrv,nTotal)
			
			//зддддддддддддддддддддддддддд©
			//ЁAtualiza o cСdigo de diАrioЁ
			//юддддддддддддддддддддддддддды
			If !Empty(aCtbDia) 
				cCodDiario := CtbaVerdia()
				For nX := 1 to Len(aCtbDia)
					aCtbDia[nX][3] := cCodDiario 
				Next nX
			EndIf
			  
			If cA100Incl(cArquivo,nHdlPrv,3,cLoteEst,lDigita,lAglutina,,,,,,aCtbDia)
				RecLock("SD3",.F.)
				Replace SD3->D3_DTLANC With ddatabase
				MsUnlock()
			EndIf			
			lCriaHeader := .T.
			KeyBoard Chr(27)
		EndIf
	EndIf
EndIf
If lRet .And. ExistBlock("A682INCL")
	ExecBlock("A682INCL",.F.,.F.)
Endif

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA682DeletaЁ Autor Ё Rodrigo de A. Sartorio Ё Data Ё 08/01/97 Ё╠╠
╠╠цддддддддддеддддддддддадддддддаддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de exclusao do apontamento de horas improdutivas   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void A682Deleta(cExp1,nExp1,nExp2)                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ cExp1 = Alias do Arquivo                                    Ё╠╠
╠╠Ё          Ё nExp1 = Numero do registro                                  Ё╠╠
╠╠Ё          Ё cExp1 = Opcao escolhida                                     Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё MATA682                                                     Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A682Deleta(cAlias,nReg,nOpc)
/*
   VariАvel nRegSH6 И utilizada para posicionar no registro correto, quando for realizar o estorno.
   Quando И executado o estorno atravИs de rotina automАtica, a funГЦo MsRotAuto pode desposicionar
   do registro da SH6. Isso ocorre quando existem 2 apontamentos para mesma OP/PRODUTO/OPERACAO/SEQ/DATAINI/HORAINI/DATAFIM/HORAFIM,
   pois dentro da funГЦo MsRotAuto И realizado um seek na tabela SH6, que posiciona na primeira ocorrencia deste indice.
*/
Local lReposic
Local LVar01:=1,nLinhas:=0,bCampo,nOpcA:=0,cCpoFil,cIdent
Local i, lOk    	:= .T.
Local nX			:= 0
Local oDlg
Local cQuery    	:= ""
Local cAliasQry 	:= "" 
Local lIntACD		:= SuperGetMV("MV_INTACD",.F.,"0") == "1"
Local lIntSFC		:= IntegraSFC() .And. !IsInCallStack("AUTO682")						
Local aButtons  	:= {}
Local aUsButtons	:= {}
Local aSize      	:= MsAdvSize() 
Local aInfo		:= { aSize[1], aSize[2], aSize[3], aSize[4], 3, 3 }
Local aPosObj		:= {}
Local aObjects		:= {}
Local lMA682BUT   := ExistBlock( "MA682BUT" )
Local lMT682EST   := ExistBlock("MT682EST")
Local lMT682GREST := ExistBlock("MT682GREST")
Local lAtuCYN  := .F.
Local cH6tipo 

CYN->(dbSelectArea("CYN"))
If CYN->(FieldPos("CYN_LGMOD")) > 0
	lAtuCYN := .T.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa perguntas deste programa                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё mv_par01 - Se mostra e permite digitar lancamentos contabeis   Ё
//Ё mv_par02 - Se deve aglutinar os lancamentos contabeis          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
pergunte("MTA680",.F.)
lDigita   := Iif(mv_par01 == 1,.T.,.F.)
lAglutina := Iif(mv_par02 == 1,.T.,.F.)

If H6_CBFLAG == "1"
	cH6tipo := H6_TIPO
EndIf 

IF !(FindFunction('IntegraSFC') .And. IntegraSFC())
	lReposic := Iif(Type("nRegSH6")=="N" .And. nRegSH6 > 0, SH6->(dbGoTo(nRegSH6)), .F.)
Else
	SH6->(dbGoTo(nReg))
Endif

AAdd( aObjects, { 100, 100, .T., .T. } )
aPosObj := MsObjSize( aInfo, aObjects, .T.)

If !Empty(cH6tipo)
	If cH6tipo != "I"
		Help(" ",1,"A682NAO")
		Return
	EndIf
Else 
	If H6_TIPO != "I"
		Help(" ",1,"A682NAO")
		Return
	EndIf
EndIf 

While .T.
	//здддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ver. se esta' alterando registro da mesma filial Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддды
	If H6_FILIAL != xFilial("SH6")
		Help(" ",1,"A000FI")
		Return .T.
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta a entrada de dados do arquivo              Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддды
	Private aTELA[0][0],aGETS[0]
	bCampo := {|nCPO| Field(nCPO) }
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Salva a integridade dos campos de Bancos de DadosЁ
	//юдддддддддддддддддддддддддддддддддддддддддддддддддды
	
	SoftLock(cAlias)
	
	RegToMemory("SH6", .F., .T.)
	If !lAtuCYN
		M->H6_DESCRI := Posicione("SX5", 1, xFilial("SX5") + "44" + M->H6_MOTIVO, "X5DESCRI()")	
	EndIf
	For i := 1 TO FCount()
		M->&(EVAL(bCampo,i)) := FieldGet(i)
	Next i
	
	A680MemVar() // Padroniza variАveis de memoria

	If l682Auto
	   nOpcA:= 2
	Else 

	DEFINE MSDIALOG oDlg TITLE cCadastro OF oMainWnd PIXEL FROM aSize[7],0 TO aSize[6],aSize[5]	
	nOpcA:=EnChoice( cAlias, nReg, nOpc,,,,aAcho,aPosObj[1],,,,,,,,.T.)

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ponto de entrada p/ adicionar botoes de usuario na EnchoiceBarЁ
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lMA682BUT
		If ValType( aUsButtons := ExecBlock( "MA682BUT", .F., .F.,{nOpc} ) ) == "A"
			AEval( aUsButtons, { |x| AAdd( aButtons, x ) } )
		EndIf
	EndIf
	
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()},,aButtons)
	Endif
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Integracao com o ACD - Faz validacao da exclusao de Hrs Improdutivas.Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lOk .And. lIntACD
	   lOk:=CBMT682EST()		
	   If ValType(lOk) # "L"
	      lOk:=.T.
	   EndIf
	ElseIf lOk .And. ExistTemplate("MT682EST") // --> Inclusao de Ponto de Entrada
	   lOk:=ExecTemplate("MT682EST",.F.,.F.)
	   If ValType(lOk) # "L"
	      lOk:=.T.
	   EndIf
	EndIf
	
	If lOK .and. lMT682EST
	   lOk:=ExecBlock("MT682EST",.F.,.F.)
	   If ValType(lOk) # "L"
	      lOk:=.T.
	   EndIf
	EndIf

	If lOk .And. lIntSFC
		lOk := A682IntSFC()
	EndIf

	If lOk .and. nOpcA == 2
		cIdent := SH6->H6_IDENT
		
		cAliasQry := GetNextAlias()
		cQuery := "SELECT "
		cQuery += "SD3.D3_FILIAL, SD3.D3_OP, SD3.D3_COD, SD3.D3_LOCAL, SD3.D3_IDENT, SD3.D3_ESTORNO, R_E_C_N_O_ SD3RecNo "
		cQuery += "FROM "
		cQuery += RetSqlName("SD3")+ " SD3 "
		cQuery += "WHERE "
		cQuery += "SD3.D3_FILIAL = '" + xFilial("SD3") + "' AND "
		cQuery += "SD3.D3_OP = '" + SH6->H6_OP + "' AND "
		cQuery += "SD3.D3_COD = '" + SH6->H6_PRODUTO + "' AND "
		cQuery += "SD3.D3_IDENT = '" + cIdent + "' AND "
		cQuery += "SD3.D3_ESTORNO = '"+ Criavar("D3_ESTORNO",.F.) +"' AND "
		cQuery += "SD3.D_E_L_E_T_ = ''"
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.T.,.T.)
   	
   		Begin Transaction
			//здддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Estorna Movimentacao da Mao de Obra.             Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддды
			If !(cAliasQry)->(Eof())
				SD3->(DbGoTo( (cAliasQry)->SD3RecNo))			
				a240DesAtu()
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verifica se o custo medio e' calculado On-Line               Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If cCusMed == "O"

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Se ele criou o arquivo de prova ele deve gravar o rodape'    Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					RodaProva(nHdlPrv,nTotal)
			
					//зддддддддддддддддддддддддддд©
					//ЁAtualiza o cСdigo de diАrioЁ
					//юддддддддддддддддддддддддддды
					If !Empty(aCtbDia) 
						cCodDiario := CtbaVerdia()
						For nX := 1 to Len(aCtbDia)
							aCtbDia[nX][3] := cCodDiario 
						Next nX
					EndIf
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Se ele criou o arquivo de prova ele deve gravar o rodape'    Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					RodaProva(nHdlPrv,nTotal)
					If UsaSeqCor()
						cCodDiario := ctbaVerdia()
						aCtbDia := {{"SD3",SD3->(Recno()),cCodDiario,"D3_NODIA","D3_DIACTB"}}
					Else
				    	aCtbDia := {}
					EndIF
					If cA100Incl(cArquivo,nHdlPrv,3,cLoteEst,lDigita,lAglutina,,,,,,aCtbDia)
						If !Empty(SD3->D3_DTLANC)
							RecLock("SD3",.F.)
							Replace SD3->D3_DTLANC With ddatabase
							MsUnlock()
						EndIf							
					EndIf
					lCriaHeader := .T.
				EndIf
			Else
				dbSelectArea("SD3")
				dbSetOrder(1)
				If dbSeek(xFilial("SD3")+SH6->H6_OP+SH6->H6_PRODUTO)
					While !EOF() .And. SD3->D3_FILIAL+SD3->D3_OP+SD3->D3_COD == xFilial("SD3")+SH6->H6_OP+SH6->H6_PRODUTO
						If SD3->D3_ESTORNO == Criavar("D3_ESTORNO",.F.) .And. SD3->D3_IDENT == cIdent
							a240DesAtu()
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Verifica se o custo medio e' calculado On-Line               Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If cCusMed == "O"
								
								//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
								//Ё Se ele criou o arquivo de prova ele deve gravar o rodape'    Ё
								//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
								RodaProva(nHdlPrv,nTotal)
			
								//зддддддддддддддддддддддддддд©
								//ЁAtualiza o cСdigo de diАrioЁ
								//юддддддддддддддддддддддддддды
								If !Empty(aCtbDia) 
									cCodDiario := CtbaVerdia()
									For nX := 1 to Len(aCtbDia)
										aCtbDia[nX][3] := cCodDiario 
									Next nX
								EndIf
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Se ele criou o arquivo de prova ele deve gravar o rodape'    Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
								RodaProva(nHdlPrv,nTotal)
								If ( lUsaSeqCor .And. UsaSeqCor() )
									cCodDiario := ctbaVerdia()
								aCtbDia := {{"SD3",SD3->(Recno()),cCodDiario,"D3_NODIA","D3_DIACTB"}}
								Else
				    				aCtbDia := {}
								EndIf
								If cA100Incl(cArquivo,nHdlPrv,3,cLoteEst,lDigita,lAglutina,,,,,,aCtbDia)
									If !Empty(SD3->D3_DTLANC)
										RecLock("SD3",.F.)
										Replace SD3->D3_DTLANC With ddatabase
										MsUnlock()
									EndIf							
								EndIf
								lCriaHeader := .T.
							EndIf
							Exit
						EndIf
						dbSkip()
					EndDo
				EndIf
			EndIf
			
			RecLock(cAlias,.F.,.T.)
			dbSelectArea(cAlias)
			dbDelete()
			MsUnLock()
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Estes pontos de entrada permitem realizar gravacoes e/ou exclusoes emЁ
			//Ё tabelas e/ou campos especificos do usuario apos realizar o Estorno.  Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ExistTemplate("MT682GREST")
	         	ExecTemplate("MT682GREST",.F.,.F.)		
			EndIf
			If lMT682GREST
    	     	ExecbLock("MT682GREST",.F.,.F.)		
			EndIf
		End Transaction
	Else
		MSUNLOCK()
	EndIf
	Exit
End

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA682DescriЁ Autor ЁRodrigo de A. Sartorio Ё Data Ё 08/01/97 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Preenche descricao do Campo Virtual                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A682Descri()
LOCAL cAlias:=Alias(),nOrder:=IndexOrd(),nRecno:=Recno()
LOCAL lRet:=.T.,nEnd
dbSelectArea("SX5")
If dbSeek(xFilial()+"44"+M->H6_MOTIVO)
	M->H6_DESCRI:=Substr(Trim(X5Descri()),1,35)
	nEnd := Ascan(aGets,{ |x| Subs(x,9,10) == "H6_DESCRI " } )
	If nEnd > 0
		aTela[Val(Subs(aGets[nEnd],1,2))][Val(Subs(aGets[nEnd],3,1))*2] := M->H6_DESCRI
	EndIf
Else
	Help(" ",1,"NOSX5")
	lRet:=.F.
EndIf
dbSelectArea(cAlias)
dbSetOrder(nOrder)
dbGoto(nRecno)
Return lRet


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁMenuDef   Ё Autor Ё Fabio Alves Silva     Ё Data Ё08/11/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Utilizacao de menu Funcional                               Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁArray com opcoes da rotina.                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁParametros do array a Rotina:                               Ё╠╠
╠╠Ё          Ё1. Nome a aparecer no cabecalho                             Ё╠╠
╠╠Ё          Ё2. Nome da Rotina associada                                 Ё╠╠
╠╠Ё          Ё3. Reservado                                                Ё╠╠
╠╠Ё          Ё4. Tipo de Transa┤└o a ser efetuada:                        Ё╠╠
╠╠Ё          Ё	1 - Pesquisa e Posiciona em um Banco de Dados     Ё╠╠
╠╠Ё          Ё   	2 - Simplesmente Mostra os Campos                       Ё╠╠
╠╠Ё          Ё   	3 - Inclui registros no Bancos de Dados                 Ё╠╠
╠╠Ё          Ё   	4 - Altera o registro corrente                          Ё╠╠
╠╠Ё          Ё   	5 - Remove o registro corrente do Banco de Dados        Ё╠╠
╠╠Ё          Ё5. Nivel de acesso                                          Ё╠╠
╠╠Ё          Ё6. Habilita Menu Funcional                                  Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Static Function MenuDef()     
PRIVATE aRotina	:= { {STR0001,"AxPesqui"  , 0 , 1, 0, .F.},;	//"Pesquisar"
						{STR0002,"A682Visual", 0 , 2, 0, nil},; 			//"Visualizar"
						{STR0003,"A682Inclui", 0 , 3, 0, nil},; 			//"Incluir"
						{STR0004,"A682Deleta", 0 , 5, 0, nil} } 			//"Estornar"	

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada utilizado para inserir novas opcoes no array aRotina  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("MTA682MNU")
	ExecBlock("MTA682MNU",.F.,.F.)
EndIf
Return(aRotina)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁA682IntSFC╨Autor  Ё Andre Anjos		 ╨ Data Ё  04/05/11   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Estorna apontamento de parada no Chao de Fabrica.          ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё MATA682													  ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A682IntSFC()
Local aArea   := GetArea()	// Salva area atual para posterior restauracao
Local lRet    := .T.		// Conteudo de retorno
Local oModel  := FWLoadModel("SFCA311")

CYI->(dbSetOrder(1))
CYI->(dbSeek(xFilial("CYX")+SH6->(H6_RECURSO+DToS(H6_DATAINI)+H6_HORAINI)))

oModel:SetOperation(5)
		
lRet := oModel:Activate() .And. oModel:VldData() .And. oModel:CommitData()

If !lRet
	aErro := oModel:GetErrorMessage()

	//здддддддддддддддддддддддддддд©
	//ЁEstrutura do vetor com erro Ё
	//юдддддддддддддддддддддддддддды
	//[1] Id do formulАrio de origem
	//[2] Id do campo de origem
	//[3] Id do formulАrio de erro
	//[4] Id do campo de erro
	//[5] Id do erro
	//[6] mensagem do erro
	//[7] mensagem da soluГЦo
	//[8] Valor atribuido
	//[9] Valor anterior

	AutoGrLog( "Id do formulАrio de origem:" + ' [' + AllToChar( aErro[1]  ) + ']' )
	AutoGrLog( "Id do campo de origem:     " + ' [' + AllToChar( aErro[2]  ) + ']' )
	AutoGrLog( "Id do formulАrio de erro:  " + ' [' + AllToChar( aErro[3]  ) + ']' )
	AutoGrLog( "Id do campo de erro:       " + ' [' + AllToChar( aErro[4]  ) + ']' )
	AutoGrLog( "Id do erro:                " + ' [' + AllToChar( aErro[5]  ) + ']' )
	AutoGrLog( "Mensagem do erro:          " + ' [' + AllToChar( aErro[6]  ) + ']' )
	AutoGrLog( "Mensagem da soluГЦo:       " + ' [' + AllToChar( aErro[7]  ) + ']' )
	AutoGrLog( "Valor atribuido:           " + ' [' + AllToChar( aErro[8]  ) + ']' )
	AutoGrLog( "Valor anterior:            " + ' [' + AllToChar( aErro[9]  ) + ']' )

	MostraErro()
EndIf

oModel:DeActivate()
RestArea(aArea)
Return(lRet)
