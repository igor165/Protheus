
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³  DATA  ³ BOPS ³Program.³					ALTERACAO 				  ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³        ³      ³        ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
#INCLUDE "ATFR270.CH"
#Include "Protheus.ch"


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR270    ³ Autor ³ Wagner Mobile Costa   ³ Data ³ 11.04.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relatorio de Lancamentos por Item contabil                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFR270()
Local oReport
 
If TRepInUse()
	oReport:=ReportDef()
	oReport:PrintDialog()
Else
   Return ATFR270R3() // Executa versão anterior do relatorio
Endif

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³Claudio D. de Souza    ³ Data ³09/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatório                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()
Local oReport	:= Nil
Local oSection1	:= Nil
Local oSecFil	:= Nil
Local cReport	:= "ATFR270"
Local cAlias1	:= "SN3"
Local cAlias2	:= "SN1"
Local cMoeda	:= GetMv("MV_ATFMOED")

Local cTitulo := OemToAnsi(STR0003) // "Lancamentos por Item Contabil"
Local cDescri := OemToAnsi(STR0001)+" "+OemToAnsi(STR0002) // "Este programa ir  emitir a rela‡Æo de Lancamentos por Item contabil."

Pergunte( "ATR270" , .F. )
                   
oReport  := TReport():New( cReport, cTitulo, "ATR270" , { |oReport| ReportPrint( oReport, cAlias1, cAlias2 ) }, cDescri )

oReport:DisableOrientation()
oReport:SetUseGC(.F.)

oReport:SetLandscape()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a 1a. secao do relatorio Valores nas Moedas   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New( oReport, STR0008 + Subst(GetMv("MV_SIMB" + AllTrim( Str( MV_PAR07 ) ) ),1,5), {cAlias1,cAlias2} )		//"Valor em "

TRCell():New( oSection1, "N3_SUBCTA"  , cAlias1 ,/*X3Titulo()*/,/*Picture*/,Len(SN3->N3_SUBCTA) + 6,/*lPixel*/,/*{|| code-block de impressao }*/)	// Centro de custo
TRCell():New( oSection1, "N3_CCUSTO"  , cAlias1 ,/*X3Titulo()*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Centro de custo
TRCell():New( oSection1, "N3_CCONTAB" , cAlias1 ,/*X3Titulo()*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Conta contabil
TRCell():New( oSection1, "SIMBMOEDA1" ,         , STR0009      ,/*Picture*/,10 /*Tamanho*/,/*lPixel*/, {|| GetMV("MV_SIMB"+ AllTrim( Str( MV_PAR07 ) )) } )	//"Simbolo da Moeda"
TRCell():New( oSection1, "N3_VORIG1"  , cAlias1 ,STR0010,/*Picture*/,20/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor atual
TRCell():New( oSection1, "N3_VRDACM1" , cAlias1 ,STR0011,/*Picture*/,20/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Deprec. Acumul
TRCell():New( oSection1, "N3_VRDMES1" , cAlias1 ,STR0012,/*Picture*/,20/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Deprec. nos Mes
TRCell():New( oSection1, "N3_VRCDA1"  , cAlias1 ,STR0013,/*Picture*/,20/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Correc. Depr. Acumul
TRCell():New( oSection1, "N3_VRCACM1" , cAlias1 ,STR0014,/*Picture*/,20/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Correc. Monet. Acumulada

oSection1:SetHeaderPage()	//Define o cabecalho da secao como padrao
oSection1:SetNoFilter({cAlias1,cAlias2})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a 2a. secao do relatorio							   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection2 := TRSection():New( oReport, STR0008 + Subst(GetMv("MV_SIMB"+cMoeda),1,5), {cAlias1,cAlias2} )	//"Valor em "
oSection2:SetLinesBefore(0)

TRCell():New( oSection2, "N3_SUBCTA"  			 , cAlias1 ,/*X3Titulo()*/,/*Picture*/,Len(SN3->N3_SUBCTA) + 6,/*lPixel*/,/*{|| code-block de impressao }*/)	// Centro de custo
TRCell():New( oSection2, "N3_CCUSTO"  			 , cAlias1 ,/*X3Titulo()*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Centro de custo
TRCell():New( oSection2, "N3_CCONTAB" 			 , cAlias1 ,/*X3Titulo()*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Conta contabil
TRCell():New( oSection2, "SIMBMOEDA2" 			 ,  	     , STR0009		  ,/*Picture*/,10 /*Tamanho*/,/*lPixel*/, {|| GetMv("MV_SIMB"+cMoeda) } )	//"Simbolo da Moeda"
TRCell():New( oSection2, "N3_VORIG" + cMoeda , cAlias1 ,X3Titulo(),/*Picture*/,20/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor atual
TRCell():New( oSection2, "N3_VRDACM"+ cMoeda , cAlias1 ,/*X3Titulo()*/,/*Picture*/,20/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Deprec. Acumul
TRCell():New( oSection2, "N3_VRDMES"+ cMoeda , cAlias1 ,/*X3Titulo()*/,/*Picture*/,20/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Deprec. nos Mes

oSection2:Cell("N3_SUBCTA" ):Hide()
oSection2:Cell("N3_CCUSTO" ):Hide()
oSection2:Cell("N3_CCONTAB"):Hide()

oSection2:SetHeaderSection(.F.)	//Define o cabecalho da secao como padrao
oSection2:SetNoFilter({cAlias1,cAlias2})

/* Relacao das filiais selecionadas para compor o relatorio */
oSecFil := TRSection():New(oReport,"SECFIL",{"SN3"})
oSecFil:SetAutoSize(.T.)
TRCell():New(oSecFil,"CODFIL" ,,STR0015,/*Picture*/,30,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Código"
TRCell():New(oSecFil,"EMPRESA",,STR0016,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Empresa"
TRCell():New(oSecFil,"UNIDNEG",,STR0017,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Unidade de negócio"	
TRCell():New(oSecFil,"NOMEFIL",,STR0018,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Filial"

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ReportPrintºAutor  ³Claudio D. de Souza º Data ³  05/06/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Query de impressao do relatorio                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint( oReport, cAlias1, cAlias2 )
Local oSection1		:= oReport:Section(1)
Local oSection2		:= oReport:Section(2)
Local oSecFil		:= oReport:Section("SECFIL")
Local cQuery		:= "SN3"
Local cAliaSN1		:= "SN1"
Local cFiltro		:= ""
Local cChave		:= ""
Local cWhere		:= ""		
Local cMoeda   		:= GetMv("MV_ATFMOED")
Local dUltDepr 		:= GetMv("MV_ULTDEPR")
Local cEntidade		:= ""
Local cCentroCus	:= ""
Local cConta		:= ""
/*-*/
Local aSelFil		:= {}
Local cTmpFil		:= ""
Local cFilSN3		:= ""
Local cFilSN1		:= ""
Local cTitulo 		:= ""
Local cFilAtu		:= ""
Local lSelFil		:= .F.
Local lCTDExFil		:= .F.
Local lCTDExUnN		:= .F.
Local lCTDExcl		:= .F.
Local aSM0			:= {}
Local nRegSM0		:= 0
Local nTamEmp		:= 0
Local nTamUnNeg		:= 0
Local nTamTit		:= 0
Local nX			:= 0
Local nLinha		:= 0
Local nLenSelFil	:= 0
Local lTotFil		:= .F. 


// Acumuladores de Conta
Local nCtaVlOri1	:=nCtaDpAcm1:=nCtaDpMes1:=nCtaCrDep1:=nCtaCrMon1:= 0
Local nCtaVlOri2	:=nCtaDpAcm2:=nCtaDpMes2:=nCtaCrDep2:=nCtaCrMon2:= 0
// Acumuladores de Item
Local nValOri1		:=nDepAcm1:=nDepMes1:=nCorDep1:=nCorMon1:= 0
Local nValOri2		:=nDepAcm2:=nDepMes2:=nCorDep2:=nCorMon2:= 0
// Acumuladores Gerais
Local nGerOri1		:=nGerDepA1:=nGerDepM1:=nGerCD1:=nGerCMon1:= 0
Local nGerOri2		:=nGerDepA2:=nGerDepM2:=nGerCD2:=nGerCMon2:= 0
// Acumuladores de filiais
LOCAL nFilOri1		:=nFilDepA1:=nFilDepM1:=nFilCD1:=nFilCMon1:= 0
LOCAL nFilOri2		:=nFilDepA2:=nFilDepM2:=nFilCD2:=nFilCMon2:= 0

// Verificação da classificação de Ativo se sofre depreciação
Local lAtClDepr 	:= .F.   

Local aClassif := {}
Local cClassif := "" 

//Ativo Custo/Provisao
Local lAtfCusPrv := AFXAtCsPrv()


If mv_par08 == 1
	aClassif := AdmGetClass()
	If Len( aClassif ) <= 0
		Return
	EndIf 			
EndIf

lSelFil := (FwSizeFilial() > 2)
If lSelFil
	If MV_PAR10 == 1 
		If FindFunction("AdmSelecFil")
			AdmSelecFil("ATR270",10,.F.,@aSelFil,"SN3",.F.)
		Else
			aSelFil := AdmGetFil(.F.,.F.,"SN3")
		Endif
	Endif
	If Empty(aSelFil)
		Aadd(aSelFil,cFilAnt)
	Endif
	lCTDExcl := (FWModeAccess("CTD",1) == "E")
	cChave := SN3->(IndexKey(6))
	nX := At("N3_FILIAL",cChave)
	If lCTDExcl
		lCTDExFil := (FWModeAccess("CTD",3) == "E")
		lCTDExUnN := (FWModeAccess("CTD",2) == "E")
		nTamEmp := Len(AllTrim(xFilial("CTD")))
		If nX > 0
			cChave := "%" + SqlOrder("Substring(N3_FILIAL,1," + AllTrim(Str(nTamEmp)) + ")" + AllTrim(Substr(cChave,nX + 9))) + "%"
		Else
			cChave := "%" + SqlOrder("Substring(N3_FILIAL,1," + AllTrim(Str(nTamEmp)) + ")+" + AllTrim(cChave)) + "%"
		Endif
	Else
		If nX > 0
			cChave := "%" + SqlOrder(AllTrim(Substr(cChave,nX + 10))) + "%"
		Else
			cChave := "%" + SqlOrder(cChave) + "%"
		Endif
	Endif
Else
	cChave 	:= "%"+SqlOrder(SN3->(IndexKey(6)))+"%"
	lCTDExcl := .F.
	lCTDTExFil := .F.
	lCTDExUnN := .F.
Endif

cQuery 	:= GetNextAlias()
cAliaSN1 := cQuery

oSection1:BeginQuery()
		
If lSelFil
	MsgRun(STR0020,STR0003,{|| cFilSN3 := GetRngFil(aSelFil,"SN3",.T.,@cTmpFil)}) //"Favor Aguardar..."###"Lancamentos por Centro de Custo"
	cFilSN3 := "%SN3.N3_FILIAL " + cFilSN3 + "%"
	cFilSN1 := "%SN1.N1_FILIAL = SN3.N3_FILIAL%"
Else
	cFilSN1 := "%SN1.N1_FILIAL = '" + xFilial("SN1") + "'%"
	cFilSN3 := "%SN3.N3_FILIAL = '" + xFilial("SN3") + "'%"
Endif

cWhere := "(SN3.N3_BAIXA < '1' OR (SN3.N3_BAIXA >= '1' AND SN3.N3_DTBAIXA LIKE '" + Left(Dtos(dUltDepr), 6) + "%'))"    


//Verifica se filtra as classificações patrimoniais	
If Len(aClassif) > 0
	cWhere += " And SN1.N1_PATRIM IN " + FormatClass(aClassif,.T.) + " "
EndIf

//NÃO mostra ativo Custo/Provisao
If lAtfCusPrv .and. mv_par09 == 2
	cWhere += " And SN3.N3_ATFCPR <> '1' "
Endif

cWhere := "%"+ cWhere + "%"

BeginSql Alias cQuery
	SELECT *
	FROM 
		%table:SN3% SN3, %table:SN1% SN1
	WHERE
		%Exp:cFilSN3% AND
		SN3.N3_SUBCTA >= %Exp:mv_par01% AND 
		SN3.N3_SUBCTA <= %Exp:mv_par02% AND
		SN3.N3_CCUSTO >= %Exp:mv_par03% AND 
		SN3.N3_CCUSTO <= %Exp:mv_par04% AND
		SN3.N3_CCONTAB >= %Exp:mv_par05% AND 
		SN3.N3_CCONTAB <= %Exp:mv_par06% AND
		(SN3.N3_SUBCTA <> ' ' AND SN3.N3_CDEPREC <> ' ' AND SN3.N3_CCDEPR <> ' ') AND
		%Exp:cWhere% AND
		SN3.%notDel% AND
		%Exp:cFilSN1% AND
		SN1.N1_CBASE = SN3.N3_CBASE AND
		SN1.N1_ITEM = SN3.N3_ITEM AND
		SN1.%notDel%
	ORDER BY %Exp:cChave%
EndSql

oSection1:EndQuery()


oSection1:Cell("SIMBMOEDA1"):SetTitle("")
oSection2:Cell("SIMBMOEDA2"):SetTitle("")

If lSelFil .And. Len(aSelFil) > 1
	If !((cQuery)->(Eof()))
		nRegSM0 := SM0->(Recno())
		aSM0 := FWLoadSM0()
		SM0->(DbGoTo(nRegSM0))
		oSecFil := oReport:Section("SECFIL")
		oSection1:SetHeaderSection(.F.)
		nTamEmp := Len(FWSM0LayOut(,1))
		nTamUnNeg := Len(FWSM0LayOut(,2))
		cTitulo := oReport:Title()
		oReport:SetTitle(cTitulo + " (" + STR0019 + ")")		//"Filiais selecionadas para o relatorio"
		nTamTit := Len(oReport:Title())
		oSecFil:Init()
		oSecFil:Cell("CODFIL"):SetBlock({||cFilSel})
		oSecFil:Cell("EMPRESA"):SetBlock({||aSM0[nLinha,SM0_DESCEMP]})
		oSecFil:Cell("UNIDNEG"):SetBlock({||aSM0[nLinha,SM0_DESCUN]})
		oSecFil:Cell("NOMEFIL"):SetBlock({||aSM0[nLinha,SM0_NOMRED]})
		For nX := 1 To Len(aSelFil)
			nLinha := Ascan(aSM0,{|sm0| sm0[SM0_GRPEMP] == cEmpAnt .And. sm0[SM0_CODFIL] == aSelFil[nX]})
			If nLinha > 0
				cFilSel := Substr(aSM0[nLinha,SM0_CODFIL],1,nTamEmp)
				cFilSel += " "
				cFilSel += Substr(aSM0[nLinha,SM0_CODFIL],nTamEmp + 1,nTamUnNeg)
				cFilSel += " "
				cFilSel += Substr(aSM0[nLinha,SM0_CODFIL],nTamEmp + nTamUnNeg + 1)
				oSecFil:PrintLine()
			Endif
		Next
		oReport:SetTitle(cTitulo)
		oSecFil:Finish()
		oSection1:SetHeaderSection(.T.)
		oReport:EndPage()
	Endif
Else
	lCTDExcl := .F.
Endif

oSection1:Init()
oSection2:Init()

cFilAtu := ""
nTamEmp := Len(AllTrim(xFilial("CTD")))
nLenSelFil := Len(aSelfil)
lTotFil := .F.
nX := 1 
While nx < nLenSelFil .And. !lTotFil
	nX++
	lTotFil := !(Substr(aSelFil[nx - 1],1,nTamEmp) == Substr(aSelFil[nX],1,nTamEmp))
Enddo

While (cQuery)->(!Eof()) .And. !oReport:Cancel()
        
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Acumula por Item Contabil ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cEntidade := (cQuery)->N3_SUBCTA
	If lCTDExcl
		If !(cFilAtu == Substr((cQuery)->N3_FILIAL,1,nTamEmp))
			cFilAtu := Substr((cQuery)->N3_FILIAL,1,nTamEmp)
			If lCTDExFil
				cTitulo := STR0018					//"Filial"
			Else
				cTitulo := STR0016					//"Empresa"
				If lCTDExUnN
					cTitulo += ("/" + STR0017)		//"Unidade de negócio"
				Endif
			Endif
			oReport:PrintText(cTitulo + ": " + cFilAtu)
			oReport:ThinLine()
		Endif
	Endif
	
	While (cQuery)->(!Eof()) .And. (cQuery)->N3_SUBCTA == cEntidade .And. If(lCTDExcl,(Substr((cQuery)->N3_FILIAL,1,nTamEmp) == cFilAtu),.T.) .And. !oReport:Cancel() 
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Acumula pela Conta Contabil do Bem ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCentroCus  := (cQuery)->N3_CUSTBEM
		cConta 		:= (cQuery)->N3_CCONTAB

		While (cQuery)->(!Eof()) .And.;
				(cQuery)->(N3_SUBCTA)  == cEntidade .And.;
			   (cQuery)->(N3_CUSTBEM) == cCentroCus .And.;
				(cQuery)->(N3_CCONTAB) == cConta .And.;
				If(lCTDExcl,(Substr((cQuery)->N3_FILIAL,1,nTamEmp) == cFilAtu),.T.) .And. ;
				! oReport:Cancel()
            
            // Verificação da classificação de Ativo se sofre depreciação
			lAtClDepr := Iif(FindFunction("AtClssVer"),AtClssVer((cAliaSN1)->N1_PATRIM),(cAliaSN1)->N1_PATRIM $ "NID")
          
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Acumula Conta em Moeda 1    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lAtClDepr .OR. (cAliaSN1)->N1_PATRIM $ " P"
				nCtaVlOri1+= &((cQuery)->("N3_VORIG" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaDpAcm1+= &((cQuery)->("N3_VRDACM" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaDpMes1+= &((cQuery)->("N3_VRDMES" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaCrDep1+= (cQuery)->N3_VRCDA1  
				nCtaCrMon1+= (cQuery)->N3_VRCACM1 
			Else
				nCtaVlOri1-= &((cQuery)->("N3_VORIG" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaDpAcm1-= &((cQuery)->("N3_VRDACM" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaDpMes1-= &((cQuery)->("N3_VRDMES" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaCrDep1-= (cQuery)->N3_VRCDA1  
				nCtaCrMon1-= (cQuery)->N3_VRCACM1 
			Endif
                 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Acumula Conta em Moeda do Ativo³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lAtClDepr .OR. (cAliaSN1)->N1_PATRIM $ " P"
				nCtaVlOri2+= &((cQuery)+"->N3_VORIG"+cMoeda)
				nCtaDpAcm2+= &((cQuery)+"->N3_VRDACM"+cMoeda)
				nCtaDpMes2+= &((cQuery)+"->N3_VRDMES"+cMoeda)
				nCtaCrDep2+= IIf(cMoeda=="1",(cQuery)->N3_VRCDA1,0)  
				nCtaCrMon2+= IIf(cMoeda=="1",(cQuery)->N3_VRCACM1,0) 
			Else
				nCtaVlOri2-= &((cQuery)+"->N3_VORIG"+cMoeda)
				nCtaDpAcm2-= &((cQuery)+"->N3_VRDACM"+cMoeda)
				nCtaDpMes2-= &((cQuery)+"->N3_VRDMES"+cMoeda)
				nCtaCrDep2-= IIf(cMoeda=="1",(cQuery)->N3_VRCDA1,0)  
				nCtaCrMon2-= IIf(cMoeda=="1",(cQuery)->N3_VRCACM1,0) 
			Endif
                 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Acumula Centro de Custo em Moeda 1 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lAtClDepr .OR. (cAliaSN1)->N1_PATRIM $ " P"
				nValOri1+= &((cQuery)->("N3_VORIG" + AllTrim( Str( MV_PAR07 ) ) ) )
				nDepAcm1+= &((cQuery)->("N3_VRDACM" + AllTrim( Str( MV_PAR07 ) ) ) )
				nDepMes1+= &((cQuery)->("N3_VRDMES" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCorDep1 += (cQuery)->N3_VRCDA1 
				nCorMon1 += (cQuery)->N3_VRCACM1
			Else
				nValOri1-= &((cQuery)->("N3_VORIG" + AllTrim( Str( MV_PAR07 ) ) ) )
				nDepAcm1-= &((cQuery)->("N3_VRDACM" + AllTrim( Str( MV_PAR07 ) ) ) )
				nDepMes1-= &((cQuery)->("N3_VRDMES" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCorDep1 -= (cQuery)->N3_VRCDA1 
				nCorMon1 -= (cQuery)->N3_VRCACM1
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Acumula Centro de Custo em Moeda do Ativo ³ 
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lAtClDepr .OR. (cAliaSN1)->N1_PATRIM $ " P"
				nValOri2 += &((cQuery)+"->N3_VORIG"+cMoeda)
				nDepAcm2 += &((cQuery)+"->N3_VRDACM"+cMoeda)
				nDepMes2 += &((cQuery)+"->N3_VRDMES"+cMoeda)
				nCorDep2 += IIf(cMoeda=="1",(cQuery)->N3_VRCDA1,0)
				nCorMon2 += IIf(cMoeda=="1",(cQuery)->N3_VRCACM1,0)
			Else
				nValOri2 -= &((cQuery)+"->N3_VORIG"+cMoeda)
				nDepAcm2 -= &((cQuery)+"->N3_VRDACM"+cMoeda)
				nDepMes2 -= &((cQuery)+"->N3_VRDMES"+cMoeda)
				nCorDep2 -= IIf(cMoeda=="1",(cQuery)->N3_VRCDA1,0)
				nCorMon2 -= IIf(cMoeda=="1",(cQuery)->N3_VRCACM1,0)
			Endif
			dbSelectArea(cQuery)
			dbSkip()
		EndDo

		oSection1:Cell("N3_SUBCTA" ):SetBlock({|| cEntidade  } )
		oSection1:Cell("N3_CCUSTO" ):SetBlock({|| cCentroCus } )
		oSection1:Cell("N3_CCONTAB"):SetBlock({|| cConta     } )
		
		oSection1:Cell("N3_VORIG1" ):SetBlock({|| nCtaVlOri1 } )
		oSection1:Cell("N3_VRDACM1"):SetBlock({|| nCtaDpAcm1 } )
		oSection1:Cell("N3_VRDMES1"):SetBlock({|| nCtaDpMes1 } )
		oSection1:Cell("N3_VRCDA1" ):SetBlock({|| nCtaCrDep1 } )
		oSection1:Cell("N3_VRCACM1"):SetBlock({|| nCtaCrMon1 } )
		
		oSection2:Cell("N3_VORIG" +cMoeda):SetBlock( { || nCtaVlOri2 } )
		oSection2:Cell("N3_VRDACM"+cMoeda):SetBlock( { || nCtaDpAcm2 } )
		oSection2:Cell("N3_VRDMES"+cMoeda):SetBlock( { || nCtaDpMes2 } )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ ImpressÆo dos dados em questÆo  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSection1:PrintLine()
		oSection2:PrintLine()
		
		dbSelectArea(cQuery)
		nCtaVlOri1:=nCtaDpAcm1:=nCtaDpMes1:=nCtaCrDep1:=nCtaCrMon1:= 0
		nCtaVlOri2:=nCtaDpAcm2:=nCtaDpMes2:=nCtaCrDep2:=nCtaCrMon2:= 0
	EndDo
  
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Mostra Total da Entidade Base ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nValOri1 != 0 .Or. nDepAcm1 != 0 .Or. nValOri2 != 0 .Or. nDepAcm2 != 0

		oSection1:Cell("N3_SUBCTA" ):SetBlock({|| STR0006 } )	//"Tot I.Contabil: "
		oSection1:Cell("N3_CCUSTO" ):SetBlock({|| cEntidade  } )
		oSection1:Cell("N3_CCONTAB"):SetBlock({|| ""  } )
		
		oSection1:Cell("N3_VORIG1" ):SetBlock({|| nValOri1 } )
		oSection1:Cell("N3_VRDACM1"):SetBlock({|| nDepAcm1 } )
		oSection1:Cell("N3_VRDMES1"):SetBlock({|| nDepMes1 } )
		oSection1:Cell("N3_VRCDA1" ):SetBlock({|| nCorDep1 } )
		oSection1:Cell("N3_VRCACM1"):SetBlock({|| nCorMon1 } )
		
		oSection2:Cell("N3_VORIG" +cMoeda):SetBlock( { || nValOri2 } )
		oSection2:Cell("N3_VRDACM"+cMoeda):SetBlock( { || nDepAcm2 } )
		oSection2:Cell("N3_VRDMES"+cMoeda):SetBlock( { || nDepMes2 } )
		
		oReport:SkipLine()
		oSection1:PrintLine()
		oSection2:PrintLine()
		oReport:SkipLine(2)

	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Acumula Total Geral em Moeda 1     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nGerOri1  += nValOri1
	nGerDepA1 += nDepAcm1
	nGerDepM1 += nDepMes1
	nGerCD1   += nCorDep1
	nGerCMon1 += nCorMon1
      
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Acumula Total Geral em Moeda do Ativo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nGerOri2  += nValOri2
	nGerDepA2 += nDepAcm2
	nGerDepM2 += nDepMes2
	nGerCD2   += nCorDep2
	nGerCMon2 += nCorMon2
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Acumula Total Filial em Moeda 1    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nFilOri1  += nValOri1
	nFilDepA1 += nDepAcm1
	nFilDepM1 += nDepMes1
	nFilCD1   += nCorDep1
	nFilCMon1 += nCorMon1
      
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Acumula Total Filial em Moeda do Ativo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nFilOri2  += nValOri2
	nFilDepA2 += nDepAcm2
	nFilDepM2 += nDepMes2
	nFilCD2   += nCorDep2
	nFilCMon2 += nCorMon2
      
	nValOri1:=nDepAcm1:=nDepMes1:=nCorDep1:=nCorMon1:= 0
	nValOri2:=nDepAcm2:=nDepMes2:=nCorDep2:=nCorMon2:= 0
	
	If lCTDExcl
		If lTotFil
			If !(Substr((cQuery)->N3_FILIAL,1,nTamEmp) == cFilAtu)
				oReport:ThinLine()
				oSection1:Cell("N3_SUBCTA" ):SetBlock({|| ""})
				oSection1:Cell("N3_CCUSTO"):SetBlock({|| ""})
				oSection1:Cell("N3_CCONTAB"):SetBlock({|| ""})

				oSection1:Cell("N3_VORIG1"):SetBlock({|| nFilOri1})
				oSection1:Cell("N3_VRDACM1"):SetBlock({|| nFilDepA1})
				oSection1:Cell("N3_VRDMES1"):SetBlock({|| nFilDepM1})
				oSection1:Cell("N3_VRCDA1"):SetBlock({|| nFilCD1})
				oSection1:Cell("N3_VRCACM1"):SetBlock({|| nFilCMon1})

				oSection2:Cell("N3_VORIG"+cMoeda):SetBlock({|| nFilOri2})
				oSection2:Cell("N3_VRDACM"+cMoeda):SetBlock({|| nFilDepA2})
				oSection2:Cell("N3_VRDMES"+cMoeda):SetBlock({|| nFilDepM2})

				oSection1:PrintLine()
				If !(cMoeda == "1")
					oSection2:PrintLine()
				Endif
				oReport:SkipLine(2)

				nFilOri1 := nFilDepA1 := nFilDepM1 := nFilCD1 := nFilCMon1 := nFilOri2 := 0 
				nFilDepA2 := nFilDepM2 := nFilCD2 := nFilCMon2 := nFilAmp1 := nFilAmp2 := 0			
			Endif
		Endif
	Endif
	
EndDo    

oReport:SkipLine()
oReport:ThinLine()

oSection1:Cell("N3_SUBCTA" ):SetBlock({|| STR0007  } )		//"TOTAL GERAL       : "
oSection1:Cell("N3_CCUSTO" ):SetBlock({|| ""  } )
oSection1:Cell("N3_CCONTAB"):SetBlock({|| ""  } )

oSection1:Cell("N3_VORIG1" ):SetBlock({|| nGerOri1  } )
oSection1:Cell("N3_VRDACM1"):SetBlock({|| nGerDepA1 } )
oSection1:Cell("N3_VRDMES1"):SetBlock({|| nGerDepM1 } )
oSection1:Cell("N3_VRCDA1" ):SetBlock({|| nGerCD1   } )
oSection1:Cell("N3_VRCACM1"):SetBlock({|| nGerCMon1 } )

oSection2:Cell("N3_VORIG" +cMoeda):SetBlock( { || nGerOri2  } )
oSection2:Cell("N3_VRDACM"+cMoeda):SetBlock( { || nGerDepA2 } )
oSection2:Cell("N3_VRDMES"+cMoeda):SetBlock( { || nGerDepM2 } )

oSection1:PrintLine()
oSection2:PrintLine()

oReport:ThinLine()

oSection1:Finish()
oSection2:Finish()

If !Empty(cTmpFil)
	MsgRun(STR0020,STR0003,{|| CtbTmpErase(cTmpFil)}) //"Favor Aguardar..."###"Lancamentos por Centro de Custo"
Endif


Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR270    ³ Autor ³ Wagner Mobile Costa   ³ Data ³ 11.04.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relatorio de Lancamentos por Item contabil                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATFR270R3
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

LOCAL cString 	:= "SN3"
LOCAL cDesc1    := OemToAnsi(STR0001) // "Este programa ira emitir a relacao de Lancamentos por"
LOCAL cDesc2    := OemToAnsi(STR0002) // "Item contabil."
LOCAL cDesc3    := ""
LOCAL wnrel

PRIVATE aReturn  := { "Zebrado", 1,"Administracao", 2, 2, 1, "",1 }
PRIVATE aLinha   := { }
PRIVATE cPerg    := "ATR270"
PRIVATE nomeprog := "ATFR270"
PRIVATE nLastKey := 0
PRIVATE tamanho  := "G"

PRIVATE titulo  := OemToAnsi(STR0003) // "Lancamentos por Item Contabil"
PRIVATE cabec1  
PRIVATE cabec2
PRIVATE nTamanho

If ! CtbMovSaldo("CTD", .T.)
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros       ³
//³ mv_par01            Do Item Contabil       ³
//³ mv_par02            Ate o item Contabil    ³
//³ mv_par03            Do Centro de Custo     ³
//³ mv_par04            Ate Centro de Custo    ³
//³ mv_par05            Da Conta               ³
//³ mv_par06            Ate Conta              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte(cPerg,.F.)

wnrel := NomeProg
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,,,Tamanho,,.F.)

If nLastKey == 27
	Return
End

SetDefault( aReturn,cString )

nTamanho:=IIf(aReturn[4]==1,15,18)

cabec1  := OemToAnsi(STR0004) // "Item Contabil	 	   C.Custo    		    Cta Contab                        Valor Atual      Deprec Acumul      Deprec no Mes   Corr Depr Acumul  Corr Monet Acumul"
*                                 0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
*                                 0         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6
*                                 0
*                                 99999999999999999999 99999999999999999999 99999999999999999999 SIMBO 999999999999999999 999999999999999999 999999999999999999 999999999999999999 999999999999999999
cabec2  :=""
	
#DEFINE COL_CCUSTO		21
#DEFINE COL_CONTA		42
#DEFINE COL_MOEDA		63
#DEFINE COL_ORIGINAL	69
#DEFINE COL_DEP_ACUM	88
#DEFINE COL_DEP_MES 	107
#DEFINE COL_CORDEPACU	126
#DEFINE COL_CORRECAO 	145

Cabec1	:= 	CtbSayApro("CTD") + Space(11) + CtbSayApro("CTT") + Space(11) +;
			Subs(Cabec1, 43)

If nLastKey == 27
   Return
Endif

RptStatus({|lEnd| FR270Imp(@lEnd,wnRel,cString)},Titulo)

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FR270Imp  ³ Autor ³ Wagner Mobile Costa   ³ Data ³ 12/04/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime o relatorio de Lancamentos por Item Contabil       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³FR270IMP(cAlias,nReg,nOpc)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function FR270Imp(lEnd,wnRel,cString)

LOCAL CbTxt
LOCAL cbCont
// Acumuladores de Conta
LOCAL nCtaVlOri1	:=nCtaDpAcm1:=nCtaDpMes1:=nCtaCrDep1:=nCtaCrMon1:= 0
LOCAL nCtaVlOri2	:=nCtaDpAcm2:=nCtaDpMes2:=nCtaCrDep2:=nCtaCrMon2:= 0

// Acumuladores de Item
LOCAL nValOri1	:=nDepAcm1:=nDepMes1:=nCorDep1:=nCorMon1:= 0
LOCAL nValOri2	:=nDepAcm2:=nDepMes2:=nCorDep2:=nCorMon2:= 0

// Acumuladores Gerais
LOCAL nGerOri1	:=nGerDepA1:=nGerDepM1:=nGerCD1:=nGerCMon1:= 0
LOCAL nGerOri2	:=nGerDepA2:=nGerDepM2:=nGerCD2:=nGerCMon2:= 0
LOCAL cMoeda1   := SUBS(GetMV("MV_SIMB"+ AllTrim( Str( MV_PAR07 ) )),1,5)
LOCAL cMoeda2   
LOCAL cEntidade	:= cCentroCus := cConta := Space(1)
Local dUltDepr 	:= GetMv("MV_ULTDEPR")
Local cAliasQry
Local lSkip 		:= .T.  

Local aFdsQry		:= {}
Local nFdsQry		:= 1


// Verificação da classificação de Ativo se sofre depreciação
Local lAtClDepr := .F.

Local aClassif := {}
Local cClassif := "" 

//Ativo Custo/Provisao
Local lAtfCusPrv := AFXAtCsPrv()

//Verificao se vai filtrar a classificação patrimonial
If mv_par08 == 1
	aClassif := AdmGetClass()	
EndIf

Private cMoeda  :=GetMv("MV_ATFMOED")

cMoeda2 := Subst(GetMv("MV_SIMB"+cMoeda),1,5)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbTxt   := SPACE(10)
cbCont  := 0
li      := 80
m_pag   := 1

dbSelectArea("SN3")
dbSetOrder(6)
  						
cQuery := " SELECT * "
cQuery += " FROM "+RetSqlName("SN3")+" SN3 "
cQuery += " WHERE N3_FILIAL = '"+xFilial("SN3")+"' "
cQuery += " AND N3_SUBCTA >= '"+mv_par01+"' "
cQuery += " AND N3_SUBCTA <= '"+mv_par02+"' "
cQuery += " AND N3_CCUSTO >= '"+mv_par03+"' "
cQuery += " AND N3_CCUSTO <= '"+mv_par04+"' "
cQuery += " AND N3_CCONTAB >= '"+mv_par05+"' "
cQuery += " AND N3_CCONTAB <= '"+mv_par06+"' "
cQuery += " AND (N3_SUBCTA <> ' ' AND "
cQuery += "      N3_CDEPREC <> ' ' AND "
cQuery += "      N3_CCDEPR <> ' ' ) "
cQuery += " AND (N3_BAIXA < '1' OR (N3_BAIXA > '1' AND N3_DTBAIXA LIKE '"+ Left(Dtos(dUltDepr), 6) +"%')) "
cQuery += " AND SN3.D_E_L_E_T_ = ' ' " 

//NÃO mostra ativo Custo/Provisao
If lAtfCusPrv .and. mv_par09 == 2
	cQuery += " AND N3_ATFCPR <> '1' "
Endif

cQuery += " ORDER BY N3_FILIAL,N3_SUBCTA,N3_CCUSTO,N3_CCONTAB,N3_CBASE,N3_ITEM,N3_TIPO,R_E_C_N_O_ "
						
cAliasQry := "SN3QRY"

If Select(cAliasQry) > 0
	dbSelectArea(cAliasQry)
	dbCloseArea()
Endif

cQuery := ChangeQuery(cQuery)
dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliasQry, .F., .T.)
dbSelectArea(cAliasQry)		
aFdsQry		:= dbStruct()
nFdsQry		:= 1
SX3->(DbSetOrder(2))
For nFdsQry := 1 To Len(aFdsQry)
	If !aFdsQry[nFdsQry,2] $ "CM" .and. SX3->(MsSeek(aFdsQry[nFdsQry,1]))
		TCSetField(cAliasQry,SX3->X3_CAMPO, SX3->X3_TIPO,SX3->X3_TAMANHO, SX3->X3_DECIMAL)
	Endif
Next		
			
While (cAliasQry)->(!Eof()) .and. (cAliasQry)->(N3_FILIAL) == xFilial("SN3")
        
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Acumula por Item Contabil ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    
    If !( (cAliasQry)->(N3_FILIAL) == xFilial("SN3") .And. (cAliasQry)->(N3_SUBCTA) >= mv_par01 .And.	(cAliasQry)->(N3_SUBCTA) <= mv_par02 .And. (cAliasQry)->(N3_CCUSTO) >= mv_par03 .And.	(cAliasQry)->(N3_CCUSTO) <= mv_par04 .And. (cAliasQry)->(N3_CCONTAB) >= mv_par05 .And.	(cAliasQry)->(N3_CCONTAB) <= mv_par06 .And. (cAliasQry)->(N3_SUBCTA) <> ' ' .And. (cAliasQry)->(N3_CDEPREC) <> ' ' .And. (cAliasQry)->(N3_CCDEPR) <> ' ' .And. ((cAliasQry)->(N3_BAIXA) < '1' .Or. ((cAliasQry)->(N3_BAIXA) > '1' .And. LEFT(DTOS((cAliasQry)->(N3_DTBAIXA)), 6) = Left(Dtos(dUltDepr), 6) )) )
    	dbSkip()
    	Loop
    Endif
       
	cEntidade := (cAliasQry)->N3_SUBCTA
	
	lSkip := .T.
	
	While (cAliasQry)->(!Eof()) .AND. (cAliasQry)->N3_FILIAL == xFilial("SN3") .And. (cAliasQry)->N3_SUBCTA == cEntidade
        
        lSkip := .T.

		If lEnd
			@PROW()+1,001 PSAY OemToAnsi(STR0005) // "CANCELADO PELO OPERADOR"
			Exit
		End
    
		If li > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTamanho)
		End

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Acumula pela Conta Contabil do Bem ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCentroCus  := (cAliasQry)->N3_CUSTBEM
		cConta 		:= (cAliasQry)->N3_CCONTAB
		While (cAliasQry)->(!Eof()) .And. (cAliasQry)->(N3_FILIAL) == xFilial("SN3") .AND. (cAliasQry)->(N3_SUBCTA) == cEntidade .And. (cAliasQry)->(N3_CUSTBEM) == cCentroCus .And.;
							(cAliasQry)->(N3_CCONTAB) == cConta
            
            // Verificação da classificação de Ativo se sofre depreciação
			//lAtClDepr := Iif(FindFunction("AtClssVer"),AtClssVer(SN1->N1_PATRIM),SN1->N1_PATRIM $ "NID")
		     
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Acumula Conta em Moeda 1    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SN1->(MsSeek((cAliasQry)->(N3_FILIAL+N3_CBASE+N3_ITEM)))  
			 // Verificação da classificação de Ativo se sofre depreciação
			lAtClDepr := AtClssVer(SN1->N1_PATRIM)
		     
			
			If Len(aClassif) > 0
				If !(SN1->N1_PATRIM $ FormatClass(aClassif,.F.))
					dbSelectArea(cAliasQry)
					dbSkip()
					Loop
				EndIf
			EndIf
						
			//NÃO mostra ativo Custo/Provisao
			If lAtfCusPrv .and. mv_par09 == 2 .And. (cAliasQry)->(N3_ATFCPR) == '1'
				(cAliasQry)->(dbSkip())	
				Loop		
			Endif

			If lAtClDepr .or. SN1->N1_PATRIM $ " P"
				nCtaVlOri1+= &((cAliasQry)->("N3_VORIG" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaDpAcm1+= &((cAliasQry)->("N3_VRDACM" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaDpMes1+= &((cAliasQry)->("N3_VRDMES" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaCrDep1+= (cAliasQry)->N3_VRCDA1  
				nCtaCrMon1+= (cAliasQry)->N3_VRCACM1 
			Else
				nCtaVlOri1-= &((cAliasQry)->("N3_VORIG" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaDpAcm1-= &((cAliasQry)->("N3_VRDACM" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaDpMes1-= &((cAliasQry)->("N3_VRDMES" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCtaCrDep1-= (cAliasQry)->N3_VRCDA1  
				nCtaCrMon1-= (cAliasQry)->N3_VRCACM1 
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Acumula Conta em Moeda do Ativo³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lAtClDepr .or. SN1->N1_PATRIM $ " P"
				nCtaVlOri2+= &((cAliasQry)+"->N3_VORIG"+cMoeda)
				nCtaDpAcm2+= &((cAliasQry)+"->N3_VRDACM"+cMoeda)
				nCtaDpMes2+= &((cAliasQry)+"->N3_VRDMES"+cMoeda)
				nCtaCrDep2+= IIf(cMoeda=="1",(cAliasQry)->N3_VRCDA1,0)  
				nCtaCrMon2+= IIf(cMoeda=="1",(cAliasQry)->N3_VRCACM1,0) 
			Else
				nCtaVlOri2-= &((cAliasQry)+"->N3_VORIG"+cMoeda)
				nCtaDpAcm2-= &((cAliasQry)+"->N3_VRDACM"+cMoeda)
				nCtaDpMes2-= &((cAliasQry)+"->N3_VRDMES"+cMoeda)
				nCtaCrDep2-= IIf(cMoeda=="1",(cAliasQry)->N3_VRCDA1,0)  
				nCtaCrMon2-= IIf(cMoeda=="1",(cAliasQry)->N3_VRCACM1,0) 
			Endif
                 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Acumula Centro de Custo em Moeda 1 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lAtClDepr .or. SN1->N1_PATRIM $ " P"
				nValOri1+= &((cAliasQry)->("N3_VORIG" + AllTrim( Str( MV_PAR07 ) ) ) )
				nDepAcm1+= &((cAliasQry)->("N3_VRDACM" + AllTrim( Str( MV_PAR07 ) ) ) )
				nDepMes1+= &((cAliasQry)->("N3_VRDMES" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCorDep1 += (cAliasQry)->N3_VRCDA1 
				nCorMon1 += (cAliasQry)->N3_VRCACM1
			Else
				nValOri1-= &((cAliasQry)->("N3_VORIG" + AllTrim( Str( MV_PAR07 ) ) ) )
				nDepAcm1-= &((cAliasQry)->("N3_VRDACM" + AllTrim( Str( MV_PAR07 ) ) ) )
				nDepMes1-= &((cAliasQry)->("N3_VRDMES" + AllTrim( Str( MV_PAR07 ) ) ) )
				nCorDep1 -= (cAliasQry)->N3_VRCDA1 
				nCorMon1 -= (cAliasQry)->N3_VRCACM1
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Acumula Centro de Custo em Moeda do Ativo ³ 
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lAtClDepr .or. SN1->N1_PATRIM $ " P"
				nValOri2 += &((cAliasQry)+"->N3_VORIG"+cMoeda)
				nDepAcm2 += &((cAliasQry)+"->N3_VRDACM"+cMoeda)
				nDepMes2 += &((cAliasQry)+"->N3_VRDMES"+cMoeda)
				nCorDep2 += IIf(cMoeda=="1",(cAliasQry)->N3_VRCDA1,0)
				nCorMon2 += IIf(cMoeda=="1",(cAliasQry)->N3_VRCACM1,0)
			Else
				nValOri2 -= &((cAliasQry)+"->N3_VORIG"+cMoeda)
				nDepAcm2 -= &((cAliasQry)+"->N3_VRDACM"+cMoeda)
				nDepMes2 -= &((cAliasQry)+"->N3_VRDMES"+cMoeda)
				nCorDep2 -= IIf(cMoeda=="1",(cAliasQry)->N3_VRCDA1,0)
				nCorMon2 -= IIf(cMoeda=="1",(cAliasQry)->N3_VRCACM1,0)
			Endif
			dbSelectArea(cAliasQry)
			dbSkip()
			lSkip := .F.
		End
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ ImpressÆo dos dados em questÆo  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		If lSkip
			dbSelectArea(cAliasQry)
			dbSkip()
			lSkip := .F.
		Else		
			@li,000				PSAY cEntidade
			@li,COL_CCUSTO		PSAY cCentroCus 
			@li,COL_CONTA		PSAY cConta 
			@li,COL_MOEDA 		PSAY SubStr(cMoeda1,1,5)                 
			@li,COL_ORIGINAL	PSAY nCtaVlOri1  Picture PesqPict("SN3","N3_VORIG1", 18)
			@li,COL_DEP_ACUM	PSAY nCtaDpAcm1  Picture PesqPict("SN3","N3_VRDACM1",18)
			@li,COL_DEP_MES 	PSAY nCtaDpMes1  Picture PesqPict("SN3","N3_VRDMES1",18)
			@li,COL_CORDEPACU	PSAY nCtaCrDep1  Picture PesqPict("SN3","N3_VRCDA1", 18)
			@li,COL_CORRECAO	PSAY nCtaCrMon1  Picture PesqPict("SN3","N3_VRCACM1",18)
			Li ++
			@li,COL_MOEDA 		PSAY SubStr(cMoeda2,1,5)
			@li,COL_ORIGINAL	PSAY nCtaVlOri2  Picture PesqPict("SN3","N3_VORIG"+cMoeda, 18)
			@li,COL_DEP_ACUM	PSAY nCtaDpAcm2  Picture PesqPict("SN3","N3_VRDACM"+cMoeda,18)
			@li,COL_DEP_MES 	PSAY nCtaDpMes2  Picture PesqPict("SN3","N3_VRDMES"+cMoeda,18)
			Li++
			dbSelectArea(cAliasQry)
			nCtaVlOri1:=nCtaDpAcm1:=nCtaDpMes1:=nCtaCrDep1:=nCtaCrMon1:= 0
			nCtaVlOri2:=nCtaDpAcm2:=nCtaDpMes2:=nCtaCrDep2:=nCtaCrMon2:= 0
		Endif
	End   
    
	If lSkip
		dbSelectArea(cAliasQry)
		dbSkip()
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Mostra Total da Entidade Base ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nValOri1 != 0 .Or. nDepAcm1 != 0 .Or. nValOri2 != 0 .Or. nDepAcm2 != 0
			Li++
			@li,000 			PSAY "Tot " + CtbSayApro("CTD") + ": "
			@li,COL_CCUSTO		PSAY cEntidade 
			@li,COL_MOEDA 		PSAY SubStr(cMoeda1,1,5)
			@li,COL_ORIGINAL	PSAY nValOri1 Picture PesqPict("SN3","N3_VORIG1", 18)
			@li,COL_DEP_ACUM	PSAY nDepAcm1 Picture PesqPict("SN3","N3_VRDACM1",18)
			@li,COL_DEP_MES 	PSAY nDepMes1 Picture PesqPict("SN3","N3_VRDMES1",18)
			@li,COL_CORDEPACU	PSAY nCorDep1 Picture PesqPict("SN3","N3_VRCDA1", 18)
			@li,COL_CORRECAO	PSAY nCorMon1 Picture PesqPict("SN3","N3_VRCACM1",18)
			Li ++
			@li,COL_MOEDA 		PSAY SubStr(cMoeda2,1,5)
			@li,COL_ORIGINAL	PSAY nValOri2 Picture PesqPict("SN3","N3_VORIG"+cMoeda, 18)
			@li,COL_DEP_ACUM	PSAY nDepAcm2 Picture PesqPict("SN3","N3_VRDACM"+cMoeda,18)
			@li,COL_DEP_MES 	PSAY nDepMes2 Picture PesqPict("SN3","N3_VRDMES"+cMoeda,18)
			Li+=2
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Acumula Total Geral em Moeda 1     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nGerOri1  += nValOri1
		nGerDepA1 += nDepAcm1
		nGerDepM1 += nDepMes1
		nGerCD1   += nCorDep1
		nGerCMon1 += nCorMon1
      
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Acumula Total Geral em Moeda do Ativo ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nGerOri2  += nValOri2
		nGerDepA2 += nDepAcm2
		nGerDepM2 += nDepMes2
		nGerCD2   += nCorDep2
		nGerCMon2 += nCorMon2
      
   		nValOri1:=nDepAcm1:=nDepMes1:=nCorDep1:=nCorMon1:= 0
		nValOri2:=nDepAcm2:=nDepMes2:=nCorDep2:=nCorMon2:= 0
	Endif

End    
       
If li != 80
	If li + 4 > 58		// Somo 4 pois sao as linhas necessarias para a totalizacao
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTamanho)
	End

	@ ++li,00 PSAY __PrtfatLine()
	Li++
	@li,000 			PSAY OemToAnsi(STR0007) // "TOTAL GERAL       : "
	@li,COL_MOEDA 		PSAY SubStr(cMoeda1,1,5)
	@li,COL_ORIGINAL	PSAY nGerOri1  Picture PesqPict("SN3","N3_VORIG1", 18)
	@li,COL_DEP_ACUM	PSAY nGerDepA1 Picture PesqPict("SN3","N3_VRDACM1",18)
	@li,COL_DEP_MES 	PSAY nGerDepM1 Picture PesqPict("SN3","N3_VRDMES1",18)
	@li,COL_CORDEPACU	PSAY nGerCD1   Picture PesqPict("SN3","N3_VRCDA1", 18)
	@li,COL_CORRECAO	PSAY nGerCMon1 Picture PesqPict("SN3","N3_VRCACM1",18)
	Li ++
	@li,COL_MOEDA 		PSAY SubStr(cMoeda2,1,5)
	@li,COL_ORIGINAL	PSAY nGerOri2  Picture PesqPict("SN3","N3_VORIG"+cMoeda, 18)
	@li,COL_DEP_ACUM	PSAY nGerDepA2 Picture PesqPict("SN3","N3_VRDACM"+cMoeda,18)
	@li,COL_DEP_MES 	PSAY nGerDepM2 Picture PesqPict("SN3","N3_VRDMES"+cMoeda,18)
	@ ++li,00 PSAY __PrtfatLine()
	Roda(cbcont,cbtxt,Tamanho)
End

dbSelectArea("SN3")
dbSetOrder(1)

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	Ourspool(wnrel)
End

MS_FLUSH() 

Return