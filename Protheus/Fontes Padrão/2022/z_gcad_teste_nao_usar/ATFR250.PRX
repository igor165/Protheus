#INCLUDE "ATFR250.CH"
#INCLUDE "Protheus.ch"

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ATFR250   ºAutor  ³Carlos A. Gomes Jr. º Data ³  06/06/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Relacao de aquisicoes por transferencia                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFR250
Local oReport

If TRepInUse()
	oReport := ReportDef()
	oReport:PrintDialog()
Else
    Return ATFR250R3() // Executa versão anterior do fonte
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ATFR260   ºAutor  ³Carlos A. Gomes Jr. º Data ³  06/06/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Definicao do objeto do relatorio personalizavel e das      º±±
±±º          ³ secoes que serao utilizadas                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportDef()

Local oReport,oSectionT,oSection1,oSectionA
Local lCtb    := CtbInUse()
Local cReport := "ATFR250"
Local cTitulo := OemToAnsi(STR0004)  //"Aquis por Transferencia"
Local cDescri := OemToAnsi(STR0001) + OemToAnsi(STR0002) + OemToAnsi(STR0003) //"Este relatorio ira imprimir as Aquisicoes por Transf den- " ##"tro dos parametros especificados" ##"Aquisicoes Por Transf"

Local cAliasCTB := Iif(lCtb,"CT1","SI1")
Local cCpoCTB   := Iif(lCtb,"CT1_DESC01","I1_DESC")

Pergunte( "AFR250" , .F. )

oReport  := TReport():New( cReport, cTitulo, "AFR250" , { |oReport| ReportPrint( oReport, lCtb , cAliasCTB ) }, cDescri )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a secao Origem / Destino                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectionT := TRSection():New( oReport, STR0029, {"SN4","SN3","SN1",cAliasCTB} ) // "Origem/Destino"
TRCell():New( oSectionT, "cTipoMov"   ,/*Alias*/,STR0029,/*Picture*/,20,/*lPixel*/,/*{|| code-block de impressao }*/)					// Origem ou Destino
TRCell():New( oSectionT, "cTxt1"      ,/*Alias*/,StrTran(STR0011,"  -  ",""),/*Picture*/,15,/*lPixel*/,{|| OemToAnsi(STR0011) } )							// Moveimento no.
TRCell():New( oSectionT, "N4_CODBAIX" , "SN4"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Movimento
TRCell():New( oSectionT, "cTxt2"      ,/*Alias*/,STR0030,/*Picture*/,10,/*lPixel*/,{|| OemToAnsi(STR0012) })	// "Texto da Data"
TRCell():New( oSectionT, "N4_DATA" 		, "SN4"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Movimento
oSectionT:SetHeaderSection(.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a 1a. secao - Dados do Ativo                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New( oSectionT, STR0031 ) // "Dados do Ativo"
TRCell():New( oSection1, "N4_CBASE"   , "SN4"   ,/*X3Titulo*/,/*Picture*/,LEN(SN4->N4_CBASE)+2,/*lPixel*/,/*{|| code-block de impressao }*/)	// Codigo
TRCell():New( oSection1, "N4_ITEM"    , "SN4"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Item
TRCell():New( oSection1, "N3_TIPO"    , "SN3"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Item
TRCell():New( oSection1, "N3_TPSALDO" , "SN3"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Item
TRCell():New( oSection1, "N1_DESCRIC" , "SN1"   ,/*X3Titulo*/,/*Picture*/,35         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// DescricaoTRCell():New( oSection1, "N1_AQUISIC" , "SN1"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Data Aquisicao
TRCell():New( oSection1, "N1_AQUISIC" , "SN1"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// DescricaoTRCell():New( oSection1, "N1_AQUISIC" , "SN1"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Data Aquisicao
TRCell():New( oSection1, "N1_CHAPA"   , "SN1"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Chapa
TRCell():New( oSection1, "N3_CUSTBEM" , "SN3"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Centro de Custo
TRCell():New( oSection1, "N3_CCONTAB" , "SN3"   ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Conta contabil
TRCell():New( oSection1, cCpoCTB      ,cAliasCTB,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Descricao conta
oSection1:SetHeaderPage(.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a secao "A" - Valor Original                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectionA := TRSection():New( oSection1, STR0037  ) // "A - Valor Original"
TRCell():New( oSectionA, "cDescValor" ,/*Alias*/,OemtoAnsi(STR0027),/*Picture*/,27,/*lPixel*/,{|| OemtoAnsi(STR0014) } )			//"Valor Original         --->"
TRCell():New( oSectionA, "nValMoeda1" ,/*Alias*/,STR0032,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// "*** MOEDA 1 ***"
TRCell():New( oSectionA, "nValMoeda2" ,/*Alias*/,STR0033,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// "*** MOEDA 2 ***"
TRCell():New( oSectionA, "nValMoeda3" ,/*Alias*/,STR0034,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// "*** MOEDA 3 ***"
TRCell():New( oSectionA, "nValMoeda4" ,/*Alias*/,STR0035,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// "*** MOEDA 4 ***"
TRCell():New( oSectionA, "nValMoeda5" ,/*Alias*/,STR0036,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// "*** MOEDA 5 ***"
oSectionA:SetHeaderPage(.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a secao "B" - Depreciacao Acumulada           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectionB := TRSection():New( oSection1, STR0038 ) // "B - Depreciacao Acumulada" 
TRCell():New( oSectionB, "cDescValor" ,/*Alias*/,OemtoAnsi(STR0027),/*Picture*/,27,/*lPixel*/,{|| OemtoAnsi(STR0015) } )			//"Depreciacao Acumulada  --->"
TRCell():New( oSectionB, "nValMoeda1" ,/*Alias*/,STR0032,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda1
TRCell():New( oSectionB, "nValMoeda2" ,/*Alias*/,STR0033,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda2
TRCell():New( oSectionB, "nValMoeda3" ,/*Alias*/,STR0034,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda3
TRCell():New( oSectionB, "nValMoeda4" ,/*Alias*/,STR0035,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda4
TRCell():New( oSectionB, "nValMoeda5" ,/*Alias*/,STR0036,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda5
oSectionB:SetHeaderSection(.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a secao "C" - Correcao                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectionC := TRSection():New( oSection1, STR0039  ) // "C - Correcao"
TRCell():New( oSectionC, "cDescValor" ,/*Alias*/,OemtoAnsi(STR0027),/*Picture*/,27,/*lPixel*/,{|| OemtoAnsi(STR0016) } )			//"Correcao               --->"
TRCell():New( oSectionC, "nValMoeda1" ,/*Alias*/,STR0032,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda1
oSectionC:SetHeaderSection(.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a secao "D" - Residual                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSectionD := TRSection():New( oSection1, STR0040  ) // "D - Residual"
TRCell():New( oSectionD, "cDescValor" ,/*Alias*/,OemtoAnsi(STR0027),/*Picture*/,27,/*lPixel*/,{|| OemtoAnsi(STR0017) } )			//"Residual               --->"
TRCell():New( oSectionD, "nValMoeda1" ,/*Alias*/,STR0032,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda1
TRCell():New( oSectionD, "nValMoeda2" ,/*Alias*/,STR0033,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda2
TRCell():New( oSectionD, "nValMoeda3" ,/*Alias*/,STR0034,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda3
TRCell():New( oSectionD, "nValMoeda4" ,/*Alias*/,STR0035,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda4
TRCell():New( oSectionD, "nValMoeda5" ,/*Alias*/,STR0036,/*Picture*/,16         ,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Moeda5
oSectionD:SetHeaderSection(.F.)

oReport:SetTotalInLine(.F.)
oSectionT:SetNoFilter({"SN1", "SN3","CT1"})

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ReportPrintºAutor  ³Carlos A. Gomes Jr. º Data ³ 06/05/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Query de impressao do relatorio                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint( oReport , lCtb , cAliasCTB )

Local oSectionT := oReport:Section(1)
Local oSection1 := oSectionT:Section(1)
Local oSectionA := oSection1:Section(1)
Local oSectionB := oSection1:Section(2)
Local oSectionC := oSection1:Section(3)
Local oSectionD := oSection1:Section(4)

Local oBreakA,oBreakB,oBreakC,oBreakD

Local cChave    := ""

Local cAliasSN3 := "SN3"
Local cAliasSN4 := "SN4"
Local cAliasSN1 := "SN1"

Local cTpReg    := ""
Local cCpoCTB   := Iif(lCtb,"CT1_DESC01","I1_DESC")

Local nOrDesAn  := 1
Local nOrDesAt  := 1
Local lAtfCusPrv := AFXAtCsPrv()

Local cQuery := ""
Local cFrom  := ""
Local cWhere := ""
Local cFiltro := ""

Local cMovim   := ""
Local cN1TipoNeg := Alltrim(SuperGetMv("MV_N1TPNEG",.F.,"")) // Tipos de N1_PATRIM que aceitam Valor originais negativos
Local cN3TipoNeg := Alltrim(SuperGetMv("MV_N3TPNEG",.F.,"")) // Tipos de N3_TIPO que aceitam Valor originais negativos   

Local aClassif := {}
Local cClassif := ""


oSectionA:Cell("cDescValor"):SetTitle("")
oSectionB:Cell("cDescValor"):SetTitle("")
oSectionC:Cell("cDescValor"):SetTitle("")
oSectionD:Cell("cDescValor"):SetTitle("")

If mv_par06 == 1
	aClassif := AdmGetClass()
	If Len( aClassif ) <= 0
		Return
	EndIf 			
EndIf

	cFrom  := "%"+RetSqlName(cAliasCTB)+" "+cAliasCTB+"%"
	
	If lCtb
		cWhere := "CT1.CT1_FILIAL = '"+xFilial("CT1")+"'  AND "
		cWhere += "SN3.N3_CCONTAB = CT1.CT1_CONTA AND "
		cWhere += "SN1.D_E_L_E_T_ = ' '" 
	Else
		cWhere := "SI1.SI1_FILIAL = '"+xFilial("CT1")+"'  AND "
		cWhere += "SN3.N3_CCONTAB = SI1.I1_CODIGO AND "
		cWhere += "SI1.D_E_L_E_T_ = ' '"
	EndIf
	//Filtra as classificações
	If Len(aClassif) > 0 
		cWhere += " AND SN1.N1_PATRIM IN " + FORMATCLAS(aClassif,.T.) 
	EndIf

	If lAtfCusPrv .And. MV_PAR07 == 2
		cWhere += " AND SN3.N3_ATFCPR <> '1' "
	EndIf 
	
	cWhere := "%"+cWhere+"%"

	If mv_par05 == 2
		cTpReg := "(N4_OCORR = '01' OR N4_OCORR = '05') AND N4_TIPOCNT = '1'"
	ElseIf mv_par05 == 3
		cTpReg := "(N4_OCORR = '15' OR N4_OCORR = '16' OR N4_OCORR = '01' OR N4_OCORR = '05') AND N4_TIPOCNT = '1'"
	Else
		cTpReg := "(N4_OCORR = '15' OR N4_OCORR = '16')"
	Endif
	cTpReg := "%"+cTpReg+"%"

	cChave    := "N4_FILIAL + N4_DATA + N4_CONTA + N4_CODBAIX + N4_OCORR"
	cChave	 := "%" + SqlOrder(cChave) + "%"
	
	cQuery    := GetNextAlias()
	cAliasSN4 := cQuery
	cAliasSN3 := cQuery
	cAliasSN1 := cQuery
	cAliasCTB := cQuery
	cCpoCTB   := "%"+cCpoCTB+"%"
		
	oSectionT:BeginQuery()
	BeginSql Alias cQuery
		SELECT
			N4_CBASE, N4_ITEM, N4_OCORR, N4_CODBAIX, N4_DATA,
			N3_CUSTBEM, N3_CCONTAB, N3_VRCACM1, N3_TIPO,N3_TPSALDO,
			N3_VORIG1, N3_VORIG2, N3_VORIG3, N3_VORIG4, N3_VORIG5,
			N3_VRDACM1, N3_VRDACM2, N3_VRDACM3, N3_VRDACM4, N3_VRDACM5,
			N3_AMPLIA1, N3_AMPLIA2, N3_AMPLIA3, N3_AMPLIA4, N3_AMPLIA5,
			N1_DESCRIC, N1_AQUISIC, N1_PATRIM, N1_CHAPA,
			%Exp:cCpoCTB%
		FROM 
			%table:SN4% SN4, %table:SN3% SN3, %table:SN1% SN1, %Exp:cFrom%
		WHERE
			SN4.N4_FILIAL = %xfilial:SN4% AND
			N4_CONTA BETWEEN %Exp:mv_par03% AND %Exp:mv_par04% AND
			N4_DATA BETWEEN %Exp:mv_par01% AND %Exp:mv_par02% AND
			%Exp:cTpReg% AND
			N4_CODBAIX <> ' ' AND
			SN4.%notDel% AND
			SN3.N3_FILIAL = %xfilial:SN3% AND
			SN3.N3_CBASE  = SN4.N4_CBASE AND
			SN3.N3_ITEM   = SN4.N4_ITEM AND 
			SN3.N3_TIPO   = SN4.N4_TIPO AND
			SN3.%notDel% AND
			SN1.N1_FILIAL = %xfilial:SN1%  AND
			SN1.N1_CBASE = SN4.N4_CBASE AND
			SN1.N1_ITEM  = SN4.N4_ITEM AND
			SN1.%notDel% AND
			%Exp:cWhere%
		ORDER BY %Exp:cChave%
	EndSql
	oSectionT:EndQuery()
	
	oSection1:SetParentQuery()
	oSectionA:SetParentQuery()
	oSectionB:SetParentQuery()
	oSectionC:SetParentQuery()
	oSectionD:SetParentQuery()

oSectionA:Cell("nValMoeda1"):SetPicture(PesqPict("SN3","N3_VORIG1",16,1))
oSectionA:Cell("nValMoeda2"):SetPicture(PesqPict("SN3","N3_VORIG2",16,1))
oSectionA:Cell("nValMoeda3"):SetPicture(PesqPict("SN3","N3_VORIG3",16,1))
oSectionA:Cell("nValMoeda4"):SetPicture(PesqPict("SN3","N3_VORIG4",16,1))
oSectionA:Cell("nValMoeda5"):SetPicture(PesqPict("SN3","N3_VORIG5",16,1))

oSectionB:Cell("nValMoeda1"):SetPicture(PesqPict("SN3","N3_VRDACM1",16,1))
oSectionB:Cell("nValMoeda2"):SetPicture(PesqPict("SN3","N3_VRDACM2",16,1))
oSectionB:Cell("nValMoeda3"):SetPicture(PesqPict("SN3","N3_VRDACM3",16,1))
oSectionB:Cell("nValMoeda4"):SetPicture(PesqPict("SN3","N3_VRDACM4",16,1))
oSectionB:Cell("nValMoeda5"):SetPicture(PesqPict("SN3","N3_VRDACM5",16,1))

oSectionC:Cell("nValMoeda1"):SetPicture(PesqPict("SN3","N3_VRCACM1",16,1))

oSectionD:Cell("nValMoeda1"):SetPicture(PesqPict("SN3","N3_VRDACM1",16,1))
oSectionD:Cell("nValMoeda2"):SetPicture(PesqPict("SN3","N3_VRDACM2",16,1))
oSectionD:Cell("nValMoeda3"):SetPicture(PesqPict("SN3","N3_VRDACM3",16,1))
oSectionD:Cell("nValMoeda4"):SetPicture(PesqPict("SN3","N3_VRDACM4",16,1))
oSectionD:Cell("nValMoeda5"):SetPicture(PesqPict("SN3","N3_VRDACM5",16,1))

oSectionA:Cell("nValMoeda1"):SetBlock({|| (cAliasSN3)->(N3_VORIG1 + N3_AMPLIA1)  })
oSectionA:Cell("nValMoeda2"):SetBlock({|| (cAliasSN3)->(N3_VORIG2 + N3_AMPLIA2)  })
oSectionA:Cell("nValMoeda3"):SetBlock({|| (cAliasSN3)->(N3_VORIG3 + N3_AMPLIA3)  })
oSectionA:Cell("nValMoeda4"):SetBlock({|| (cAliasSN3)->(N3_VORIG4 + N3_AMPLIA4)  })
oSectionA:Cell("nValMoeda5"):SetBlock({|| (cAliasSN3)->(N3_VORIG5 + N3_AMPLIA5)  })

oSectionB:Cell("nValMoeda1"):SetBlock({|| (cAliasSN3)->N3_VRDACM1  })
oSectionB:Cell("nValMoeda2"):SetBlock({|| (cAliasSN3)->N3_VRDACM2  })
oSectionB:Cell("nValMoeda3"):SetBlock({|| (cAliasSN3)->N3_VRDACM3  })
oSectionB:Cell("nValMoeda4"):SetBlock({|| (cAliasSN3)->N3_VRDACM4  })
oSectionB:Cell("nValMoeda5"):SetBlock({|| (cAliasSN3)->N3_VRDACM5  })

oSectionC:Cell("nValMoeda1"):SetBlock({|| (cAliasSN3)->N3_VRCACM1  })

oSectionD:Cell("nValMoeda1"):SetBlock({|| (cAliasSN3)->(N3_VORIG1 + N3_AMPLIA1 + (N3_VRDACM1 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) + N3_VRCACM1 )  })
oSectionD:Cell("nValMoeda2"):SetBlock({|| (cAliasSN3)->(N3_VORIG2 + N3_AMPLIA2 + (N3_VRDACM2 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) )  })
oSectionD:Cell("nValMoeda3"):SetBlock({|| (cAliasSN3)->(N3_VORIG3 + N3_AMPLIA3 + (N3_VRDACM3 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) )  })
oSectionD:Cell("nValMoeda4"):SetBlock({|| (cAliasSN3)->(N3_VORIG4 + N3_AMPLIA4 + (N3_VRDACM4 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) )  })
oSectionD:Cell("nValMoeda5"):SetBlock({|| (cAliasSN3)->(N3_VORIG5 + N3_AMPLIA5 + (N3_VRDACM5 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) )  })

oBreakA := TRBreak():New ( oSectionT , { || (cAliasSN4)->N4_DATA } , OemtoAnsi(STR0028) + StrTran(OemtoAnsi(STR0014), "--->", ""))
oBreakB := TRBreak():New ( oSectionT , { || (cAliasSN4)->N4_DATA } , OemtoAnsi(STR0028) + StrTran(OemtoAnsi(STR0015), "--->", ""))
oBreakC := TRBreak():New ( oSectionT , { || (cAliasSN4)->N4_DATA } , OemtoAnsi(STR0028) + StrTran(OemtoAnsi(STR0016), "--->", ""))
oBreakD := TRBreak():New ( oSectionT , { || (cAliasSN4)->N4_DATA } , OemtoAnsi(STR0028) + StrTran(OemtoAnsi(STR0017), "--->", ""))

TRFunction():New ( oSectionA:Cell("nValMoeda1") , , "SUM" , oBreakA , , , , .F. , .F. )
TRFunction():New ( oSectionA:Cell("nValMoeda2") , , "SUM" , oBreakA , , , , .F. , .F. )
TRFunction():New ( oSectionA:Cell("nValMoeda3") , , "SUM" , oBreakA , , , , .F. , .F. )
TRFunction():New ( oSectionA:Cell("nValMoeda4") , , "SUM" , oBreakA , , , , .F. , .F. )
TRFunction():New ( oSectionA:Cell("nValMoeda5") , , "SUM" , oBreakA , , , , .F. , .F. )

TRFunction():New ( oSectionB:Cell("nValMoeda1") , , "SUM" , oBreakB , , , , .F. , .F. )
TRFunction():New ( oSectionB:Cell("nValMoeda2") , , "SUM" , oBreakB , , , , .F. , .F. )
TRFunction():New ( oSectionB:Cell("nValMoeda3") , , "SUM" , oBreakB , , , , .F. , .F. )
TRFunction():New ( oSectionB:Cell("nValMoeda4") , , "SUM" , oBreakB , , , , .F. , .F. )
TRFunction():New ( oSectionB:Cell("nValMoeda5") , , "SUM" , oBreakB , , , , .F. , .F. )

TRFunction():New ( oSectionC:Cell("nValMoeda1") , , "SUM" , oBreakC , , , , .F. , .F. )

TRFunction():New ( oSectionD:Cell("nValMoeda1") , , "SUM" , oBreakD , , , , .F. , .F. )
TRFunction():New ( oSectionD:Cell("nValMoeda2") , , "SUM" , oBreakD , , , , .F. , .F. )
TRFunction():New ( oSectionD:Cell("nValMoeda3") , , "SUM" , oBreakD , , , , .F. , .F. )
TRFunction():New ( oSectionD:Cell("nValMoeda4") , , "SUM" , oBreakD , , , , .F. , .F. )
TRFunction():New ( oSectionD:Cell("nValMoeda5") , , "SUM" , oBreakD , , , , .F. , .F. )


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicio da impressao do fluxo do relatório                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:SetMeter((cAliasSN4)->(RecCount()))

Do While !oReport:Cancel() .And. !(cAliasSN4)->( Eof() )

	If (cAliasSN4)->(N4_OCORR == "15" .Or. N4_OCORR == "01")
		oSectionT:Cell("cTipoMov"):SetBlock({|| OemtoAnsi(STR0010)  })
		nOrDesAn := 1
		nOrDesAt := 1
	Else
		oSectionT:Cell("cTipoMov"):SetBlock({|| OemtoAnsi(STR0013) })
		nOrDesAn := 2
		nOrDesAt := 2
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Abre a secao totalizadora do relatorio                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSectionT:Init()
	oReport:ThinLine ( )
	oSectionT:PrintLine()
	oReport:ThinLine ( )

	cMovim := (cAliasSN4)->N4_CODBAIX
	oSection1:Init()
	oSectionA:Init()
	oSectionB:Init()
	oSectionC:Init()
	oSectionD:Init()
	
	Do While !oReport:Cancel() .And. !(cAliasSN4)->( Eof() ) .And. (cAliasSN4)->N4_CODBAIX == cMovim .And. nOrDesAn == nOrDesAt

		oReport:IncMeter()	
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Imprime a 1a. secao do relatorio                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSection1:PrintLine()
		oReport:SkipLine()
		oSectionA:PrintLine()
		oSectionB:PrintLine()
		oSectionC:PrintLine()
		oSectionD:PrintLine()
		oReport:SkipLine()
						
		(cAliasSN4)->(dbSkip())
		If lOrDes := (cAliasSN4)->(N4_OCORR == "15" .Or. N4_OCORR == "01")
			nOrDesAt := 1
		Else
			nOrDesAt := 2
		EndIf
	EndDo
	
	oSection1:Finish()
	oSectionA:Finish()
	oSectionB:Finish()
	oSectionC:Finish()
	oSectionD:Finish()
	oSectionT:Finish()
	
EndDo

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR250  ³ Autor ³ Alberto N. Gama Junior³ Data ³ 21.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relacao de aquisicoes por transferencia                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ATFR250                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF - Somente Windows                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFR250R3
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL wnrel
LOCAL cString 	 := "SN4"
LOCAL cDesc1  	 := OemToAnsi(STR0001)  //"Este relatorio ira imprimir as Aquisicoes por Transf den- "
LOCAL cDesc2  	 := OemToAnsi(STR0002)  //"tro dos parametros especificados"
LOCAL cDesc3  	 := OemToAnsi(STR0003)  //"Aquisicoes Por Transf"
PRIVATE aReturn  := { "Zebrado", 1,"Administracao", 2, 2, 1, "",1 }
PRIVATE aLinha   := { }
PRIVATE cPerg    := "AFR250"
PRIVATE nomeprog := "ATFR250"
PRIVATE nLastKey := 0
Private nTamCC	 := Len(SN3->N3_CUSTBEM)
PRIVATE tamanho  := If(nTamCC > 9, "G", "M")
PRIVATE titulo   := OemToAnsi(STR0004)  //"Aquis por Transferencia"
PRIVATE cabec1
PRIVATE cabec2


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01            // Data Inicial                          ³
//³ mv_par02            // Data Final                            ³
//³ mv_par03            // Da Conta                              ³
//³ mv_par04            // Ate a conta                           ³
//³ mv_par06            //Selec Classif Patrimonial?	         ³  	 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  


pergunte( "AFR250",.F. )

wnrel := "ATFR250"
aOrd  := { OemToAnsi(STR0005) }  //" Data "
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,Tamanho)

If nLastKey == 27
	Return
Endif

SetDefault( aReturn,cString )
RptStatus({|lEnd| ATFR250Imp(@lEnd,wnRel,cString)},Titulo)

If nLastKey == 27
	Return
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ATFR250IMP³ Autor ³ Alberto N. Gama Junior³ Data ³ 21.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relacao de aquisicoes por transferencia                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ATFR250                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd    - A‡ao do Codeblock                                ³±±
±±³          ³ wnRel   - T¡tulo do relat¢rio                              ³±±
±±³          ³ cString - Arquivo Principal                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ATFR250.PRX                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³C. Denardi  ³06/10/03³66.785³Localizacao de casas decimais na Picture  ³±±
±±³            ³        ³      ³em todos os relatorios do Ativo Fixo      ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFR250Imp(lEnd,WnRel,cString)

LOCAL CbTxt, CbCont
LOCAL cMovim
Local nOrdem:=0
LOCAL aMotBaixa	:= {}, nLaco := 0, lImp := .F.
LOCAL limite 	:= 132
Local _nFds
                       
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Valores dos Itens de Origem e Destino - TOTAL DA QUEBRA - Moedas 1 a 5    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL aValorOrig := { 0, 0, 0, 0, 0 }  // Valor Original Origem
LOCAL aDepreOrig := { 0, 0, 0, 0, 0 }  // Depreciacao Acumulada Origem
LOCAL nCorreOrig := 0					// Correcao Origem
LOCAL aValorDest := { 0, 0, 0, 0, 0 }  // Valor Original Destino
LOCAL aDepreDest := { 0, 0, 0, 0, 0 }  // Depreciacao Acumulada Destino
LOCAL aCorreDest := 0					// Correcao Destino

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para o indice temporario no SN4         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cIndSN4, cChvSN4
LOCAL lImpCab1, lImpCab2
Local lCtb := CtbInUse()
Local cAliasQry
Local cDt2Prn := ""
Local cQb2Prn := ""
Local lPrtTot := .F.
Local cN1TipoNeg := Alltrim(SuperGetMv("MV_N1TPNEG",.F.,"")) // Tipos de N1_PATRIM que aceitam Valor originais negativos
Local cN3TipoNeg := Alltrim(SuperGetMv("MV_N3TPNEG",.F.,"")) // Tipos de N3_TIPO que aceitam Valor originais negativos  
Local aClassif := {}
Local cClassif := ""
Local lAtfCusPrv := AFXAtCsPrv()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbtxt    := SPACE( 10 )
cbcont   := 0
// ********  01234567890123456789012345678901234567890123456789012345678901234567890123456789012345679012345678901234567890123456789012345678901
// ********  0         1         2         3         4         5         6         7         8        9         0         1         2         3
cabec1 	:= OemToAnsi(STR0006)  //"COD. BASE  ITEM  DESCRICAO                           DT. AQUIS   CHAPA   C.CUSTO    CONTA                 DESCRICAO CONTA          "

If nTamCC > 9
	Cabec1 := Stuff(Cabec1, 84, 0, Repl(Space(1), nTamCC - 9))
Endif

cabec2	:= OemToAnsi(STR0007)  //"                                             *** MOEDA 1 ***   *** MOEDA 2 ***   *** MOEDA 3 ***   *** MOEDA 4 ***   *** MOEDA 5 ***"
cString  := "SN4"
li       := 80
m_pag    := 1
nOrdem 	:= aReturn[8]
titulo   += aOrd[nOrdem]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Seleciona os arquivos que serao utilizados                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SN1") // Ativos
dbSetOrder(1)		// Codigo Bem + Item

dbSelectArea("SN3")	// Saldos e Valores
dbSetOrder(1)		// Codigo Bem + Item + Tipo + Baixa + Sequencia
                 
If lCtb
	dbSelectArea("CT1")	// Plano de Contas
	dbSetOrder(1)			// Codigo Conta
Else
	dbSelectArea("SI1")	// Plano de Contas
	dbSetOrder(1)			// Codigo Conta
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a Chave, a Expressao para quebra e indexa o SN4       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOrdem == 1   // Por Data
	cChvSN4:= "N4_FILIAL + DTOS(N4_DATA) + N4_CODBAIX + N4_OCORR"
	cCond2 := "cQuebra == N4_DATA"
	cCond1 := "STOD(N4_DATA) >= mv_par01 .and. STOD(N4_DATA) <= mv_par02"
Else				  // Por Conta
	// *** NAO ESTA' SENDO UTILIZADO
	cChvSN4:= "N4_FILIAL + DTOS(N4_DATA) + N4_CONTA + N4_CODBAIX + N4_OCORR"
	cCond1 := "N4_DATA >= mv_par01 .and. N4_DATA <= mv_par02"
	cCond2 := "cQuebra == N4_CONTA"
EndIf

If mv_par06 == 1
	aClassif := AdmGetClass()	
EndIf

		If mv_par05 = 2
			cTpReg := "(N4_OCORR = '01' OR N4_OCORR = '05') AND N4_TIPOCNT = '1'"
		ElseIf mv_par05 = 3
			cTpReg := "(N4_OCORR = '15' OR N4_OCORR = '16' OR N4_OCORR = '01' OR N4_OCORR = '05') AND N4_TIPOCNT = '1'"
		Else
			cTpReg := "(N4_OCORR = '15' OR N4_OCORR = '16')"
		Endif
		
		cAliasQry := "SN4QRY"
		cQry := " SELECT * " 
		cQry += " FROM "+RetSqlName("SN4")+" SN4 "
		cQry += " WHERE N4_FILIAL = '"+xFilial("SN4")+"' "
		cQry += " AND N4_CONTA >= '"+mv_par03+"' "
		cQry += " AND N4_CONTA <= '"+mv_par04+"' " 
		cQry += " AND N4_DATA >= '"+DTOS(mv_par01)+"' " 
		cQry += " AND N4_DATA <= '"+DTOS(mv_par02)+"' "
		cQry += " AND "+cTpReg
		cQry += " AND N4_CODBAIX <> ' ' "
		cQry += " AND SN4.D_E_L_E_T_ = ' ' "
		cQry += " ORDER BY "+SqlOrder(cChvSn4)

		If Select(cAliasQry) > 0
			dbSelectArea(cAliasQry)
			dbCloseArea()
		Endif

		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQry), cAliasQry, .F., .T.)
		aFdsQry := dbStruct()
		dbSelectArea("SX3")
		dbSetOrder(2)
		For _nFds := 1 to Len(aFdsQry)
			If ! aFdsQry[_nFds,2] $ "CM"				/// SE NAO FOR CARACTER OU MEMO
				MsSeek(aFdsQry[_nFds,1],.T.)
				TCSetField(cAliasQry, SX3->X3_CAMPO, SX3->X3_TIPO, SX3->X3_TAMANHO, SX3->X3_DECIMAL)
			Endif
		Next
		dbSelectArea(cAliasQry)

Do While (cAliasQry)->(!Eof() .and. N4_FILIAL == xFilial("SN4"))
                                   
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ ZERA OS TOTAIS PARA A QUEBRA                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aValorOrig 	:= { 0, 0, 0, 0, 0 }  	// Valor Original Origem
	aDepreOrig 	:= { 0, 0, 0, 0, 0 }  	// Depreciacao Acumulada Origem
	nCorreOrig 	:= 0					// Correcao Origem
	           	
	aValorDest 	:= { 0, 0, 0, 0, 0 }  	// Valor Original Destino
	aDepreDest 	:= { 0, 0, 0, 0, 0 }  	// Depreciacao Acumulada Destino
	nCorreDest 	:= 0					// Correcao Destino

	cQuebra  	:= Iif( nOrdem == 1, (cAliasQry)->(N4_DATA),(cAliasQry)->(N4_CONTA) )
    
    lPrtTot		:= .F.
    
	Do While (cAliasQry)->(!Eof() .and. N4_FILIAL == xFilial("SN4")) .AND. (cAliasQry)->(&cCond1 .AND. &cCond2)
	
		cMovim 	 := (cAliasQry)->(N4_CODBAIX)
		lImpCab1 := lImpCab2 := .T.
		
	    lPrtTot		:= .F.

		Do While (cAliasQry)->(!Eof() .and. N4_FILIAL == xFilial("SN4")) .AND. (cAliasQry)->(&cCond1 .AND. &cCond2 .AND. N4_CODBAIX ==cMovim )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Localiza item no SN1, SN3 e a conta no SI1                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SN1")
			dbSeek( xFilial("SN1") + (cAliasQry)->(N4_CBASE + N4_ITEM), .F. ) 
			//Verifica se filtra as classificações patrimoniais
			If Len(aClassif) > 0
				cClassif := FormatClass(aClassif,.F.)
				If !(SN1->N1_PATRIM $ cClassif)
					dbSelectArea(cAliasQry)
					dbSkip()
					Loop    
				EndIf
			EndIf  
			       
			dbSelectArea("SN3")
			dbSeek( xFilial("SN3") + (cAliasQry)->(N4_CBASE + N4_ITEM + N4_TIPO), .T. )

			If (mv_par05 = 2 .Or. mv_par05 = 3) .And. SN3->N3_IDBAIXA = "2" .And. ((cAliasQry)->(N4_OCORR) = "01" .And. Empty((cAliasQry)->(N4_MOTIVO)))
				dbSelectArea(cAliasQry)
				dbSkip()
				Loop
			Endif
			
			If lAtfCusPrv .And. MV_PAR07 == 2
				If SN3->N3_ATFCPR == '1'
					dbSelectArea(cAliasQry)
					dbSkip()
					Loop
				EndIf
			EndIf

			If lCtb
				dbSelectArea("CT1")
				dbSetOrder(1)
				dbSeek( xFilial("CT1") + SN3->N3_CCONTAB, .F. )
			Else
				dbSelectArea("SI1")
				dbSeek( xFilial("SI1") + SN3->N3_CCONTAB, .F. )
			EndIf
       
			dbSelectArea("SN4")

			If li > 56
				cabec( titulo, cabec1, cabec2, nomeprog, tamanho, Iif(aReturn[4]==1,15,18))
			End
      
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Faz a Separacao entre Itens Origem e Destino pela ocorrencia        ³
			//³       15 = Itens Origem   /  16 = Itens Destino                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ((cAliasQry)->(N4_OCORR == "15" .Or. N4_OCORR == "01")) .AND. lImpCab1
				If ValType((cAliasQry)->(N4_DATA)) == "C"
		      		cDt2Prn := DTOC(STOD((cAliasQry)->(N4_DATA)))				/// COM QUERY
		  		Else
		      		cDt2Prn := DTOC((cAliasQry)->(N4_DATA))					/// CODEBASE
		  		Endif
				@ li,  0  PSAY Replicate("-",132 + (nTamCC - 9))
				li++
				@ li,  0  PSAY OemtoAnsi(STR0010) + OemToAnsi(STR0011) + (cAliasQry)->(N4_CODBAIX) + OemToAnsi(STR0012) + cDt2Prn  //"ITENS DE ORIGEM"###"  -  MOVIMENTO No. "###"  -  Data: "
				li+=2
				@ li,  0  PSAY Replicate("-",132 + (nTamCC - 9))
				lImpCab1 := .F.
				li := li + 3
			ElseIf (cAliasQry)->((N4_OCORR == "16" .Or. N4_OCORR == "05")) .AND. lImpCab2
	      		@ li  ,  0  PSAY Replicate("-",132 + (nTamCC - 9))
				@ li+1,  0  PSAY OemToAnsi(STR0013) + OemToAnsi(STR0011) + (cAliasQry)->(N4_CODBAIX) + OemtoAnsi(STR0012) + cDt2Prn  //"ITENS DESTINO"###"  -  MOVIMENTO No. "###"  -  Data: "
				@ li+2,  0  PSAY Replicate("-",132 + (nTamCC - 9))
				lImpCab2 := .F.
				li := li + 3
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Imprime os Dados Movimento / Bem                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			@ li,  0    PSAY    (cAliasQry)->(N4_CBASE)
			@ li, 11    PSAY    (cAliasQry)->(N4_ITEM)
			@ li, 17    PSAY    Substr(SN1->N1_DESCRIC,1,35)
			@ li, 53    PSAY    SN1->N1_AQUISIC
			@ li, 65    PSAY    SN1->N1_CHAPA
			@ li, 73    PSAY    SN3->N3_CUSTBEM
			@ li, 84 + (nTamCC - 9) PSAY SN3->N3_CCONTAB
			@ li, 106 + (nTamCC - 9) PSAY Iif(lCtb,Substr(CT1->CT1_DESC01,1,25),Substr(SI1->I1_DESC,1,25))
			li := li + 2

  			If (cAliasQry)->(N4_OCORR == "15" .Or. N4_OCORR == "01")
  	       // Totais por Quebra
				aValorOrig[1] := aValorOrig[1] + SN3->N3_VORIG1
				aValorOrig[2] := aValorOrig[2] + SN3->N3_VORIG2
				aValorOrig[3] := aValorOrig[3] + SN3->N3_VORIG3
				aValorOrig[4] := aValorOrig[4] + SN3->N3_VORIG4
				aValorOrig[5] := aValorOrig[5] + SN3->N3_VORIG5

				aDepreOrig[1] := aDepreOrig[1] + SN3->N3_VRDACM1  
				aDepreOrig[2] := aDepreOrig[2] + SN3->N3_VRDACM2  
				aDepreOrig[3] := aDepreOrig[3] + SN3->N3_VRDACM3  
				aDepreOrig[4] := aDepreOrig[4] + SN3->N3_VRDACM4  
				aDepreOrig[5] := aDepreOrig[5] + SN3->N3_VRDACM5  
     	
				nCorreOrig := nCorreOrig + SN3->N3_VRCACM1

			ElseIf (cAliasQry)->(N4_OCORR == "16" .Or. N4_OCORR == "05")
	       	// Totais por Quebra
				aValorDest[1] := aValorDest[1] + SN3->N3_VORIG1 + SN3->N3_AMPLIA1
				aValorDest[2] := aValorDest[2] + SN3->N3_VORIG2 + SN3->N3_AMPLIA2
				aValorDest[3] := aValorDest[3] + SN3->N3_VORIG3 + SN3->N3_AMPLIA3
				aValorDest[4] := aValorDest[4] + SN3->N3_VORIG4 + SN3->N3_AMPLIA4
				aValorDest[5] := aValorDest[5] + SN3->N3_VORIG5 + SN3->N3_AMPLIA5

				aDepreDest[1] := aDepreDest[1] + SN3->N3_VRDACM1  
				aDepreDest[2] := aDepreDest[2] + SN3->N3_VRDACM2  
				aDepreDest[3] := aDepreDest[3] + SN3->N3_VRDACM3  
				aDepreDest[4] := aDepreDest[4] + SN3->N3_VRDACM4  
				aDepreDest[5] := aDepreDest[5] + SN3->N3_VRDACM5  
			
				nCorreDest := nCorreDest + SN3->N3_VRCACM1

			EndIf	

			If li > 56
				cabec( titulo, cabec1, cabec2, nomeprog, tamanho, Iif(aReturn[4]==1,15,18))
			End

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Imprime os valores do Item atual                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			@ li,  15   PSAY OemtoAnsi(STR0014)  //"Valor Original         --->"
			@ li,  44   PSAY SN3->N3_VORIG1 + SN3->N3_AMPLIA1 Picture PesqPict("SN3","N3_VORIG1",16,1)  // Moeda 1
			@ li,  62   PSAY SN3->N3_VORIG2 + SN3->N3_AMPLIA2 Picture PesqPict("SN3","N3_VORIG2",16,2)  // Moeda 2
			@ li,  80   PSAY SN3->N3_VORIG3 + SN3->N3_AMPLIA3 Picture PesqPict("SN3","N3_VORIG3",16,3)  // Moeda 3
			@ li,  98   PSAY SN3->N3_VORIG4 + SN3->N3_AMPLIA4 Picture PesqPict("SN3","N3_VORIG4",16,4)  // Moeda 4
			@ li, 116   PSAY SN3->N3_VORIG5 + SN3->N3_AMPLIA5 Picture PesqPict("SN3","N3_VORIG5",16,5)  // Moeda 5
			li++
			
			@ li,  15   PSAY OemtoAnsi(STR0015)  //"Depreciacao Acumulada  --->"
			@ li,  44   PSAY SN3->N3_VRDACM1 Picture PesqPict("SN3","N3_VRDACM1",16,1) // Moeda 1
			@ li,  62   PSAY SN3->N3_VRDACM2 Picture PesqPict("SN3","N3_VRDACM2",16,2) // Moeda 2
			@ li,  80   PSAY SN3->N3_VRDACM3 Picture PesqPict("SN3","N3_VRDACM3",16,3) // Moeda 3
			@ li,  98   PSAY SN3->N3_VRDACM4 Picture PesqPict("SN3","N3_VRDACM4",16,4) // Moeda 4
	 		@ li, 116   PSAY SN3->N3_VRDACM5 Picture PesqPict("SN3","N3_VRDACM5",16,5) // Moeda 5
			li++

			@ li,  15   PSAY OemToAnsi(STR0016)  //"Correcao               --->"
			@ li,  44   PSAY SN3->N3_VRCACM1  Picture PesqPict("SN3","N3_VRCACM1",16,1) // Moeda 1
			li++

			@ li,  15   PSAY OemToAnsi(STR0017)  //"Residual               --->"
			If SN3->N3_TIPO = "05" .Or. SN1->N1_PATRIM $ cN1TipoNeg .Or. SN3->N3_TIPO $ cN3TipoNeg
				@ li,  44   PSAY SN3->N3_VORIG1 + SN3->N3_AMPLIA1 + Abs(SN3->N3_VRDACM1 + SN3->N3_VRCACM1) 	Picture PesqPict("SN3","N3_VRDACM1",16,1) // Moeda 1
				@ li,  62   PSAY SN3->N3_VORIG2 + SN3->N3_AMPLIA2 + SN3->N3_VRDACM2 					Picture PesqPict("SN3","N3_VRDACM2",16,2) // Moeda 2
				@ li,  80   PSAY SN3->N3_VORIG3 + SN3->N3_AMPLIA3 + SN3->N3_VRDACM3 					Picture PesqPict("SN3","N3_VRDACM3",16,3) // Moeda 3
				@ li,  98   PSAY SN3->N3_VORIG4 + SN3->N3_AMPLIA4 + SN3->N3_VRDACM4 					Picture PesqPict("SN3","N3_VRDACM4",16,4) // Moeda 4
				@ li, 116   PSAY SN3->N3_VORIG5 + SN3->N3_AMPLIA5 + SN3->N3_VRDACM5 					Picture PesqPict("SN3","N3_VRDACM5",16,5) // Moeda 5
			Else
				@ li,  44   PSAY SN3->N3_VORIG1 + SN3->N3_AMPLIA1 - SN3->N3_VRDACM1 + SN3->N3_VRCACM1	Picture PesqPict("SN3","N3_VRDACM1",16,1) // Moeda 1
				@ li,  62   PSAY SN3->N3_VORIG2 + SN3->N3_AMPLIA2 - SN3->N3_VRDACM2 					Picture PesqPict("SN3","N3_VRDACM2",16,2) // Moeda 2
				@ li,  80   PSAY SN3->N3_VORIG3 + SN3->N3_AMPLIA3 - SN3->N3_VRDACM3 					Picture PesqPict("SN3","N3_VRDACM3",16,3) // Moeda 3
				@ li,  98   PSAY SN3->N3_VORIG4 + SN3->N3_AMPLIA4 - SN3->N3_VRDACM4 					Picture PesqPict("SN3","N3_VRDACM4",16,4) // Moeda 4
				@ li, 116   PSAY SN3->N3_VORIG5 + SN3->N3_AMPLIA5 - SN3->N3_VRDACM5 					Picture PesqPict("SN3","N3_VRDACM5",16,5) // Moeda 5
			Endif
			li := li + 2
		
			lImp := .T.
			dbSelectArea(cAliasQry)
			dbSkip()
			lPrtTot := .T.
		EndDo
   
	EndDo

	If li > 56
		cabec( titulo, cabec1, cabec2, nomeprog, tamanho, Iif(aReturn[4]==1,15,18))
	End
    
	If lPrtTot
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime os valores Totais da Quebra - ITENS ORIGEM                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ValType((cAliasQry)->(N4_DATA)) == "C"					/// COM QUERY
   			cQb2Prn := (cAliasQry)->(DTOC(STOD(cQuebra)))
		Else
   			cQb2Prn := (cAliasQry)->(DTOC(cQuebra))					/// CODEBASE
		Endif	
		@ li,  0   PSAY OemToAnsi(STR0018) + Iif( nOrdem==1,cQb2Prn, " " )  //"TOTAL MOVIMENTDADO NO DIA "
		li := li + 2
	
		@ li,   0   PSAY OemToAnsi(STR0019)  //"TOTAL (Itens Origem) Valor Original   --->"
		@ li,  44   PSAY aValorOrig[1] Picture PesqPict("SN3","N3_VORIG1",16,1)  // Moeda 1
		@ li,  62   PSAY aValorOrig[2] Picture PesqPict("SN3","N3_VORIG2",16,2)  // Moeda 2
		@ li,  80   PSAY aValorOrig[3] Picture PesqPict("SN3","N3_VORIG3",16,3)  // Moeda 3
		@ li,  98   PSAY aValorOrig[4] Picture PesqPict("SN3","N3_VORIG4",16,4)  // Moeda 4
		@ li, 116   PSAY aValorOrig[5] Picture PesqPict("SN3","N3_VORIG5",16,5)  // Moeda 5
		li++
		@ li,   0   PSAY OemToAnsi(STR0020)  //"TOTAL (Itens Origem) Depr. Acumulada  --->"
		@ li,  44   PSAY aDepreOrig[1] Picture PesqPict("SN3","N3_VRDACM1",16,1) // Moeda 1
		@ li,  62   PSAY aDepreOrig[2] Picture PesqPict("SN3","N3_VRDACM2",16,2) // Moeda 2
		@ li,  80   PSAY aDepreOrig[3] Picture PesqPict("SN3","N3_VRDACM3",16,3) // Moeda 3
		@ li,  98   PSAY aDepreOrig[4] Picture PesqPict("SN3","N3_VRDACM4",16,4) // Moeda 4
		@ li, 116   PSAY aDepreOrig[5] Picture PesqPict("SN3","N3_VRDACM5",16,5) // Moeda 5
		li++
		@ li,   0   PSAY OemToAnsi(STR0021)  //"TOTAL (Itens Origem) Correcao         --->"
		@ li,  44   PSAY nCorreOrig Picture PesqPict("SN3","N3_VRCACM1",16,1) // Moeda 1
		li++
		@ li,   0   PSAY OemToAnsi(STR0022)  //"TOTAL (Itens Origem) Residual         --->"
		@ li,  44   PSAY aValorOrig[1] - aDepreOrig[1] + nCorreOrig 	Picture PesqPict("SN3","N3_VORIG1",16,1) // Moeda 1
		@ li,  62   PSAY aValorOrig[2] - aDepreOrig[2] 					Picture PesqPict("SN3","N3_VORIG2",16,2) // Moeda 2
		@ li,  80   PSAY aValorOrig[3] - aDepreOrig[3] 					Picture PesqPict("SN3","N3_VORIG3",16,3) // Moeda 3
		@ li,  98   PSAY aValorOrig[4] - aDepreOrig[4] 					Picture PesqPict("SN3","N3_VORIG4",16,4) // Moeda 4
		@ li, 116   PSAY aValorOrig[5] - aDepreOrig[5] 					Picture PesqPict("SN3","N3_VORIG5",16,5) // Moeda 5
		li := li + 2
	
		If li > 56
			cabec( titulo, cabec1, cabec2, nomeprog, tamanho, Iif(aReturn[4]==1,15,18))
		End

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime os valores Totais da Quebra - ITENS DESTINO                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		@ li,   0   PSAY OemToAnSI(STR0023)  //"TOTAL (Itens Destino) Valor Original  --->"
		@ li,  44   PSAY aValorDest[1] Picture PesqPict("SN3","N3_VORIG1",16,1)  // Moeda 1
		@ li,  62   PSAY aValorDest[2] Picture PesqPict("SN3","N3_VORIG2",16,2)  // Moeda 2
		@ li,  80   PSAY aValorDest[3] Picture PesqPict("SN3","N3_VORIG3",16,3)  // Moeda 3
		@ li,  98   PSAY aValorDest[4] Picture PesqPict("SN3","N3_VORIG4",16,4)  // Moeda 4
		@ li, 116   PSAY aValorDest[5] Picture PesqPict("SN3","N3_VORIG5",16,5)  // Moeda 5
		li++
		@ li,   0   PSAY OemtoAnsi(STR0024)  //"TOTAL (Itens Destino) Depr. Acumulada --->"
		@ li,  44   PSAY aDepreDest[1] Picture PesqPict("SN3","N3_VRDACM1",16,1) // Moeda 1
		@ li,  62   PSAY aDepreDest[2] Picture PesqPict("SN3","N3_VRDACM2",16,2) // Moeda 2
		@ li,  80   PSAY aDepreDest[3] Picture PesqPict("SN3","N3_VRDACM3",16,3) // Moeda 3
		@ li,  98   PSAY aDepreDest[4] Picture PesqPict("SN3","N3_VRDACM4",16,4) // Moeda 4
		@ li, 116   PSAY aDepreDest[5] Picture PesqPict("SN3","N3_VRDACM5",16,5) // Moeda 5
		li++
		@ li,   0   PSAY OemToAnsi(STR0025)  //"TOTAL (Itens Destino) Correcao        --->"
		@ li,  44   PSAY nCorreDest  Picture PesqPict("SN3","N3_VRCACM1",16,1) 		// Moeda 1
		li ++
		@ li,   0   PSAY OemToAnsi(STR0026)  //"TOTAL (Itens Destino) Residual        --->"
		@ li,  44   PSAY aValorDest[1] - aDepreDest[1] + nCorreDest 	Picture PesqPict("SN3","N3_VORIG1",16,1) // Moeda 1
		@ li,  62   PSAY aValorDest[2] - aDepreDest[2] 					Picture PesqPict("SN3","N3_VORIG2",16,2) // Moeda 2
		@ li,  80   PSAY aValorDest[3] - aDepreDest[3] 					Picture PesqPict("SN3","N3_VORIG3",16,3) // Moeda 3
		@ li,  98   PSAY aValorDest[4] - aDepreDest[4] 					Picture PesqPict("SN3","N3_VORIG4",16,4) // Moeda 4
		@ li, 116   PSAY aValorDest[5] - aDepreDest[5] 					Picture PesqPict("SN3","N3_VORIG5",16,5) // Moeda 5
		li := li + 2
	Else
		dbSelectArea(cAliasQry)
		dbSkip()
	Endif
		
EndDo	
   	
If lImp
	Roda( cbcont, cbtxt )
EndIf

If Select(cAliasQry) > 0
	dbSelectArea(cAliasQry)
	dbCloseArea()
Endif
	
If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	Ourspool(wnrel)
Endif

MS_FLUSH()

Return()