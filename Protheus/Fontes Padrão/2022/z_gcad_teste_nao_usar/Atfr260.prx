#INCLUDE "ATFR260.CH"
#Include "Protheus.ch"


// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR260  ³ Autor ³Carlos A. Gomes Jr.    ³ Data ³ 05.06.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relat¢rio de Amplia‡”es                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ATFR260                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFR260
Local oReport

If TRepInUse()
	oReport := ReportDef()
	oReport:PrintDialog()
Else
    Return ATFR260R3() // Executa versão anterior do fonte
Endif

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ATFR210   ºAutor  ³Carlos A. Gomes Jr. º Data ³  06/05/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Definicao do objeto do relatorio personalizavel e das      º±±
±±º          ³ secoes que serao utilizadas                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportDef()

Local oReport,oSection1
Local cReport  := "ATFR260"
Local cTitulo  := OemToAnsi(STR0003) // "AMPLIACOES"
Local cDescri  := OemToAnsi(STR0001)+" "+OemToAnsi(STR0002) // "Este relatorio ir   imprimir as ampliacoes dos bens dentro" ## "dos parametros especificados."
Local oSecFil //Gestao Corporativa

Pergunte( "AFR260" , .F. )

cMoedaDesc:=GetMV("MV_MOEDA"+StrZero(MV_PAR07+1,1))

oReport  := TReport():New( cReport, cTitulo, "AFR260" , { |oReport| ATFR260Imp( oReport ) }, cDescri )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a 1a. secao do relatorio Valores nas Moedas   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

oSection1 := TRSection():New( oReport, STR0011 , {"SN4","SN3","SN1"} )		//"Bens Ampliados"
TRCell():New( oSection1, "N4_CBASE"   , "SN4"   ,/*X3Titulo*/      	,/*Picture*/,Len(SN4->N4_CBASE),/*lPixel*/,/*{|| code-block de impressao }*/)	// Codigo
TRCell():New( oSection1, "N4_ITEM"    , "SN4"   ,/*X3Titulo*/      	,/*Picture*/,Len(SN4->N4_ITEM),/*lPixel*/,/*{|| code-block de impressao }*/)	// Item
TRCell():New( oSection1, "N4_TIPO"    , "SN4"   ,/*X3Titulo*/      	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Tipo
TRCell():New( oSection1, "N1_DESCRIC" , "SN1"   ,/*X3Titulo*/      	,/*Picture*/,Len(SN1->N1_DESCRIC)-10/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Descricao
TRCell():New( oSection1, "N4_DATA"    , "SN4"   ,/*X3Titulo*/      	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Data
TRCell():New( oSection1, "N3_CCONTAB" , "SN4"   ,/*X3Titulo*/      	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Conta Contabil
TRCell():New( oSection1, "N4_VLROC1"  , "SN4"   ,/*X3Titulo*/      	,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	//Valor da Ampliacao 
TRCell():New( oSection1, "nValAtual"  ,/*Alias*/,STR0008					,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Valor Atualizado
TRCell():New( oSection1, "nVLROC"     ,/*Alias*/,STR0009+CRLF+cMoedaDesc ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*"RIGHT"*/,,"RIGHT",,,.F.)	// Valor Ampliacao outra moeda
TRCell():New( oSection1, "nVORIG"     ,/*Alias*/,STR0010+CRLF+cMoedaDesc ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*"RIGHT"*/,,"RIGHT",,,.F.)	// Valor Atualizado outra moeda
oSection1:SetHeaderPage()
oSection1:SetColSpace(1)

//Gestão Corporativa - Início
oSecFil := TRSection():New(oReport,"SECFIL",{})
TRCell():New(oSecFil,"CODFIL" ,,STR0013,/*Picture*/,20,/*lPixel*/,/*{|| code-block de impressao }*/)		//"Código"
TRCell():New(oSecFil,"EMPRESA",,STR0014,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)		//"Empresa"
TRCell():New(oSecFil,"UNIDNEG",,STR0015,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)		//"Unidade de negócio"
TRCell():New(oSecFil,"NOMEFIL",,STR0016,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)		//"Filial"

oReport:SetUseGC(.F.)
//Gestão Corporativa - Fim

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ATFR260ImpºAutor  ³Carlos A. Gomes Jr. º Data ³  06/05/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Query de impressao do relatorio                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATFR260Imp( oReport )

Local oSection1 := oReport:Section(1)
Local cQuery := ""
Local cChave := ""

Local cAliasSN3 := "SN3"
Local cAliasSN4 := "SN4"


Local nMoeda     := mv_par07 + 1
Local cMoeda     := StrZero(nMoeda,1)
Local cMoedaDesc := SuperGetMV("MV_MOEDA"+cMoeda)

//Gestao Corporativa - Inicio
Local oBreak
Local oSecFil := oReport:Section(2)
Local lGestao := ( FWSizeFilial() > 2 )
Local lSN4Excl := Iif( lGestao, FWModeAccess("SN4",1) == "E", FWModeAccess("SN4",3) == "E")
Local nRegSM0 := SM0->(Recno())
Local aSM0 := {}
Local aSelFil := {}
Local cFilSel := ""
Local nLinha := 0
Local nX := 0
Local nTamEmp := Len(FWSM0LayOut(,1))
Local nTamUnNeg := Len(FWSM0LayOut(,2))
Local nTamTit := 0
Local cFilSN4 := ""
Local cRngFilSN4 := ""
Local aTmpFil := {}
Local cTmpSN4Fil := ""

If (lSN4Excl .and. mv_par08 == 1)
	If FindFunction("AdmSelecFil")
		AdmSelecFil("ATR260",8,.F.,@aSelFil,"SN4",.F.)
	Else
		aSelFil := AdmGetFil(.F.,.F.,"SN4")
	Endif
Endif

If mv_par08 == 1 .And. !Empty(aSelFil)
	aSort(aSelFil)
	oSection1:SetHeaderSection(.F.)
	SM0->(DbGoTo(nRegSM0))
	aSM0 := FWLoadSM0()
	cTitulo := oReport:Title()
	oReport:SetTitle(cTitulo + " (" + STR0017 +  ")")	//"Filiais selecionadas para o relatorio"
	nTamTit := Len(oReport:Title())
	oSecFil:Init()
	oSecFil:Cell("CODFIL"):SetBlock({||cFilSel})
	oSecFil:Cell("EMPRESA"):SetBlock({||aSM0[nLinha,SM0_DESCEMP]})
	oSecFil:Cell("UNIDNEG"):SetBlock({||aSM0[nLinha,SM0_DESCUN]})
	oSecFil:Cell("NOMEFIL"):SetBlock({||aSM0[nLinha,SM0_NOMRED]})
	For nX := 1 To Len(aSelFil)
		nLinha := Ascan(aSM0,{|sm0|,sm0[SM0_CODFIL] == aSelFil[nX] .And. sm0[SM0_GRPEMP] == cEmpAnt})
		If nLinha > 0
			cFilSel := Substr(aSM0[nLinha,SM0_CODFIL],1,nTamEmp)
			cFilSel += " "
			cFilSel += Substr(aSM0[nLinha,SM0_CODFIL],nTamEmp + 1,nTamUnNeg)
			cFilSel += " "
			cFilSel += Substr(aSM0[nLinha,SM0_CODFIL],nTamEmp + nTamUnNeg + 1)
			oSecFil:PrintLine()
		Endif
	Next
	oReport:SetTitle(cTitulo)
	oSecFil:Finish()
	oReport:EndPage()
	oSection1:SetHeaderSection(.T.)
Endif

If Empty(aSelFil)
	aSelFil := {cFilAnt}
	cFilSN4 := " N4_FILIAL = '"+ xFilial("SN4") + "' AND "
Else
	cRngFilSN4 := GetRngFil(aSelFil,"SN4",.T.,@cTmpSN4Fil)
	aAdd(aTmpFil,cTmpSN4Fil)
	cFilSN4 := " N4_FILIAL "+ cRngFilSN4 + " AND "
Endif

cFilSN4 := "%"+cFilSN4+"%"
//Gestao Corporativa - Fim

cChave    := "%"+SqlOrder("N4_FILIAL+N4_CBASE+N4_ITEM+N4_TIPO+N4_DATA")+"%"
cQuery    := GetNextAlias()
cAliasSN3 := cQuery
cAliasSN4 := cQuery

oSection1:BeginQuery()
BeginSql Alias cQuery
	SELECT
		N4_FILIAL, N4_CBASE, N4_ITEM, N4_TIPO, N4_DATA, 
		N4_VLROC1, N4_VLROC2, N4_VLROC3, N4_VLROC4, N4_VLROC5, 
		N3_CCONTAB, N3_VRCACM1, N3_AMPLIA1, 
		N3_VORIG1, N3_VORIG2, N3_VORIG3, N3_VORIG4, N3_VORIG5,  
		N1_DESCRIC
	FROM 
		%table:SN4% SN4, %table:SN3% SN3, %table:SN1% SN1
	WHERE %exp:cFilSN4%
		SN4.N4_CBASE  >= %Exp:mv_par01% AND
		SN4.N4_CBASE  <= %Exp:mv_par02% AND
		SN4.N4_DATA   >= %Exp:mv_par03% AND 
		SN4.N4_DATA   <= %Exp:mv_par04% AND
		SN4.N4_CONTA  >= %Exp:mv_par05% AND 
		SN4.N4_CONTA  <= %Exp:mv_par06% AND
		SN4.N4_OCORR  = '09' AND
		SN4.%notDel% AND
		SN3.N3_FILIAL = SN4.N4_FILIAL AND
		SN3.N3_CBASE  = SN4.N4_CBASE AND
		SN3.N3_ITEM   = SN4.N4_ITEM AND 
		SN3.N3_TIPO   = SN4.N4_TIPO AND
		SN3.%notDel% AND
		SN1.N1_FILIAL = SN4.N4_FILIAL AND
		SN1.N1_CBASE = SN4.N4_CBASE AND
		SN1.N1_ITEM  = SN4.N4_ITEM AND
		SN1.%notDel%
	ORDER BY %Exp:cChave%
EndSql
oSection1:EndQuery()

If mv_par08 == 1
	oReport:PrintText(STR0012 + (cAliasSN4)->N4_FILIAL) //"Filial : "
	oReport:SkipLine()
	oBreak := TRBreak():New(oSection1,{|| (cAliasSN4)->N4_FILIAL})
	oBreak:OnBreak({|| oReport:SkipLine(2)})
	oBreak:OnPrintTotal({|| IIf(!(cAliasSN4)->(EOF()),oReport:PrintText(STR0012 + (cAliasSN4)->N4_FILIAL),"")}) //"Filial : "
EndIf

oSection1:Init()

If !(cAliasSN4)->(EOF())
	oSection1:Cell("N3_CCONTAB"):SetBlock({|| Mascara((cAliasSN3)->N3_CCONTAB) })
	oSection1:Cell("nValAtual"):SetBlock({|| (cAliasSN3)->( N3_VORIG1 + N3_VRCACM1 + N3_AMPLIA1 ) })
	oSection1:Cell("N4_VLROC1"):SetPicture( PesqPict("SN4","N4_VLROC1",18,1) )
	oSection1:Cell("nValAtual"):SetPicture( PesqPict("SN3","N3_VORIG1",18,1) )

	oSection1:Cell("nVLROC"):SetBlock({|| &( cAliasSN4 + "->N4_VLROC" + cMoeda ) })
	oSection1:Cell("nVLROC"):SetTitle( OemToAnsi(STR0009)+CRLF+cMoedaDesc )
	oSection1:Cell("nVLROC"):SetPicture( PesqPict("SN4","N4_VLROC"+cMoeda,18,nMoeda) )

	oSection1:Cell("nVORIG"):SetBlock({|| &( cAliasSN3 + "->N3_VORIG" + cMoeda ) })
	oSection1:Cell("nVORIG"):SetTitle( OemToAnsi(STR0010)+CRLF+cMoedaDesc )
	oSection1:Cell("nVORIG"):SetPicture( PesqPict("SN3","N3_VORIG"+cMoeda,18,nMoeda) )

	oSection1:Print()
EndIf

oSection1:Finish()

//Gestão Corporativa - Início
For nX := 1 TO Len(aTmpFil)
	CtbTmpErase(aTmpFil[nX])
Next
//Gestão Corporativa - Fim

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR260R4  ³ Autor ³ Alice Y Yamamoto     ³ Data ³ 25.04.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relat¢rio de Amplia‡”es                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ATFR260                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function ATFR260R3()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Vari veis         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cString		:= "SN4"
Local cDesc1		:= OemToAnsi(STR0001) // "Este relatorio ir   imprimir as ampliacoes dos bens dentro"
Local cDesc2		:= OemToAnsi(STR0002) // "dos parametros especificados."
Local cDesc3		:= ""
Local wnrel			:= ""

Private aReturn		:= { "Zebrado", 1,"Administracao", 2, 2, 1, "",1 }
Private nLastKey	:= 0
Private nomeprog	:= "ATFR260"
Private cPerg		:= "AFR260"
Private tamanho		:= "M"
Private titulo		:= OemToAnsi(STR0003) // "AMPLIACOES"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte( "AFR260",.F. )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parƒmetros           ³
//³ mv_par01         // C¢digo Base de             ³
//³ mv_par02         // C¢digo Base Ate            ³
//³ mv_par03         // Data de                    ³
//³ mv_par04         // Data Ate                   ³
//³ mv_par05         // Conta de                   ³
//³ mv_par06         // Conta Ate                  ³
//³ mv_par07         // E na Moeda                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel := "ATFR260"
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,,,Tamanho)

If nLastKey == 27
	Return
Endif

SetDefault( aReturn,cString )

If  nLastKey == 27
    Return
End

RptStatus({|lEnd| FR260Imp(@lEnd,wnRel,cString)},Titulo)

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ FR260IMP   ³ Autor ³ Alice Y Yamamoto    ³ Data ³ 25.04.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Ampliacoes                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ FR260IMP(lEnd,WnRel,Titulo)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd    - A‡„o do Codeblock                                ³±±
±±³          ³ wnRel   - T¡tulo do relat¢rio                              ³±±
±±³          ³ cString - Mensagem                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³C. Denardi  ³06/10/03³66.785³Localizacao de casas decimais na Picture  ³±±
±±³            ³        ³      ³em todos os relatorios do Ativo Fixo      ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function FR260Imp(lEnd,WnRel,cString)
Local NomeProg	:= "ATFR260"
Local cChave	:= ""
Local nValAtual	:= 0
Local nMoeda	:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impress„o do Cabe‡alho e Rodap‚    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbtxt     := SPACE( 10 )
cbcont    := 0
cMoedaDesc:= GetMV("MV_MOEDA"+StrZero(MV_PAR07+1,1))
nMoeda	  := mv_par07+1
cMoeda    := StrZero(nMoeda,1)
cabec1    :=OemToAnsi(STR0004) // "Cod. Base  Item Tp Descricao Sintetica                      Data Ampli Conta Contabil Bem   Valor da Amplia‡ao   Valor Atualizado"
*                                  xxxxxxxxxx xxxx xx XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX 99/99/9999 XXXXXXXXXXXXXXXXXXXX 999,999,999,999,99 999,999,999,999.99
*                                  01234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^67890
*                                  0         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5
*                                  0                                                                                                   1                         
cabec2 := ""

cString:= "SN4"
li     := 80
m_pag  := 1

titulo += OemToAnsi(STR0005) +Dtoc(mv_par03) + " - " + DtoC(mv_par04) // " entre "
dbSelectArea("SN4")
dbSetOrder(1)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta express„o do Filtro para sele‡„o							  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cIndex := CriaTrab(nil,.f.)
cChave  := "N4_FILIAL+N4_CBASE+N4_ITEM+N4_TIPO+Dtos(N4_DATA)"
IndRegua("SN4",cIndex,cChave,,Af260IndR(),OemToAnsi(STR0006)) // "Selecionando Registros..."
nIndex := RetIndex("SN4")
dbSelectArea("SN4")
dbSetOrder(nIndex+1)
DbGoTop()
If BOF() .and. EOF()
	 Help(" ",1,"RECNO")
	 RetIndex("SN4")
	 dbSetOrder(1)
	 Set Filter to
	 FErase (cIndex+OrdBagExt())
	 Return
EndIf

SetRegua(SN4->(RecCount()))
While !Eof()
    
	IF lEnd
		@PROW()+1,001 PSAY OemToAnsi(STR0007) // "CANCELADO PELO OPERADOR"
		Exit
	End
    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimenta a regua       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IncRegua()
    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Localiza item no SN3           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SN3")
	dbSetOrder(1)
	If !(dbSeek(xFilial("SN3")+SN4->N4_CBASE+SN4->N4_ITEM+SN4->N4_TIPO))
		dbSelectArea("SN4")
		dbSkip()
		Loop
	Endif
	nValAtual := SN3->N3_VORIG1 + SN3->N3_VRCACM1 + SN3->N3_AMPLIA1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Localiza item no SN1          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SN1")
	dbSetOrder(1)
	If !(dbSeek(xFilial("SN1")+SN4->N4_CBASE+SN4->N4_ITEM))
		dbSelectArea("SN4")
		dbSkip()
		Loop
	Endif
    
	IF li > 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,IIF(aReturn[4]==1,15,18))
	Endif
	@ li, 00  PSAY SN4->N4_CBASE
	@ li, 11  PSAY SN4->N4_ITEM
	@ li, 16  PSAY SN4->N4_TIPO
	@ li, 19  PSAY Substr(SN1->N1_DESCRIC,1,24)
	@ li, 44  PSAY SN4->N4_DATA
	@ li, 55  PSAY Mascara(SN3->N3_CCONTAB)
	@ li, 76  PSAY SN4->N4_VLROC1 	Picture PesqPict("SN4","N4_VLROC1",18,1)
	@ li, 95  PSAY nValAtual      	Picture PesqPict("SN3","N3_VORIG1",18,1)
	@ li, 114 PSAY SN3->N3_VORIG1   Picture PesqPict("SN3","N3_VORIG1",18,nMoeda)
	li++
	@ li, 62  PSAY cMoedaDesc
	@ li, 76  PSAY &('SN4->N4_VLROC'+cMoeda) Picture PesqPict("SN4","N4_VLROC"+cMoeda,18,nMoeda)
	nValAtual := &('SN3->N3_VORIG'+cMoeda)
	nValAtual += &('SN3->N3_VRCACM'+cMoeda)
	nValAtual += &('SN3->N3_AMPLIA'+cMoeda)
	@ li, 95  PSAY nValAtual      	Picture PesqPict("SN3","N3_VORIG1",18,1)
	@ li, 114 PSAY &('SN3->N3_VORIG'+cMoeda) Picture PesqPict("SN3","N3_VORIG"+cMoeda,18,nMoeda)
	li++
	dbSelectArea("SN4")
	dbSkip()
Enddo

roda(cbcont,cbtxt,Tamanho)
DbClearFilter( ) 

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	Ourspool(WnRel)
End

RetIndex("SN4")
dbSetOrder(1)
Set Filter to
Ferase (cIndex+OrdBagExt())
Return
MS_FLUSH()


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ AF060IndR  ³ Autor ³ Alice Yamamoto       ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Criar Indice Condicional da indRegua                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ IndRegua                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Af260IndR()

	Private cFiltro := ""

	cFiltro:=      'N4_FILIAL=="'+xFilial("SN4")
	cFiltro+='".AND. N4_CBASE>="'+mv_par01+'" .AND. N4_CBASE<="'+mv_par02
	cFiltro+='".AND. Dtos(N4_DATA)>="'+Dtos(mv_par03)+'" .AND. Dtos(N4_DATA)<="'+Dtos(mv_par04)
	cFiltro+='".AND. N4_CONTA>="'+mv_par05+'" .AND. N4_CONTA<="'+mv_par06
	cFiltro+='".AND. N4_OCORR=="09"'

Return cFiltro
