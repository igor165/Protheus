#INCLUDE "ATFR050.CH"
#INCLUDE "Protheus.ch"

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.
// TRADUCAO DE CH'S PARA PORTUGAL
// TRADUCAO DE CH'S PARA PORTUGAL - 21/07/08      



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR050    ³ Autor ³ Vinicius Barreira     ³ Data ³ 01.12.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Saldos a depreciar                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ATFR050                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFR050()
Local oReport

If FindFunction("TRepInUse") .And. TRepInUse()
	oReport:=ReportDef()
	oReport:PrintDialog()
Else
   Return ATFR050R3() // Executa versão anterior do relatorio
Endif
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³Claudio D. de Souza    ³ Data ³28/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatório                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()
Local oReport,oSection1,oSection11
Local cReport := "ATFR050"
Local cAlias1 := "SN3"
Local cTitulo := STR0003 // "Saldo a depreciar"
Local cDescri := STR0001 + " " + STR0002 // "Este programa ir  emitir a rela‡Æo dos valores que ainda faltam depreciar para cada bem"
Local bReport := { |oReport|	oReport:SetTitle( oReport:Title() + OemToAnsi(STR0007)+; // " por " 
										aOrd[oSection1:GetOrder()] ),; 
									 	ReportPrint( oReport ) }
Local aOrd := {}
Local cMoeda 

DbSelectArea("SN1") // Forca a abertura do SN1

aOrd  := {	OemToAnsi(STR0011),; // "Conta"
				OemToAnsi(STR0012)} // "C Custo"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualizacao das perguntas utilizadas pelo relatorio  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte( "ATR050" , .F. )
oReport  := TReport():New( cReport, cTitulo, "ATR050" , bReport, cDescri )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a 1a. secao do relatorio Valores nas Moedas   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New( oReport, STR0017, {cAlias1,"SN1"}, aOrd )	//"Dados da Entidade"
TRCell():New( oSection1, "N3_CCONTAB"	, cAlias1,/*X3Titulo*/,/*Picture*/,Len(SN3->N3_CCONTAB + " - " + CT1->CT1_DESC01)/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection1, "N3_CCUSTO" 	, cAlias1,/*X3Titulo*/,/*Picture*/,Len(SN3->N3_CCUSTO + " - " + CTT->CTT_DESC01)/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSection11 := TRSection():New( oSection1, STR0018, {cAlias1,"SN1"} )	//"Dados do Bem"
TRCell():New( oSection11, "N3_CBASE"  	, cAlias1,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N3_ITEM"   	, cAlias1,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N3_TIPO"   	, cAlias1,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N1_DESCRIC"	, "SN1"  ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N3_SALDO1"	, "SN3"  ,AllTrim(GetMV("MV_SIMB1")),PesqPict("SN3","N3_VORIG1",18,1)/*Picture*/,18/*Tamanho*/,/*lPixel*/,{|| (N3_VORIG1 + N3_VRCACM1 + N3_AMPLIA1) - (N3_VRDACM1 + N3_VRCDA1) },/*"RIGHT"*/,,"RIGHT",,,.F.  )
TRCell():New( oSection11, "N3_SALDO2"	, "SN3"  ,AllTrim(GetMV("MV_SIMB2")),PesqPict("SN3","N3_VORIG1",18,2)/*Picture*/,18/*Tamanho*/,/*lPixel*/,{|| (N3_VORIG2 + N3_AMPLIA2) - N3_VRDACM2 },/*"RIGHT"*/,,"RIGHT" )
TRCell():New( oSection11, "N3_SALDO3"	, "SN3"  ,AllTrim(GetMV("MV_SIMB3")),PesqPict("SN3","N3_VORIG1",18,3)/*Picture*/,18/*Tamanho*/,/*lPixel*/,{|| (N3_VORIG3 + N3_AMPLIA3) - N3_VRDACM3 },/*"RIGHT"*/,,"RIGHT" )
TRCell():New( oSection11, "N3_SALDO4"	, "SN3"  ,AllTrim(GetMV("MV_SIMB4")),PesqPict("SN3","N3_VORIG1",18,4)/*Picture*/,18/*Tamanho*/,/*lPixel*/,{|| (N3_VORIG4 + N3_AMPLIA4) - N3_VRDACM4 },/*"RIGHT"*/,,"RIGHT" )
TRCell():New( oSection11, "N3_SALDO5"	, "SN3"  ,AllTrim(GetMV("MV_SIMB5")),PesqPict("SN3","N3_VORIG1",18,5)/*Picture*/,18/*Tamanho*/,/*lPixel*/,{|| (N3_VORIG5 + N3_AMPLIA5) - N3_VRDACM5 },/*"RIGHT"*/,,"RIGHT" )
    
oSection11:Cell("N3_SALDO1"):lHeaderSize	:= .F.
oSection11:Cell("N3_SALDO2"):lHeaderSize	:= .F.
oSection11:Cell("N3_SALDO3"):lHeaderSize	:= .F.
oSection11:Cell("N3_SALDO4"):lHeaderSize	:= .F.
oSection11:Cell("N3_SALDO5"):lHeaderSize	:= .F.

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ReportPrintºAutor  ³Claudio D. de Souza º Data ³  23/06/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Query de impressao do relatorio                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint( oReport )
Local oSection1  := oReport:Section(1)
Local oSection11 := oReport:Section(1):Section(1)
Local cChave
Local cQuery		:= "SN3"
Local cAliasCT1	:= "CT1"
Local cAliasCTT	:= "CTT"
Local nOrder   := oSection1:GetOrder()
Local cWhere	:= ""
Local cQuebra	:= .T.
//*****************************
// Tipos de Ativos gerenciais *
//*****************************
Local cTpGerenc	:= Iif(FindFunction("ATFXTpBem"),ATFXTpBem(2) ,Alltrim(SuperGetMv("MV_ATFTGER",.F.,"")))
Local aTiposF	:= Iif(FindFunction("ATFXTpBem"),ATFXTpBem(1,.T.) ,Alltrim(SuperGetMv("MV_ATFTGER",.F.,"")))
Local aTiposG	:= Iif(FindFunction("ATFXTpBem"),ATFXTpBem(2,.T.) ,Alltrim(SuperGetMv("MV_ATFTGER",.F.,"")))
lOCAL cTipos	:= '' 
Local nX        := 0
Local cWhereAux	:= ""
Local i			:= 0

Local aClassif := {}
Local cClassif := ""
Local cWhereSN1:= ""

Local lRealProv	:= .F.

If mv_par09 == 1 .And. FindFunction("AdmGetClass")
	aClassif := AdmGetClass()
	If Len( aClassif ) <= 0
		Return
	EndIf 	
EndIf
If MV_PAR11 = 1
	cTipos := "'"
	For nx := 1 to Len(aTiposG)
		cTipos += aTiposG[nX]
		
		If nx != Len(aTiposG)
			cTipos += "','"
		EndIf
	Next nX
	cTipos += "'"
ElseIf MV_PAR11 = 2
	cTipos := "'"
	For nx := 1 to Len(aTiposF)
		cTipos += aTiposF[nX]
		
		If nx != Len(aTiposF)
			cTipos += "','"
		EndIf
	Next nX
	cTipos += "'"
EndIf


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Localiza registro inicial                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF nOrder == 1
	SN3->(dbSetOrder(2))
	cWhere := "N3_CCONTAB <> ' ' AND "
	oSection1:Cell("N3_CCUSTO"):Disable()
	oSection11:SetTotalText({ || STR0019 + cQuebra } )		//"Total da Conta: "
	oSection1:Cell("N3_CCONTAB"):SetBlock( { || Mascara(N3_CCONTAB) + " - " + (cAliasCT1)->CT1_DESC01 } )
ElseIF nOrder == 2
	SN3->(dbSetOrder(3))
	cWhere := "N3_CCUSTO <> ' ' AND "
	oSection1:Cell("N3_CCONTAB"):Disable()
	oSection1:Cell("N3_CCUSTO"):SetBlock( { || N3_CCUSTO + " - " + (cAliasCTT)->CTT_DESC01 } )
	oSection11:SetTotalText( {|| STR0020 + cQuebra } )		//"Total do Centro de Custo: "
End
cChave := SN3->(IndexKey())


/*
 * Verificação do campo para ativos de custo de provisão
 */
lRealProv := SN3->(FieldPos("N3_ATFCPR")) > 0

cQuery 		:= Criatrab( , .F. )
cAliasCT1	:= cQuery
cAliasCTT	:= cQuery

cChave 	:= "%"+SqlOrder(cChave)+"%"

cWhere 	+=	"SN3.N3_BAIXA = '0' AND "
cWhere 	+=	"SN3.N3_TXDEPR1 <> 0 AND "
IF MV_PAR08 == 2 // Somente bens com saldo a depreciar
	For i := 3 To 7
		If	&("mv_par0" + ALLTRIM(STR(i))) == 1	//Se a moeda foi escolhida pelo usuario
			If !Empty( cWhereAux )
				cWhereAux += " OR "
			EndIf
			cWhereAux += "( SN3.N3_VRDACM"+ALLTRIM(STR(i-2))+" < (SN3.N3_VORIG"+ALLTRIM(STR(i-2))+" + SN3.N3_AMPLIA"+ALLTRIM(STR(i-2))+") )"
		EndIf
	Next i

	If !Empty( cWhereAux )
		cWhere += "( " + cWhereAux + " ) AND"
	EndIf
ENDIF
	
//Verifica se filtra as classificações patrimoniais
If Len(aClassif) > 0  .And.  FindFunction("FormatClas")
	cWhereAux := ""
	cWhereAux += " SN1.N1_PATRIM IN " + FORMATCLAS(aClassif,.T.) 
	If !Empty( cWhereAux )
		cWhere += "( " + cWhereAux + " ) AND"
	EndIf
EndIf

If lRealProv .AND. MV_PAR10 == 2
	cWhere += " (SN3.N3_ATFCPR = '2' OR SN3.N3_ATFCPR = ' ') AND "
EndIf	

cWhere 	+=	"(SN3.N3_CDEPREC <> ' ' OR "
cWhere 	+=	" SN3.N3_CDESP <> ' ' OR "
cWhere 	+=  " SN3.N3_CCDEPR <> ' ' ) AND "
If MV_PAR11 != 3
	cWhere 	+=  " SN3.N3_TIPO NOT IN (" + cTipos + ") AND
EndIf

cWhere	:= "%" + cWhere + "%"

oSection1:BeginQuery()

BeginSql Alias cQuery
	SELECT
		N3_TIPO ,N3_CBASE, N3_ITEM, N3_CCUSTO, N3_CCONTAB, N3_VORIG1, N3_AMPLIA1, N3_VRCACM1, N3_VRDACM1, N3_VRCDA1, 
		N3_VORIG2, N3_AMPLIA2, N3_VRDACM2, N3_VORIG3, N3_AMPLIA3, N3_VRDACM3, N3_VORIG4, N3_AMPLIA4, N3_VRDACM4,
		N3_VORIG5, N3_AMPLIA5, N3_VRDACM5, N3_CDEPREC, N3_CCDEPR, N1_DESCRIC, CTT_DESC01, CT1_DESC01, N1_PATRIM
	FROM %table:SN3% SN3
		JOIN %table:SN1% SN1 ON 
		SN1.N1_FILIAL =  %xfilial:SN1%  
		AND SN1.N1_CBASE = SN3.N3_CBASE 
		AND SN1.N1_ITEM = SN3.N3_ITEM 
		AND SN1.%notDel%
		LEFT JOIN %table:CT1% CT1 ON
		CT1.CT1_FILIAL =  %xfilial:CT1%
		AND CT1.CT1_CONTA = SN3.N3_CCONTAB 
		AND CT1.%notDel%
		LEFT JOIN %table:CTT% CTT ON
		CTT.CTT_FILIAL =  %xfilial:CTT%
		AND CTT.CTT_CUSTO = SN3.N3_CCUSTO 
		AND CTT.%notDel%
	WHERE
		SN3.N3_FILIAL = %xfilial:SN3% AND
		SN3.N3_CBASE >= %Exp:mv_par01% AND 
		SN3.N3_CBASE <= %Exp:mv_par02% AND 
		%Exp:cWhere%
		SN3.%notDel%
	ORDER BY %Exp:cChave%
EndSql

oSection1:EndQuery()
oSection11:SetParentQuery()

// Cria variável a ser usada para impressao do texto da quebra da secao

//****************************************************************************
// Tipos de Ativos gerenciais 
// Deixar a linha comentada abaixo- Ideal é criar pergunta para selecionar 
// os tipos de ativo a serem considerados
//****************************************************************************
//oSection11:SetLineCondition( { || If(nOrder==1, cQuebra := (Mascara(N3_CCONTAB) + " - " + (cAliasCT1)->CT1_DESC01), cQuebra := (N3_CCUSTO + " - " + (cAliasCTT)->CTT_DESC01)),!(N3_TIPO $ cTpGerenc) } )
oSection11:SetLineCondition( { || If(nOrder==1, cQuebra := (Mascara(N3_CCONTAB) + " - " + (cAliasCT1)->CT1_DESC01), cQuebra := (N3_CCUSTO + " - " + (cAliasCTT)->CTT_DESC01)) } )
oSection11:SetTotalInLine(.F.)
oReport:SetTotalInLine(.F.)
oReport:SetTotalText(STR0010)  //"T O T A I S"

If nOrder == 1
	oSection11:SetParentFilter({|cParam| (cQuery)->N3_CCONTAB == cParam },{|| (cQuery)->N3_CCONTAB })
Else
	oSection11:SetParentFilter({|cParam| (cQuery)->N3_CCUSTO == cParam },{|| (cQuery)->N3_CCUSTO })
Endif	

// Desabilita as celulas conforme parametros
If	mv_par03 == 2
	oSection11:Cell("N3_SALDO1"):Disable()
Else
	TRFunction():New(oSection11:Cell("N3_SALDO1"),,"SUM",,,,, .T., .T. )	
Endif
If	mv_par04 == 2
	oSection11:Cell("N3_SALDO2"):Disable()
Else
	TRFunction():New(oSection11:Cell("N3_SALDO2"),,"SUM",,,,, .T., .T. )
Endif
If	mv_par05 == 2
	oSection11:Cell("N3_SALDO3"):Disable()
Else	
	TRFunction():New(oSection11:Cell("N3_SALDO3"),,"SUM",,,,, .T., .T. )
Endif
If	mv_par06 == 2
	oSection11:Cell("N3_SALDO4"):Disable()
Else
	TRFunction():New(oSection11:Cell("N3_SALDO4"),,"SUM",,,,, .T., .T. )
Endif
If mv_par07 == 2
	oSection11:Cell("N3_SALDO5"):Disable()
Else	
	TRFunction():New(oSection11:Cell("N3_SALDO5"),,"SUM",,,,, .T., .T. )
Endif

oSection1:Print()

Return Nil

//--------------------------------------------------RELEASE 3-----------------------------------------------------//

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR050R3  ³ Autor ³ Vinicius Barreira     ³ Data ³ 01.12.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Saldos a depreciar                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ATFR050R3                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION ATFR050R3

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

LOCAL cString 	 := "SN3"
LOCAL cDesc1    := OemToAnsi(STR0001) // "Este programa ir  emitir a rela‡Æo dos valores que"
LOCAL cDesc2    := OemToAnsi(STR0002) //"ainda faltam depreciar para cada bem"
LOCAL cDesc3    := ""
LOCAL wnrel 

        


PRIVATE aReturn	:= { OemToAnsi(STR0013), 1,OemToAnsi(STR0014), 2, 2, 1, "",1 }  //"Zebrado"###"Administracao"
PRIVATE aLinha   := { }
PRIVATE cPerg    := "ATR050"
PRIVATE nomeprog := "ATFR050"
PRIVATE nLastKey := 0
PRIVATE tamanho  := "M"

PRIVATE titulo	:= OemToAnsi(STR0003) // "Saldo a depreciar"
PRIVATE cabec1 := OemToAnsi(STR0004) // "Saldos a Depreciar por Bens"
PRIVATE cabec2 := OemToAnsi(STR0005) // "Codigo  Descricao"
PRIVATE aOrd   := {OemtoAnsi(STR0011),OemtoAnsi(STR0012)}   //   "Conta","C Custo" 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros           ³
//³ mv_par01            Do Bem                     ³
//³ mv_par02            Ate o Bem                  ³
//³ mv_par03            Moeda 1                    ³
//³ mv_par04            Moeda 2                    ³
//³ mv_par05            Moeda 3                    ³
//³ mv_par06            Moeda 4                    ³
//³ mv_par07            Moeda 5                    ³
//³ mv_par09            Selec Classif Patrimonial? ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualizacao das perguntas utilizadas pelo relatorio  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("ATR050",.F.)

wnrel := "ATFR050"
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,Tamanho)

If	nLastKey == 27
	Return
End

SetDefault( aReturn,cString )
nOrdem := aReturn[8]

If nLastKey == 27
   Return                                       
Endif

RptStatus({|lEnd| FR050Imp(@lEnd,wnRel,cString)},Titulo)

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FR050Imp  ³ Autor ³ Vinicius Barreira     ³ Data ³ 19/07/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime as Etiquetas dentro do padrao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³FR050IMP(cAlias,nReg,nOpc)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³C. Denardi  ³06/10/03³66.785³Localizacao de casas decimais na Picture  ³±±
±±³            ³        ³      ³em todos os relatorios do Ativo Fixo      ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function FR050Imp(lEnd,wnRel,cString)

LOCAL CbTxt
LOCAL cbCont

LOCAL cQuery
LOCAL cCond
LOCAL nTotalG1:= nTotalG2:= nTotalG3:= nTotalG4:= nTotalG5:= 0 //Totais Gerais
LOCAL nTotal1 := nTotal2 := nTotal3 := nTotal4 := nTotal5 := 0 //Total da Conta
LOCAL nSaldo1 := nSaldo2 := nSaldo3 := nSaldo4 := nSaldo5 := 0
LOCAL cMoeda1 := cMoeda2 := cMoeda3 := cMoeda4 := cMoeda5 := 0
Local cFilterUser := aReturn[7]  

//*****************************
// Tipos de Ativos gerenciais *
//*****************************  

Local cTpGerenc	:= Iif(FindFunction("ATFXTpBem"),ATFXTpBem(2) ,Alltrim(SuperGetMv("MV_ATFTGER",.F.,"")))
Local aTiposF	:= Iif(FindFunction("ATFXTpBem"),ATFXTpBem(1,.T.) ,Alltrim(SuperGetMv("MV_ATFTGER",.F.,"")))
Local aTiposG	:= Iif(FindFunction("ATFXTpBem"),ATFXTpBem(2,.T.) ,Alltrim(SuperGetMv("MV_ATFTGER",.F.,"")))
lOCAL cTipos	:= ''

Local cWhereAux	:= ""
Local i			:= 0 
Local aClassif  := {}
Local cClassif  := ""
Local lRealProv	:= .F.
Local cFilADVPL := ""
Local nX        := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbTxt   := SPACE(10)
cbCont  := 0
li      := 80
m_pag   := 1

cMoeda1 := AllTrim(GetMV("MV_SIMB1"))
cMoeda2 := AllTrim(GetMV("MV_SIMB2"))
cMoeda3 := AllTrim(GetMV("MV_SIMB3"))
cMoeda4 := AllTrim(GetMV("MV_SIMB4"))
cMoeda5 := AllTrim(GetMV("MV_SIMB5"))

cMoeda1 := Space(19 - Len(cMoeda1) ) + cMoeda1
cMoeda2 := Space(18 - Len(cMoeda2) ) + cMoeda2
cMoeda3 := Space(18 - Len(cMoeda3) ) + cMoeda3
cMoeda4 := Space(18 - Len(cMoeda4) ) + cMoeda4
cMoeda5 := Space(18 - Len(cMoeda5) ) + cMoeda5

cabec1  := OemToAnsi(STR0006) +Space(8) // "Codigo     Item Descricao"
cabec1  +=  IIf(mv_par03 == 1,cMoeda1,"" )
cabec1  +=  IIf(mv_par04 == 1,cMoeda2,"" )
cabec1  +=  IIf(mv_par05 == 1,cMoeda3,"" )
cabec1  +=  IIf(mv_par06 == 1,cMoeda4,"" )
cabec1  +=  IIf(mv_par07 == 1,cMoeda5,"" )
cabec2  := ""

titulo  += OemToAnsi(STR0007) + aOrd[nOrdem] // " por "
lPrim   := .T.


If mv_par09 == 1 .And. FindFunction("AdmGetClass")
	aClassif := AdmGetClass()	
EndIf
If MV_PAR11 = 1
	cTipos := "'"
	For nx := 1 to Len(aTiposG)
		cTipos += aTiposG[nX]
		
		If nx != Len(aTiposG)
			cTipos += "','"
		EndIf
	Next nX
	cTipos += "'"
ElseIf MV_PAR11 = 2
	cTipos := "'"
	For nx := 1 to Len(aTiposF)
		cTipos += aTiposF[nX]
		
		If nx != Len(aTiposF)
			cTipos += "','"
		EndIf
	Next nX
	cTipos += "'"
EndIf

/*
 * Verificação do campo para ativos de custo de provisão
 */
lRealProv := SN3->(FieldPos("N3_ATFCPR")) > 0

If !Empty(cFilterUser)
	cFilADVPL := PcoParseFil(cFilterUser,"SN3") 
EndIf

cQuery := "SELECT SN3.N3_TIPO, SN3.N3_FILIAL, SN3.N3_CBASE, SN3.N3_ITEM, "
cQuery += "       SN1.N1_DESCRIC, "
cQuery += "       SN3.N3_BAIXA,  SN3.N3_CCONTAB, "
cQuery += "	      SN3.N3_CCUSTO, SN3.N3_TXDEPR1, SN3.N3_CDEPREC, SN3.N3_CDESP,   SN3.N3_CCDEPR, "
cQuery += "       SN3.N3_VORIG1, SN3.N3_VRCACM1, SN3.N3_AMPLIA1, SN3.N3_VRDACM1, SN3.N3_VRCDA1, "
cQuery += "       SN3.N3_VORIG2, SN3.N3_AMPLIA2, SN3.N3_VRDACM2, "
cQuery += "       SN3.N3_VORIG3, SN3.N3_AMPLIA3, SN3.N3_VRDACM3, "
cQuery += "       SN3.N3_VORIG4, SN3.N3_AMPLIA4, SN3.N3_VRDACM4, "
cQuery += "       SN3.N3_VORIG5, SN3.N3_AMPLIA5, SN3.N3_VRDACM5  "

cQuery += "FROM "+RetSQLName("SN3")+" SN3, "+RetSQLName("SN1")+" SN1 "

cQuery += "WHERE SN3.N3_FILIAL = '"+xFilial("SN3")+"' AND " 

If !Empty(cFilterUser)
	cQuery += " (" + cFilADVPL + ") AND "
EndIf

cQuery += "	     SN3.N3_CBASE BETWEEN '"+mv_par01+"' AND '"+mv_par02+"' AND "

cQuery += "      SN3.N3_TXDEPR1 <> 0  AND "
cQuery += "      SN3.N3_BAIXA   =  '0'  AND "

cQuery += "	     (SN3.N3_CDEPREC <> ' ' OR "
cQuery += "	      SN3.N3_CDESP   <> ' ' OR "
cQuery += "       SN3.N3_CCDEPR  <> ' ' ) AND 

IF MV_PAR08 == 2 // Somente bens com saldo a depreciar
	For i := 3 To 7
		If	&("mv_par0" + ALLTRIM(STR(i))) == 1	//Se a moeda foi escolhida pelo usuario
			If !Empty( cWhereAux )
				cWhereAux += " OR "
			EndIf
			cWhereAux += "( SN3.N3_VRDACM"+ALLTRIM(STR(i-2))+" < (SN3.N3_VORIG"+ALLTRIM(STR(i-2))+" + SN3.N3_AMPLIA"+ALLTRIM(STR(i-2))+") )"
		EndIf
	Next i

	If !Empty( cWhereAux )
		cQuery += "( " + cWhereAux + " ) AND"
	EndIf
ENDIF
//Verifica se filtra as classificações patrimoniais
If Len(aClassif) > 0  .And.  FindFunction("FormatClas")
	cQuery += " N1_PATRIM IN " + FORMATCLAS(aClassif,.T.) + " AND "+ CRLF
EndIf



If nOrdem == 1 
	cQuery += " SN3.N3_CCONTAB <> '' AND "

ElseIf nOrdem == 2
	cQuery += " SN3.N3_CCUSTO <> '' AND "

EndIf

If lRealProv .AND. MV_PAR10 == 2
	cQuery += " (SN3.N3_ATFCPR = '2' OR SN3.N3_ATFCPR = ' ') AND "
EndIf

cQuery += "      SN3.D_E_L_E_T_ <> '*' AND "


cQuery += "      SN1.N1_CBASE = SN3.N3_CBASE AND "
cQuery += "      SN1.N1_ITEM  = SN3.N3_ITEM AND "      
cQuery += "      SN1.N1_FILIAL = '"+xFilial("SN1")+"' AND "
If MV_PAR11 != 3
	cQuery 	+=  " SN3.N3_TIPO NOT IN (" + cTipos + ") AND
EndIf
cQuery += "      SN1.D_E_L_E_T_ <> '*' "

If nOrdem == 1       // Saldos a Depreciar por Conta Contabil - Ordem 2 da SN3
	cQuery += "ORDER BY SN3.N3_FILIAL, SN3.N3_CCONTAB, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO, SN3.N3_BAIXA "

ElseiF nOrdem == 2   // Saldos aDepreciar por Centro de Custo - Ordem 3 da SN3
	cQuery += "ORDER BY SN3.N3_FILIAL, SN3.N3_CCUSTO, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO, SN3.N3_BAIXA "

Endif

cQuery := ChangeQuery(cQuery)                     
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),"TRB", .F., .T.)


SetRegua(TRB->(RecCount()))

While !TRB->(Eof())

	// Incrementa Regua
	IncRegua()
	
	//*****************************
	// Tipos de Ativos gerenciais *
	//*****************************
	/*If TRB->N3_TIPO $ cTpGerenc
		TRB->( DbSkip() )
		Loop
	EndIf*/
	If mv_par11 == 1
		If TRB->N3_TIPO $ cTpGerenc
			TRB->( DbSkip() )
			Loop
		EndIf
	EndIf

	cCond := Iif(nOrdem == 1, TRB->N3_CCONTAB, TRB->N3_CCUSTO)
	nTotal1 := nTotal2 := nTotal3 := nTotal4 := nTotal5 := 0
		

	While !TRB->(EOF()) .and. cCond == Iif(nOrdem==1, TRB->N3_CCONTAB, TRB->N3_CCUSTO)
		
		//**********************************************
		// Tipos de Ativos gerenciais - Chamado THCQOP *
		// Comentado abaixopara que mostre todos os tipos
		// de ativo. Ideal é criar pergunta
		//**********************************************
		/*If TRB->N3_TIPO $ cTpGerenc
			TRB->( DbSkip() )
			Loop
		EndIf*/
		If mv_par11 == 1
			If TRB->N3_TIPO $ cTpGerenc
				TRB->( DbSkip() )
				Loop
			EndIf
		EndIf

		// Incrementa Regua
		IncRegua()
		
		

		IF	lEnd
			@PROW()+1,001 PSAY OemToAnsi(STR0008) //"CANCELADO PELO OPERADOR"
			Exit
		EndIf

		IF	li > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,IIF(aReturn[4]==1,15,18))
		Endif



		nSaldo1 := 	(TRB->N3_VORIG1  + TRB->N3_VRCACM1 +  TRB->N3_AMPLIA1) - ;
					(TRB->N3_VRDACM1 + TRB->N3_VRCDA1 )
		nSaldo2 := 	(TRB->N3_VORIG2  + TRB->N3_AMPLIA2) - TRB->N3_VRDACM2
		nSaldo3 := 	(TRB->N3_VORIG3  + TRB->N3_AMPLIA3) - TRB->N3_VRDACM3
		nSaldo4 := 	(TRB->N3_VORIG4  + TRB->N3_AMPLIA4) - TRB->N3_VRDACM4
		nSaldo5 := 	(TRB->N3_VORIG5  + TRB->N3_AMPLIA5) - TRB->N3_VRDACM5
	
		nTotal1 += nSaldo1
		nTotal2 += nSaldo2
		nTotal3 += nSaldo3
		nTotal4 += nSaldo4
		nTotal5 += nSaldo5
	

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ ImpressÆo dos dados em questÆo  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lPrim
			@li++,00 PSAY aOrd[nOrdem]+" : "+cCond
			lPrim := .F.
		EndIf

		@li,00 PSAY TRB->N3_CBASE + " "  + TRB->N3_ITEM + " "+ Left(TRB->N1_DESCRIC,25) + " "

		If	mv_par03 == 1
			@Prow(),Pcol() PSAY nSaldo1 Picture PesqPict("SN3","N3_VORIG1",18,1)
		Endif

		If	mv_par04 == 1
			@Prow(),Pcol() PSAY nSaldo2 Picture PesqPict("SN3","N3_VORIG2",18,2)
		Endif

		If	mv_par05 == 1
			@Prow(),Pcol() PSAY nSaldo3 Picture PesqPict("SN3","N3_VORIG3",18,3)
		Endif

		If	mv_par06 == 1
			@Prow(),Pcol() PSAY nSaldo4 Picture PesqPict("SN3","N3_VORIG4",18,4)
		Endif

		If 	mv_par07 == 1
			@Prow(),Pcol() PSAY nSaldo5 Picture PesqPict("SN3","N3_VORIG5",18,5)
		Endif

		Li ++
		
		TRB->( DbSkip() )
	EndDo


	If	li != 80
		If nTotal1 != 0 .Or. nTotal2 != 0 .Or. nTotal3 != 0 .Or.nTotal4 != 0 .Or. nTotal5 != 0
			@li,00 Say repl("-",132)
			@++li,00 PSAY OemToAnsi(STR0009) + Space(25) // "S U B T O T A I S" 25

			If	mv_par03 == 1
				@Prow(),Pcol() PSAY nTotal1 Picture PesqPict("SN3","N3_VORIG1",18,1)
			Endif

			If mv_par04 == 1
				@Prow(),Pcol() PSAY nTotal2 Picture PesqPict("SN3","N3_VORIG2",18,2)
			Endif

			If mv_par05 == 1
				@Prow(),Pcol() PSAY nTotal3 Picture PesqPict("SN3","N3_VORIG3",18,3)
			Endif

			If	mv_par06 == 1
				@Prow(),Pcol() PSAY nTotal4 Picture PesqPict("SN3","N3_VORIG4",18,4)
			Endif

			If	mv_par07 == 1
				@Prow(),Pcol() PSAY nTotal5 Picture PesqPict("SN3","N3_VORIG5",18,5)
			Endif

			@++li,00 PSAY repl("-",132)
			li++
		Endif
	EndiF
	nTotalG1 += nTotal1
	nTotalG2 += nTotal2
	nTotalG3 += nTotal3
	nTotalG4 += nTotal4
	nTotalG5 += nTotal5                                                                             
	
	lPrim := .T.
	
EndDo

If	li != 80
	@li,00 Say repl("*",132)
	@++li,00 PSAY OemToAnsi(STR0010) + Space(31)  //"T O T A I S" 31

	If	mv_par03 == 1
		@Prow(),Pcol() PSAY nTotalG1 Picture PesqPict("SN3","N3_VORIG1",18,1)
	Endif

	If 	mv_par04 == 1
		@Prow(),Pcol() PSAY nTotalG2 Picture PesqPict("SN3","N3_VORIG2",18,2)
	Endif

	If 	mv_par05 == 1
		@Prow(),Pcol() PSAY nTotalG3 Picture PesqPict("SN3","N3_VORIG3",18,3)
	Endif

	If	mv_par06 == 1
		@Prow(),Pcol() PSAY nTotalG4 Picture PesqPict("SN3","N3_VORIG4",18,4)
	Endif

	If	mv_par07 == 1
		@Prow(),Pcol() PSAY nTotalG5 Picture PesqPict("SN3","N3_VORIG5",18,5)
	Endif

	@++li,00 PSAY repl("*",132)
	Roda(cbcont,cbtxt,Tamanho)
EndIf

DbSelectArea("TRB")
DbCloseArea()

DbSelectArea("SN3")

If aReturn[5] = 1
   Set Printer To
   dbCommitAll()
   Ourspool(wnrel)
End

MS_FLUSH()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ AF50RetCla ³ Autor ³ Rodrigo Gimenes       ³ Data ³ 11.09.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna a classificação patrimonial do bem                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AF50RetCla(cChave)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ATFR050                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AF50RetCla(cChave)

Local aArea 	:= GetArea()
Local aAreaSN1 	:= {}
Local cRet		:= ""

DbSelectArea("SN1")
aAreaSN1 := SN1->(GetArea())

SN1->(dbSetOrder(1))
If SN1->(DbSeek(xFilial("SN1") + cChave))		
	cRet:= SN1->N1_PATRIM	                                      
Endif                                                  

RestArea(aAreaSN1)
RestArea(aArea)           

Return cRet
