#INCLUDE "ATFR200.CH"
#Include "Protheus.ch"

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.  
// TRADUCAO DE CH'S PARA PORTUGAL     
// TRADUCAO DE CH'S PARA PORTUGAL - 21/07/08
/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR200    ³ Autor ³ Carlos A. Gomes Jr.   ³ Data ³ 01.06.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relatorio de Bens Totalmente Depreciados                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFR200()
Local oReport

If TRepInUse()
	oReport := ReportDef()
	oReport:PrintDialog()
Else
    Return ATFR200R3() // Executa versão anterior do fonte
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ATFR200   ºAutor  ³Carlos A. Gomes Jr. º Data ³  06/01/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Definicao do objeto do relatorio personalizavel e das      º±±
±±º          ³ secoes que serao utilizadas                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportDef()

Local oReport,oSection1,oSection2
Local cReport := "ATFR200"
Local cAlias1 := "SN3"
Local cAlias2 := "SN1"
Local cTitulo := OemToAnsi(STR0003) // "Bens Depreciados Por %"
Local cDescri := OemToAnsi(STR0001)+" "+OemToAnsi(STR0002) // "Este programa ir  emitir a rela‡„o dos Bens que j "  // "foram totalmente depreciados por %"
Local oSecFil	:= Nil

Pergunte( "ATR200" , .F. )

oReport  := TReport():New( cReport, cTitulo, "ATR200" , { |oReport| ATFR200Imp( oReport, cAlias1, cAlias2 ) }, cDescri,.T.,,,,,,0)

oReport:SetUseGC(.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a 1a. secao do relatorio Valores nas Moedas   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New( oReport, cTitulo, {cAlias1,cAlias2},,,,,,,,,,,,,.T. )
oSection1:SetAutoSize(.T.)
oSection1:SetLinesBefore(2)
TRCell():New( oSection1, "N3_TXDEPR1"	, cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Taxa Anual Deprec
TRCell():New( oSection1, "N3_CBASE"		, cAlias1 ,STR0021			,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //"Codigo"
TRCell():New( oSection1, "N3_ITEM"		, cAlias1 ,STR0022			,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //"Item"
TRCell():New( oSection1, "N3_FILIAL"	, cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Taxa Anual Deprec
TRCell():New( oSection1, "N3_TIPO"		, cAlias1 ,STR0023			,/*Picture*/,/*nTam*/	,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //"Tipo"
TRCell():New( oSection1, "N3_TPSALDO"	, cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Tipo de Saldo
TRCell():New( oSection1, "N1_DESCRIC"	, cAlias2 ,/*X3Titulo*/     ,/*Picture*/,20			,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,.T.			) //Descricao
TRCell():New( oSection1, "N3_VORIG1"	, cAlias1 ,GetMV("MV_SIMB1"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Moeda 01
TRCell():New( oSection1, "N3_VORIG2"	, cAlias1 ,GetMV("MV_SIMB2"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Moeda 02
TRCell():New( oSection1, "N3_VORIG3"	, cAlias1 ,GetMV("MV_SIMB3"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Moeda 03
TRCell():New( oSection1, "N3_VORIG4"	, cAlias1 ,GetMV("MV_SIMB4"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Moeda 04
TRCell():New( oSection1, "N3_VORIG5"	, cAlias1 ,GetMV("MV_SIMB5"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Moeda 05
TRCell():New( oSection1, "N3_VRCACM1"	, cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Correcao Acumulada
TRCell():New( oSection1, "N3_VRDACM1"	, cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Depr Acumulada
TRCell():New( oSection1, "N3_VRCDA1"	, cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Corr Depr Acumul
TRCell():New( oSection1, "N3_AMPLIA1"	, cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,/*cAlign*/,/*lLineBreak*/	) //Vlr Ampliacao

/* Relacao das filiais selecionadas para compor o relatorio */
oSecFil := TRSection():New(oReport,"SECFIL",{"SN3"})
TRCell():New(oSecFil,"CODFIL" ,,STR0024,/*Picture*/,30,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Código"
TRCell():New(oSecFil,"EMPRESA",,STR0025,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Empresa"
TRCell():New(oSecFil,"UNIDNEG",,STR0026,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Unidade de negócio"	
TRCell():New(oSecFil,"NOMEFIL",,STR0027,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Filial"

oReport:SetTotalInLine(.F.)
oReport:DisableOrientation()

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ATFR200ImpºAutor  ³Carlos A. Gomes Jr. º Data ³  06/01/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Query de impressao do relatorio                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATFR200Imp( oReport, cAlias1, cAlias2 )
Local oBreak		:= Nil
Local oSection1		:= oReport:Section(1)
Local cQuery		:= "SN3"
Local cChave		:= ""
Local cMoeda		:= GetMv("MV_ATFMOED")
Local cWhere		:= ""        
Local aClassif		:= {}
Local cClassif		:= ""
Local lAtfCusPrv	:= AFXAtCsPrv()
Local cFiltro		:= ""
/*-*/
Local aSelFil		:= {}
Local cTmpFil		:= ""
Local cFilSN1		:= ""
Local cFilSN3		:= ""
Local lSelFil		:= .F.

If mv_par11 == 1
	aClassif := AdmGetClass()	
	If Len( aClassif ) <= 0
		Return
	EndIf 		
EndIf

If mv_par10 = 1
	cChave  := "N3_FILIAL+STR(N3_TXDEPR1,8,4)"
Else
	cChave  := "N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO"
EndIf

	lSelFil := (FwSizeFilial() > 2)
	If lSelFil
		If mv_par10 = 1
			cChave  := "STR(N3_TXDEPR1,8,4)"
		Else
			cChave  := "N3_CBASE+N3_ITEM+N3_TIPO"
		EndIf
		oSection1:Cell("N1_DESCRIC"):SetSize(If(MV_PAR10 == 1,20,30))
		If MV_PAR13 == 1 
			AdmSelecFil("ATR200",13,.F.,@aSelFil,"SN3",.F.)
		Endif
		If Empty(aSelFil)
			Aadd(aSelFil,cFilAnt)
		Endif
	Else
		oSection1:Cell("N3_FILIAL"):Disable()
	Endif
	cQuery := GetNextAlias()			
	If lSelFil
		MsgRun(STR0028,STR0003,{|| cFilSN1 := GetRngFil(aSelFil,"SN1",.T.,@cTmpFil)}) //"Favor Aguardar..."###"Bens Depreciados"
		cFilSN3 := "%SN3.N3_FILIAL " + cFilSN1 + "%"
		cFilSN1 := "%SN1.N1_FILIAL " + cFilSN1 + "%"
	Else
		cFilSN1 := "%SN1.N1_FILIAL = '" + xFilial("SN1") + "'%"
		cFilSN3 := "%SN3.N3_FILIAL = '" + xFilial("SN3") + "'%"
	Endi
	
	cWhere := "% AND (ABS(N3_VORIG"+cMoeda+") + N3_AMPLIA"+cMoeda+") - ABS(N3_VRDACM"+cMoeda+") <= 0 "	
	//Filtra as classificações
	If Len(aClassif) > 0 
		cWhere += " AND SN1.N1_PATRIM IN " + FORMATCLAS(aClassif,.T.) 
	EndIf
	
	If lAtfCusPrv .And. MV_PAR12 == 2
		cWhere += " AND N3_ATFCPR <> '1' "
	EndIf 
	
	cWhere += " %"	
	cChave := "%" + SqlOrder( cChave ) + "%"
	
	oSection1:BeginQuery()

	BeginSql Alias cQuery
		SELECT
			N3_FILIAL,N3_CBASE, N3_ITEM, N3_TXDEPR1, 
			N3_VORIG1, N3_VORIG2, N3_VORIG3, N3_VORIG4, N3_VORIG5,
			N3_AMPLIA1, N3_AMPLIA2, N3_AMPLIA3, N3_AMPLIA4, N3_AMPLIA5, 
			N3_VRCACM1, N3_VRDACM1, N3_VRCDA1, N3_AMPLIA1,      
			N1_DESCRIC,N3_TPSALDO,N3_TIPO
		FROM 
			%table:SN3% SN3, %table:SN1% SN1
		WHERE
			%Exp:cFilSN3% AND
			( ( SN3.N3_FIMDEPR >= %Exp:DTOS(mv_par01)% AND 
			    SN3.N3_FIMDEPR <= %Exp:DTOS(mv_par02)% ) OR 
			  ( N3_VORIG1 <= ( N3_VRDACM1 + N3_AMPLIA1 + N3_VRCACM1 ) ) ) AND
			SN3.N3_CBASE >= %Exp:mv_par03% AND 
			SN3.N3_CBASE <= %Exp:mv_par04% AND
			SN3.N3_CDEPREC <> ' ' AND
			SN3.N3_CCDEPR <> ' ' AND
			SN3.N3_BAIXA = '0' AND
			SN3.%notDel% AND
			%Exp:cFilSN1% AND
			SN1.N1_CBASE = SN3.N3_CBASE AND
			SN1.N1_ITEM = SN3.N3_ITEM AND
			SN1.%notDel%
			%Exp:cWhere%
		ORDER BY %Exp:cChave%
	EndSql

	oSection1:EndQuery()

oSection1:Cell("N3_VORIG1"):SetBlock({|| (cQuery)->(N3_VORIG1+N3_AMPLIA1) })
oSection1:Cell("N3_VORIG2"):SetBlock({|| (cQuery)->(N3_VORIG2+N3_AMPLIA2) })
oSection1:Cell("N3_VORIG3"):SetBlock({|| (cQuery)->(N3_VORIG3+N3_AMPLIA3) })
oSection1:Cell("N3_VORIG4"):SetBlock({|| (cQuery)->(N3_VORIG4+N3_AMPLIA4) })
oSection1:Cell("N3_VORIG5"):SetBlock({|| (cQuery)->(N3_VORIG5+N3_AMPLIA5) })

If mv_par10 == 1
	oBreak := TRBreak():New ( oSection1 , oSection1:Cell("N3_TXDEPR1") , OemToAnsi(STR0008))//+Str(oSection1:Cell("N3_TXDEPR1"):GetValue(),8,2), )
Else
	oSection1:Cell("N3_TXDEPR1"):Disable()
EndIf

If	mv_par05 != 1
	oSection1:Cell("N3_VORIG1"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG1") , , "SUM" , Iif(mv_par10 == 1,oBreak,) , , , , .F. ,  )
EndIf
If	mv_par06 != 1
	oSection1:Cell("N3_VORIG2"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG2") , , "SUM" , Iif(mv_par10 == 1,oBreak,) , , , , .F. ,  )
EndIf
If	mv_par07 != 1
	oSection1:Cell("N3_VORIG3"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG3") , , "SUM" , Iif(mv_par10 == 1,oBreak,) , , , , .F. ,  )
EndIf
If	mv_par08 != 1
	oSection1:Cell("N3_VORIG4"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG4") , , "SUM" , Iif(mv_par10 == 1,oBreak,) , , , , .F. ,  )
EndIf
If	mv_par09 != 1
	oSection1:Cell("N3_VORIG5"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG5") , , "SUM" , Iif(mv_par10 == 1,oBreak,) , , , , .F. ,  )
EndIf

TRFunction():New ( oSection1:Cell("N3_VRCACM1") , , "SUM" , Iif(mv_par10 == 1,oBreak,) , , , , .F. ,  )
TRFunction():New ( oSection1:Cell("N3_VRDACM1") , , "SUM" , Iif(mv_par10 == 1,oBreak,) , , , , .F. ,  )
TRFunction():New ( oSection1:Cell("N3_VRCDA1" ) , , "SUM" , Iif(mv_par10 == 1,oBreak,) , , , , .F. ,  )
TRFunction():New ( oSection1:Cell("N3_AMPLIA1") , , "SUM" , Iif(mv_par10 == 1,oBreak,) , , , , .F. ,  )

If lSelFil .And. Len(aSelFil) > 1
	oSection1:OnPrintLine({|| AFR200Fil(@aSelFil,oReport)})
Endif

oSection1:Print()

If !Empty(cTmpFil)
	MsgRun(STR0028,STR0003,{|| CtbTmpErase(cTmpFil)}) //"Favor Aguardar..."###"Bens Depreciados"
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AFR200FIL  ºAutor  ³                    º Data ³ 24/01/2014  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Relacao das filiais selecionadas para o relatorio            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AFR200Fil(aSelFil,oReport)
Local oSecFil	:= Nil
Local aSM0		:= {}
Local nRegSM0	:= 0
Local nTamEmp	:= 0
Local nTamUnNeg	:= 0
Local nTamTit	:= 0
Local nX		:= 0
Local nLinha	:= 0
Local cTitulo 	:= ""

oReport:Section(1):OnPrintLine({|| })
oSecFil := oReport:Section("SECFIL")
nRegSM0 := SM0->(Recno())
aSM0 := FWLoadSM0()
SM0->(DbGoTo(nRegSM0))
nTamEmp := Len(FWSM0LayOut(,1))
nTamUnNeg := Len(FWSM0LayOut(,2))
cTitulo := oReport:Title()
oReport:SetTitle(cTitulo + " (" + STR0029 + ")")		//"Filiais selecionadas para o relatorio"
nTamTit := Len(oReport:Title())
oSecFil:Init()
oSecFil:Cell("CODFIL"):SetBlock({||cFilSel})
oSecFil:Cell("EMPRESA"):SetBlock({||aSM0[nLinha,SM0_DESCEMP]})
oSecFil:Cell("UNIDNEG"):SetBlock({||aSM0[nLinha,SM0_DESCUN]})
oSecFil:Cell("NOMEFIL"):SetBlock({||aSM0[nLinha,SM0_NOMRED]})
For nX := 1 To Len(aSelFil)
	nLinha := Ascan(aSM0,{|sm0| sm0[SM0_GRPEMP] == cEmpAnt .And. sm0[SM0_CODFIL] == aSelFil[nX]})
	If nLinha > 0
		cFilSel := Substr(aSM0[nLinha,SM0_CODFIL],1,nTamEmp)
		cFilSel += " "
		cFilSel += Substr(aSM0[nLinha,SM0_CODFIL],nTamEmp + 1,nTamUnNeg)
		cFilSel += " "
		cFilSel += Substr(aSM0[nLinha,SM0_CODFIL],nTamEmp + nTamUnNeg + 1)
		oSecFil:PrintLine()
	Endif
Next
oReport:SetTitle(cTitulo)
oSecFil:Finish()
oReport:EndPage()
Return()

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR200R3  ³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 02.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relatorio de Bens Totalmente Depreciados                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFR200R3()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

LOCAL cString 	:= "SN3"
LOCAL cDesc1    := OemToAnsi(STR0001) // "Este programa ir  emitir a rela‡„o dos Bens que j "
LOCAL cDesc2    := OemToAnsi(STR0002) // "foram totalmente depreciados por %"
LOCAL cDesc3    := ""
LOCAL wnrel

PRIVATE aReturn  := { "Zebrado", 1,"Administracao", 2, 2, 1, "",1 }
PRIVATE aLinha   := { }
PRIVATE cPerg    := "ATR200"
PRIVATE nomeprog := "ATFR200"
PRIVATE nLastKey := 0
PRIVATE tamanho  := "M"
PRIVATE titulo  := OemToAnsi(STR0003) // "Bens Depreciados Por %"
PRIVATE cabec1  := OemToAnsi(STR0004) // "Saldos Depreciados por Bens"
PRIVATE cabec2  := OemToAnsi(STR0005) // "Codigo  Descricao"
PRIVATE nTamanho
PRIVATE cExpres := ""
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros       		³
//³ mv_par01            Data de                		³
//³ mv_par02            Data At‚               		³
//³ mv_par03            Do C¢digo              		³
//³ mv_par04            Ao C¢digo              		³
//³ mv_par05            Moeda 1                		³
//³ mv_par06            Moeda 2                		³
//³ mv_par07            Moeda 3                		³
//³ mv_par08            Moeda 4                		³
//³ mv_par09            Moeda 5                		³
//³ mv_par10            Agrupa po Taxa         		³
//³ mv_par11            Selec Classif Patrimonial?	³  	 
//³ MV_PAR12            Exibir Ativos Real. Provis?	³  	 
//³ MV_PAR13            Seleciona filiais          	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
pergunte("ATR200",.F.)

wnrel := "ATFR200"
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,,,Tamanho)

If nLastKey == 27
	Return
End

SetDefault( aReturn,cString )

nTamanho:=IIf(aReturn[4]==1,15,18)

If nLastKey == 27
   Return
Endif

RptStatus({|lEnd| FR200Imp(@lEnd,wnRel,cString)},Titulo)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FR200Imp  ³ Autor ³ Alice Y Yamamoto      ³ Data ³ 02.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impress„o de bens totalmente depreciados por percentual    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³FR200IMP(cAlias,nReg,nOpc)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function FR200Imp(lEnd,wnRel,cString)

LOCAL CbTxt
LOCAL cbCont

LOCAL nTotal1 := 0, nTotal2 := 0, nTotal3 := 0, nTotal4 := 0, nTotal5 := 0
LOCAL nTotalCorrAc := 0, nTotalDeprAc := 0, nTotalCrDpAc := 0, nTotalAmplia := 0
LOCAL nSaldo1 := 0
LOCAL cMoeda1 := 0, cMoeda2 := 0, cMoeda3 := 0, cMoeda4 := 0, cMoeda5 := 0
Local cIndex:="",cChave:=""
Local nSavOrd1:=IndexOrd() ,nTaxa

Local nTotGeral1 := 0, nTotGeral2 := 0, nTotGeral3 := 0, nTotGeral4 := 0, nTotGeral5 := 0
Local nTotGCorAc := 0, nTotGDepAc := 0, nTotGCrDpAc := 0, nTotGAmplia := 0
               
Local lSomaLi := .F.
Local aClassif := {}
Local cClassif := ""

Local lAtfCusPrv := AFXAtCsPrv()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbTxt   := SPACE(10)
cbCont  := 0
li      := 80
m_pag   := 1

cMoeda1 := AllTrim(GetMV("MV_SIMB1"))
cMoeda2 := AllTrim(GetMV("MV_SIMB2"))
cMoeda3 := AllTrim(GetMV("MV_SIMB3"))
cMoeda4 := AllTrim(GetMV("MV_SIMB4"))
cMoeda5 := AllTrim(GetMV("MV_SIMB5"))

cMoeda1 := Spac(19 - Len(cMoeda1) ) + cMoeda1
cMoeda2 := Spac(18 - Len(cMoeda2) ) + cMoeda2
cMoeda3 := Spac(18 - Len(cMoeda3) ) + cMoeda3
cMoeda4 := Spac(18 - Len(cMoeda4) ) + cMoeda4
cMoeda5 := Spac(18 - Len(cMoeda5) ) + cMoeda5

cabec1  := OemToAnsi(STR0006) + Space(08) // "Codigo     Item Descricao"
cabec1  +=  IIf(mv_par05 == 1,cMoeda1,"" )
cabec1  +=  IIf(mv_par06 == 1,cMoeda2,"" )
cabec1  +=  IIf(mv_par07 == 1,cMoeda3,"" )
cabec1  +=  IIf(mv_par08 == 1,cMoeda4,"" )
cabec1  +=  IIf(mv_par09 == 1,cMoeda5,"" )
cabec2  := OemToAnsi(STR0010)    //"                                  Correcao Acumulada    Depr Acumulada  Corr Depr Acumul"
                        *           012345678901234567890123456789012345678901234567890123456789012345678901234567890

dbSelectArea("SN3")
dbSetOrder(1)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta express„o do Filtro para sele‡„o							  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cIndex := CriaTrab(nil,.f.)
If mv_par10 == 1
	cChave  := "N3_FILIAL+STR(N3_TXDEPR1,8,4)"
Else
	cChave  := "N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO"
EndIf 

If mv_par11 == 1
	aClassif := AdmGetClass()	
EndIf

IndRegua("SN3",cIndex,cChave,,Af200IndR(),"Selecionando Registros...")
nIndex := RetIndex("SN3")
dbSelectArea("SN3")
dbSetOrder(nIndex+1)
If BOF() .and. EOF()
	Help(" ",1,"RECNO")
	RetIndex("SN3")
	dbSetOrder(nSavOrd1)
	dbGoTop()
	FErase (cIndex+OrdBagExt())
EndIf

SetRegua(SN3->(RecCount()))
DBGOTOP()
nTaxa := N3_TXDEPR1

If Len(aClassif) > 0
	cClassif := FormatClass(aClassif,.F.)
EndIf 

While SN3->(!Eof()) .and. SN3->N3_FILIAL == xFilial("SN3")

	// Incrementa Regua
	IncRegua()

	IF	lEnd
		@PROW()+1,001 PSAY OemToAnsi(STR0007) // "CANCELADO PELO OPERADOR"
		Exit
	End
	
	cCond := IIf(mv_par10 = 1,SN3->N3_TXDEPR1 == nTaxa,.T.)

	While SN3->(!Eof()) .And. SN3->N3_FILIAL==xFilial("SN3") .AND. cCond
		
		If lAtfCusPrv .And. MV_PAR12 == 2
			If SN3->N3_ATFCPR == '1'
				SN3->(dbSkip())
				Loop
			EndIf
		EndIf  
		
		//Verifica se filtra as classificações patrimoniais
		If !Empty(cClassif)
			If SN1->(MSSeek(xFilial("SN1")+SN3->(N3_CBASE + N3_ITEM) ))
				If !(SN1->N1_PATRIM $ cClassif)
					SN3->(dbSkip())
					Loop
				EndIf
			EndIf
		EndIf 
		
		IF li > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTamanho)
			lSomaLi := .F.
		Endif

		nTaxAnt := SN3->N3_TXDEPR1

		cMoeda  := GetMv("MV_ATFMOED")
		nSaldo1 := NoRound(Abs( &('SN3->N3_VORIG'+cMoeda) + &('SN3->N3_AMPLIA'+cMoeda)),2) - NoRound(Abs( &('SN3->N3_VRDACM'+cMoeda) ),2)
		// Verifica se ja foi Totalmente depreciado
		If nSaldo1 > 0
			dbSkip()
			Loop
		End
		
		   

		nTotal1 += SN3->N3_VORIG1+SN3->N3_AMPLIA1
		nTotal2 += SN3->N3_VORIG2+SN3->N3_AMPLIA2
		nTotal3 += SN3->N3_VORIG3+SN3->N3_AMPLIA3
		nTotal4 += SN3->N3_VORIG4+SN3->N3_AMPLIA4
		nTotal5 += SN3->N3_VORIG5+SN3->N3_AMPLIA5

		nTotalCorrAc += SN3->N3_VRCACM1
		nTotalDeprAc += SN3->N3_VRDACM1
		nTotalCrDpAc += SN3->N3_VRCDA1
		nTotalAmplia += SN3->N3_AMPLIA1

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona o cadastro de bens para ler a descri‡Æo ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SN1")
		dbSetOrder(1)
		dbSeek( xFilial("SN1") + SN3->N3_CBASE + SN3->N3_ITEM )
        
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ ImpressÆo dos dados em questÆo  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lSomaLi
			Li++
		EndIf
		@li,00 PSAY SN3->N3_CBASE + " "  + SN3->N3_ITEM + " "+ Left(SN1->N1_DESCRIC,17) + " "
		If	mv_par05 == 1
			@Prow(),Pcol() PSAY SN3->N3_VORIG1+SN3->N3_AMPLIA1 Picture PesqPict("SN3","N3_VORIG1",18,1)
		Endif
		If	mv_par06 == 1
			@Prow(),Pcol() PSAY SN3->N3_VORIG2+SN3->N3_AMPLIA2 Picture PesqPict("SN3","N3_VORIG2",18,2)
		Endif
		If	mv_par07 == 1
			@Prow(),Pcol() PSAY SN3->N3_VORIG3+SN3->N3_AMPLIA3 Picture PesqPict("SN3","N3_VORIG3",18,3)
		Endif
		If mv_par08 == 1
			@Prow(),Pcol() PSAY SN3->N3_VORIG4+SN3->N3_AMPLIA4 Picture PesqPict("SN3","N3_VORIG4",18,4)
		Endif
		If mv_par09 == 1
			@Prow(),Pcol() PSAY SN3->N3_VORIG5+SN3->N3_AMPLIA5 Picture PesqPict("SN3","N3_VORIG5",18,5)
		Endif
		Li ++
		@Li, 034 PSAY SN3->N3_VRCACM1 Picture PesqPict("SN3","N3_VRCACM1",18,1)
		@Li, 052 PSAY SN3->N3_VRDACM1 Picture PesqPict("SN3","N3_VRDACM1",18,1)
		@Li, 070 PSAY SN3->N3_VRCDA1  Picture PesqPict("SN3","N3_VRCDA1" ,18,1)
		@Li, 088 PSAY SN3->N3_AMPLIA1 Picture PesqPict("SN3","N3_AMPLIA1",18,1)
		lSomaLi := .T.
		dbSelectArea("SN3")
		dbSkip()
		If mv_par10 = 1
			nTaxa := SN3->N3_TXDEPR1
			If nTaxa != nTaxAnt
				Exit
			EndIf
		Endif
	EndDo

	If mv_par10 = 1
		Li ++
		If	li != 80
			If nTotal1 != 0 .or. nTotal2 != 0 .or. nTotal3 != 0 .or. nTotal4 != 0 .or. nTotal5 != 0
				IF li > 58
					cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTamanho)
				Endif
				@li,00 Say repl("*",132)
				@++li,00 PSAY Padr((OemToAnsi (STR0008) + Str(nTaxAnt)),16) // + Spac(5) // "T O T A I S DA TAXA: "
				If	mv_par05 == 1
					@Prow(),Pcol() PSAY nTotal1 Picture PesqPict("SN3","N3_VORIG1",18,1)
				Endif
				If mv_par06 == 1
					@Prow(),Pcol() PSAY nTotal2 Picture PesqPict("SN3","N3_VORIG2",18,2)
				Endif
				If mv_par07 == 1
					@Prow(),Pcol() PSAY nTotal3 Picture PesqPict("SN3","N3_VORIG3",18,3)
				Endif
				If	mv_par08 == 1
					@Prow(),Pcol() PSAY nTotal4 Picture PesqPict("SN3","N3_VORIG4",18,4)
				Endif
				If	mv_par09 == 1
					@Prow(),Pcol() PSAY nTotal5 Picture PesqPict("SN3","N3_VORIG5",18,5)
				Endif
				Li++
				@li  ,034 PSAY nTotalCorrAc Picture PesqPict("SN3","N3_VRCACM1",18,1)
				@li  ,052 PSAY nTotalDeprAc Picture PesqPict("SN3","N3_VRDACM1",18,1)
				@li  ,070 PSAY nTotalCrDpAc Picture PesqPict("SN3","N3_VRCDA1" ,18,1)
				@li  ,088 PSAY nTotalAmplia Picture PesqPict("SN3","N3_AMPLIA1",18,1)
				@li++,00 Say repl("*",132)
			EndIf
		EndIf
		li++
	EndIf
	nTotGeral1 += nTotal1
	nTotGeral2 += nTotal2
	nTotGeral3 += nTotal3
	nTotGeral4 += nTotal4
	nTotGeral5 += nTotal5
	nTotGCorAc  += nTotalCorrAc
	nTotGDepAc  += nTotalDeprAc
	nTotGCrDpAc += nTotalCrDpAc
	nTotGAmplia += nTotalAmplia
	nTotal1 := 0
	nTotal2 := 0
	nTotal3 := 0
	nTotal4 := 0
	nTotal5 := 0
	nTotalCorrAc := 0
	nTotalDeprAc := 0
	nTotalCrDpAc := 0
	nTotalAmplia := 0

	dbSelectArea("SN3")
	If mv_par10 =1
		nTaxa := SN3->N3_TXDEPR1
	EndiF
EndDo

If	li != 80
	IF li > 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTamanho)
	Endif
	If nTotGeral1 != 0 .or. nTotGeral2 != 0 .or. nTotGeral3 != 0 .or. nTotGeral4 != 0 .or. nTotGeral5 != 0
		@li,00 Say repl("*",132)
		@++li,00 PSAY OemToAnsi(STR0009) + Space(16) // "T O T A I S GERAIS"
		If	mv_par05 == 1
			@Prow(),Pcol() PSAY nTotGeral1 Picture PesqPict("SN3","N3_VORIG1",18,1)
		Endif
		If mv_par06 == 1
			@Prow(),Pcol() PSAY nTotGeral2 Picture PesqPict("SN3","N3_VORIG2",18,2)
		Endif
		If mv_par07 == 1
			@Prow(),Pcol() PSAY nTotGeral3 Picture PesqPict("SN3","N3_VORIG3",18,3)
		Endif
		If	mv_par08 == 1
			@Prow(),Pcol() PSAY nTotGeral4 Picture PesqPict("SN3","N3_VORIG4",18,4)
		Endif
		If	mv_par09 == 1
			@Prow(),Pcol() PSAY nTotGeral5 Picture PesqPict("SN3","N3_VORIG5",18,5)
		Endif
		Li++
		@Li,034 PSAY nTotGCorAc  Picture PesqPict("SN3","N3_VRCACM1",18,1)
		@Li,052 PSAY nTotGDepAc  Picture PesqPict("SN3","N3_VRDACM1",18,1)
		@Li,070 PSAY nTotGCrDpAc Picture PesqPict("SN3","N3_VRCDA1" ,18,1)
		@Li,088 PSAY nTotGAmplia Picture PesqPict("SN3","N3_AMPLIA1",18,1)
	EndIf
	@++li,00 PSAY repl("*",132)
	Roda(cbcont,cbtxt,Tamanho)
EndiF

If aReturn[5] = 1
	Set Printer to
	dbCommitAll()
	Ourspool(wnrel)
End

RetIndex("SN3")
Set Filter to
dbSetOrder(nSavOrd1)
dbGoTop()
FErase (cIndex+OrdBagExt())


MS_FLUSH()

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ AF200IndR  ³ Autor ³ Alice Yamamoto 		  ³ Data ³02.03.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Criar Indice Condicional da indRegua							³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ IndRegua 													³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Af200IndR()

Local cOldArea := Alias()
	cExpres := 'N3_FILIAL == "' + xFilial("SN3") + '" .AND. '
	cExpres += 'If( !Empty( N3_FIMDEPR ), '
	cExpres += '    DTOS( N3_FIMDEPR ) >= "' + DTOS( mv_par01 ) + '" .AND. '
	cExpres += '    DTOS( N3_FIMDEPR ) <= "' + DTOS( mv_par02 ) + '", '
	cExpres += '    N3_VORIG1 <= ( N3_VRDACM1 + N3_AMPLIA1 + N3_VRCACM1 ) ) .AND. '
	cExpres += 'N3_CBASE >= "' + mv_par03 + '" .AND. '
	cExpres += 'N3_CBASE <= "' + mv_par04 + '" .AND. '
	cExpres += '( !EMPTY( N3_CDEPREC ) ) .AND. '
	cExpres += '( !EMPTY( N3_CCDEPR  ) ) .AND. '
	cExpres += '( N3_BAIXA == "0" )'
dbSelectArea(cOldArea)
Return cExpres
