#INCLUDE "ATFR090.CH"
#include "Protheus.ch"


// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.

// TRADUCAO DE CH'S PARA PORTUGAL - 21/07/08         

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR090  ³ Autor ³ Wagner Xavier         ³ Data ³ 28.01.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Rela‡„o de Adiantamentos                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Sintaxe e ³ ATFR090                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	Last change:  CIA   4 Jan 96    2:11 pm
*/
Function ATFR090
Local oReport 

If TRepInUse()
	oReport:=ReportDef()
	oReport:PrintDialog()
Else
   Return ATFR090R3() // Executa versão anterior do relatorio
Endif

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³Claudio D. de Souza    ³ Data ³28/06/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatório                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()
Local oReport,oSection1,oSection11
Local cReport := "ATFR090"
Local cAlias1 := "SN3"
Local cTitulo := STR0004 // "Rela‡„o dos Adiantamentos"
Local cDescri :=	STR0001 + " " + ;	// "Este programa ir  a rela‡„o dos bens adquiridos como"
						STR0002 + " " + ;	// "adiantamentos. Poder  ser listado por C¢digo, Conta, C.Custo,"
						STR0003 				// "Projeto ou Data de Aquisi‡„o."
Local bReport := { |oReport|	oReport:SetTitle( oReport:Title() + " - " + OemToAnsi(STR0010)+; // " Valores em " 
																AllTrim(GETMV("MV_SIMB" + Str(mv_par11,1,0)))),;
										ReportPrint( oReport ) }
Local aOrd

aOrd  := { 	OemToAnsi(STR0005),; // "Por C¢digo"
				OemToAnsi(STR0006),; //	"Por Conta"
				OemToAnsi(STR0007),; // "Por C.Custo"
				OemToAnsi(STR0008),; // "Projeto"
				OemToAnsi(STR0009),; // "Data
				STR0017 				,; // "Item Contabil"
				STR0018           }  // "Classe de Valor"

Pergunte( "AFR090" , .F. )
oReport  := TReport():New( cReport, cTitulo, "AFR090" , bReport, cDescri )

oReport:SetLandScape(.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a 1a. secao do relatorio Valores nas Moedas   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1 := TRSection():New( oReport, STR0029, {cAlias1}, aOrd )		//"Ordem dos Adiantamentos"

TRCell():New( oSection1, "N1_PROJETO"		, "SN1",/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection1, "N1_AQUISIC"		, "SN1",/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection1, "N3_CBASE"			, cAlias1,STR0028 /*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) // "Código"
TRCell():New( oSection1, "N3_CCONTAB"		, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection1, "N3_SUBCTA"		, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection1, "N3_CLVL"			, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection1, "N3_CCUSTO"		, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

oSection11 := TRSection():New( oSection1, STR0030, {cAlias1} )		//"Movimentos de Adiantamentos"
TRCell():New( oSection11, "N3_CBASE"		, cAlias1,STR0028 /*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) // "Código"
TRCell():New( oSection11, "N3_ITEM"			, cAlias1,STR0027 /*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/) // "Item"
TRCell():New( oSection11, "N3_CCONTAB"		, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N3_CCUSTO"		, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N1_PROJETO"		, "SN1",/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N3_AQUISIC"		, "SN3",/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "VALOR"			,      ,STR0026 /*X3Titulo*/     ,PesqPict("SN3","N3_VORIG"+Str( mv_par11, 1 ),18,mv_par11)/*Picture*/,18/*Tamanho*/,/*lPixel*/, /* { || code-block de impressoa } */ ) // "Valor Adiatamento"
TRCell():New( oSection11, "N3_CDEPREC"		, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N3_CCDEPR"		, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N3_CCORREC"		, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N3_SUBCTA"		, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New( oSection11, "N3_CLVL"			, cAlias1,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ReportPrintºAutor  ³Claudio D. de Souza º Data ³  23/06/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Query de impressao do relatorio                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAATF                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint( oReport )
Local oSection1  	:= oReport:Section(1)
Local oSection11 	:= oReport:Section(1):Section(1)
Local cChave
Local cQuery		:= "SN3"
Local cAliasSn1	:= "SN1"
Local cQuebra		:= ""
Local cWhere		:= ""
Local nOrder   := oSection1:GetOrder()
Local bFilter
Local bQuebra
Local bTexto := { || "" }
Local bValor
Local cNomeArq
Local nIndex 
Local aStru
Local cFilterUser 
Local aClassif := {}
Local cClassif := ""
Local lRealProv := .F.

/*
 * Verificação do campo para ativos de custo de provisão
 */
If Select("SN3") == 0
	DbSelectArea("SN3")	
EndIf

lRealProv := .T.

Do Case
Case nOrder != 4 .And. nOrder != 5
	cChave := SN3->(IndexKey(nOrder))
Case nOrder	== 4
	cChave := "N1_FILIAL + N1_PROJETO + N1_CBASE + N1_ITEM"
Case nOrder	== 5
	cChave := "N1_FILIAL + DTOS(N1_AQUISIC) + N1_CBASE + N1_ITEM"
EndCase	

If mv_par17 == 1
	aClassif := AdmGetClass()
	If Len( aClassif ) <= 0
		Return
	EndIf 	
EndIf	
	
cQuery 		:= GetNextAlias()
cAliasSn1	:= cQuery
cChave 	:= "%"+SqlOrder(cChave)+"%"
// Se nao considera itens baixados, seleciona apenas os itens em aberto
If mv_par16 == 2
	cWhere := "N3_BAIXA = '0' AND"
Endif	
	 
//Filtra Classificação Patrimonial
If mv_par17 == 1 .And. Len(aClassif) > 0
	cWhere += " N1_PATRIM IN " + FORMATCLAS(aClassif,.T.) + " AND "
Endif	
	
cFilterUser := oSection1:GetSqlExp("SN3")
	 
//Adiciona o Filtro do usuario, caso o mesmo seja informado
If !Empty(cFilterUser)
	cWhere := cWhere + cFilterUser + " AND "
Endif
	
If lRealProv .AND. MV_PAR18 == 2
	cWhere += " SN3.N3_ATFCPR IN ('2',' ') AND "
EndIf
	
cWhere   := "%" + cWhere + "%"
	
oSection1:BeginQuery()
	
BeginSql Alias cQuery
	SELECT 
		N3_FILIAL, N3_CBASE, N3_ITEM, N3_CCONTAB, N3_CCUSTO, N3_SUBCTA, N3_CLVL, N3_AQUISIC, N3_VORIG1, N3_VORIG2,
		N3_VORIG3, N3_VORIG4, N3_VORIG5, N3_VRCACM1, N3_AMPLIA1, N3_AMPLIA2, N3_AMPLIA3, N3_AMPLIA4, N3_AMPLIA5,
		N3_CDEPREC, N3_CCDEPR, N3_CCORREC, N1_FILIAL, N1_AQUISIC, N1_CBASE, N1_ITEM, N1_PROJETO
	FROM %table:SN3% SN3
		JOIN %table:SN1% SN1 ON
		SN1.N1_FILIAL = %xfilial:SN1% AND
		SN3.N3_CBASE = SN1.N1_CBASE AND
		SN3.N3_ITEM = SN1.N1_ITEM AND
		SN1.%notDel%
	WHERE
		SN3.N3_FILIAL = %xfilial:SN3% AND
		SN1.N1_AQUISIC >= %Exp:mv_par01% AND 
		SN1.N1_AQUISIC <= %Exp:mv_par02% AND 
		SN3.N3_CCONTAB >= %Exp:mv_par03% AND 
		SN3.N3_CCONTAB <= %Exp:mv_par04% AND 
		SN3.N3_CCUSTO 	>= %Exp:mv_par05% AND 
		SN3.N3_CCUSTO 	<= %Exp:mv_par06% AND 
		SN3.N3_CBASE 	>= %Exp:mv_par07% AND 
		SN3.N3_CBASE 	<= %Exp:mv_par08% AND 
		SN1.N1_PROJETO >= %Exp:mv_par09% AND 
		SN1.N1_PROJETO <= %Exp:mv_par10% AND 
		SN3.N3_SUBCTA	>= %Exp:mv_par12% AND 
		SN3.N3_SUBCTA	<= %Exp:mv_par13% AND 
		SN3.N3_CLVL 	>= %Exp:mv_par14% AND 
		SN3.N3_CLVL 	<= %Exp:mv_par15% AND 
		SN3.N3_TIPO = '03' AND
		%Exp:cWhere%
		SN3.%notDel%
	ORDER BY %Exp:cChave%
EndSql

oSection1:EndQuery()
oSection11:SetParentQuery()

oSection1:Cell("N1_PROJETO"):Disable()
oSection1:Cell("N1_AQUISIC"):Disable()
oSection1:Cell("N3_CBASE"):Disable()
oSection1:Cell("N3_CCONTAB"):Disable()
oSection1:Cell("N3_SUBCTA"):Disable()
oSection1:Cell("N3_CLVL"):Disable()
oSection1:Cell("N3_CCUSTO"):Disable()

Do Case
Case nOrder == 1
	bFilter	:= {|cParam| (cQuery)->N3_CBASE == cParam }
	bQuebra	:= {|| (cQuery)->N3_CBASE }
	bTexto 	:= { || STR0021 }  // " DO CODIGO BASE "
	oSection1:Cell("N3_CBASE"):Enable()
	oSection11:Cell("N3_CBASE"):Disable()
Case nOrder == 2
	bFilter	:= {|cParam| (cQuery)->N3_CCONTAB == cParam }
	bQuebra	:= {|| (cQuery)->N3_CCONTAB }	
	bTexto 	:= { || STR0022 }  // " DA CONTA CONTABIL "
	oSection1:Cell("N3_CCONTAB"):Enable()
	oSection11:Cell("N3_CCONTAB"):Disable()
Case nOrder == 3
	bFilter	:= {|cParam| (cQuery)->N3_CCUSTO == cParam }
	bQuebra	:= {|| (cQuery)->N3_CCUSTO }		
	bTexto := { || STR0025 } // " DO CENTRO DE CUSTO "
	oSection1:Cell("N3_CCUSTO"):Enable()
	oSection11:Cell("N3_CCUSTO"):Disable()
Case nOrder == 4
	bFilter	:= {|cParam| (cAliasSn1)->N1_PROJETO == cParam }
	bQuebra	:= {|| (cAliasSn1)->N1_PROJETO }
	oSection1:Cell("N1_PROJETO"):Enable()
	oSection11:Cell("N1_PROJETO"):Disable()
Case nOrder == 5
	bFilter	:= {|cParam| Dtoc((cAliasSn1)->N1_AQUISIC) == cParam }
	bQuebra	:= {|| Dtoc((cAliasSn1)->N1_AQUISIC) }
	oSection1:Cell("N1_AQUISIC"):Enable()
	oSection11:Cell("N3_AQUISIC"):Disable()
Case nOrder == 6
	bFilter	:= {|cParam| (cQuery)->N3_SUBCTA == cParam }
	bQuebra	:= {|| (cQuery)->N3_SUBCTA }		
	bTexto 	:= { || STR0023 }  // " DO ITEM CONTABIL "
	oSection1:Cell("N3_SUBCTA"):Enable()
	oSection11:Cell("N3_SUBCTA"):Disable()
Case nOrder == 7
	bFilter	:= {|cParam| (cQuery)->N3_CLVL == cParam }
	bQuebra	:= {|| (cQuery)->N3_CLVL }
	bTexto 	:= { || STR0024 } // " DA CLASSE DE VALOR "
	oSection1:Cell("N3_CLVL"):Enable()
	oSection11:Cell("N3_CLVL"):Disable()
EndCase

bValor := "{||" + 'N3_VORIG'+Str(mv_par11,1)+'+N3_AMPLIA'+Str(mv_par11,1)+If(mv_par11==1,'+N3_VRCACM'+Str(mv_par11,1),"") + "}"
bValor := &bValor
oSection11:Cell("VALOR"):SetBlock( bValor )
oSection11:Cell("N1_PROJETO"):SetBlock( { || (cAliasSn1)->N1_PROJETO } )
oSection11:Cell("N3_AQUISIC"):SetBlock( { || (cAliasSn1)->N1_AQUISIC } )

oSection1:Cell("N1_PROJETO"):SetBlock( { || (cAliasSn1)->N1_PROJETO } )
oSection1:Cell("N1_AQUISIC"):SetBlock( { || (cAliasSn1)->N1_AQUISIC } )

TRFunction():New(oSection11:Cell("VALOR"),,"SUM",,,, /* bAction */, .T. , .T. )

// Cria variável a ser usada para impressao do texto da quebra da secao
oSection11:OnPrintLine( { ||	cQuebra := Eval(bQuebra), .T. } )

// Forma o titulo da quebra, conforme a ordem escolhida
oSection11:SetTotalText({ ||  OemToAnsi(STR0016) + Eval(bTexto) + cQuebra } )
oSection11:SetTotalInLine(.F.)
oSection11:SetParentFilter(bFilter,bQuebra)

oSection1:Print()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFR090  ³ Autor ³ Wagner Xavier         ³ Data ³ 28.01.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Rela‡„o de Adiantamentos                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ATFR090                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	Last change:  CIA   4 Jan 96    2:11 pm
*/
Function AtfR090R3( )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cString := "SN3"
LOCAL cDesc1  := OemToAnsi(STR0001) // "Este programa ir  a rela‡„o dos bens adquiridos como"
LOCAL cDesc2  := OemToAnsi(STR0002) // "adiantamentos. Poder  ser listado por C¢digo, Conta, C.Custo,"
LOCAL cDesc3  := OemToAnsi(STR0003) // "Projeto ou Data de Aquisi‡„o."
LOCAL wnrel

PRIVATE aReturn  := { OemToAnsi(STR0014), 1,OemToAnsi(STR0015), 2, 2, 1, "",1 }
PRIVATE aLinha   := { }
PRIVATE cPerg    := "AFR090"
PRIVATE nomeprog := "ATFR090"
PRIVATE nLastKey := 0
PRIVATE tamanho := "G"
PRIVATE titulo := OemToAnsi(STR0004) // "Rela‡„o dos Adiantamentos"
PRIVATE cabec1
PRIVATE cabec2
pergunte("AFR090",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                    ³
//³ mv_par01         // a partir da data                    ³
//³ mv_par02         // ate a data                          ³
//³ mv_par03         // da conta                            ³
//³ mv_par04         // ate a conta                         ³
//³ mv_par05         // do centro de custo                  ³
//³ mv_par06         // ate o centro de custo               ³
//³ mv_par07         // do codigo                           ³
//³ mv_par08         // ate o codigo                        ³
//³ mv_par09         // do projeto                          ³
//³ mv_par10         // at‚ o projeto                       ³
//³ mv_par11         // moeda                               ³
//³ mv_par12         // Do Item contabil					³
//³ mv_par13         // Ate Item contabil					³
//³ mv_par14         // Da Classe de Valor					³
//³ mv_par15         // Ate a Classe de Valor				³
//³ mv_par16         // Considera Itens Baixados	  		³
//³ mv_par17         // Selec Classif Patrimonial?   		³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel := "ATFR090"
aOrd  := { 	OemToAnsi(STR0005),; // "Por C¢digo"
				OemToAnsi(STR0006),; //	"Por Conta"
				OemToAnsi(STR0007),; // "Por C.Custo"
				OemToAnsi(STR0008),; // "Projeto"
				OemToAnsi(STR0009),; // "Data
				STR0017 				,; // "Item Contabil"
				STR0018           }  // "Classe de Valor"
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,Tamanho)

If	nLastKey == 27
    Return
End

SetDefault(aReturn,cString)

If nLastKey == 27
   Return
Endif

RptStatus({|lEnd| FR090ImpT(@lEnd,wnRel,cString)},Titulo)

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ FR090IMPT³ Autor ³ Fabio Jadao Caires    ³ Data ³ 01.10.07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Rela‡„o de Adiantamentos R3 - Query                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ FR090IMPT(lEnd,wnRel,cString)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd    - A‡Æo do Codeblock                                ³±±
±±³          ³ wnRel   - T¡tulo do relat¢rio                              ³±±
±±³          ³ cString - Mensagem                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Pedro P Lima³11/12/07³137505³O campo "data de aquisicao" estava sendo  ³±±
±±³  TI6434    ³        ³      ³preenchido com o valor sem formato adequad³±±
±±³            ³        ³      ³retornando uma string ao inves de data.   ³±±
±±³            ³        ³      ³Foi tambem incluida alteracao referente ao³±±
±±³            ³        ³      ³BOPS 00000136837 versao 912.              ³±±
±±³            ³        ³      ³Olhar descricao do BOPS para maiores      ³±±
±±³            ³        ³      ³detalhes.                                 ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function FR090ImpT(lEnd,wnRel,cString)
    
LOCAL CbTxt
LOCAL cbCont
LOCAL limite    := 220
LOCAL nTotValor := 0
LOCAL nGerValor := 0
Local nIndex
Local nMoeda
Local bTexto 	:= { || "" }
Local lFirst

Local cQuery 	:= ""
Local aCposQry	:= {}
Local nX		:= 0
Local cFilterUser := aReturn[7]
Local aClassif := {}
Local cClassif := ""
Local lRealProv	:= .F.

Local aFldConv	:= {}

If Select("SN3") == 0
	DbSelect("SN3")
EndIf

lRealProv := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbtxt    := Space(10)
cbcont   := 0
cString  := "SN1"
li       := 80
m_pag    := 1

nOrdem  := aReturn[ 8 ]
nMoeda	:= mv_par11 // Alteracao conforme BOPS 66.785
cMoeda  := Str( nMoeda, 1 )
cArquivo:= "SN3"
cabec1  := OemToAnsi(STR0010) +AllTrim(GETMV("MV_SIMB"+cMoeda)) // "Valores em "
cabec2  := OemToAnsi(STR0011) //"Codigo      Item   Conta Contabil         C. Custo    Projeto      Data        Valor Adiantam.      Cta Deprec.            Conta Depr Acum        Conta Correcao       Item Contabil        Classe de Valor"
   

If mv_par17 == 1
	aClassif := AdmGetClass()	
EndIf

//                                                                                                     1                                                                                                   2
//           1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0         1         2
// 01234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^
//"Codigo      Item   Conta Contabil         C. Custo    Projeto      Data        Valor Adiantam.      Cta Deprec.            Conta Depr Acum        Conta Correcao       Item Contabil        Classe de Valor "
// XXXXXXXXXX  XXXX   XXXXXXXXXXXXXXXXXXXX   XXXXXXXXX   XXXXXXXXXX   99/99/9999  999.999.999.999,99   XXXXXXXXXXXXXXXXXXXX   XXXXXXXXXXXXXXXXXXXX   XXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXX

IF nOrdem = 4 .Or. nOrdem = 5

   IF nOrdem == 4
      	cCondicao1 := "TRB->N1_PROJETO <= mv_par10"
      	cCondicao2 := "TRB->N1_PROJETO"
   Else
		cCondicao1 := "TRB->N1_AQUISIC <= mv_par02"
		cCondicao2 := "TRB->N1_AQUISIC"
   EndIf

EndIf

IF nOrdem != 4 .And. nOrdem != 5

   IF nOrdem == 1
      cCondicao1 := "TRB->N3_CBASE <= mv_par08"
      cCondicao2 := "TRB->N3_CBASE"
      bTexto := { || STR0021 }  // " DO CODIGO BASE "

   Elseif nOrdem == 2
      cCondicao1 := "TRB->N3_CCONTAB <= mv_par04"
      cCondicao2 := "TRB->N3_CCONTAB"
      bTexto := { || STR0022 }  // " DA CONTA CONTABIL "

   Elseif nOrdem == 6
      cCondicao1 := "TRB->N3_SUBCTA  <= mv_par13"
      cCondicao2 := "TRB->N3_SUBCTA"
      bTexto := { || STR0023 }  // " DO ITEM CONTABIL "
      cabec2  := STR0019 // "Codigo      Item   Conta Contabil         C. Custo                Projeto                  Data            Valor Adiantam.     Cta Deprec.            Conta Depr Acum        Conta Correção Classe Valor"
      OemToAnsi(STR0011)

   Elseif nOrdem == 7
      cCondicao1 := "TRB->N3_CLVL  <= mv_par15"
      cCondicao2 := "TRB->N3_CLVL"
      bTexto := { || STR0024 } // " DA CLASSE DE VALOR "
      cabec2  := STR0020 // "Codigo      Item   Conta Contabil         C. Custo                Projeto                  Data            Valor Adiantam.     Cta Deprec.            Conta Depr Acum        Conta Correção    Item Contabil"

   Else
      cCondicao1 := "TRB->N3_CCUSTO  <= mv_par06"
      cCondicao2 := "TRB->N3_CCUSTO"
      bTexto := { || " DO CENTRO DE CUSTO " }
   EndIf
EndIf

cQuery := " SELECT "
cQuery += ATFFld2Str("SN3",.T.,aFldConv) + ", "
cQuery += ATFFld2Str("SN1",.T.,aFldConv,.f.)

cQuery += "FROM   "+RetSQLName("SN1")+" SN1 INNER JOIN "+RetSQLName("SN3")+" SN3 ON "
cQuery += "       SN1.N1_FILIAL  = SN3.N3_FILIAL AND "
cQuery += "       SN1.N1_CBASE   = SN3.N3_CBASE  AND "
cQuery += "       SN1.N1_ITEM    = SN3.N3_ITEM   AND "

If mv_par02 == mv_par01
	cQuery += "   SN1.N1_AQUISIC = '"+DTOS(mv_par01)+"' AND "
Else
	cQuery += "   SN1.N1_AQUISIC BETWEEN '"+DTOS(mv_par01)+"' AND '"+DTOS(mv_par02)+"' AND "
EndIf             

If mv_par10 == mv_par09
	cQuery += "   SN1.N1_PROJETO = '"+mv_par09+"' AND "
Else
	If Empty(mv_par09) .AND. ( Upper(mv_par10) == Replicate("Z",len(mv_par10)) .OR. ;
	                                  mv_par10  == Replicate("9",len(mv_par10)) )            
		cQuery += " "
	Else                           
		cQuery += " SN1.N1_PROJETO BETWEEN '"+mv_par09+"' AND '"+mv_par10+"' AND "
	EndIf
EndIf

//Filtra as classificações
If Len(aClassif) > 0 
	cQuery += " SN1.N1_PATRIM IN " + FORMATCLAS(aClassif,.T.) + " AND "+ CRLF
EndIf

cQuery += "        SN1.D_E_L_E_T_ = ' ' "



cQuery += "WHERE   SN3.N3_FILIAL  = '"+cFilial +"' AND "

If mv_par04 == mv_par03
	cQuery += "    SN3.N3_CCONTAB = '"+mv_par03+"' AND "
Else           
	If Empty(mv_par03) .AND. ( Upper(mv_par04) == Replicate("Z",len(mv_par04)) .OR. ;
	                                  mv_par04  == Replicate("9",len(mv_par04)) )  

		cQuery += " "
	Else	                                      
		cQuery += " SN3.N3_CCONTAB BETWEEN '"+mv_par03+"' AND '"+mv_par04+"' AND "
	EndIf
EndIf



If mv_par06 == mv_par05
	cQuery += "     SN3.N3_CCUSTO  = '"+ mv_par05 +"' AND "
Else              
	If Empty(mv_par05) .AND. ( Upper(mv_par06) == Replicate("Z",len(mv_par06)) .OR. ;
	                                  mv_par06  == Replicate("9",len(mv_par06)) )  

		cQuery += " "
	Else	                            
		cQuery += " SN3.N3_CCUSTO  BETWEEN '"+mv_par05+"' AND '"+mv_par06+"' AND "
	EndIf
EndIf



If mv_par08 == mv_par07
	cQuery += "     SN3.N3_CBASE  = '"+ mv_par07 +"' AND "
Else              
	If Empty(mv_par07) .AND. ( Upper(mv_par08) == Replicate("Z",len(mv_par08)) .OR. ;
	                                  mv_par08  == Replicate("9",len(mv_par08)) )  

		cQuery += " "
	Else	                            
		cQuery += " SN3.N3_CBASE BETWEEN '"+mv_par07+"' AND '"+mv_par08+"' AND "
	EndIf
EndIf



If mv_par13 == mv_par12
	cQuery += "     SN3.N3_SUBCTA  = '"+ mv_par12 +"' AND "
Else              
	If Empty(mv_par12) .AND. ( Upper(mv_par13) == Replicate("Z",len(mv_par13)) .OR. ;
	                                  mv_par13  == Replicate("9",len(mv_par13)) )  

		cQuery += " "
	Else	                            
		cQuery += " SN3.N3_SUBCTA BETWEEN '"+mv_par12+"' AND '"+mv_par13+"' AND "
	EndIf
EndIf



If mv_par15 == mv_par14
	cQuery += "     SN3.N3_CLVL  = '"+ mv_par14 +"' AND "
Else              
	If Empty(mv_par14) .AND. ( Upper(mv_par15) == Replicate("Z",len(mv_par15)) .OR. ;
	                                  mv_par15  == Replicate("9",len(mv_par15)) )  

		cQuery += " "
	Else	                            
		cQuery += " SN3.N3_CLVL BETWEEN '"+mv_par14+"' AND '"+mv_par15+"' AND "
	EndIf
EndIf



//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se considera itens baixados ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF mv_par16 == 2                                  
	cQuery += "     SN3.N3_BAIXA = '0' AND "
Endif


cQuery += "         SN3.N3_TIPO    =  '03' AND "
cQuery += "         SN3.D_E_L_E_T_ = ' ' "
                                                            
If lRealProv .AND. MV_PAR18 == 2
	cQuery += " AND SN3.N3_ATFCPR IN ('2',' ') "
EndIf

//Considera o Filtro do usuario
If ( !Empty(cFilterUser) )

	ATFAdjFil(@cFilterUser,.t.)

	cQuery += " AND " + cFilterUser

EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ordem 1 - Por C¢digo ; Ordem 2 - Por Conta; Ordem 3 - Por C. Custo  ³
//³ Ordem 4 - Por Projeto; Ordem 5 - Por Data (SN1)                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Do  Case
	Case nOrdem == 1  // Por Codigo    
		 cQuery += " ORDER BY SN3.N3_FILIAL, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO "

	Case nOrdem == 2  // Por Conta
		 cQuery += " ORDER BY SN3.N3_FILIAL, SN3.N3_CCONTAB, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO "
	
	Case nOrdem == 3  // Por Centro de custo
		 cQuery += " ORDER BY SN3.N3_FILIAL, SN3.N3_CCUSTO, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO "

	Case nOrdem == 4  // Por Projeto
		 cQuery += " ORDER BY SN3.N3_FILIAL, SN1.N1_PROJETO, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO "

	Case nOrdem == 5  // Por Data
		 cQuery += " ORDER BY SN3.N3_FILIAL, SN1.N1_AQUISIC, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO "

	Case nOrdem == 6  // Por Item Contabil
		 cQuery += " ORDER BY SN3.N3_FILIAL, SN3.N3_SUBCTA, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO "

	Case nOrdem == 7  // Por Classe de Valor
		 cQuery += " ORDER BY SN3.N3_FILIAL, SN3.N3_CLVL, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO "

EndCase


cQuery := ChangeQuery(cQuery)                     
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),"TRB", .F., .T.)

For nX := 1 to Len(aFldConv)
	
	If ( aFldConv[nX,2] <> "C" )
		TCSetField("TRB",aFldConv[nX,1],aFldConv[nX,2],aFldConv[nX,3])
	EndIf
		
Next nX

SetRegua(TRB->(RecCount()))

While !TRB->( Eof() ) .and. &cCondicao1

	cChaveAnterior := &cCondicao2
	lFirst := .T.

	While	!TRB->( Eof() ) .and. &cCondicao2 == cChaveAnterior
	
		IF	lEnd
			@PROW()+1,001 PSAY OemToAnsi(STR0012) // "CANCELADO PELO OPERADOR"
			Exit
		EndIf

		// Incrementa regua
		IncRegua()
        
		IF	li > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,IIF(aReturn[4]==1,15,18))
		EndIf

		If lFirst
			@li, 0 PSAY SubStr(Eval(bTexto), 5) + Transform(cChaveAnterior, "")
			li++
			li++
			lFirst := .F.
		Endif	
		
		@ li,  0 PSAY 	TRB -> N3_CBASE
		@ li, 12 PSAY	TRB -> N3_ITEM
		@ li, 19 PSAY	TRB -> N3_CCONTAB
		@ li, 42 PSAY 	TRB -> N3_CCUSTO
		@ li, 65 PSAY 	TRB -> N1_PROJETO
		@ li, 90 PSAY 	ATFCastType(TRB->N3_AQUISIC,"C","dd/mm/aaaa")
		
		If cMoeda = "1"
			@ li,102 PSAY 	&('TRB->N3_VORIG'+cMoeda)+&('TRB->N3_VRCACM'+cMoeda)+&('TRB->N3_AMPLIA'+cMoeda) PICTURE  PesqPict("SN3","N3_VORIG"+cMoeda,18,nMoeda)
		Else
			@ li,102 PSAY 	&('TRB->N3_VORIG'+cMoeda)+&('TRB->N3_AMPLIA'+cMoeda) PICTURE  PesqPict("SN3","N3_VORIG"+cMoeda,18,nMoeda)
		Endif	
		
		@ li,125 PSAY 	TRB -> N3_CDEPREC
		@ li,147 PSAY 	TRB -> N3_CCDEPR  
		@ li,170 PSAY 	TRB -> N3_CCORREC 
		
		// Ordem por item contabil, imprime a Classe de Valor
		If nOrdem == 6
			@ li,193 PSAY 	TRB -> N3_CLVL 
		// Ordem por classe de valor, imprime o item contabil
		ElseIf nOrdem == 7
			@ li,192 PSAY 	TRB -> N3_SUBCTA
		Else	
			// Senao, imprime as duas entidades
			@ li,193 PSAY 	TRB -> N3_CLVL 
			@ li,208 PSAY 	TRB -> N3_SUBCTA
		Endif
		
		li++
		
		nTotValor += &('TRB->N3_VORIG'+cMoeda)

		TRB->( dbSkip( ) )
	EndDo

	If	(nTotValor) != 0
		IF	(li+4) > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,IIF(aReturn[4]==1,15,18))
		EndIf
		li++                                        
		@li	, 0 	PSAY Replicate( "-",limite )		
		li++	
		@ li, 0 	PSAY OemToAnsi(STR0016) + Eval(bTexto) + Iif(ValType(cChaveAnterior)="D",DtoC(cChaveAnterior),cChaveAnterior)
		@ li, 79 	PSAY nTotValor	PICTURE PesqPict("TRB","N3_VORIG"+cMoeda, 18,nMoeda) 
		li++
		@li	, 0 	PSAY Replicate( "-",limite )
		li++
		li++
		nGerValor += nTotValor
		nTotValor := 0
	EndIf
EndDo

IF li !=  80

	IF	(li+4) > 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,IIF(aReturn[4]==1,15,18))
	EndIf
	
	li+=2
	
	@ li, 0 PSAY Replicate( "-",limite )			
	
	li++
	
	@ li, 0 PSAY OemToAnsi(STR0013) // "TOTAL GERAL: " 
	@ li,79 PSAY nGerValor	PICTURE PesqPict("TRB","N3_VORIG"+cMoeda, 18, nMoeda ) 

	li++                                               

	@ li, 0	PSAY Replicate( "-",limite )				

	Roda( cbcont,cbtxt ,Tamanho)
    
EndIf


dbSelectArea( "TRB" )
DbCloseArea()


dbSelectArea( "SN3" )
dbSetOrder( 1 )

If	aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	Ourspool( wnrel )
End


MS_FLUSH()