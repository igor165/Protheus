#INCLUDE "ATFR180.CH"
#Include "Protheus.ch"

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.
Static lFWCodFil := .T.

// TRADUCAO DE CH'S PARA PORTUGAL
// TRADUCAO DE CH'S PARA PORTUGAL - 21/07/08

/*
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo    ?ATFR180    ?Autor ?Mario Angelo          ?Data ?30.04.96 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio ?Relatorio de Bens Totalmente Depreciados                     潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso      ?SIGAATF                                                      潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
*/
Function ATFR180()
Local oReport

PRIVATE cPerg    := PadR("ATR180", Len(SX1->X1_GRUPO) )

If TRepInUse()
	oReport:=ReportDef()
	oReport:PrintDialog()
Else
   Return ATFR180R3() // Executa verso anterior do relatorio
Endif

/*/
苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Programa  ReportDef ?Autor Claudio D. de Souza    ?Data ?5/06/2006潮?
北媚哪哪哪哪呐哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio A funcao estatica ReportDef devera ser criada para todos os 潮?
北?         relatorios que poderao ser agendados pelo usuario.          潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Retorno   ExpO1: Objeto do relatrio                                  潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北ParametrosNenhum                                                      潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪穆哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?  DATA   ?Programador   Manutencao efetuada                         潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?         ?              ?                                           潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
/*/
Static Function ReportDef()
Local oReport,oSection1
Local cReport := "ATFR180"
Local cAlias1 := "SN3"
Local cAlias2 := "SN1"
Local cTitulo := OemToAnsi(STR0003) // "Bens Depreciados"
Local cDescri := OemToAnsi(STR0001)+" "+OemToAnsi(STR0002) // "Este programa ir?emitir a relao dos Bens que j?  // "foram totalmente depreciados por %"
Local oSecFil	:= Nil

Pergunte( cPerg , .F. )
                   
oReport  := TReport():New( cReport, cTitulo, "ATR180" , { |oReport| ReportPrint( oReport, cAlias1, cAlias2 ) }, cDescri )

oReport:SetUseGC(.F.)

//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪目
//?Define a 1a. secao do relatorio Valores nas Moedas   ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪馁
oSection1 := TRSection():New( oReport, STR0004, {cAlias1,cAlias2} )
oSection1:SetAutoSize(.T.)

TRCell():New( oSection1, "N3_CBASE"   , cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Codigo
TRCell():New( oSection1, "N3_ITEM"    , cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Item
TRCell():New( oSection1, "N3_TIPO"    , cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Item
TRCell():New( oSection1, "N3_TPSALDO" , cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Item
TRCell():New( oSection1, "N1_DESCRIC" , cAlias2 ,/*X3Titulo*/     ,/*Picture*/,,,,,.T.)	// Descricao
TRCell():New( oSection1, "N3_VORIG1"  , cAlias1 ,GetMV("MV_SIMB1"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Moeda 01
TRCell():New( oSection1, "N3_VORIG2"  , cAlias1 ,GetMV("MV_SIMB2"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Moeda 02
TRCell():New( oSection1, "N3_VORIG3"  , cAlias1 ,GetMV("MV_SIMB3"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Moeda 03
TRCell():New( oSection1, "N3_VORIG4"  , cAlias1 ,GetMV("MV_SIMB4"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Moeda 04
TRCell():New( oSection1, "N3_VORIG5"  , cAlias1 ,GetMV("MV_SIMB5"),/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Moeda 05
TRCell():New( oSection1, "N3_VRCACM1" , cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Correcao Acumulada
TRCell():New( oSection1, "N3_VRDACM1" , cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Depr Acumulada
TRCell():New( oSection1, "N3_VRCDA1"  , cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Corr Depr Acumul
TRCell():New( oSection1, "N3_AMPLIA1" , cAlias1 ,/*X3Titulo*/     ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Vlr Ampliacao

/* Relacao das filiais selecionadas para compor o relatorio */
oSecFil := TRSection():New(oReport,"SECFIL",{"SN3"})
TRCell():New(oSecFil,"CODFIL" ,,STR0021,/*Picture*/,30,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Cdigo"
TRCell():New(oSecFil,"EMPRESA",,STR0022,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Empresa"
TRCell():New(oSecFil,"UNIDNEG",,STR0023,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Unidade de negcio"	
TRCell():New(oSecFil,"NOMEFIL",,STR0024,/*Picture*/,60,/*lPixel*/,/*{|| code-block de impressao }*/)			//"Filial"

oReport:SetLandScape()
oReport:DisableOrientation()

Return oReport

/*
苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
北赏屯屯屯屯脱屯屯屯屯屯退屯屯屯脱屯屯屯屯屯屯屯屯屯屯送屯屯脱屯屯屯屯屯屯突北
北Programa  ReportPrintAutor  Claudio D. de Souza ?Data ? 05/06/06   罕?
北掏屯屯屯屯拓屯屯屯屯屯褪屯屯屯拖屯屯屯屯屯屯屯屯屯屯释屯屯拖屯屯屯屯屯屯凸北
北Desc.     Query de impressao do relatorio                              罕?
北掏屯屯屯屯拓屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯凸北
北Uso       ?SIGAATF                                                     罕?
北韧屯屯屯屯拖屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯图北
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
*/
Static Function ReportPrint( oReport, cAlias1, cAlias2 )
Local oSection1 := oReport:Section(1)
Local cQuery  := "SN3"
Local cAliasSn1 := "SN1"
Local cFiltro := ""
Local cChave  := ""
Local cWhere  := ""		
Local cMoeda  := GetMv("MV_ATFMOED")
Local cN1TipoNeg := Alltrim(SuperGetMv("MV_N1TPNEG",.F.,"")) // Tipos de N1_PATRIM que aceitam Valor originais negativos
Local cN3TipoNeg := Alltrim(SuperGetMv("MV_N3TPNEG",.F.,"")) // Tipos de N3_TIPO que aceitam Valor originais negativos
Local cFilterUser:= ""
Local aClassif := {}
Local cClassif := ""
Local lAtfCusPrv := AFXAtCsPrv()
Local aSelFil	:= {}
Local aTmpFil	:= {}
Local cTmpFil	:= ""
Local cFilSN	:= ""
Local lSelFil	:= .F.

//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
//?Variaveis utilizadas para parametros       		?          
//?mv_par01            Do Bem                 		?
//?mv_par02            Ate o Bem              		?
//?mv_par03            Moeda 1                		?
//?mv_par04            Moeda 2                		?
//?mv_par05            Moeda 3                		?
//?mv_par06            Moeda 4                		?
//?mv_par07            Moeda 5                		?
//?mv_par08            Cons. filiais abaixo ? 		?
//?mv_par09                                   		?
//?mv_par10            ZZ                     		?
//?mv_par11            Selec Classif Patrimonial?	? 	  
//?mv_par12            Exibir Ativos Real. Provis?	?	  
//?mv_par13            Seleciona filiais ?			?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪? 

If mv_par11 == 1
	aClassif := AdmGetClass() 
	If Len( aClassif ) <= 0
		Return
	EndIf 			
EndIf

	lSelFil := (FwSizeFilial() > 2)
	If lSelFil
		If MV_PAR13 == 1 
			AdmSelecFil("ATR180",13,.F.,@aSelFil,"SN3",.F.)
		Endif
		If Empty(aSelFil)
			lSelFil := .F.
		Endif
	Endif

	cQuery := GetNextAlias()
	cAliasSn1 := cQuery
	cChave		:= "%" + SqlOrder( SN3->( IndexKey( 1 ) ) ) + "%"
	cWhere		:= "%"
			
	If lSelFil
		MsgRun(STR0026,STR0003,{|| cFilSN := GetRngFil(aSelFil,"SN3",.T.,@cTmpFil)}) //"Favor Aguardar..."###"Bens Depreciados"
		Aadd(aTmpFil,cTmpFil)
		cWhere += "SN3.N3_FILIAL " + cFilSN + " AND "
	Else
		If mv_par08 == 2
			cWhere += "SN3.N3_FILIAL = '" + xFilial("SN3") + "' AND "
		Else
			cWhere += "SN3.N3_FILIAL between '" + mv_par09 + "' AND '" + mv_par10 + "' AND "
		Endif
	Endif
	
	If Upper(AllTrim(mv_par01)) == Upper(AllTrim(mv_par02))
		cWhere += "SN3.N3_CBASE = '" + mv_par01 + "' AND "
	Else	
		cWhere += "SN3.N3_CBASE BETWEEN '" + mv_par01 + "' AND '" + mv_par02 + "' AND "
	EndIf
	cWhere += "SN3.N3_BAIXA = '0' AND "
	cWhere += "( SN3.N3_CDEPREC != ' ' OR SN3.N3_CDESP != ' ' OR SN3.N3_CCDEPR != ' ' ) AND "
	cWhere += "SN3.D_E_L_E_T_ = ' ' AND "
	cWhere += "SN1.N1_FILIAL = SN3.N3_FILIAL AND "
	
	//Filtra as classifica珲es
	If Len(aClassif) > 0 
			cWhere += " SN1.N1_PATRIM IN " + FORMATCLAS(aClassif,.T.) + " AND "
	EndIf
	
	If lAtfCusPrv .And. MV_PAR12 == 2
		cWhere += " SN3.N3_ATFCPR <> '1' AND "
	EndIf 
	
	cWhere += "SN1.N1_CBASE = SN3.N3_CBASE AND "
	cWhere += "SN1.N1_ITEM = SN3.N3_ITEM AND "

	//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪目
	//?Considera filtro do usuario                                  ?
	//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪馁
	cFilterUser := oSection1:GetSqlExp()
	If !Empty(cFilterUser)
		cWhere += cFilterUser + " AND "
	EndIf

	cWhere += "SN1.D_E_L_E_T_ = ' ' "
	cWhere += "%"

	oSection1:BeginQuery()

	BeginSql Alias cQuery
		SELECT
			SN3.N3_FILIAL, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TXDEPR1, SN3.N3_TIPO,
			SN3.N3_VORIG1, SN3.N3_VORIG2, SN3.N3_VORIG3, SN3.N3_VORIG4, SN3.N3_VORIG5,
			SN3.N3_VRCACM1, SN3.N3_VRDACM1, SN3.N3_VRDACM2, SN3.N3_VRDACM3, SN3.N3_VRDACM4, 
			SN3.N3_VRDACM5, SN3.N3_VRCDA1, SN3.N3_AMPLIA1, SN3.N3_AMPLIA2, SN3.N3_AMPLIA3,
			SN3.N3_AMPLIA4,	SN3.N3_AMPLIA5, SN1.N1_DESCRIC, SN1.N1_PATRIM,SN3.N3_TPSALDO
	 
		FROM 
			%table:SN3% SN3, %table:SN1% SN1
		
		WHERE 
			%Exp:cWhere%

		ORDER BY %Exp:cChave%
	EndSql

	oSection1:EndQuery()

oSection1:Cell("N3_VORIG1"):SetBlock( {|| (cQuery)->(N3_VORIG1+N3_AMPLIA1) })	// Moeda 01
oSection1:Cell("N3_VORIG2"):SetBlock( {|| (cQuery)->(N3_VORIG2+N3_AMPLIA2) })	// Moeda 02
oSection1:Cell("N3_VORIG3"):SetBlock( {|| (cQuery)->(N3_VORIG3+N3_AMPLIA3) })	// Moeda 03
oSection1:Cell("N3_VORIG4"):SetBlock( {|| (cQuery)->(N3_VORIG4+N3_AMPLIA4) })	// Moeda 04
oSection1:Cell("N3_VORIG5"):SetBlock( {|| (cQuery)->(N3_VORIG5+N3_AMPLIA5) })	// Moeda 05

oReport:SetTotalInLine(.F.)

If	mv_par03 != 1
	oSection1:Cell("N3_VORIG1"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG1") , , "SUM" , , , , , .F. ,  )
EndIf
If	mv_par04 != 1
	oSection1:Cell("N3_VORIG2"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG2") , , "SUM" , , , , , .F. ,  )
EndIf
If	mv_par05 != 1
	oSection1:Cell("N3_VORIG3"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG3") , , "SUM" , , , , , .F. ,  )
EndIf
If	mv_par06 != 1
	oSection1:Cell("N3_VORIG4"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG4") , , "SUM" , , , , , .F. ,  )
EndIf
If	mv_par07 != 1
	oSection1:Cell("N3_VORIG5"):Disable()
Else
	TRFunction():New ( oSection1:Cell("N3_VORIG5") , , "SUM" , , , , , .F. ,  )
EndIf

TRFunction():New ( oSection1:Cell("N3_VRCACM1") , , "SUM" , , , , , .F. ,  )
TRFunction():New ( oSection1:Cell("N3_VRDACM1") , , "SUM" , , , , , .F. ,  )
TRFunction():New ( oSection1:Cell("N3_VRCDA1" ) , , "SUM" , , , , , .F. ,  )
TRFunction():New ( oSection1:Cell("N3_AMPLIA1") , , "SUM" , , , , , .F. ,  )

oReport:Section(1):SetLineCondition({|| AfrSaldo(cQuery,cMoeda,cAliasSn1,cN1TipoNeg,cN3TipoNeg) <= 0 })

If lSelFil .And. Len(aSelFil) > 1
	oSection1:OnPrintLine({|| AFR180Fil(@aSelFil,oReport)})
Endif

oSection1:Print()

If !Empty(aTmpFil)
	MsgRun(STR0026,STR0003,{|| Aeval(aTmpFil,{|cTmpFil| CtbTmpErase(cTmpFil)})}) //"Favor Aguardar..."###"Bens Depreciados"
Endif

Return

/*
苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
北赏屯屯屯屯脱屯屯屯屯屯退屯屯屯脱屯屯屯屯屯屯屯屯屯屯送屯屯脱屯屯屯屯屯屯突北
北Programa  AFR180FIL  Autor  ?                   ?Data ?24/01/2014  罕?
北掏屯屯屯屯拓屯屯屯屯屯褪屯屯屯拖屯屯屯屯屯屯屯屯屯屯释屯屯拖屯屯屯屯屯屯凸北
北Desc.     Relacao das filiais selecionadas para o relatorio            罕?
北掏屯屯屯屯拓屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯凸北
北Uso       ?SIGAATF                                                     罕?
北韧屯屯屯屯拖屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯图北
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
*/
Function AFR180Fil(aSelFil,oReport)
Local oSecFil	:= Nil
Local aSM0		:= {}
Local nRegSM0	:= 0
Local nTamEmp	:= 0
Local nTamUnNeg	:= 0
Local nTamTit	:= 0
Local nX		:= 0
Local nLinha	:= 0
Local cTitulo 	:= ""

oReport:Section(1):OnPrintLine({|| })
oSecFil := oReport:Section("SECFIL")
nRegSM0 := SM0->(Recno())
aSM0 := FWLoadSM0()
SM0->(DbGoTo(nRegSM0))
nTamEmp := Len(FWSM0LayOut(,1))
nTamUnNeg := Len(FWSM0LayOut(,2))
cTitulo := oReport:Title()
oReport:SetTitle(cTitulo + " (" + STR0025 + ")")		//"Filiais selecionadas para o relatorio"
nTamTit := Len(oReport:Title())
oSecFil:Init()
oSecFil:Cell("CODFIL"):SetBlock({||cFilSel})
oSecFil:Cell("EMPRESA"):SetBlock({||aSM0[nLinha,SM0_DESCEMP]})
oSecFil:Cell("UNIDNEG"):SetBlock({||aSM0[nLinha,SM0_DESCUN]})
oSecFil:Cell("NOMEFIL"):SetBlock({||aSM0[nLinha,SM0_NOMRED]})
For nX := 1 To Len(aSelFil)
	nLinha := Ascan(aSM0,{|sm0| sm0[SM0_GRPEMP] == cEmpAnt .And. sm0[SM0_CODFIL] == aSelFil[nX]})
	If nLinha > 0
		cFilSel := Substr(aSM0[nLinha,SM0_CODFIL],1,nTamEmp)
		cFilSel += " "
		cFilSel += Substr(aSM0[nLinha,SM0_CODFIL],nTamEmp + 1,nTamUnNeg)
		cFilSel += " "
		cFilSel += Substr(aSM0[nLinha,SM0_CODFIL],nTamEmp + nTamUnNeg + 1)
		oSecFil:PrintLine()
	Endif
Next
oReport:SetTitle(cTitulo)
oSecFil:Finish()
oReport:EndPage()
Return()

/*
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo    ?AfrSaldo   ?Autor ?Claudio Donizete      ?Data ?06.06.06 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio ?REtorna o saldo para impressao ou nao do bem                 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso      ?SIGAATF                                                      潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
*/
Static Function AfrSaldo(cAliasSn3,cMoeda,cAliasSn1,cN1TipoNeg,cN3TipoNeg)
Local nSaldo := 0
	If cMoeda == "1"
		If (cAliasSn3)->N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. (cAliasSn3)->N3_TIPO $ cN3TipoNeg
			nSaldo := (cAliasSn3)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1+Abs(N3_VRDACM1+N3_VRCDA1))
		Else
			nSaldo := (cAliasSn3)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1)-((cAliasSn3)->N3_VRDACM1+(cAliasSn3)->N3_VRCDA1)
		Endif
	Else
		If (cAliasSn3)->N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. (cAliasSn3)->N3_TIPO $ cN3TipoNeg
			If cMoeda == "2"
				nSaldo := (cAliasSn3)->( N3_VORIG2 + N3_AMPLIA2 + Abs(N3_VRDACM2) )
			ElseIf cMoeda == "3"
				nSaldo := (cAliasSn3)->( N3_VORIG3 + N3_AMPLIA3 + Abs(N3_VRDACM3) )
			ElseIf cMoeda == "4"
				nSaldo := (cAliasSn3)->( N3_VORIG4 + N3_AMPLIA4 + Abs(N3_VRDACM4) )
			ElseIf cMoeda == "5"
				nSaldo := (cAliasSn3)->( N3_VORIG5 + N3_AMPLIA5 + Abs(N3_VRDACM5) )
			EndIf	
		Else       
			If cMoeda == "2"
				nSaldo :=(cAliasSn3)->( N3_VORIG2 + N3_AMPLIA2 - N3_VRDACM2 )
			ElseIf cMoeda == "3"
				nSaldo :=(cAliasSn3)->( N3_VORIG3 + N3_AMPLIA3 - N3_VRDACM3 )
			ElseIf cMoeda == "4"
				nSaldo :=(cAliasSn3)->( N3_VORIG4 + N3_AMPLIA4 - N3_VRDACM4 )
			ElseIf cMoeda == "5"
				nSaldo :=(cAliasSn3)->( N3_VORIG5 + N3_AMPLIA5 - N3_VRDACM5 )
			EndIf					
		Endif
	Endif
Return nSaldo
	
//----------------------------------------------------RELEASE3----------------------------------------------------//
	
/*
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo    ?ATFR180    ?Autor ?Mario Angelo          ?Data ?30.04.96 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio ?Relatorio de Bens Totalmente Depreciados - R3                潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso      ?SIGAATF                                                      潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
*/
Static Function ATFR180R3()

//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪目
//?Define Variaveis                                             ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪馁

LOCAL cString 	:= "SN3"
LOCAL cDesc1    := OemToAnsi(STR0001) // "Este programa ir?emitir a relao dos Bens que j?
LOCAL cDesc2    := OemToAnsi(STR0002) // "foram totalmente depreciados"
LOCAL cDesc3    := ""
LOCAL wnrel

PRIVATE aReturn  := { "Zebrado", 1,"Administracao", 2, 2, 1, "",1 }
PRIVATE aLinha   := { }
PRIVATE nomeprog := "ATFR180"
PRIVATE nLastKey := 0
PRIVATE tamanho  := "M"
PRIVATE titulo  := OemToAnsi(STR0003) // "Bens Depreciados"
PRIVATE cabec1  := OemToAnsi(STR0004) // "Saldos Depreciados por Bens"
PRIVATE cabec2  := OemToAnsi(STR0005) // "Codigo  Descricao"
PRIVATE nTamanho

//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
//?Variaveis utilizadas para parametros       		?
//?mv_par01            Do Bem                 		?
//?mv_par02            Ate o Bem              		?
//?mv_par03            Moeda 1                		?
//?mv_par04            Moeda 2                		?
//?mv_par05            Moeda 3                		?
//?mv_par06            Moeda 4                		?
//?mv_par07            Moeda 5                		?
//?mv_par08            Cons. filiais abaixo ? 		?
//?mv_par09                                   		?
//?mv_par10            ZZ                     		?
//?mv_par11            Selec Classif Patrimonial?	? 	 
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪? 



pergunte(cPerg,.F.)

wnrel := "ATFR180"
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,,,Tamanho)

If nLastKey == 27
	Return
End

SetDefault( aReturn,cString )

nTamanho:=IIf(aReturn[4]==1,15,18)

cMoeda1 := AllTrim(GetMV("MV_SIMB1"))   // Simbolo da moeda vigente (R$)
cMoeda2 := AllTrim(GetMV("MV_SIMB2"))   // Simbolo da moeda 2 (ex:US$)
cMoeda3 := AllTrim(GetMV("MV_SIMB3"))   // Simbolo da moeda 3
cMoeda4 := AllTrim(GetMV("MV_SIMB4"))
cMoeda5 := AllTrim(GetMV("MV_SIMB5"))

cMoeda1 := Space(19 - Len(cMoeda1) ) + cMoeda1
cMoeda2 := Space(18 - Len(cMoeda2) ) + cMoeda2
cMoeda3 := Space(18 - Len(cMoeda3) ) + cMoeda3
cMoeda4 := Space(18 - Len(cMoeda4) ) + cMoeda4
cMoeda5 := Space(18 - Len(cMoeda5) ) + cMoeda5

cabec1  := OemToAnsi(STR0006) + Space(09) // "Codigo         Descricao" 
cabec1  +=  IIf(mv_par03 == 1,cMoeda1,"" )
cabec1  +=  IIf(mv_par04 == 1,cMoeda2,"" )
cabec1  +=  IIf(mv_par05 == 1,cMoeda3,"" )
cabec1  +=  IIf(mv_par06 == 1,cMoeda4,"" )
cabec1  +=  IIf(mv_par07 == 1,cMoeda5,"" )
cabec2  := ""

If nLastKey == 27
   Return                                               
Endif

RptStatus({|lEnd| FR180Imp(@lEnd,wnRel,cString)},Titulo)

Return

/*
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo    FR180Imp  ?Autor ?Mario Angelo          ?Data ?30/04/96 潮?
北媚哪哪哪哪呐哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio ?Imprime as Etiquetas dentro do padrao                      潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Sintaxe e FR180IMP(cAlias,nReg,nOpc)                                  潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Parametros?ExpC1 = Alias do arquivo                                   潮?
北?         ?ExpN1 = Numero do registro                                 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso      ?Generico                                                   潮?
北媚哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?        ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             潮?
北媚哪哪哪哪哪穆哪哪哪哪履哪哪穆哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Programador ?Data   ?BOPS ? Motivo da Alteracao                     潮?
北媚哪哪哪哪哪呐哪哪哪哪拍哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北C. Denardi  ?6/10/03?6.785Localizacao de casas decimais na Picture  潮?
北?           ?       ?     em todos os relatorios do Ativo Fixo      潮?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/

Static Function FR180Imp(lEnd,wnRel,cString)

LOCAL CbTxt
LOCAL cbCont

LOCAL nTotal1 := 0, nTotal2 := 0, nTotal3 := 0, nTotal4 := 0, nTotal5 := 0
LOCAL nTotalCorrAc := 0, nTotalDeprAc := 0, nTotalCrDpAc := 0, nTotalAmplia := 0
LOCAL nSaldo1 := 0, nSaldo2 := 0, nSaldo3 := 0, nSaldo4 := 0, nSaldo5 := 0
LOCAL cMoeda1 := 0, cMoeda2 := 0, cMoeda3 := 0, cMoeda4 := 0, cMoeda5 := 0

Local cN1TipoNeg 	:= Alltrim(SuperGetMv("MV_N1TPNEG",.F.,"")) // Tipos de N1_PATRIM que aceitam Valor originais negativos
Local cN3TipoNeg 	:= Alltrim(SuperGetMv("MV_N3TPNEG",.F.,"")) // Tipos de N3_TIPO que aceitam Valor originais negativos
Local cMoeda  		:= GetMv("MV_ATFMOED")
Local cQuery  		:= ""
Local cAliasQry		:= "TRBN3"                            
Local cAliasSN1	 	:= "SN1"
Local cAliasSN3		:= "SN3"
Local cFilSN3			:= Iif(mv_par08 == 2,xFilial("SN3"),"")

Local cPicVRCACM1	:= PesqPict("SN3","N3_VRCACM1",18,1)
Local cPicVRDACM1	:= PesqPict("SN3","N3_VRDACM1",18,1)
Local cPicVRCDA1	:= PesqPict("SN3","N3_VRCDA1",18,1)
Local cPicAMPLIA1	:= PesqPict("SN3","N3_AMPLIA1",18,1)

Local cPicVORIG1	:= PesqPict("SN3","N3_VORIG1",18,1)
Local cPicVORIG2	:= PesqPict("SN3","N3_VORIG2",18,2)
Local cPicVORIG3	:= PesqPict("SN3","N3_VORIG3",18,3)
Local cPicVORIG4	:= PesqPict("SN3","N3_VORIG4",18,4)
Local cPicVORIG5	:= PesqPict("SN3","N3_VORIG5",18,5)

Local lMoeda1		:= (mv_par03 == 1)
Local lMoeda2		:= (mv_par04 == 1)
Local lMoeda3		:= (mv_par05 == 1)
Local lMoeda4		:= (mv_par06 == 1)
Local lMoeda5		:= (mv_par07 == 1)

Local aArea			:= GetArea()

Local cFilterUser	:= aReturn[7] // Filtro definido pela SetPrint    

Local aClassif := {}
Local cClassif := ""

Local lAtfCusPrv := AFXAtCsPrv()

//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪目
//?Variaveis utilizadas para Impressao do Cabecalho e Rodape    ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪馁
cbTxt   := SPACE(10)
cbCont  := 0
li      := 80
m_pag   := 1

cMoeda1 := AllTrim(GetMV("MV_SIMB1"))
cMoeda2 := AllTrim(GetMV("MV_SIMB2"))
cMoeda3 := AllTrim(GetMV("MV_SIMB3"))
cMoeda4 := AllTrim(GetMV("MV_SIMB4"))
cMoeda5 := AllTrim(GetMV("MV_SIMB5"))

cMoeda1 := Spac(19 - Len(cMoeda1) ) + cMoeda1
cMoeda2 := Spac(18 - Len(cMoeda2) ) + cMoeda2
cMoeda3 := Spac(18 - Len(cMoeda3) ) + cMoeda3
cMoeda4 := Spac(18 - Len(cMoeda4) ) + cMoeda4
cMoeda5 := Spac(18 - Len(cMoeda5) ) + cMoeda5

cabec1  :=  OemToAnsi(STR0007) + Space(08) // "Codigo     Item Descricao"
cabec1  +=  IIf(lMoeda1,cMoeda1,"" )
cabec1  +=  IIf(lMoeda2,cMoeda2,"" )
cabec1  +=  IIf(lMoeda3,cMoeda3,"" )
cabec1  +=  IIf(lMoeda4,cMoeda4,"" )
cabec1  +=  IIf(lMoeda5,cMoeda5,"" )
cabec2  := OemToAnsi(STR0008) // "                                  Correcao Acumulada    Depr Acumulada  Corr Depr Acumul"
//           012345678901234567890123456789012345678901234567890123456789012345678901234567890
nTotal1 := nTotal2 := nTotal3 := nTotal4 := nTotal5 := 0
nSaldo1 := nSaldo2 := nSaldo3 := nSaldo4 := nSaldo5 := 0

If mv_par11 == 1
	aClassif := AdmGetClass()	
EndIf

	cQuery := "SELECT  "
	cQuery += "SN3.N3_FILIAL, SN3.N3_CBASE, SN3.N3_BAIXA, SN3.N3_ITEM, SN3.N3_TIPO, SN3.N3_BAIXA, SN3.N3_SEQ, SN3.N3_TXDEPR1, "
	cQuery += "SN3.N3_VORIG1, SN3.N3_VORIG2, SN3.N3_VORIG3, SN3.N3_VORIG4, SN3.N3_VORIG5, SN3.N3_VRCACM1, SN3.N3_VRDACM1, "
	cQuery += "SN3.N3_VRDACM2, SN3.N3_VRDACM3, SN3.N3_VRDACM4, SN3.N3_VRDACM5, SN3.N3_VRCDA1, SN3.N3_AMPLIA1, SN3.N3_AMPLIA2, "
	cQuery += "SN3.N3_AMPLIA3, SN3.N3_AMPLIA4, SN3.N3_AMPLIA5, SN1.N1_FILIAL, SN1.N1_CBASE, SN1.N1_ITEM, SN1.N1_DESCRIC, SN1.N1_PATRIM,SN3.N3_TPSALDO "
	cQuery += "FROM " + RetSqlName("SN3") + " SN3, " + RetSqlName("SN1") + " SN1 "
	cQuery += "WHERE "

	If mv_par08 == 2
		cQuery += "SN3.N3_FILIAL = '" + xFilial("SN3") + "' AND "
	Else
		cQuery += "SN3.N3_FILIAL between '" + mv_par09 + "' AND '" + mv_par10 + "' AND "
	Endif

	If Upper(AllTrim(mv_par01)) == Upper(AllTrim(mv_par02))
		cQuery += "SN3.N3_CBASE = '" + mv_par01 + "' AND "
	Else	
		cQuery += "SN3.N3_CBASE BETWEEN '" + mv_par01 + "' AND '" + mv_par02 + "' AND "
	EndIf
	cQuery += "SN3.N3_BAIXA = '0' AND "
	cQuery += "( SN3.N3_CDEPREC != ' ' OR SN3.N3_CDESP != ' ' OR SN3.N3_CCDEPR != ' ' ) AND "
	cQuery += "SN3.D_E_L_E_T_ = ' ' AND "
	cQuery += "SN1.N1_FILIAL = SN3.N3_FILIAL AND "
	
	//Filtra as classifica珲es
	If Len(aClassif) > 0 
		cQuery += " SN1.N1_PATRIM IN " + FORMATCLAS(aClassif,.T.) + " AND "
	EndIf
	
	If lAtfCusPrv .And. MV_PAR12 == 2
		cQuery += " SN3.N3_ATFCPR <> '1' AND "
	EndIf 

	cQuery += "SN1.N1_CBASE = SN3.N3_CBASE AND "
	cQuery += "SN1.N1_ITEM = SN3.N3_ITEM AND "
	cQuery += "SN1.D_E_L_E_T_ = ' ' "

	//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪目
	//?Se houver filtro executa parse para converter expressoes adv para SQL    ?
	//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪馁
	If ! Empty(cFilterUser)
		cQuery += " AND ( " + ADMParSQL(cFilterUser) + " ) "
	Endif

	cQuery += "ORDER BY SN3.N3_FILIAL, SN3.N3_CBASE, SN3.N3_ITEM, SN3.N3_TIPO, SN3.N3_BAIXA, SN3.N3_SEQ "
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.T.,.T.)
	cAliasSN1 := cAliasQry
	cAliasSN3 := cAliasQry              
	SetRegua(SN1->(LastRec()))

Do While (cAliasSN3)->( !Eof() ).and. Iif(mv_par08 == 2,(cAliasSN3)->N3_FILIAL == cFilSN3,;
			((cAliasSN3)->N3_FILIAL >= mv_par09 .Or. (cAliasSN3)->N3_FILIAL <= mv_par10)) 

	// Incrementa Regua
	IncRegua()

	// Verifica se ja foi Totalmente depreciado
	If AfrSaldo( cAliasSN3, cMoeda, cAliasSN1, cN1TipoNeg, cN3TipoNeg ) > 0
		(cAliasSN3)->(dbSkip())
		Loop
	EndIf

	If lEnd
     	@PROW()+1,001 PSAY OemToAnsi(STR0009) // "CANCELADO PELO OPERADOR"
    	Exit
	EndIf

	If li > 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTamanho)
	EndIf
	
	nTotal1 += (cAliasSN3)->(N3_VORIG1+N3_AMPLIA1)
	nTotal2 += (cAliasSN3)->(N3_VORIG2+N3_AMPLIA2)
	nTotal3 += (cAliasSN3)->(N3_VORIG3+N3_AMPLIA3)
	nTotal4 += (cAliasSN3)->(N3_VORIG4+N3_AMPLIA4)
	nTotal5 += (cAliasSN3)->(N3_VORIG5+N3_AMPLIA5)
        
	nTotalCorrAc += (cAliasSN3)->N3_VRCACM1
	nTotalDeprAc += (cAliasSN3)->N3_VRDACM1
	nTotalCrDpAc += (cAliasSN3)->N3_VRCDA1
	nTotalAmplia += (cAliasSN3)->N3_AMPLIA1

	//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
	//?Impresso dos dados em questo  ?
	//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
	@li,00 PSAY (cAliasSN3)->N3_CBASE + " "  + (cAliasSN3)->N3_ITEM + " "+ Left((cAliasSN1)->N1_DESCRIC,17) + " "
	If	lMoeda1
		@Prow(),Pcol() PSAY (cAliasSN3)->(N3_VORIG1+N3_AMPLIA1) 	Picture cPicVORIG1
	Endif
	If	lMoeda2
		 @Prow(),Pcol() PSAY (cAliasSN3)->(N3_VORIG2+N3_AMPLIA2) 	Picture cPicVORIG2
	Endif
	If	lMoeda3
		 @Prow(),Pcol() PSAY (cAliasSN3)->(N3_VORIG3+N3_AMPLIA3) 	Picture cPicVORIG3
	Endif
	If	lMoeda4
		@Prow(),Pcol() PSAY (cAliasSN3)->(N3_VORIG4+N3_AMPLIA4) 	Picture cPicVORIG4
	Endif
	If 	lMoeda5
		@Prow(),Pcol() PSAY (cAliasSN3)->(N3_VORIG5+N3_AMPLIA5) 	Picture cPicVORIG5
	Endif
	Li ++
	@Li,034 PSAY (cAliasSN3)->N3_VRCACM1 Picture cPicVRCACM1
	@Li,052 PSAY (cAliasSN3)->N3_VRDACM1 Picture cPicVRDACM1
	@Li,070 PSAY (cAliasSN3)->N3_VRCDA1  Picture cPicVRCDA1
	@Li,088 PSAY (cAliasSN3)->N3_AMPLIA1 Picture cPicAMPLIA1
	Li ++
	(cAliasSN3)->(dbSkip())
EndDo

If	li != 80
	@li,00 Say repl("*",132)
	@++li,00 PSAY OemToAnsi(STR0010)+Space(23) // "T O T A I S"
	If	lMoeda1
		@Prow(),Pcol() PSAY nTotal1 Picture cPicVORIG1
	Endif
	If	lMoeda2
		@Prow(),Pcol() PSAY nTotal2 Picture cPicVORIG2
	Endif
	If	lMoeda3
		@Prow(),Pcol() PSAY nTotal3 Picture cPicVORIG3
	Endif
	If	lMoeda4
		@Prow(),Pcol() PSAY nTotal4 Picture cPicVORIG4
	Endif
	If	lMoeda5
		@Prow(),Pcol() PSAY nTotal5 Picture cPicVORIG5
	Endif
	Li++
	@Li,034 PSAY nTotalCorrAc Picture cPicVRCACM1
	@Li,052 PSAY nTotalDeprAc Picture cPicVRDACM1
	@Li,070 PSAY nTotalCrDpAc Picture cPicVRCDA1
	@Li,088 PSAY nTotalAmplia Picture cPicAMPLIA1
    
	@++li,00 PSAY Replicate("*",132)
	Roda(cbcont,cbtxt,Tamanho)
EndIf
          
If aReturn[5] = 1
	Set Printer to
   dbCommitAll()
   Ourspool(wnrel)
EndIf

MS_FLUSH()

(cAliasQry)->( dbCloseArea() )

RestArea( aArea )

Return Nil



