#INCLUDE "ATFR170.CH"
#INCLUDE "PROTHEUS.CH"

#DEFINE CRLF CHR(13)+CHR(10)

Static _oATFR1701
Static _oATFR1702

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.

/*/


Ŀ
Funo     ATFR170   Autor  Daniel Tadashi Batori  Data  06.09.06 
Ĵ 
Descrio  Resumo da Correo Monetaria					                 
Ĵ
Sintaxe e  ATFR170(void)                                              
Ĵ
Parametros                                                            
Ĵ
 Uso       SIGAATF	                                                  
ٱ


*/
Function ATFR170()
Local oReport

If TRepInUse()
	oReport := ReportDef()
	oReport:PrintDialog()
Else
	Return ATFR170R3() // Executa verso anterior do fonte
Endif

Return

/*


Ŀ
Funo     ReportDef Autor  Daniel Batori          Data  06.09.06 
Ĵ
Descrio  Definicao do layout do Relatorio									  
Ĵ
Sintaxe    ReportDef(void)                                            
Ĵ
 Uso       Generico                                                   
ٱ


*/
Static Function ReportDef()
Local oReport		:= Nil
Local oSection1		:= Nil
Local nTamDesc		:= 0
Local nTamVal		:= 0
Local nMoeda		:= 0
Local lCtb			:= CtbInUse()
Local cMoeda		:= ""
Local cPict			:= ""
Local cPictSal		:= ""
Local cDescMoedA	:= ""
Local cDescMoed1	:= ""

oReport := TReport():New("ATFR170",STR0003,"AFR170",{|oReport| ReportPrint(oReport)},STR0001+STR0002)

oReport:SetUseGC(.F.)

pergunte("AFR170",.F.)
//Ŀ
// Variaveis utilizadas para parametros                     
// mv_par01            // Data Conta                        
// mv_par02            // Ate a Conta                       
// mv_par03            // Separa conta de depreciacao       
//

oReport:SetPortrait(.T.)

cMoeda := GETMV("MV_ATFMOED") // Localiza a moeda do ativo
nMoeda := Val(cMoeda)
cPictSal	:= PesqPict("SN5","N5_VALOR"+cMoeda,18,nMoeda)

cDescMoedA:= Alltrim(Subs(GetMV("MV_MOEDA"+Alltrim(cMoeda)),1,6))
cDescMoed1  := Alltrim(Subs(GetMV("MV_MOEDA1"),1,6))

cDescMoedA:=Iif(Len(cDescMoedA)< 6,cDescMoedA+space(6 - Len(cDescMoedA)),cDescMoedA)
cDescMoed1:=Iif(Len(cDescMoed1)< 6,cDescMoed1+space(6 - Len(cDescMoed1)),cDescMoed1)

nTamDesc := If( lCtb, TamSX3("CT1_DESC01")[1], TamSX3("I1_DESC")[1] )
nTamVal	:= TamSX3("N3_VORIG1")[1]
cPict		:= PesqPict("SN3","N3_VORIG1")

oSection1 := TRSection():New(oReport,SubStr(STR0004,1,At(" ",STR0004)-1),{"cNomeArq","SN3"})
oSection1:SetAutoSize(.T.)
TRCell():New(oSection1,"N3_CCONTAB","SN3",STR0013+CRLF+STR0014,,,.F.,{|| cNomeArq->CONTA }) //"Contas sujeitas""a correcao"
TRCell():New(oSection1,"DESCRICAO",,STR0015,,nTamDesc,.F.,{|| cNomeArq->DESCRIC }) //"Descrio"
TRCell():New(oSection1,"SALDO",,STR0016+" "+cDescMoedA+CRLF+STR0017,cPictSal,18,.F.,{|| cNomeArq->SALDO },,,"RIGHT")  //"Saldo em Ufir""no Razo Auxiliar"
TRCell():New(oSection1,"SALD1",,STR0018+" "+cDescMoed1+CRLF+STR0019,cPict,nTamVal,.F.,{|| cNomeArq->SALD1 },,,"RIGHT")  //"Valores em Reais""corrigidos"
TRCell():New(oSection1,"DEVED",,STR0020+CRLF+STR0021,cPict,nTamVal,.F.,{|| cNomeArq->DEVED },,,"RIGHT")  //"Saldo da Correo""Acumulada Devedor"
TRCell():New(oSection1,"CREDO",,STR0020+CRLF+STR0022,cPict,nTamVal,.F.,{|| cNomeArq->CREDO },,,"RIGHT")  //"Saldo da Correo""Acumulada Credor"
TRCell():New(oSection1,"AMPLIA1",,STR0023+CRLF+STR0024,cPict,nTamVal,.F.,{|| cNomeArq->AMPLIA1 },,,"RIGHT")  //"Valor da""Ampliao"

Return oReport                                                                              

/*


Ŀ
Programa  ReportPrint Autor Daniel Batori           Data 10.07.06	
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os  
          relatorios que poderao ser agendados pelo usuario.           
Ĵ
Retorno   Nenhum                                                       
Ĵ
ParametrosExpO1: Objeto Report do Relatrio                            
Ĵ
   DATA    Programador   Manutencao efetuada                          
Ĵ
                                                                      
ٱ


*/
Static Function ReportPrint(oReport)
Local oSection1		:= oReport:Section(1)
Local cWhere 		:= ""
Local cOrdem 		:= ""
Local cAliasQry 	:= GetNextAlias()
Local oTotal		:= Nil
Local oImpDeve		:= Nil
Local oImpCred		:= Nil
Local cIndex		:= ""
Local cChave		:= ""
LOCAL dUltProc		:= Ctod("//") 
LOCAL dDataProx		:= Ctod("//") 
LOCAL nUfir			:= 0
LOCAL aCampos  		:={}
Local aTam			:={}
Local lCtb 			:= CtbInUse()
Local cMoeda		:= ""
Local cFilterUser	:= ""
Local aSelFil		:= {}
Local aSM0			:= {}
Local cTmpFil		:= ""
Local cFilSN3		:= ""
Local cFilSM0		:= ""
Local nRegSM0		:= 0
Local nPos			:= 0
Local nLenFil		:= 0
Local nTtlExer		:= 0
Local lSelFil		:= .F.

// Verificao da classificao de Ativo se sofre depreciao
Local lAtClDepr 	:= .F.

nRegSM0 := SM0->(Recno())

	aSM0 := FWLoadSM0()
	lSelFil := (FwSizeFilial() > 2)
	If lSelFil
		If MV_PAR04 == 1 
				AdmSelecFil("AFR170",04,.F.,@aSelFil,"SN3",.F.)
		Endif
		If Empty(aSelFil)
			Aadd(aSelFil,cFilAnt)
		Endif
	Endif
	//Ŀ
	// Analiza contas de/ate'       
	//
	If	!Empty( mv_par01 ) // Data Conta
		cWhere += " AND N3_CCONTAB > '" + mv_par01 + "'"
		
		If mv_par03 == 1 // Separa conta de depreciacao? SIM/NAO
			cWhere += " AND N3_CCDEPR > '" + mv_par01 + "'"
		EndIf
	Endif

	If	!Empty( mv_par02 ) // Ate a Conta
		cWhere += " AND N3_CCONTAB < '" + mv_par02 + "'"
		
		If mv_par03 == 1 // Separa conta de depreciacao? SIM/NAO
			cWhere += " AND N3_CCDEPR < '" + mv_par02 + "'"
		EndIf
	Endif
	cFilterUser := oSection1:GetSqlExp("SN3")
	
	If !Empty(cFilterUser)
		cWhere := cWhere + " AND " + cFilterUser
	Endif	

	cOrdem := " ORDER BY " + SqlOrder( SN3->(IndexKey(1)) )
	
	cWhere := "% " + cWhere + cOrdem + " %"		
	
	If lSelFil
		MsgRun(STR0026,STR0003,{|| cFilSN3 := GetRngFil(aSelFil,"SN3",.T.,@cTmpFil)}) //"Favor Aguardar..."###""Resultado da Correcao Monetaria"
		cFilSN3 := "%N3_FILIAL " + cFilSN3 + "%"
	Else
		cFilSN3 := "%N3_FILIAL = '" + xFilial("SN3") + "'%"
	Endif

	BeginSql Alias cAliasQry
		SELECT N3_FILIAL,N3_CBASE, N3_ITEM, N3_CCONTAB, N3_AMPLIA1, N3_VRCACM1, N3_CCDEPR,
				N3_VORIG1, N3_VORIG2, N3_VORIG3, N3_VORIG4, N3_VORIG5,
				N3_VRDACM1, N3_VRDACM2, N3_VRDACM3, N3_VRDACM4, N3_VRDACM5,
				N3_AMPLIA1, N3_AMPLIA2, N3_AMPLIA3, N3_AMPLIA4, N3_AMPLIA5,
				N3_VRCDA1
		FROM %table:SN3% SN3
		WHERE %Exp:cFilSN3%
				AND SN3.N3_BAIXA = '0'
				AND SN3.%NotDel%
				%Exp:cWhere% 
	EndSQL

cMoeda := GETMV("MV_ATFMOED") // Localiza a moeda do ativo
//Ŀ
// Localiza a Ufir do mes seguinte                              
//
dUltProc 	:= GetMV("MV_ULTDEPR")			// Localiza a data do ultimo calculo
dDataProx 	:= LastDay( dUltProc ) + 1   
SM2->( dbSeek( dDataProx, .T. ) )
nUfir 		:= &('SM2->M2_MOEDA'+cMoeda)

//Ŀ
// Gera arquivo de Trabalho      
//
AADD(aCampos,{"FILIAL","C",FWSizeFilial(),0})
aTam := TamSX3("N5_CONTA")
AADD(aCampos,{"CONTA"	,"C"	,aTam[1],aTam[2]	} )
aTam := If(lCtb, TamSX3("CT1_DESC01"), TamSX3("I1_DESC") )
AADD(aCampos,{"DESCRIC"	,"C"	,aTam[1],aTam[2]	} )
AADD(aCampos,{"SALDO"	,"N"  ,16   ,4 } )
AADD(aCampos,{"SALD1"	,"N"  ,16   ,4 } )
AADD(aCampos,{"DEVED"	,"N"  ,16   ,4 } )
AADD(aCampos,{"CREDO"	,"N"  ,16   ,4 } )
AADD(aCampos,{"AMPLIA1"	,"N"  ,16   ,4 } )
AADD(aCampos,{"REGSM0"	,"N"  ,10   ,0 } )

If _oATFR1701 <> Nil
	_oATFR1701:Delete()
	_oATFR1701 := Nil
Endif

_oATFR1701 := FWTemporaryTable():New( "cNomeArq" )  
_oATFR1701:SetFields(aCampos) 
_oATFR1701:AddIndex("1", {"FILIAL","CONTA"})

//------------------
//Criao da tabela temporaria
//------------------
_oATFR1701:Create()  

If !lSelFil
	oDesc	:= TRFunction():New(oSection1:Cell("DESCRICAO"),,"ONPRINT",,"",,{|| STR0009 },.T.,.F.,.F.) // "E X E R C I C I O"
	oTotal	:= TRFunction():New(oSection1:Cell("DEVED"),,"SUM",,"",,{|| cNomeArq->DEVED - cNomeArq->CREDO },.T.,.F.,.F.)
	oImpDeve:= TRFunction():New(oSection1:Cell("DEVED"),,"ONPRINT",,"",,{|| If(oTotal:GetValue()>0	,oTotal:GetValue()		,) },.T.,.F.,.F.)
	oImpCred:= TRFunction():New(oSection1:Cell("CREDO"),,"ONPRINT",,"",,{|| If(oTotal:GetValue()<=0	,Abs(oTotal:GetValue())	,) },.T.,.F.,.F.)
	
	oTotal:Disable()

	oSection1:SetTotalText({||""})

	oSection1:SetTotalInLine(.F.)
Endif	

oReport:NoUserFilter()
oReport:SetMeter((cAliasQry)->(Reccount()))

While (cAliasQry)->(!Eof())

	oReport:IncMeter()
		
	dbSelectArea("SN1")
	dbSetOrder(1)
	dbSeek(xFilial("SN1",(cAliasQry)->N3_FILIAL) + (cAliasQry)->N3_CBASE + (cAliasQry)->N3_ITEM)

	If lCtb
		dbSelectArea("CT1")
		dbSetOrder(1)
		dbSeek( xFilial("CT1",(cAliasQry)->N3_FILIAL) + (cAliasQry)->N3_CCONTAB )
	Else
		dbSelectArea("SI1")
		dbSetOrder(1)
		dbSeek( xFilial("SI1",(cAliasQry)->N3_FILIAL) + (cAliasQry)->N3_CCONTAB )
	Endif

	If	!cNomeArq->(dbSeek((cAliasQry)->N3_FILIAL + (cAliasQry)->N3_CCONTAB ) )
		RecLock( "cNomeArq" , .T. )
		cNomeArq->FILIAL := (cAliasQry)->N3_FILIAL	
		cNomeArq->CONTA := (cAliasQry)->N3_CCONTAB
		
		cFilSM0 := AllTrim((cAliasQry)->N3_FILIAL)
		nLenFil := Len(cFilSM0)
		If Empty(cFilSM0)
			nPos := Ascan(aSM0,{|sm0| sm0[SM0_GRPEMP] == cEmpAnt })
			cNomeArq->REGSM0 := aSM0[nPos,SM0_RECNO]
		Else
			nPos := Ascan(aSM0,{|sm0| sm0[SM0_GRPEMP] == cEmpAnt .And. Substr(sm0[SM0_CODFIL],1,nLenFil) == cFilSM0})
			cNomeArq->REGSM0 := aSM0[nPos,SM0_RECNO]
		EndIf		
		If lCtb
			cNomeArq->DESCRIC := CT1->CT1_DESC01
		Else
			cNomeArq->DESCRIC := SI1->I1_DESC
		Endif
	Else
		RecLock( "cNomeArq" )
	Endif
    
	// Verificao da classificao de Ativo se sofre depreciao
	lAtClDepr := AtClssVer(SN1->N1_PATRIM)

	//Ŀ
	// Saldo e':Valor original+Correo do Acumul-Deprec.Acumulada        
	//
	If lAtClDepr .OR. SN1->N1_PATRIM $ " P"
		cNomeArq->SALDO  += ( (cAliasQry)->&("(N3_VORIG"+cMoeda+"+N3_AMPLIA"+cMoeda+")") )  // Valor Original em Ufir
		cNomeArq->SALD1  += ( (cAliasQry)->N3_VORIG1 + (cAliasQry)->N3_AMPLIA1 + (cAliasQry)->N3_VRCACM1 )  // Valor Original em Moeda corrente
		cNomeArq->AMPLIA1+= (cAliasQry)->N3_AMPLIA1
	Else
		cNomeArq->SALDO  -= ( (cAliasQry)->&("(N3_VORIG"+cMoeda+"+N3_AMPLIA"+cMoeda+")") )  // Valor Original em Ufir
		cNomeArq->SALD1  -= ( (cAliasQry)->N3_VORIG1 + (cAliasQry)->N3_VRCACM1 )  // Valor Original em Moeda corrente	
		cNomeArq->AMPLIA1-= (cAliasQry)->N3_AMPLIA1
	Endif
      
	If lCtb
		If CT1->CT1_NORMAL == "1"
			cNomeArq->DEVED += ( (cAliasQry)->N3_VRCACM1 )  // VrcAcm1: Correo Acumulada Moeda
			Iif(mv_par03 = 2,cNomeArq->CREDO += ( (cAliasQry)->N3_VRCDA1  ),;
			                 cNomeArq->CREDO += 0 )      // Vrcda1: Corr.Depr.Acum
		Else
			cNomeArq->CREDO += ( (cAliasQry)->N3_VRCACM1 )  // VrcAcm1: Correo Acumulada Moeda
			Iif(mv_par03 = 2,cNomeArq->DEVED += ( (cAliasQry)->N3_VRCDA1  ),;
			                 cNomeArq->DEVED += 0)       // Vrcda1: Corr.Depr.Acum
		Endif
	Else
		If SI1->I1_NORMAL == "D"
			cNomeArq->DEVED += ( (cAliasQry)->N3_VRCACM1 )  // VrcAcm1: Correo Acumulada Moeda
			Iif(mv_par03 = 2,cNomeArq->CREDO += ( (cAliasQry)->N3_VRCDA1  ),;
			                 cNomeArq->CREDO += 0 )      // Vrcda1: Corr.Depr.Acum
		Else
			cNomeArq->CREDO += ( (cAliasQry)->N3_VRCACM1 )  // VrcAcm1: Correo Acumulada Moeda
			Iif(mv_par03 = 2,cNomeArq->DEVED += ( (cAliasQry)->N3_VRCDA1  ),;
			                 cNomeArq->DEVED += 0)       // Vrcda1: Corr.Depr.Acum
		Endif
	EndIf       

	If mv_par03 == 1 // Separa conta de depreciacao? SIM/NAO

		If !Empty((cAliasQry)->N3_CCDEPR)   //Verifico se a conta de deprec acum	
			If lCtb
				dbSelectArea("CT1")
				dbSetOrder(1)
				dbSeek( xFilial("CT1",(cAliasQry)->N3_FILIAL) + (cAliasQry)->N3_CCDEPR )
			Else
				dbSelectArea("SI1")
				dbSetOrder(1)
				dbSeek( xFilial("SI1",(cAliasQry)->N3_FILIAL) + (cAliasQry)->N3_CCDEPR )
			Endif
			dbSelectArea( "cNomeArq" )
			If	!cNomeArq->(dbSeek((cAliasQry)->N3_FILIAL +  (cAliasQry)->N3_CCDEPR ) )
				RecLock( "cNomeArq" , .T. )
				cNomeArq->FILIAL := (cAliasQry)->N3_FILIAL
				cNomeArq->CONTA := (cAliasQry)->N3_CCDEPR
		
				cFilSM0 := AllTrim((cAliasQry)->N3_FILIAL)
				nLenFil := Len(cFilSM0)
				nPos := Ascan(aSM0,{|sm0| sm0[SM0_GRPEMP] == cEmpAnt .And. Substr(sm0[SM0_CODFIL],1,nLenFil) == cFilSM0})
				cNomeArq->REGSM0 := aSM0[nPos,SM0_RECNO]
				
				If lCtb
					cNomeArq->DESCRIC := CT1->CT1_DESC01
				Else
					cNomeArq->DESCRIC := SI1->I1_DESC
				Endif
			Else
				RecLock( "cNomeArq" )
			Endif

			//Ŀ
			// Saldo e':Valor original+Correo do Acumul-Deprec.Acumulada        
			//
			cNomeArq->SALDO += ( (cAliasQry)->&('N3_VRDACM'+cMoeda) )  // Valor Original em Ufir
			cNomeArq->SALD1 += ( (cAliasQry)->N3_VRDACM1 + (cAliasQry)->N3_VRCDA1 )  // Valor Original em Moeda corrente
			If lCtb
				If CT1->CT1_NORMAL == "1"
					cNomeArq->CREDO += ( (cAliasQry)->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
				Else
					cNomeArq->DEVED += ( (cAliasQry)->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
				Endif
			Else
				If SI1->I1_NORMAL == "D"
					cNomeArq->CREDO += ( (cAliasQry)->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
				Else
					cNomeArq->DEVED += ( (cAliasQry)->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
				Endif
			Endif
		Endif
	Endif
	(cAliasQry)->(dbSkip())
End

If lSelFil
	cNomeArq->(DbGoTop())
	oSection1:Init()
	While !(cNomeArq->(Eof()))
		cFilSM0 := cNomeArq->FILIAL
		nTtlExer := 0
		SM0->(DbGoto(cNomeArq->REGSM0))
		While !(cNomeArq->(Eof())) .And. cNomeArq->FILIAL == cFilSM0 
			oSection1:PrintLine()
			nTtlExer += (cNomeArq->DEVED - cNomeArq->CREDO) 
			cNomeArq->(DbSkip())
		Enddo
		oSection1:Cell("N3_CCONTAB"):Hide()
		oSection1:Cell("DESCRICAO"):SetBlock({|| STR0009}) //// "E X E R C I C I O"
		oSection1:Cell("SALDO"):Hide()
		oSection1:Cell("SALD1"):Hide()
		oSection1:Cell("AMPLIA1"):Hide()
		If nTtlExer > 0
			oSection1:Cell("DEVED"):SetBlock({|| nTtlExer })
			oSection1:Cell("CREDO"):Hide()
		Else
			oSection1:Cell("DEVED"):Hide()
			oSection1:Cell("CREDO"):SetBlock({|| Abs(nTtlExer) })
		Endif
		oReport:ThinLine()
		oSection1:PrintLine()
		oSection1:Cell("N3_CCONTAB"):Show()
		oSection1:Cell("DESCRICAO"):SetBlock({|| cNomeArq->DESCRIC})
		oSection1:Cell("SALDO"):Show()
		oSection1:Cell("SALD1"):Show()
		oSection1:Cell("DEVED"):SetBlock({|| cNomeArq->DEVED })
		oSection1:Cell("DEVED"):Show()
		oSection1:Cell("CREDO"):SetBlock({|| cNomeArq->CREDO })
		oSection1:Cell("CREDO"):Show()
		oSection1:Cell("AMPLIA1"):Show()
		oReport:EndPage()
	Enddo
	oSection1:Finish()
Else
	oSection1:Print()
Endif

cNomeArq->(dbCloseArea())

//Deleta tabela temporaria do banco de dados
If _oATFR1701 <> Nil
	_oATFR1701:Delete()
	_oATFR1701 := Nil
Endif

SM0->(DbGoTo(nRegSM0))

If !Empty(cTmpFil)
	MsgRun(STR0026,STR0003,{|| CtbTmpErase(cTmpFil)}) //"Favor Aguardar..."###""Resultado da Correcao Monetaria"
Endif

Return




/*
---------------------------------------------------------- RELEASE 3 ---------------------------------------------
*/
/*


Ŀ
Funo     ATFR170R3 Autor  Vinicius Barreira      Data  16.01.95 
Ĵ
Descrio  Resumo da Correo Monetaria                               
Ĵ
Sintaxe e  ATFR170R3                                                  
Ĵ
 Uso       SIGAATF                                                    
ٱ


*/
Function AtfR170R3
//Ŀ
// Define Variaveis                                             
//
LOCAL cString  := "SN3"
LOCAL cDesc1	:= OemToAnsi(STR0001) // "Emisso de Resultado da Correcao Monetaria do Ativo"
LOCAL cDesc2	:= OemToAnsi(STR0002) // "Fixo Por Conta."
LOCAL cDesc3	:=""
LOCAL wnrel

PRIVATE aReturn := { "Zebrado", 1,"Administracao", 2, 2, 1, "",1 }
PRIVATE aLinha	 := { }
PRIVATE cPerg	 :="AFR170"
PRIVATE NomeProg:="ATFR170"
PRIVATE nLastKey := 0

PRIVATE titulo:= OemToAnsi(STR0003) // "Resultado da Correcao Monetaria"
PRIVATE cabec1
PRIVATE cabec2
PRIVATE tamanho:="M"

//Ŀ
// Verifica as perguntas selecionadas                           
//
pergunte("AFR170",.F.)

//Ŀ
// Variaveis utilizadas para parametros                     
// mv_par01            // Data Conta                        
// mv_par02            // Ate a Conta                       
// mv_par03            // Separa conta de depreciacao       
//
wnrel := "ATFR170"
wnrel := SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.T.,"",,Tamanho)

IF	nLastKey == 27
	Return
End

SetDefault(aReturn,cString)

IF nLastKey == 27
	Return
End

nTipo := IIF(aReturn[4]=1,15,18)

RptStatus({|lEnd| ATF170Res(@lEnd,wnRel,cString)},Titulo)


/*


Ŀ
Funo     ATF170Res Autor  Claudinei M. Benzi     Data  03.08.93 
Ĵ
Descrio  Listagem do Resumo por Conta Contbil                      
Ĵ
Sintaxe e  ATF170Res(lEnd,WnRel,Titulo)                               
Ĵ
Parametros lEnd    - Ao do Codeblock                                
           wnRel   - Ttulo do relatrio                              
           cString - Mensagem                                         
Ĵ
 Uso       Genrico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
C. Denardi  06/10/0366.785Localizacao de casas decimais na Picture  
                          em todos os relatorios do Ativo Fixo      


*/
Function Atf170Res(lEnd,WnRel,cString)
LOCAL dUltProc 
LOCAL dDataProx 
LOCAL nUfir
LOCAL cMascara 
LOCAL cMascaraUF
LOCAL nTotal 	:= 0
LOCAL aCampos	:={}
Local aTam		:={}
LOCAL cbTxt
LOCAL cbCont
Local lCtb 		:= CtbInUse()
Local nMoeda 	:= 0    

// Verificao da classificao de Ativo se sofre depreciao
Local lAtClDepr 	:= .F.

Private cDescMoedAtf	:=" "
Private cDescMoed1		:=" "


cMoeda := GETMV("MV_ATFMOED") // Localiza a moeda do ativo
nMoeda := Val(cMoeda)
cMascara 	:= PesqPict("SN5","N5_VALOR1"      ,16,1     )
cMascaraUF	:= PesqPict("SN5","N5_VALOR"+cMoeda,18,nMoeda)
//Ŀ
// Variaveis utilizadas para Impressao do Cabecalho e Rodape    
//
cbtxt    := SPACE(10)
cbcont   := 0
// cabec1   := OemToAnsi(STR0004)  // "Contas sujeitas                               Saldo em Ufir Valores em Reais       Saldo da Correcao Acumulada         Valor da"
cabec2   := OemToAnsi(STR0005)  // "a correcao         Descricao              no Razao Auxiliar       corrigidos          Devedor           Credor        Ampliacao"

cDescMoedAtf:= Alltrim(Subs(GetMV("MV_MOEDA"+Alltrim(cMoeda)),1,6))
cDescMoed1  := Alltrim(Subs(GetMV("MV_MOEDA1"),1,6))

cDescMoedAtf:=Iif(Len(cDescMoedAtf)< 6,cDescMoedAtf+space(6 - Len(cDescMoedAtf)),cDescMoedAtf)
cDescMoed1:=Iif(Len(cDescMoed1)< 6,cDescMoed1+space(6 - Len(cDescMoed1)),cDescMoed1)
	
Cabec1:= OemToAnsi(STR0010)+" "+cDescMoedAtf+" "+ OemToAnsi(STR0011)+ cDescMoed1 + " " + OemToAnsi(STR0012)


//Contas sujeitas                             Saldo em       "  
//                                                              "Valores em 123456
//                                                                                    Saldo da Correcao Acumulada         Valor da"
//a correcao           Descricao              no Razao Auxiliar       corrigidos          Devedor           Credor        Ampliacao"

//Contas sujeitas                                 Saldo em Ufir Valores em Reais       Saldo da Correcao Acumulada         Valor da"
//a correcao           Descricao              no Razao Auxiliar       corrigidos          Devedor           Credor        Ampliacao"
//XXXXXXXXXXXXXXXXXXXX xxxxxxxxxxxxxxxxxxxx 99,999,999,999.9999 9.999.999.999,99 9.999.999.999,99 9.999.999.999,99 9.999.999.999,99 
//01234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^678901234^6789012
//          1         2         3         4         5         6         7         8         9        10        11        12        13  

li       := 80
m_pag    := 1

nTipo := IIF(aReturn[4]=1,15,18)
//Ŀ
// Localiza a Ufir do mes seguinte                              
//

dUltProc 	:= GetMV("MV_ULTDEPR")			// Localiza a data do ultimo calculo
dDataProx 	:= LastDay( dUltProc ) + 1   
SM2->( dbSeek( dDataProx, .T. ) )
nUfir 		:= &('SM2->M2_MOEDA'+cMoeda)

//Ŀ
// Gera arquivo de Trabalho      
//
aTam:=TamSX3("N5_CONTA")
AADD(aCampos,{"CONTA"	,"C"	,aTam[1],aTam[2]	} )
AADD(aCampos,{"SALDO"	,"N"  ,16   ,4 } )
AADD(aCampos,{"SALD1"	,"N"  ,16   ,4 } )
AADD(aCampos,{"DEVED"	,"N"  ,16   ,4 } )
AADD(aCampos,{"CREDO"	,"N"  ,16   ,4 } )
AADD(aCampos,{"AMPLIA1"	,"N"  ,16   ,4 } )

If _oATFR1702 <> Nil
	_oATFR1702:Delete()
	_oATFR1702 := Nil
Endif

_oATFR1702 := FWTemporaryTable():New( "cNomeArq" )  
_oATFR1702:SetFields(aCampos) 
_oATFR1702:AddIndex("1", {"CONTA"})

//------------------
//Criao da tabela temporaria
//------------------
_oATFR1702:Create()  

dbSelectArea("SN3")
dbSetOrder(1)
dbSeek(xFilial("SN3"))

SetRegua(SN3->( Reccount()))

While	!Eof() .and. SN3->N3_FILIAL == xFilial("SN3")

	IF	lEnd
		@PROW()+1,001 PSAY OemToAnsi(STR0006) // "CANCELADO PELO OPERADOR"
		Exit
	End

	//Ŀ
	// Movimentao da regua                   
	//
	IncRegua()
		
	//Ŀ
	// No considera os bens baixados  
	//
	If	Val(SN3->N3_BAIXA) > 0
		SN3->( dbSkip() )
		Loop
	Endif

	//Ŀ
	// Analiza contas de/ate'       
	//
	If	!Empty( mv_par01 )
		If	SN3->N3_CCONTAB < mv_par01 
			SN3->( dbSkip() )
			Loop
		Endif
	Endif
	If	!Empty( mv_par02 )
		If	SN3->N3_CCONTAB > mv_par02
			SN3->( dbSkip() )
			Loop
		Endif
	Endif

	dbSelectArea("SN1")
	dbSetOrder(1)
	dbSeek(xFilial("SN1")+SN3->N3_CBASE+SN3->N3_ITEM)

	dbSelectArea( "cNomeArq" )
	If	!cNomeArq->(dbSeek( SN3->N3_CCONTAB ) )
		RecLock( "cNomeArq" , .T. )	
		cNomeArq->CONTA := SN3->N3_CCONTAB
	Else
		RecLock( "cNomeArq" )
	Endif

	If lCtb
		dbSelectArea("CT1")
		dbSetOrder(1)
		dbSeek( xFilial("CT1") + SN3->N3_CCONTAB )
	Else
		dbSelectArea("SI1")
		dbSetOrder(1)
		dbSeek( xFilial("SI1") + SN3->N3_CCONTAB )
	Endif

	// Verificao da classificao de Ativo se sofre depreciao
	lAtClDepr := AtClssVer(SN1->N1_PATRIM)

	//Ŀ
	// Saldo e':Valor original+Correo do Acumul-Deprec.Acumulada        
	//
	If lAtClDepr .OR. SN1->N1_PATRIM $ " P"
		cNomeArq->SALDO  += ( &('SN3->N3_VORIG'+cMoeda)+&('SN3->N3_AMPLIA'+cMoeda))  // Valor Original em Ufir
		cNomeArq->SALD1  += ( SN3->N3_VORIG1+SN3->N3_AMPLIA1+SN3->N3_VRCACM1 )  // Valor Original em Moeda corrente
		cNomeArq->AMPLIA1+= SN3->N3_AMPLIA1
	Else
		cNomeArq->SALDO  -= ( &('SN3->N3_VORIG'+cMoeda)+&('SN3->N3_AMPLIA'+cMoeda ))  // Valor Original em Ufir
		cNomeArq->SALD1  -= ( SN3->N3_VORIG1+SN3->N3_VRCACM1 )  // Valor Original em Moeda corrente	
		cNomeArq->AMPLIA1-= SN3->N3_AMPLIA1
	Endif
      
	If lCtb
		If CT1->CT1_NORMAL == "1"
			cNomeArq->DEVED += ( SN3->N3_VRCACM1 )  // VrcAcm1: Correo Acumulada Moeda
			Iif(mv_par03 = 2,cNomeArq->CREDO += ( SN3->N3_VRCDA1  ),;
			                 cNomeArq->CREDO += 0 )      // Vrcda1: Corr.Depr.Acum
		Else
			cNomeArq->CREDO += ( SN3->N3_VRCACM1 )  // VrcAcm1: Correo Acumulada Moeda
			Iif(mv_par03 = 2,cNomeArq->DEVED += ( SN3->N3_VRCDA1  ),;
			                 cNomeArq->DEVED += 0)       // Vrcda1: Corr.Depr.Acum
		Endif
	Else
		If SI1->I1_NORMAL == "D"
			cNomeArq->DEVED += ( SN3->N3_VRCACM1 )  // VrcAcm1: Correo Acumulada Moeda
			Iif(mv_par03 = 2,cNomeArq->CREDO += ( SN3->N3_VRCDA1  ),;
			                 cNomeArq->CREDO += 0 )      // Vrcda1: Corr.Depr.Acum
		Else
			cNomeArq->CREDO += ( SN3->N3_VRCACM1 )  // VrcAcm1: Correo Acumulada Moeda
			Iif(mv_par03 = 2,cNomeArq->DEVED += ( SN3->N3_VRCDA1  ),;
			                 cNomeArq->DEVED += 0)       // Vrcda1: Corr.Depr.Acum
		Endif
	EndIf       

	If mv_par03 == 1
		//Ŀ
		// Analiza contas de/ate'       
		//
		If	!Empty( mv_par01 )
			If	SN3->N3_CCDEPR < mv_par01 
				dbSelectArea("SN3")
				dbSkip()
				Loop
			Endif
		Endif
		If	!Empty( mv_par02 )
			If	SN3->N3_CCDEPR > mv_par02
				dbSelectArea("SN3")
				dbSkip()
				Loop
			Endif
		Endif
		
		If !Empty(SN3->N3_CCDEPR)   //Verifico se a conta de deprec acum
			dbSelectArea( "cNomeArq" )
			If	!cNomeArq->(dbSeek( SN3->N3_CCDEPR ) )
				RecLock( "cNomeArq" , .T. )	
				cNomeArq->CONTA := SN3->N3_CCDEPR
			Else
				RecLock( "cNomeArq" )
			Endif

			If lCtb
				dbSelectArea("CT1")
				dbSetOrder(1)				
				dbSeek( xFilial("CT1") + SN3->N3_CCDEPR )			
			Else
				dbSelectArea("SI1")
				dbSetOrder(1)				
				dbSeek( xFilial("SI1") + SN3->N3_CCDEPR )
			EndIf
			//Ŀ
			// Saldo e':Valor original+Correo do Acumul-Deprec.Acumulada        
			//
			cNomeArq->SALDO += ( &('SN3->N3_VRDACM'+cMoeda) )  // Valor Original em Ufir
			cNomeArq->SALD1 += ( SN3->N3_VRDACM1+SN3->N3_VRCDA1 )  // Valor Original em Moeda corrente
			If lCtb
				If CT1->CT1_NORMAL == "1"
					cNomeArq->CREDO += ( SN3->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
				Else
					cNomeArq->DEVED += ( SN3->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
				Endif
			Else
				If SI1->I1_NORMAL == "D"
					cNomeArq->CREDO += ( SN3->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
				Else
					cNomeArq->DEVED += ( SN3->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
				Endif
			Endif
		Endif
	Endif
	dbSelectArea("SN3")
	SN3->(dbSkip())
End

dbSelectArea("cNomeArq")

SetRegua(Reccount())

dbGoTop()
If Reccount() > 0
	While	!Eof()

		//Ŀ
		// Movimentao da regua              
		//
		IncRegua()
		
		IF li > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,IIF(aReturn[4]==1,15,18))
			li ++
			@li,00 say OemToAnsi(STR0007)+ dToc( dUltProc )                                 // "BALANCO ENCERRADO EM " 
			li ++
			@li,00 say OemToAnsi(STR0008)+ cDescMoedAtf + space(13 -Len(cDescMoedAtf))+ TransForm( nUfir, "999999.9999" ) // " VALOR DA UFIR        " 
			li ++
			li ++
		Endif

		IF	lEnd
			@PROW()+1,001 PSAY OemToAnsi(STR0006) // "CANCELADO PELO OPERADOR"
			Exit
		End
		If lCtb
			dbSelectArea( "CT1" )
			dbSetOrder( 1 )
			dbSeek( cFilial+cNomeArq->conta )
		Else
			dbSelectArea( "SI1" )
			SI1->( dbSetOrder( 1 ) )
			SI1->( dbSeek( cFilial+cNomeArq->conta ) )
		EndIf
		nTotal += ( cNomeArq->DEVED - cNomeArq->CREDO )

		@ li, 00	PSAY	cNomeArq->conta 
		@ li, 21	PSAY	Iif(lCtb,Left(CT1->CT1_DESC01,20),Left(SI1->I1_DESC,20) )
		@ li, 42	PSAY	cNomeArq->SALDO   PICTURE cMascaraUF
		@ li, 62	PSAY	cNomeArq->SALD1   PICTURE cMascara 
		@ li, 79	PSAY	cNomeArq->DEVED   PICTURE cMascara 
		@ li, 96	PSAY	cNomeArq->CREDO   PICTURE cMascara 
		@ li,113	PSAY	cNomeArq->AMPLIA1 PICTURE cMascara 

		li++
		dbSelectArea("cNomeArq")
		dbSkip()
	End
	li ++
	@ li,50	PSAY	OemToAnsi(STR0009) // "E X E R C I C I O"

	If nTotal > 0
		@ li, 79	PSAY nTotal	     PICTURE cMascara 
	Else
		@ li, 96	PSAY Abs(nTotal) PICTURE cMascara 
	Endif

	IF li != 80
		roda(cbcont,cbtxt,Tamanho)
	End
Endif
cNomeArq->(dbCloseArea())

If _oATFR1702 <> Nil
	_oATFR1702:Delete()
	_oATFR1702 := Nil
Endif

Set Device To Screen

IF aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
End
MS_FLUSH()
dbSelectArea("SN3")
dbSetOrder(1)
Return