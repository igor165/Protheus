#INCLUDE "HHXAPI.ch"

#INCLUDE "PROTHEUS.CH"
#define VM_INSERT 08192   // Inclusao de Registro
#define VM_UPDATE 16384   // Alteracao de Registro
#define VM_DELETE 32768   // Exclusao de Registro
#IFNDEF TOP
	#define RetSqlName RetDbfName
#ENDIF
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³HHChkIntr ³ Rev.  ³Eduardo Riera          ³ Data ³09/01/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Checagem da instrucao para sincronizacao com o Hand Held    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpC1: Código da instrucao                                  ³±±
±±³          ³       [I] Insercao                                         ³±±
±±³          ³       [A] Atualizacao                                      ³±±
±±³          ³       [E] Exclusao                                         ³±±
±±³          ³ExpC2: Alias da Tabela                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo da operacao                                   ³±±
±±³          ³       [000] Update completo                                ³±±
±±³          ³       [064] Inclusao de registros                          ³±±
±±³          ³       [128] Alteracao de registros                         ³±±
±±³          ³       [256] Exclusao de registro                           ³±±
±±³          ³       [I] Insercao                                         ³±±
±±³          ³       [A] Atualizacao                                      ³±±
±±³          ³       [E] Exclusao                                         ³±±
±±³          ³ExpC2: Alias da Tabela                                      ³±±
±±³          ³ExpL3: Informe se o registro foi encontrado na tabela de    ³±±
±±³          ³       destino.                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function HHChkIntr(nOper,cAlias,lFound)
Local cIntr    := ""
Local cIntrFld := cAlias + "->" + PrefixoCpo(cAlias) + "_INTR"
DEFAULT lFound := .F.

Do Case
	Case nOper != VM_DELETE .And. lFound .And. &(cIntrFld) == "I"
		cIntr := "I"
	Case nOper == VM_INSERT
		cIntr := "I"
	Case nOper == VM_UPDATE
//		If !lFound
			cIntr := "I"
//		Else
//			cIntr := "A"
//		EndIf
	Case nOper == VM_DELETE
		cIntr := "E"
	OtherWise
		cIntr := "I"
EndCase

Return cIntr

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³HHNextVer ³ Rev.  ³Eduardo Riera          ³ Data ³09/01/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna a próxima versao do registro para sincronizacao     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpN1: Versao do proximo registro para sincronizacao        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Id do usuário                                        ³±±
±±³          ³ExpC2: Alias da tabela de sincronizacao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function HHNextVer(cID,cAlias)

Local aArea     := GetArea()
Local cTable    := ""
Local cSeek     := ""
Local nOrder    := 1
Local nMaxVer   := 0
Local bWhile    := {|| .T.}
#IFDEF TOP
Local cQuery    := ""
#ENDIF

cID := PadR(cID,10)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Abre as tabelas de controle do Hand Held                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Select("HHT") = 0
	 POpenTbl()
EndIf
If Select("HHCTR") = 0
	POpenHHCtr()
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica o nome fisico da tabela de sincronizacao                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cTable := RetSqlName(cAlias)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Pesquisa na tabela de controle a próxima versao do registro                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("HHT")
dbSetOrder(2)
If MsSeek(cAlias)
	If HHT->HHT_GEN == "2" // Tabela Nao Generica
		cSeek := cID+cTable
		nOrder := 1
		bWhile := {|| cID+cTable == cSeek }
	Else				  // Tabela Generica
		cSeek := cTable
		nOrder := 2
		bWhile := {|| cSeek == cTable }
	EndIf
	
	dbSelectArea("HHCTR")
	dbSetOrder(nOrder)
	If MsSeek(cSeek)
		#IFNDEF TOP
			While !Eof() .And. Eval(bWhile)
				RecLock("HHCTR")
				If HHCTR->VERSION < HHCTR->MAXVERSION
					nMaxVer := Max(HHCTR->MAXVERSION,nMaxVer)
				Else
					nMaxVer := Max(HHCTR->MAXVERSION + 1,nMaxVer)
				EndIf
				dbSelectArea("HHCTR")
				dbSkip()
			EndDo
		#ELSE
			RecLock("HHCTR")
			If HHCTR->VERSION < HHCTR->MAXVERSION
				nMaxVer := Max(HHCTR->MAXVERSION,nMaxVer)
			Else
				nMaxVer := Max(HHCTR->MAXVERSION + 1,nMaxVer)
			EndIf
			
			cQuery := "SELECT MAX(VERSION) VERSION,MAX(MAXVERSION) MAXVERSION "
			cQuery += "FROM HHCTR "
			cQuery += "WHERE "
			If HHT->HHT_GEN == "2" // Tabela Nao Generica
				cQuery += "USERID='"+cID+"' AND "
			EndIf
			cQuery += "TABLENAME='"+cTable+"'  "
			HHCTR->(DbCommit())
			cQuery := ChangeQuery(cQuery)
			
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"HHNEXTVER")
			If !HHNEXTVER->(Eof())
				If HHNEXTVER->VERSION < HHNEXTVER->MAXVERSION
					nMaxVer := Max(HHNEXTVER->MAXVERSION,nMaxVer)
				Else
					nMaxVer := Max(HHNEXTVER->MAXVERSION + 1,nMaxVer)
				EndIf
			EndIf
			dbCloseArea()
			dbSelectArea("HHCTR")
		#ENDIF
	EndIf
	nMaxVer := Max(1,nMaxVer)
EndIf
RestArea(aArea)
Return(nMaxVer)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³HHUpdVer  ³ Rev.  ³Eduardo Riera          ³ Data ³09/01/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Atualiza a versao do registro de sincronizacao              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Id do usuário                                        ³±±
±±³          ³ExpC2: Alias da tabela de sincronizacao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function HHUpdCtr(cID,cAlias)

Local aArea     := GetArea()
Local aArea2    := (cAlias)->(GetArea())
Local nMaxVer   := HHNextVer(cID,cAlias)
Local nQtdRec   := 0
#IFNDEF TOP
Local cTable	:= RetSqlName(cAlias)
Local cSeek     := ""
#ELSE
Local cQuery    := ""
#ENDIF

If nMaxVer > 0
	cID := IIf(HHT->HHT_GEN=="1",Space(6),cID)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica a quantidade de registros desta versao                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP
		cQuery := "SELECT COUNT(*) QTDREC "
		cQuery += "FROM " + RetSqlName(cAlias)+" "
		cQuery += "WHERE " + cAlias + "_ID = '" +cID+ "' AND "
		cQuery += cAlias+"_INTR IN ('I','A','E') AND "
		cQuery += cAlias+"_VER >= " + Str(nMaxVer,10,0)
		(cAlias)->(DbCommit())
		cQuery := ChangeQuery(cQuery)
		
		dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),GetNextAlias())	
		nQtdRec := QTDREC
		dbCloseArea()
		dbSelectArea("HHT")
	#ELSE
		cQuery := "('" + cAlias + "')->" + cAlias + "_ID = '" +cID+ "' .And. "
		cQuery += "('" + cAlias + "')->" + cAlias + "_INTR$'I/A/E' .And. "
		cQuery += "('" + cAlias + "')->" + cAlias + "_VER >= " + Str(nMaxVer,10,0)
		dbSelectArea(cAlias)
		MsSeek(xFilial(cAlias))
		While !Eof() .And. (cAlias)->(FieldGet(FieldPos(PrefixoCpo(cAlias)+"_FILIAL"))) == xFilial(cAlias)
			If &(cQuery)
				nQtdRec ++
			EndIf
			dbSelectArea(cAlias)
			dbSkip()
		EndDo		
	#ENDIF
		
	#IFDEF TOP
		cQuery := "UPDATE HHCTR "
		cQuery += "SET MAXVERSION = " + Str(nMaxVer,10,0)+","
		cQuery += "AMNT = "+Str(nQtdRec,10,0)+","
		cQuery += "OPER = 'P' "
		cQuery += "WHERE "
		cQuery += "TABLENAME = '"+RetSqlName(cAlias)+ "' AND "
		If HHT->HHT_GEN == "2"  // Tabela Nao Generica
			cQuery += " USERID = '" + cID + "' AND "
		EndIf
		cQuery += "OPER <> 'T' "
		TcSqlExec(cQuery)
		
		If HHT->HHT_GEN == "1"  // Tabela Generica
			cQuery := "UPDATE HHCTR "
			cQuery += "SET MAXVERSION = " + Str(nMaxVer,10,0)+","
			cQuery += "AMNT = "+Str(nQtdRec,10,0)+","
			cQuery += "OPER = 'T' "
			cQuery += "WHERE "
			cQuery += "TABLENAME = '"+RetSqlName(cAlias)+ "' AND "
			cQuery += "OPER = 'T' "
			TcSqlExec(cQuery)
		EndIf
	#ELSE
		dbSelectArea("HHT")
		dbSetOrder(2)
		If MsSeek(cAlias)
			If HHT->HHT_GEN == "2" // Tabela Nao Generica
				cSeek := cID+cTable
				dbSelectArea("HHCTR")
				dbSetOrder(1)
			Else				  // Tabela Generica
				cSeek := cTable
				dbSelectArea("HHCTR")
				dbSetOrder(2)
			EndIf			
			dbSelectArea("HHCTR")
			If MsSeek(cSeek)
				While !Eof() .And. IIf(HHT->HHT_GEN=="2",HHCTR->USERID == cID,.T.) .And. HHCTR->TABLENAME==RetSqlName(cAlias)
					Begin Transaction
					RecLock("HHCTR")
					HHCTR->MAXVERSION := nMaxVer
					HHCTR->AMNT       := nQtdRec
					HHCTR->OPER       := IIF(HHCTR->OPER=="T","T","P")
					MsUnLock()
					End Transaction
					dbSelectArea("HHCTR")
					dbSkip()					
				EndDo
			EndIf
		EndIf		
	#ENDIF
EndIf
RestArea(aArea2)
RestArea(aArea)
Return   


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³RetDbfNameºAutor  ³Rodrigo  A. Godinho º Data ³  02/03/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna o nome fisico da tabela.                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cAlias - alias da tabela.                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³String com nome fisico da tabela.                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³HHXAPI                                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
#IFNDEF TOP
	Static Function RetDbfName(cAlias)
	Local aArea		:=	GetArea()
	Local aAreaSX2	:=	SX2->(GetArea())
	Local cRet		:=	"" 
	
	dbSelectArea("SX2")
	SX2->(dbSetOrder(1))
	
	If SX2->(dbSeek(cAlias))
		cRet := SX2->X2_ARQUIVO
	EndIf
	
	RestArea(aAreaSX2)
	RestArea(aArea)
	Return cRet
#ENDIF

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³HHMail    ³Autor  ³Eduardo Riera          ³ Data ³09/03/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Envio de email após sincronizacao dos dados                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: E-mail com os destinatários.                         ³±±
±±³          ³ExpA2: Dados da tabela                                      ³±±
±±³          ³ExpA3: Dados da seção observação                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function HHMail(aTo,aTable,aObs,aErro)

Local cHeader   := ""
Local cTable    := ""
Local cTail     := ""
Local cSubject  := STR0001+aTable[1][2] // //"Informativo - Vendedor: "
Local aUsrMsg   := {}
Local nX        := 0
Local nMail     := SuperGetMv("MV_HHMAIL",.T.,1) // 1 - Não Envia E-mail; 2 - Envia quando ocorrer erro; 3 - Sempre Envia
Local cMailTo   := GetNewPar("MV_HHMAIL2","")
Local cMensagem := ""
Local cAdmMail   := UsrRetMail("000000")

DEFAULT aErro := {}
DEFAULT aObs  := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se for uma ocorrencia de erro envia os dados para o administrador do sistema     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aErro)>0
	aadd(aTo,cAdmMail)
EndIf
If !Empty(cMailTo)
	aadd(aTo,cMailTo)
EndIf

cHeader += '<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">'
cHeader += '<html>'
cHeader += '<head>'
cHeader += '<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">'
cHeader += '<title>Microsiga Software SA</title>'
cHeader += '</head>'
cHeader += '<body>'
cHeader += '<table style="width: 614px; height: 168px;" border="0" cellpadding="0" cellspacing="0">'
cHeader += '<font face="verdana, tahoma, arial, helvetica" size="1">'
cHeader += '<tbody>'
cHeader += '<tr>'
cHeader += '<td>'
cHeader += '<h2 style="text-align: center;"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
cHeader += '***Esse é um e-mail automático. Não é necessário respondê-lo ***'
cHeader += '</font></h2>'
cHeader += '</td>'
cHeader += '</tr>'
cHeader += '<tr>'
cHeader += '<td>'
cHeader += '<h3 style="text-align: center;"><span style="text-decoration: underline;">'
cHeader += 'Informativo de sincronização'
cHeader += '</span></h3>'
cHeader += '<div style="text-align: justify;"><br>'

For nX := 1 To Len(aTable)
	If nX == 1
		cTable  += '<table style="text-align: left; width: 609px; height: 56px;" border="0" cellpadding="2" cellspacing="2">'
		cTable  += '<tbody>'
	EndIf
	cTable  += '<tr>'
	cTable  += '<td style="background-color: rgb(234, 234, 234); height: 25%; width: 25%;">'
	cTable  += AllTrim(aTable[nX][1])+" :"
	cTable  += '</td>'
	cTable  += '<td style="background-color: rgb(214, 214, 214); ">'
	cTable  += aTable[nX][2]
	cTable  += '</td>'
	cTable  += '</tr>
	If nX == Len(aTable)
		cTable  +='</tbody>
		cTable  +='</table>	
	EndIf
Next nX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada de complemento do conteúdo no E-mail
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("HHMAIL01")
	aUsrMsg := ExecBlock("HHMAIL01",.F.,.F.)
	If Len(aUsrMsg) > 0
		cSubject := If(!Empty(aUsrMsg[1]),aUsrMsg[1],cSubject)
		cTable   += aUsrMsg[2]
		cTail    += aUsrMsg[3]
	EndIf
EndIf

cTail   +='<br><br><span style="font-weight: bold; text-decoration: underline;">'
cTail   +='Observações'
cTail   +='</span><font face="verdana, tahoma, arial, helvetica">'
cTail   +='</font><br><br>'
For nX := 1 To Len(aObs)
	cTail += aObs[nX]
	cTail += '<br>'
Next nX


If Len(aErro)>0
	cTail += '<br>'
	cTail   +='<br><span style="font-weight: bold; text-decoration: underline;">'
	cTail   +='Messagem de erro'
	cTail   +='</span><font face="verdana, tahoma, arial, helvetica">'
	cTail   +='</font><br><br>'
	For nX := 1 To Len(aErro)
		cTail += aErro[nX]
		cTail += '<br>'
	Next nX
EndIf
cTail   +='</div>'
cTail   +='</td>'
cTail   +='</tr>'
cTail   +='</tbody>'
cTail   +='</font>'
cTail   +='</table>'
cTail   +='<br>'
cTail   +='<br>'
cTail   +='</body>'
cTail   +='</html>'

If (nMail == 2 .And. Len(aErro) > 0) .Or. nMail == 3
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de entrada de complemento dos destinatarios de E-mail
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If ExistBlock("HHMAIL02")
		aTo := ExecBlock("HHMAIL02",.F.,.F.,{aTo})
	EndIf	
	
	For nX := 1 To Len(aTo)
		If !Empty(aTo[nX])
			MConnect(cHeader+cTable+cTail,{{"",aTo[nX]}},cSubject,.T.,.F.)
		EndIf
	Next nX
	MDisconnect()
EndIf

cMensagem := "MOBILE SYNC :"+CRLF
cMensagem += Space(12)
cMensagem += 'Informativo de sincronização'+CRLF
For nX := 1 To Len(aTable)
	cMensagem += Space(12)
	cMensagem += aTable[nX][1]+": "+aTable[nX][2]+CRLF	
Next nX
If Len(aObs) > 0
	cMensagem += Space(12)
	cMensagem += 'Observações'+CRLF
	For nX := 1 To Len(aObs)
		cMensagem += Space(12)
		cMensagem += aObs[nX]+CRLF
	Next nX
EndIf
If Len(aErro) > 0
	cMensagem += Space(12)
	cMensagem += 'Messagem de erro'+CRLF
	For nX := 1 To Len(aErro)
		cMensagem += Space(12)
		cMensagem += aErro[nX]+CRLF
	Next nX
EndIf
ConOut(cMensagem)

Return(.T.)
