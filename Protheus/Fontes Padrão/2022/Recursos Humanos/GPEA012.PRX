#INCLUDE "PROTHEUS.CH" 
#INCLUDE 'GPEA012.CH'
#INCLUDE 'FWMVCDEF.CH'

/*/


Ŀ
Funcao     GPEA012   Autor  Leandro Drumond                    Data  10/09/2014 
Ĵ
Descricao  Cadastro Refeicao                                                        
Ĵ
Sintaxe    GPEA012()                                                                
Ĵ
Uso        Generico                                                                 
Ĵ
            ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL                         
Ĵ
Programador  Data      FNC              Motivo da Alteracao                       
Ĵ
Renan Borges09/10/2014          TQODYYCorrigir error.log ao cadastrar a rotina na 
                                      rotina de privilgios.                      
Christiane V08/04/2015    PCREQ-2683  Criao do campo Ref.Contrato e incluso de 
                                      consistncias para o mesmo.                 
Gabriel A.  13/05/2015PCDEF-31989     Alteraes realizadas para vincular o       
                                      fonte ao requisito PCDEF-31989.             
Ĵ
ٱ

*/
Function GPEA012()
Local cFiltraRh
Local oBrwRFO  
Local oDlg
Local oRadio
Local nRadio
Local nOpca := 1
 
Private nTpVale	 := 1
Private lIntRHGS := fIntRHGS()

oBrwRFO := FWmBrowse():New()		
oBrwRFO:SetAlias( 'RFO' )
oBrwRFO:SetDescription( STR0009 )   //"Definicao de Vale Refeio"
		
//Inicializa o filtro utilizando a funcao FilBrowse
cFiltraRh := CHKRH("GPEA012","RFO","1")

If !Empty(cFiltraRh)
	cFiltraRh += " .And. "
EndIf 

cFiltraRh += "RFO_TPVALE == '" +CValtoChar(nTpVale) + "'"

//Filtro padrao do Browse conforme tabela RFO (Definicao de Beneficios)
oBrwRFO:SetFilterDefault(cFiltraRh)
	
oBrwRFO:Activate()    

Return( Nil )

/*/


Ŀ
Funo    MenuDef      Autor  Equipe RH Inovacao     Data  14/01/13 
Ĵ
Descrio Menu Funcional                                                
Ĵ
Sintaxe    MenuDef()                                                    
Ĵ
Parametros                                                              
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina := {}
	
	ADD OPTION aRotina Title STR0004  Action 'PesqBrw'			OPERATION 1 ACCESS 0 //"Pesquisar"
	ADD OPTION aRotina Title STR0005  Action 'VIEWDEF.GPEA012'	OPERATION 2 ACCESS 0 //"Visualizar"
	ADD OPTION aRotina Title STR0006  Action 'VIEWDEF.GPEA012'	OPERATION 3 ACCESS 0 //"Incluir"
	ADD OPTION aRotina Title STR0007  Action 'VIEWDEF.GPEA012'	OPERATION 4 ACCESS 0 //"Atualizar"
	ADD OPTION aRotina Title STR0008  Action 'VIEWDEF.GPEA012'	OPERATION 5 ACCESS 0 //"Excluir"
	
Return aRotina

/*/


Ŀ
Funo    ModelDef     Autor  Equipe RH Inovacao     Data  14/01/13 
Ĵ
Descrio Modelo de dados e Regras de Preenchimento para o Cadastro     
                                                                        
Ĵ
Sintaxe    ModelDef()                                                   
Ĵ
Parametros                                                              
Ĵ
 Uso                                                                    
ٱ


/*/
Static Function ModelDef()	
// Cria a estrutura a ser usada no Modelo de Dados
Local oStruRFO := FWFormStruct( 1, 'RFO', /*bAvalCampo*/,/*lViewUsado*/ )
Local oStruRFP := FWFormStruct( 1, 'RFP', /*bAvalCampo*/,/*lViewUsado*/ )
Local oModel
      
// Blocos de codigo do modelo
Local bPosValid 	:= { |oModel| Gp012PosVal( oModel )}

If (Type("nTpVale") == "U")
	nTpVale := 1
EndIf

oStruRFP:SetProperty( 'RFP_PRODSX' , MODEL_FIELD_WHEN,{|oMdl| HabilitCpo('RFP_PRODSX', 'GPEA012_MRFO') })
oStruRFP:SetProperty( 'RFP_FORMSX' , MODEL_FIELD_WHEN,{|oMdl| HabilitCpo('RFP_FORMSX', 'GPEA012_MRFO') })

// Inicializa campos
oStruRFO:SetProperty( "RFO_TPVALE", MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'"+StrZero(nTpVale,1)+"'" ) )

oStruRFP:SetProperty( "RFP_TPVALE", MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'"+StrZero(nTpVale,1)+"'" ) )

// Cria o objeto do Modelo de Dados
oModel := MPFormModel():New('GPEA012', /*bPreValid*/, bPosValid, /*bCommit*/, /*bCancel*/ )

// Adiciona ao modelo uma estrutura de formulrio de edio por campo
oModel:AddFields( 'GPEA012_MRFO', /*cOwner*/, oStruRFO, /*bLOkVld*/, /*bTOkVld*/, /*bCarga*/ )

// Adiciona Grid no modelo
oModel:AddFields( 'GPEA012_MRFP', 'GPEA012_MRFO', oStruRFP, /*bLOkVld*/, /*bTOkVld*/, /*bCarga*/ )

// Faz o Relacionamento dos arquivos //-RFO_FILIAL+RFO_TPVALE+RFO_CODIGO+RFO_TPBEN ### RFP_FILIAL+RFP_TPVALE+RFP_CODIGO+RFP_TPBEN
oModel:SetRelation( 'GPEA012_MRFP', {{"RFP_FILIAL", 'xFilial( "RFP" )'}, {"RFP_TPVALE", 'RFO_TPVALE'}, {"RFP_CODIGO", 'RFO->RFO_CODIGO'}, {"RFP_TPBEN", 'RFO->RFO_TPBEN'}}, RFP->(IndexKey(1)) )

// Adiciona a descricao do Componente do Modelo de Dados
If FunName() == "GPEA013"
	oModel:SetDescription( STR0016 )   //"Definicao de Vale Alimentao"
Else
	oModel:SetDescription( STR0009 )   //"Definicao de Vale Refeio"
EndIf

//--Valida se o model deve ser ativado
oModel:SetVldActivate( { |oModel| fVldModel(oModel,oModel:GetOperation()) } )
	
Return oModel

/*/


Ŀ
Funo    ViewDef      Autor  Equipe RH Inovacao     Data  14/01/13 
Ĵ
Descrio  Visualizador de dados do Cadastro de Vale Refeicao           
Ĵ
Sintaxe    ViewDef()                                                    
Ĵ
Parametros                                                              
Ĵ
 Uso                                                                    
ٱ

/*/
Static Function ViewDef()
// Cria um objeto de Modelo de Dados baseado no ModelDef do fonte informado
Local oModel   := FWLoadModel( 'GPEA012' )

// Cria a estrutura a ser usada na View
Local oStruRFO := FWFormStruct( 2, 'RFO' )
Local oStruRFP := FWFormStruct( 2, 'RFP' )
Local oView

Local lIntRHGS := fIntRHGS()

// Cria o objeto de View
oView := FWFormView():New()

// Define qual o Modelo de dados ser utilizado
oView:SetModel( oModel )

//Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)
oView:AddField( 'GPEA012_VRFO', oStruRFO, 'GPEA012_MRFO' )

//Adiciona Grid na interface
oView:AddField( 'GPEA012_VRFP', oStruRFP, 'GPEA012_MRFP' )

oStruRFO:RemoveField("RFO_TPVALE")
If !lIntRHGS
	oStruRFO:RemoveField("RFO_CONTRA")
Endif
oStruRFP:RemoveField("RFP_TPVALE")
oStruRFP:RemoveField("RFP_CODIGO")
oStruRFP:RemoveField("RFP_TPBEN")
oStruRFP:RemoveField("RFP_CDOPSX")
oStruRFP:RemoveField("RFP_LINHSX")
oStruRFP:RemoveField("RFP_VBRECA")
oStruRFP:RemoveField("RFP_TKCDOP")
oStruRFP:RemoveField("RFP_TKCDBL")
oStruRFP:RemoveField("RFP_TKTPBL")

// Criar um "box" horizontal para receber algum elemento da view
oView:CreateHorizontalBox( 'FORMRFO' , 50 )
oView:CreateHorizontalBox( 'FORMRFP' , 50 )

// Relaciona o ID da View com o "box" para exibicao
oView:SetOwnerView( 'GPEA012_VRFO', 'FORMRFO' )
oView:SetOwnerView( 'GPEA012_VRFP', 'FORMRFP' )
	
Return oView


/*/


Ŀ
Funo    Gp012PosVal  Autor  Equipe RH Inovacao     Data  14/01/13 
Ĵ
Descrio  Pos validacao do Vale Refeicao                               
Ĵ
Sintaxe    Gp012PosVal( oModel )                                        
Ĵ
Parametros oModel = objeto do modelo                                    
Ĵ
 Uso       ModelDef()                                                   
ٱ

/*/
Static Function Gp012PosVal( oModel )
Local lRetorno 	:= .T.
Local cMyVale	:= cValtoChar(Iif( Type("nTpVale") == "U", 1, nTpVale ) )
Local cMyCod	:= ""
Local nOperation
Local oMyMdl 	:= oModel:GetModel("GPEA012_MRFP")

// Seta qual e a operacao corrente
nOperation := oModel:GetOperation()

If nOperation == MODEL_OPERATION_INSERT .or. ( nOperation == MODEL_OPERATION_UPDATE .and. ( oMyMdl:GetValue('RFP_CODIGO') <> RFP->RFP_CODIGO ) )
	oModel:GetModel("GPEA012_MRFO"):LoadValue('RFO_TPVALE',cMyVale)
	oMyMdl:LoadValue('RFP_TPVALE',cMyVale)
	
	//-Chave Unica-RFP_FILIAL+RFP_TPVALE+RFP_CODIGO+RFP_TPBEN
	cMyCod	 := oMyMdl:GetValue('RFP_CODIGO')
	
	dbSelectArea( "RFP" )
	If dbSeek(xFilial("RFP") + cMyVale + cMyCod ,.F.)			
		Help(" ",1,"EXISTEBEN")
		lRetorno := .F.		
	EndIf
EndIf    
	
Return( lRetorno )

/*/


Ŀ
Funo    Gp012PosVal  Autor  Equipe RH Inovacao     Data  14/01/13 
Ĵ
Descrio  Pre validacao do Vale Refeicao                               
Ĵ
Sintaxe    Gp012PreVal( oModel )                                        
Ĵ
Parametros oModel = objeto do modelo                                    
Ĵ
 Uso       ModelDef()                                                   
ٱ

/*/
Static Function fVldModel( oModel, nOperation )
Local lRetorno 	:= .T.
Local cMyVale	:= cValtoChar(Iif( Type("nTpVale") == "U", 1, nTpVale ) )
Local cMyCod	:= ""
Local oMyMdl 	:= oModel:GetModel("GPEA012_MRFO")
Local cTipPesq	:= ""
Local cVale		:= ""

If (Type("nTpVale") == "U")
	nTpVale := 1
EndIf

cTipPesq := IIF(nTpVale == 1, "VR", "VA")

//No permitir alterao ou excluso se existir lanamento de vnculos para o vale
If ( nOperation == 4 .Or.  nOperation == 5 ) .and. RFO->RFO_CONTRA == "1"  
	cVale := RFO->RFO_CODIGO

	lRetorno := !fTemVincul(cTipPesq, cVale)
	If !lRetorno
		Help( ,, OemToAnsi(STR0017),, OemToAnsi(STR0018)+ " " + OemToAnsi(STR0019), 1, 0 ) //"Ateno" ## "O vale selecionado  referente a contrato e possui lanamento de vnculos efetuado. Selecione a opo de visualizao."
	Endif
EndIf    
	
Return( lRetorno )

/*


ͻ
Programa  GPEA012   Autor  Microsiga            Data  22/01/2013  
͹
Desc.      Usado no Valid do campo RFO_TPBEN:                         
͹
Uso        X3_VALID                                                   
͹
Sintaxe    AtuCpoDet1                                                 
ͼ

*/
Function AtuCpoDet1()
Local lRet 		:= .T.
Local oMdl    	:= FWModelActive()
Local oMyMdl01 	:= oMdl:GetModel("GPEA012_MRFO")
Local oMyMdl02 	:= oMdl:GetModel("GPEA012_MRFP")
Local cMyCod   	:= oMyMdl01:GetValue('RFO_CODIGO')
Local cMyBen   	:= oMyMdl01:GetValue('RFO_TPBEN')
	
oMyMdl02:SetValue( 'RFP_CODIGO', cMyCod )
oMyMdl02:SetValue( 'RFP_TPBEN',  cMyBen )
oMyMdl02:LoadValue("RFP_PRODSX",oMyMdl02:GetValue("RFP_PRODSX")) //Utilizado para reavaliar o WHEN dos campos da RFP

Return lRet


/*


ͻ
Programa  GPEA012   Autor  Microsiga            Data  22/01/2013  
͹
Desc.      Usado no Valid do campo RFO_TPBEN:                         
͹
Uso        X3_VALID                                                   
͹
Sintaxe    AtuCpoDet1                                                 
ͼ

*/
Function fTemVincul(cTpPesq, cVale)
	Local lRet 		:= .F.
	Local cCriter	:= fRetCriter()
	
	DbSelectARea("SLY")
	SLY->(dbSetOrder(3))
	If DbSeek(xFilial("SLY") + cCriter + cTpPesq + cVale)
		lRet := .T.
	Endif
	
Return lRet