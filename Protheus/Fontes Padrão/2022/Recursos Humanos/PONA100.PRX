#INCLUDE "PONA100.CH"
#INCLUDE "PROTHEUS.CH"
 
Static lPort1510 	:= Port1510() 	//Verifica se Portaria 1510/2009 esta em vigor.
/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё PONA100  Ё Autor Ё J.Ricardo             Ё Data Ё 20/03/96 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Cadastro de Eventos                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё PONA100()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico 												  Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁLeandro Dr. Ё14/04/14Ё      ЁRetirada de ajustes, database e FieldPos  Ё╠╠
╠╠Ё            Ё        Ё      Ёque nao serao utilizados na P12.		  Ё╠╠
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/     

Function PONA100

Local aArea			:= GetArea()
Local aIndexSP9		:= {}
Local cFiltraSP9	    := ""

Private cCadastro := OemToAnsi( STR0009 ) // "Cadastro de Eventos"

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё So Executa se os Modos de Acesso dos Arquivos Relacionados esЁ
Ё tiverm OK.											  	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF ValidArqPon()
   	
	Private aRotina := MenuDef()
	Private bFiltraBrw := { || NIL }

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Inicializa o filtro utilizando a funcao FilBrowse                      Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	cFiltraRh := CHKRH("PONA100","SP9","1")
	bFiltraBrw 	:= {|| FilBrowse("SP9",@aIndexSP9,@cFiltraRH) }
	Eval(bFiltraBrw)
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Endereca a funcao de BROWSE                                  Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	dbSelectArea ( "SP9" )
	dbGoTop()
	mBrowse( 6 , 1 , 22 ,75 , "SP9" )
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Deleta o filtro utilizando a funcao FilBrowse                     	 Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	EndFilBrw("SP9",aIndexSP9)

EndIF
	
RestArea( aArea )

Return( NIL )

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona100Inc		ЁAutorЁMarinaldo de Jesus Ё Data Ё25/07/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁChamada a AxInclui com Validacao no TudoOk                  Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA100                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁNIL															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona100Inc( cAlias , nReg , nOpcx )
Return( AxInclui( cAlias , nReg , nOpcx , NIL , NIL , NIL , "Pona100TudOk()" ) )

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona100Alt		ЁAutorЁMarinaldo de Jesus Ё Data Ё25/07/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁChamada a AxAltera com Validacao no TudoOk                  Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA100                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁNIL															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona100Alt( cAlias , nReg , nOpcx )
Return( AxAltera( cAlias , nReg , nOpcx , NIL , NIL , NIL , NIL , "Pona100TudOk()" ) )

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona100TudOk	ЁAutorЁMarinaldo de Jesus Ё Data Ё19/03/2003Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁValida o Conteudo da Enchoice                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA100                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁNIL															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona100TudOk()

Local lTudoOk	:= .T.
Local cMsgInfo	:= ""
Local cClasEv	:= ""
Local cIdPon	:= ""

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё  Valida o Campo P9_CODFOL de acordo com o Tipo se Evento de Provento soЁ
Ё  permite Codigo para a Folha de Provento ou Base, se Evento de DescontoЁ
Ё  permite Codigo para a Folha de Desconto ou Base. Eventos de Base   naoЁ
Ё  possuem Validacao													 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF ( M->P9_TIPOCOD $ "1_2" ) .and. !Empty( M->P9_CODFOL )
	IF ( M->P9_TIPOCOD == "1" )
		IF !( lTudoOk := ( PosSrv( M->P9_CODFOL , cFilAnt , "RV_TIPOCOD" ) $ "1_3" ) )
			cMsgInfo := STR0024	//"Para Evento do Tipo Provento so pode ser vinculada"
			cMsgInfo += CRLF
			cMsgInfo += STR0025	//"Verba da Folha, tambem, do Tipo Provento ou de Base."
			cMsgInfo += CRLF
			cMsgInfo += CRLF
			cMsgInfo += STR0026	//"Acerte o Codigo para Folha informando uma verba valida."
		EndIF	                                                             
	Else
		IF !( lTudoOk := ( PosSrv( M->P9_CODFOL , cFilAnt , "RV_TIPOCOD" ) $ "2_3" ) )
			cMsgInfo := STR0027	//"Para Evento do Tipo Desconto so pode ser vinculada"
			cMsgInfo += CRLF
			cMsgInfo += STR0028	//"Verba da Folha, tambem, do Tipo Desconto ou de Base."
			cMsgInfo += CRLF
			cMsgInfo += CRLF
			cMsgInfo += STR0029	//"Acerte o Codigo para Folha informando uma verba valida."
		EndIF	
	EndIF
	IF !( lTudoOk )
		MsgInfo( OemToAnsi( cMsgInfo ) , OemToAnsi( STR0030 ) ) //"Aviso de Inconsitencia!"
	EndIF                   
EndIF

IF ( ( lTudoOk ) .and. !( M->P9_BHNATE >= M->P9_BHNDE ) )
	lTudoOk		:= .F.
	cMsgInfo	:= STR0031	//"Conteudo do Campo 'Horas De' Maior "
	cMsgInfo	+= CRLF
	cMsgInfo	+= STR0032	//"que do Campo 'Horas Ate' ."
	MsgInfo( OemToAnsi( cMsgInfo ) , OemToAnsi( STR0030 ) ) //"Aviso de Inconsitencia!"
EndIF

IF ( ( lPort1510 ) .and. ( lTudoOk ) .and. !( Empty( M->P9_IDPON ) ) )
	cClasEv     := M->P9_CLASEV
	cIdPon		:= M->P9_IDPON
	
	If cClasEv == "01"
		lTudoOk := cIdPon $ ('029A|038N')  //Eventos classificados como HE nao devem possuir identificadores de ponto
	ElseIf cClasEv == "02"
		lTudoOk := cIdPon $ ('007N|008A|009N|010A|032A|033N|034A|035N')
	ElseIF cClasEv == "03"
		lTudoOk := cIdPon $ ('011N|012A|021N|022A')
	ElseIF cClasEv == "04"
		lTudoOk := cIdPon $ ('019N|020A')
	ElseIF cClasEv == "05"
		lTudoOk := cIdPon $ ('013N|014A')
	Else
		lTudoOk := .T. //!cIdPon $ ('007N|008A|009N|010A|032A|033N|034A|035N|013N|014A|011N|012A|021N|022A|019N|020A')
	EndIf

	If !lTudoOk
		cMsgInfo	:= STR0033	//"ClassificaГЦo nЦo esta de acordo com"
		cMsgInfo	+= CRLF
		cMsgInfo	+= STR0034	//"o identificador de ponto selecionado."
		MsgInfo( OemToAnsi( cMsgInfo ) , OemToAnsi( STR0030 ) ) //"Aviso de Inconsitencia!"
	EndIf
EndIF

Return( lTudoOk )

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona100ChkDel	ЁAutorЁMarinaldo de Jesus Ё Data Ё30/08/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerificar se um Evento Pode Ser Deletado					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Firmais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA100                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁNIL															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Firmais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona100ChkDel( cAlias , nReg , nOpcx )

Local aArea			:= GetArea()
Local aAreas		:= {}
Local cFilSP9		:= xFilial( "SP9" )
Local cEvento		:= SP9->P9_CODIGO
Local cMsgYesNo		:= ""
Local cTitLog		:= STR0018	//"Log de Ocorrencias na Exclusao de Eventos do Ponto"
Local lDelOk		:= .T.
Local nAreas		:= 0

//SPK
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPK->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PK_FILIAL"
				aAreas[nAreas,02,02] := "PK_CODEVE"
aAreas[nAreas,03] := RetOrdem( "SPK" , "PK_FILIAL+PK_CODEVE" , .T. )

//SPC
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPC->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PC_FILIAL"
				aAreas[nAreas,02,02] := "PC_PD"
aAreas[nAreas,03] := RetOrdem( "SPC" , "PC_FILIAL+PC_PD" , .T. )

//SPC
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPC->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PC_FILIAL"
				aAreas[nAreas,02,02] := "PC_PDI"
aAreas[nAreas,03] := RetOrdem( "SPC" , "PC_FILIAL+PC_PDI" , .T. )

//SPH
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPH->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PH_FILIAL"
				aAreas[nAreas,02,02] := "PH_PD"
aAreas[nAreas,03] := RetOrdem( "SPH" , "PH_FILIAL+PH_PD" , .T. )

//SP4
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SP4->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "P4_FILIAL"
				aAreas[nAreas,02,02] := "P4_CODNAUT"
aAreas[nAreas,03] := RetOrdem( "SP4" , "P4_FILIAL+P4_CODNAUT" , .T. )

//SP4
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SP4->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "P4_FILIAL"
				aAreas[nAreas,02,02] := "P4_CODAUT"
aAreas[nAreas,03] := RetOrdem( "SP4" , "P4_FILIAL+P4_CODAUT" , .T. )

//SP6
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SP6->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "P6_FILIAL"
				aAreas[nAreas,02,02] := "P6_EVENTO"
aAreas[nAreas,03] := RetOrdem( "SP6" , "P6_FILIAL+P6_EVENTO" , .T. )

//SPI
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPI->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PI_FILIAL"
				aAreas[nAreas,02,02] := "PI_PD"
aAreas[nAreas,03] := RetOrdem( "SPI" , "PI_FILIAL+PI_PD" , .T. )  

//SPM
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPM->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PM_FILIAL"
				aAreas[nAreas,02,02] := "PM_PD"
aAreas[nAreas,03] := RetOrdem( "SPM" , "PM_FILIAL+PM_PD" , .T. )

//SPM
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPM->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PM_FILIAL"
				aAreas[nAreas,02,02] := "PM_PDEMPR"
aAreas[nAreas,03] := RetOrdem( "SPM" , "PM_FILIAL+PM_PDEMPR" , .T. )

cMsgYesNo 	:= STR0010	//"O Sistema Agora Ira efetuar a Verificacao para ver se o Evento Selecionado para" 
cMsgYesNo 	+= CRLF
cMsgYesNo 	+= STR0011	//"Exclusao esta sendo utilizado. A verifica┤└o pode ser demorada !!"
cMsgYesNo 	+= CRLF
cMsgYesNo 	+= CRLF
cMsgYesNo 	+= STR0012	//"Confirma a exclus└o do Evento?"

( cAlias )->( MsGoto( nReg ) )

lDelOk := ChkDelRegs(	cAlias			,;	//01 -> Alias do Arquivo Principal
						nReg			,;	//02 -> Registro do Arquivo Principal
						nOpcx			,;	//03 -> Opcao para a AxDeleta
						cFilSP9			,;	//04 -> Filial do Arquivo principal para Delecao
						cEvento			,;	//05 -> Chave do Arquivo Principal para Delecao
						aAreas			,;	//06 -> Array contendo informacoes dos arquivos a serem pesquisados
						cMsgYesNo  		,;	//07 -> Mensagem para MsgYesNo
						cTitLog			,;	//08 -> Titulo do Log de Delecao
						NIL				,;	//09 -> Mensagem para o corpo do Log
						.T.				,;	//10 -> Se executa AxDeleta
						.T.				,;	//11 -> Se deve Mostrar o Log
						NIL				,;	//12 -> Array com o Log de Exclusao
						NIL				,;	//13 -> Array com o Titulo do Log
						NIL				,;	//14 -> Bloco para Posicionamento no Arquivo
						NIL				,;	//15 -> Bloco para a Condicao While
						NIL				 ;	//16 -> Bloco para Skip/Loop no While
					)

RestArea( aArea )

Return( lDelOk )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддддбдддддддбддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё Pn100VPerc()  Ё Autor Ё Mauricio MR      Ё Data Ё 03/01/03 Ё╠╠
╠╠цддддддддддедддддддддддддддадддддддаддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica Percentuais Complementares B.Horas e Folha	      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё PONA100                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function Pn100VPerc()
Local lRet:= .T.
Local cMsg    

If !( (M->P9_PBH+M->P9_PFOL) <= 100 )
 	cMsg := STR0023	// "A Soma dos Percentuais para B.Horas e Folha de Pagamento Ultrapassa 100%."
	MsgInfo( OemToAnsi( cMsg ) )
	lRet:=.F.
Endif

Return(lRet)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддддбдддддддбддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁP9FiltraEvento Ё Autor Ё Leandro Drumond  Ё Data Ё 14/10/09 Ё╠╠
╠╠цддддддддддедддддддддддддддадддддддаддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Filtra os eventos de hora extra.                   	      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function P9FiltraEvento()

Local cFiltro := "(SP9->P9_FILIAL == '" + xFilial("SP9") + "')"
Local cCampo  := Alltrim( ReadVar() )

If lPort1510 .and. cCampo $ ('M->P4_CODNAUT|M->P4_CODAUT')
	cFiltro += " .AND. "
	cFiltro += "(SP9->P9_CLASEV == '01')"
EndIf

cFiltro := "@#" + cFiltro + "@#"

Return( cFiltro )

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Almeida     Ё Data Ё21/11/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas blibliotecas Framework da Versao 9.12 .     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA100                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function MenuDef()
 
 Local aRotina := {;
							{ STR0004	, "PesqBrw"			, 0 , 1, ,.F. } ,; // "Pesquisar"
	                    	{ STR0005	, "AxVisual"		, 0 , 2 } ,; // "Visualizar"
	                     	{ STR0006	, "Pona100Inc"		, 0 , 3 } ,; // "Incluir"
	                     	{ STR0007	, "Pona100Alt"		, 0 , 4 } ,; // "Alterar"
	                     	{ STR0008	, "Pona100ChkDel"	, 0 , 5 }  ; // "Excluir"
	                    }  
	                    
Return aRotina