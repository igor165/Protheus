#Include "PROTHEUS.CH"
#Include "GPEA131.CH"
#INCLUDE "FWMVCDEF.CH"

Static cVALBox	:= ""
Static cVRFBox	:= ""
Static cVTRBox	:= ""
Static cFilVTRBox	:= ""

/*


Ŀ
Funcao     Gpea133   Autor  Equipe RH Inovacao            Data         15/01/14  
Ĵ
Descricao  Cadastramento de Beneficios:                                              
           -Vale Transporte, Vale Refeicao, Vale Alimentacao                         
Ĵ 
Uso        Generico                                                                  
Ĵ
              ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL                        
Ŀ
Programador    Data    FNC/Chamado      Motivo da Alteracao                        
Ĵ
Raquel Hager17/01/2014M12RH01  RQ2008  Unificacao Folha de Pagamento.              
Jaqueline L 17/01/2018DRHPAG-7577      Retirada a obrigatoriedade do preenchimento 
Jaqueline L 17/01/2018DRHPAG-7577      dos dias da semana quando o parametro       
Jaqueline L 17/01/2018DRHPAG-7577      MV_USACPER estiver como .F.                 
ٱ

*/
Function Gpea133()

Local oMBrowse
Local cFiltraRh
	
If ChecaSM7()
	
	oMBrowse := FWMBrowse():New()
	oMBrowse:SetMenuDef("GPEA133")      
	oMBrowse:SetAlias("SRA")
	oMBrowse:SetDescription(OemToAnsi(STR0046)) //"Atualizao Vales"
	
	GpLegMVC(@oMBrowse)	// Adicionar a Legenda no Browse
	
	oMBrowse:ForceQuitButton()	//"Incluir botao de sair" 
	
	/*
	Ŀ
	 Inicializa o filtro utilizando a funcao FilBrowse                      
	*/
	cFiltraRh := CHKRH("GPEA133","SRA","1")
	oMBrowse:SetFilterDefault( cFiltraRh )
	oMBrowse:DisableDetails()
	oMBrowse:Activate()

EndIf

Return

/*


Ŀ
Funcao     Menudef      Autor  Equipe RH Inovacao  Data  10/01/07 
Ĵ
Descricao  Isola opcoes de menu para que as opcoes da rotina possam   
           ser lidas pelas bibliotecas Framework da Versao 9.12.      
Ĵ
Uso        GPEA133                                                    
ٱ

*/
Static Function MenuDef()
Local aRet

Local aRotina := { 	{ OemToAnsi(STR0001) 	,"PesqBrw"	, 0 , 1, 0 ,.T.},;    			//"Pesquisar"
					{ OemToAnsi(STR0002)	,"VIEWDEF.GPEA133"	, 0 , 2, 0 , NIL},;  	//"Visualizar"
					{ OemToAnsi(STR0028) 	,"VIEWDEF.GPEA133"	, 0 , 4, 0, NIL },;  	//"Manuteno"
					{ OemToAnsi(STR0004) 	,"VIEWDEF.GPEA133"	, 0 , 4, 0, NIL},;  	//"Alterar"
					{ OemToAnsi(STR0006)	,"GpLegend"	, 0 , 5 , 0 ,.F.}}				//"Legenda"


//Ŀ
// Ponto de entrada para adicionar opcoes de menu.			      
//
If IsInCallStack("GPEA131")
	If ExistBlock( "GPA131MN")                                                 
		IF ( ValType( aRet := ExecBlock( "GPA131MN" ,.F.,.F.,{aClone(aRotina)}, .F. ) ) == "A" )
	   		aRotina := aClone(aRet)
		Endif	
	EndIf
	If funname() == "GPEA131" 
		SetFunName("GPEA133")
	EndIf                                                          
EndIf
Return( aRotina )

/*                                	
Ŀ
Funo     ModelDef 		Autor  Flavio Correa	   Data 13/07/2016
Ĵ
Descrio Regras de Modelagem da gravacao.                            
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      GPEA003                                                     
Ĵ
 Retorno  aRotina														
Ĵ
Parametros< Vide Parametros Formais >									
*/
Static Function ModelDef()
	
	Local oModel
	Local oStructSRA
	Local oSR00
	Local oSR01
	Local oSR01
	Local oSM70
	Local oSM71
	Local oSM71
	Local oSP7
	Local lUsaCPer	:= SuperGetMv("MV_USACPER",, .F.) 
	Local aGpa133FSM7 := { "M7_DIASPRO" }
	
	oModel := MPFormModel():New("GPEA133", {|oMdl| fsetOSM7(oMdl) } ,/*tudook*/,/*bCommit*/, /*bCancel*/)
	oModel:SetDescription( OemToAnsi(STR0029) ) //"Cadastro de beneficios"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                            
	
	If lUsaCPer
		aadd(aGpa133FSM7,"M7_VALDOM")
		aadd(aGpa133FSM7,"M7_VALSEG")
		aadd(aGpa133FSM7,"M7_VALTER")
		aadd(aGpa133FSM7,"M7_VALQUA")
		aadd(aGpa133FSM7,"M7_VALQUI")
		aadd(aGpa133FSM7,"M7_VALSEX")
		aadd(aGpa133FSM7,"M7_VALSAB")
		aadd(aGpa133FSM7,"M7_VALFIX")
	EndIf
	
	// Cabecalho de dados - SRA (Funcionario)//
	oStructSRA := FWFormStruct(1, "SRA",  { |cCampo| Gpa133SRAStru(cCampo) })
	oModel:AddFields("GPEA133_MSRA", NIL, oStructSRA )
	oModel:GetModel( "GPEA133_MSRA" ):SetDescription( OemToAnsi(STR0029) ) 
	oModel:GetModel('GPEA133_MSRA'):SetOnlyQuery(.T.)
	oModel:GetModel('GPEA133_MSRA'):SetOnlyView(.T.)
	
	//SP7 - Faltas
	oSP7 := FWFormStruct(1, "SP7")
	oModel:AddGrid("GPEA133_MSP7", "GPEA133_MSRA", oSP7)
	oModel:GetModel("GPEA133_MSP7" ):SetDescription( OemToAnsi(STR0001) ) //"Histrico de Vales"
	oModel:GetModel("GPEA133_MSP7" ):SetOptional( .T. )
	oModel:GetModel('GPEA133_MSP7'):SetOnlyView(.T.)
	oModel:GetModel('GPEA133_MSP7'):SetOnlyQuery(.T.)
	oModel:SetRelation( "GPEA133_MSP7", { { "P7_FILIAL", 'SRA->RA_FILIAL' }, { "P7_MAT", 'SRA->RA_MAT' }}, SP7->( IndexKey( 1 ) ) )
	
	// ************* CADASTRO *****************
	
	//SM7 - VALE TRANSPORTE
	oSM70 := FWFormStruct(1, "SM7")
	A133Str(@oSM70,"SM7")
	A133Remove(@oSM70,aGpa133FSM7)
	oSM70:SetProperty("M7_TPVALE"	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'0'"))
	oModel:AddGrid("GPEA133_MSM70", "GPEA133_MSRA", oSM70,, { |oGrid| SM7LinOk(oGrid)}, /*bPre*/, /*bPost*/, { |oGrid| CargaAux(oGrid, "SM7", "0") } )
	oModel:GetModel( "GPEA133_MSM70" ):SetUniqueLine( { 'M7_TPVALE', 'M7_CODIGO'})
	oModel:GetModel( "GPEA133_MSM70" ):SetDescription( OemToAnsi(STR0017) ) 
	oModel:GetModel( "GPEA133_MSM70" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSM70", { { "M7_FILIAL", 'SRA->RA_FILIAL' }, { "M7_MAT", 'SRA->RA_MAT' }}, SM7->( IndexKey( 1 ) ) )
	
	//SM7 - VALE REFEICAO
	oSM71 := FWFormStruct(1, "SM7")
	A133Str(@oSM71,"SM7")
	A133Remove(@oSM71,aGpa133FSM7)
	oSM71:SetProperty("M7_TPVALE"	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'1'"))
	oSM71:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| (oMdl:GetValue('M7_TPCALC') <> '2') } )
	oModel:AddGrid("GPEA133_MSM71", "GPEA133_MSRA", oSM71,, { |oGrid| SM7LinOk(oGrid)}, /*bPre*/, /*bPost*/, { |oGrid| CargaAux(oGrid, "SM7", "1") } )
	oModel:GetModel( "GPEA133_MSM71" ):SetUniqueLine( {  'M7_TPVALE', 'M7_CODIGO'} )
	oModel:GetModel( "GPEA133_MSM71" ):SetDescription( OemToAnsi(STR0018) ) 
	oModel:GetModel( "GPEA133_MSM71" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSM71", { { "M7_FILIAL", 'SRA->RA_FILIAL' }, { "M7_MAT", 'SRA->RA_MAT' }}, SM7->( IndexKey( 1 ) ) )
	
	//SM7 - VALE ALIMENTACAO
	oSM72 := FWFormStruct(1, "SM7")
	A133Str(@oSM72,"SM7")
	A133Remove(@oSM72,aGpa133FSM7)
	oSM72:SetProperty("M7_TPVALE",	MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'2'"))
	oSM72:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| (oMdl:GetValue('M7_TPCALC') <> '2') } )
	oModel:AddGrid("GPEA133_MSM72", "GPEA133_MSRA", oSM72,, { |oGrid| SM7LinOk(oGrid)}, /*bPre*/, /*bPost*/, { |oGrid| CargaAux(oGrid, "SM7", "2") } )
	oModel:GetModel( "GPEA133_MSM72" ):SetUniqueLine( { 'M7_TPVALE', 'M7_CODIGO'} )
	oModel:GetModel( "GPEA133_MSM72" ):SetDescription( OemToAnsi(STR0019) ) 
	oModel:GetModel( "GPEA133_MSM72" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSM72", { { "M7_FILIAL", 'SRA->RA_FILIAL' }, { "M7_MAT", 'SRA->RA_MAT' }}, SM7->( IndexKey( 1 ) ) )
	
	// ************** CALCULO *****************
	
	//SR0 - VALE TRANSPORTE
	oSR00 := FWFormStruct(1, "SR0")
	A133Str(@oSR00,"SR0")
	oSR00:SetProperty("R0_CODIGO" 	, MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID,"NaoVazio()"))
	oSR00:SetProperty("R0_TPVALE"	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'0'"))
	oSR00:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| .F. } )
	oSR00:SetProperty("R0_PEDIDO"	, MODEL_FIELD_WHEN, {|oMdl| .T. } )
	oModel:AddGrid("GPEA133_MSR00", "GPEA133_MSRA", oSR00, { |oGrid, nLine, cAction, cField| A133DelOk(oGrid, nLine, cAction, cField) }, { |oGrid| BenLin133Ok(oGrid) }, /*bPre*/, /*bPost*/, { |oGrid| CargaAux(oGrid, "SR0", "0") } )
	oModel:GetModel( "GPEA133_MSR00" ):SetUniqueLine( { 'R0_FILIAL', 'R0_MAT', 'R0_TPVALE', 'R0_CODIGO','R0_NROPED'} )
	oModel:GetModel( "GPEA133_MSR00" ):SetDescription( OemToAnsi(STR0029) ) 
	oModel:GetModel( "GPEA133_MSR00" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSR00", { { "R0_FILIAL", 'SRA->RA_FILIAL' }, { "R0_MAT", 'SRA->RA_MAT' }}, SR0->( IndexKey( 1 ) ) )
	
	//SR0 - VALE REFEICAO
	oSR01 := FWFormStruct(1, "SR0")
	A133Str(@oSR01,"SR0")
	oSR01:SetProperty("R0_CODIGO" 	, MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID,"NaoVazio()"))
	oSR01:SetProperty("R0_TPVALE"	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'1'"))
	oSR01:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| .F. } )
	oSR01:SetProperty("R0_PEDIDO"	, MODEL_FIELD_WHEN, {|oMdl| .T. } )
	oModel:AddGrid("GPEA133_MSR01", "GPEA133_MSRA", oSR01,{ |oGrid, nLine, cAction, cField| A133DelOk(oGrid, nLine, cAction, cField) }, { |oGrid| BenLin133Ok(oGrid) }, /*bPre*/, /*bPost*/, { |oGrid| CargaAux(oGrid, "SR0", "1") } )
	oModel:GetModel( "GPEA133_MSR01" ):SetUniqueLine( { 'R0_FILIAL', 'R0_MAT', 'R0_TPVALE', 'R0_CODIGO','R0_NROPED'} )
	oModel:GetModel( "GPEA133_MSR01" ):SetDescription( OemToAnsi(STR0029) ) 
	oModel:GetModel( "GPEA133_MSR01" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSR01", { { "R0_FILIAL", 'SRA->RA_FILIAL' }, { "R0_MAT", 'SRA->RA_MAT' }}, SR0->( IndexKey( 1 ) ) )
	
	//SR0 - VALE ALIMENTACAO
	oSR02 := FWFormStruct(1, "SR0")
	A133Str(@oSR02,"SR0")
	oSR02:SetProperty("R0_CODIGO" ,MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID,"NaoVazio()"))
	oSR02:SetProperty("R0_TPVALE",	MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'2'"))
	oSR02:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| .F. } )
	oSR02:SetProperty("R0_PEDIDO"	, MODEL_FIELD_WHEN, {|oMdl| .T. } )
	oModel:AddGrid("GPEA133_MSR02", "GPEA133_MSRA", oSR02,{ |oGrid, nLine, cAction, cField| A133DelOk(oGrid, nLine, cAction, cField) }, { |oGrid| BenLin133Ok(oGrid) }, /*bPre*/, /*bPost*/, { |oGrid| CargaAux(oGrid, "SR0", "2") } )
	oModel:GetModel( "GPEA133_MSR02" ):SetUniqueLine( { 'R0_FILIAL', 'R0_MAT', 'R0_TPVALE', 'R0_CODIGO','R0_NROPED'} )
	oModel:GetModel( "GPEA133_MSR02" ):SetDescription( OemToAnsi(STR0029) ) 
	oModel:GetModel( "GPEA133_MSR02" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSR02", { { "R0_FILIAL", 'SRA->RA_FILIAL' }, { "R0_MAT", 'SRA->RA_MAT' }}, SR0->( IndexKey( 1 ) ) )
	
Return( oModel )

/*                                	
Ŀ
Funo     ViewDef  		Autor  Flavio Correa	  Data 13/07/2016
Ĵ
Descrio Regras de Interface com o Usuario                           
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      GPEA003                                                     
Ĵ
 Retorno  aRotina														
Ĵ
Parametros< Vide Parametros Formais >									
*/
Static Function ViewDef()
Local oView 
Local oModel
Local oStructSRA
Local oSR00
Local oSR01
Local oSR02
Local oSM70
Local oSM71
Local oSM72
Local aGpa133FSR0 	:= { "R0_FILIAL", "R0_MAT", "R0_MEIO", "R0_DESC","R0_TPVALE" }
Local aGpa133FSM7 	:= { "M7_FILIAL", "M7_MAT", "M7_TPVALE","M7_DIASPRO" }
Local aTmp			:= {}
Local aButtons		:= {{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,"Salvar"},{.T.,"Fechar"},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil}}
Local lUsaCPer		:= SuperGetMv("MV_USACPER",, .F.) 
Local bFolder 		:= {|| A133Folder(oView)}
Local oSP7		

If GetMv("MV_GSPUBL", Nil, "1") == "2"
	aGpa133FSR0 := { "R0_FILIAL", "R0_MAT", "R0_CODIGO", "R0_TPVALE", "R0_VLRVALE", "R0_VLRFUNC", "R0_VLREMP", "R0_FALTAS", "R0_FERIAS", "R0_AFAST" }
EndIf

If lUsaCPer
	aadd(aGpa133FSM7,"M7_VALDOM")
	aadd(aGpa133FSM7,"M7_VALSEG")
	aadd(aGpa133FSM7,"M7_VALTER")
	aadd(aGpa133FSM7,"M7_VALQUA")
	aadd(aGpa133FSM7,"M7_VALQUI")
	aadd(aGpa133FSM7,"M7_VALSEX")
	aadd(aGpa133FSM7,"M7_VALSAB")
	aadd(aGpa133FSM7,"M7_VALFIX")
	
	aadd(aGpa133FSR0,"R0_QVALDOM")
	aadd(aGpa133FSR0,"R0_QVALSEG")
	aadd(aGpa133FSR0,"R0_QVALTER")
	aadd(aGpa133FSR0,"R0_QVALQUA")
	aadd(aGpa133FSR0,"R0_QVALQUI")
	aadd(aGpa133FSR0,"R0_QVALSEX")
	aadd(aGpa133FSR0,"R0_QVALSAB")
	aadd(aGpa133FSR0,"R0_QVALFIX")
EndIf

// Vincular o View ao Model //
oModel := FWLoadModel("GPEA133")

oView := FWFormView():New()
oView:SetModel(oModel)

// Criacao do Cabecalho - SRA (Funcionario)
oStructSRA := FWFormStruct(2, "SRA", { |cCampo| Gpa133SRAStru(cCampo) })
oStructSRA:SetNoFolder()
oView:AddField("GPEA133_VSRA", oStructSRA, "GPEA133_MSRA" )

oSM70 	:= FWFormStruct(2, "SM7")
A133Remove(@oSM70,aGpa133FSM7)
oSM70:SetProperty("M7_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(0,.T.) )
oView:AddGrid("GPEA133_VSM70", oSM70, "GPEA133_MSM70" )

oSM71 	:= FWFormStruct(2, "SM7")
A133Remove(@oSM71,aGpa133FSM7)
oSM71:SetProperty("M7_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(1,.T.) )
oView:AddGrid("GPEA133_VSM71", oSM71, "GPEA133_MSM71" )

oSM72 	:= FWFormStruct(2, "SM7")
A133Remove(@oSM72,aGpa133FSM7)
oSM72:SetProperty("M7_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(2,.T.) )
oView:AddGrid("GPEA133_VSM72", oSM72, "GPEA133_MSM72" )

oSP7 	:= FWFormStruct(2, "SP7")
oView:AddGrid("GPEA133_VSP7", oSP7, "GPEA133_MSP7" )

oSR00 	:= FWFormStruct(2, "SR0")
aTmp := aClone(aGpa133FSR0)
aadd(aTmp,"R0_TPCALC")
A133Remove(@oSR00,aTmp)
oSR00:SetProperty("R0_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(0,.T.) )
oSR00:SetProperty("R0_NROPED" , MVC_VIEW_ORDEM, "0" )
oSR00:SetProperty("R0_PEDIDO" , MVC_VIEW_ORDEM, "0" )
oView:AddGrid("GPEA133_VSR00", oSR00, "GPEA133_MSR00" )

oSR01 	:= FWFormStruct(2, "SR0")
aTmp := aClone(aGpa133FSR0)
aAdd(aTmp, "RG2_DIADIF")
aAdd(aTmp, "RG2_VALDIF")
aAdd(aTmp, "RG2_CUNIDF")
aAdd(aTmp, "RG2_CFUNDF")
aAdd(aTmp, "RG2_CEMPDF")
aAdd(aTmp, "RG2_VTDUTE")
aAdd(aTmp, "RG2_VTDNUT")
aAdd(aTmp, "RG2_DUTILM")
A133Remove(@oSR01,aTmp)
oSR01:SetProperty("R0_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(1,.T.) )
oSR01:SetProperty("R0_NROPED" , MVC_VIEW_ORDEM, "0" )
oSR01:SetProperty("R0_PEDIDO" , MVC_VIEW_ORDEM, "0" )
oView:AddGrid("GPEA133_VSR01", oSR01, "GPEA133_MSR01" )

oSR02 	:= FWFormStruct(2, "SR0")
aTmp := aClone(aGpa133FSR0)
aAdd(aTmp, "RG2_DIADIF")
aAdd(aTmp, "RG2_VALDIF")
aAdd(aTmp, "RG2_CUNIDF")
aAdd(aTmp, "RG2_CFUNDF")
aAdd(aTmp, "RG2_CEMPDF")
aAdd(aTmp, "RG2_VTDUTE")
aAdd(aTmp, "RG2_VTDNUT")
aAdd(aTmp, "RG2_DUTILM")
A133Remove(@oSR02,aTmp)
oSR02:SetProperty("R0_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(2,.T.) )
oSR02:SetProperty("R0_NROPED" , MVC_VIEW_ORDEM, "0" )
oSR02:SetProperty("R0_PEDIDO" , MVC_VIEW_ORDEM, "0" )
oView:AddGrid("GPEA133_VSR02", oSR02, "GPEA133_MSR02" )


// Desenho da Tela //
oView:CreateHorizontalBox("SRA_HEAD", 10)
oView:CreateHorizontalBox("SM7", 45)
oView:CreateHorizontalBox("SR0", 45)

oView:CreateFolder( 'FOLDER_SM7', 'SM7')
oView:AddSheet('FOLDER_SM7','VT1',STR0017,{|| A133Folder(oView,'FOLDER_SM7','FOLDER_SR0')})//"Vale Transporte"
oView:CreateHorizontalBox( 'VT1', 100, , , 'FOLDER_SM7', 'VT1')
oView:AddSheet('FOLDER_SM7','VR1',STR0018,{|| A133Folder(oView,'FOLDER_SM7','FOLDER_SR0')})//"Vale Refeio"
oView:CreateHorizontalBox( 'VR1', 100, , , 'FOLDER_SM7', 'VR1')
oView:AddSheet('FOLDER_SM7','VA1',STR0019,{|| A133Folder(oView,'FOLDER_SM7','FOLDER_SR0')})//"Vale Alimentao" 
oView:CreateHorizontalBox( 'VA1', 100, , , 'FOLDER_SM7', 'VA1')
oView:AddSheet('FOLDER_SM7','VSP7',STR0045)//"Faltas
oView:CreateHorizontalBox( 'VSP7', 100, , , 'FOLDER_SM7', 'VSP7')


oView:CreateFolder( 'FOLDER_SR0', 'SR0')
oView:AddSheet('FOLDER_SR0','VT',STR0017,{|| A133Folder(oView,'FOLDER_SR0','FOLDER_SM7')})//"Vale Transporte"
oView:CreateHorizontalBox( 'VT', 100, , , 'FOLDER_SR0', 'VT')
oView:AddSheet('FOLDER_SR0','VR',STR0018,{|| A133Folder(oView,'FOLDER_SR0','FOLDER_SM7')})//"Vale Refeio"
oView:CreateHorizontalBox( 'VR', 100, , , 'FOLDER_SR0', 'VR')
oView:AddSheet('FOLDER_SR0','VA',STR0019,{|| A133Folder(oView,'FOLDER_SR0','FOLDER_SM7')})//"Vale Alimentao" 
oView:CreateHorizontalBox( 'VA', 100, , , 'FOLDER_SR0', 'VA')


oView:SetOwnerView( "GPEA133_VSRA", "SRA_HEAD" )
oView:SetOwnerView('GPEA133_VSM70','VT1')
oView:SetOwnerView('GPEA133_VSM71','VR1')
oView:SetOwnerView('GPEA133_VSM72','VA1')
oView:SetOwnerView('GPEA133_VSP7','VSP7')
oView:SetOwnerView('GPEA133_VSR00','VT')
oView:SetOwnerView('GPEA133_VSR01','VR')
oView:SetOwnerView('GPEA133_VSR02','VA')


oView:addUserButton(OemToAnsi(STR0030) ,"MAGIC_BMP", {|| FWExecView(STR0030, 'GPEA132',MODEL_OPERATION_UPDATE, , { || .T. }, , ,aButtons)},   OemToAnsi(STR0030),, {MODEL_OPERATION_INSERT,MODEL_OPERATION_UPDATE,MODEL_OPERATION_VIEW } ) //"Histrico"


oView:SetCloseOnOk({ || .T. })//retira bota salvar e criar novo
Return oView

Static Function A133Folder(oView,cFolder1,cFolder2)
Local aFolder := oView:GetFolderActive(cFolder1,2)
Local aFolder1 := oView:GetFolderActive(cFolder2,2)
If Len(aFolder) > 0 .And. Len(aFolder1) > 0 .And. aFolder[1] <> aFolder1[1]
	oView:SelectFolder(cFolder2,aFolder[1],2)
EndIf
Return


/*                                	
Ŀ
Funo     A133Remove      	Autor  Flavio Correa	 Data 13/10/2011
Ĵ
Descrio Remove do View campos iguais dos grids'''''            
Ĵ
*/
Static Function A133Remove(oObj,aCampos)
Local nI 	:= 1
Local nTam 	:= Len(aCampos)
For nI := 1 To nTam
	oObj:RemoveField( aCampos[nI] )
Next nI

Return

/*                                	
Ŀ
Funo     A133Str      	Autor  Flavio Correa	 Data 13/10/2011
Ĵ
Descrio SAltera strutura comum do model para os 3 grid's            
Ĵ
*/
Static Function A133Str(oObj,cTabela)
Local lUsaCPer	:= SuperGetMv("MV_USACPER",, .F.) // .T. Cadastro de Perodo, .F. Ponto Eletrnico

If cTabela == "SR0"
	oObj:SetProperty("R0_SALBASE"	, MODEL_FIELD_WHEN, {|| .F. } )
	oObj:SetProperty("R0_TPVALE"	, MODEL_FIELD_WHEN, {|| .T. } )
	oObj:SetProperty('R0_MAT'		, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_DIASPRO'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_QDIADIF'	, MODEL_FIELD_OBRIGAT, .F. )
	If !lUsaCPer
		oObj:SetProperty('R0_QDIAINF'	, MODEL_FIELD_OBRIGAT, .F. )
	EndIf
	oObj:SetProperty('R0_VALDIF'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_QDIACAL'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_VALCAL'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_SALBASE'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_CC'		, MODEL_FIELD_OBRIGAT, .F. )
Else
	oObj:SetProperty('M7_MAT'		  , MODEL_FIELD_OBRIGAT, .F. )
	If !lUsaCPer
		oObj:SetProperty('M7_QDIAINF'	, MODEL_FIELD_OBRIGAT, .F. )
	EndIf
EndIf
Return

/*                                	
Ŀ
Funo     Gpa133SRAStru	Autor  Flavio Correa	 Data 13/10/2011
Ĵ
Descrio Selecionar os campos para a estrutura do SRA                
Ĵ
*/
Static Function Gpa133SRAStru( cCampo )
	Local lRet := .F.
	cCampo := AllTrim( cCampo )
	If cCampo $ 'RA_MAT*RA_NOME*RA_ADMISSA' 
		lRet := .T.
	EndIf
Return lRet

/*


ͻ
Funcao     BenLin133Ok   Autor  Marcos A. Stiefano  Data 19/07/95 
͹
Descricao  Critica linha digitada.      						      
͹
Uso        GPEA133							                          
ͼ

*/
Function BenLin133Ok(oGrid)
Local lRet  := .T.
Local lUsaCPer	:= SuperGetMv("MV_USACPER",, .F.) 
               
If !oGrid:IsDeleted()
	If lUsaCPer .And. Empty(oGrid:GetValue("R0_QDIAINF")) .AND.  Empty(oGrid:GetValue("M7_QDNUTIL"))
		Help(" ",1,"GP130SDIA") 
		lRet:= .F.
	EndIf
	
	If Empty(oGrid:GetValue("R0_CODIGO"))
		Help(" ",1,"GP130SMEI") 
		lRet:= .F.
	EndIf
EndIf       

Return( lRet )

/*


ͻ
Funcao     SM7LinOk      Autor  Marcos A. Stiefano  Data 19/07/95 
͹
Descricao  Critica linha digitada.      						      
͹
Uso        GPEA133							                          
ͼ

*/
Function SM7LinOk(oGrid)
Local lRet  	:= .T.
Local lUsaCPer	:= SuperGetMv("MV_USACPER",, .F.) 
Local cMsg		:= STR0031//"Integrao com ponto ligado, obrigatorio preencher a quantidade de vales de todos os dias da semana"
Local lAux 		:= .F.

If !oGrid:IsDeleted()
	If !lUsaCPer
		If Empty(oGrid:GetValue("M7_VALSEG")) .And. ;
			 Empty(oGrid:GetValue("M7_VALTER")) .And. ;
			 Empty(oGrid:GetValue("M7_VALQUA")) .And. ;
			 Empty(oGrid:GetValue("M7_VALQUI")) .And. ;
			 Empty(oGrid:GetValue("M7_VALSEX")) .And. ;
			 Empty(oGrid:GetValue("M7_VALSAB")) .And. ;
			 Empty(oGrid:GetValue("M7_VALDOM")) .And. ;
			 Empty(oGrid:GetValue("M7_QDIAINF"))
				cMsg += CRLF + " Tipo : " + oGrid:cDescription + " / Linha : " + AllTrim(Str(oGrid:nLine))
				Help( ,, "QTDVALES",, OemToAnsi(cMsg), 1,,,,,,, {OemToAnsi(STR0047)} )
				lRet:= .F.
		EndIf
	Else
		If Empty(oGrid:GetValue("M7_QDIAINF")) .AND. Empty(oGrid:GetValue("M7_QDNUTIL"))
			Help(" ",1,"GP130SDIA") 
			lRet:= .F.
		EndIf
	EndIf
EndIf       

Return( lRet )


/*


Ŀ
Funcao     CodBenef  Autor  Equipe RH Inovacao  Data  16/01/13 
Ĵ
Descricao  Monta Combo com os codigos de beneficios                   
           periodo indicado.                                          
Ĵ
Uso        GPEA133                                                    
ٱ

*/
Function CodBenef(nTpVale, lArray)
	
	Local aArea		:= GetArea()
	Local aAreaSR0	:= SR0->( GetArea() )
	Local aAreaRFO	:= RFO->( GetArea() )
	Local cOpcBox   := ""
	Local cRet
	Local cChave	:= xFilial('RFO',SRA->RA_FILIAL) + StrZero(nTpVale, 1)
	Local nTamCod	:= TamSX3("M7_CODIGO")[1]
	Local cFilSRN	:= xFilial('SRN', SRA->RA_FILIAL)
	Local lValidBloq := SRN->(FieldPos("RN_MSBLQL")) > 0
	
	DEFAULT nTpVale := 0
	DEFAULT lArray  := .F.
	
	If nTpVale == 0 .And. !Empty(cVTRBox) .And. cFilSRN == cFilVTRBox
		cRet := If( lArray, StrToArray(cVTRBox,";"), cVTRBox )
		Return cRet
	ElseIf nTpVale == 1 .And. !Empty(cVRFBox)
		cRet := If( lArray, StrToArray(cVRFBox,";"), cVRFBox )
		Return cRet
	ElseIf nTpVale == 2 .And. !Empty(cVALBox)
		cRet := If( lArray, StrToArray(cVALBox,";"), cVALBox )
		Return cRet
	EndIf
	
	dbSelectArea('SR0')
	
	If nTpVale = 0
		DbSelectArea("SRN")
		DbSetOrder(1) //-RN_FILIAL+RN_COD
		SRN->(DbGoTop())
		SRN->(dbSeek(cFilSRN))
		While SRN->(!Eof() .And. RN_FILIAL == cFilSRN)
			If !lValidBloq .Or. (lValidBloq .And. SRN->RN_MSBLQL <> "1")
				cOpcBox += SRN->( RN_COD + "=" + RN_DESC + ";" )
			EndIf
			SRN->(dbSkip())
		EndDo
		cFilVTRBox := cFilSRN
		
	Else //-nTpVale $ "1*2"
		
		DbSelectArea("RFO")
		DbSetOrder(1) //-RFO_FILIAL+RFO_TPVALE+RFO_DESCR+RFO_TPBEN+RFO_CODIGO
		RFO->(DbGoTop())
		RFO->(dbSeek(cChave))
		While RFO->( !Eof() .And. RFO_FILIAL + RFO_TPVALE == cChave)
			cOpcBox += RFO->( PadR(RFO_CODIGO, nTamCod ) + "=" + RFO_DESCR + ";" )
			RFO->(dbSkip())
		EndDo
	EndIf
	
	RestArea(aAreaSR0)
	RestArea(aAreaRFO)
	RestArea(aArea) 
	
	If nTpVale == 0
		cVTRBox := cOpcBox
	ElseIf nTpVale == 1
		cVRFBox := cOpcBox
	ElseIf nTpVale == 2
		cVALBox := cOpcBox
	EndIf
	
	If lArray 
		cRet := StrToArray(cOpcBox,";")
	Else
		cRet := cOpcBox
	EndIf
	
Return( cRet )    



/*


Ŀ
Funo     ChecaSM7     Autor Flavio Correa        Data  01/08/16 
Ĵ
Descrio  Verifica a necessidade de converter SR0 para SM7  		  
Ĵ
 Uso       GPEA133                                                    
ٱ

*/
Static Function ChecaSM7()
Local aArea		:= GetArea()
Local lEofSR0	:= .F.
Local lEofSM7	:= .F.
Local lConv		:= .T.
Local oProcess

If ChkFile("SM7")
	dbSelectArea("SM7")
	SM7->(dbSetOrder(1))
	SM7->(dbGotop())
	lEofSM7 := SM7->(Eof())
	
	dbSelectArea("SR0")
	SR0->(dbSetOrder(1))
	lEofSR0 := SR0->(Eof())
	
	If TamSx3("M7_CODIGO")[1] <> TamSx3("R0_CODIGO")[1]
		lConv := .F.
		MsgInfo( STR0048 ,STR0033 )// "Campos R0_CODIGO e M7_CODIGO devem ter o mesmo tamanho, verifique com o administrador do sistema"#"Cadastro de Benefcios"
	EndIf
	
	If lConv .And. !lEofSR0 .And. lEofSM7
		lConv := MsgNoYes(STR0032 , STR0033 ) // "Nova rotina de Benefcios identificada, porm no convertida. Deseja converter agora?"#"Cadastro de Benefcios"
		If lConv
			oProcess:=	MsNewProcess():New( {|lEnd| lConv := CONVSR0SM7(oProcess) } , OemToAnsi(STR0034) , OemToAnsi(STR0035) )     //"Processando/"Benefcios"
			oProcess:Activate()
		Else
			MsgInfo( STR0036, STR0033 ) //"Rotina no convertida. Para executar o clculo  necessrio realizar a converso" #"Cadastro de Benefcios"
		EndIF
	EndIf
Else
	lConv := .F.
	MsgInfo(STR0037 ,STR0033 )// "Tabela SM7 no encontrada, favor executar o UPDDISTR da verso 12.1.13 ou superior."#"Cadastro de Benefcios"
EndIf
RestArea(aArea)
Return lConv

/*


Ŀ
Funo     CONVSR0SM7   Autor Flavio Correa        Data  01/08/16 
Ĵ
Descrio  Atualiza SM7 de acordo com SR0							  
Ĵ
 Uso       GPEA133                                                    
ٱ

*/
Static Function CONVSR0SM7(oProcess)
Local aArea		:= GetArea()
Local lRet		:= .T.
Local nReg		:= 0
Local cNome		:= ""
Local cRoteiro	:= ""
Local aPerAtual	:= {}
Local cRot0 	:= fGetCalcRot('8')
Local cRot1 	:= fGetCalcRot('D')
Local cRot2 	:= fGetCalcRot('E') 
Local cChave	:= ""
Local aLogs		:= {}
Local aTitles	:= {}
Local aErr		:= {}
Local cNumero	:= ""

DbSelectArea("SR0")
SR0->(dbGotop())
Count To nReg  
dbSelectArea("SR0")   
SR0->( DbGoTop() )

dbSelectArea("SR0")
fCriaTemp(cNome)

SR0->( DbGoTop() )

oProcess:SetRegua1(nReg)	

SM7->(dbSetOrder(1))

dbSelectArea("SRA")
SRA->(dbSetOrder(1))

cNumero := GETSX8NUM("SR0","R0_NROPED")
ConfirmSx8()

Begin Transaction
	While !SR0->(eof())
		If SRA->(dbSeek(SR0->R0_FILIAL+SR0->R0_MAT))
			If SR0->R0_TPVALE == '0' //VTR
				cRoteiro := cRot0 
			ElseIf SR0->R0_TPVALE == '1' //VRF
				cRoteiro := cRot1 
			ElseIf SR0->R0_TPVALE == '2' //VAL
				cRoteiro := cRot2 
			Endif
					
			oProcess:IncRegua1(STR0034)//"Processando"

			//Atualiza novos campos SR0
			If cChave <> SR0->R0_FILIAL+cRoteiro+SRA->RA_PROCES
				aPerAtual := {}
				fGetPerAtual( @aPerAtual,xFilial("RCH",SR0->R0_FILIAL) ,SRA->RA_PROCES , cRoteiro )
				cChave := SR0->R0_FILIAL+cRoteiro+SRA->RA_PROCES
				If Empty( aPerAtual )
					If ascan(aErr,{|x| x==SR0->R0_FILIAL+cRoteiro+SRA->RA_PROCES}) <=0 
						aadd(aErr,SR0->R0_FILIAL+cRoteiro+SRA->RA_PROCES)
						aadd(aLogs,{STR0038 +cRoteiro + STR0039+SRA->RA_PROCES + STR0040 +xFilial("RCH",SR0->R0_FILIAL)})//"Periodo no cadastrado : Roteiro->" # " / Processo->" # " / Filial->"
					EndIf
				EndIf
			EndIf
			
			If !Empty( aPerAtual )
				RecLock("SR0",.F.)
					SR0->R0_PERIOD 	:= aPerAtual[1, 1]
					SR0->R0_NROPGT	:= aPerAtual[1, 2]
					SR0->R0_ANOMES	:= SubSTr(aPerAtual[1, 1],5,2) + SubSTr(aPerAtual[1, 1],1,4)
					SR0->R0_NROPED	:= cNumero
					SR0->R0_TPBEN	:= BuscaTBen(SR0->R0_TPVALE,SR0->R0_CODIGO	,SR0->R0_FILIAL)
					SR0->R0_ROTEIR	:= cRoteiro
					SR0->R0_PEDIDO	:= "1"
				SR0->(msUnlock())

				//gera SM7
				If !SM7->(dbSeek(SR0->R0_FILIAL+SR0->R0_MAT+SR0->R0_CODIGO+SR0->R0_TPVALE))//M7_FILIAL + M7_MAT + M7_CODIGO + M7_TPVALE
					RecLock("SM7",.T.)
						SM7->M7_FILIAL	:= SR0->R0_FILIAL
						SM7->M7_MAT		:= SR0->R0_MAT
						SM7->M7_TPVALE	:= SR0->R0_TPVALE
						SM7->M7_CODIGO	:= SR0->R0_CODIGO	
						SM7->M7_DIASPRO	:= SR0->R0_DIASPRO
						SM7->M7_DPROPIN	:= SR0->R0_DPROPIN
						SM7->M7_QDIAINF	:= SR0->R0_QDIAINF
						SM7->M7_QDNUTIL	:= SR0->R0_QDNUTIL
						SM7->M7_TPCALC	:= If(Empty(SR0->R0_TPCALC),"1",SR0->R0_TPCALC)
						SM7->M7_COMPL	:= "2"
						SM7->M7_VALDOM  := SR0->R0_QVALDOM
						SM7->M7_VALSEG  := SR0->R0_QVALSEG
						SM7->M7_VALTER	:= SR0->R0_QVALTER
						SM7->M7_VALQUA	:= SR0->R0_QVALQUA
						SM7->M7_VALQUI	:= SR0->R0_QVALQUI
						SM7->M7_VALSEX	:= SR0->R0_QVALSEX
						SM7->M7_VALSAB	:= SR0->R0_QVALSAB
						SM7->M7_VALFIX	:= SR0->R0_QVALFIX
						
					SM7->(msUnlock())
				EndIf					
			EndIf
			
		EndIf
		SR0->(dbSkip())
	EndDo
	
	If(Len(aLogs) > 0)		
		lRet := .F.
		//"Para algumas Filials/Processos foram encontradas inconsistencias, favor corrigir os dados e executar a rotina novamente.  preciso ter perodo ativo de acordo com o roteiro."
		//"Para as demais a converso foi realizada com sucesso!" 
		MsgInfo( STR0041 + CRLF + CRLF + STR0055, STR0033 ) 
		aadd(aTitles,STR0043) //"Inconsistencia no cadastro de perodos"
		fMakeLog(  aLogs , aTitles , NIL , .T. , "GPEA133_"+FWTimeStamp(1), NIL,"G","L",NIL,.F.) 
	EndIf
	
	
End Transaction

RestArea(aArea)
Return lRet

/*


Ŀ
Funo     BuscaTBen    Autor Flavio Correa        Data  01/08/16 
Ĵ
Descrio  Tipo beneficios											  
Ĵ
 Uso       GPEA133                                                    
ٱ

*/
Function BuscaTBen(nTpVale,cCodigo,cFil)
Local cTpBen := ""

If nTpVale == "0"
	DbSelectArea("SRN")
   	SRN->(DbSetOrder(1)) //-RN_FILIAL+RN_COD
    If SRN->(dbSeek(xFilial('SRN',cFil)+cCodigo))
    	cTpBen := SRN->RN_TPBEN
   	EndIf
Else //-nTpVale $ "1*2"
   	DbSelectArea("RFO")
   	RFO->(DbSetOrder(1)) //-RFO_FILIAL, RFO_TPVALE, RFO_CODIGO
   	If RFO->(dbSeek(xFilial('RFO',cFil)+nTpVale+cCodigo))
		cTpBen := RFO->RFO_TPBEN
	EndIf
EndIf

Return cTpBen

/*


Ŀ
Funo    A133DelOk  Autor  Flavio Correa          Data  07.04.15 
Ĵ
Descrio Valida exclusao da linha                                    
Ĵ
 Uso       Generico                                                   
ٱ

*/
Static Function A133DelOk(oGrid, nLine, cAction, cField)
Local lRet 			:= .T.
Local oModel 		:= oGrid:GetModel()
Local nOperation 	:= oModel:GetOperation()

If cAction == 'DELETE' .and. nOperation == MODEL_OPERATION_UPDATE

	If oGrid:GetValue("R0_PEDIDO") == "2"
		Help( , ,'Help', ,STR0044,1,0) //"Item j foi gerado pedido e no pode ser excluda!"
		lRet := .F.
		oGrid:UnDeleteLine()
	EndIf
	
EndIf

Return lRet


/*/{Protheus.doc}fCriaTemp()
- Cria tabela de backup da SR0
@author: Leandro Drumond
@since:  28/09/2017
/*/
Static Function fCriaTemp(cAliasLock)
Local aFields		:= {}
Local lRet			:= .T.  
Local nI			:= 1

cAliasLock := "BKPSR0_" + cEmpAnt
aFields := SR0->(dbStruct())
If TCCanOpen(cAliasLock)
	TCDelFile(cAliasLock)
EndIf
	
MsCreate(cAliasLock,aFields,"TOPCONN")
MsOpenDbf( .T. , __cRdd , cAliasLock , cAliasLock , .T. )
While !SR0->(eof())
	RecLock(cAliasLock,.T.)
	For nI := 1 To Len(aFields)
		&((cAliasLock)->(aFields[nI][1])) := &("SR0->"+aFields[nI][1])
	Next nI
	(cAliasLock)->(msUnlock())
	SR0->(dbSkip())
EndDo
(cAliasLock)->(dbCloseArea())
Return lRet

Static Function fSetoSM7(oModel)
Local oView := FWViewActive()
Local nPos  := 0
Local nX	:= 0
Local nY	:= 2
	
	For nX := 0 To 2
		nPos := Ascan(oModel:getModel("GPEA133_MSM7"+cValtoChar(nX), "M7_CODIGO"):OFORMMODELSTRUCT:aFields,{|x| x[3]== "M7_CODIGO"})
		If(nPos > 0)
			oModel:getModel("GPEA133_MSM7"+cValtoChar(nX), "M7_CODIGO"):OFORMMODELSTRUCT:aFields[nPos][9] := CodBenef(nX,.T.)
			oView:aviews[nY++][3]:OSTRUCTVIEW:afields[1][13] := CodBenef(nX,.T.)			
		EndIf	
		nPos := 0
	Next nX	
	oView:SetModel(oModel)
Return (.T.)

/*/{Protheus.doc} CargaAux
Carrega registros de cadastro/clculo de benefcios
@author Allyson Luiz Mesashi
@since 09/03/2022
@param oGrid, objeto, grid da estrutura a ser carregada
@param cTab, caracter, tabela de busca dos dados
@param cTipo, caracter, tipo do benefcio
@return aRet, array, array com os registros carregados
/*/
Static Function CargaAux(oGrid, cTab, cTipo)

Local aRet 			:= {}
Local aRet2			:= {}
Local nCont			:= 0
Local nPosTpVale	:= 0

aRet2 := FormLoadGrid(oGrid)

If cTab == "SM7"
	nPosTpVale	:= oGrid:GetIdField("M7_TPVALE")
Else
	nPosTpVale	:= oGrid:GetIdField("R0_TPVALE")
EndIf

For nCont := 1 To Len(aRet2)
	If aRet2[nCont, 2, nPosTpVale] == cTipo
		aAdd( aRet, aClone(aRet2[nCont]) )
	EndIf
Next

Return aRet
