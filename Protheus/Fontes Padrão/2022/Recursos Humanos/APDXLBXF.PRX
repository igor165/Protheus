#INCLUDE "PROTHEUS.CH"
#INCLUDE "APDXLBXF.CH"

#DEFINE obj_margin	05
#DEFINE btn_size	25
#DEFINE btn_space	05
/*/
зддддддддддбддддддддддбдддддбдддддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁAPDXLBXF  ЁAutorЁEquipe Advanced RH       Ё Data Ё16/07/2003Ё
цддддддддддеддддддддддадддддадддддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁBiblioteca de Funcoes para montagem de ListBox para  selecaoЁ
Ё          Ёde Participantes                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                    Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё            ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL           Ё
цддддддддддддбддддддддддбдддддддддддбддддддддддддддддддддддддддддддддддд╢
ЁProgramador ЁData      ЁBOPS       ЁMotivo da Alteracao                Ё
цддддддддддддеддддддддддедддддддддддеддддддддддддддддддддддддддддддддддд╢
ЁCecilia Car.Ё04/07/2014ЁTPZWF1     ЁIncluido o fonte da 11 para a 12 e Ё
Ё            Ё          Ё           Ё efetuada a limpeza.               Ё  
юддддддддддддаддддддддддадддддддддддаддддддддддддддддддддддддддддддддддды/*/
/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInApdxLbxfExecЁAutor ЁMarinaldo de Jesus   Ё Data Ё24/08/2004Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de APDXLBXF                          Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInApdxLbxfExec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InApdxLbxfExec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= &( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбдддддддддддбдддддбддддддддддддддддддддддддбддддддбдддддддддд©
ЁPrograma  ЁAPDListFil ЁAutorЁNatie Sugahara	      Ё Data Ё 30/09/02 Ё
цддддддддддадддддддддддадддддаддддддддддддддддддддддддаддддддадддддддддд╢
Ё           Selecao de Participantes ( RD0 )                            Ё
цддддддддддддбдддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe     Ё APDListFil()                                             Ё
цддддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametros  Ё<Vide Parametros Formais>							        Ё
цддддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno     Ё Array contendo participantes selecionados                Ё
цддддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso         Ё Generico                                          		Ё
цддддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁComentario  Ё Efetua a filtragem de 2 ListBox                          Ё
юддддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function APDListFilter(	nBrowseOpcx		,;	//01 -> Opcao do aRotina
						aObjSize		,;	//02 -> Array com o Dimensionamento Disponivel
						aLbx2a			,;	//03 -> Array dos Participantes Selecionados
						aLstCampo		,;	//04 -> Array com os Campos para o ListBox
						oDlg			,;	//05 -> Dialogo onde sera montado o ListBox
						cAlias			,;	//06 -> Alias para a Carga das Informacoes
						cFiltro			,;	//07 -> Filtro Inicial
						aLbxKey			,;	//08 -> Array contendo os campos Chaves
						oFont			,;	//09 -> Fonte
						aNotLstCpo		,;	//10 -> Campos que nao deverao constar no ListBox
						aYesLstCpo		,;	//11 -> Campos que deverao Constar no ListBox
						aIndex			,;	//12 -> Array com os Indices para FilBrowse
						bEvalAntFiltro	,;	//13 -> Bloco a ser executado Antes da execucao do Filtro
						bEvalPosFiltro	 ;	//14 -> Bloco a ser executado apos a execucao do Filtro	
					)

Local aCabec	:= {}								//-- Array do cabecalho da Listbox 
Local aCpoX3	:= {}								//-- Array dos Campos da ListBox de acordo com  aLstCampo,  inclusive o campo d flag
Local aObjCoord	:= {}								//-- Array Dimensoes dos objetos

//-- Objeto Botao
Local oBtnPesquisa
Local oBtnFiltro
Local oBtnDesmTod
Local oBtnMarcTod
Local oBtnDesmT2
Local oBtnMarcT2
Local oGroup1
Local oGroup2
Local oGroup3
Local oLinhas1
Local oLinhas2
Local cSvFiltro := cFiltro
Local aAdv1Size		:= {}
Local aInfo1AdvSize	:= {}
Local aObj1Size		:= {}	
Local aObj1Coords 	:= {}
	
Local aAdv2Size		:= {}
Local aInfo2AdvSize	:= {}
Local aObj2Size		:= {}	
Local aObj2Coords 	:= {}
	
Local aAdv3Size		:= {}
Local aInfo3AdvSize	:= {}
Local aObj3Size		:= {}	
Local aObj3Coords 	:= {}

Local bEvalPesquisa  := { |cpos| ApdPesqFunc(cpos) }

DEFAULT aLbxKey			:= IF( Len( aLbxKey ) <= 0,{ ( PrefixoCpo( cAlias ) + "_FILIAL" ) , } , aLbxKey )
DEFAULT aObjSize 		:= IF( Len( aObjSize )<=0 ,{ 21 , 0 , oDlg:nWidth , oDlg:nHeight-(obj_margin) } , aObjSize ) 
DEFAULT aNotLstCpo		:= {}
DEFAULT aYesLstCpo		:= {}
DEFAULT aIndex			:= {}
DEFAULT bEvalAntFiltro	:= { || .T. }
DEFAULT bEvalPosFiltro  := { || .T. }

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁInicializa as variaveis Privates na Funcao que Chamou( 1 nivelЁ
Ёacima )													   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/

IF ( Type( "aLbx1a" ) == "U" )
	_SetOwnerPrvt("aLbx1a"	, {}  )
Else
	aLbx1a := {}
EndIF
IF ( Type( "oLbx1" ) == "U" )
	_SetOwnerPrvt("oLbx1"	, NIL )
Else
	oLbx1 := NIL
EndIF	
IF ( Type( "oLbx2" ) == "U" )
	_SetOwnerPrvt("oLbx2"	, NIL )
Else
	oLbx2 := NIL	
EndIF
IF ( Type( "oBtnPrev" ) == "U" )
	_SetOwnerPrvt("oBtnPrev", NIL )
Else
	oBtnPrev := NIL	
EndIF
IF ( Type( "oBtnPgPrev" ) == "U" )
	_SetOwnerPrvt("oBtnPgPrev", NIL )
Else
	oBtnPgPrev := NIL	
EndIF
IF ( Type( "oBtnNext" ) == "U" )
	_SetOwnerPrvt("oBtnNext", NIL )
Else
	oBtnNext := NIL	
EndIF
IF ( Type( "oBtnPgNext" ) == "U" )
	_SetOwnerPrvt("oBtnPgNext", NIL )
Else
	oBtnPgNext := NIL
EndIF

IF !( ValType("oFont") == "O" )
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
EndIF

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁSe aLstCampo estiver Vazio Carrega Todos os Campos de   AcordoЁ
Ёcom o Alias Passado										   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF Empty( aLstCampo )
	aLstCampo := ( ApdCarrCpos( cAlias , aNotLstCpo , aYesLstCpo )[1] )
EndIF

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁArray do cabecalho da Listbox 								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
aCabec	:= Array(Len(aLstCampo)+1)
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁArray dos Campos da ListBox de acordo com aLstCampo, inclusiveЁ
Ё o campo de flag											   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
aCpoX3	:= Array(Len(aLstCampo)+1)

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁCria Array com um Elemento em Branco Caso Estejam Vazios      Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF Empty( aLbx1a )
	ApdLbxMArray(cAlias,@aLbx1a,aLstCampo)
EndIF

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁCria Array com um Elemento em Branco Caso Estejam Vazios      Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF Empty( aLbx2a )
	ApdLbxMArray(cAlias,@aLbx2a,aLstCampo)
EndIF

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Retorna array com  as dimensoes do objeto                    Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
aInfo1AdvSize    := { aObjSize[2] , aObjSize[1] , aObjSize[4] , aObjSize[3] , 5 , 5 }
aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )
aAdd( aObj1Coords , { 020 , 000 , .F. , .T. } )
aAdd( aObj1Coords , { 000 , 000 , .T. , .T. } )
aObj1Size := MsObjSize( aInfo1AdvSize , aObj1Coords, ,.T. )

aAdv2Size    := aClone(aObj1Size[1])
aInfo2AdvSize    := { aAdv2Size[2] , aAdv2Size[1] , aAdv2Size[4] , aAdv2Size[3] , 5 , 5 }
aAdd( aObj2Coords , { 000 , 015 , .T. , .F. } )
aAdd( aObj2Coords , { 000 , 000 , .T. , .T., .T. } )
aAdd( aObj2Coords , { 000 , 010 , .T. , .F. } )
aObj2Size := MsObjSize( aInfo2AdvSize , aObj2Coords )


aAdv3Size    := aClone(aObj1Size[3])
aInfo3AdvSize    := { aAdv3Size[2] , aAdv3Size[1] , aAdv3Size[4] , aAdv3Size[3] , 5 , 5 }
aAdd( aObj3Coords , { 000 , 015 , .T. , .F. } )
aAdd( aObj3Coords , { 000 , 000 , .T. , .T., .T. } )
aAdd( aObj3Coords , { 000 , 010 , .T. , .F. } )
aObj3Size := MsObjSize( aInfo3AdvSize , aObj3Coords )
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Monta array com conteudo dos Campos e Cabecalho da Listbox   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
ApdLbxCpSxs(aLstCampo,@aCabec,@aCpoX3,cAlias)

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Monta Array 'Selecao dos Participantes (aLbx1a)'             Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
//'Carregando Participantes para sele┤└o...'
CursorWait()
MsAguarde( { || ApdLbxParFil(cAlias,aCpoX3,@aLbx1a,aLbx2a,cFiltro,aLstCampo,,,aLbxKey,@aIndex)}, NIL , OemToAnsi(STR0027) ) 
CursorArrow()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁCria Array com um Elemento em Branco Caso Estejam Vazios      Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF Empty( aLbx1a )
	ApdLbxMArray(cAlias,@aLbx1a,aLstCampo)
EndIF

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁCria Array com um Elemento em Branco Caso Estejam Vazios      Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF Empty( aLbx2a )
	ApdLbxMArray(cAlias,@aLbx2a,aLstCampo)
EndIF

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё 	Grupo : Selecao dos Participantes                          Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ aObj1Size[1,1], aObj1Size[1,2] GROUP oGroup2 TO  aObj1Size[1,3], aObj1Size[1,4]  ;
	PROMPT OemToAnsi(STR0003) OF oDlg PIXEL
	oGroup2:oFont:= oFont
				
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁBotoes Marca Todos e Desmarca Todos						   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ aObj2Size[1,1]*2,aObj2Size[1,2]*2  	BtnBmp oBtnMarcTod  RESOURCE 'CHECKED' Size 40,40 Design ;
	ACTION( If(nBrowseOpcx<> 2 .and. !Empty(aLbx1a[oLbx1:nAt,3]),ApdLbxTroca(aLbx1a,"M"),""),oLbx1:Refresh() ) OF oGroup2 MESSAGE OemToAnsi(STR0007)

@ aObj2Size[1,1]*2,(aObj2Size[1,2]*2)+30 BtnBmp oBtnDesmTod  RESOURCE 'UNCHECKED' Size 40,40 Design ;
	ACTION( If(nBrowseOpcx<> 2 .and. !Empty(aLbx1a[oLbx1:nAt,3]),ApdLbxTroca(aLbx1a,"D"),""),oLbx1:Refresh()  ) OF oGroup2 MESSAGE OemToAnsi(STR0008)

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁBotao Novo Filtro											   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ aObj2Size[1,1]+5,aObj2Size[1,2]+40 BUTTON oBtnFiltro PROMPT OemToAnsi("&"+STR0006) FONT oFont	SIZE 30,10 OF oGroup2	PIXEL;
	ACTION ( If(!ApdLbxChkTrf() ,;
				If( ApdLbxFilter(cAlias,@cFiltro) ,;
					(;
						CursorWait(),;
						Eval( bEvalAntFiltro ),;
						MsAguarde( {|| ApdLbxParFil(cAlias,aCpoX3,@aLbx1a,aLbx2a,cFiltro,aLstCampo,@oLbx1,aCabec,aLbxKey,@aIndex)  } , NIL , OemToAnsi(STR0028) ) ,; //'Filtrando Participantes para nova sele┤└o...'
						Eval( bEvalPosFiltro ),;
						cSvFiltro := cFiltro,;
						IF( Empty( aLbx1a ) , ApdLbxMArray( cAlias , @aLbx1a , aLstCampo ) , NIL ),;
						CursorArrow();
					),;
					cSvFiltro := cFiltro;
				   ),;
					MsgInfo( OemToAnsi(STR0026) );
				);
			)
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁBotao Novo Pesquisar 										   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ aObj2Size[1,1]+5,aObj2Size[1,2]+80 BUTTON oBtnPesquisa PROMPT OemToAnsi("&"+STR0030) FONT oFont	SIZE 40,10 OF oGroup2	PIXEL; // "Pesquisar"
	ACTION ( If(!ApdLbxChkTrf() ,;
				Eval( bEvalPesquisa, aLstCampo ),;
				MsgInfo( OemToAnsi(STR0026) );
				);
			)

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁListBox : Selecao dos Participantes                           Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
oLBx1 := TWBrowse():New( aObj2Size[2,1],aObj2Size[2,2],aObj2Size[2,3],aObj2Size[2,4],,aCabec,,oGroup2,,,,,,,,,,,,.F.,,.T.,,.F.,,,)
@ aObj2Size[3,1],aObj2Size[3,2] SAY oLinhas1 VAR StrZero(oLBx1:nAt,6)+"/"+StrZero(Len(oLBx1:aArray),6) SIZE 146,10 OF oGroup2 PIXEL FONT oFont
oLBx1:bChange 		:= { || oLinhas1:Refresh() }
oLBx1:bLDblClick	:= { || (aLbx1a[oLbx1:nAt,1]:=!aLbx1a[oLbx1:nAt,1] , oLbx1:Refresh(.F.) ) }

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁCria Array com um Elemento em Branco Caso Estejam Vazios      Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF Empty( aLbx1a )
	ApdLbxMArray( cAlias , @aLbx1a , aLstCampo )
EndIF
oLbx1 :SetArray( aLbx1a )
oLbx1 :bLine:= { || ApdLbxbLine( oLbx1 ) }
oLbx1 :Refresh()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁGrupo:Participantes Selecionados                              Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ aObj1Size[3,1], aObj1Size[3,2] GROUP oGroup3 TO  aObj1Size[3,3], aObj1Size[3,4] ;
	Prompt OemToAnsi(STR0004) OF oDlg PIXEL 
	oGroup3:oFont:= oFont
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁBotoes Marca Todos e Desmarca Todos						   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ aObj3Size[1,1]*2,aObj3Size[1,2]*2 	BtnBmp oBtnMarcT2  RESOURCE 'CHECKED'   Size 40,40 Design ;
	ACTION( If(nBrowseOpcx<> 2 .and. !Empty(aLbx2a[oLbx2:nAt,3]),ApdLbxTroca(aLbx2a,"M"),""),oLbx2:Refresh() ) OF oGroup3 MESSAGE OemToAnsi(STR0007)

@ aObj3Size[1,1]*2,(aObj3Size[1,2]*2)+30	BtnBmp oBtnDesmT2  RESOURCE 'UNCHECKED' Size 40,40 Design ;
	ACTION( If(nBrowseOpcx<> 2 .and. !Empty(aLbx2a[oLbx2:nAt,3]),ApdLbxTroca(aLbx2a,"D"),""),oLbx2:Refresh() )  OF oGroup3 MESSAGE OemToAnsi(STR0008)
   
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁListox: Participantes Selecionados							   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
oLBx2 := TWBrowse():New(  aObj3Size[2,1],aObj3Size[2,2],aObj3Size[2,3],aObj3Size[2,4],,aCabec,,oGroup3,,,,,,,,,,,,.F.,,.T.,,.F.,,,)      // Coordenada 
@ aObj3Size[3,1],aObj3Size[3,2] SAY oLinhas2 VAR StrZero(oLBx2:nAt,6)+"/"+StrZero(Len(oLBx2:aArray),6) SIZE 146,10 OF oGroup3 PIXEL FONT oFont
oLBx2:bChange 		:= { || oLinhas2:Refresh() }
oLBx2:bLDblClick	:= { || (aLbx2a[oLbx2:nAt,1]:=!aLbx2a[oLbx2:nAt,1] , oLbx2:Refresh(.F.) )   }

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©	
ЁCria Array com um Elemento em Branco Caso Estejam Vazios      Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF Empty( aLbx2a )
	ApdLbxMArray( cAlias , @aLbx2a , aLstCampo )
EndIF
oLbx2 :SetArray( aLbx2a )
oLbx2 :bLine:= { || ApdLbxbLine( oLbx2 ) }
oLbx2 :Refresh()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁBotoes de Movimentacao dos itens da Listbox                   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ (aObj1Size[2,1]*2)+10 ,(aObj1Size[2,2]*2)+10   BtnBmp oBtnNext  Resource 'NEXT'   Size 25,25 Design ;
	ACTION(If(nBrowseOpcx<> 2 .and. !ApdLbxChkTrf() ,ApdLbxFlgUse(1,"1",aLbx1a,aLbx2a,aLstCampo,cAlias,aLbxKey,aCabec,aCpoX3), ;
		MsgInfo( OemToAnsi(STR0026)) ) , oDlg) ;
		OF oDlg   MESSAGE OemToAnsi(STR0022)  

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁTransf. Grupo de Participantes								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ (aObj1Size[2,1]*2)+40 ,(aObj1Size[2,2]*2)+10  BtnBmp oBtnPgNext  Resource 'PGNEXT'   Size 25,25 Design ;
	ACTION(If(nBrowseOpcx<> 2 .and. !ApdLbxChkTrf(),ApdLbxFlgUse(1,"2",aLbx1a,aLbx2a,aLstCampo,cAlias,aLbxKey,aCabec,aCpoX3),;
		MsgInfo( OemToAnsi(STR0026)) ), oDlg)  ;
		OF oDlg MESSAGE OemToAnsi(STR0023) 
    
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁVolta  um  Unico											   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ (aObj1Size[2,1]*2)+70 , (aObj1Size[2,2]*2)+10  BtnBmp oBtnPrev  Resource 'PREV'   Size 25,25 Design ;
	ACTION(If(nBrowseOpcx<> 2 .and. !ApdLbxChkTrf(),ApdLbxFlgUse(2,"1",aLbx1a,aLbx2a,aLstCampo,cAlias,aLbxKey,aCabec,aCpoX3),;
		MsgInfo( OemToAnsi(STR0026)) ), oDlg)  ;
		OF oDlg MESSAGE OemToAnsi(STR0024)  
                                 
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁVolta Grupo Selecionado    								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
@ (aObj1Size[2,1]*2)+100 , (aObj1Size[2,2]*2)+10 BtnBmp oBtnPgPrev Resource 'PGPREV'   Size 25,25 Design ;
	ACTION(If(nBrowseOpcx<> 2 .and. !ApdLbxChkTrf(), ApdLbxFlgUse(2,"2",aLbx1a,aLbx2a,aLstCampo,cAlias,aLbxKey,aCabec,aCpoX3),;
		MsgInfo( OemToAnsi(STR0026)) ), oDlg)  ;
		OF oDlg MESSAGE OemToAnsi(STR0025)   

Return( NIL )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApdLbxFilter	ЁAutorЁNatie Sugahara	  Ё Data Ё17/09/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁCarregar os Campos do SX3                					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDListFil     												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function ApdLbxFilter( cAlias , cFiltroRet )

Local aFilterBldExp	:= {}
Local aRetFiltro	:= {}
Local cFiltroAux	:= cFiltroRet
Local cRetFiltro	:= ""
Local lFilterNew	:= .F.

IF ( MsgYesNo( STR0029 ) )	//"Filtro RАpido?"
	cRetFiltro := cFiltroRet
	lFilterNew := GpFltBldExp( cAlias , NIL , @cRetFiltro , NIL )
Else
	aAdd( aFilterBldExp , { "FILTRO_ALS" , cAlias    , .T. , ".and." } )	//Apenas os Filtros para o RD0 (cAlias)
	aAdd( aFilterBldExp , { "FILTRO_PRG" , FunName() , NIL , NIL 	 } )	//e cujo programa seja o Retornado pela FunName()
	aRetFiltro	:= FilterBuildExpr( aFilterBldExp )
	cRetFiltro	:= GpFltAlsGet( aRetFiltro , cAlias )
	lFilterNew	:= GpFltBtnOk()
EndIF	

//-- Se Novo filtro, monta novo array com  os participantes 
IF ( lFilterNew := ( lFilterNew .and. ( cFiltroAux <> cRetFiltro ) ) )
	cFiltroRet	:= cRetFiltro
Else
	cFiltroRet	:= cFiltroAux
EndIF

Return( lFilterNew )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApdLbxFlgUseЁAutor Ё Natie Sugahara       Ё Data Ё 17/09/02 Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Marca e desmarca itens da ListBox						    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function ApdLbxFlgUse(	nLbx		,;	//-- 1- Origem  2- Destino
				         		cTipo		,;	//-- 1- Transf. nAt  2-Duplo Click
				         		aLbx1a		,;	//-- Sel.Participantes
				         		aLbx2a		,;	//-- Particip.Selecionados
				         		aCposAux	,;	//-- Campos 
				         		cAlias		,;
				         		aLbxKeyAux	,;
						 		aCabAux		,;
						 		aCpoX3		 ;
						 	)  
						 
Local nCnt := nLbxCpo	:=0
Local nLoop

Local aLbxOrig  := aClone( If(nLbx == 1, aLbx1a, aLbx2a) )
Local aLbxDest  := aClone( If(nLbx == 1, aLbx2a, aLbx1a) )
Local nPosLbx   := If(nLbx == 1, oLbx1:nAt, oLbx2:nAt)
Local aLbxAux											//-- Array Auxiliar  para composicao LbxOrig
Local aLbxKeyCpo:= {}

CursorWait()

Begin Sequence

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Retorna caso esteja tentando mover de uma ListBox vazia		 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( Len(aLbxOrig) == 1 .And. Empty(aLbxOrig[1,3]) )
		Break
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Caso a ListBox destino esteja vazia, exclui o elemento vazio Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Len(aLbxDest) == 1 .And. Empty(aLbxDest[1,3])
		aDel(aLbxDest,1)
		aSize(aLbxDest,Len(aLbxDest)-1)
	EndIf
	
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inclui na ListBox destino e exclui da origem                 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	If cTipo = "2"								//-- Movimenta um  grupo  em  um  unica vez
		//-- Deixa  o flag da Listbox Desativado 
		aEval( aLbxDest , { |x,y| aLbxDest[y,1] := .F. } )	
		aLbxAux:= {}
		//-- Transf varios partc. para Listbox  destino
		aEval( aLbxOrig,{|x,y| If(aLbxOrig[y,1],aAdd(aLbxDest, aLbxOrig[y] ),"" ) } )
		aEval( aLbxorig,{|x,y| If(!(aLbxOrig[y,1]),aAdd(aLbxAux,aLbxOrig[y] ),"") } )
		aLbxorig	:= {}
		aLbxOrig	:= aClone(aLbxAux)
	Else                                       //-- Movimenta apenas o item posicionado 
		//-- Deixa  o flag da Listbox Desativado
		aEval( aLbxDest , { |x,y| aLbxDest[y,1] := .F. } )	
		If !AlbxOrig[nPosLbx,1]
			AlbxOrig[nPosLbx,1]:= .T.
		Endif
		If Ascan(aLbxDest,{|x| aLbxOrig[nPosLbx] = x} ) >=0 
			aAdd(aLbxDest,aLbxOrig[nPosLbx] )
			aDel(aLbxOrig,nPosLbx)
			aSize(aLbxOrig,Len(aLbxOrig)-1)
		Endif
	Endif
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Caso a ListBox origem tenha ficado vazia, inclui um elemento Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Empty(aLbxOrig)
		ApdLbxMArray(cAlias,@aLbxOrig,aCposAux)
	ElseIF Empty(aLbxDest)
		ApdLbxMArray(cAlias,@aLbxDest,aCposAux)
	EndIf 
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ordena a Listbox                                             Ё   
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nLbxCpo := If(Len(aLbxKeyAux) > 2 ,2,Len(aLbxKeyAux))
	For nLoop := 1 to nLbxCpo
		nLbxkey  		:= ascan(aCpoX3,{|x| x = aLbxKeyAux[nLoop] })
		aAdd(aLbxKeyCpo	,nLbxKey)
	Next nLoop
	aSort(aLbxOrig,,,{|x,y| x[ aLbxKeyCpo[1] ]+x[ aLbxKeyCpo[2] ]  < y[ aLbxKeyCpo[1] ]+ y[ aLbxKeyCpo[2] ]  })
	aSort(aLbxDest,,,{|x,y| x[ aLbxKeyCpo[1] ]+x[ aLbxKeyCpo[2] ]  < y[ aLbxKeyCpo[1] ]+ y[ aLbxKeyCpo[2] ]  })
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Retorna o conteudo original das ListBox                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aLbx1a := aClone( If(nLbx == 1, aLbxOrig, aLbxDest) )
	aLbx2a := aClone( If(nLbx == 1, aLbxDest, aLbxOrig) )
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁCria Array com um Elemento em Branco Caso Estejam Vazios      Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF Empty( aLbx1a )
		ApdLbxMArray(cAlias,@aLbx1a,aLstCampo)
	EndIF
	oLbx1 :SetArray(aLbx1a)
	oLbx1 :bLine:= { || ApdLbxbLine( oLbx1 ) }
	oLbx1 :Refresh()
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁCria Array com um Elemento em Branco Caso Estejam Vazios      Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF Empty( aLbx2a )
		ApdLbxMArray(cAlias,@aLbx2a,aLstCampo)
	EndIF
	oLbx2 :SetArray(aLbx2a)
	oLbx2 :bLine:= { || ApdLbxbLine( oLbx2 ) }
	oLbx2 :Refresh()

End Sequence
	
CursorArrow()

Return( NIL )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApdLbxParFil	ЁAutorЁNatie Sugahara	  Ё Data Ё17/09/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁMonta Array Selecao dos Participantes (aLbx1a) de acordo comЁ
Ё          ЁFiltro														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDListFil     												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function ApdLbxParFil(	cAlias		,;	//-- Alias
						   		aCpoX3		,;	//-- Array Campos da listbox
						   		aLbxAux		,;	//-- Array Listbox Selecao Participantes
						   		aLbx2Aux	,;	//-- Array Listbox Part. Selecionados
						   		cFiltra		,;	//-- Filtro 
						   		aCpoAux		,;	//-- Campos da Listbox
						   		oLbxAux		,;	//-- Objeto da Listbox 
						   		aCabAux		,;	//-- Cabecalho 
						   		aLbxKeyAux	,;	//-- Chave de ordenacao
						   		aIndex		 ;	//-- Indices Criados Pela FilBrowse
						   	)
						   
Local cFiltrAux	:= ""
Local nCpos	 	:= Len(aCpoX3)
Local nCpo		:= 0
Local nCont 	:= 0
Local nLbxkey	:= 0 
Local aArea		:= GetArea()
Local aLbx1		:= {}
Local cFilPartic:= xFilial(cAlias)
Local aLbxKeyCpo:= {}
Local nLoop

DEFAULT aIndex	:= {}

Private bFiltraLbx 	:= {|| NIL }			//-- Variavel para Filtro

dbSelectArea(cAlias)

CursorWait()

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa Filtro dos Dados                                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
IF !Empty( cFiltrAux := CHKRH("APDXFUN",cAlias,"1") )
	cFiltrAux += ( " .and. " + cFiltra )
Else
	cFiltrAux := cFiltra
EndIF
bFiltraLbx	:= {|| FilBrowse(cAlias,@aIndex,@cFiltrAux) }
Eval(bFiltraLbx)
	
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta Array ListBox1                                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If (cAlias)->( dbSeek( cFilpartic , .F. ) )
	While (cAlias)->( !Eof() .and. &( PrefixoCpo(cAlias)+"_FILIAL" ) == cFilPartic )
		nCont++
		aAdd(aLbx1,Array(nCpos) )
		For nLoop := 1 to nCpos
			cCampo:= aCpoX3[nLoop]
			aLbx1[len(aLbx1),nLoop]	:= (cAlias)->( &( cCampo ) )
		Next nLoop
		nLoop := 0
		(cAlias)->( dbSkip() )
	Enddo
Endif	
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁSe nao for filtrado nada, inclui um  elemento vazio na listBox1 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If(Len(aLbx1)) = 0
	ApdLbxMArray(cAlias,@aLbx1,aCpoAux)
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se Particip. ja foi selecionado. Se Sim, nao carregaЁ
//Ё na Listbox 1 (Sel. Participantes)                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aLbxAux		:= {}

If  ( nCpo := ascan(aCpoAux, {|Cpo|  Cpo = PrefixoCpo( cAlias )+"_CODIGO" }   ) ) > 0 .or. ;
	( nCpo := ascan(aCpoAux, {|Cpo|  Cpo = PrefixoCpo( cAlias )+"_MAT"    }   ) ) > 0

	For nLoop := 1 to Len(aLbx1)
		//-- Compara novo Array e nao permite q mesmo participante apareca nos arrays de Partic. Selecion e Sel. Particip.
		nPos:= ascan(aLbx2Aux,{|x| aLbx1[nLoop,nCpo+1] == x[nCpo+1] }  )
		If nPos <= 0
			aAdd( aLbxAux,aLbx1[ nLoop ] )							//-- Monta nova Listbox: Selecao de Participantes
		Endif
	Next nLoop
Endif

	
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ordena a Listbox                                             Ё   
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
nLbxCpo 	:= If(Len(aLbxKeyAux) > 2 , 2 ,Len(aLbxKeyAux)) 
For nLoop := 1 to nLbxCpo
	nLbxkey	:= ascan(aCpoX3,{|x| x = aLbxKeyAux[nLoop] })
	aAdd(aLbxKeyCpo	,nLbxKey)
Next nLoop

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁSe nao for filtrado nada, inclui um  elemento vazio na listBox1 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If(Len(aLbxAux)) = 0
	ApdLbxMArray(cAlias,@aLbxAux,aCpoAux)
Endif
aSort(aLbxAux ,,,{|x,y| x[ aLbxKeyCpo[1] ]+x[ aLbxKeyCpo[2] ]  < y[ aLbxKeyCpo[1] ]+ y[ aLbxKeyCpo[2] ]  })
aSort(aLbx2Aux,,,{|x,y| x[ aLbxKeyCpo[1] ]+x[ aLbxKeyCpo[2] ]  < y[ aLbxKeyCpo[1] ]+ y[ aLbxKeyCpo[2] ]  })

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Retorna novo conteudo da Listbox de acordo com novo filtro   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ValType(oLbxAux) = "O"
	oLbxAux:SetArray(aLbxAux)
	oLbxAux:bLine:= { || ApdLbxbLine( oLbxAux ) } 
	oLbxAux:Refresh()
Endif

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Deleta o filtro utilizando a funcao FilBrowse                Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
EndFilBrw( cAlias , @aIndex )

CursorArrow()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura Area e Ordem de Entrada                              Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aArea )

Return( NIL )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApdLbxCpSxs		ЁAutorЁNatie Sugahara	  Ё Data Ё17/09/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁVerifica existencia de cpo no SX3 e  alimenta array com o tiЁ
Ё          Ёtulo do campo												Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDListFil     												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function ApdLbxCpSxs( aLstCampo , aCabAux , aCpoX3 , cAlias )

Local nX		:= 0
Local aArea		:= GetArea()

cAlias	:= Upper( AllTrim( cAlias ) )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Salva a integridade dos campos de Bancos de Dados            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("Sx3")
dbSetOrder(2)
aCpoX3[1] := ".T."
aCabAux[1]:= "U"
For  nX:= 1 to Len(aLstCampo)
	If( dbSeek(	aLstCampo[nx],.F.)  ) 
		aCpoX3[nX+1 ]	:= aLstCampo[nX]
		aCabAux[nX+1]	:= TRIM(X3Titulo() )
	Else
		aCpoX3[nX+1]	:= " "
		aCabAux[nX+1]	:= " "
	Endif
next nX 

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura Area e Ordem de Entrada                              Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aArea )

Return

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApdLbxTroca		ЁAutorЁNatie Sugahara	  Ё Data Ё17/09/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁEfetua a troca da selecao no ListBox 						Ё
Ё          Ё(controle Usado/nao usado )									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDListFil     												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function ApdLbxTroca(	aArray	,;	//Array contendo os elementos para troca
					   			cTipo	 ;	//Tipo da Multipla Selecao "M"Marca todos ; "D" Desmarca Todos
					   	    )		

IF cTipo == "M"	//-- Marca todos
	aEval( aArray , { |x,y| aArray[y,1] := .T. } )
Else			//-- Desmarca todos 
	aEval( aArray , { |x,y| aArray[y,1] := .F. } )
EndIF

Return( aArray )



/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApdLbxChkTrf	ЁAutorЁNatie Sugahara	  Ё Data Ё17/09/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁCheca a Transferencia no ListBox                        	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDListFil     												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function ApdLbxChkTrf()
Return( If( Type("oTree") == "O" .and. Left( oTree:GetCargo() , 1 ) == '*', .T. ,  .F. ) )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApdCarrCpos		ЁAutorЁMarinaldo de Jesus Ё Data Ё09/11/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁCarregar os Campos do SX3                					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico()     												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function ApdCarrCpos(	cAlias 		,;
						aNotLstCpo	,;
						aYesLstCpo	,;
						nUsado		,;
						aHeader		,;
						aVirtual	,;
						aVisual		,;
						lAllFields	 ;
					)	

Local aLstCampo		:= {}
Local aNewHeader	:= {}
Local aRet			:= Array( 2 )
Local lYesLstCpos	:= .F.
Local nCpo			:= 0.00
Local nCpos			:= 0.00

DEFAULT cAlias		:= Alias()
DEFAULT aNotLstCpo	:= {}
DEFAULT aYesLstCpo	:= {}
DEFAULT nUsado		:= 0.00
DEFAULT aHeader		:= {}
DEFAULT aVirtual	:= {}
DEFAULT aVisual		:= {}
DEFAULT lAllFields	:= .T.

lYesLstCpos	:= ( Len( aYesLstCpos ) > 0.00 )

cAlias	:= Upper( AllTrim( cAlias ) )

aHeader := GdMontaHeader(	@nUsado		,;	//01 -> Por Referencia contera o numero de campos em Uso
							@aVirtual	,;	//02 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Virtuais
		   					@aVisual	,;	//03 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Visuais
							cAlias		,;	//04 -> Opcional, Alias do Arquivo Para Montagem do aHeader
							aNotLstCpo	,;	//05 -> Opcional, Campos que nao Deverao constar no aHeader
							lAllFields	 ;	//06 -> Opcional, Carregar Todos os Campos
		   	 			 )

nUsado	:= 0.00
nCpos	:= Len( aHeader )
For nCpo := 1 To nCpos
	cCpo := Upper( aHeader[ nCpo , 2 ] )
	IF ( aScan( aNotLstCpo , { |x| Upper( AllTrim( x ) ) == cCpo } ) > 0.00 )
		Loop
	EndIF
	IF ( lYesLstCpos )
		IF ( aScan( aYesLstCpo , { |x| Upper( AllTrim( x ) ) == cCpo } ) == 0.00 )
			Loop
		EndIF
	EndIF	
	IF ( aScan( aVirtual , { |x| Upper( AllTrim( x ) ) == cCpo } ) > 0.00 )
		Loop
	EndIF
	aAdd( aLstCampo , cCpo )
	aAdd( aNewHeader , aClone( aHeader[ nCpo ] ) )
	++nUsado
Next nCpo

aRet[1] := aClone( aLstCampo  )
aRet[2] := aClone( aNewHeader )

Return( aClone( aRet ) )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApdLbxbLine		ЁAutorЁMarinaldo de Jesus Ё Data Ё26/07/2003Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁMontar a Linha do ListBox no bLine              			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDListFil     												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function ApdLbxbLine( oListBox )

Static aOkNo
Static oOk
Static oNo

DEFAULT oOk 	:= LoadBitmap( GetResources(),"LBTIK" )
DEFAULT oNo 	:= LoadBitmap( GetResources(),"LBNO"  )
DEFAULT aOkNo	:= { oOk , oNo }

Return( LineLstBox( oListBox , .T. , aOkNo , 1 , NIL ) )

/*/
зддддддддддбддддддддддддбдддддбддддддддддддддддддддддддбдддддбдддддддддд©
ЁFun┤┘o    ЁApdLbxMArrayЁAutorЁNatie Sugahara / Naldo  ЁData Ё17/09/2002Ё
цддддддддддеддддддддддддадддддаддддддддддддддддддддддддадддддадддддддддд╢
ЁDescri┤┘o ЁMontagem Array Vazio										Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDListFil     												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function ApdLbxMArray(	cAlias		,;	//-- Alias do arquivo
		 				aArray		,;	//-- Array que devera retornar vazio
						aCposAux	 ;	//-- Array com os campos da listbox
					 )			

Local aArea		:= {}
Local aAreaSX3	:= {}
Local nCont		:= 0.00
Local nNrCpo 	:= 0.00
Local nPos		:= 0.00

Static aMArrayCpos

IF ( PCount() == 0.00 )
	aMArrayCpos := NIL
Else

	DEFAULT cAlias		:= Alias()
	DEFAULT aArray		:= {}
	DEFAULT aCposAux	:= ( ApdCarrCpos( cAlias )[1] )

	DEFAULT aMArrayCpos := {}

	cAlias	:= Upper( AllTrim( cAlias ) )
	aArray	:= {}

	aArea		:= GetArea()
	aAreaSX3	:= SX3->( GetArea() )
	nCont		:= 1.00
	nNrCpo 		:= ( Len( aCposAux ) + 1 )	//-- Numero total de Cpos da Listbox
	nPos		:= 0.00

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe os Campos Nao Tiverem Sido Carregados, Carrega-os a  partirЁ
	Ёdo Dicionario de Dados SX3									   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( nPos := aScan( aMArrayCpos , { |x| x[1] == cAlias } ) ) == 0.00
		aAdd( aMArrayCpos , { cAlias , {} } )
		nPos := Len( aMArrayCpos )
		aMArrayCpos[ nPos , 02 ] := Array( nNrCpo )
		SX3->( dbSetOrder( 01 ) )
		SX3->( dbSeek( cAlias ) )
		While SX3->( !Eof() .and. ( X3_ARQUIVO == cAlias ) )
			IF ( aScan( aCposAux , AllTrim( SX3->X3_CAMPO ) ) == 0.00 )
				SX3->( dbSkip() )
				Loop
			EndIF
			IF ( nCont == 1 )
				aMArrayCpos[ nPos , 02 , nCont ] :=.F.
				nCont++
			EndIF
			aMArrayCpos[ nPos , 02 , nCont ] := SX3->( GetValType( X3_TIPO , X3_TAMANHO ) )
			nCont ++ 
			IF ( nCont > nNrCpo )
				Exit
			EndIF
			SX3->( dbSkip() )
		End While
	EndIF
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁCarrega os Campos a Partir de aMArrayCpos					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( nPos > 0.00 )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta Array do 1╖ Elemento Vazio                             Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aAdd( aArray , aClone( aMArrayCpos[ nPos , 02 ] ) )
	EndIF	
		
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁRestaura Area e Ordem de Entrada                              Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	RestArea( aAreaSX3 )
	RestArea( aArea )

EndIF
	
Return( NIL )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁApdPesqFuncЁ Autor Ё IP Rh Inovacao       Ё Data Ё 19/09/12 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Pesquisa participantes das avaliacoes 	                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё APDXLBXF                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function ApdPesqFunc( aLstCampo )

Local oDlg
Local oCbx
Local bSet15
Local bSet24
Local cCampo	:= ""
Local cOrd		:= ""
Local nOpca		:= 0
Local nI		:= 0
Local nPosL		:= 0
Local nOpt1		:= 1
Local nTamCampo	:= 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё DeclaraГЦo de arrays para dimensionar tela		             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aGDCoord		:= {}   

Default aLstCampo	:= {}

Private aOrd := {}

If Len(aLstCampo) == 0 .Or. Len(aLbx1a) == 0
	Return	
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Processa Choices dos Campos do Banco de Dados          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
cOrd   := ""
cCampo := SPACE(40)
For ni := 1  To Len(aLstCampo)
	aAdd(aOrd, OemToAnsi(Capital( TITSX3( aLstCampo[ni] )[1] )) )
Next
cOrd := aOrd[1]

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Monta as Dimensoes dos Objetos         					   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
aAdvSize		:= MsAdvSize()
aAdvSize[5]	:=	420	//horizontal
aAdvSize[6]	:=  170	//Vertical
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }					 
aAdd( aObjCoords , { 000 , 015 , .T. , .F. } )
aAdd( aObjCoords , { 000 , 015 , .T. , .F. } )
aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords )

DEFINE MSDIALOG oDlg FROM aAdvSize[7], 0 TO aAdvSize[6], aAdvSize[5] TITLE OemToAnsi(STR0030) PIXEL//"Pesquisa"
bSet15 := {|| nOpca := 1,oDlg:End()}
bSet24 := {|| oDlg:End()}    

@ aObjSize[1][1],aObjSize[1][2] COMBOBOX oCBX VAR cOrd ITEMS aOrd  SIZE 165,44 ON CHANGE (nOpt1:=(oCbx:nAt+1)) OF oDlg FONT oDlg:oFont PIXEL
@ aObjSize[2][1],aObjSize[2][2]  MSGET cCampo SIZE 165,10 PIXEL 

ACTIVATE MSDIALOG oDlg ON INIT Enchoicebar(oDlg, bSet15, bSet24, Nil, Nil) CENTERED

If nOpca == 1  
	If nOpt1 == 1
		nOpt1++
	Endif
	aLbx1a := aSort(aLbx1a ,,,{|x,y| x[nOpt1]+x[2] < y[nOpt1]+y[2] })
	oLbx1 :SetArray( aLbx1a )
	oLbx1 :bLine:= { || ApdLbxbLine( oLbx1 ) }
	nTamCampo	:= Len(AllTrim(cCampo))
	If ( nPosL := aScan(aLbx1a, {|x| SubStr(UPPER(AllTrim(x[nOpt1])),1,nTamCampo) == UPPER(AllTrim(cCampo)) } )) > 0
       oLbx1:nAt:= nPosL
	Endif	

	oLbx1 :Refresh()
Endif

Return