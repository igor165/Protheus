#INCLUDE "GPEXCALC.CH"
#INCLUDE "PROTHEUS.CH"

Static aCriterio 	:= {}
Static aFaltas		:= {}
Static cTipoCri		:= ""
Static cNroPed		:= ""
Static cFilPed      := ""
Static lUsaCPer		:= GetMvRH("MV_USACPER",, .F.) //.T.=Usa periodo folha/.F.= usa calendario ponto
Static lUsaGS       := GetMvRH("MV_TECXRH", NIL, .F. )
Static lGp210Sal
Static lGPM5001
Static lGPVTRDias
Static nFaltasAut	:= 0 // Dias de falta para o cแlculo automแtico
Static nDiasRef		:= 0 // Dias de refer๊ncia para VA e VR
Static nTamPed 		:= GetSx3Cache( "R0_NROPED", "X3_TAMANHO" )
Static __lMemCalc
Static lGPM19RGB 
Static lTemCCT
Static lOtbCCT

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณ			ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.			  			ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณProgramador ณ Data	ณ FNC			 ณ  Motivo da Alteracao 					ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณFlavio Correณ25/02/15ณPCREQ-2898      ณInclusao fonte							ณฑฑ
ฑฑณAllyson M.  ณ04/03/15ณTRIA28    		 ณAjuste em fMedia13() p/ verificar se e'   ณฑฑ
ฑฑณ            ณ        ณ          		 ณcomplemento de 13o.    					ณฑฑ
ฑฑณMariana M.  ณ04/09/15ณTSZLXV    		 ณAjuste em fVTRCalc() p/ quando percentual ณฑฑ
ฑฑณ            ณ        ณ          		 ณfor diferente de 6%. Sistema irแ respeitarณฑฑ
ฑฑณ            ณ        ณ          		 ณo que foi informado no cadastro da verba. ณฑฑ
ฑฑณ            ณ        ณ          		 ณTamb้m foi ajustado o sistema para quando ณฑฑ
ฑฑณ            ณ        ณ          		 ณpossuir sแbados como nใo trabalhados, mas ณฑฑ
ฑฑณ            ณ        ณ          		 ณa empresa para o VT para os funcionแrios, ณฑฑ
ฑฑณ            ณ        ณ          		 ณo sistema terแ que calcular a quantidade  ณฑฑ
ฑฑณ            ณ        ณ          		 ณde Dias Uteis de VT e Nใo Uteis de VT cor-ณฑฑ
ฑฑณ            ณ        ณ          		 ณretamente.								ณฑฑ
ฑฑณAllyson M.  ณ19/10/15ณTTQ964    		 ณAjuste em fVRFCalc() p/ considerar os diasณฑฑ
ฑฑณ            ณ        ณ          		 ณde VR que sao no sabado/domingo			ณฑฑ
ฑฑณRenan Borgesณ14/12/15ณTTUHKO    		 ณAjuste no cแlculo de VT quando hแ dias fi-ณฑฑ
ฑฑณ            ณ        ณ          		 ณxos cadastrados no meio de VT.            ณฑฑ
ฑฑณChristiane V.ณ06/11/15ณTTSM85         ณAjuste na FCalcDSR para considerar a data ณฑฑ
ฑฑณ             ณ        ณ               ณde admissao ao pesquisar os dias de DSR   ณฑฑ
ฑฑณ             ณ        ณ               ณsomente no mes de admissao do funcionario.ณฑฑ
ฑฑณAllyson M.   ณ11/12/15ณTU9321         ณAjuste na validacao dos dias afastados    ณฑฑ
ฑฑณ             ณ        ณ               ณquando for calculo do dissidio            ณฑฑ
ฑฑณAllyson M.   ณ11/12/15ณTU9321         ณAjuste em fVTRCalc() p/ nao descontar     ณฑฑ
ฑฑณ             ณ        ณ               ณo VT de funcionario estagiario.           ณฑฑ
ฑฑณRenan Borges ณ18/12/15ณTUBRN8		 ณAjuste para fechar alias temporario abertoณฑฑ
ฑฑณAllyson M.   ณ24/12/15ณTTZEZ2         ณAjuste em fVlsAus() e em fVTRCalc() p/ naoณฑฑ
ฑฑณ             ณ        ณ               ณcalcular VT p/ funcionario afastado no    ณฑฑ
ฑฑณ             ณ        ณ               ณperiodo inteiro    						ณฑฑ
ฑฑณRicardo D.  ณ19/12/15ณTUCIXY		     ณArredondamento no calculo de Vale Alimen- ณฑฑ
ฑฑณ            ณ        ณ          		 ณtacao e Refeicao nas funcoes FVRFCALC e   ณฑฑ
ฑฑณ            ณ        ณ          		 ณFVALCALC								    ณฑฑ
ฑฑณEduardo F.  ณ05/05/16ณTUVDWU		     ณAjuste para que na integra็ใo da rotina   ณฑฑ
ฑฑณ            ณ        ณ          		 ณBEN nใo duplicar o valor.                 ณฑฑ
ฑฑณRaquel Hagerณ17/05/16ณTUYLIQ		     ณAjuste na fun็ใo fValCalc p/ contabilizar ณฑฑ
ฑฑณ            ณ        ณ          		 ณqualquer dia desde que seja trabalhado.   ณฑฑ
ฑฑณAllyson M.  ณ23/06/16ณTVJ289          ณAjuste em fVlsAus() p/ guardar os dias de ณฑฑ
ฑฑณ            ณ        ณ                ณferias p/ gravacao correta no cabecalho   ณฑฑ
ฑฑณGabriel A.  ณ24/06/16ณTVGZA5          ณAjuste para zerar o valor da empresa      ณฑฑ
ฑฑณ            ณ        ณ                ณquando o valor do benefํcio for zero.     ณฑฑ
ฑฑณEsther V.   ณ27/06/16ณ	   TVIAMO    ณAjuste na gravacao do campo R0_DIASPRO no ณฑฑ
ฑฑณ            ณ        ณ          		 ณcalculo de VR/VA quando hแ calculo propor-ณฑฑ
ฑฑณ            ณ        ณ          		 ณcional.									ณฑฑ
ฑฑณAllyson M.  ณ12/07/16ณTURHSE          ณAjuste p/ desconto do VT p/ verificar as  ณฑฑ
ฑฑณ            ณ        ณ                ณverbas que serใo somadas ao salแrio       ณฑฑ
ฑฑณAllyson M.  ณ12/08/16ณTURHSE          ณAjuste p/ desconto do VT p/ verificar a   ณฑฑ
ฑฑณ            ณ        ณ                ณgarantia de comissใo       				ณฑฑ
ฑฑณRaquel Hagerณ12/08/16ณTURHSE          ณAjuste nas fun็๕es fCrgAutom e fCrgBenAut ณฑฑ
ฑฑณ            ณ        ณ                ณpara cแlculo automแtico de Benefํcios em  ณฑฑ
ฑฑณ            ณ        ณ                ณambiente com Gestใo de Servi็os para consiณฑฑ
ฑฑณ            ณ        ณ                ณderar os dias de cแlculo conforme cadastroณฑฑ
ฑฑณAllyson M.  ณ20/09/16ณTVYDXQ          ณAjuste p/ verificar corretamente os bene- ณฑฑ
ฑฑณ            ณ        ณ                ณfํcios quando hแ integra็ใo com gestใo de ณฑฑ
ฑฑณ            ณ        ณ                ณservi็os 									ณฑฑ
ฑฑณRaquel Hagerณ27/09/16ณTWCLU0          ณRealizado ajuste para pagamento devido da ณฑฑ
ฑฑณ            ณ        ณ                ณparte empresa conforme configura็๕es da   ณฑฑ
ฑฑณ            ณ        ณ                ณdefini็ใo de benefํcio.                   ณฑฑ
ฑฑณClaudinei S.ณ05/10/16ณTVQFFA          ณAjuste na fun็ใo fAuxCre() para calcular oณฑฑ
ฑฑณ            ณ        ณ                ณauxํlio-creche com reten็ใo de IR no novo ณฑฑ
ฑฑณ            ณ        ณ                ณID de cแlculo 1414, conforme a tabela S015ณฑฑ
ฑฑณCํcero Alvesณ19/10/16ณTWGETX          ณAjuste na fun็ใo fVlsRGB para procurar as ณฑฑ
ฑฑณ            ณ        ณ                ณfaltas na RGB com o perํodo do roteiro queณฑฑ
ฑฑณ            ณ        ณ                ณestแ sendo executado						ณฑฑ
ฑฑณLeandro Dr. ณ01/12/16ณ98909           ณAjuste para pagar todos os dias de VR e VAณฑฑ
ฑฑณ            ณ        ณ                ณque estejam definidos no cadastro de perioณฑฑ
ฑฑณ            ณ        ณ                ณdo para ser pago, mesmo feriados.			ณฑฑ
ฑฑณRenan Borgesณ05/12/16ณMRH-2368        ณAjuste para descontar faltas de acordo comณฑฑ
ฑฑณ            ณ        ณ                ณMnemonico P_BENBUSFA.                     ณฑฑ
ฑฑณAllyson M.  ณ28/12/16ณMRH-2534        ณAjuste em fCargaRI1() p/ validar as datas ณฑฑ
ฑฑณ            ณ        ณ                ณde inํcios e fim corretamente.			ณฑฑ
ฑฑณAllyson M.  ณ28/12/16ณMRH-3331        ณAjuste em fVTRCalc() p/ sempre gravar o   ณฑฑ
ฑฑณ            ณ        ณ                ณcแlculo de VT.							ณฑฑ
ฑฑณRaquel Hagerณ04/01/16ณMRH-4035        ณAjuste em fOtbCalc p/ validar Demissใo do ณฑฑ
ฑฑณ            ณ        ณ                ณfuncionแrio no cแlculo de Oturos Benef.	ณฑฑ
ฑฑณRaquel Hagerณ19/01/16ณMRH-5140        ณAjuste em fVTRCalc p/ considerar qtde de  ณฑฑ
ฑฑณ            ณ        ณ                ณvales por dia util no cแlc. de Dif. VTR. eณฑฑ
ฑฑณ            ณ        ณ                ณo valor jแ custeado pelo funcionแrio.   	ณฑฑ
ฑฑณRenan Borgesณ19/01/16ณMRH-5185		 ณAjuste para nใo gerar error.log quando ca-ณฑฑ
ฑฑณ            ณ        ณ          		 ณlendแrio for sint้tico (MV_TPCALEN).      ณฑฑ
ฑฑณAllyson M.  ณ20/01/17ณMRH-825         ณAjuste p/ considerar o ID 0242 p/ descontoณฑฑ
ฑฑณ            ณ        ณ                ณdas faltas e p/ nใo considerar os dias de	ณฑฑ
ฑฑณ            ณ        ณ                ณf้rias programadas se jแ considerou os dasณฑฑ
ฑฑณ            ณ        ณ                ณf้rias calculadas							ณฑฑ
ฑฑณCํcero Alvesณ08/06/17ณDRHPAG-2445	 ณAltera็ใo na fVTRSal para considerar a	ณฑฑ
ฑฑณ            ณ        ณ                ณverba de salแrio horista quando lan็ada naณฑฑ
ฑฑณ            ณ        ณ                ณRGB em vez dasa horas no cadastro de		ณฑฑ
ฑฑณ            ณ        ณ                ณfuncionแrios								ณฑฑ
ฑฑณCecํlia C.  ณ22/06/17ณDRHPAG-3454     ณAltera็ใo na fAuxCre para acessar o regis-ณฑฑ
ฑฑณ            ณ        ณ                ณtro da tabela S015 correta.               ณฑฑ
ฑฑณCecํlia C.  ณ04/07/17ณDRHPAG-3753     ณAjuste em fAuxCre para nใo ocorrer error.ณฑฑ
ฑฑณ            ณ        ณ                ณlog em fPosTab e acessar a tabela S015 corณฑฑ
ฑฑณ            ณ        ณ                ณretamente.                                ณฑฑ
ฑฑณCecํlia C.  ณ28/07/17ณDRHPAG-4463     ณAjuste em para calcular VA/VR/VT nใo pro- ณฑฑ
ฑฑณ            ณ        ณ                ณporcional quando utiliza calendแrio sint้-ณฑฑ
ฑฑณ            ณ        ณ                ณtico.                                     ณฑฑ
ฑฑณEsther V.   ณ03/10/17ณDRHPAG-6110     ณVerifica se funcionario estแ demitido no  ณฑฑ
ฑฑณ            ณ        ณ                ณperํodo de cแlculo.                       ณฑฑ
ฑฑณIsabel N.   ณ17/10/17ณDRHPAG-6790     ณAjuste no PE GPVTRDIAS.                   ณฑฑ
ฑฑณJaqueline L ณ15/12/17ณDRHPAG-7346     ณInclusใo do PE GPM5001                    ณฑฑ
ฑฑณJaqueline L ณ15/12/17ณDRHPAG-7670     ณQuando preenchido o R0_DPROPIN deverแ ser ณฑฑ
ฑฑณ            ณ        ณ  				 ณconsiderado ao inv้s do RFO_DIAFIX NO vr	ณฑฑ
ฑฑณJaqueline L ณ20/12/17ณDRHPAG-7941     ณVerifico se o funcionแrio ้ comissionado  ณฑฑ
ฑฑณ            ณ        ณ  				 ณse for, atualizo o custo com base no 		ณฑฑ
ฑฑณ            ณ        ณ  				 ณRCE_GCOMIS do sindicato do funcionแio 	ณฑฑ
ฑฑณJaqueline L ณ07/02/18ณDRHPAG-12323    ณQuando preenchido o R0_DPROPIN deverแ ser ณฑฑ
ฑฑณ            ณ        ณ  				 ณconsiderado ao inv้s do RFO_DIAFIX no VA  ณฑฑ
ฑฑณJaqueline L ณ08/02/18ณDRHPAG-12906    ณCorre็ใo para quando o R0_DPROPIN nใo est ณฑฑ
ฑฑณ            ณ        ณ  				 ณiver preenchido e o dias fixo sim, manter ณฑฑ
ฑฑณ            ณ        ณ  				 ณos dias fixo     							ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ fAssMed2 บAutor  ณ Adilson Silva      บ Data ณ 01/11/2009  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Novo Calculo da Assistencia Medica.                        บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ P11                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function fAssmed2(aCodFol)

 // Valores de Desconto
 Local nMedTit  := 0
 Local nMedDep  := 0
 Local nMedAgr  := 0

 // Valores da Parte Empresa
 Local nEmprTit := 0
 Local nEmprDep := 0
 Local nEmprAgr := 0

 // Bases p/ Calculo
 Local nBaseTit := 0
 Local nBaseDep := 0
 Local nBaseAgr := 0

 // Percentuais de Calculo do Desconto
 Local nPercTit := 0
 Local nPercDep := 0
 Local nPercAgr := 0

 // Quantidades Processadas - Funcionario / Dependentes / Agregados
 Local nQdeTit  := 0
 Local nQdeDep  := 0
 Local nQdeAgr  := 0

 // Variaveis Totalizadoras
 Local nDesc    := 0
 Local nRef     := 0
 Local nEmpr    := 0

 // Verbas Utilizadas no Calculo
 Local cPdDescTit
 Local cPdDescDep
 Local cPdDescAgr
 Local cPdEmprTit
 Local cPdEmprDep
 Local cPdEmprAgr
 Local nBusca, cTab, nLinha

 Local aPdDescDep := {}
 Local nX := 0
 Local dDataCalc
 Local nUltDia
 Local cIdadeDesc    := SuperGetMv( "MV_PLSBASI",, "1" )

If !FP_CODFOL(@aCodFol,SRA->RA_FILIAL)
	Return
EndIf

cPdDescTit := aCodFol[049,1]		//-- Assistencia Medica
cPdDescDep := aCodFol[723,1]		//-- Desc.Ass.Medica Dependentes
cPdDescAgr := aCodFol[724,1]		//-- Desc.Ass.Medica Agregados
cPdEmprTit := aCodFol[213,1]		//-- Base parte Empresa Assist.Medica
cPdEmprDep := aCodFol[725,1]		//-- Base parte Empresa Ass.Medica Dependentes
cPdEmprAgr := aCodFol[726,1]		//-- Base parte Empresa Ass.Medica Agregados


 If aCodFol[49,1] # Space( 03 ) .And. SRA->RA_ASMEDIC # Space( 02 )
    If ( c__Roteiro == "RES" .And. ( cCompl # "S" .Or. ( cCompl == "S" .And. MesAno(GetMemVar("RG_DATADEM")) == MesAno(GetMemVar("RG_DTGERAR")) ) .And. aIncRes[11] == "S")) .Or. ;
       ( c__Roteiro == "FOL" .And. ( SRA->RA_TIPOPGT # "S" .Or. ( SRA->RA_TIPOPGT == "S" .And. lUltSemana)) )

	   // Busca salแrio m๊s correspondente
	   If (SRA->RA_CATFUNC = "H")
       		nBusca := SRA->RA_HRSMES * SRA->RA_SALARIO
       Else
	   		nBusca := SALMES
	   EndIf

       // Busca Tabela Relacionada
       cTab   := "S008"
       If SRA->RA_TIPAMED == "2"
          nBusca := Val( Left(cFolMes,4) ) - Year( SRA->RA_NASC )
          cTab   := "S009"
          If Month( SRA->RA_NASC ) >  Val( Right(cFolMes,2) ) .OR. ( cIdadeDesc == "2" .AND. ( Month( SRA->RA_NASC ) ==  Val( Right(cFolMes,2) )))
             nBusca--
          EndIf
       EndIf

       // Calculo da Assistencia Medica
       If ( nLinha := fPosTab(cTab,SRA->RA_ASMEDIC,"=",4,nBusca,"<=",6) ) > 0
          nBaseTit := fTabela(cTab,nLinha,7)
          nPercTit := fTabela(cTab,nLinha,10) / 100

          nMedTit  := nBaseTit * nPercTit

          nEmprTit := nBaseTit - nMedTit
          nQdeTit ++


          If lDissidio
           	 dDataCalc := dDatabase
          Else
             nUltDia:= f_UltDia(CToD("01"+"/"+Substr(cFolMes,5,2)+"/"+Substr(cFolMes,1,4)))   //Ultimo dia do Mes
		 	 dDataCalc:= CToD(StrZero(nUltDia,2)+"/"+Substr(cFolMes,5,2)+"/"+Substr(cFolMes,1,4))
          EndIf
          SRB->(dbSetOrder( 1 ))
          SRB->(dbSeek( SRA->(RA_FILIAL + RA_MAT) ))
          Do While !(SRB->(Eof())) .And. SRB->(RB_FILIAL + RB_MAT) == SRA->(RA_FILIAL + RA_MAT)
				If 	(!Empty(SRB->RB_DTINIAM) .or. !Empty(SRB->RB_DTFIMAM))

						If (!Empty(SRB->RB_DTINIAM) .and. dDataCalc < SRB->RB_DTINIAM) .or.;
						   (!Empty(SRB->RB_DTFIMAM) .and. dDataCalc > SRB->RB_DTFIMAM)
							SRB->(dbSkip())
							Loop
						EndIf
				EndIf

		   	If (cPaisLoc == "BRA")
		   		aAdd(aPdDescDep,{SRB->RB_VBDESAM,0,,1})
		   	EndIf


             // Busca Tabela Relacionada
             nBusca := SALMES
             cTab   := "S008"
             If SRB->RB_TIPAMED == "2"
                nBusca := Val( Left(cFolMes,4) ) - Year( SRB->RB_DTNASC )
                cTab   := "S009"
                If Month( SRB->RB_DTNASC ) >  Val( Right(cFolMes,2) ) .OR. ( cIdadeDesc == "2" .AND. ( Month( SRB->RB_DTNASC ) ==  Val( Right(cFolMes,2) )))
                   nBusca--
                EndIf
             EndIf

             // Calculo da Assistencia Medica
             If ( nLinha := fPosTab(cTab,SRB->RB_CODAMED,"=",4,nBusca,"<=",6) ) > 0
                nBaseDep := fTabela(cTab,nLinha,8)
                nBaseAgr := fTabela(cTab,nLinha,9)
                nPercDep := fTabela(cTab,nLinha,11) / 100
                nPercAgr := fTabela(cTab,nLinha,12) / 100

                If SRB->RB_TPDEPAM == "1"
	               If (cPaisLoc == "BRA")
	                   aPdDescDep[Len(aPdDescDep),2] := nBaseDep * nPercDep
	                   aPdDescDep[Len(aPdDescDep),3] := "1"	//significa que ้ Dependente
	               EndIf
                   nMedDep  += nBaseDep * nPercDep
                   nEmprDep += nBaseDep - ( nBaseDep * nPercDep )
                   nQdeDep ++
                ElseIf SRB->RB_TPDEPAM == "2"
                   If (cPaisLoc == "BRA")
	                   aPdDescDep[Len(aPdDescDep),2] := nBaseAgr * nPercAgr
	                   aPdDescDep[Len(aPdDescDep),3] := "2"	//significa que ้ Agregado
                   EndIf
                   nMedAgr  += nBaseAgr * nPercAgr
                   nEmprAgr += nBaseAgr - ( nBaseAgr * nPercAgr )
                   nQdeAgr ++
                EndIf
             EndIf

             SRB->(dbSkip())
          EndDo
       EndIf
    EndIf
 EndIf

 If nMedTit + nMedDep + nMedAgr + nEmprTit + nEmprDep + nEmprAgr > 0
	//Verificar se o valor calculado ้ superior ao limite para desconto.
	//Caso o valor seja superior, os valores de descontos serใo alterados para atingir o Limite de Desconto.
	//A altera็ใo serแ na seguinte ordem: 1- Agregado / 2- Dependente / 3- Titular

    nDesc += nMedTit
    nRef  += nQdeTit
    nEmpr += nEmprTit

    // Soma Desconto dos Dependentes Caso Nao Tenha Identificador Especifico Cadastrado
//    If Empty( cPdDescDep )
//       nDesc += nMedDep
//       nRef  += nQdeDep
//    EndIf

    // Soma Desconto dos Agregados Caso Nao Tenha Identificador Especifico Cadastrado
//    If Empty( cPdDescAgr )
//       nDesc += nMedAgr
//       nRef  += nQdeAgr
//    EndIf

    // Soma Parte Empresa dos Dependentes Caso Nao Tenha Identificador Especifico Cadastrado
    If Empty( cPdEmprDep )
       nEmpr += nEmprDep
    EndIf

    // Soma Parte Empresa dos Agregados Caso Nao Tenha Identificador Especifico Cadastrado
    If Empty( cPdEmprAgr )
       nEmpr += nEmprAgr
    EndIf

    // Grava Desconto
    If nDesc > 0
       If cPaisLoc == "BRA"
          If ! (( Ascan(aPd,{|X| X[1] == cPdDescTit .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
          		fGeraVerba(cPdDescTit,nDesc,nRef)
          EndIf
       Else
       		fGeraVerba(cPdDescTit,nDesc,nRef,,,,,,,,.T.)
       EndIf
    EndIf
    // Grava Parte Empresa
    If nEmpr > 0
	   If cPaisLoc == "BRA"
          If ! (( Ascan(aPd,{|X| X[1] == cPdEmprTit .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
          		fGeraVerba(cPdEmprTit,nEmpr)
          EndIf
       Else
	       fGeraVerba(cPdEmprTit,nEmpr,,,,,,,,,.T.)
	   EndIf
    EndIf
    If (cPaisLoc == "BRA")
	    // Grava Desconto dos Dependentes
	    If nMedDep > 0
			For nX := 1 to Len(aPdDescDep)
				If aPdDescDep[nX,3] == "1"	//significa que ้ Dependente
					If cPaisLoc == "BRA"
				          If ! (( Ascan(aPd,{|X| X[1] == aPdDescDep[nX,1] .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
				          		fGeraVerba(aPdDescDep[nX,1],aPdDescDep[nX,2],aPdDescDep[nX,4])
				          EndIf
			       	Else
					fGeraVerba(aPdDescDep[nX,1],aPdDescDep[nX,2],aPdDescDep[nX,4],,,,,,,,.T.)
					EndIf
		  		EndIf
	  		Next nX
	    EndIf
    EndIf
    // Grava Parte Empresa dos Dependentes
    If !Empty( cPdEmprDep ) .And. nEmprDep > 0
    	If cPaisLoc == "BRA"
			If ! (( Ascan(aPd,{|X| X[1] == cPdEmprDep .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
				fGeraVerba(cPdEmprDep,nEmprDep)
			EndIf
		Else
       		fGeraVerba(cPdEmprDep,nEmprDep,,,,,,,,,.T.)
       	EndIf
    EndIf
    If (cPaisLoc = "BRA")
	    // Grava Desconto dos Agregados
	    If nMedAgr > 0
			For nX := 1 to Len(aPdDescDep)
				If aPdDescDep[nX,3] == "2"	//significa que ้ Agregado
					If cPaisLoc == "BRA"
						If ! (( Ascan(aPd,{|X| X[1] == aPdDescDep[nX,1] .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
							fGeraVerba(aPdDescDep[nX,1],aPdDescDep[nX,2],aPdDescDep[nX,4])
						EndIf
					Else
					fGeraVerba(aPdDescDep[nX,1],aPdDescDep[nX,2],aPdDescDep[nX,4],,,,,,,,.T.)
					EndIf
		  		EndIf
	  		Next nX
	    EndIf
    EndIf
    // Grava Parte Empresa dos Agregados

    If !Empty( cPdEmprAgr ) .And. nEmprAgr > 0
    	If cPaisLoc == "BRA"
			If ! (( Ascan(aPd,{|X| X[1] == cPdEmprAgr .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
				fGeraVerba(cPdEmprAgr,nEmprAgr)
			EndIf
		Else
       		fGeraVerba(cPdEmprAgr,nEmprAgr,,,,,,,,,.T.)
       	EndIf
    EndIf
 EndIf

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ fAssOdon บAutor  ณ Renata Bueno       บ Data ณ 18/11/2009  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Calculo da Assistencia Odontologica.                       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ P11                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function fAssOdon(aCodfol)

 // Valores de Desconto
 Local nOdonTit := 0
 Local nOdonDep := 0
 Local nOdonAgr := 0

 // Valores da Parte Empresa
 Local nEmprTit := 0
 Local nEmprDep := 0
 Local nEmprAgr := 0

 // Bases p/ Calculo
 Local nBaseTit := 0
 Local nBaseDep := 0
 Local nBaseAgr := 0

 // Percentuais de Calculo do Desconto
 Local nPercTit := 0
 Local nPercDep := 0
 Local nPercAgr := 0

 // Quantidades Processadas - Funcionario / Dependentes / Agregados
 Local nQdeTit  := 0
 Local nQdeDep  := 0
 Local nQdeAgr  := 0

 // Variaveis Totalizadoras
 Local nDesc    := 0
 Local nRef     := 0
 Local nEmpr    := 0

 // Verbas Utilizadas no Calculo
 Local cPdDescTit := aCodFol[714,1]		//-- Desconto Assist.Odontologica Titular
 Local cPdEmprTit := aCodFol[717,1]		//-- Parte Empresa Assist.Odontologica Titular
 Local cPdEmprDep := aCodFol[718,1]		//-- Parte Empresa Assist.Odontologica Dependente
 Local cPdEmprAgr := aCodFol[719,1]		//-- Parte Empresa Assist.Odontologica Agregado

 Local dDataCalc
 Local nUltDia

 Local aPdDescDep := {}
 Local nX := 0
 Local nBusca, cTab, nLinha

 If aCodFol[49,1] # Space( 03 ) .And. SRA->RA_ASODONT # Space( 02 )
    If ( c__Roteiro == "RES" .And. ( cCompl # "S" .Or. ( cCompl == "S" .And. MesAno(GetMemVar("RG_DATADEM")) == MesAno(GetMemVar("RG_DTGERAR")) ) .And. aIncRes[11] == "S")) .Or. ;
       ( c__Roteiro == "FOL" .And. ( SRA->RA_TIPOPGT # "S" .Or. ( SRA->RA_TIPOPGT == "S" .And. lUltSemana)) )

       // Busca Tabela Relacionada
       nBusca := SALMES
       cTab   := "S013"
       If SRA->RA_TPASODO == "2"
          nBusca := Val( Left(cFolMes,4) ) - Year( SRA->RA_NASC )
          cTab   := "S014"
          If Month( SRA->RA_NASC ) > Val( Right(cFolMes,2) )
             nBusca--
          EndIf
       EndIf

       // Calculo da Assistencia Odontologica
       If ( nLinha := fPosTab(cTab,SRA->RA_ASODONT,"=",4,nBusca,"<=",6) ) > 0
          nBaseTit := fTabela(cTab,nLinha,7)
          nPercTit := fTabela(cTab,nLinha,10) / 100

          nOdonTit := nBaseTit * nPercTit
          nEmprTit := nBaseTit - nOdonTit
          nQdeTit ++


          If lDissidio
           	 dDataCalc := dDatabase
          Else
             nUltDia:= f_UltDia(CToD("01"+"/"+Substr(cFolMes,5,2)+"/"+Substr(cFolMes,1,4)))   //Ultimo dia do Mes
		 	 dDataCalc:= CToD(StrZero(nUltDia,2)+"/"+Substr(cFolMes,5,2)+"/"+Substr(cFolMes,1,4))
          EndIf
          SRB->(dbSetOrder( 1 ))
          SRB->(dbSeek( SRA->(RA_FILIAL + RA_MAT) ))
          Do While !(SRB->(Eof())) .And. SRB->(RB_FILIAL + RB_MAT) == SRA->(RA_FILIAL + RA_MAT)
				If 	(!Empty(SRB->RB_DTINIAO) .or. !Empty(SRB->RB_DTFIMAO))

					If (!Empty(SRB->RB_DTINIAO) .and. dDataCalc < SRB->RB_DTINIAO) .or.;
						(!Empty(SRB->RB_DTFIMAO) .and. dDataCalc > SRB->RB_DTFIMAO)
		            SRB->(dbSkip())
						Loop
					EndIf
				EndIf

			    If(cPaisLoc == "BRA")
					aAdd(aPdDescDep,{SRB->RB_VBDESAO,0,,1})
                EndIf

             // Busca Tabela Relacionada
             nBusca := SALMES
             cTab   := "S013"
             If SRB->RB_TPASODO == "2"
                nBusca := Val( Left(cFolMes,4) ) - Year( SRB->RB_DTNASC )
                cTab   := "S014"
                If Month( SRB->RB_DTNASC ) > Val( Right(cFolMes,2) )
                   nBusca--
                EndIf
             EndIf

             // Calculo da Assistencia Odontologica
             If ( nLinha := fPosTab(cTab,SRB->RB_ASODONT,"=",4,nBusca,"<=",6) ) > 0
                nBaseDep := fTabela(cTab,nLinha,8)
                nBaseAgr := fTabela(cTab,nLinha,9)
                nPercDep := fTabela(cTab,nLinha,11) / 100
                nPercAgr := fTabela(cTab,nLinha,12) / 100

                If SRB->RB_TPDPODO == "1"
                	If(cPaisLoc == "BRA")
	                   aPdDescDep[Len(aPdDescDep),2] := nBaseDep * nPercDep
	                   aPdDescDep[Len(aPdDescDep),3] := "1"	//significa que ้ Dependente
	                EndIf
                   nOdonDep += nBaseDep * nPercDep
                   nEmprDep += nBaseDep - ( nBaseDep * nPercDep )
                   nQdeDep ++
                ElseIf SRB->RB_TPDPODO == "2"
                	If(cPaisLoc == "BRA")
	                   aPdDescDep[Len(aPdDescDep),2] := nBaseAgr * nPercAgr
	                   aPdDescDep[Len(aPdDescDep),3] := "2"	//significa que ้ Agregado
                    EndIf
                   nOdonAgr += nBaseAgr * nPercAgr
                   nEmprAgr += nBaseAgr - ( nBaseAgr * nPercAgr )
                   nQdeAgr ++
                EndIf
             EndIf

             SRB->(dbSkip())
          EndDo
       EndIf
    EndIf
 EndIf

 If nOdonTit + nOdonDep + nOdonAgr + nEmprTit + nEmprDep + nEmprAgr > 0
	/*Verificar se o valor calculado ้ superior ao limite para desconto.
	 Caso o valor seja superior, os valores de descontos serใo alterados para atingir o Limite de Desconto.
	 A altera็ใo serแ na seguinte ordem: 1- Agregado / 2- Dependente / 3- Titular	*/

    nDesc += nOdonTit
    nRef  += nQdeTit
    nEmpr += nEmprTit

    // Soma Parte Empresa dos Dependentes Caso Nao Tenha Identificador Especifico Cadastrado
    If Empty( cPdEmprDep )
       nEmpr += nEmprDep
    EndIf

    // Soma Parte Empresa dos Agregados Caso Nao Tenha Identificador Especifico Cadastrado
    If Empty( cPdEmprAgr )
       nEmpr += nEmprAgr
    EndIf

    // Grava Desconto
    If nDesc > 0
    	If cPaisLoc == "BRA"
			If ! (( Ascan(aPd,{|X| X[1] == cPdDescTit .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
				fGeraVerba(cPdDescTit,nDesc,nRef)
			EndIf
		Else
	       fGeraVerba(cPdDescTit,nDesc,nRef,,,,,,,,.T.)
	    EndIf
    EndIf
    // Grava Parte Empresa
    If nEmpr > 0
    	If cPaisLoc == "BRA"
			If ! (( Ascan(aPd,{|X| X[1] == cPdEmprTit .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
				fGeraVerba(cPdEmprTit,nEmpr)
			EndIf
		Else
	       fGeraVerba(cPdEmprTit,nEmpr,,,,,,,,,.T.)
	    EndIf
    EndIf
    If(cPaisLoc = "BRA")
	    // Grava Desconto dos Dependentes
	    If nOdonDep > 0
			For nX := 1 to Len(aPdDescDep)
				If aPdDescDep[nX,3] == "1"	//significa que ้ Dependente
					If cPaisLoc == "BRA"
						If ! (( Ascan(aPd,{|X| X[1] == aPdDescDep[nX,1] .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
								fGeraVerba(aPdDescDep[nX,1],aPdDescDep[nX,2],aPdDescDep[nX,4])
						EndIf
					Else
					fGeraVerba(aPdDescDep[nX,1],aPdDescDep[nX,2],aPdDescDep[nX,4],,,,,,,,.T.)
					EndIf
		  		EndIf
	  		Next nX
	    EndIf
    EndIf
    // Grava Parte Empresa dos Dependentes
    If !Empty( cPdEmprDep ) .And. nEmprDep > 0
    	If cPaisLoc == "BRA"
			If ! (( Ascan(aPd,{|X| X[1] == cPdEmprDep .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
					fGeraVerba(cPdEmprDep,nEmprDep)
			EndIf
		Else
	       fGeraVerba(cPdEmprDep,nEmprDep,,,,,,,,,.T.)
	    EndIf
    EndIf
    If (cPaisLoc == "BRA")
	    // Grava Desconto dos Agregados
	    If nOdonAgr > 0
			For nX := 1 to Len(aPdDescDep)
				If aPdDescDep[nX,3] == "2"	//significa que ้ Agregado
					If cPaisLoc == "BRA"
						If ! (( Ascan(aPd,{|X| X[1] == aPdDescDep[nX,1] .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
								fGeraVerba(aPdDescDep[nX,1],aPdDescDep[nX,2],aPdDescDep[nX,4])
						EndIf
					Else
					fGeraVerba(aPdDescDep[nX,1],aPdDescDep[nX,2],aPdDescDep[nX,4],,,,,,,,.T.)
					EndIf
		  		EndIf
	  		Next nX
	    EndIf
    EndIf
    // Grava Parte Empresa dos Agregados
    If !Empty( cPdEmprAgr ) .And. nEmprAgr > 0
    	If cPaisLoc == "BRA"
			If ! (( Ascan(aPd,{|X| X[1] == cPdEmprAgr .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
					fGeraVerba(cPdEmprAgr,nEmprAgr)
			EndIf
		Else
	       fGeraVerba(cPdEmprAgr,nEmprAgr,,,,,,,,,.T.)
	    EndIf
    EndIf
 EndIf

Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfAuxCre   บAutor  ณ Renata Elena       บ Data ณ 30/11/2009  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Calculo do Auxilio Creche                                  บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ P11                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Function fAuxCre(aCodfol,cPdMes)

Local cPdMeIsIr	:= If(Len(aCodFol) > 1413, aCodFol[1414,1], "")
Local dDtRef   	:= StoD( cAnoMes + "01" )
Local nValBase	:= 0
Local nValCre   := 0
Local nValCrIsIR:= 0 	//Valor do Auxilio com a isen็ใo de IR
Local nQdeDep  	:= 0
Local nQdeDeIsIR:= 0 	//Quantidade de Dependentes com isen็ใo de IR
Local nPerc		:= 100
Local nPercIsIr	:= 100	// Percentual da verba com isen็ใo de IR
Local lValorDep	:= .F. // Se o valor do Auxํlio-Creche a ser considerado serแ o valor informado no cad. de dependentes.
Local lIsIR		:= .F.
Local nBusca, cTab, nLinha
Local nSizeSin := TamSX3("RA_SINDICA")[1]

nPerc		:= RetValSRV(cPdMes,SRA->RA_FILIAL,"RV_PERC") /100
nPercIsIr	:= RetValSRV(cPdMeIsIr,SRA->RA_FILIAL,"RV_PERC") /100

SRB->(dbSetOrder( 1 ))
SRB->(dbSeek( SRA->(RA_FILIAL + RA_MAT) ))

Do While !(SRB->(Eof())) .And. SRB->(RB_FILIAL + RB_MAT) == SRA->(RA_FILIAL + RA_MAT)

	If !(SRB->RB_AUXCRE == "1") .or. ( !Empty(SRB->RB_DTBAIXA) .and. AnoMes(SRB->RB_DTBAIXA) <= cAnoMes)
		SRB->(dbSkip())
		Loop
	Else

		If Empty(cPdMes)
			CMSGLOG := FMSGFORM({19}) + " -> 0721 - " + aCodFol[721,2] //"Nao existe verba cadastrada para identificador de calculo "
			S_MSGLOG()
			FINALCALC()
			Return()
		EndIf

		If SRB->RB_VLRCRE > 0
			lValorDep := .T.
    	Else
    		lValorDep := .F.
    	Endif
    	// Busca Tabela Relacionada
		nBusca :=  DateDiffMonth( dDtRef , SRB->RB_DTNASC )
		cTab   := "S015"
		dDataRef	:= dDataBase

		If !lValorDep
			//verifica se dados para o sindicato mes / ano / filial
			nLinha := fPosTab(cTab,SRA->RA_SINDICA,"==",4 ,,,,,,,,SRA->RA_FILIAL,,,,dDtRef) //filial+mes/ano+sindicato (preenchidos)
			If nLinha == 0
			     nLinha := fPosTab(cTab,SRA->RA_SINDICA,"==",4 ,,,,,,,,SRA->RA_FILIAL,,,,Ctod("  /  /    ")) //filial+sindicato (preenchidos)
			EndIf
			If nLinha == 0
			    nLinha := fPosTab(cTab,SRA->RA_SINDICA,"==",4 ,,,,,,,,Space(FWGetTamFilial),,,,dDtRef) //mes/ano+sindicato (preenchidos)
			EndIf
			If nLinha == 0
			    nLinha := fPosTab(cTab,SRA->RA_SINDICA,"==",4 ,,,,,,,,Space(FWGetTamFilial),,,,Ctod("  /  /    ")) //sindicato (preenchido)
			EndIf
			If nLinha == 0
			    nLinha := fPosTab(cTab,Space(nSizeSin),"==",4 ,,,,,,,,Space(FWGetTamFilial),,,,Ctod("  /  /    ")) //filial+mes/ano+sindicato (em branco)
			EndIf

			If nLinha > 0
				//so depois verifica os meses que tem direito para nใo mostrar msg indevidamente, pq se o dependente nใo tem mais direito nใo preciso mostrar msg
				nLinha := fPosTab(cTab,SRA->RA_SINDICA,"==",4 ,nBusca,"<=",5,,,,,SRA->RA_FILIAL,nBusca,">=",7,dDtRef) //filial+mes/ano+sindicato (preenchidos)
				If nLinha == 0
				     nLinha := fPosTab(cTab,SRA->RA_SINDICA,"==",4 ,nBusca,"<=",5,,,,,SRA->RA_FILIAL,nBusca,">=",7,Ctod("  /  /    ")) //filial+sindicato (preenchidos)
				EndIf
				If nLinha == 0
				    nLinha := fPosTab(cTab,SRA->RA_SINDICA,"==",4 ,nBusca,"<=",5,,,,,Space(FWGetTamFilial),nBusca,">=",7,dDtRef) //mes/ano+sindicato (preenchidos)
				EndIf
				If nLinha == 0
				    nLinha := fPosTab(cTab,SRA->RA_SINDICA,"==",4 ,nBusca,"<=",5,,,,,Space(FWGetTamFilial),nBusca,">=",7,Ctod("  /  /    ")) //sindicato (preenchido)
				EndIf
				If nLinha == 0
				    nLinha := fPosTab(cTab,Space(nSizeSin),"==",4 ,nBusca,"<=",5,,,,,Space(FWGetTamFilial),nBusca,">=",7,Ctod("  /  /    ")) //filial+mes/ano+sindicato (em branco)
				EndIf

				If nLinha > 0
					nValBase := fTabela(cTab,nLinha,6)
					lIsIR	 := fTabela(cTab,nLinha,8) == "N"
				EndIf
			Else
				CMSGLOG := FMSGFORM({40}) + " S015 para o Sindicato: " + SRA->RA_SINDICA  //"Nใo existem dados cadastrados na Tabela  "
				S_MSGLOG()
			EndIf
		Else
			nValBase := SRB->RB_VLRCRE
		EndIf

		If nValBase > 0
			If !lIsIR
	  			nValCre  += (nValBase * If(nPerc == 0, 1, nPerc) )
	  			nQdeDep ++
	  		Else
	  			nValCrIsIR += (nValBase * If(nPercIsIr == 0, 1, nPercIsIr) )
	  			nQdeDeIsIR ++
	  		EndIf
	  	EndIf
	EndIf

	SRB->(dbSkip())
EndDo

If nValCre > 0 .Or. nValCrIsIR > 0
	If cPaisLoc == "BRA"
 		If ! (( Ascan(aPd,{|X| X[1] == cPdMes .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
      		fGeraVerba(cPdMes,nValCre,nQdeDep)
       EndIf
		IF ! (( Ascan(aPd,{|X| X[1] == cPdMeIsIr .And. X[9] <> "D" .And. X[7] == "I"}) > 0 ) )
      		fGeraVerba(cPdMeIsIr,nValCrIsIR,nQdeDeIsIR)
       Endif
   	Else
   		fGeraVerba(cPdMes,nValCre,nQdeDep,,,,,,,,.T.)
   	EndIf
EndIf

Return
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ FornVld  บAutor  ณ Adrianne Furtado   บ Data ณ 11/11/2010  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Validacao de Fornedores na Configura็ใo de Ass Medica/Odontบฑฑ
ฑฑบ          ณ modelo 2                                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ P10                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function FornVld(cCodTab)
Local lRet := .F.
Local nPosIni := 1
Local nLenCod
Local nOrdem
Local nX := 1

Local cVar		:= Alltrim(ReadVar())
Local cFOrigem := ""

Local oModel
Local oStructRHX
Local cTpForn

If cVar == "M->RB_CODAMED" .or. cVar == "SRB->RB_CODAMED" // DEPENDENTES ASSISTENCIA MEDICA
	cFOrigem := xFilial("SRB")
ElseIf cVar == "M->RB_ASODONT" .or. cVar == "SRB->RB_ASODONT" // DEPENDENTES ASSISTENCIA ODONTOLOGICA
	cFOrigem := xFilial("SRB")
ElseIf __READVAR == "M->CODFOR"           //CONSULTA F3 A PARTIR DE UMA TABELA SNNN
	cFOrigem := aCols[n][1]
	If cCodTab == "S074"
		cTpForn := aCols[n][5]
		If cTpForn == "1"
			cCodTab := "S016"
		Else
			cCodTab := "S017"
		EndIf
	Endif
ElseIf cVar $ "M->RHK_CODFOR/M->RHL_CODFOR/M->RHM_CODFOR/M->RHN_CODFOR/M->RHO_CODFOR/M->JX_CODFORN"
	oModel 		:= FWModelActive()

	If cVar == "M->RHK_CODFOR"
		oStructRHX	:= oModel:GetModel( "GPEA001_MRHK" )
		cTpForn		:= oStructRHX:GetValue("RHK_TPFORN")
	ElseIf cVar == "M->RHL_CODFOR"
		oStructRHX	:= oModel:GetModel( "GPEA001_MRHL" )
		cTpForn		:= oStructRHX:GetValue("RHL_TPFORN")
	ElseIf cVar == "M->RHM_CODFOR"
		oStructRHX	:= oModel:GetModel( "GPEA001_MRHM" )
		cTpForn		:= oStructRHX:GetValue("RHM_TPFORN")
	ElseIf cVar == "M->RHN_CODFOR"
		oStructRHX	:= oModel:GetModel( "GPEA002_MRHN" )
	  	cTpForn		:= oStructRHX:GetValue("RHN_TPFORN")
	ElseIf cVar == "M->RHO_CODFOR"
		oStructRHX	:= oModel:GetModel( "GPEA003_MRHO" )
	  	cTpForn		:= oStructRHX:GetValue("RHO_TPFORN")
	ElseIf cVar == "M->JX_CODFORN"
		oStructSJX	:= oModel:GetModel( "GPEA063_MSJX" )
		cTpForn := oStructSJX:GetValue("JX_TPFORN")
	EndIf

	If FunName() = "GPEA063"
		cFOrigem := xFilial("SJX")
	Else
		cFOrigem := xFilial("SRA")
	EndIf

	If cTpForn == "1"
		cCodTab := "S016"
	ElseIf cTpForn == "2"
		cCodTab := "S017"
	EndIf

	M->CODFOR := &(cVar)
EndIf

RCB->(dbSetOrder(3))
RCB->(dbSeek(xFilial("RCB")+"CODIGO    "+cCodTab))

nLenCod := RCB->RCB_TAMAN
nOrdem := Val(RCB->RCB_ORDEM)

RCB->(dbSetOrder(1))
RCB->(dbSeek(xFilial("RCB")+cCodTab))

While nX < nOrdem .and. RCB->RCB_CODIGO == cCodTab

	If Val(RCB->RCB_ORDEM) < nOrdem
		nPosIni += RCB->RCB_TAMAN
		nX ++
	EndIf
	RCB->(DbSkip())
EndDo

RCC->(dbSetOrder(1))

If RCC->(dbSeek(xFilial("RCC")+cCodTab))
	While RCC->RCC_CODIGO == cCodTab .and. !lRet
		If M->CODFOR == SubStr(RCC->RCC_CONTEU,nPosIni,nLenCod) .and.;
			(Empty(RCC->RCC_FIL) .or. RCC->RCC_FIL == cFOrigem)
			lRet := .T.
		EndIf
		RCC->(DbSkip())
	EndDo
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfCarDiasMes    บAutor  ณLeandro Drumond  บ Data ณ  24/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCarrega dados dos beneficios                                  บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fCarDiasMes(aDias, lPropAdm, aPeriod)

	Local nX 			:= 0
	Local lCalc			:= .F.
	Local cTipo			:= "3"
	Local cVale			:= ""
	Local cMotivo		:= ""
	Local dBkpFim		:= cToD("//")
	Local dIni			:= ctod("")
	Local dFim			:= ctod("")
	Local lAdd			:= .T.
	Local aTabCalend	:= {}
	Local lPropFerias	:= .F. //APERGUNTE[4, 3] == 1
	Local lFeriado		:= .F.

	DEFAULT aPeriod := aPeriodo
	DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo

	If Len(aPeriod) < 1
		If __lMemCalc
			fAddMemLog("Perํodo nใo carregado" ,1,1)
		EndIf
		Return(Nil)
	EndIf

	If cTipoRot == "8"
		cTipo 		:= "0"
		lPropFerias	:= APERGUNTE[4,3] == 1	// Se calcula proporcional a f้rias programadas
	ElseIf cTipoRot $ "D*E"
		cTipo		:= If(cTipoRot == "D", "1", "2" )
		lPropFerias	:= APERGUNTE[3,3] == 1	// Se calcula proporcional a f้rias programadas
	EndIf

	If SRA->RA_TPCONTR == "2" .And. !Empty(SRA->RA_DTFIMCT)
		dBkpFim				:= aPeriod[nPosSem, 4]
		aPeriod[nPosSem, 4] := Min( SRA->RA_DTFIMCT, aPeriod[nPosSem, 4] )
	EndIf

	If LNOVOCALCB
		dIni		:=  aPeriod[nPosSem, 3]
		dFim		:=  aPeriod[nPosSem, 4]
	EndIf

	If __lMemCalc
		fAddMemLog("Proporcional a f้rias : " + If(lPropFerias,"Sim","Nใo") ,1,1)
		fAddMemLog("Proporcional a admissใo : " + If(LPROPADM,"Sim","Nใo") ,1,1)
		fAddMemLog("Data Admissใo : " + dtoc(SRA->RA_ADMISSA)  ,1,1)
		fAddMemLog("Periodo de Calculo : " + dtoc(dIni) + " a " + dtoc(dFim) ,1,1)
		fAddMemLog("Calendario utilizado(MV_USACPER) : " +If(lUsaCPer,"FOLHA","PONTO") ,1,1)
	EndIf

	aDias := {}

	// Se utilizar o calendแrio do ponto carrega o aDias com base nesse calendแrio
	If !lUsaCPer
		aTabCalend := fCarPonto(aPeriod,lPropAdm)
		If Len(aTabCalend) > 1
			SPA->( dbSeek( xFilial("SPA", SRA->RA_FILIAL ) + SRA->RA_REGRA ) )
			lFeriado := ( SPA->PA_FERIADO == "S" .Or. ( SPA->( Type("PA_RECBENF") ) != "U" .And. SPA->PA_RECBENF == "1" ) )
			aEval( aTabCalend, { |x| If( If( lPropFerias, (Empty(dDataDem) .Or. x[1] < dDataDem) .And. ((( x[6] == "S" .And. ( x[19] == .F. .or. x[10] == "E" ) ) .Or. ( lFeriado .And. (( x[6] == "F" .And. x[24] == .F. .And. x[36] == "S") .Or. ( x[6] == "S" .And. x[24] == .F. .And. x[36] == "S" .And. x[19] == .T.))))), (Empty(dDataDem) .OR. x[1] < dDataDem) .AND.((( ( x[6] == "S" .And. x[19] == .F. ) .Or. ( lFeriado .And. ( (x[6] == "F" .And. x[24] == .F. .And. x[36] == "S" ) .Or. (x[6] == "S" .And. x[24] == .F. .And. x[36] == "S" .And. x[19] == .T.)))) ) .Or. ( x[6] == "N" .And. x[19] == .F. .And. x[24] == .T. .And. x[36] == "S" ) )), aAdd( aDias, { x[1], If(x[6] == "S","1",""), "1", .T., "" }), aAdd( aDias, { x[1], If(x[6] == "S","1",""), "1", .F., "" })) } )
		EndIf
		If __lMemCalc
			If !Empty(dDataDem)
				fAddMemLog("Data de demissใo : " + dtoc(dDataDem) ,1,1)
				fAddMemLog("Considera feriado cadastro de Regra(SPA) : " + If(lFeriado,"Sim","Nใo") ,1,1)
			EndIf
		EndIf
		// Proporcionaliza a admissใo
		If lPropAdm .Or. SRA->RA_ADMISSA > aPeriod[nPosSem, 4]
			For nX := 1 To Len(aDias)
				If (SRA->RA_ADMISSA > aDias[nX][1])
					aDias[nX][4] := .F.
					aDias[nX][5] := "ADM"
				EndIf
			Next
		EndIf

		// Dias de refer๊ncia
		If(Empty(nDiasRef), aEval( aDias, {|x| If( x[4], nDiasRef++, Nil ) }), Nil)

		Return (aDias)
	EndIf

	For nX := 1 to Len(aPeriod[1][5])
		lAdd := .T.
		If LNOVOCALCB .And. ! (aPeriod[1][5][nX][1] >= dIni .And. aPeriod[1][5][nX][1] <= dFim )
			lAdd := .F.
		EndIf

		If lAdd
			lCalc	:= .T.
			cMotivo	:= ""

			If cTipo = "0"  //Vale Transporte
				cVale := aPeriod[1][5][nX][3]
			ElseIf cTipo = "1" //Vale Refei็ใo
				cVale := aPeriod[1][5][nX][5]
			ElseIf cTipo = "2" //Vale Alimenta็ใo
				cVale := aPeriod[1][5][nX][8]
			Else //Outros beneficios
				cVale := "1"
			Endif
			lCalc := cVale == "1"

			If lPropAdm .And. (SRA->RA_ADMISSA > aPeriod[1][5][nX][1])
				lCalc := .F.
				cMotivo  := "ADM"
			EndIf

			aAdd( aDias, { aPeriod[nPosSem][5][nX][1], aPeriod[nPosSem][5][nX][2], cVale, lCalc, cMotivo })
		EndIf
	Next nX

	// Dias de Refer๊ncia
	If(Empty(nDiasRef), aEval( aDias, {|x| If( x[4], nDiasRef++, Nil ) }), Nil)

	If SRA->RA_TPCONTR == "2" .And. !Empty(SRA->RA_DTFIMCT)
		aPeriod[nPosSem, 4] := dBkpFim
	EndIf

Return (aDias)


//-------------------------------------------------------------------
/*/{Protheus.doc} function RHOTPATEN
Retorna os tipos de Atendimentos disponํveis na rotina de Co-participa็ใo e Reembolso. Usado no ComboBox de Cadastro.
@author  Hugo de Oliveira
@since   04/04/2019


@version 12.1.23
/*/
//-------------------------------------------------------------------
Function RHOTPATEN()
	Local cOpcBox := "1=Eletivo;2=PS;3=Exame;4=PAC;5=Material/Medicamento;6=Interna็ใo;7=Terapia;8=Taxa/Diแria;9=Case;10=Reembolso;11=Remo็ใo;12=Nใo Informado;13=Ambulatorio"
Return(cOpcBox)


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfCarPonto    บAutor  ณLeandro Drumond  บ Data ณ  24/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCarrega dados dos beneficios                                  บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fCarPonto(aPeriod,lPropAdm)
Local dIni		:= CtoD("")
Local dFim		:= CtoD("")
Local aTabPadrao:= {}
Local aTurnos	:= {}
Local aTabCalend:= {}
Local lPropAfast:= .T.
Local lPropFER	:= .T.

Static _aTabAux	:= {}
Static __cFunTab:= ""

DEFAULT aPeriod  := aPeriodo
DEFAULT lPropAdm := If(CTIPOROT == "8",APERGUNTE[2,3] == 1, If(cTipoRot $ "D*E",APERGUNTE[1,3] == 1,.F.))

If cTipoRot == "8"
	lPropAfast 	:= (APERGUNTE[3, 3] == 1 .Or. APERGUNTE[3, 3] == 4)
	lPropFER 	:= (APERGUNTE[3, 3] == 1 .Or. APERGUNTE[3, 3] == 3)
ElseIf cTipoRot $ "D*E"
	lPropAfast 	:= (APERGUNTE[2, 3] == 1 .Or. APERGUNTE[2, 3] == 4)
	lPropFER 	:= (APERGUNTE[2, 3] == 1 .Or. APERGUNTE[2, 3] == 3)
EndIf

dIni		:=  aPeriod[nPosSem,3]
dFim		:=  aPeriod[nPosSem,4]

If LNOVOCALCB
	If cTipoRot == "8" .And. aPergunte[1,3] == 3
		dIni :=  aPergunte[12, 3]
		dFim :=  aPergunte[13, 3]
	ElseIf cTipoRot $ "D*E" .And. aPergunte[6,3] == 1
		dIni :=  aPergunte[8, 3]
		dFim :=  aPergunte[9, 3]
	EndIf
EndIf

//Tratamento para performance
If __cFunTab <> SRA->RA_FILIAL + SRA->RA_MAT
	_aTabAux := {}
EndIf

//Carrega apenas se jแ nใo tiver sido carregado
If Empty(_aTabAux) .or. _aTabAux[1,1] > dFim .or. _aTabAux[Len(_aTabAux)][1] < dFim

	CriaCalend(		dIni 			,;	//01 -> Data Inicial do Periodo
					dFim			,;	//02 -> Data Final do Periodo
					SRA->RA_TNOTRAB	,;	//03 -> Turno Para a Montagem do Calendario
					SRA->RA_SEQTURN	,;	//04 -> Sequencia Inicial para a Montagem Calendario
					@aTabPadrao		,;	//05 -> Array Tabela de Horario Padrao
					@_aTabAux		,;	//06 -> Array com o Calendario de Marcacoes
					SRA->RA_FILIAL  ,;	//07 -> Filial para a Montagem da Tabela de Horario
					SRA->RA_MAT   	,;	//08 -> Matricula para a Montagem da Tabela de Horario
					SRA->RA_CC   	,;	//09 -> Centro de Custo para a Montagem da Tabela
					@aTurnos		,;	//10 -> Array com as Trocas de Turno
					NIL				,;	//11 -> Array com Todas as Excecoes do Periodo
					NIL				,;	//12 -> Se executa Query para a Montagem da Tabela Padrao
					.T.				,;	//13 -> Se executa a funcao se sincronismo do calendario (Default .T.)
					.T.			 	;	//14 -> Se Forca a Criacao de Novo Calendario
					)

	__cFunTab := SRA->RA_FILIAL + SRA->RA_MAT
	
EndIf

aEval( _aTabAux, { |x| If( x[4] == "1E" .and. x[1] >= dIni .and. x[1] <= dFim, aAdd( aTabCalend, aClone(x) ), Nil)  } )

If lPropAdm
	aEval(aTabCalend,{ |x| if(x[1] < SRA->RA_ADMISSA, x[6]:= "N",nil)})
EndIf

If !lPropAfast
	aEval(aTabCalend, { |x| if(x[24] .and. x[25] == "A", ( x[6] := x[36], x[24] := .F.), nil)})
EndIf

If !lPropFER
	aEval(aTabCalend, { |x| if(x[24] .and. x[25] == "F", ( x[6] := x[36], x[24] := .F.), nil)})
EndIf

Return (aTabCalend)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVlsAus		   บAutor  ณLeandro Drumond  บ Data ณ  24/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณVerifica ausencias para pagamento de beneficios               บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVlsAus(dDtPesq1, dDtPesq2, aAfast, aDiasMes)
Local aArea		:= GetArea()
Local cTipoAfas	:= ""
Local dDataAux	:= CtoD("")
Local dDataAux2	:= CtoD("")
Local nAfas		:= 0
Local nLenAfa 	:= 0
Local nX		:= 0
Local nY		:= 0
Local nAfaFer	:= If(cTipoRot == "I", 0, If(cTipoRot == '8', aPergunte[3,3], aPergunte[2,3]))

DEFAULT dDtPesq1 	:= aPeriodo[nPosSem,3] //Data inicial do periodo de calculo
DEFAULT dDtPesq2 	:= aPeriodo[nPosSem,4] //Data final do periodo de calculo
DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo

aAfast 		:= {}
nDiasAfas 	:= 0
nFerVen		:= 0
DIASTRAB	:= 0

// -- Apura Dias Trabalhados
fDiasTrab(@DIASTRAB)

If nAfaFer <> 1
	cTipoAfas := If(nAfaFer == 3, '4','123')
EndIf
// -- Apura Dias Trabalhados e Dias de Afastamento
fDiasAfast(@nAfas,@DIASTRAB,dDataBase,,,cTipoAfas)

DIASTRAB	:= Max( ( DIASTRAB  + nDiasMat + nDPrgSalMa ) , 0 )

fRetAfas(dDtPesq1, dDtPesq2 , , , ,cTipoAfas, @aAfast,,,,,,,,,.F.)

If __lMemCalc
	fAddMemLog("Periodo de Calculo : " + dtoc(dDtPesq1) + " a " + dtoc(dDtPesq2) ,1,1)
	fAddMemLog("Dias trabalhados: " + cvaltochar(DIASTRAB) ,1,1)
	fAddMemLog("Dias afastados: " + cvaltochar(nAfas) ,1,1)
	fAddMemLog("Dias maternidade: " + cvaltochar(nDiasMat + nDPrgSalMa) ,1,1)
EndIf

If !Empty(aAfast)
	nLenAfa := Len(aAfast)
	If Len(aDiasMes) > 0
		For nX := 1 to nLenAfa
			dDataAux  := aAfast[nX,3]
			dDataAux2 := aAfast[nX,4]
			If ( !Empty(dDataAux2) .And. aDiasMes[1,1] > dDataAux2 ) .Or. aDiasMes[Len(aDiasMes),1] < dDataAux
				Loop
			EndIf
			For nY := 1 to Len(aDiasMes)
				If aDiasMes[nY,1] >= dDataAux .and. ( aDiasMes[nY,1] <= dDataAux2 .OR. Empty(dDataAux2))
					aDiasMes[nY,4] := .F.
					If aAfast[nX,5] == "A"
						aDiasMes[nY,5] := "AFA"
						nDiasAfas++
					ElseIf aAfast[nX,5] == "F"
						aDiasMes[nY,5] := "FER"
						nFerVen++
					EndIf
				EndIf
			Next nY
		Next nX
	EndIf
EndIf
RestArea(aArea)

Return Nil

/*/{Protheus.doc} fVlsFerPrg
Verifica ferias programadas para pagamento de beneficios
@author Leandro Drumond
@since 24/07/2013
@param dDtPesq1, date, Data para o inํcio da pesquisa
@param dDtPesq2, date, Data para o Fim da Pesquisa
@param aDiasMes, array, Array com os dias do mes
@example
	fVlsFerPrg(cTod("01/09/2017"), cTod("30/09/2017"), @aDiasMes)
/*/
Function fVlsFerPrg(dDtPesq1, dDtPesq2, aDiasMes, lBusca)

	Local aArea		:= GetArea()
	Local cMesAno	:= ""
	Local dDataAux	:= CtoD("")
	Local dDataAux2	:= CtoD("")
	Local nDiasFer	:= 0
	Local nPos		:= 0
	Local nY		:= 0

	Default dDtPesq1	:= aPeriodo[nPosSem,3]	// Data inicial do periodo de calculo
	Default dDtPesq2	:= aPeriodo[nPosSem,4]	// Data final do periodo de calculo
	Default lBusca		:= .F.					// Apenas realiza a busca, nใo atualiza o mnem๔nico nFerVen
	DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo

	DbSelectArea("SRF")
	DbSetOrder(1)

	If DbSeek( xFilial("SRF") + SRA->RA_MAT )
		cMesAno := MesAno(dDtPesq1)
		While SRF->( ! Eof() .and. RF_FILIAL + RF_MAT == xFilial("SRF") + SRA->RA_MAT )
			If SRF->RF_STATUS = "1"		//Ativo
				If __lMemCalc
					fAddMemLog("Periodo de f้rias ativo : " + dtoc(SRF->RF_DATABAS) + " a " + dtoc(SRF->RF_DATAFIM) ,1,1)
				EndIf
				dDataAux := CtoD("")
				If MesAno(SRF->RF_DATAINI) == cMesAno .And. SRF->RF_DATAINI <= dDtPesq2
					dDataAux := SRF->RF_DATAINI
					nDiasFer := SRF->RF_DFEPRO1
				ElseIf MesAno(SRF->RF_DATINI2) == cMesAno .And. SRF->RF_DATINI2 <= dDtPesq2
					dDataAux := SRF->RF_DATINI2
					nDiasFer := SRF->RF_DFEPRO2
				ElseIf MesAno(SRF->RF_DATINI3) == cMesAno .And. SRF->RF_DATINI3 <= dDtPesq2
					dDataAux := SRF->RF_DATINI3
					nDiasFer := SRF->RF_DFEPRO3
				EndIf
				If ! Empty(dDataAux)
					dDataAux2 := dDataAux + nDiasFer - 1 //Subtrai um porque o dia de inicio deve ser contado.
					dDataAux2 := Min(dDataAux2, dDtPesq2) //Limite eh a menor data entre final do periodo e fim das ferias
					If !Empty(aDiasMes)
						nPos := Ascan( aDiasMes, { |X| X[1] == dDataAux } )
						If nPos == 0
							nPos := Ascan( aDiasMes, { |X| X[1] == dDataAux2 } )
						EndIf
						If nPos > 0
							For nY := nPos to Len(aDiasMes)
								If aDiasMes[nY, 1] >= dDataAux .and. aDiasMes[nY,1] <= dDataAux2
									If !lBusca .And. aDiasMes[nY, 5] != "FER"
										nFerVen++
									EndIf
									aDiasMes[nY, 4] := .F.
									aDiasMes[nY, 5] := "PRG"
								Else
									Exit //Sai do laco interno
								EndIf
							Next nY
						EndIf
					EndIf
				EndIf
				Exit //So deve existir uma programacao ativa
			EndIf
			SRF->(DbSkip())
		EndDo
	EndIf

	If __lMemCalc
		fAddMemLog("Dias ferias programadas : " + cvaltochar(nFerVen) ,1,1)
	EndIf
	RestArea(aArea)

Return Nil


/*/{Protheus.doc} fVlsRGB
Verifica exist๊ncia de faltas na RGB, SRC ou SRD e alimenta mnem๔nico
@author Leandro Drumond
@since 25/07/2013
@version 12.1.17
@history 01/11/2017, Cํcero Alves, Incluํdo tratamento para busca das faltas no ponto
/*/
Function fVlsRGB()

	Local aArea			:= GetArea()
	Local aPerAtual		:= {}
	Local cAliasQry		:= ""
	Local cWhere		:= ""
	Local cWhere1		:= ""
	Local cPdAux		:= ""
	Local nFDias		:= 0
	Local nFHoras		:= 0
	Local nDesconto		:= 0  //faltas ja descontadas para o periodo
	Local dDt1			:= ctod("")
	Local dDt2			:= ctod("")
	Local nMesAnt		:= If( Type("P_BENBUSFA") == "U", 0, P_BENBUSFA)
	Local cEventosPon	:= If( Type("P_BENPONEV") == "U", "", StrTran( P_BENPONEV, ",", "','" ))	// Eventos do ponto que serใo considerados para desconto
	Local cPerBusFal	:= ""
	Local aPerAberto	:= {}
	Local aPerFechado	:= {}
	Local cRotFun		:= If(SRA->RA_CATFUNC $ 'A|P', fGetCalcRot('9'), fGetCalcRot('1'))
	Local dDataIni		:= cTod("//")
	Local dDataFim		:= cTod("//")
	Local dPonIni		:= cTod("//")
	Local dPonFim		:= cTod("//")
	Local cAliasPON		:= ""
	Local nFalArray		:= 0

	DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo

	nFaltas := 0 //Mnemonico
	aFaltas := {}

	If LNOVOCALCB //mnemonico
		If cTipoRot == "8" .AND. APERGUNTE[1, 3] == 3
			dDt1 := APERGUNTE[12, 3]
			dDt2 := APERGUNTE[13, 3]
		EndIF

		If cTipoRot <> "8" .AND. APERGUNTE[6,3] == 1
			dDt1 := APERGUNTE[8, 3]
			dDt2 := APERGUNTE[9, 3]
		EndIF

		nDesconto := FaltasDes(cPeriodo, dDt1, dDt2)
		If __lMemCalc
			fAddMemLog("Calendario utilizado(MV_USACPER) : " +If(lUsaCPer,"FOLHA","PONTO") ,1,1)
			fAddMemLog("Faltas jแ descontadas de pedidos anteriores: " + cvaltochar(nDesconto) ,1,1)
			fAddMemLog("Meses para busca de faltas (P_BENBUSFA): " + cvaltochar(nMesAnt) ,1,1)
			fAddMemLog("Eventos do ponto para desconto (P_BENPONEV): " + cEventosPon ,1,1)
		EndIf
	EndIf

	fGetPerAtual( @aPerAtual, , SRA->RA_PROCES, cRot )

	// Se usa calendแrio do ponto e se o mnem๔nico P_BENPONEV estแ preenchido com os eventos do ponto verifica as faltas no ponto
	If !lUsaCPer .And. !Empty(cEventosPon) .And. !Empty(aPerAtual)

		cAliasPON := GetNextAlias()

		// Bustca o perํodo atual do ponto
		GetPerAtu(@dPonIni, @dPonFim )

		// Carrega o perํodo de apontamento considerando o conte๚do do mnem๔nico P_BenBusFa
		PerAponta(@dDataIni, @dDataFim, MonthSub(aPerAtual[1][6], nMesAnt),,,,,,,.T.)

		If __lMemCalc
			fAddMemLog("Periodo atual do ponto : " + dtoc(dPonIni) + " a " + dtoc(dPonFim) ,1,1)
			fAddMemLog("Periodo de busca de desconto : " + dtoc(dDataIni) + " a " + dtoc(dDataFim) ,1,1)
		EndIf

		// Se for o perํodo atual ou superior busca na SPC
		If dDataIni >= dPonIni

			BeginSQL Alias cAliasPON

				SELECT COUNT(*) FALTAS
				FROM %Table:SPC% SPC
				WHERE ( PC_PD IN (%Exp: cEventosPon%)
				AND PC_PDI = ''
				OR PC_PDI IN (%Exp: cEventosPon% ))
				AND PC_DATA BETWEEN %Exp: dTos(dDataIni)% AND %Exp: dTos(dDataFim)%
				AND PC_MAT = %Exp: SRA->RA_MAT%
				AND PC_FILIAL = %Exp: SRA->RA_FILIAL%
				AND SPC.%NotDel%

			EndSQL

		Else
			//Caso seja um perํodo anterior busca na SPH

			BeginSQL Alias cAliasPON

				SELECT COUNT(*) FALTAS
				FROM %Table:SPH% SPH
				WHERE ( PH_PD IN (%Exp: cEventosPon%)
				AND PH_PDI = ''
				OR PH_PDI IN (%Exp: cEventosPon% ))
				AND PH_DATA BETWEEN %Exp: dTos(dDataIni)% AND %Exp: dTos(dDataFim)%
				AND PH_MAT = %Exp: SRA->RA_MAT%
				AND PH_FILIAL = %Exp: SRA->RA_FILIAL%
				AND SPH.%NotDel%

			EndSQL

		EndIf

		nFaltas := (cAliasPON)->(FALTAS)

		(cAliasPON)->(dbCloseArea())

		If nFaltas > 0
			//Posi็๕es no array: 1=domingo,2=segunda,3=ter็a,4=quarta,5=quinta,6=sexta,7=sabado
			aFaltas := {0,0,0,0,0,0,0}
			//Busca as faltas por dia da semana
			cAliasPON := GetNextAlias()
			If dDataIni >= dPonIni
				BeginSQL Alias cAliasPON

					SELECT DISTINCT PC_DATA AS DATAFAL
					FROM %Table:SPC% SPC
					WHERE ( PC_PD IN (%Exp: cEventosPon%)
					AND PC_PDI = ''
					OR PC_PDI IN (%Exp: cEventosPon% ))
					AND PC_DATA BETWEEN %Exp: dTos(dDataIni)% AND %Exp: dTos(dDataFim)%
					AND PC_MAT = %Exp: SRA->RA_MAT%
					AND PC_FILIAL = %Exp: SRA->RA_FILIAL%
					AND SPC.%NotDel%

				EndSQL

			Else
				//Caso seja um perํodo anterior busca na SPH

				BeginSQL Alias cAliasPON

					SELECT DISTINCT PH_DATA AS DATAFAL
					FROM %Table:SPH% SPH
					WHERE ( PH_PD IN (%Exp: cEventosPon%)
					AND PH_PDI = ''
					OR PH_PDI IN (%Exp: cEventosPon% ))
					AND PH_DATA BETWEEN %Exp: dTos(dDataIni)% AND %Exp: dTos(dDataFim)%
					AND PH_MAT = %Exp: SRA->RA_MAT%
					AND PH_FILIAL = %Exp: SRA->RA_FILIAL%
					AND SPH.%NotDel%

				EndSQL
			EndIf

			While (cAliasPON)->(!Eof())
				aFaltas[Dow(StoD((cAliasPON)->DATAFAL))] += 1 //Acrescenta uma falta no dia da semana referente a data
				(cAliasPON)->(DbSkip())
			EndDo

			(cAliasPON)->(dbCloseArea())
		EndIf

	ElseIf ! Empty(aPerAtual) .and. (!Empty(aCodFol[54, 1]) .or. !Empty(aCodFol[242, 1]))

		If ! Empty(aCodFol[54, 1])
			cPdAux := "'" + aCodFol[54, 1] + "'"
		EndIf
		If ! Empty(aCodFol[242, 1])
			cPdAux += If(Empty(cPdAux), "'" + aCodFol[242, 1] + "'", ",'" + aCodFol[242, 1] + "'")
		EndIf
		If ! Empty(aCodFol[0244, 1])
			cPdAux += If(Empty(cPdAux), "'" + aCodFol[0244, 1] + "'", ",'" + aCodFol[0244, 1] + "'")
		EndIf

		If nMesAnt == 0
			//Posi็๕es no array: 1=domingo,2=segunda,3=ter็a,4=quarta,5=quinta,6=sexta,7=sabado
			aFaltas := {0,0,0,0,0,0,0}

			cWhere1 += " RGB.RGB_FILIAL = '" + SRA->RA_FILIAL + "' "
			cWhere1 += " AND RGB.RGB_MAT = '" + SRA->RA_MAT + "' "
			cWhere1 += " AND RGB.RGB_PROCES = '" + aPerAtual[1, 8] + "' "
			cWhere1 += " AND RGB.RGB_PERIOD = '" + aPerAtual[1, 1] + "' "
			cWhere1 += " AND RGB.RGB_ROTEIR = '" + cRotFun + "' "
			cWhere1 += " AND RGB.RGB_PD IN (" + cPdAux + ") "

			cWhere := "%" + cWhere1 + " AND RGB.RGB_TIPO1 = 'D'"
			cWhere += "%"

			cAliasQry:= "QRGB"
			BeginSql alias cAliasQry
				SELECT RGB_PD, RGB_HORAS, RGB_DTREF
				FROM %table:RGB% RGB
				WHERE %exp:cWhere% AND
				RGB.%notDel%
			EndSql

			While !(cAliasQry)->( EOF() )
				If (cAliasQry)->RGB_PD == aCodFol[0244,1]
					nFDias -= (cAliasQry)->RGB_HORAS
				Else
					nFDias += (cAliasQry)->RGB_HORAS
				EndIf
				If !Empty((cAliasQry)->RGB_DTREF)
					aFaltas[Dow(StoD((cAliasQry)->RGB_DTREF))] += 1 //Acrescenta uma falta no dia da semana referente a data
				EndIf
				(cAliasQry)->( DbSkip() )
			EndDo

			(cAliasQry)->(DbCloseArea())

			cWhere := "%" + cWhere1 + " AND RGB.RGB_TIPO1 = 'H'"
			cWhere += "%"

			cAliasQry:= "QRGB"
			BeginSql alias cAliasQry
				SELECT RGB_PD, RGB_HORAS, RGB_DTREF
				FROM %table:RGB% RGB
				WHERE %exp:cWhere% AND
				RGB.%notDel%
			EndSql

			While !(cAliasQry)->( EOF() )
				If (cAliasQry)->RGB_PD == aCodFol[0244,1]
					nFHoras -= Round((cAliasQry)->RGB_HORAS / SRA->RA_HRSDIA, 0)
				Else
					nFHoras += Round((cAliasQry)->RGB_HORAS / SRA->RA_HRSDIA, 0)
				EndIf
				If !Empty((cAliasQry)->RGB_DTREF)
					aFaltas[Dow(StoD((cAliasQry)->RGB_DTREF))] += 1 //Acrescenta uma falta no dia da semana referente a data
				EndIf
				(cAliasQry)->( DbSkip() )
			EndDo

			(cAliasQry)->(DbCloseArea())

		Else
			cPerBusFal := MesAno(MonthSub(aPerAtual[1][6], nMesAnt))
			If __lMemCalc
				fAddMemLog("Periodo de busca de desconto : " + cPerBusFal ,1,1)
				fAddMemLog("Verbas  : " + cPdAux ,1,1)
			EndIf

			RetPerAbertFech(SRA->RA_PROCES,; // Processo selecionado na Pergunte.
						cRotFun		,; // Roteiro selecionado na Pergunte.
						cPerBusFal	,; // Periodo selecionado na Pergunte.
						"99"		,; // Numero de Pagamento selecionado na Pergunte.
						NIL			,; // Periodo Ate - Passar "NIL", pois neste relatorio eh escolhido apenas um periodo.
						NIL			,; // Numero de Pagamento Ate - Passar "NIL", pois neste relatorio eh escolhido apenas um numero de pagamento.
						@aPerAberto	,; // Retorna array com os Periodos e NrPagtos Abertos
						@aPerFechado ) // Retorna array com os Periodos e NrPagtos Fechados

			If nMesAnt == 1 .and. ! Empty(aPerAberto)
				cWhere1 += " SRC.RC_FILIAL = '" + SRA->RA_FILIAL + "' "
				cWhere1 += " AND SRC.RC_MAT = '" + SRA->RA_MAT    + "' "
				cWhere1 += " AND SRC.RC_PROCES = '" + SRA->RA_PROCES + "' "
				cWhere1 += " AND SRC.RC_PERIODO = '" + aPerAberto[1,1] + "' "
				cWhere1 += " AND SRC.RC_ROTEIR = '" + cRotFun + "' "
				cWhere1 += " AND SRC.RC_PD IN (" + cPdAux   + ") "

				cWhere := "%" + cWhere1 + " AND SRC.RC_TIPO1 = 'D'"
				cWhere += "%"

				cAliasQry:= "QSRC"
				BeginSql alias cAliasQry
					SELECT RC_PD, RC_HORAS
					FROM %table:SRC% SRC
					WHERE %exp:cWhere% AND
					SRC.%notDel%
				EndSql

				While !(cAliasQry)->( EOF() )
					If (cAliasQry)->RC_PD == aCodFol[0244,1]
						nFDias -= (cAliasQry)->RC_HORAS
					Else
						nFDias += (cAliasQry)->RC_HORAS
					EndIf

					(cAliasQry)->( DbSkip() )
				EndDo

				(cAliasQry)->(DbCloseArea())

				cWhere := "%" + cWhere1 + " AND SRC.RC_TIPO1 = 'H'"
				cWhere += "%"
				cAliasQry:= "QSRC"
				BeginSql alias cAliasQry
					SELECT RC_PD,RC_HORAS
					FROM %table:SRC% SRC
					WHERE %exp:cWhere% AND
					SRC.%notDel%
				EndSql

				While !(cAliasQry)->( EOF() )
					If (cAliasQry)->RC_PD == aCodFol[0244,1]
						nFHoras -= Round((cAliasQry)->RC_HORAS / SRA->RA_HRSDIA, 0)
					Else
						nFHoras += Round((cAliasQry)->RC_HORAS / SRA->RA_HRSDIA, 0)
					EndIf

					(cAliasQry)->( DbSkip() )
				EndDo

				(cAliasQry)->(DbCloseArea())
			ElseIf !Empty(aPerFechado)
				cWhere1 += " SRD.RD_FILIAL = '" + SRA->RA_FILIAL + "' "
				cWhere1 += " AND SRD.RD_MAT = '" + SRA->RA_MAT + "' "
				cWhere1 += " AND SRD.RD_PROCES = '" + SRA->RA_PROCES + "' "
				cWhere1 += " AND SRD.RD_PERIODO = '" + aPerFechado[1, 1] + "' "
				cWhere1 += " AND SRD.RD_ROTEIR = '" + cRotFun + "' "
				cWhere1 += " AND SRD.RD_PD IN (" + cPdAux + ") "

				cWhere := "%" + cWhere1 + " AND SRD.RD_TIPO1 = 'D'"
				cWhere += "%"

				cAliasQry:= "QSRD"
				BeginSql alias cAliasQry
					SELECT RD_PD,RD_HORAS
					FROM %table:SRD% SRD
					WHERE %exp:cWhere% AND
					SRD.%notDel%
				EndSql

				While !(cAliasQry)->( EOF() )
					If (cAliasQry)->RD_PD == aCodFol[0244,1]
						nFDias -= (cAliasQry)->RD_HORAS
					Else
						nFDias += (cAliasQry)->RD_HORAS
					EndIf

					(cAliasQry)->( DbSkip() )
				EndDo

				(cAliasQry)->(DbCloseArea())

				cWhere := "%" + cWhere1 + " AND SRD.RD_TIPO1 = 'H'"
				cWhere += "%"
				cAliasQry:= "QSRD"
				BeginSql alias cAliasQry
					SELECT RD_PD,RD_HORAS
					FROM %table:SRD% SRD
					WHERE %exp:cWhere% AND
					SRD.%notDel%
				EndSql

				While !(cAliasQry)->( EOF() )
					If (cAliasQry)->RD_PD == aCodFol[0244,1]
						nFHoras -= Round((cAliasQry)->RD_HORAS / SRA->RA_HRSDIA, 0)
					Else
						nFHoras += Round((cAliasQry)->RD_HORAS / SRA->RA_HRSDIA, 0)
					EndIf

					(cAliasQry)->( DbSkip() )
				EndDo

				(cAliasQry)->(DbCloseArea())
			EndIf
		EndIf
		nFaltas := nFDias + nFHoras
	EndIf
	If __lMemCalc
		fAddMemLog("Faltas no periodo : " + cvaltochar(nFaltas) ,1,1)
	EndIf

	aEval(aFaltas, {|x| nFalArray += x})
	If nFalArray == 0
		aFaltas := {}
	EndIf

	nFaltas := nFaltas - nDesconto

	If __lMemCalc
		fAddMemLog("Faltas no periodo - faltas ja descontadas em outros pedidos: " + cvaltochar(nFaltas) ,1,1)
	EndIf
	If LNOVOCALCB .And. nFaltas < 0 .And. nDesconto > 0
		nFaltas := 0
		aFaltas := {}
	EndIf

	RestArea(aArea)
Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVTRSal		   บAutor  ณLeandro Drumond  บ Data ณ  25/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณMonta salario base para calculo do vale transporte		      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVTRSal()

	Local aArea		:= GetArea()
	Local lCVTUso	:= GetMvRH( "MV_CVTUSO", .F., "2" ) == "1"
	Local cVTProp	:= GetMvRH( "MV_CVTPROP", .F., "P" )
	Local cCodigos	:= ""
	Local nDiaTra	:= 0
	Local nTVerbas	:= 0
	Local nQtdPerg	:= 8
	Local nCont		:= 0
	Local nInfNorm	:= 0
	Local nInfDesc	:= 0
	Local nValRed	:= 0
	Local lSALHBEN	:= .F.
	Local lLDSRHRSP := Type("P_LDSRHRSP") # "U" .And. P_LDSRHRSP

	DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo

	If LNOVOCALCB
		nQtdPerg	:= 7
	EndIf

	fSalario(@nSalario, @SalHora, @SalDia, @nSalMes, "A")

	If SRA->RA_CATFUNC == "H" .And. lLDSRHRSP
		nSalMes := (SRA->RA_SALARIO * nHrsCal)
	EndIf

	If DIASTRAB == 0 .and. Len(aAfast) == 0
		fDiasTrab(@DIASTRAB)
	EndIf

	If lCVTUso
		nDiatra := If(cVTProp == "T", If(GetMvRH("MV_DIASPER",,"1") == "1",nDiasP,(If(SRA->RA_TIPOPGT =="M", P_QTDIAMES, P_NTOTDIAS))), DIASTRAB)
	EndIf

	nSalBsVT := 0 //Salario Base VTR - Mnemonico

	aPd := FCARMVTO(SRA->RA_FILIAL, SRA->RA_MAT, CSEMANA, CPROCESSO, CPERIODO, "FOL", CNUMPAG, ,.F.)

	lSALHBEN := If( Type("P_SALHBEN") == "U", .F., P_SALHBEN)

	If __lMemCalc
		fAddMemLog("MV_CVTUSO : " + If(lCVTUso,"Sim","Nใo") ,1,1)
		fAddMemLog("MV_CVTPROP : " + cVTProp ,1,1)
		fAddMemLog("P_LDSRHRSP : " + If(P_LDSRHRSP,"Sim","Nใo")  ,1,1)
		fAddMemLog("P_SALHBEN : " + If(lSALHBEN,"Sim","Nใo")  ,1,1)
		fAddMemLog("Dias Periodo : " + cvaltochar(nDiatra) ,1,1)
		fAddMemLog("Dias trabalhados : " + cvaltochar(DIASTRAB) ,1,1)
		fAddMemLog("Dias afastamento : " + cvaltochar(nDiasAfas) ,1,1)
		fAddMemLog("Salario : " + Transform(nSalario,"@E 999,999,999.99")  ,1,1)
		fAddMemLog("Salario hora : " + Transform(SalHora,"@E 999,999,999.99")  ,1,1)
		fAddMemLog("Salario dia : " + Transform(SalDia,"@E 999,999,999.99")  ,1,1)
		fAddMemLog("Salario incorporado : " + Transform(nSalMes,"@E 999,999,999.99")  ,1,1)
		fAddMemLog("Horas NORMAL : " + cvaltochar(Normal) ,1,1)
		fAddMemLog("Horas DESCANSO : " + cvaltochar(Descanso) ,1,1)
	EndIf

	If SRA->RA_CATFUNC == "H" .AND. lSALHBEN
		//Percorre o aPd em busca das verbas de salแrio horista (Id 0032) e DSR horista (Id 0033)
		For nCont := 1 To Len(aPd)
			If aPd[nCont, 1] == aCodFol[032, 1] .And. aPd[nCont, 9] != "D" .And. AllTrim( aPd[nCont, 3] ) == AllTrim( cSemana ) .And. aPd[nCont, 7] == "I"
				nInfNorm += aPd[nCont, 4]
			EndIf
			If aPd[nCont, 1] == aCodFol[033, 1] .And. aPd[nCont, 9] != "D" .And. AllTrim( aPd[nCont, 3] ) == AllTrim( cSemana ) .And. aPd[nCont, 7] == "I"
				nInfDesc += aPd[nCont, 4]
			EndIf
		Next nCont

		//Caso tenha informado a verba de salแrio horista, utiliza as horas informadas
		If nInfNorm > 0
			Normal := nInfNorm
			//Caso possua afastamento, divide as horas pelas dias trabalhados e multiplica pelos dias de cแlculo para considerar as horas previstas no m๊s
			If nDiasAfas > 0
				Normal := (Normal / DiasTrab * nDiasC)
			EndIf
			//Verifica a quantidade de horas trabalhadas por dia, de acordo com as horas informadas
			//Conta serแ as horas trabalhadas, dividido pelos dias ๚teis, multiplicado pelos dias de cแlculo e dividido por 30
			nHrsDia := (Normal / aPeriodo[nPosSem, 24] * aPeriodo[nPosSem, 20] / 30)
			//Caso tenha informado a verba de DSR horista, utiliza as horas informadas
			If nInfDesc > 0
				Descanso := nInfDesc
			//Caso contrแrio, multiplica as horas trabalhadas por dia pelos dias de DSR
			Else
				Descanso := nHrsDia * aPeriodo[nPosSem, 7]
			EndIf

			//Retira a incorpora็ใo das variแveis de salแrio, pois foram consideradas as horas cadastrais
			nSalMes -= (SRA->RA_SALARIO * nHrsCal)
			SalDia -= (SRA->RA_SALARIO * SRA->RA_HRSDIA)

			//Efetua a incorpora็ใo das variแveis de salแrio, com base nas horas trabalhadas
			nHrsCal := nHrsDia * nDiasC
			nSalMes += (SRA->RA_SALARIO * nHrsCal)
			SalDia += (SRA->RA_SALARIO * nHrsDia)

			If __lMemCalc
				fAddMemLog("Horas informadas NORMAL : " + cvaltochar(nInfNorm) ,1,1)
				fAddMemLog("Horas informadas DESCANSO : " + cvaltochar(nInfDesc) ,1,1)
				fAddMemLog("Horas calculo : " + cvaltochar(nHrsCal) ,1,1)
				fAddMemLog("novo Salario dia : " + Transform(SalDia,"@E 999,999,999.99")  ,1,1)
				fAddMemLog("novo Salario incorporado : " + Transform(nSalMes,"@E 999,999,999.99")  ,1,1)
			EndIf
		EndIf
	EndIf

	If Len(aPergunte) > nQtdPerg
		If LNOVOCALCB
			cCodigos := AllTrim(aPergunte[7, 3]) + AllTrim(aPergunte[8, 3]) + AllTrim(aPergunte[9, 3]) + AllTrim(aPergunte[10, 3])
		Else
			cCodigos := AllTrim(aPergunte[8, 3]) + AllTrim(aPergunte[9, 3]) + AllTrim(aPergunte[10, 3]) + AllTrim(aPergunte[11, 3])
		EndIf
		If !Empty(cCodigos) // Incorpora verbas do Movimento Mensal ao Salario
			nTVerbas := 0
			fSVerbas(cCodigos, @nTVerbas, aPd)
			nSalMes += nTVerbas
			If __lMemCalc
				fAddMemLog("Verbas a serem incorporadas : " + cCodigos ,1,1)
				fAddMemLog("valor das verbas : " + Transform(nTVerbas,"@E 999,999,999.99") ,1,1)
				fAddMemLog("novo Salario incorporado : " + Transform(nSalMes,"@E 999,999,999.99")  ,1,1)
			EndIf
		EndIf
	EndIf

	If P_lPPEAtiv .And. P_RedSalB
		fInssResEx()
		fSalRed(@nValRed, Nil, Nil, nSalMes)
		nSalMes -= nValRed
	EndIf

	If lCVTUso //Usa parametro MV_CVTPROP
		If !BaseVTProp(@nSalBsVT, nDiaTra, cVTProp)
			Return(.F.)
		EndIf
	Else
		nSalBsVT  := nSalMes //Considera salario integral
		If __lMemCalc
			fAddMemLog("SALARIO BASE para calculo do beneficio(nSalMes) : " + Transform(nSalMes,"@E 999,999,999.99") ,1,1)
		EndIf
	EndIf

	// Verifico se o Funcionแrio ้ Comissionado
	If SRA->RA_CATFUNC == "C" .And. (nSalBsVT == 0 .OR. empty(nSalBsVT))
		nSalBsVT := fDesc("RCE", SRA->RA_SINDICA, "RCE_GCOMISS", Nil, SRA->RA_FILIAL)
		If __lMemCalc
			fAddMemLog("Garantia comissao(salario) : " + Transform(nSalBsVT,"@E 999,999,999.99") ,1,1)
		EndIf
	Endif

RestArea(aArea)

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณBaseVTProp	   บAutor  ณLeandro Drumond  บ Data ณ  25/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณMonta base de salario para VT baseado no parametro		      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function BaseVTProp(nSalBsVT,nDiaTra,cVTProp)
Local lRet		:= .T.
Local nX		:= 0
Local nDiasFunc	:= 0
Local nDPropInf	:= 0
Local nDiasPer	:= 0
Local nDiasTb	:= 0

DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo

If __lMemCalc
	fAddMemLog("Calendแrio  : " + if( Empty(aDiasMes),"Sint้tico","Analํtico") ,1,1)
EndIf

If cVTProp == "I" 	//Integral
	nSalBsVT := nSalMes
ElseIf cVTProp == "T"	//Trabalhado
	If Empty(aDiasMes) //Periodo sintetico, nao tem informacao de dia. Utilizar salario do mes
		nSalBsVT := nSalMes		//Considera salario integral
		If __lMemCalc
			fAddMemLog("SALARIO BASE para calculo do beneficio(nSalMes) : " + Transform(nSalMes,"@E 999,999,999.99") ,1,1)
		EndIf
	Else
		For nX := 1 to Len(aDiasMes)
			If ( aDiasMes[nX,2] $ "1*3" .and. Empty(aDiasMes[nX,5]) ) //Dias ๚teis e DSR sem afastamento
				nDiasFunc++
			EndIf
		Next nX
		nSalBsVT := nSalMes * (nDiasFunc/nDiaTra)
		If __lMemCalc
			fAddMemLog("Dias trabalhados(calendario)  : " + cvaltochar(nDiasFunc) ,1,1)
			fAddMemLog("SALARIO BASE para calculo do beneficio(nSalMes * (nDiasFunc/nDiaTra)) : " + Transform(nSalBsVT,"@E 999,999,999.99") ,1,1)
		EndIf
	EndIf
ElseIf cVTProp == "P"
	If LNOVOCALCB
		DbSelectArea("SM7")
		DbSetOrder(RetOrdem("SM7","M7_FILIAL+M7_MAT+M7_TPVALE+M7_CODIGO"))
		If DbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"0")
			While ( !Eof() .and. M7_FILIAL+M7_MAT+M7_TPVALE == SRA->RA_FILIAL+SRA->RA_MAT+"0" )
				//Se existir algum meio de transporte sem dias proporcionais informados ou com dias proporcionais diferentes entre si, campo serแ desconsiderado
				If SM7->M7_DPROPIN == 0 .or. ( nDPropInf <> 0 .and. nDPropInf <> SM7->M7_DPROPIN .and. SM7->M7_DPROPIN <> 99 )
					nDPropInf := 0
					Exit
				EndIf
				If SM7->M7_DPROPIN <> 99
					nDPropInf := SM7->M7_DPROPIN
				EndIf
				DbSkip()
			EndDo
		EndIf
	Else
		DbSelectArea("SR0")
		DbSetOrder(RetOrdem("SR0","R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO"))
		If DbSeek(SRA->RA_FILIAL+SRA->RA_MAT+"0")
			While ( !Eof() .and. R0_FILIAL+R0_MAT+R0_TPVALE == SRA->RA_FILIAL+SRA->RA_MAT+"0" )
				//Se existir algum meio de transporte sem dias proporcionais informados ou com dias proporcionais diferentes entre si, campo serแ desconsiderado
				If SR0->R0_DPROPIN == 0 .or. ( nDPropInf <> 0 .and. nDPropInf <> SR0->R0_DPROPIN .and. SR0->R0_DPROPIN <> 99 )
					nDPropInf := 0
					Exit
				EndIf
				If SR0->R0_DPROPIN <> 99
					nDPropInf := SR0->R0_DPROPIN
				EndIf
				DbSkip()
			EndDo
		EndIf
	EndIf
	If __lMemCalc
		fAddMemLog("Dias proporcionais informados  : " + cvaltochar(nDPropInf) ,1,1)
	EndIf
	If nDPropInf == 0
		If Empty(aDiasMes)
			nSalBsVT  := nSalMes	//Considera salario integral pois nao eh possivel calcular dias proporcionais informados
			nDPropInf := 0
			If __lMemCalc
				fAddMemLog("SALARIO BASE para calculo do beneficio(nSalMes) : " + Transform(nSalMes,"@E 999,999,999.99") ,1,1)
			EndIf
		Else
			For nX := 1 to Len(aDiasMes)
				If aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]  //RCG_VTRANS = "S"
					nDPropInf++
				EndIf
			Next nX
		EndIf
	EndIf
	If nDPropInf > 0
		nDiasPer  := If(SRA->RA_TIPOPGT =="S", P_NTOTDIAS, nDiasC)
		nSalBsVT  := nSalMes/nDiasPer*nDPropInf
		If __lMemCalc
			fAddMemLog("Dias proporcionais calendario  : " + cvaltochar(nDPropInf) ,1,1)
			fAddMemLog("SALARIO BASE para calculo do beneficio(nSalMes/nDiasPer*nDPropInf ) : " + Transform(nSalBsVT,"@E 999,999,999.99") ,1,1)
		EndIf
	EndIf
ElseIf cVTProp == "S" //Base Saldo de Salario
	nDiasTb     := Max(DiasTrab - ( nDiasMat + nDPrgSalMa ), 0)
	nDiasPer	:= If(GetMvRH("MV_DIASPER",,"1") == "1",nDiasP,(If(SRA->RA_TIPOPGT =="M", P_QTDIAMES, P_NTOTDIAS)))
	If !Empty(aDiasMes)
		For nX := 1 to Len(aDiasMes)
			If !aDiasMes[nX,4] .and. aDiasMes[nX,5] == "PRG" .and. Day(aDiasMes[nX,1]) <= nDiasPer //Se tiver f้rias programadas, subtrai dos dias trabalhados
				nDiasTb--
			EndIf
		Next nX
	EndIf
	nSalBsVT 	:= (( nSalMes / nDiasPer ) * nDiasTb )
	If __lMemCalc
		fAddMemLog("Dias trabalhados - dias de ferias programadas  : " + cvaltochar(DiasTrab-nDiasTb) ,1,1)
		fAddMemLog("Dias trabalhados  : " + cvaltochar(nDiasTb) ,1,1)
		fAddMemLog("SALARIO BASE para calculo do beneficio((( nSalMes / nDiasPer ) * nDiasTb )	 ) : " + Transform(nSalBsVT,"@E 999,999,999.99") ,1,1)
	EndIf
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfChkSR0  	   บAutor  ณLeandro Drumond  บ Data ณ  26/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCheca se existem dados na SR0							      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fChkSR0(cFilFunc,cMatFunc,cTipoVale,lAgrup)
Local aArea		:= GetArea()
Local lRet		:= .T.

DEFAULT cFilFunc  := xFilial("SR0")
DEFAULT cMatFunc  := SRA->RA_MAT
DEFAULT cTipoVale := If(cTipoRot == "8","0",(If(cTipoRot == "D","1","2"))) //0-Vale Transporte / 1-Vale Refei็ใo / 2-Vale Alimenta็ใo
DEFAULT lAgrup	  := .F.


DbSelectArea("SR0")
DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE"))

If lAgrup
	If DbSeek(cFilFunc + cMatFunc + cTipoVale)
		While !SR0->(Eof()) .And. SR0->(R0_FILIAL+R0_MAT+R0_TPVALE) == cFilFunc + cMatFunc + cTipoVale
			If SR0->R0_TPCALC <> "2"
				lRet := .F.
			Else
				RecLock("SR0",.F.)
				DbDelete()
				MsUnLock()
			EndIf
			SR0->(dbSkip())
		EndDo
	EndIf
Else
	If !DbSeek(cFilFunc + cMatFunc + cTipoVale)
		lRet := .F.
	EndIf
EndIf
RestArea(aArea)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfChkSM7  	   บAutor  ณLeandro Drumond  บ Data ณ  26/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCheca se existem dados na SR0							      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fChkSM7(cFilFunc,cMatFunc,cTipoVale,lAgrup)
Local aArea		:= GetArea()
Local lRet		:= .T.

DEFAULT cFilFunc  := xFilial("SM7")
DEFAULT cMatFunc  := SRA->RA_MAT
DEFAULT cTipoVale := If(cTipoRot == "8","0",(If(cTipoRot == "D","1","2"))) //0-Vale Transporte / 1-Vale Refei็ใo / 2-Vale Alimenta็ใo
DEFAULT lAgrup	  := .F.


DbSelectArea("SM7")
SM7->(DbSetOrder(RetOrder("SM7", "M7_FILIAL+M7_MAT+M7_TPVALE")))

If lAgrup
	If SM7->(DbSeek(cFilFunc + cMatFunc + cTipoVale))
		While !SM7->(Eof()) .And. SM7->(M7_FILIAL+M7_MAT+M7_TPVALE) == cFilFunc + cMatFunc + cTipoVale
			If SM7->M7_TPCALC <> "2"
				lRet := .F.
			Else
				RecLock("SM7",.F.)
				DbDelete()
				MsUnLock()
			EndIf
			SM7->(dbSkip())
		EndDo
	EndIf
Else
	If !SM7->(DbSeek(cFilFunc + cMatFunc + cTipoVale))
		lRet := .F.
	EndIf
EndIf
RestArea(aArea)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfChkRG2  	   บAutor  ณLeandro Drumond  บ Data ณ  29/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCheca se existe pedido completo na RG2					      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fChkRG2(cFilFunc, cMatFunc, cTipoVale)

Local aArea		:= GetArea()
Local lRet		:= .T.
Local cTpRG2Ped	:= GetSx3Cache("RG2_PEDIDO", "X3_TIPO")

DEFAULT cFilFunc  := xFilial("RG2")
DEFAULT cMatFunc  := SRA->RA_MAT
DEFAULT cTipoVale := If(cTipoRot == "8","0",(If(cTipoRot == "D","1","2"))) //0-Vale Transporte / 1-Vale Refei็ใo / 2-Vale Alimenta็ใo

DbSelectArea("RG2")
DbSetOrder(RetOrder("RG2", "RG2_FILIAL+RG2_PERIOD+RG2_NROPGT+RG2_ROTEIR+RG2_MAT+RG2_TPVALE+RG2_CODIGO"))

If DbSeek(cFilFunc + cPeriodo + cSemana + cRot + cMatFunc + cTipoVale)
	If RG2->RG2_PEDIDO == If( cTpRG2Ped == "C", "2", 2 )			//Se pedido foi concluido, nao recalcula
		If GetRemoteType() == -1
			MsgLogGrid( STR0036, .F. ) // "Pedido de compra do beneficio jแ foi concluํdo. Nใo serแ recalculado."
		Else
			If IsInCallStack( "EXECROT" ) //Se estiver executando roteiro de calculo
				AddLogExecRot( STR0036 )	// "Pedido de compra do beneficio jแ foi concluํdo. Nใo serแ recalculado."
			Else
				Help(" ", 1, "Help",, OemToAnsi(STR0036), 1, 0)  // "Pedido de compra do beneficio jแ foi concluํdo. Nใo serแ recalculado."
			EndIf
		EndIf
		lRet := .F.
	EndIf
EndIf

RestArea(aArea)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVTRCalc	   บAutor  ณLeandro Drumond  บ Data ณ  25/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCalculo do Vale Transporte								      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVTRCalc(aVTCalc)
Local aArea		:= GetArea()
Local cFilFunc  := SRA->RA_FILIAL
Local cMatFunc  := SRA->RA_MAT
Local cKeyAux	:= ""
Local nCustAux  := 0
Local nCustFunc := 0
Local nDiaNUtil	:= 0
Local nDiaUtil	:= 0
Local nDiasFix	:= 0
Local nValUni	:= 0
Local nValUniAnt:= 0
Local nValUniDif:= 0
Local nDiaTran	:= 0
Local nDiaNTran	:= 0
Local nDiaUCal	:= 0
Local nDiaNUCal	:= 0
Local nValVTR	:= 0
Local nTotVTR	:= 0
Local nDiasVTR	:= 0
Local nDiasProp	:= 0
Local nX		:= 0
Local nH		:= 0
Local nValAux	:= 0
Local nDiasPer	:= If(SuperGetMv("MV_DIASPER",,"1") == "1",nDiasP,(If(SRA->RA_TIPOPGT =="M", P_QTDIAMES, P_NTOTDIAS)))

Private nPercentual := RetValSRV(aCodFol[051,1],SRA->RA_FILIAL,"RV_PERC")
Private aVTRDias    := {}

DEFAULT lGp210Sal	:= ExistBlock("GP210SAL")
DEFAULT lGPVTRDias  := ExistBlock("GPVTRDias")

If lGp210Sal
	EXECBLOCK("GP210SAL", .F. , .F. ) //Retorna nPercentual
	If ValType(nPercentual) <> "N"
		nPercentual := 6
	EndIf
EndIf

If SRA->RA_CATFUNC $ "E*G"
	nCustFunc := nPercentual := 0
Else
	nCustFunc := Round(NSALBSVT * ( nPercentual / 100 ),2) //Custo Mensal do Funcionario
EndIf

aVTCalc	  := {}

DbSelectArea("SR0")
DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO"))

DbSeek(cFilFunc + cMatFunc + "0")

While SR0->(!Eof() .and. R0_FILIAL + R0_MAT + R0_TPVALE == cFilFunc + cMatFunc + "0")

	nDiaNUtil := SR0->R0_QDNUTIL	//Qtde vales por dia nใo util de VT
	nDiaUtil  := SR0->R0_QDIAINF	//Qtde vales por dia util de VT
	nDiasProp := SR0->R0_DPROPIN
	nValUni	  := PosAlias( "SRN" , SR0->R0_CODIGO , xFilial("SRN") , "RN_VUNIATU" , 1 )
	nDiasFix  := PosAlias( "SRN" , SR0->R0_CODIGO , xFilial("SRN") , "RN_DIASFIX" , 1 )

	If SR0->R0_DPROPIN == 99 //Se diasprop for 99 nใo calcula meio de transporte
		aAdd(aVTCalc , {SR0->(R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO), 0, nValUni, 0,  0,0, 0, 0, 0 } )
		SR0->(DbSkip())
		Loop
	EndIf

	If aPergunte[1,3] == 2 //Se for calculo da diferenca
		//Controle pelo Hist๓rico de altera็๕es
		SQY->(dbSeek(xFilial("SQY")+SRN->RN_COD+dtos(aPeriodo[nPosSem,3]),.T.))
		if SQY->QY_COD == SRN->RN_COD .AND. (SQY->QY_DATVIGE >= aPeriodo[nPosSem,3] .and. SQY->QY_DATVIGE <= aPeriodo[nPosSem,4])
		    nValUniAnt := SQY->QY_VUNIANT
		    nValUniDif := nValUni - nValUniAnt
		    nDiasVtr := 0
		    for nh := 1 to len(aPeriodo[nPosSem][5])
		    	if aPeriodo[nPosSem][5][nh][1] >= SQY->QY_DATVIGE.and. aPeriodo[nPosSem][5][nh][3] == '1'
			    	nDiasVTR ++
			    endif
		    next
		else
		    nValUniAnt := PosAlias( "SRN" , SR0->R0_CODIGO , xFilial("SRN") , "RN_VUNIANT" , 1 )
		    nValUniDif := nValUni - nValUniAnt
		    nDiasVTR   := aPeriodo[nPosSem,11] //RCF_DDIFVT
		endif
		nValVTR    := (nDiasVTR * nDiaUtil)  * nValUniDif
		nDiasProp  := 0	//Zera pois nao sera utilizado
	Else
		nDiaTran  := 0
		nDiaNTran := 0
		If Empty(nDiasFix) .And. Empty(nDiasProp)	//Caso informe dias proporcionais ou fixos, considera os dias informados ao inv้s de calcular
			If !Empty(aDiasMes)	//Se existir dados no array aDiasMes, utiliza estes dados para montar dias uteis e nao uteis de vale transporte (analitico)
				For nX := 1 to Len(aDiasMes)
					If aDiasMes[nX,2] == "1" .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
						nDiaTran += 1
					ElseIf aDiasMes[nX,2] <> "1" .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
						nDiaNTran += 1
					EndIf
				Next nX
			Else				//do contrario utiliza cadastro de periodo - RCF_DUTILT e RCF_DNUTIL (sintetico)
				If aPergunte[3,3] == 1 .And. ( DiasTrab - ( nDiasMat + nDPrgSalMa ) ) < nDiasPer
					nDiaTran  := Int( Round( aPeriodo[nPosSem,10] * ( ( DiasTrab - ( nDiasMat + nDPrgSalMa ) ) / nDiasPer ), 2 ) )
					nDiaNTran := Int( Round( aPeriodo[nPosSem,09] * ( ( DiasTrab - ( nDiasMat + nDPrgSalMa ) ) / nDiasPer ), 2 ) )
				Else
					nDiaTran  := aPeriodo[nPosSem,10]
					nDiaNTran := aPeriodo[nPosSem,9]
				EndIf
			EndIf
		EndIf

		// Ponto de entrada para alteracao dos dias de VTR e dias trabalhados no mes.
		If lGPVTRDias
			aVTRDias := {}
			aAdd(aVTRDias , {nDiaNTran, nDiaTran, nDiasProp, nDiasFix } )
			aVTRDias := ExecBlock("GPVTRDias",.F.,.F.)

			If ValType(aVTRDias) == "A" .And. Len(aVTRDias) > 0 .And. Len(aVTRDias[1]) >= 4
				nDiaNTran := aVTRDias[1,1]
				nDiaTran  := aVTRDias[1,2]
				nDiasProp := aVTRDias[1,3]
				nDiasFix  := aVTRDias[1,4]
			EndIf
		EndIf

		//Caso informe dias proporcionais, considera o valor como dias ๚teis de VT
		If( !Empty(SR0->R0_DPROPIN), nDiaTran := nDiasProp, )
		If( nDiasFix > 0 .And. ( Empty(SR0->R0_DPROPIN) .Or. fIntRHGS() ), nDiaTran := nDiasFix, )
		//Considera dias fixos caso use integra็ใo com Servi็os; para os demais, apenas considera fixos caso nใo informe dias proporcionais
		nDiasProp := nDiaTran + nDiaNTran
		nDiaUcal  := ( nDiaTran * nDiaUtil ) - aPergunte[6,3]
		nDiaNUCal := nDiaNTran * nDiaNUtil

		nDiasVTR  := Max( If( nDiasFix > 0 .And. ( Empty(SR0->R0_DPROPIN) .Or. fIntRHGS() ), nDiasFix * nDiaUtil, nDiaUCal + nDiaNUCal - ( nFaltas * nDiaUtil ) ), 0 )
		nValVTR  := nDiasVTR * nValUni
	EndIf

	If aPergunte[1,3] == 2
		nTotVTR += nValVTR + SR0->R0_VALCAL
	Else
		nTotVTR += nValVTR
	EndIf

	cKeyAux := SR0->(R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO)

		aAdd(aVTCalc , {cKeyAux, nDiasVTR, nValUni, nValVTR,  nCustFunc,(nValVTR-nCustFunc), nValUniDif, nDiasProp, 0 } )

	SR0->(dbSkip())

EndDo

For nX := 1 to Len(aVTCalc)
	aVTCalc[nX,9] := nTotVTR //Atualiza valor total de vale transporte
	If nCustFunc > nTotVTR
		aVTCalc[nX,5] := aVtCalc[nX,4] //Se Custo funcionario for maior que soma do total de VT, custo do funcinario passa a ser o valor total do VT
		aVTCalc[nX,6] := 0
	Else
		If aPergunte[1,3] == 2
			aVTCalc[nX,5] := 0 // Custo Funcionario
			aVTCalc[nX,6] := aVTCalc[nX,4] //Custo Empresa
		Else
			aVTCalc[nX,5] := Round(nCustFunc / nTotVTR * aVTCalc[nX,4],2) //Rateia custo do VT
			If nX == Len(aVTCalc)
				aVTCalc[nX,5] := nCustFunc - nValAux //For็a com que, mesmo com arredondamento, a somat๓ria dos valores seja id๊ncia aos 6% definidos originalmente.
			Else
				nValAux += aVTCalc[nX,5]
			EndIf
			aVTCalc[nX,6] := aVTCalc[nX,4] - aVTCalc[nX,5] //Custo Empresa
			nCustAux += aVTCalc[nX,5]
		EndIf
	EndIf
Next nX

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVTRCalcN	   บAutor  ณLeandro Drumond  บ Data ณ  25/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCalculo do Vale Transporte								      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVTRCalcN(aVTCalc)
Local aArea		:= GetArea()
Local cFilFunc  := SRA->RA_FILIAL
Local cMatFunc  := SRA->RA_MAT
Local cKeyAux	:= ""
Local nCustFunc := 0
Local nDiaNUtil	:= 0
Local nDiaUtil	:= 0
Local nDiasFix	:= 0
Local nValUni	:= 0
Local nValUniAnt:= 0
Local nValUniDif:= 0
Local nDiaTran	:= 0
Local nDiaNTran	:= 0
Local nDiaUCal	:= 0
Local nDiaNUCal	:= 0
Local nValVTR	:= 0
Local nTotVTR	:= 0
Local nDiasVTR	:= 0
Local nDiasProp	:= 0
Local nX		:= 0
Local nPos		:= 0
Local lPeriodico	:= aPergunte[1,3] == 3//Avulso/Complemnto/Semanal/diario
Local nDiasSeg 		:= 0									//-- Qtde Segundas trabalhadas
Local nDiasTer 		:= 0									//-- Qtde Tercas trabalhadas
Local nDiasQua		:= 0									//-- Qtde Quartas trabalhadas
Local nDiasQui 		:= 0									//-- Qtde Quintas trabalhadas
Local nDiasSex 		:= 0									//-- Qtde Sextas trabalhadas
Local nDiasSab 		:= 0									//-- Qtde Sabados trabalhadas
Local nDiasDom 		:= 0									//-- Qtde Domingos trabalhadas
Local nDiasPonto 	:= 0
Local nFalAux		:= 0
Local lDias			:= .F.
Local lFeriado 		:= .F.
Local lPropFerias	:= APERGUNTE[4,3] == 1 .Or. APERGUNTE[3,3] == 1 .Or. APERGUNTE[3,3] == 3
Local aTabCalend	:= If (!lUsaCPer,fCarPonto(),{})
Local lSP7			:= .F.
Local dDt1			:= ctod("")
Local dDt2			:= ctod("")
Local nH			:= 0
Local nDiaNTraPE	:= 0
Local nDiaTraPE		:= 0
Local nCustAux		:= 0
Local nDias			:= 0
Local nDNUtil		:= 0
Local nDUtil		:= 0

Private nPercentual := RetValSRV(aCodFol[051,1],SRA->RA_FILIAL,"RV_PERC")
Private aVTRDias    := {}

DEFAULT lGp210Sal	:= ExistBlock("GP210SAL")
DEFAULT lGPVTRDias  := ExistBlock("GPVTRDias")
DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo

If lGp210Sal
	EXECBLOCK("GP210SAL", .F. , .F. ) //Retorna nPercentual
	If ValType(nPercentual) <> "N"
		nPercentual := 6
	EndIf
EndIf

If SRA->RA_CATFUNC $ "E*G"
	nCustFunc := nPercentual := 0
Else
	nCustFunc := Round(NSALBSVT * ( nPercentual / 100 ),2) //Custo Mensal do Funcionario
EndIf

If lPeriodico .and. nCustFunc > 0//Verifica se jแ houve pagamento de VT no m๊s para abater o custo do funcionแrio
	SR0->(DbSetOrder(3)) //R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO+R0_PEDIDO
	If SR0->(DbSeek(SRA->RA_FILIAL +  SRA->RA_MAT + "0"))
		While(SR0->(!Eof() .and. R0_FILIAL + R0_MAT + R0_TPVALE == SRA->RA_FILIAL +  SRA->RA_MAT + "0"))
			If SR0->R0_PERIOD == cPeriodo .and. SR0->R0_PEDIDO == "2" //Apenas pedidos concluํdos
				nCustFunc -= SR0->R0_VLRFUNC
			EndIf
			SR0->(DbSkip())
		EndDo
		nCustFunc := Max(nCustFunc,0)
	EndIf
EndIf

If !lUsaCPer .And. Len(aTabCalend) > 1
	SPA->( dbSeek( xFilial("SPA", SRA->RA_FILIAL ) + SRA->RA_REGRA ) )
	lFeriado := ( SPA->PA_FERIADO == "S" .Or. ( SPA->( Type("PA_RECBENF") ) != "U" .And. SPA->PA_RECBENF == "1" ) )
EndIf

If __lMemCalc
	fAddMemLog("Calendario utilizado(MV_USACPER) : " +If(lUsaCPer,"FOLHA","PONTO") ,1,1)
	fAddMemLog("Considera feriado cadastro de Regra(SPA) : " + If(lFeriado,"Sim","Nใo") ,1,1)
	If !lUsaCPer
		fAddMemLog("Tabela padrใo do ponto carregada : " + If(Len(aTabCalend) > 1 ,"Sim","Nใo") ,1,1)
	EndIf
	fAddMemLog("Proporcional a f้rias : " + If(lPropFerias,"Sim","Nใo") ,1,1)
	fAddMemLog("Calculo peri๓dico : " + If(lPeriodico,"Sim","Nใo") ,1,1)
EndIf

aVTCalc	  := {}

DbSelectArea("SM7")
SM7->(DbSetOrder(RetOrder("SM7", "M7_FILIAL+M7_MAT+M7_TPVALE+M7_CODIGO")))

If SM7->(DbSeek(cFilFunc + cMatFunc + "0"))
	While SM7->(!Eof() .and. M7_FILIAL + M7_MAT + M7_TPVALE == cFilFunc + cMatFunc + "0")

		nFalAux := 0

		If lPeriodico .And. aPergunte[14,3] == 1 .And. SM7->M7_COMPL <> '1'
			SM7->(dbSkip())
			Loop
		EndIf

		lDias :=  SM7->M7_VALSEG > 0 .Or. SM7->M7_VALTER  > 0 .Or. SM7->M7_VALQUA  > 0 .Or.  SM7->M7_VALQUI  > 0 .Or.  SM7->M7_VALSEX  > 0 .Or.  SM7->M7_VALSAB  > 0 .Or.  SM7->M7_VALDOM >0
		aDiaVales := {SM7->M7_VALSEG , SM7->M7_VALTER , SM7->M7_VALQUA , SM7->M7_VALQUI , SM7->M7_VALSEX , SM7->M7_VALSAB , SM7->M7_VALDOM}
		nDiasSeg := 0									//-- Qtde Segundas trabalhadas
		nDiasTer := 0									//-- Qtde Tercas trabalhadas
		nDiasQua := 0									//-- Qtde Quartas trabalhadas
		nDiasQui := 0									//-- Qtde Quintas trabalhadas
		nDiasSex := 0									//-- Qtde Sextas trabalhadas
		nDiasSab := 0									//-- Qtde Sabados trabalhadas
		nDiasDom := 0
		nDias    := 0
		nDNUtil	 := 0
		nDUtil	 := 0

		nDiaNUtil := SM7->M7_QDNUTIL //Qtde vale em dias nใo ๚teis de VT
		nDiaUtil  := SM7->M7_QDIAINF //Qtde vale em dias ๚teis de VT
		nDiasProp := SM7->M7_DPROPIN
		nValUni	  := PosAlias( "SRN" , SM7->M7_CODIGO , xFilial("SRN") , "RN_VUNIATU" , 1 )
		nDiasFix  := PosAlias( "SRN" , SM7->M7_CODIGO , xFilial("SRN") , "RN_DIASFIX" , 1 )

		If !lUsaCPer .And. SM7->M7_VALFIX > 0
			nDiasFix := SM7->M7_VALFIX
		EndIf

		If SM7->M7_DPROPIN == 99 //Se diasprop for 99 nใo calcula meio de transporte
			SM7->(DbSkip())
			Loop
		EndIf

		If lPeriodico //complemento
			nDiasFix	:= 0
			nDiaNUtil 	:= 0
			If aPergunte[11,3] > 0
				nDiaUtil	:= aPergunte[11,3]
			EndIf
			aPergunte[6,3] 	:= 0 //vales a deduzir
		EndIf

		If __lMemCalc
			fAddMemLog("Benefํcio : " + SM7->M7_CODIGO ,1,1)
			fAddMemLog("Benefํcio avulso : " +If(SM7->M7_COMPL=="1","SIM","NรO") ,1,1)
			fAddMemLog("Dias da semana preenchidos : " +If(lDias,"SIM","NรO") ,1,1)
			fAddMemLog("Dias fixos : " + cvaltochar(nDiasFix) ,1,1)
			fAddMemLog("Quantidade de vales dias ๚teis : " + cvaltochar(nDiaUtil) ,1,1)
			fAddMemLog("Quantidade de vales dias nใo ๚teis : " + cvaltochar(nDiaNUtil) ,1,1)
			fAddMemLog("Quantidade de vales proporcionais : " + cvaltochar(nDiasProp) ,1,1)
			If lPeriodico
				fAddMemLog("Vales por dia informados : " + cvaltochar(nDiaUtil) ,1,1)
			EndIf
			fAddMemLog("vales a deduzir informado : " + cvaltochar(aPergunte[6,3]) ,1,1)
			fAddMemLog("Faltas  : " + cvaltochar(nFaltas) ,1,1)
			fAddMemLog("Valor Unitแrio  : " + Transform(nValUni,"@E 999,999,999.99") ,1,1)
		EndIf

		If aPergunte[1,3] == 2 //Se for calculo da diferenca
			SQY->(dbSeek(xFilial("SQY")+SM7->M7_CODIGO+dtos(aPeriodo[nPosSem,3]),.T.))
			if SQY->QY_COD == SM7->M7_CODIGO .AND. (SQY->QY_DATVIGE >= aPeriodo[nPosSem,3] .and. SQY->QY_DATVIGE <= aPeriodo[nPosSem,4])
				nValUniAnt := SQY->QY_VUNIANT
				nValUniDif := nValUni - nValUniAnt
				nDiasVtr := 0
				for nh := 1 to len(aPeriodo[nPosSem][5])
					if aPeriodo[nPosSem][5][nh][1] >= SQY->QY_DATVIGE.and. aPeriodo[nPosSem][5][nh][3] == '1'
						nDiasVTR ++
					EndIf
				Next
			Else
				nValUniAnt := PosAlias( "SRN" , SM7->M7_CODIGO , xFilial("SRN") , "RN_VUNIANT" , 1 )
				nValUniDif := nValUni - nValUniAnt
				nDiasVTR   := aPeriodo[nPosSem,11] //RCF_DDIFVT
			EndIf
			nValVTR    := (nDiasVTR  * nDiaUtil) * nValUniDif
			nDiasProp  := 0	//Zera pois nao sera utilizado
		Else
			nDiaTran  := 0
			nDiaNTran := 0
			If Empty(nDiasFix) .And. Empty(nDiasProp)	//Caso informe dias proporcionais ou fixos, considera os dias informados ao inv้s de calcular
				If !lUsaCPer .And. Len(aTabCalend) > 1 .And. lDias
					
					GetDiasTab(aTabCalend,aDiaVales,lPropFerias,lFeriado,.T.,nDUtil,nDiaNUtil,@nDiasSeg,@nDiasTer,@nDiasQua,@nDiasQui,@nDiasSex,@nDiasSab,@nDiasDom,@nDias,@nDiaNTran)

					//-- Calculo dos Dias Uteis     ( Qtde de cada dia semana * No vales de cada dia da semana ) - deducao
					nDiaTran := nDiasSeg + nDiasTer + nDiasQua + nDiasQui + nDiasSex  + nDiasSab + nDiasDom
					
					nDiasPonto	:= ( ( nDiasSeg * SM7->M7_VALSEG) + ( nDiasTer * SM7->M7_VALTER ) + ( nDiasQua *  SM7->M7_VALQUA ) + ( nDiasQui * SM7->M7_VALQUI ) + ( nDiasSex * SM7->M7_VALSEX ) + ( nDiasSab * SM7->M7_VALSAB ) + ( nDiasDom * SM7->M7_VALDOM ) )

					nDiaUCal  := nDiasPonto - aPergunte[6,3]
					nDiaNUCal := 0

					If nFaltas > 0 .and. Len(aFaltas) > 0
						If SM7->M7_VALDOM > 0
							nFalAux += aFaltas[1] * SM7->M7_VALDOM
						EndIf
						If SM7->M7_VALSEG > 0
							nFalAux += aFaltas[2] * SM7->M7_VALSEG
						EndIf
						If SM7->M7_VALTER > 0
							nFalAux += aFaltas[3] * SM7->M7_VALTER
						EndIf
						If SM7->M7_VALQUA > 0
							nFalAux += aFaltas[4] * SM7->M7_VALQUA
						EndIf
						If SM7->M7_VALQUI > 0
							nFalAux += aFaltas[5] * SM7->M7_VALQUI
						EndIf
						If SM7->M7_VALSEX > 0
							nFalAux += aFaltas[6] * SM7->M7_VALSEX
						EndIf
						If SM7->M7_VALSAB > 0
							nFalAux += aFaltas[7] * SM7->M7_VALSAB
						EndIf
						nDiaUCal -= nFalAux
					EndIf
				Else
					If !Empty(aDiasMes) //Se existir dados no array aDiasMes, utiliza estes dados para montar dias uteis e nao uteis de vale transporte
						If __lMemCalc
							fAddMemLog("Calendario ANALอTICO " ,1,1)
						EndIf

						If lPeriodico
							nPos := aScan(aDiasMes,{|x| x[1] == aPergunte[12,3]})
						EndIf
						If nPos == 0
							nPos := 1
						EndIf
						aEval(aDiasMes, {|x| If(x[2] == "1" .And. x[3] == "1", nDUtil ++, If(x[2] != "1" .And. x[3] == "1", nDNUtil ++,  ))})
						For nX := nPos to Len(aDiasMes)
							If (!lUsaCPer .And. aDiasMes[nX,4]) .Or. (aDiasMes[nX,2] == "1" .and. (Empty(dDataDem) .OR. aDiasMes[nX,1] < dDataDem) .and. aDiasMes[nX,4])
								nDiaTran += 1
							ElseIf (!lUsaCPer .And. aDiasMes[nX,4]) .Or. (aDiasMes[nX,2] <> "1" .and. (Empty(dDataDem) .OR. aDiasMes[nX,1] < dDataDem) .and. aDiasMes[nX,4])
								nDiaNTran += 1
							EndIf
							If lPeriodico .And. aDiasMes[nX][1]==aPergunte[13,3]
								exit
							EndIf
						Next nX
					Else //do contrario utiliza cadastro de periodo - RCF_DUTILT e RCF_DNUTIL
						If __lMemCalc
							fAddMemLog("Calendario SINTษTICO " ,1,1)
						EndIf

						If lPeriodico
							nDiaTran  := DateDiffDay( aPergunte[12,3],aPergunte[13,3] ) + 1
							nDiaNTran := 0
						Else
							nDiaTran  := aPeriodo[nPosSem,10]
							nDiaNTran := aPeriodo[nPosSem,9]
						EndIf
					EndIf
					If __lMemCalc
						fAddMemLog("Dias ๚teis : " +  cvaltochar(nDiaTran) ,1,1)
						fAddMemLog("Dias nใo ๚teis : " +  cvaltochar(nDiaNTran) ,1,1)
					EndIf
					nDiaUCal  := ( nDiaTran * nDiaUtil ) - aPergunte[6,3]
					nDiaNUCal := nDiaNTran * nDiaNUtil
				EndIf
			EndIf

			// Ponto de entrada para alteracao dos dias de vales e dias trabalhados no mes.
			If lGPVTRDias
				aVTRDias := {}
				aAdd(aVTRDias , {nDiaNTran, nDiaTran, nDiasProp, nDiasFix } )
				aVTRDias := ExecBlock("GPVTRDias",.F.,.F.)

				If ValType(aVTRDias) == "A" .And. Len(aVTRDias) > 0 .And. Len(aVTRDias[1]) >= 4
					nDiaNTraPE	:= aVTRDias[1,1]
					nDiaTraPE	:= aVTRDias[1,2]
					nDiasProp	:= aVTRDias[1,3]
					nDiasFix	:= aVTRDias[1,4]
				EndIf

				If ( nDiaNTraPE != nDiaNTran ) .Or. ( nDiaTraPE != nDiaTran )	//Se qtde. de dias ๚teis ou nใo ๚teis for alterada,
					nDiaNUCal += ( (nDiaNTraPE - nDiaNTran) * nDiaNUtil )
					nDiaUcal += ( (nDiaTraPE - nDiaTran) * nDiaUtil )			//soma com a quantidade de vales jแ calculada
					nDiaNTran := nDiaNTraPE
					nDiaTran  := nDiaTraPE										//e dias prop passa a ser informado
				EndIf
			EndIf

			//Considera dias fixos caso use integra็ใo com Servi็os; para os demais, apenas considera fixos caso nใo informe dias proporcionais
			If nDiasFix > 0 .And. ( Empty(SM7->M7_DPROPIN) .Or. fIntRHGS() )
				nDiaTran  := nDiasFix
				nDiaNTran := 0
				nDiasProp := nDiasFix
			ElseIf !Empty(SM7->M7_DPROPIN) .And. nDiasProp > 0	//Caso informe dias proporcionais, considera o valor como dias ๚teis de VT e zera dias nใo ๚teis
				nDiaTran  := nDiasProp
				nDiaNTran := 0
				nDiaNUCal := 0
				nDiaUcal  := ( nDiaTran * nDiaUtil ) - aPergunte[6,3]
			EndIf

			If !lSP7 .And. nFaltas > 0 .And. nFaltas >= nDiaTran
				If lPeriodico
					dDt1	:= aPergunte[12,3]
					dDt2	:= aPergunte[13,3]
				Else
					dDt1	:= aPeriodo[nPosSem,3]
					dDt2	:= aPeriodo[nPosSem,4]
				EndIf
				GravaSP7(nDiaTran,dDt1,dDt2)
				lSP7 := .T.
			EndIf

			nDiasVTR  := Max( If( nDiasFix > 0 .And. ( Empty(SM7->M7_DPROPIN) .Or. fIntRHGS() ), nDiasFix * nDiaUtil, If (!lUsaCPer .And. Len(aTabCalend) > 1 .And. lDias, nDiaUCal ,nDiaUCal + nDiaNUCal - ( nFaltas * nDiaUtil ) ) ), 0 )
			nValVTR  := nDiasVTR * nValUni
			If __lMemCalc
				fAddMemLog("Dias de calculo : " +  cvaltochar(nDiasVTR) ,1,1)
				fAddMemLog("Valor calculo : " +  Transform(nValVTR,"@E 999,999,999.99") ,1,1)
			EndIf
		EndIf

		nTotVTR += nValVTR
		cKeyAux := SM7->(M7_FILIAL+M7_MAT+M7_TPVALE+M7_CODIGO)

		aAdd(aVTCalc , {cKeyAux, nDiasVTR, nValUni, nValVTR,  nCustFunc,(nValVTR-nCustFunc), nValUniDif, nDiasProp, 0 ,SM7->M7_CODIGO,Iif(ndias > 0,nDias,nDiaTran),nDiaUtil,nDiaNUtil,SM7->M7_TPCALC,aDiaVales,SM7->M7_VALFIX,If(!Empty(nDNUtil),nDNUtil,nDiaNTran),nDUtil,""} )

		If SM7->M7_COMPL == "1"
			RecLock("SM7",.F.)
			SM7->(dbDelete())
			SM7->(msUnlock())
		EndIf

		SM7->(dbSkip())

	EndDo
EndIf

If __lMemCalc
	fAddMemLog("Total VTR : " +  Transform(nTotVTR,"@E 999,999,999.99") ,1,1)
	fAddMemLog("Percentual funcionแrio  : " +  Transform(nPercentual,"@E 999,999,999.99") + " %" ,1,1)
	fAddMemLog("Teto do custo VT  = Salario Base * Percentual  : " +  Transform(nCustFunc,"@E 999,999,999.99")  ,1,1)
EndIf

For nX := 1 to Len(aVTCalc)
	aVTCalc[nX,9] := nTotVTR //Atualiza valor total de vale transporte
	If nCustFunc > nTotVTR
		aVTCalc[nX,5] := aVtCalc[nX,4] //Se Custo funcionario for maior que soma do total de VT, custo do funcinario passa a ser o valor total do VT
		aVTCalc[nX,6] := 0
	Else
		If nX == Len(aVTCalc)
			aVTCalc[nX,5] := nCustFunc - nCustAux //Rateia custo do VT
			aVTCalc[nX,6] := aVTCalc[nX,4] - aVTCalc[nX,5] //Custo Empresa
		Else
			aVTCalc[nX,5] := Round(aVTCalc[nX,5] / nTotVTR * aVTCalc[nX,4],2) //Rateia custo do VT
			aVTCalc[nX,6] := aVTCalc[nX,4] - aVTCalc[nX,5] //Custo Empresa
			nCustAux += aVTCalc[nX,5]
		EndIf
	EndIf
Next nX

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVTRGrv 	   บAutor  ณLeandro Drumond  บ Data ณ  29/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณGravacao do calculo do vale transporte					      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVTRGrv(aVTCalc)
Local aArea		:= GetArea()
Local cKeyAux	:= ""
Local nX		:= 0

DEFAULT lGPM5001	:= ExistBlock("GPM5001")

DbSelectArea("SR0")
DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO"))

For nX := 1 to Len(aVTCalc)
	cKeyAux := aVTCalc[nX,1]
	DbSeek(cKeyAux)
	If Reclock( "SR0" , .F. )
		If aVTCalc[nX,8] > 0
			SR0->R0_DIASPRO := aVTCalc[nX,8]  //Grava apenas se utilizou o campo R0_DPROPIN
		Else
			SR0->R0_DIASPRO := 0
		EndIf

		SR0->R0_DUTILM	:= aPeriodo[nPosSem,10] 	//Dias Uteis no M๊s - campo RCF_DUTILT
		SR0->R0_DNUTIM	:= aPeriodo[nPosSem,9]		//Dias Nใo Uteis no M๊s - campo RCF_DNUTIL
		SR0->R0_SALBASE	:= nSalmes					//Salแrio Base
		SR0->R0_CC		:= SRA->RA_CC				//Centro de Custo do Funcionแrio
		SR0->R0_FALTAS 	:= nFaltas					//Dias correspondentes เ faltas (mnem๔nico NFALTAS)
		SR0->R0_FERIAS 	:= nFerVen					//Dias correspondentes a f้rias (mnem๔nico NFERVEN)
		SR0->R0_AFAST 	:= nDiasAfas				//Dias correspondentes a afastamentos (mnem๔nico NDIASAFAS)

		If aPergunte[1,3] == 2
			SR0->R0_QDIADIF	:= aVTCalc[nX,2]	//Dias de Difer. calculados
			SR0->R0_VALDIF 	:= aVTCalc[nX,4]	//Valor de Dif. VTR calculado
			SR0->R0_CUNIDF 	:= aVTCalc[nX,7]	//Custo Unitแrio de Difer. (RN_VUNIATU - RN_VUNIANT)
			SR0->R0_CFUNDF	:= aVTCalc[nX,5]	//Custo Funcionแrio Difer.
			SR0->R0_CEMPDF	:= aVTCalc[nX,6]	//Custo Empresa Difer.
		Else
			SR0->R0_QDIACAL	:= aVTCalc[nX,2]	//Dias calculados
			SR0->R0_VALCAL 	:= aVTCalc[nX,4] 	//Valor VTR calculado
			SR0->R0_VLRVALE	:= aVTCalc[nX,3]	//Custo Unitแrio
			SR0->R0_VLRFUNC	:= aVTCalc[nX,5] 	//Custo Funcionแrio
			SR0->R0_VLREMP 	:= aVTCalc[nX,6]	//Custo Empresa
		EndIf

		SR0->( MsUnLock() )
		//PE para gravar campos personalizados
		If lGPM5001
			ExecBlock("GPM5001",.F.,.F.)
		EndIf
	EndIf

Next nX

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVTRGrvN 	   บAutor  ณLeandro Drumond  บ Data ณ  29/07/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณGravacao do calculo do vale transporte					      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVTRGrvN(aVTCalc)
Local aArea		:= GetArea()
Local cNumPed   := ""
Local cKey		:= ""
Local cCodCCT 	:= ""
Local nX		:= 0
Local nPos      := 0
Local lNovo		:= .T.
Local lPeriodico:= aPergunte[1,3] == 3//Avulso/Complemnto/Semanal/diario
Local lGpem040  := FunName() == "GPEM040" .or. FunName() == "GPEM040B"

DEFAULT lGPM5001  := ExistBlock("GPM5001")
DEFAULT lTemCCT	  := SR0->(ColumnPos("R0_CODCCT")) > 0

If lGpem040 //Se for rescisใo, inclui valores calculados no array para desconto na rescisใo

	aBenefRes[1] := aClone(aVTCalc)

Else

	If lTemCCT .and. !Empty(SRA->RA_SINDICA)
		cCodCCT := fBuscaCCT(SRA->RA_SINDICA)
	EndIf

	DbSelectArea("SR0")
	SR0->(DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO+R0_PEDIDO")))

	// Exclui cแlculo anterior para o mesmo perํodo, caso exista
	cKey := SRA->RA_FILIAL + SRA->RA_MAT + "0"
	If SR0->(dbSeek(cKey))
		While SR0->(R0_FILIAL + R0_MAT + R0_TPVALE) == cKey
			//Somente deleta o cแlculo se for recแlculo. Caso o pedido jแ tenha sido emitido, nใo deleta.
			If SR0->R0_DTREFI == aPeriodo[nPosSem, 3] .And. SR0->R0_DTREFF == aPeriodo[nPosSem, 4] .And. SR0->R0_PEDIDO == "1" .AND. aPergunte[1,3] != 2 ;
				.Or. (Empty(SR0->R0_DTREFI) .And. Empty(SR0->R0_DTREFF) .And. SR0->R0_PEDIDO != "2")
				//Se jแ existir um cแlculo em aberto, quer dizer que ้ recแlculo, entใo armazena o numero do pedido jแ utilizado.
				If ( nPos := aScan(aVTCalc, {|x| x[1] == SR0->R0_FILIAL+SR0->R0_MAT+SR0->R0_TPVALE+SR0->R0_CODIGO}) ) > 0
					aVTCalc[nPos,19] := SR0->R0_NROPED
				EndIf
				Reclock("SR0", .F.)
				SR0->(dbDelete())
				SR0->(MsUnlock())
			EndIf
			SR0->(dbSkip())
		EndDo
		SR0->(dbGoTop())
	EndIf

	For nX := 1 to Len(aVTCalc)
		lNovo 	  := .T.
		cNumPed   := ""
		If SR0->(DbSeek(aVTCalc[nX, 1] + "1"))
			While SR0->( !EoF() ) .And. SR0->R0_FILIAL+SR0->R0_MAT+SR0->R0_TPVALE+SR0->R0_CODIGO+SR0->R0_PEDIDO == aVTCalc[nX, 1] + "1"
				If SR0->R0_NROPGT == aPeriodo[nPosSem, 2] 
					lNovo := .F.
					Exit
				EndIf
				SR0->( dbSkip() )
			EndDo
		EndIf
		If !lNovo .And. !Empty(SR0->R0_NROPED)
			cNroPed := SR0->R0_NROPED
		ElseIf lNovo
			cNumPed := aVTCalc[nX,19]
		EndIf
		If Reclock( "SR0" , lNovo)
			If aVTCalc[nX,8] > 0  .OR. (lUsaCPer .And. (aVTCalc[nX,11] != aPeriodo[nPosSem,10]))
				SR0->R0_DPROPIN := aVTCalc[nX,8]	// Grava apenas se utilizou o campo R0_DPROPIN
			Else
				SR0->R0_DPROPIN := 0
			EndIf
			SR0->R0_FILIAL	:= SRA->RA_FILIAL
			SR0->R0_MAT		:= SRA->RA_MAT
			SR0->R0_TPVALE	:= "0"
			SR0->R0_CODIGO	:= aVTCalc[nX,10]
			SR0->R0_DIASPRO	:= aVTCalc[nX,11]
			SR0->R0_QDIAINF	:= aVTCalc[nX,12]
			SR0->R0_QDNUTIL	:= aVTCalc[nX,13]
			SR0->R0_TPCALC	:= aVTCalc[nX,14]
			SR0->R0_ROTEIR	:= cRot
			SR0->R0_PERIOD 	:= cPeriodo
			SR0->R0_NROPGT	:= cNumPag
			SR0->R0_ANOMES	:= SubSTr(cPeriodo,5,2) + SubSTr(cPeriodo,1,4)
			SR0->R0_PEDIDO	:= "1"
			SR0->R0_QVALSEG  := aVTCalc[nX,15,1]
			SR0->R0_QVALTER  := aVTCalc[nX,15,2]
			SR0->R0_QVALQUA  := aVTCalc[nX,15,3]
			SR0->R0_QVALQUI  := aVTCalc[nX,15,4]
			SR0->R0_QVALSEX  := aVTCalc[nX,15,5]
			SR0->R0_QVALSAB  := aVTCalc[nX,15,6]
			SR0->R0_QVALDOM  := aVTCalc[nX,15,7]
			SR0->R0_QVALFIX  := aVTCalc[nX,16]

			If lPeriodico
				SR0->R0_DTREFI	:= aPergunte[12,3]
				SR0->R0_DTREFF	:= aPergunte[13,3]
			Else
				SR0->R0_DTREFI	:= aPeriodo[nPosSem,3]
				SR0->R0_DTREFF	:= aPeriodo[nPosSem,4]
			EndIf

			If lNovo
				//Caso nใo exista pedido, pode ser novo m๊s ou mais de 1 pedido no m๊s, entใo assume o novo n๚mero do pedido.
				If cNumPed <> ""
					cNroPed := cNumPed
				EndIf
				SR0->R0_NROPED	:= GetNroPed()
			EndIf
			SR0->R0_TPBEN	:= BuscaTBen(SR0->R0_TPVALE,SR0->R0_CODIGO,SR0->R0_FILIAL)

			SR0->R0_DUTILM	:= If(!Empty(aVTCalc[nX,18]), aVTCalc[nX,18], aVTCalc[nX,11])	//Dias Uteis no M๊s - campo RCF_DUTILT
			SR0->R0_DNUTIM	:= aVTCalc[nX,17]			//Dias Nใo Uteis no M๊s - campo RCF_DNUTIL
			SR0->R0_SALBASE	:= nSalmes					//Salแrio Base
			SR0->R0_CC		:= SRA->RA_CC				//Centro de Custo do Funcionแrio
			SR0->R0_FALTAS 	:= nFaltas					//Dias correspondentes เ faltas (mnem๔nico NFALTAS)
			SR0->R0_FERIAS 	:= nFerVen					//Dias correspondentes a f้rias (mnem๔nico NFERVEN)
			SR0->R0_AFAST 	:= nDiasAfas				//Dias correspondentes a afastamentos (mnem๔nico NDIASAFAS)

			If aPergunte[1,3] == 2
				SR0->R0_QDIADIF	:= aVTCalc[nX,2]	//Dias de Difer. calculados
				SR0->R0_VALDIF 	:= aVTCalc[nX,4]	//Valor de Dif. VTR calculado
				SR0->R0_CUNIDF 	:= aVTCalc[nX,7]	//Custo Unitแrio de Difer. (RN_VUNIATU - RN_VUNIANT)
				SR0->R0_CFUNDF	:= aVTCalc[nX,5]	//Custo Funcionแrio Difer.
				SR0->R0_CEMPDF	:= aVTCalc[nX,6]	//Custo Empresa Difer.
			Else
				SR0->R0_QDIACAL	:= aVTCalc[nX,2]	//Dias calculados
				SR0->R0_VALCAL 	:= aVTCalc[nX,4] 	//Valor VTR calculado
				SR0->R0_VLRVALE	:= aVTCalc[nX,3]	//Custo Unitแrio
				SR0->R0_VLRFUNC	:= aVTCalc[nX,5] 	//Custo Funcionแrio
				SR0->R0_VLREMP 	:= aVTCalc[nX,6]	//Custo Empresa
			EndIf

			If lTemCCT
				SR0->R0_CODCCT 	:= cCodCCT	//Conven็ใo Coletiva de Trabalho
			EndIf

			SR0->( MsUnLock() )

			//PE para gravar campos personalizados
			If lGPM5001
				ExecBlock("GPM5001",.F.,.F.)
			EndIf
		EndIf

		If cNumPed <> ""
			cNroPed := "" //Zera static para nใo usar o mesmo n๚mero para outro funcionแrio causando chave duplicada.
		EndIf

	Next nX

	CustoVales("0")

	RestArea(aArea)

EndIf

Return Nil


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVRFCalc 	   บAutor  ณLeandro Drumond  บ Data ณ  03/08/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณEfetua o calculo do vale refeicao   					      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVRFCalc(aVRCalc)
Local aArea		:= GetArea()
Local aPerAux	:= {}
Local cFilFunc  := SRA->RA_FILIAL
Local cMatFunc  := SRA->RA_MAT
Local cKeyAux	:= ""
Local lPropin	:= .F.
Local lPriVez	:= .T.
Local nCustFunc := 0
Local nCustEmp	:= 0
Local nValVRF	:= 0
Local nDiaInf	:= 0
Local nValUni	:= 0
Local nPerc		:= 0
Local nTeto		:= 0
Local nDiasFix	:= 0
Local nDiaRef	:= 0
Local nDiaCal	:= 0
Local nX		:= 0
Local nTotDif	:= 0
Local nDiaUtil	:= 0
Local nDiaNUtil	:= 0
Local nDUtil	:= 0
Local nDNUtil	:= 0
Local nValOri	:= 0
Local nDiasPer	:= If(SuperGetMv("MV_DIASPER",,"1") == "1",nDiasP,(If(SRA->RA_TIPOPGT =="M", P_QTDIAMES, P_NTOTDIAS)))
Local nFaltVRF  := nFaltas

aVRCalc	  := {}

DbSelectArea("RFO")
DbSetOrder(RetOrder("RFO","RFO_FILIAL+RFO_TPVALE+RFO_CODIGO"))

DbSelectArea("SR0")
DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO"))

If( DbSeek(cFilFunc + cMatFunc + "1") )

	While SR0->(!Eof() .and. R0_FILIAL + R0_MAT + R0_TPVALE == cFilFunc + cMatFunc + "1")

	    If SR0->R0_DPROPIN == 99 //Se diasprop for 99 nใo calcula meio de transporte
	    	SR0->(DbSkip())
	    	Loop
	    EndIf

	    nDUtil	  := SR0->R0_QDIAINF
	    nDNUtil	  := SR0->R0_QDNUTIL
    	nValUni   := 0
    	nPerc	  := 0
    	nTeto	  := 0
    	nDiasFix  := 0
    	nDiaRef	  := 0
		nDiaUtil  := 0
		nDiaNUtil := 0
    	lPropin	  := .F.

	    If RFO->(DbSeek(xFilial("RFO")+SR0->(R0_TPVALE + R0_CODIGO)))
	    	nValUni  := RFO->RFO_VALOR
	    	nPerc	 := RFO->RFO_PERC
	    	nTeto	 := RFO->RFO_TETO
	    	nDiasFix := RFO->RFO_DIAFIX
	    EndIf

	    If SR0->R0_DPROPIN > 0
	    	nDiaRef := SR0->R0_DPROPIN
	    	lPropin	:= .T.
    	ElseIf !Empty(aDiasMes)	//Se existir dados no array aDiasMes, utiliza estes dados para montar dias uteis e nao uteis de vale transporte (analitico)
			For nX := 1 to Len(aDiasMes)
				If aDiasMes[nX,2] == "1" .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
					nDiaUtil += 1
				ElseIf aDiasMes[nX,2] <> "1" .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
					nDiaNUtil += 1
				EndIf
			Next nX
		Else				//do contrario utiliza cadastro de periodo - RCF_DUTILT e RCF_DNUTIL (sintetico)
			If aPergunte[2,3] <> 2 .And. DiasTrab < nDiasPer
				nDiaUtil  := Int( Round( aPeriodo[nPosSem,12] * ( ( DiasTrab - ( nDiasMat + nDPrgSalMa ) ) / nDiasPer ), 2 ) )
				nDiaNUtil := 0
			Else
				nDiaUtil  := aPeriodo[nPosSem,12]
				nDiaNUtil := 0
			EndIf
		EndIf

    	If nDiasFix > 0
    		nDiaCal := iif(!Empty(nDiaRef) .and. lPropin ,nDiaRef,nDiasFix) // Quando o nDiaRef estiver preenchido ้ mandat๓rio, pois deve ser considerado primeiro que o nDiasFix
    	Else
    		If nDiaRef > 0
    			nDiaInf := nDUtil
    			nDiaCal := Max( ( nDiaRef - nFaltVRF ) * nDiaInf - aPergunte[5,3], 0 ) //Parametro MV_PAR05 - Deducoes
    		Else
    			nDiaRef   := nDiaUtil + nDiaNUtil
	    		nDiaCal   := ( (( nDiaUtil * nDUtil ) - aPergunte[5,3]) + (nDiaNUtil * nDNUtil) ) - (nFaltVRF * nDUtil)
	    	EndIf

			If nDUtil > 0
				nFaltVRF := 0
			EndIf
    	EndIf

    	nValVRF 	:= nDiaCal * nValUni

    	//Inicio do trecho para recalculo dos beneficios gerados pela integra็ใo com servi็os
    	cKeyAux := SR0->(R0_FILIAL + R0_MAT + R0_TPVALE + R0_CODIGO)

		//Inicio do trecho para recalculo dos beneficios gerados pela integra็ใo com servi็os

		//Recalcula ๚ltimo VR pago por crit้rios definidos na intergra็ใo com servi็os para verificar se deve ser feito algum desconto ou acr้scimo
		If lPriVez .And. ( fIntRHGS() .AND. SuperGetMv("MV_BENEXGS",,.T.) )
			fRetBenAnt(SRA->RA_FILIAL, SRA->RA_MAT, SRA->RA_PROCES, "1", @aPerAux)
			If Len(aPerAux) > 0
				nTotDif := fApurPAnt(aPerAux[1], "1")[1]
			EndIf
			lPriVez := .F.
		EndIf

		nValOri := nValVRF // Guarda o valor calculado originalmente

		If nTotDif != 0

			If nTotDif > 0 //Se houver diferen็a positiva para pagar em rela็ใo ao m๊s anterior, soma no valor total do VR
				nValVRF += nTotDif
				nTotDif := 0
			ElseIf ( nValVRF + nTotDif ) >= 0
				nValVRF += nTotDif
				nTotDif := 0
			Else
				nTotDif += nValVRF
				nValVRF := 0
			EndIf

		EndIf

		//Fim do trecho para recalculo dos beneficios gerados pela integra็ใo com servi็os

		nCustFunc   := Round(( nValVRF * nPerc ) / 100,2)
		If nTeto > 0
			nCustFunc   := Min(nCustFunc, nTeto)
		EndIf
		nCustEmp	:= nValVRF - nCustFunc

		Aadd(aVRCalc , {cKeyAux, nDiaCal, nValUni, nValVRF,  nCustFunc, nCustEmp, nDiaRef, lPropin, 0, nValOri,,,,,,,,,nDiaUtil,nDiaNUtil} )

		SR0->(DbSkip())

	EndDo

	If nTotDif < 0 //Grava o saldo negativo no ๚ltimo vale calculado
		aVRCalc[Len(aVRCalc),9] := nTotDif
	EndIf

EndIf

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVRFCalcN 	   บAutor  ณLeandro Drumond  บ Data ณ  03/08/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณEfetua o calculo do vale refeicao   					      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVRFCalcN(aVRCalc)
Local aArea			:= GetArea()
Local aPerAux		:= {}
Local aDias			:= {}
Local cFilFunc  	:= SRA->RA_FILIAL
Local cMatFunc  	:= SRA->RA_MAT
Local cKeyAux		:= ""
Local lPropin		:= .F.
Local lPriVez		:= .T.
Local nCustFunc 	:= 0
Local nCustEmp		:= 0
Local nValVRF		:= 0
Local nDiaInf		:= 0
Local nValUni		:= 0
Local nPerc			:= 0
Local nTeto			:= 0
Local nDiasFix		:= 0
Local nDiaRef		:= 0
Local nDiaCal		:= 0
Local nX			:= 0
Local nTotDif		:= 0
Local nValOri		:= 0
Local nDiasProp 	:= 0
Local lPeriodico	:= aPergunte[6,3] == 1//Avulso/Complemnto/Semanal/diario
Local nPos			:= 0
Local nDiasSeg 		:= 0									//-- Qtde Segundas trabalhadas
Local nDiasTer 		:= 0									//-- Qtde Tercas trabalhadas
Local nDiasQua 		:= 0									//-- Qtde Quartas trabalhadas
Local nDiasQui 		:= 0									//-- Qtde Quintas trabalhadas
Local nDiasSex 		:= 0									//-- Qtde Sextas trabalhadas
Local nDiasSab 		:= 0									//-- Qtde Sabados trabalhadas
Local nDiasDom 		:= 0									//-- Qtde Domingos trabalhadas
Local nDiasPonto 	:= 0
Local nDiaUtil		:= 0
Local nDiaNUtil		:= 0
Local nDUtil		:= 0
Local nDNUtil		:= 0
Local nAuxUtil		:= 0
Local nAuxNUtil		:= 0
Local nFalAux		:= 0
Local lDias			:= .F.
Local lFeriado 		:= .F.
Local lPropFerias	:= APERGUNTE[3,3] == 1
Local aTabCalend	:= If (!lUsaCPer,fCarPonto(),{})
Local aDiaVales		:= {}
Local lSP7			:= .F.
Local dDt1			:= ctod("")
Local dDt2			:= ctod("")
Local nDiasPer		:= If(GetMvRH("MV_DIASPER",,"1") == "1",nDiasP,(If(SRA->RA_TIPOPGT =="M", P_QTDIAMES, P_NTOTDIAS)))

DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo


If !lUsaCPer .And. Len(aTabCalend) > 1
	SPA->( dbSeek( xFilial("SPA", SRA->RA_FILIAL ) + SRA->RA_REGRA ) )
	lFeriado := ( SPA->PA_FERIADO == "S" .Or. ( SPA->( Type("PA_RECBENF") ) != "U" .And. SPA->PA_RECBENF == "1" ) )
EndIf

If __lMemCalc
	fAddMemLog("Calendario utilizado(MV_USACPER) : " +If(lUsaCPer,"FOLHA","PONTO") ,1,1)
	fAddMemLog("Considera feriado cadastro de Regra(SPA) : " + If(lFeriado,"Sim","Nใo") ,1,1)
	If !lUsaCPer
		fAddMemLog("Tabela padrใo do ponto carregada : " + If(Len(aTabCalend) > 1 ,"Sim","Nใo") ,1,1)
	EndIf
	fAddMemLog("Proporcional a f้rias : " + If(lPropFerias,"Sim","Nใo") ,1,1)
	fAddMemLog("Calculo peri๓dico : " + If(lPeriodico,"Sim","Nใo") ,1,1)
	fAddMemLog("Dias perํodo : " + cvaltochar(nDiasPer) ,1,1)
EndIf

aVRCalc	  := {}

DbSelectArea("RFO")
RFO->(DbSetOrder(RetOrder("RFO","RFO_FILIAL+RFO_TPVALE+RFO_CODIGO")))

DbSelectArea("SM7")
SM7->(DbSetOrder(RetOrder("SM7", "M7_FILIAL+M7_MAT+M7_TPVALE+M7_CODIGO")))

If SM7->(DbSeek(cFilFunc + cMatFunc + "1") )

	While SM7->(!Eof() .and. M7_FILIAL + M7_MAT + M7_TPVALE == cFilFunc + cMatFunc + "1")

		nFalAux := 0

		If lPeriodico .And. aPergunte[10,3] == 1 .And. SM7->M7_COMPL <> '1'
			SM7->(dbSkip())
			Loop
		EndIf

		If SM7->M7_DPROPIN == 99 //Se diasprop for 99 nใo calcula meio de transporte
			SM7->(DbSkip())
			Loop
		EndIf

		lDias :=  SM7->M7_VALSEG > 0 .Or. SM7->M7_VALTER  > 0 .Or. SM7->M7_VALQUA  > 0 .Or.  SM7->M7_VALQUI  > 0 .Or.  SM7->M7_VALSEX  > 0 .Or.  SM7->M7_VALSAB  > 0 .Or.  SM7->M7_VALDOM >0
		aDiaVales := {SM7->M7_VALSEG , SM7->M7_VALTER , SM7->M7_VALQUA , SM7->M7_VALQUI , SM7->M7_VALSEX , SM7->M7_VALSAB , SM7->M7_VALDOM}

		nDiasSeg := 0									//-- Qtde Segundas trabalhadas
		nDiasTer := 0									//-- Qtde Tercas trabalhadas
		nDiasQua := 0									//-- Qtde Quartas trabalhadas
		nDiasQui := 0									//-- Qtde Quintas trabalhadas
		nDiasSex := 0									//-- Qtde Sextas trabalhadas
		nDiasSab := 0									//-- Qtde Sabados trabalhadas
		nDiasDom := 0

		nDUtil	 := SM7->M7_QDIAINF
	    nDNUtil	 := SM7->M7_QDNUTIL
		nDiasProp := SM7->M7_DPROPIN
		nValUni  := 0
		nPerc	 := 0
		nTeto	 := 0
		nDiasFix := 0
		nDiaRef	 := 0
		lPropin	 := .F.
		nDiaUtil := 0
		nDiaNUtil:= 0

		If RFO->(DbSeek(xFilial("RFO")+SM7->(M7_TPVALE + M7_CODIGO)))
			nValUni  := RFO->RFO_VALOR
			nPerc	 := RFO->RFO_PERC
			nTeto	 := RFO->RFO_TETO
			nDiasFix := RFO->RFO_DIAFIX
		EndIf

		If !lUsaCPer .And. SM7->M7_VALFIX > 0
			nDiasFix := SM7->M7_VALFIX
		EndIf

		If lPeriodico //complemento
			nDiasFix	:= 0
			nDiaInf	  := SM7->M7_QDIAINF
			If aPergunte[7,3] > 0
				nDiaInf	:= aPergunte[7,3]
			EndIf
			aPergunte[5,3] 	:= 0 //vales a deduzir
		EndIf
		If __lMemCalc
			fAddMemLog("Benefํcio : " + SM7->M7_CODIGO ,1,1)
			fAddMemLog("Benefํcio avulso : " +If(SM7->M7_COMPL=="1","SIM","NรO") ,1,1)
			fAddMemLog("Dias da semana preenchidos : " +If(lDias,"SIM","NรO") ,1,1)
			fAddMemLog("Dias fixos : " + cvaltochar(nDiasFix) ,1,1)
			fAddMemLog("Quantidade de vales dias ๚teis : " + cvaltochar(nDUtil) ,1,1)
			fAddMemLog("Quantidade de vales dias nใo ๚teis : " + cvaltochar(nDNUtil) ,1,1)
			fAddMemLog("Quantidade de vales proporcionais : " + cvaltochar(nDiasProp) ,1,1)
			If lPeriodico
				fAddMemLog("Vales por dia informados : " + cvaltochar(nDiaInf) ,1,1)
			EndIf
			fAddMemLog("vales a deduzir informado : " + cvaltochar(aPergunte[5,3]) ,1,1)
			fAddMemLog("Faltas  : " + cvaltochar(nFaltas) ,1,1)
			fAddMemLog("Valor Unitแrio  : " + Transform(nValUni,"@E 999,999,999.99") ,1,1)
		EndIf
		If nDiasProp > 0
			nDiaRef := nDiasProp
			lPropin	:= .T.
			nDiaCal := Max( ( nDiaRef - If(nDUtil > 0,nFaltas,0) ) * If (lPeriodico, nDiaInf - aPergunte[5,3], nDUtil), 0 ) //Parametro MV_PAR05 - Deducoes
		Else
			If nDiasFix > 0
				nDiaRef  := nDiasFix
				nDiasProp := nDiasFix
				nDiaCal :=  nDiaRef - aPergunte[5,3] //Parametro MV_PAR05 - Deducoes
			Else

				If !lUsaCPer .And. Len(aTabCalend) > 1 .And. lDias

					GetDiasTab(aTabCalend,aDiaVales,lPropFerias,lFeriado,.F.,nDUtil,nDNUtil,@nDiasSeg,@nDiasTer,@nDiasQua,@nDiasQui,@nDiasSex,@nDiasSab,@nDiasDom,@nDiaUtil,@nDiaNUtil)

					//-- Calculo dos Dias Uteis     ( Qtde de cada dia semana * No vales de cada dia da semana ) - deducao
					nDiaRef := nDiasSeg + nDiasTer + nDiasQua + nDiasQui + nDiasSex  + nDiasSab + nDiasDom
					
					nDiasPonto	:= ( ( nDiasSeg * SM7->M7_VALSEG) + ( nDiasTer * SM7->M7_VALTER ) + ( nDiasQua *  SM7->M7_VALQUA ) + ( nDiasQui * SM7->M7_VALQUI ) + ( nDiasSex * SM7->M7_VALSEX ) + ( nDiasSab * SM7->M7_VALSAB ) + ( nDiasDom * SM7->M7_VALDOM ) )
					nDiaCal := nDiasPonto - aPergunte[5,3] //Parametro MV_PAR05 - Deducoes
					
					If nFaltas > 0 .and. Len(aFaltas) > 0
						If SM7->M7_VALDOM > 0
							nFalAux += aFaltas[1]
						EndIf
						If SM7->M7_VALSEG > 0
							nFalAux += aFaltas[2]
						EndIf
						If SM7->M7_VALTER > 0
							nFalAux += aFaltas[3]
						EndIf
						If SM7->M7_VALQUA > 0
							nFalAux += aFaltas[4]
						EndIf
						If SM7->M7_VALQUI > 0
							nFalAux += aFaltas[5]
						EndIf
						If SM7->M7_VALSEX > 0
							nFalAux += aFaltas[6]
						EndIf
						If SM7->M7_VALSAB > 0
							nFalAux += aFaltas[7]
						EndIf
						nDiaCal -= nFalAux
					EndIf
				Else
					If !Empty(aDiasMes)	//Se existir dados no array aDiasMes, utiliza estes dados para montar dias uteis e nao uteis de vale transporte (analitico)
						If __lMemCalc
							fAddMemLog("Calendario ANALอTICO " ,1,1)
						EndIf
						nDiaRef  := 0
						If lPeriodico
							nPos := aScan(aDiasMes,{|x| x[1] == aPergunte[8,3]})
						EndIf
						If nPos == 0
							nPos := 1
						EndIf
						For nX := nPos to Len(aDiasMes)
							If aDiasMes[nX,2] == "1" .and. (Empty(dDataDem) .OR. aDiasMes[nX,1] < dDataDem) .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
								nDiaUtil += 1
							ElseIf aDiasMes[nX,2] <> "1" .and. (Empty(dDataDem) .OR. aDiasMes[nX,1] < dDataDem) .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
								nDiaNUtil += 1
							EndIf
							If lPeriodico .And. aDiasMes[nX][1]==aPergunte[9,3]
								exit
							EndIf
						Next nX
					Else				//do contrario utiliza cadastro de periodo - RCF_DUTILT e RCF_DNUTIL (sintetico)
						If __lMemCalc
							fAddMemLog("Calendario SINTษTICO " ,1,1)
						EndIf
						If lPeriodico
							nDiaRef  := DateDiffDay( aPergunte[8,3],aPergunte[9,3] ) + 1
						ElseIf aPergunte[2,3] <> 2 .And. DiasTrab < nDiasPer
							nDiaUtil  := Int( Round( aPeriodo[nPosSem,12] * ( ( DiasTrab - ( nDiasMat + nDPrgSalMa ) ) / nDiasPer ), 2 ) )
							nDiaNUtil := 0
						Else
							nDiaUtil  := aPeriodo[nPosSem,12]
							nDiaNUtil := 0
						EndIf
					EndIf
					If __lMemCalc
						fAddMemLog("Dias ๚teis : " +  cvaltochar(nDiaUtil) ,1,1)
						fAddMemLog("Dias nใo ๚teis : " +  cvaltochar(nDiaNUtil) ,1,1)
					EndIf

					// O array aFaltas s๓ ้ utilizado quando o lUsaCPer = .F. (Usa informa็๕es do Ponto)
					If Len(aFaltas) > 0
						aFaltas := {}
						nFalAux := nFaltas
					EndIf

					If nDiaRef > 0
						nDiaInf := If(lPeriodico .and. nDiaInf > 0, nDiaInf, nDUtil)
		    			nDiaCal := Max( ( nDiaRef -  If(nDUtil > 0,nFaltas,0) ) * nDiaInf - aPergunte[5,3], 0 ) //Parametro MV_PAR05 - Deducoes
					Else
			    		nAuxUtil  := ( nDiaUtil * If(lPeriodico .and. nDiaInf > 0, nDiaInf, nDUtil) ) - aPergunte[5,3]
			    		nAuxNUtil := nDiaNUtil * If(lPeriodico .and. nDiaInf > 0, nDiaInf, nDNUtil)
			    		nDiaCal   := ( nAuxUtil + nAuxNUtil ) - ( If(nDUtil > 0,nFaltas,0) * nDUtil)
					EndIf
				EndIf
			EndIf
		EndIf

		If !lSP7 .And. If(nDUtil > 0,nFaltas,0) > 0 .And. If(nDUtil > 0,nFaltas,0) >= nDiaRef
			If lPeriodico
				dDt1	:= aPergunte[8,3]
				dDt2	:= aPergunte[9,3]
			Else
				dDt1	:= aPeriodo[nPosSem,3]
				dDt2	:= aPeriodo[nPosSem,4]
			EndIf
			GravaSP7(nDiaRef,dDt1,dDt2)
			lSP7 := .T.
		EndIf

		nValVRF := nDiaCal * nValUni
		If __lMemCalc
			fAddMemLog("Dias de calculo : " +  cvaltochar(nDiaCal) ,1,1)
			fAddMemLog("Valor calculo : " +  Transform(nValVRF,"@E 999,999,999.99") ,1,1)
		EndIf

		//Inicio do trecho para recalculo dos beneficios gerados pela integra็ใo com servi็os

		//Recalcula ๚ltimo VR pago por crit้rios definidos na intergra็ใo com servi็os para verificar se deve ser feito algum desconto ou acr้scimo
		If lPriVez .And. ( fIntRHGS() .AND. SuperGetMv("MV_BENEXGS",,.T.) )
			fRetBenAnt(SRA->RA_FILIAL, SRA->RA_MAT, SRA->RA_PROCES, "1", @aPerAux)
			If Len(aPerAux) > 0
				nTotDif := fApurPAnt(aPerAux[1], "1")[1]
			EndIf
			If __lMemCalc
				fAddMemLog("Integra็ใo com servi็os ATIVADA "  ,1,1)
				fAddMemLog("Valor diferen็a m๊s anterior : " +  Transform(nTotDif,"@E 999,999,999.99") ,1,1)
			EndIf

			lPriVez := .F.
		EndIf

		nValOri := nValVRF	// Guarda o valor calculado originalmente

		If nTotDif != 0

			If nTotDif > 0 //Se houver diferen็a positiva para pagar em rela็ใo ao m๊s anterior, soma no valor total do VR
				nValVRF += nTotDif
				nTotDif := 0
			ElseIf ( nValVRF + nTotDif ) >= 0
				nValVRF += nTotDif
				nTotDif := 0
			Else
				nTotDif += nValVRF
				nValVRF := 0
			EndIf

		EndIf

		//Fim do trecho para recalculo dos beneficios gerados pela integra็ใo com servi็os

		nCustFunc   := Round(( nValVRF * nPerc ) / 100,2)

		If nTeto > 0
			nCustFunc   := Min(nCustFunc, nTeto)
		EndIf
		nCustEmp	:= nValVRF - nCustFunc

		If __lMemCalc
			fAddMemLog("Percentual desconto funcionแrio : " +  cvaltochar(nPerc) + " %" ,1,1)
			fAddMemLog("Teto  : " +  Transform(nTeto,"@E 999,999,999.99") ,1,1)
			fAddMemLog("Custo funcionแrio : " +  Transform(nCustFunc,"@E 999,999,999.99") ,1,1)
			fAddMemLog("Custo empresa : " +  Transform(nCustEmp,"@E 999,999,999.99") ,1,1)
		EndIf

		cKeyAux := SM7->(M7_FILIAL+M7_MAT+M7_TPVALE+M7_CODIGO)

		aAdd(aVRCalc, {cKeyAux, nDiaCal, nValUni, nValVRF, nCustFunc, nCustEmp, nDiasRef, lPropin, SM7->M7_CODIGO, nDiasProp, nDUtil, nDNUtil, SM7->M7_TPCALC, SM7->M7_TPVALE, aDiaVales, SM7->M7_VALFIX, 0, nValOri,nDiaUtil,nDiaNUtil, If( nDUtil > 0, If(Len(aFaltas) > 0 , nFalAux, nFaltas ), 0),""} )

		If SM7->M7_COMPL == "1"
			RecLock("SM7",.F.)
			SM7->(dbDelete())
			SM7->(msUnlock())
		EndIf

		SM7->(DbSkip())

	EndDo

	If nTotDif < 0 //Grava o saldo negativo no ๚ltimo vale calculado
		aVRCalc[Len(aVRCalc), 17] := nTotDif
	EndIf

Else // Nใo Encontrou Registros na SM7, mas deve recalcular o m๊s anterior

	If fIntRHGS() .AND. SuperGetMv("MV_BENEXGS",,.T.) // Apenas integra็ใo com servi็os
		fRetBenAnt(SRA->RA_FILIAL, SRA->RA_MAT, SRA->RA_PROCES, "1", @aPerAux)
		If Len(aPerAux) > 0
			aDias := fApurPAnt(aPerAux[1], "1")
			nTotDif := aDias[1]
			cCodBen := aDias[2]
		EndIf
	EndIf

	nValOri := nValVRF	// Guarda o valor calculado originalmente

	If nTotDif != 0

		//Grava na SM7
		AtuSM7Aut("1", cCodBen)

		If nTotDif > 0	// Se houver diferen็a positiva para pagar em rela็ใo ao m๊s anterior, soma no valor total do VA
			nValVRF += nTotDif
			nTotDif := 0
		ElseIf ( nValVRF + nTotDif ) >= 0	// Se houver diferen็a negativa, debita do total at้ o minimo de zero
			nValVRF += nTotDif
			nTotDif := 0
		Else
			nTotDif += nValVRF
			nValVRF := 0
		EndIf

		cKeyAux := SRA->(RA_FILIAL + RA_MAT + "1" + cCodBen)
		aAdd(aVRCalc, {cKeyAux, 0, nValUni, nValVRF, nCustFunc, nCustEmp, nDiaRef, lPropin, cCodBen, nDiasProp, nDUtil, nDNUtil, "2", "1", {0, 0, 0, 0, 0, 0, 0}, 0, nTotDif, nValOri,0,0,nFaltas,"" })

	EndIf

EndIf

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVALCalc 	   บAutor  ณLeandro Drumond  บ Data ณ  04/08/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณEfetua o calculo do vale alimentacao   					      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVALCalc(aVACalc)
Local aArea		:= GetArea()
Local aPerAux 	:= {}
Local cFilMat   := SRA->RA_FILIAL + SRA->RA_MAT
Local cKeyAux	:= ""
Local lPropin	:= .F.
Local lPriVez 	:= .T.
Local nCustFunc := 0
Local nCustEmp	:= 0
Local nValAli	:= 0
Local nDiaInf	:= 0
Local nValUni	:= 0
Local nPerc		:= 0
Local nTeto		:= 0
Local nDiasFix	:= 0
Local nDiaAlim	:= 0
Local nDiaCal	:= 0
Local nTotDif 	:= 0
Local nValOri	:= 0
Local nX		:= 0
Local nDiaUtil	:= 0
Local nDiaNUtil	:= 0
Local nDUtil	:= 0
Local nDNUtil	:= 0
Local nDiasPer	:= If(SuperGetMv("MV_DIASPER",,"1") == "1",nDiasP,(If(SRA->RA_TIPOPGT =="M", P_QTDIAMES, P_NTOTDIAS)))

aVACalc	  := {}

DbSelectArea("RFO")
DbSetOrder(RetOrder("RFO","RFO_FILIAL+RFO_TPVALE+RFO_CODIGO"))

DbSelectArea("SR0")
DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO"))

If( DbSeek(cFilMat + "2") )

	While SR0->(!Eof() .and. R0_FILIAL + R0_MAT + R0_TPVALE == cFilMat + "2")

	    If SR0->R0_DPROPIN == 99 //Se diasprop for 99 nใo calcula meio de transporte
	    	SR0->(DbSkip())
	    	Loop
	    EndIf

	    nDUtil	 := SR0->R0_QDIAINF
	    nDNUtil	 := SR0->R0_QDNUTIL
    	nValUni  := 0
    	nPerc	 := 0
    	nTeto	 := 0
    	nDiasFix := 0
    	nDiaAlim := 0
    	lPropin	 := .F.
	    nDiaUtil := 0
	    nDiaNUtil:= 0

	    If RFO->(DbSeek(xFilial("RFO")+SR0->(R0_TPVALE + R0_CODIGO)))
	    	nValUni  := RFO->RFO_VALOR
	    	nPerc	 := RFO->RFO_PERC
	    	nTeto	 := RFO->RFO_TETO
	    	nDiasFix := RFO->RFO_DIAFIX
	    EndIf

	    If SR0->R0_DPROPIN > 0
	    	nDiaAlim := SR0->R0_DPROPIN
	    	lPropin	 := .T.
    	ElseIf !Empty(aDiasMes)	//Se existir dados no array aDiasMes, utiliza estes dados para montar dias uteis e nao uteis de vale transporte (analitico)
			For nX := 1 to Len(aDiasMes)
				If aDiasMes[nX,2] == "1" .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
					nDiaUtil += 1
				ElseIf aDiasMes[nX,2] <> "1" .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
					nDiaNUtil += 1
				EndIf
			Next nX
		Else				//do contrario utiliza cadastro de periodo - RCF_DUTILT e RCF_DNUTIL (sintetico)
			If aPergunte[2,3] <> 2 .And. DiasTrab < nDiasPer
				nDiaUtil  := Int( Round( aPeriodo[nPosSem,21] * ( ( DiasTrab - ( nDiasMat + nDPrgSalMa ) ) / nDiasPer ), 2 ) )
				nDiaNUtil := 0
			Else
				nDiaUtil  := aPeriodo[nPosSem,21]
				nDiaNUtil := 0
			EndIf
		EndIf

    	If nDiasFix > 0
    		nDiaCal := iif(!empty(nDiaAlim) .and. lPropin, nDiaAlim,nDiasFix) // Quando o nDiaAlim estiver preenchido ้ mandat๓rio, pois deve ser considerado primeiro que o nDiasFix
    	Else
    		If nDiaAlim > 0
    			nDiaInf := nDUtil
    			nDiaCal := Max( ( nDiaAlim - nFaltas ) * nDiaInf - aPergunte[5,3], 0 ) //Parametro MV_PAR05 - Deducoes
    		Else
    			nDiaAlim  := nDiaUtil + nDiaNUtil
	    		nDiaCal   := ( (( nDiaUtil * nDUtil ) - aPergunte[5,3]) + (nDiaNUtil * nDNUtil) ) - (nFaltas * nDUtil)
	    	EndIf
    	EndIf

    	nValAli 	:= nDiaCal * nValUni

	    //Inicio do trecho para recalculo dos beneficios gerados pela integra็ใo com servi็os
	    cKeyAux := SR0->(R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO)

		//Recalcula ๚ltimo VA pago por crit้rios definidos na intergra็ใo com servi็os para verificar se deve ser feito algum desconto ou acr้scimo
		If lPriVez .and. ( fIntRHGS() .AND. SuperGetMv("MV_BENEXGS",,.T.) )
			fRetBenAnt(SRA->RA_FILIAL, SRA->RA_MAT, SRA->RA_PROCES, "2", @aPerAux)
			If Len(aPerAux) > 0
				nTotDif := fApurPAnt(aPerAux[1], "2")[1]
			EndIf
			lPriVez := .F.
		EndIf

		nValOri := nValAli	// Guarda o valor calculado originalmente

		If nTotDif != 0
			If nTotDif > 0 //Se houver diferen็a positiva para pagar em rela็ใo ao m๊s anterior, soma no valor total do VA
				nValALI += nTotDif
				nTotDif := 0
			ElseIf ( nValALI + nTotDif ) >= 0 //Se houver diferen็a negativa, debita do total at้ o minimo de zero
				nValALI += nTotDif
				nTotDif := 0
			Else
				nTotDif += nValALI
				nValALI := 0
			EndIf
		EndIf

		//Fim do trecho para recalculo dos beneficios gerados pela integra็ใo com servi็os

		nCustFunc := Round(( nValAli * nPerc ) / 100, 2)
		If nTeto > 0
			nCustFunc   := Min(nCustFunc, nTeto)
		EndIf
		nCustEmp := nValAli - nCustFunc

		aAdd(aVACalc, {cKeyAux, nDiaCal, nValUni, nValAli,  nCustFunc, nCustEmp, nDiaAlim, lPropin, 0, nValOri,,,,,,,,,nDiaUtil,nDiaNUtil} )

		SR0->(DbSkip())

	EndDo

	If nTotDif < 0 //Grava o saldo negativo no ๚ltimo vale calculado
		aVACalc[Len(aVACalc), 9] := nTotDif
	EndIf

EndIf

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfVALCalcN 	   บAutor  ณLeandro Drumond  บ Data ณ  04/08/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณEfetua o calculo do vale alimentacao   					      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fVALCalcN(aVACalc)
Local aArea			:= GetArea()
Local aDias 		:= {}
Local aPerAux 		:= {}
Local cFilMat   	:= SRA->RA_FILIAL + SRA->RA_MAT
Local cKeyAux		:= ""
Local lPropin		:= .F.
Local nCustFunc 	:= 0
Local nCustEmp		:= 0
Local nValAli		:= 0
Local nDiaInf		:= 0
Local nValUni		:= 0
Local nPerc			:= 0
Local nTeto			:= 0
Local nDiasFix		:= 0
Local nDiaAlim		:= 0
Local nDiaCal		:= 0
Local nTotDif 		:= 0
Local nValOri		:= 0
Local nX			:= 0
Local lPeriodico	:= aPergunte[6,3] == 1//Avulso/Complemnto/Semanal/diario
Local nPos			:= 0
Local nDiasProp 	:= 0
Local nDiasSeg 		:= 0									//-- Qtde Segundas trabalhadas
Local nDiasTer 		:= 0									//-- Qtde Tercas trabalhadas
Local nDiasQua 		:= 0									//-- Qtde Quartas trabalhadas
Local nDiasQui 		:= 0									//-- Qtde Quintas trabalhadas
Local nDiasSex 		:= 0									//-- Qtde Sextas trabalhadas
Local nDiasSab 		:= 0									//-- Qtde Sabados trabalhadas
Local nDiasDom 		:= 0									//-- Qtde Domingos trabalhadas
Local nDiasPonto 	:= 0
Local nDiaUtil		:= 0
Local nDiaNUtil		:= 0
Local nDUtil		:= 0
Local nDNUtil		:= 0
Local nAuxUtil		:= 0
Local nAuxNUtil		:= 0
Local nFalAux		:= 0
Local lDias			:= .F.
Local lFeriado 		:= .F.
Local lPropFerias	:= APERGUNTE[3,3] == 1
Local aTabCalend	:= If (!lUsaCPer,fCarPonto(),{})
Local lSP7			:= .F.
Local dDt1			:= ctod("")
Local dDt2			:= ctod("")
Local cCodBen		:= ""
Local nDiasPer		:= If(GetMvRH("MV_DIASPER",,"1") == "1",nDiasP,(If(SRA->RA_TIPOPGT =="M", P_QTDIAMES, P_NTOTDIAS)))

DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo

If !lUsaCPer .And. Len(aTabCalend) > 1
	SPA->( dbSeek( xFilial("SPA", SRA->RA_FILIAL ) + SRA->RA_REGRA ) )
	lFeriado := ( SPA->PA_FERIADO == "S" .Or. ( SPA->( Type("PA_RECBENF") ) != "U" .And. SPA->PA_RECBENF == "1" ) )
EndIf

If __lMemCalc
	fAddMemLog("Calendario utilizado(MV_USACPER) : " +If(lUsaCPer,"FOLHA","PONTO") ,1,1)
	fAddMemLog("Considera feriado cadastro de Regra(SPA) : " + If(lFeriado,"Sim","Nใo") ,1,1)
	If !lUsaCPer
		fAddMemLog("Tabela padrใo do ponto carregada : " + If(Len(aTabCalend) > 1 ,"Sim","Nใo") ,1,1)
	EndIf
	fAddMemLog("Proporcional a f้rias : " + If(lPropFerias,"Sim","Nใo") ,1,1)
	fAddMemLog("Calculo peri๓dico : " + If(lPeriodico,"Sim","Nใo") ,1,1)
	fAddMemLog("Dias perํodo : " + cvaltochar(nDiasPer) ,1,1)
EndIf

aVACalc	  := {}

DbSelectArea("RFO")
DbSetOrder(RetOrder("RFO","RFO_FILIAL+RFO_TPVALE+RFO_CODIGO"))

DbSelectArea("SM7")
SM7->(DbSetOrder(RetOrder("SM7", "M7_FILIAL+M7_MAT+M7_TPVALE+M7_CODIGO")))

If SM7->( DbSeek(cFilMat + "2") )

	While SM7->(!Eof() .and. M7_FILIAL + M7_MAT + M7_TPVALE == cFilMat + "2")

		nFalAux := 0

		If lPeriodico .And. aPergunte[10,3] == 1 .And. SM7->M7_COMPL <> '1'
			SM7->(dbSkip())
			Loop
		EndIf

		If SM7->M7_DPROPIN == 99 //Se diasprop for 99 nใo calcula meio de transporte
			SM7->(DbSkip())
			Loop
		EndIf

		lDias :=  SM7->M7_VALSEG > 0 .Or. SM7->M7_VALTER  > 0 .Or. SM7->M7_VALQUA  > 0 .Or.  SM7->M7_VALQUI  > 0 .Or.  SM7->M7_VALSEX  > 0 .Or.  SM7->M7_VALSAB  > 0 .Or.  SM7->M7_VALDOM >0
		aDiaVales := {SM7->M7_VALSEG , SM7->M7_VALTER , SM7->M7_VALQUA , SM7->M7_VALQUI , SM7->M7_VALSEX , SM7->M7_VALSAB , SM7->M7_VALDOM}
		nDiasSeg := 0									//-- Qtde Segundas trabalhadas
		nDiasTer := 0									//-- Qtde Tercas trabalhadas
		nDiasQua := 0									//-- Qtde Quartas trabalhadas
		nDiasQui := 0									//-- Qtde Quintas trabalhadas
		nDiasSex := 0									//-- Qtde Sextas trabalhadas
		nDiasSab := 0									//-- Qtde Sabados trabalhadas
		nDiasDom := 0
		nDiaUtil := 0
		nDiaNUtil := 0

		nDUtil	 := SM7->M7_QDIAINF
		nDNUtil	 := SM7->M7_QDNUTIL
		nDiasProp:= SM7->M7_DPROPIN
		nValUni  := 0
		nPerc	 := 0
		nTeto	 := 0
		nDiasFix := 0
		nDiaAlim := 0
		lPropin	 := .F.

		If RFO->(DbSeek(xFilial("RFO")+SM7->(M7_TPVALE + M7_CODIGO)))
			nValUni  := RFO->RFO_VALOR
			nPerc	 := RFO->RFO_PERC
			nTeto	 := RFO->RFO_TETO
			nDiasFix := RFO->RFO_DIAFIX
		EndIf

		If !lUsaCPer .And. SM7->M7_VALFIX > 0
			nDiasFix := SM7->M7_VALFIX
		EndIf

		If lPeriodico //complemento
			nDiasFix	:= 0
			nDiaInf	  := SM7->M7_QDIAINF
			If aPergunte[7,3] > 0
				nDiaInf	:= aPergunte[7,3]
			EndIf
			aPergunte[5,3] 	:= 0 //vales a deduzir
		EndIf
		If __lMemCalc
			fAddMemLog("Benefํcio : " + SM7->M7_CODIGO ,1,1)
			fAddMemLog("Benefํcio avulso : " +If(SM7->M7_COMPL=="1","SIM","NรO") ,1,1)

			fAddMemLog("Dias da semana preenchidos : " +If(lDias,"SIM","NรO") ,1,1)
			fAddMemLog("Dias fixos : " + cvaltochar(nDiasFix) ,1,1)
			fAddMemLog("Quantidade de vales dias ๚teis : " + cvaltochar(nDUtil) ,1,1)
			fAddMemLog("Quantidade de vales dias nใo ๚teis : " + cvaltochar(nDNUtil) ,1,1)
			fAddMemLog("Quantidade de vales proporcionais : " + cvaltochar(nDiasProp) ,1,1)
			If lPeriodico
				fAddMemLog("Vales por dia informados : " + cvaltochar(nDiaInf) ,1,1)
			EndIf
			fAddMemLog("vales a deduzir informado : " + cvaltochar(aPergunte[5,3]) ,1,1)
			fAddMemLog("Faltas  : " + cvaltochar(nFaltas) ,1,1)
			fAddMemLog("Valor Unitแrio  : " + Transform(nValUni,"@E 999,999,999.99") ,1,1)
		EndIf

		If nDiasProp > 0
			nDiaAlim := nDiasProp
			lPropin	 := .T.
			nDiaCal := Max( ( nDiaAlim - nFaltas ) * If(lPeriodico, nDiaInf - aPergunte[5,3], nDUtil), 0 ) //Parametro MV_PAR05 - Deducoes
		Else
			If nDiasFix > 0
				nDiaAlim  := nDiasFix
				nDiasProp := nDiasFix
				nDiaCal   := nDiaAlim - aPergunte[5,3] //Parametro MV_PAR05 - Deducoes
			Else
				If !lUsaCPer .And. Len(aTabCalend) > 1 .And. lDias

					GetDiasTab(aTabCalend,aDiaVales,lPropFerias,lFeriado,.F.,nDUtil,nDNUtil,@nDiasSeg,@nDiasTer,@nDiasQua,@nDiasQui,@nDiasSex,@nDiasSab,@nDiasDom,@nDiaUtil,@nDiaNUtil)
					
					//-- Calculo dos Dias Uteis     ( Qtde de cada dia semana * No vales de cada dia da semana ) - deducao
					nDiaAlim := nDiasSeg + nDiasTer + nDiasQua + nDiasQui + nDiasSex  + nDiasSab + nDiasDom
									
					nDiasPonto	:= ( ( nDiasSeg * SM7->M7_VALSEG) + ( nDiasTer * SM7->M7_VALTER ) + ( nDiasQua *  SM7->M7_VALQUA ) + ( nDiasQui * SM7->M7_VALQUI ) + ( nDiasSex * SM7->M7_VALSEX ) + ( nDiasSab * SM7->M7_VALSAB ) + ( nDiasDom * SM7->M7_VALDOM ) )
					nDiaCal := nDiasPonto - aPergunte[5,3] //Parametro MV_PAR05 - Deducoes

					If nFaltas > 0 .and. Len(aFaltas) > 0
						If SM7->M7_VALDOM > 0
							nFalAux += aFaltas[1]
						EndIf
						If SM7->M7_VALSEG > 0
							nFalAux += aFaltas[2]
						EndIf
						If SM7->M7_VALTER > 0
							nFalAux += aFaltas[3]
						EndIf
						If SM7->M7_VALQUA > 0
							nFalAux += aFaltas[4]
						EndIf
						If SM7->M7_VALQUI > 0
							nFalAux += aFaltas[5]
						EndIf
						If SM7->M7_VALSEX > 0
							nFalAux += aFaltas[6]
						EndIf
						If SM7->M7_VALSAB > 0
							nFalAux += aFaltas[7]
						EndIf
						nDiaCal -= nFalAux
					EndIf
				Else
					If !Empty(aDiasMes) //Se existir dados no array aDiasMes, utiliza estes dados para montar dias uteis e nao uteis de vale transporte
						If __lMemCalc
							fAddMemLog("Calendario ANALอTICO " ,1,1)
						EndIf
						nDiaAlim  := 0
						If lPeriodico
							nPos := aScan(aDiasMes,{|x| x[1] == aPergunte[8,3]})
						EndIf
						If nPos == 0
							nPos := 1
						EndIf
						For nX := nPos to Len(aDiasMes)
							If aDiasMes[nX,2] == "1" .and. (Empty(dDataDem) .OR. aDiasMes[nX,1] < dDataDem) .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
								nDiaUtil += 1
							ElseIf aDiasMes[nX,2] <> "1" .and. (Empty(dDataDem) .OR. aDiasMes[nX,1] < dDataDem) .and. aDiasMes[nX,3] == "1" .and. aDiasMes[nX,4]
								nDiaNUtil += 1
							EndIf
							If lPeriodico .And. aDiasMes[nX][1]==aPergunte[9,3]
								exit
							EndIf
						Next nX
					Else //do contrario utiliza cadastro de periodo - RCF_DALIM
						If __lMemCalc
							fAddMemLog("Calendario SINTษTICO " ,1,1)
						EndIf
						If lPeriodico
							nDiaAlim  := DateDiffDay( aPergunte[8,3],aPergunte[9,3] ) + 1
						ElseIf aPergunte[2,3] <> 2 .And. DiasTrab < nDiasPer
							nDiaUtil  := Int( Round( aPeriodo[nPosSem,21] * ( ( DiasTrab - ( nDiasMat + nDPrgSalMa ) ) / nDiasPer ), 2 ) )
							nDiaNUtil := 0
						Else
							nDiaUtil  := aPeriodo[nPosSem,21]
							nDiaNUtil := 0
						EndIf
						nDiasRef := nDiaUtil
					EndIf
					If __lMemCalc
						fAddMemLog("Dias ๚teis : " +  cvaltochar(nDiaUtil) ,1,1)
						fAddMemLog("Dias nใo ๚teis : " +  cvaltochar(nDiaNUtil) ,1,1)
					EndIf
					If nDiaAlim > 0
		    			nDiaInf := If(lPeriodico .and. nDiaInf > 0, nDiaInf, nDUtil)
		    			nDiaCal := Max( ( nDiaAlim - nFaltas ) * nDiaInf - aPergunte[5,3], 0 ) //Parametro MV_PAR05 - Deducoes
		    		Else
			    		nAuxUtil  := ( nDiaUtil * If(lPeriodico .and. nDiaInf > 0, nDiaInf, nDUtil) ) - aPergunte[5,3]
			    		nAuxNUtil := nDiaNUtil * If(lPeriodico .and. nDiaInf > 0, nDiaInf, nDNUtil)
			    		nDiaCal   := ( nAuxUtil + nAuxNUtil ) - (nFaltas * nDUtil)
			    	EndIf
				EndIf
			EndIf
		EndIf

		If !lSP7 .And. nFaltas > 0 .And. nFaltas >= nDiaAlim
			If lPeriodico
				dDt1	:= aPergunte[8,3]
				dDt2	:= aPergunte[9,3]
			Else
				dDt1	:= aPeriodo[nPosSem,3]
				dDt2	:= aPeriodo[nPosSem,4]
			EndIf
			GravaSP7(nDiaAlim,dDt1,dDt2)
			lSP7 := .T.
		EndIf

		nValAli 	:= nDiaCal * nValUni
		If __lMemCalc
			fAddMemLog("Dias de calculo : " +  cvaltochar(nDiaCal) ,1,1)
			fAddMemLog("Valor calculo : " +  Transform(nValAli,"@E 999,999,999.99") ,1,1)
		EndIf
		//Inicio do trecho para recalculo dos beneficios gerados pela integra็ใo com servi็os

		//Recalcula ๚ltimo VA pago por crit้rios definidos na intergra็ใo com servi็os para verificar se deve ser feito algum desconto ou acr้scimo
		If fIntRHGS() .AND. SuperGetMv("MV_BENEXGS",,.T.)
			fRetBenAnt(SRA->RA_FILIAL, SRA->RA_MAT, SRA->RA_PROCES, "2", @aPerAux)
			If Len(aPerAux) > 0
				nTotDif := fApurPAnt(aPerAux[1], "2", Nil, SM7->M7_CODIGO)[1]
			EndIf
			If __lMemCalc
				fAddMemLog("Integra็ใo com servi็os ATIVADA "  ,1,1)
				fAddMemLog("Valor diferen็a m๊s anterior : " +  Transform(nTotDif,"@E 999,999,999.99") ,1,1)
			EndIf
		EndIf

		nValOri := nValAli	// Guarda o valor calculado originalmente

		If nTotDif != 0
			If nTotDif > 0 //Se houver diferen็a positiva para pagar em rela็ใo ao m๊s anterior, soma no valor total do VA
				nValALI += nTotDif
				nTotDif := 0
			ElseIf ( nValALI + nTotDif ) >= 0 //Se houver diferen็a negativa, debita do total at้ o minimo de zero
				nValALI += nTotDif
				nTotDif := 0
			Else
				nTotDif += nValALI
				nValALI := 0
			EndIf
		EndIf
		//Fim do trecho para recalculo dos beneficios gerados pela integra็ใo com servi็os

		nCustFunc   := Round(( nValAli * nPerc ) / 100, 2)
		If nTeto > 0
			nCustFunc   := Min(nCustFunc, nTeto)
		EndIf
		nCustEmp	:= nValAli - nCustFunc
		If __lMemCalc
			fAddMemLog("Percentual desconto funcionแrio : " +  cvaltochar(nPerc) + " %" ,1,1)
			fAddMemLog("Teto  : " +  Transform(nTeto,"@E 999,999,999.99") ,1,1)
			fAddMemLog("Custo funcionแrio : " +  Transform(nCustFunc,"@E 999,999,999.99") ,1,1)
			fAddMemLog("Custo empresa : " +  Transform(nCustEmp,"@E 999,999,999.99") ,1,1)
		EndIf
		cKeyAux := SM7->(M7_FILIAL + M7_MAT + M7_TPVALE + M7_CODIGO)

		aAdd(aVACalc , {cKeyAux, nDiaCal, nValUni, nValAli, nCustFunc, nCustEmp, nDiasRef, lPropin, SM7->M7_CODIGO, nDiasProp, nDUtil, nDNUtil, SM7->M7_TPCALC, SM7->M7_TPVALE, aDiaVales, SM7->M7_VALFIX, 0, nValOri,nDiaUtil,nDiaNUtil,If(Len(aFaltas)>0,nFalAux,nFaltas),""} )

		If SM7->M7_COMPL == "1"
			RecLock("SM7",.F.)
			SM7->(dbDelete())
			SM7->(msUnlock())
		EndIf

		SM7->(DbSkip())

	EndDo

	If nTotDif < 0 //Grava o saldo negativo no ๚ltimo vale calculado
		aVACalc[Len(aVACalc),17] := nTotDif
	EndIf

Else // Nใo Encontrou Registros na SM7, mas deve recalcular o m๊s anterior

	If fIntRHGS() .AND. SuperGetMv("MV_BENEXGS",,.T.) // Apenas integra็ใo com servi็os
		fRetBenAnt(SRA->RA_FILIAL, SRA->RA_MAT, SRA->RA_PROCES, "2", @aPerAux)
		If Len(aPerAux) > 0
			aDias := fApurPAnt(aPerAux[1], "2")
			nTotDif := aDias[1]
			cCodBen := aDias[2]
		EndIf
	EndIf

	nValOri := nValAli	// Guarda o valor calculado originalmente

	If nTotDif != 0

		//Grava na SM7
		AtuSM7Aut("2", cCodBen)

		If nTotDif > 0 //Se houver diferen็a positiva para pagar em rela็ใo ao m๊s anterior, soma no valor total do VA
			nValALI += nTotDif
			nTotDif := 0
		ElseIf ( nValALI + nTotDif ) >= 0 //Se houver diferen็a negativa, debita do total at้ o minimo de zero
			nValALI += nTotDif
			nTotDif := 0
		Else
			nTotDif += nValALI
			nValALI := 0
		EndIf

		cKeyAux := SRA->(RA_FILIAL + RA_MAT + "2" + cCodBen)

		aAdd(aVACalc, {cKeyAux, 0, nValUni, nValAli, nCustFunc, nCustEmp, nDiaAlim, lPropin, cCodBen, nDiasProp, nDUtil, nDNUtil, "2", "2", {0, 0, 0, 0, 0, 0, 0}, 0, nTotDif, nValOri,0,0,nFaltas,"" })
	EndIf

EndIf

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfValGrv 	   บAutor  ณLeandro Drumond  บ Data ณ  03/08/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณGravacao do calculo do vale refeicao/alimentacao		      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fValGrv(aVLCalc)
Local aArea		:= GetArea()
Local cKeyAux	:= ""
Local nX		:= 0
Local nFalVal   := nFaltas

DEFAULT aVLCalc := {}

DbSelectArea("SR0")
DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO"))

For nX := 1 to Len(aVLCalc)
	cKeyAux := aVLCalc[nX,1]
	DbSeek(cKeyAux)
	If Reclock( "SR0" , .F. )
		If aVLCalc[nX,8] .OR. ( aVLCalc[nX,7] <> IIf( SR0->R0_TPVALE == "1", aPeriodo[nPosSem,12] , aPeriodo[nPosSem,21] ) )
			SR0->R0_DIASPRO := aVLCalc[nX,7]							//Grava apenas se utilizou o campo R0_DPROPIN -OU-
																		//Se dias calculados diferente da quantidade de dias do mes.
		Else
			SR0->R0_DIASPRO := 0
		EndIf

		If aVLCalc[nX,19] > 0 .or. aVLCalc[nX,20] > 0
			SR0->R0_DUTILM	:= aVLCalc[nX,19]
			SR0->R0_DNUTIM	:= aVLCalc[nX,20]
		Else
			SR0->R0_DUTILM	:= aPeriodo[nPosSem,10]							//Dias Uteis no M๊s - campo RCF_DUTILT
			SR0->R0_DNUTIM	:= aPeriodo[nPosSem,9]							//Dias Nใo Uteis no M๊s - campo RCF_DNUTIL
		EndIf
		SR0->R0_SALBASE	:= SRA->RA_SALARIO								//Salแrio Base
		SR0->R0_CC		:= SRA->RA_CC									//Centro de Custo do Funcionแrio
		SR0->R0_FALTAS	:= If(Empty(nFaltasAut), nFalVal, nFaltasAut)	//Dias correspondentes เ faltas (mnem๔nico NFALTAS)
		SR0->R0_FERIAS 	:= nFerVen										//Dias correspondentes a f้rias (mnem๔nico NFERVEN)
		SR0->R0_AFAST 	:= nDiasAfas									//Dias correspondentes a afastamentos (mnem๔nico NDIASAFAS)

		SR0->R0_QDIACAL	:= aVLCalc[nX,2]								//Dias calculados
		SR0->R0_VALCAL 	:= aVLCalc[nX,4]								//Valor Vale calculado
		SR0->R0_VLRVALE	:= aVLCalc[nX,3]								//Custo Unitแrio
		SR0->R0_VLRFUNC	:= aVLCalc[nX,5]								//Custo Funcionแrio
		SR0->R0_VLREMP 	:= aVLCalc[nX,6]								//Custo Empresa
		If SR0->(ColumnPos("R0_SLDANT")) > 0
			SR0->R0_SLDANT  := aVLCalc[nX,9]							//Saldo negativo
			SR0->R0_VALORI	:= aVLCalc[nX,10]							//Valor Caculado originalmente
		EndIf

		SR0->( MsUnLock() )

		nFalVal := 0
	EndIf

Next nX

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfValGrvN 	   บAutor  ณLeandro Drumond  บ Data ณ  03/08/2013 บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณGravacao do calculo do vale refeicao/alimentacao		      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณ Roteiro de Calculo                                           บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fValGrvN(aVLCalc)

	Local aArea		:= GetArea()
	Local lNovo		:= .T.
	Local nX		:= 0
	Local nPos 		:= 0
	Local lPeriodico:= aPergunte[6,3] == 1//Avulso/Complemnto/Semanal/diario
	Local cNumPed   := ""
	Local cCodCCT   := ""
	Local lGpem040  := FunName() == "GPEM040" .or. FunName() == "GPEM040B"

	DEFAULT aVLCalc := {}
	DEFAULT lTemCCT	  := SR0->(ColumnPos("R0_CODCCT")) > 0

If lGpem040 //Se for rescisใo, inclui valores calculados no array para desconto na rescisใo

	aBenefRes[If(cRot == "VRF",2,3)] := aClone(aVLCalc)

Else

	If lTemCCT .and. !Empty(SRA->RA_SINDICA)
		cCodCCT := fBuscaCCT(SRA->RA_SINDICA)
	EndIf

	DbSelectArea("SM7")
	SM7->(DbSetOrder(RetOrder("SM7","M7_FILIAL+M7_MAT+M7_TPVALE+M7_CODIGO")))

	DbSelectArea("SR0")
	SR0->(DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO+R0_PEDIDO")))

	// Exclui cแlculo anterior para o mesmo perํodo, caso exista
	cKey := SRA->RA_FILIAL + SRA->RA_MAT + If(cRot == "VAL", "2", If(cRot == "VRF", "1", "0"))
	If SR0->(dbSeek(cKey))
		While SR0->(R0_FILIAL + R0_MAT + R0_TPVALE) == cKey
			//Somente deleta o cแlculo se for recแlculo. Caso o pedido jแ tenha sido emitido, nใo deleta.
			If (SR0->R0_DTREFI == aPeriodo[nPosSem, 3] .And. SR0->R0_DTREFF == aPeriodo[nPosSem, 4] .And. SR0->R0_PEDIDO != "2") ;
				.Or. (Empty(SR0->R0_DTREFI) .And. Empty(SR0->R0_DTREFF) .And. SR0->R0_PEDIDO != "2")
				//Se jแ existir um cแlculo em aberto, quer dizer que ้ recแlculo, entใo armazena o numero do pedido jแ utilizado.
				If ( nPos := aScan(aVLCalc, {|x| x[1] == SR0->R0_FILIAL+SR0->R0_MAT+SR0->R0_TPVALE+SR0->R0_CODIGO}) ) > 0
					aVLCalc[nPos,22] := SR0->R0_NROPED
				EndIf
				Reclock("SR0", .F.)
				SR0->(dbDelete())
				SR0->(MsUnlock())
			EndIf
			SR0->(dbSkip())
		EndDo
		SR0->(dbGoTop())
	EndIf
	For nX := 1 to Len(aVLCalc)
		SM7->(DbSeek(SRA->RA_FILIAL + SRA->RA_MAT + aVLCalc[nX, 14] + aVLCalc[nX, 9]))
		lNovo 	  := .T.
		cNumPed   := ""
		If SR0->(DbSeek(aVLCalc[nX, 1] + "1"))
			While SR0->( !EoF() ) .And. SR0->R0_FILIAL+SR0->R0_MAT+SR0->R0_TPVALE+SR0->R0_CODIGO+SR0->R0_PEDIDO == aVLCalc[nX, 1] + "1"
				If SR0->R0_NROPGT == aPeriodo[nPosSem, 2] 
					lNovo := .F.
					Exit
				EndIf
				SR0->( dbSkip() )
			EndDo
		EndIf		
		If ! lNovo .And. ! Empty(SR0->R0_NROPED)
			cNroPed := SR0->R0_NROPED
		ElseIf lNovo
			cNumPed := aVLCalc[nX,22]
		EndIf
		If Reclock( "SR0", lNovo)
			If !Empty(aVLCalc[nX, 10]) .And. (aVLCalc[nX,8] .OR. ( aVLCalc[nX,10] <> IIf( aVLCalc[nX, 14] == "1", aPeriodo[nPosSem,12] , aPeriodo[nPosSem,21] ) ))
				//Grava apenas se utilizou o campo R0_DPROPIN -OU-
				//Se dias calculados diferente da quantidade de dias do mes.
				SR0->R0_DPROPIN	:= aVLCalc[nX, 10]
				SR0->R0_DIASPRO := aVLCalc[nX, 10]
			Else
				SR0->R0_DPROPIN := 0
				If aVLCalc[nX, 21] >= 0
					SR0->R0_DIASPRO := If(aVLCalc[nX, 2] < aVLCalc[nX, 7], aVLCalc[nX, 2], aVLCalc[nX, 7])
				Else
					// Se houver devolu็ใo de Faltas soma a devolu็ใo aos dias proporcionais
					SR0->R0_DIASPRO := If(aVLCalc[nX, 2] < aVLCalc[nX, 7], aVLCalc[nX, 2] - aVLCalc[nX, 21], aVLCalc[nX, 7] - aVLCalc[nX, 21])
				EndIf
			EndIf
			SR0->R0_FILIAL	:= SRA->RA_FILIAL
			SR0->R0_MAT		:= SRA->RA_MAT
			SR0->R0_TPVALE	:= aVLCalc[nX, 14]
			SR0->R0_CODIGO	:= aVLCalc[nX, 9]
			SR0->R0_QDIAINF	:= aVLCalc[nX, 11]
			SR0->R0_QDNUTIL	:= aVLCalc[nX, 12]
			SR0->R0_TPCALC	:= aVLCalc[nX, 13]
			SR0->R0_ROTEIR	:= cRot
			SR0->R0_PERIOD 	:= cPeriodo
			SR0->R0_NROPGT	:= cNumPag
			SR0->R0_ANOMES	:= SubSTr(cPeriodo,5,2) + SubSTr(cPeriodo,1,4)
			SR0->R0_PEDIDO	:= "1"
			SR0->R0_QVALSEG  := aVLCalc[nX, 15, 1]
			SR0->R0_QVALTER  := aVLCalc[nX, 15, 2]
			SR0->R0_QVALQUA  := aVLCalc[nX, 15, 3]
			SR0->R0_QVALQUI  := aVLCalc[nX, 15, 4]
			SR0->R0_QVALSEX  := aVLCalc[nX, 15, 5]
			SR0->R0_QVALSAB  := aVLCalc[nX, 15, 6]
			SR0->R0_QVALDOM  := aVLCalc[nX, 15, 7]
			SR0->R0_QVALFIX  := aVLCalc[nX, 16]
			If lPeriodico
				SR0->R0_DTREFI	:= aPergunte[8, 3]
				SR0->R0_DTREFF	:= aPergunte[9, 3]
			Else
				SR0->R0_DTREFI	:= aPeriodo[nPosSem, 3]
				SR0->R0_DTREFF	:= aPeriodo[nPosSem, 4]
			EndIf

			If lNovo
				//Caso nใo exista pedido, pode ser novo m๊s ou mais de 1 pedido no m๊s, entใo assume o novo n๚mero do pedido.
				If cNumPed <> ""
					cNroPed := cNumPed
				EndIf
				SR0->R0_NROPED	:= GetNroPed()
			EndIf
			SR0->R0_TPBEN	:= BuscaTBen(SR0->R0_TPVALE, SR0->R0_CODIGO, SR0->R0_FILIAL)

			If aVLCalc[nX,19] > 0 .or. aVLCalc[nX,20] > 0
				SR0->R0_DUTILM	:= aVLCalc[nX,19]
				SR0->R0_DNUTIM	:= aVLCalc[nX,20]
			Else
				SR0->R0_DUTILM	:= aPeriodo[nPosSem,10]							//Dias Uteis no M๊s - campo RCF_DUTILT
				SR0->R0_DNUTIM	:= aPeriodo[nPosSem,9]							//Dias Nใo Uteis no M๊s - campo RCF_DNUTIL
			EndIf

			SR0->R0_SALBASE	:= SRA->RA_SALARIO								//Salแrio Base
			SR0->R0_CC		:= SRA->RA_CC									//Centro de Custo do Funcionแrio
			SR0->R0_FALTAS	:= If(Len(aVlCalc[nX]) >= 21, aVlCalc[nX,21], (If(Empty(nFaltasAut), If(SM7->M7_QDIAINF > 0, nFaltas, 0), nFaltasAut)))	//Dias correspondentes เ faltas (mnem๔nico NFALTAS) - S๓ desconta faltas se houvere pagamento de dias ๚teis.
			SR0->R0_FERIAS	:= nFerVen										//Dias correspondentes a f้rias (mnem๔nico NFERVEN)
			SR0->R0_AFAST	:= nDiasAfas									//Dias correspondentes a afastamentos (mnem๔nico NDIASAFAS)

			SR0->R0_QDIACAL	:= aVLCalc[nX, 2]								//Dias calculados
			SR0->R0_VALCAL 	:= aVLCalc[nX, 4]								//Valor Vale calculado
			SR0->R0_VLRVALE	:= aVLCalc[nX, 3]								//Custo Unitแrio
			SR0->R0_VLRFUNC	:= aVLCalc[nX, 5]								//Custo Funcionแrio
			SR0->R0_VLREMP 	:= aVLCalc[nX, 6]								//Custo Empresa

			If SR0->(ColumnPos("R0_SLDANT")) > 0
				SR0->R0_SLDANT  := aVLCalc[nX, 17]							//Saldo negativo
				SR0->R0_VALORI	:= aVLCalc[nX, 18]							//Valor Caculado originalmente
			EndIf

			If lTemCCT
				SR0->R0_CODCCT 	:= cCodCCT	//Conven็ใo Coletiva de Trabalho
			EndIf

			SR0->( MsUnLock() )
		EndIf

		If cNumPed <> ""
			cNroPed := "" //Zera static para nใo usar o mesmo n๚mero para outro funcionแrio causando chave duplicada.
		EndIf

	Next nX

	If Len(aVLCalc) > 0
		CustoVales( aVLCalc[1, 14])
	EndIf

EndIf

// Zera Variแveis Static
RestXCBEN()

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfCargaRI1      บAutor  ณLeandro Drumond  บ Data ณ 21/02/2014  บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCarrega dados dos beneficios do funcionario.                  บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณRoteiros de Calculo			                                  บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fCargaRI1(aBenRI1)
Local aArea		:= GetArea()
Local lRet		:= .F.
Local lRetAut	:= .F.

lRetAut := fCrgBenAut(.F.) //Verifica se existem crit้rios automแticos para o funcionแrio

DbSelectArea("RI1")
DbSetOrder(1)

lRI1Afast := .F.


If DbSeek(SRA->RA_FILIAL + SRA->RA_MAT)
	If Empty(SRA->RA_DEMISSA) .or. SRA->RA_DEMISSA > dDataDe
		
		While RI1->(!Eof() .and. RI1_FILIAL + RI1_MAT == SRA->RA_FILIAL + SRA->RA_MAT )
			If RI1->( RI1_TPCALC != "2" .And. (( Empty(RI1_DINIPG) .or. RI1_DINIPG <= dDataAte ) .And. (Empty(RI1_DFIMPG) .Or. RI1_DFIMPG >= dDataDe)))
				lRet := .T.
				RI1->(aAdd(aBenRI1, {RI1_MAT, RI1_BENEF, RI1_TABELA, RI1_PD, RI1_PD1, RI1_PD2, RI1_PROPOR}))
				If RI1->RI1_PROPOR == "1"
					lRI1Afast := .T.
				EndIf
			EndIf
			RI1->(DbSkip())
		EndDo
		
	EndIf
EndIf

lRet := lRet .Or. lRetAut

If lRI1Afast .Or. lRetAut //Se existir cแlculo proporcional, carrega dias trabalhados
	fDiasTrab(@DiasTrab)
EndIf

If lRetAut .And. RIS->RIS_PROPOR == "1"
	lRI1Afast := .T.
EndIf

RestArea(aArea)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfOtbCalc       บAutor  ณLeandro Drumond  บ Data ณ 21/02/2014  บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCalcula outros beneficios.                                    บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณRoteiros de Calculo			                                  บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fOtbCalc()
Local cCodBenef	:= ""
Local cCodTab	:= ""
Local cTpRef	:= ""
Local cTpDesc	:= ""
Local cPdBen	:= ""
Local cPdFunc	:= ""
Local cPdEmp	:= ""
Local nValBen	:= 0
Local nValEmp	:= 0
Local nValFunc	:= 0
Local nRef		:= 0
Local nRefCalc	:= 0
Local nPercFunc	:= 0
Local nPercBen	:= 0
Local nPercEmp	:= 0
Local nValMin	:= 0
Local nValMax	:= 0
Local nX 		:= 0
Local nDiasProp	:= 0
Local nValAux	:= 0
Local nValRed	:= 0
Local nBkpSalMes:= SalMes
Local nDiasPer	:= If(GetMvRH("MV_DIASPER",,"1") == "1",nDiasP,(If(SRA->RA_TIPOPGT =="M", P_QTDIAMES, P_NTOTDIAS)))
Local lProp		:= .F.
Local lSalOuBen := If( Type("P_SALOUBEN") <> "U", P_SALOUBEN, .F.)

// Valida Demissใo do Funcionแrio
If ( !Empty(SRA->RA_DEMISSA) .And. MesAno(SRA->RA_DEMISSA) <= cAnoMes .And. cSitFolh == "D")
    Return()
EndIf

DbSelectArea("RIS")
DbSetOrder(1)

For nX := 1 to Len(aBenRI1)
	cCodBenef 	:= aBenRI1[nX,2]
	cCodTab		:= aBenRI1[nX,3]
	cPdBen		:= aBenRI1[nX,4]
	cPdFunc		:= aBenRI1[nX,5]
	cPdEmp		:= aBenRI1[nX,6]
	lProp		:= aBenRI1[nX,7] == "1"
	aRISCalc	:= {}
	SalMes		:= nBkpSalMes

	If Empty(cPdBen) .and. Empty(cPdFunc) .and. Empty(cPdEmp)
		Loop
	EndIf

	If RIS->(DbSeek(xFilial("RIS")+cCodBenef+cCodTab))
		While RIS->(!Eof() .and. RIS_FILIAL + RIS_TPBENE + RIS_COD == xFilial("RIS")+cCodBenef + cCodTab )
			If Empty(RIS->RIS_SALATE)
				nValAux := RIS->RIS_SALMIN * Val_SalMin
			Else
				nValAux := RIS->RIS_SALATE
			EndIf
			If SalMes <= nValAux
				cTpRef 		:= RIS_TPREF
				cTpDesc		:= RIS_TPDESC
				nRef		:= RIS_REF
				nPercFunc	:= RIS_FUNCD
				nPercBen	:= RIS_FUNCP
				nPercEmp	:= RIS_EMP
				nValMin		:= RIS_MINIMO
				nValMax		:= RIS_MAXIMO
				nValBen		:= 0
				nValEmp		:= 0
				nValFunc	:= 0
				nRefCalc	:= 0
				nDiasProp	:= 0

				If cTpRef == "1" //Valor Fixo
					nRefCalc := nRef
				ElseIf cTpRef == "2" //Percentual do Salario Base
					If !lSalOuBen
						nRefCalc := SalMes * ( nRef / 100 )
					Else
						fSalInc(@nSalario,@SalMes,@SalHora,@SalDia)
						//Se for comissionado, utiliza a mesma estrutura utilizada na f๓rmula S_NVLGOMIS()
						If SRA->RA_CATFUNC == "C"
							nTotComis := FBUSCAPD(ACODFOL[165,1] + "," + ACODFOL[166,1],,CSEMANA)
                    		nGComisPro := 0
                    		FPROPGCOMIS('2',@nGComisPro)
    	                    SalMes += MAX(SalMes,nTotComis)
    	                    SalMes := MAX(SalMes,nGComisPro)
						EndIf
						//Se for tarefeiro, utiliza a mesma estrutura utilizada na f๓rmula S_NVLGTARE()
						If SRA->RA_CATFUNC == "T"
							nGTarPro := 0
                   			FPROPGTAR('2',@nGTarPro)
                   			SalMes += MAX(SalMes,nTotTarefa)
                    		SalMes := MAX(SalMes,nGTarPro)
						EndIf
						nRefCalc := SalMes * ( nRef / 100 )
					EndIf
				ElseIf cTpRef == "3" //Percentual sobre salario minimo
					nRefCalc := Val_SalMin * ( nRef / 100 )
				EndIf

				If !Empty(nPercBen)
					nValBen := nRefCalc * ( nPercBen / 100 )
				EndIf

				If lProp
					nValBen := Round( ( nValBen / nDiasPer ) * DiasTrab, 2)
					nDiasProp := DiasTrab
				EndIf

				If !Empty(cPdFunc) .and. !Empty(nPercFunc) .And. nValBen > 0
					If cTpDesc == "1" //Percentual do Beneficio
						nValFunc := nValBen * ( nPercFunc / 100 )
					ElseIf cTpDesc == "2"
						If !lSalOuBen
							If P_lPPEAtiv .And. P_RedSalB
								If Empty(aPPE)
									fInssResEx()
									fSalRed(@nValRed, Nil, Nil, nSalMes)
								EndIf
								SalMes -= nValRed
							EndIf
							nValFunc := SalMes * ( nPercFunc / 100 )
						Else
							fSalInc(@nSalario,@SalMes,@SalHora,@SalDia)
							If P_lPPEAtiv .And. P_RedSalB
								If Empty(aPPE)
									fInssResEx()
									fSalRed(@nValRed, Nil, Nil, nSalMes)
									EndIf
								SalMes -= nValRed
							EndIf
							If SRA->RA_CATFUNC == "C"
								nTotComis := FBUSCAPD(ACODFOL[165,1] + "," + ACODFOL[166,1],,CSEMANA)
								nGComisPro := 0
								FPROPGCOMIS('2',@nGComisPro)
								SalMes += MAX(SalMes,nTotComis)
								SalMes := MAX(SalMes,nGComisPro)
							EndIf
							If SRA->RA_CATFUNC == "T"
								nGTarPro := 0
								FPROPGTAR('2',@nGTarPro)
								SalMes += MAX(SalMes,nTotTarefa)
								SalMes := MAX(SalMes,nGTarPro)
							EndIf
							nValFunc := SalMes  * ( nPercFunc / 100 )
						EndIf
					ElseIf cTpDesc == "3" //Percentual Salario Minimo
						nValFunc := Val_SalMin * ( nPercFunc / 100 )
					EndIf
					nValFunc := Round(nValFunc,2)
					If nValMin > 0
						nValFunc := Max(nValFunc,nValMin)
					EndIf
					If nValMax > 0
						nValFunc := Min(nValFunc,nValMax)
					EndIf
				EndIf

				// Quando informado 100% no campo RIS_FUNCP(%Pagto.Func) quer dizer que o pagamento
				// daquele benefํcio realizado por aquela empresa. Por exemplo, ao informar que aquela empresa
				// pagarแ 50% sobre um benefํcio no valor de R$ 60,00, entใo R$ 30,00
				// ้ que serใo utilizados para construir o desconto do funcionแrio e o desconto da empresa.
				nValEmp := If(nValBen == 0, nValBen, nValBen - nValFunc )

				aAdd(aCalcRI1,{cCodBenef, nValBen, nValFunc, nValEmp, cPdBen, cPdFunc, cPdEmp, cCodTab, nDiasProp, nRefCalc})

				Exit
			EndIf
			RIS->(DbSkip())
		EndDo
	EndIf
Next nX

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfOtbCalc       บAutor  ณLeandro Drumond  บ Data ณ 21/02/2014  บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCalcula outros beneficios.                                    บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณRoteiros de Calculo			                                  บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fGrvOtB()
Local cCodCCT 	:= ""
Local nX 		:= 0

DEFAULT lOtbCCT	  	:= RIQ->(ColumnPos("RIQ_CODCCT")) > 0

If lOtbCCT .and. !Empty(SRA->RA_SINDICA)
	cCodCCT := fBuscaCCT(SRA->RA_SINDICA)
EndIf

fDelRIQ()

DbSelectArea("RIQ")

For nX := 1 to Len(aCalcRI1)
	RecLock("RIQ",.T.)

	RIQ_FILIAL 	:= SRA->RA_FILIAL
	RIQ_MAT	   	:= SRA->RA_MAT
	RIQ_TPBENE	:= aCalcRI1[nX,1]
	RIQ_VALBEN	:= aCalcRI1[nX,2]
	RIQ_VLRFUN	:= aCalcRI1[nX,3]
	RIQ_VLREMP	:= aCalcRI1[nX,4]
	RIQ_PD		:= aCalcRI1[nX,5]
	RIQ_PD1		:= aCalcRI1[nX,6]
	RIQ_PD2		:= aCalcRI1[nX,7]
	RIQ_COD		:= aCalcRI1[nX,8]
	RIQ_DIAPRO	:= aCalcRI1[nX,9]
	RIQ_VALCAL	:= aCalcRI1[nX,10]
	RIQ_SALBAS	:= SalMes
	RIQ_SALMIN	:= Val_SalMin
	RIQ_PROCES	:= cProcesso
	RIQ_ROTEIR	:= cRot
	RIQ_PERIOD	:= cPeriodo
	RIQ_NUMPAG	:= cSemana
	RIQ_TIPO	:= "1"

	If lOtbCCT
		RIQ_CODCCT := cCodCCT 
	EndIf

	MsUnLock()
Next nX

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfDelRIQ        บAutor  ณLeandro Drumond  บ Data ณ 21/02/2014  บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณDeleta calculo anterior.	                                  บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณRoteiros de Calculo			                                  บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function fDelRIQ()

Local aArea := GetArea()
Local cQuery
Local cNameDB
Local cDelet
Local cSqlName := InitSqlName( "RIQ" )

/*
ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
ณO banco DB2 nao aceita o nome da tabela apos o comando DELETE			 ณ
ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู*/
cNameDB	:= Upper(TcGetDb())

cQuery	:= "DELETE "

cDelet	:= "RIQ.D_E_L_E_T_ = ' ' "

If ( cNameDB $ "INFORMIX" )
	cDelet := cSqlName + ".D_E_L_E_T_ = ' ' "
EndIf

If !( cNameDB $ "DB2_ORACLE_INFORMIX_POSTGRES" )
	cQuery += cSqlName
EndIf

 		/*
ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
ณO Informix precisa do nome da tabela ao inves do Alias no comando DELETEณ
ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู*/
If ( cNameDB $ "INFORMIX" )
	cQuery += " FROM " + cSqlName
Else
	cQuery += " FROM " + cSqlName + " RIQ"
	cSqlName := "RIQ"
EndIf

cQuery += " WHERE " + cSqlName + ".RIQ_FILIAL = '" + SRA->RA_FILIAL + "'"
cQuery += " AND " + cSqlName + ".RIQ_MAT = '" + SRA->RA_MAT + "'"
cQuery += " AND " + cSqlName + ".RIQ_PROCES = '" + cProcesso + "'"
cQuery += " AND " + cSqlName + ".RIQ_ROTEIR = '" + cRot + "'"
cQuery += " AND " + cSqlName + ".RIQ_PERIOD = '" + cPeriodo + "'"
cQuery += " AND " + cSqlName + ".RIQ_NUMPAG = '" + cSemana + "'"
cQuery += " AND " + cDelet

TcSqlExec( cQuery )

RestArea( aArea )

Return( .T. )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfIntegraOUT    บAutor  ณLeandro Drumond  บ Data ณ 22/02/2014  บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณIntegra outros beneficios com a folha.                        บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณRoteiros de Calculo			                                  บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fIntegraOUT(cFil, cMat, cProcBen, cProcFol, cRotFol, cPerFol, cSemFol, cRotBen, cPerBen, cSemBen)
Local aArea			:= GetArea()
Local aVerbas		:= {"RIQ->RIQ_PD","RIQ->RIQ_PD1","RIQ->RIQ_PD2"}
Local aValAux		:= {"RIQ->RIQ_VALBEN","RIQ->RIQ_VLRFUN","RIQ->RIQ_VLREMP"}
Local cKeyRIQ		:= cFil + cMat + cProcBen + cRotBen + cPerBen
Local cPdAux		:= ""
Local cPdGerado     := ""
Local cTpBenAux		:= ""
Local cCodAux		:= ""
Local cSeq			:= ""
Local lGrava		:= .T.
Local lAtualiza		:= .F.
Local nValAux		:= 0
Local nX			:= 0
Local dDataBen

DEFAULT lGPM19RGB 	:= ExistBlock("GPM19RGB")

Pergunte("GPEM015OUT",.F.) //Carrega o grupo de perguntas, a defini็ใo da sobreposi็ใo vira do conte๚do gravado, mesmo quando a integra็ใo ocorrer pelo GPEM019
nSobrepor := mv_par04

DbSelectArea("SRA")
DbSetOrder(1)
DbSeek(cFil + cMat)

DbSelectArea("RGB")
DbSetOrder(RetORder("RGB","RGB_FILIAL+RGB_PROCES+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_MAT+RGB_PD+RGB_SEQ"))

DbSelectArea("RIQ")
DbSetOrder(1)

If(DbSeek(cKeyRIQ))
	While RIQ->(!Eof() .and. RIQ_FILIAL+RIQ_MAT+RIQ_PROCES+RIQ_ROTEIR+RIQ_PERIOD == cKeyRIQ )
		If !Empty(cSemBen) .and. RIQ->RIQ_NUMPAG <> cSemBen
			RIQ->(DbSkip())
			Loop
		EndIf

		For nX := 1 to Len(aVerbas)
			cPdAux 		:= &(aVerbas[nX])
			lGrava 		:= .T.
			lAtualiza 	:= .F.
			cSeq 		:= ""
			
			If Empty(cPdAux) .or. &(aValAux[nX]) == 0
				Loop
			Else
				nValAux := &(aValAux[nX])
			EndIf

			If RGB->(DbSeek(cFil + cProcFol + cPerFol + cSemFol + cRotFol + cMat + cPdAux))
				If cTpBenAux == RIQ->RIQ_TPBENE .and. cCodAux <> RIQ->RIQ_COD
				  	If Val(RetValSRV(  cPdAux , cFil , "RV_QTDLANC" )) > 1
				  		If nSobrepor <> 1
					  		If Empty(cSeq)
					  			cSeq := "1"
					  		Else
								cSeq := Soma1(cSeq)
							EndIf
						Else
							cSeq := RGB->RGB_SEQ
						EndIf
						If RGB->(DbSeek(cFil + cProcFol + cPerFol + cSemFol + cRotFol + cMat + cPdAux+cSeq))
							RGB->( RecLock( "RGB" , .F. ) )
						Else
							RGB->( RecLock( "RGB" , .T. ) )
						Endif
					Else
						if nSobrepor == 1
				  			nValAux := RGB->RGB_VALOR
				  		Else
				  			nValAux += RGB->RGB_VALOR
				  		Endif
				  		RGB->( RecLock( "RGB" , .F. ) )
					EndIf
				ElseIf RGB->RGB_ROTORI == cRotBen .and. RGB->RGB_TIPO2 == "G" .and. cPdAux $ cPdGerado //Dois beneficios diferentes, para o mesmo funcionแrio, calculado na mesma verba
					RGB->( RecLock( "RGB" , .F. ) )
					lAtualiza := .T.
				ElseIf nSobrepor == 1
					RGB->( RecLock( "RGB" , .F. ) )
				Else
					aAdd( aLog[Len(aLog)], SRA->RA_FILIAL + "  " + SRA->RA_MAT + "-" + If(lOfusca, Replicate('*',15), SRA->RA_NOME) + "   " + cPdAux + "   " + fDesc( "SRV" , cPdAux  , "RV_DESC" ) )
					lGrava := .F.
				EndIf
			Else
				RGB->( RecLock( "RGB" , .T. ) )
			EndIf

			cTpBenAux := RIQ->RIQ_TPBENE
			dDataBen  := StoD(cPerFol+"01")

			If lGrava
				RGB->RGB_FILIAL	:= RIQ->RIQ_FILIAL
				RGB->RGB_MAT 	:= RIQ->RIQ_MAT
				RGB->RGB_CC		:= SRA->RA_CC
				RGB->RGB_PD		:= cPdAux
				RGB->RGB_TIPO1	:= RetValSRV(  cPdAux , cFil , "RV_TIPO" )
				RGB->RGB_TIPO2	:= "G"
				RGB->RGB_VALOR	:= If(lAtualiza,RGB->RGB_VALOR + nValAux, nValAux)
				RGB->RGB_PARCEL	:= 0
				RGB->RGB_ITEM 	:= SRA->RA_ITEM
				RGB->RGB_CLVL 	:= SRA->RA_CLVL
				RGB->RGB_PROCES	:= cProcFol
				RGB->RGB_PERIOD	:= cPerFol
				RGB->RGB_ROTEIR	:= cRotFol
				RGB->RGB_SEMANA	:= cSemFol
				RGB->RGB_ROTORI	:= cRotBen
				RGB->RGB_SEQ	:= cSeq
				RGB->RGB_DTREF	:= dDataBen

				If lGPM19RGB
					ExecBlock("GPM19RGB", .F. , .F., { cRotBen, RIQ->RIQ_FILIAL, RIQ->RIQ_MAT, cPerFol, cSemFol } )
				EndIf

				RGB->(MsUnLock())

				cPdGerado += cPdAux + "/"
			EndIf
		Next nX

		cCodAux	  := RIQ->RIQ_COD

		RIQ->(DbSkip())
	EndDo
EndIf

RestArea(aArea)

Return .T.

/*/{Protheus.doc} fCrgAutom
Carrega benefํcios por crit้rio
@author Flavio S Correa
@since 22/02/2014
@param cTipo, characters, "VR" - Vale refei็ใo ou "VA" - Vale alimenta็ใo
@param cTipoVale, characters, "1" - Vale Refei็ใo ou "2" - Vale Alimenta็ใo
/*/
Function fCrgAutom(cTipo, cTipoVale, lGrava)
	
	Local aArea			:= GetArea()
	Local aAreaRFO		:= RFO->(GetArea())
	Local aAloc			:= {}
	Local nI, nJ		:= 1
	Local lOk			:= .F.
	Local nCrit			:= 0
	Local nX			:= 0
	Local aDias			:= {}
	Local nDias			:= 0
	Local nPosAdias		:= 0
	Local lDesprezar	:= .F.
	Local lNovo			:= .T.
	Local lFrist		:= .T.
	Local lRepete		:= .F.
	Local cChave		:= ""
	Local cCCChave		:= ""
	Local cFilChave		:= ""
	local cDeptoChave	:= ""
	Local cPostoChave	:= ""
	Local cSindChave	:= ""
	Local cCargoChave	:= ""
	Local cCodBen		:= ""
	Local cTurnoChave	:= ""
	Local cFuncaoChave	:= ""
	Local cChaveBEN		:= ""
	Local cCCTChave		:= ""
	Local cTipoBen		:= ""
	Local cCodSLY		:= ""
	Local cEntidade		:= ""
	Local aCCTChave		:= ""
	Local aTransf		:= {}
		
	DEFAULT cTipo		:= If(cTipoRot == "D", "VR", "VA") //1-Vale Refei็ใo / 2-Vale Alimenta็ใo
	DEFAULT cTipoVale	:= If(cTipoRot == "D", "1", "2") //0-Vale Transporte / 1-Vale Refei็ใo / 2-Vale Alimenta็ใo
	DEFAULT lGrava		:= .T.
	DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo
	
	If __lMemCalc
		fAddMemLog("Periodo de Calculo : " + dtoc(dDataDe) + " a " + dtoc(dDataAte) ,1,1)
	EndIf
	
	fCarrAdias(dDataDe, dDataAte) // Carrega o array _aDias com os dias do m๊s
	
	If Empty(aCriterio) .Or. cTipo <> cTipoCri
		CargaCrit(cTipo, @aCriterio, dDataDe, dDataAte)
		cTipoCri := cTipo
	EndIf
	
	// Carrega transfer๊ncias do funcionแrio
	If fTransf(@aTransf, MesAno(dDataDe), , , , , , , , , , , SRA->RA_FILIAL, SRA->RA_MAT)
		For nI := 1 To Len(aTransf)
			If aTransf[nI][2] != SRA->RA_FILIAL		// Transfer๊ncia de Filial
				cFilChave += SubStr(aTransf[nI][2], 1, FWGetTamFilial) + "|"
			EndIf
			If aTransf[nI][3] != SRA->RA_CC			// Transfer๊ncia de Centro de Custo
				cCCChave += aTransf[nI][3] + "|"
			EndIf
			If aTransf[nI][16] != SRA->RA_DEPTO		// Transfer๊ncia de Departamento
				cDeptoChave += aTransf[nI][16] + "|"
			EndIf
			If aTransf[nI][18] != SRA->RA_POSTO		// Transfer๊ncia de Posto
				cPostoChave += aTransf[nI][18] + "|"
			EndIf
		Next
	EndIf
	
	If __lMemCalc .And. !Empty(cFilChave)
		fAddMemLog("Transfer๊ncias Filial : " + cFilChave ,1,1)
	EndIf
	If __lMemCalc .And. !Empty(cCCChave)
		fAddMemLog("Transfer๊ncias CC : " + cCCChave ,1,1)
	EndIf
	If __lMemCalc .And. !Empty(cDeptoChave)
		fAddMemLog("Transfer๊ncias Depto : " + cDeptoChave ,1,1)
	EndIf
	If __lMemCalc .And. !Empty(cPostoChave)
		fAddMemLog("Transfer๊ncias Posto : " + cPostoChave ,1,1)
	EndIf
	
	//Carrega altera็๕es no cadastro do Funcionแrio
	//Altera็ใo de Sindicato
	aTransf := HistSRA("RCE")
	For nI := 1 To Len(aTransf)
		cSindChave += aTransf[nI][2] + "|"
	Next
	
	//Altera็ใo de Cargo
	aTransf := HistSRA("SQ3")
	For nI := 1 To Len(aTransf)
		cCargoChave += aTransf[nI][2] + "|"
	Next
	
	//Altera็ใo de Turno
	aTransf := HistSPF( dDataDe, dDataAte)
	For nI := 1 To Len(aTransf)
		cTurnoChave += aTransf[nI][2] + "|"
	Next
	
	//Altera็ใo de Fun็ใo
	aTransf := HistSR7()
	For nI := 1 To Len(aTransf)
		cFuncaoChave += aTransf[nI][2] + "|"
	Next
	
	cCCChave		+= SRA->RA_CC		// Adiciona Centro de Custo atual na Chave
	cFilChave		+= SRA->RA_FILIAL	// Adiciona Filial atual na Chave
	cDeptoChave		+= SRA->RA_DEPTO	// Adiciona Departamento atual na Chave
	cPostoChave		+= SRA->RA_POSTO	// Adiciona Posto atual na Chave
	cSindChave		+= SRA->RA_SINDICA	// Adiciona Sindicato atual na Chave
	cCargoChave		+= SRA->RA_CARGO	// Adiciona Cargo atual na Chave
	cTurnoChave		+= SRA->RA_TNOTRAB	// Adiciona Turno atual na Chave
	cFuncaoChave	+= SRA->RA_CODFUNC	// Adiciona Fun็ใo atual na Chave
	aCCTChave		:= {} 				//Zera pois deve carregar apenas se houver crit้rio de CCT definido
	
	For nI := 1 To Len(aCriterio)
		
		If cTipoVale == "OUT" .And. aCriterio[nI][12] $ "VR*VA*PS"
			Loop
		EndIf
		
		// Verifica se possui mesmo Tipo de Beneficio em mais de 1 linha do crit้rio na mesma Entidade
		// ou o mesmo tipo de benefํcio em hiearquias diferentes
		lDesprezar 	:= .F.
		lRepete 	:= .F.
		If nI > 1 
			For nX := 1 To (nI-1)
				If cTipoVale <> "OUT" .And. cEntidade == aCriterio[nI, 1] .And. aCriterio[nI, 1] == aCriterio[nX, 1] .And. aCriterio[nI, 4] == aCriterio[nX, 4]
					lRepete 	:= .T.
				ElseIf cTipoVale == "OUT" .And. aCriterio[nI, 1] != aCriterio[nX, 1] .And. aCriterio[nI, 12] == aCriterio[nX, 12]
					lDesprezar 	:= .T.		
				EndIf
				If lRepete .Or. lDesprezar
					Exit
				EndIf
			Next nX
		EndIf

		If lDesprezar
			Loop
		EndIf

		cChave	:= Alltrim(aCriterio[nI][2])
		If __lMemCalc
			fAddMemLog("Tabela Crit้rio : " + aCriterio[nI][1] ,1,1)
			fAddMemLog("Chave Crit้rio : " + cChave ,1,1)
		EndIf
		
		nDias	:= 0
		lOk		:= .F.
		lNovo 	:= .T.
		
		If ! lOk .And. (aCriterio[nI][1] == "SQB") // Departamento
			lOk := cChave $ cDeptoChave
			nDias := If(lOk, NroDias("SQB", cChave, dDataDe, dDataAte,, lRepete), 0)
		EndIf
		If ! lOk .And. (aCriterio[nI][1] == "RCE") // Sindicato
			lOk := cChave $ cSindChave
			nDias := If(lOk, NroDias("RCE", cChave, dDataDe, dDataAte,, lRepete), 0)
		EndIf
		If ! lOk .And. (aCriterio[nI][1] == "CTT") // Centro de Custo
			lOk := cChave $ cCCChave
			nDias := If(lOk, NroDias("CTT", cChave, dDataDe, dDataAte,, lRepete), 0)
		EndIf
		If ! lOk .And. (aCriterio[nI][1] == "SQ3") // Cargo
			lOk := cChave $ cCargoChave
			nDias := If(lOk, NroDias("SQ3", cChave, dDataDe, dDataAte,, lRepete), 0)
		EndIf
		If ! lOk .And. (aCriterio[nI][1] == "RCL") // Posto
			lOk := cChave $ cPostoChave
			nDias := If(lOk, NroDias("RCL", cChave, dDataDe, dDataAte,, lRepete), 0)
		EndIf
		If ! lOk .And. (aCriterio[nI][1] == "SRJ") // Fun็ใo
			lOk := cChave $ cFuncaoChave
			nDias := If(lOk, NroDias("SRJ", cChave, dDataDe, dDataAte,, lRepete), 0)
		EndIf
		If ! lOk .And. (aCriterio[nI][1] == "SR6") // Turno
			lOk := cChave $ cTurnoChave
			nDias := If(lOk, NroDias("SR6", cChave, dDataDe, dDataAte,, lRepete), 0)
		EndIf
		If ! lOk .And. (aCriterio[nI][1] == "SWY") // Conven็ใo Coletiva de Trabalho
			If Empty(aCCTChave)
				aCCTChave := fLoadCCT(cChave, cFilChave)
			EndIf
			cCCTChave := ""
			aEval(aCCTChave, {|x| If(x $ cSindChave, cCCTChave += x + "/", Nil)})
			lOk := !Empty(cCCTChave)
			nDias := If(lOk, NroDias("SWY", cCCTChave, dDataDe, dDataAte,, lRepete), 0)
		EndIf
		If ! lOk .And. (aCriterio[nI][1] == "SM0") // Filial
			lOk := cChave $ cFilChave
			nDias := If(lOk, NroDias("SM0", cChave, dDataDe, dDataAte,, lRepete), 0)
		EndIf
		If !lOk .And. lUsaGS .and. (aCriterio[nI][1] $ "ABS*SA1*TDX") // Local de Atendimento ou Cliente ou Turno do Posto
			If lFrist
				AA1->(DbSetOrder(7)) // AA1_FILIAL, AA1_CDFUNC, AA1_FUNFIL
				If AA1->(DbSeek(xFilial("AA1") + SRA->RA_MAT + SRA->RA_FILIAL))
					aAloc := TXRetAloc(SRA->RA_FILIAL, SRA->RA_MAT, dDataDe, dDataAte)
					For nX := 1 To Len(aAloc)
						Aadd(aAloc[nX], 0)
					Next
				EndIf
				lFrist := .F.
			EndIf
			For nX := 1 to Len(aAloc)
				If ( aCriterio[nI][1] == "ABS" .And. AllTrim(aAloc[nX][07]) == cChave ) .or. ( aCriterio[nI][1] == "SA1" .And. AllTrim(aAloc[nX][10] + aAloc[nX][11]) == cChave ) .or. ( aCriterio[nI][1] == "TDX" .And. AllTrim(aAloc[nX][15] + aAloc[nX][08]) == cChave )
					lPaga := fVldCrit(aAloc[nX],aCriterio[nI],aDiasMes)
					
					If lPaga .And. aAloc[nX][Len(aAloc[nX])] == 0	// Se o dia deve ser pago e nใo foi pago em um crit้rio anterior
						nDias++
						If cTipoVale != "OUT" 
							aAloc[nX][Len(aAloc[nX])] := 1
							
							If (nPosAdias := Ascan( _aDias, { |X| X[1] == aAloc[nX][02] } )) > 0
								_aDias[nPosAdias][4] := .F.
								If FindFunction("TecBAtuDia")
									TecBAtuDia( SRA->RA_FILIAL, SRA->RA_MAT, _aDias[nPosAdias][1], .F. )
								EndIf
							EndIf
						EndIf
					ElseIf ! lPaga	// Se o dia nใo deve ser pago
						aAloc[nX][Len(aAloc[nX])] := 1
						If (nPosAdias := Ascan( _aDias, { |X| X[1] == aAloc[nX][02] } )) > 0
							_aDias[nPosAdias][4] := .F.
							If FindFunction("TecBAtuDia")
								TecBAtuDia( SRA->RA_FILIAL, SRA->RA_MAT, _aDias[nPosAdias][1], .F. )
							EndIf
						EndIf
					EndIf
				EndIf
			Next nX
			If __lMemCalc
				fAddMemLog("Mํnimo informado no crit้rio : " + cValtochar(aCriterio[nI][13]) ,1,1)
				fAddMemLog("Dias  : " + cvaltochar(nDias) ,1,1)
			EndIf
			If cTipoVale != "OUT" .And. nDias > 0 .And. nDias >= aCriterio[nI, 13]	//Se quantidade de dias maior que 0 e maior que o n๚mero minimo de dias informado no crit
				aAdd(aDias, {nI, nDias, .T., 0})
			ElseIf cTipoVale == "OUT" 
				lOk := .T.
				If nDias < aCriterio[nI, 13]
					nDias := 0
				EndIf
			Endif
		EndIf
		If lOk .And. nDias > 0
			nCrit 		:= nI
			cCodBen 	:= Alltrim(aCriterio[nCrit][3])
			cTipoBen 	:= Alltrim(aCriterio[nCrit][12])
			cCodSLY 	:= Alltrim(aCriterio[nCrit][3])
			For nJ := 1 To Len(aDias)
				If cTipoVale == "OUT" .And. aCriterio[aDias[nJ, 1]][12] == cTipoBen .And. aCriterio[aDias[nJ, 1]][3] == cCodSLY
					lNovo := .F.
					EXIT
				Else
					If (cTipoVale != "OUT" .And. Alltrim(aCriterio[aDias[nJ, 1]][3]) == cCodBen) .Or. (cTipoVale == "OUT" .And. aCriterio[aDias[nJ, 1]][12] == cTipoBen .And. Alltrim(aCriterio[aDias[nJ, 1]][3]) == cCodBen)
						aDias[nJ][2] += nDias
						lNovo := .F.
					EndIf
				EndIf
			Next
			If lNovo
				Aadd(aDias, {nI, nDias, .T., 0})
				lNovo := .T.
				If Empty(cEntidade)
					cEntidade := aCriterio[nCrit][1]
				EndIf
			EndIf
		EndIf
	Next nI
	
	If Len(aDias) > 0
		
		RFO->(DbSetOrder(1)) //RFO_FILIAL+RFO_TPVALE+RFO_CODIGO
		
		If cTipoVale <> "OUT"
			If LNOVOCALCB
				//Grava SM7
				DbSelectArea("SM7")
				SM7->(DbSetOrder(3))
				
				For nX := 1 to Len(aDias)
					nCrit := aDias[nX,1]
					
					RFO->(DbSeek(xFilial("RFO")+cTipoVale + Alltrim(aCriterio[nCrit][3])))
					
					If SM7->(DbSeek(SRA->RA_FILIAL + SRA->RA_MAT + cTipoVale + Alltrim(aCriterio[nCrit][3])))
						lNovo := .F.
					Else
						lNovo := .T.
					EndIf
					
					RecLock("SM7",lNovo)
						
						SM7->M7_FILIAL 	:= SRA->RA_FILIAL
						SM7->M7_MAT		:= SRA->RA_MAT
						SM7->M7_CODIGO	:= Alltrim(aCriterio[nCrit][3])
						SM7->M7_TPCALC	:= "2"
						SM7->M7_QDIAINF	:= 1
						SM7->M7_TPVALE	:= cTipoVale
						SM7->M7_COMPL	:= "2"
						If aDias[nX,3] //Se for criterio de servi็os, grava os dias calculados no campo R0_DPROPIN para que elessejam considerados no calculo.
							If RFO->RFO_DIAFIX > 0
								SM7->M7_DPROPIN := RFO->RFO_DIAFIX
							Else
								SM7->M7_DPROPIN := aDias[nX, 2]
							EndIf
						EndIf
						
					SM7->(MsUnlock())
				Next nX
			Else
				//Grava SR0
				DbSelectArea("SR0")
				SR0->(DbSetOrder(3))
				
				For nX := 1 to Len(aDias)
					nCrit := aDias[nX, 1]
					
					RFO->(DbSeek(xFilial("RFO")+cTipoVale + Alltrim(aCriterio[nCrit][3])))
					
					If SR0->(DbSeek(SRA->RA_FILIAL + SRA->RA_MAT + cTipoVale + Alltrim(aCriterio[nCrit][3])))
						lNovo := .F.
					Else
						lNovo := .T.
					EndIf
					
					RecLock("SR0",lNovo)
						
						SR0->R0_FILIAL 	:= SRA->RA_FILIAL
						SR0->R0_MAT		:= SRA->RA_MAT
						SR0->R0_CODIGO	:= Alltrim(aCriterio[nCrit][3])
						SR0->R0_TPCALC	:= "2"
						SR0->R0_QDIAINF	:= 1
						SR0->R0_TPVALE	:= cTipoVale
						If aDias[nX,3] //Se for criterio de servi็os, grava os dias calculados no campo R0_DPROPIN para que elessejam considerados no calculo.
							If RFO->RFO_DIAFIX > 0
								SR0->R0_DPROPIN := RFO->RFO_DIAFIX
							Else
								SR0->R0_DPROPIN := aDias[nX, 2]
							EndIf
							SR0->R0_QDIADIF := aDias[nX, 4]//Grava a diferen็a de dias com rela็ใo ao periodo anterior
						EndIf
						
					SR0->(MsUnlock())
				Next nX
			EndIf
		Else
			DbSelectArea("RIS")
			DbSetOrder(1) //RIS_FILIAL + RIS_TPBENE + RIS_COD + STR(RIS_SALATE)
			
			DbSelectArea("RI1")
			RI1->(DbSetOrder(1)) // RI1_FILIAL + RI1_MAT + RI1_BENEF + RI1_TABELA
			
			For nX := 1 to Len(aDias)
				nCrit := aDias[nX,1]
				
				If aCriterio[nCrit][12] $ "VR*VA*PS"
					Loop
				EndIf
				
				// Preserva os benefํcios informados
				cChaveBEN := Alltrim(aCriterio[nCrit][3]) + Alltrim(aCriterio[nCrit][12])
				If aScan(aBenRI1, {|x| AllTrim(x[3]) + AllTrim(x[2]) == cChaveBEN }) > 0
					LOOP
				EndIf
				
				RIS->(DbSeek(xFilial("RIS", SRA->RA_FILIAL) + aCriterio[nCrit][12] + Alltrim(aCriterio[nCrit][3] )))
				cProp := RIS->RIS_PROPOR
				
				If Empty(cProp)
					cProp := "2"
				EndIf
				
				If lGrava
					If RI1->(DbSeek(SRA->RA_FILIAL + SRA->RA_MAT + aCriterio[nCrit][12] + Alltrim(aCriterio[nCrit][3])))
						lNovo := .F.
					Else
						lNovo := .T.
					EndIf
					
					RecLock("RI1", lNovo)
						
						RI1->RI1_FILIAL 	:= SRA->RA_FILIAL
						RI1->RI1_MAT		:= SRA->RA_MAT
						RI1->RI1_COD		:= "001"
						RI1->RI1_BENEF		:= aCriterio[nCrit][12]
						RI1->RI1_TABELA		:= Alltrim(aCriterio[nCrit][3])
						RI1->RI1_PROPOR		:= cProp
						RI1->RI1_TPCALC		:= "2"
						RI1->RI1_PD			:= RIS->RIS_PD
						RI1->RI1_PD1		:= RIS->RIS_PD1
						RI1->RI1_PD2		:= RIS->RIS_PD2
						RI1->RI1_DINIPG		:= StoD(aCriterio[nCrit][14])
						RI1->RI1_DFIMPG		:= StoD(aCriterio[nCrit][15])
						
					RI1->(MsUnlock())
				EndIf
				//Adiciona no array para calculo
				aAdd(aBenRI1, {RI1->RI1_MAT, RI1->RI1_BENEF, RI1->RI1_TABELA, RI1->RI1_PD, RI1->RI1_PD1, RI1->RI1_PD2, RI1->RI1_PROPOR})
			Next nX
		EndIf
	EndIf
	
	_aDias				:= {} // Zera array com os dias do funcionแrio
	RestArea(aAreaRFO)
	RestArea(aArea)

Return Nil

/*/{Protheus.doc} fApurPAnt
Apura pagamento de beneficios no periodo anterior.
@author Leandro Drumond
@since 28/04/2015
@param aPerAux, array, Perํodo que serแ recalculado
@param cTipoVale, characters, "1" - Vale Refei็ใo ou "2" - Vale Alimenta็ใo
@param aCritRet, array, Array com o crit้rio de benefํcios
@return nDiferenca, Num้rico, Diferen็a entre o que foi pago e o que era devido para o funcionแrio no m๊s anterior
/*/
Function fApurPAnt(aPerAux, cTipoVale, aCritRet, cCodSM7)

	Local aAloc			:= {}
	Local aDiasAux		:= {}
	Local aDiasMesAnt	:= {}
	Local aTransf		:= {}
	Local aRet			:= {0, ""}
	Local cChave		:= ""
	Local cTnoAux		:= SRA->RA_TNOTRAB
	Local nI			:= 0
	Local nX			:= 0
	Local nDias			:= 0
	Local nDiasAux1		:= 0
	Local nDiasAux2		:= 0
	Local nCrit			:= 0
	Local nPosAdias		:= 0
	Local nValPago		:= 0
	Local nValDevido	:= 0
	Local nDiasFixos	:= 0
	Local nValUni		:= 0
	Local nDiferenca	:= 0
	Local lAchou		:= .F.
	Local lAchouRG2		:= .F.
	Local lOk			:= .F.
	Local lPaga			:= .F.
	Local lFrist		:= .T.
	Local cCodBen		:= ""
	Local cCCChave		:= ""
	Local cDeptoChave	:= ""
	Local cPostoChave	:= ""
	Local cFilChave		:= ""
	Local cSindChave	:= ""
	Local cCargoChave	:= ""
	Local cTurnoChave	:= ""
	Local cFuncaoChave	:= ""
	Local cCampos		:= ""
	Local cAliasRG2		:= ""
	Local cCCTChave		:= ""
	Local aCCTChave		:= {}
	Local cWhereCod		:= "%%"
	Local cTipo			:= If(cTipoVale == "2", "VA", If(cTipoVale == "1", "VR", ""))
	Local lSLDANT		:= .F.

	Static nTamRg2Cd	:= TamSX3("RG2_CODIGO")[1]

	DEFAULT aCritRet 	:= {}
	DEFAULT cCodSM7 	:= ""

	CargaCrit(cTipo, @aCriterio, aPerAux[5], aPerAux[6])

	If ! Empty(aCritRet)
		aCriterio := aCritRet
	EndIf

	fCarrAdias(aPerAux[5], aPerAux[6], aPerAux[1]) // Carrega o array _aDias com os dias do m๊s

	// Carrega transfer๊ncias do funcionแrio
	If fTransf(@aTransf, aPerAux[1], , , , , , , , , , , SRA->RA_FILIAL, SRA->RA_MAT)
		For nI := 1 To Len(aTransf)
			If aTransf[nI][2] != SRA->RA_FILIAL		// Transfer๊ncia de Filial
				cFilChave += SubStr(aTransf[nI][2], 1, FWGetTamFilial) + "|"
			EndIf
			If aTransf[nI][3] != SRA->RA_CC			// Transfer๊ncia de Centro de Custo
				cCCChave += aTransf[nI][3] + "|"
			EndIf
			If aTransf[nI][16] != SRA->RA_DEPTO		// Transfer๊ncia de Departamento
				cDeptoChave += aTransf[nI][16] + "|"
			EndIf
			If aTransf[nI][18] != SRA->RA_POSTO		// Transfer๊ncia de posto
				cPostoChave += aTransf[nI][18] + "|"
			EndIf
		Next
	EndIf

	// Verifica altera็๕es no cadastro de Funcionแrios
	// Altera็๕es de Sindicato
	aTransf := HistSRA("RCE")
	For nI := 1 To Len(aTransf)
		cSindChave += aTransf[nI][2] + "|"
	Next

	// Altera็๕es de Cargo
	aTransf := HistSRA("SQ3")
	For nI := 1 To Len(aTransf)
		cCargoChave += aTransf[nI][2] + "|"
	Next

	// Altera็๕es de Turno
	aTransf := HistSPF( aPerAux[5], aPerAux[6])
	For nI := 1 To Len(aTransf)
		cTurnoChave += aTransf[nI][2] + "|"
	Next

	// Altera็๕es de Fun็ใo
	aTransf := HistSR7()
	For nI := 1 To Len(aTransf)
		cFuncaoChave += aTransf[nI][2] + "|"
	Next

	cCCChave		+= SRA->RA_CC		// Adiciona Centro de Custo atual na Chave
	cFilChave		+= SRA->RA_FILIAL	// Adiciona Filial atual na Chave
	cDeptoChave		+= SRA->RA_DEPTO	// Adiciona Departamento atual na Chave
	cPostoChave		+= SRA->RA_POSTO	// Adiciona Posto atual na Chave
	cSindChave		+= SRA->RA_SINDICA	// Adiciona Sindicato atual na Chave
	cCargoChave		+= SRA->RA_CARGO	// Adiciona Cargo atual na Chave
	cTurnoChave		+= SRA->RA_TNOTRAB	// Adiciona Turno atual na Chave
	cFuncaoChave	+= SRA->RA_CODFUNC	// Adiciona Fun็ใo atual na Chave
	aCCTChave		:= {} 				//Zera pois deve carregar apenas se houver crit้rio de CCT definido

	For nI := 1 To Len(aCriterio)
		cChave := Alltrim(aCriterio[nI][2])
		nDias  := 0
		If !Empty(cCodSM7) .And. AllTrim(cCodSM7) != Alltrim(aCriterio[nI][3])
			Loop
		EndIf
		Do Case
			Case (aCriterio[nI][1] $ "ABS*SA1*TDX") // Local de Atendimento * Cliente * Turno do Posto (Entidades do Gestใo de Servi็os)

				If lFrist
					If lUsaGS
						AA1->(DbSetOrder(7)) // AA1_FILIAL, AA1_CDFUNC, AA1_FUNFIL
						If AA1->(DbSeek(xFilial("AA1") + SRA->RA_MAT + SRA->RA_FILIAL))
							aAloc := TXRetAloc(SRA->RA_FILIAL, SRA->RA_MAT, aPerAux[5], aPerAux[6])

							For nX := 1 To Len(aAloc)
								Aadd(aAloc[nX], 0)
							Next
						EndIf
					EndIf
					lFrist := .F.
				EndIf

				For nX := 1 to Len(aAloc)
					If ( aCriterio[nI][1] == "ABS" .And. aAloc[nX][07] == cChave ) .or. ( aCriterio[nI][1] == "SA1" .And. aAloc[nX][10] + aAloc[nX][11] == cChave ) .or. ( aCriterio[nI][1] == "TDX" .And. aAloc[nX][15] + aAloc[nX][08] == cChave )
						If ( aCriterio[nI][10] <> "1" .or. aCriterio[nI][11] <> "1" ) .And. Empty(aDiasMesAnt) //Se nใo paga ferias ou afastamentos, monta DiasMes do periodo anterior.
							RCG->(DbSetOrder(RetOrder("RCG","RCG_FILIAL+RCG_PROCES+RCG_PER+RCG_SEMANA+RCG_ROTEIR+RCG_TNOTRA+DTOS(RCG_DIAMES)")))
							lAchou := RCG->( dbSeek(xFilial("RCG") + SRA->RA_PROCES + aPerAux[1] + aPerAux[2] + Space(3) + cTnoAux ) )
							If !lAchou
								cTnoAux := "@@@"
								lAchou := RCG->( dbSeek(xFilial("RCG") + SRA->RA_PROCES + aPerAux[1] + aPerAux[2] + Space(3) + cTnoAux ) )
							EndIf
							If lAchou
								While !RCG->( Eof() )  .And. RCG->(RCG_FILIAL + RCG_PROCES + RCG_PER + RCG_SEMANA + RCG_ROTEIR + RCG_TNOTRA ) == xFilial("RCG") + SRA->RA_PROCES + aPerAux[1] + aPerAux[2] + Space(3) + cTnoAux
									aAdd( aDiasMesAnt, {RCG->RCG_DIAMES, RCG->RCG_TIPDIA, "", .T.,  "" } )
									RCG->( DbSkip() )
								Enddo
								nDiasAux1 := DiasTrab
								nDiasAux2 := nDiasAfas
								fVlsAus(aPerAux[5], aPerAux[6], , @aDiasMesAnt)
								nDiasAfas := nDiasAux2
								DiasTrab  := nDiasAux1
								nDiasAux1 := nFerVen
								fVlsFerPrg(aPerAux[5], aPerAux[6],@aDiasMesAnt)
								nFerVen   := nDiasAux1
							EndIf
						EndIf
						lPaga := fVldCrit(aAloc[nX], aCriterio[nI], aDiasMesAnt)
						If lPaga .And. aAloc[nX][Len(aAloc[nX])] == 0 // Se o dia deve ser paga e se ainda nใo foi pago em outro crit้rio
							nDias++
							aAloc[nX][Len(aAloc[nX])] := 1
							If (nPosAdias := Ascan( _aDias, { |X| X[1] == aAloc[nX][02] } )) > 0
								_aDias[nPosAdias][4] := .F.
							EndIf
						ElseIf ! lPaga // Se o dia nใo deve ser pago
							aAloc[nX][Len(aAloc[nX])] := 1
							If (nPosAdias := Ascan( _aDias, { |X| X[1] == aAloc[nX][02] } )) > 0
								_aDias[nPosAdias][4] := .F.
							EndIf
						EndIf
					EndIf
				Next nX

				lOk := nDias > 0

			Case (aCriterio[nI][1] == "CTT") // Centro de Custo
				lOk := cChave $ cCCChave
				nDias := If(lOk, NroDias("CTT", cChave, aPerAux[5], aPerAux[6], aPerAux[1]), 0)

			Case (aCriterio[nI][1] == "SQB") // Departamento
				lOk := cChave $ cDeptoChave
				nDias := If(lOk, NroDias("SQB", cChave, aPerAux[5], aPerAux[6], aPerAux[1]), 0)

			Case (aCriterio[nI][1] == "RCL") // Posto
				lOk := cChave $ cPostoChave
				nDias := If(lOk, NroDias("RCL", cChave, aPerAux[5], aPerAux[6], aPerAux[1]), 0)

			Case (aCriterio[nI][1] == "SM0") // Filial
				lOk := cChave $ cFilChave
				nDias := If(lOk, NroDias("SM0", cChave, aPerAux[5], aPerAux[6], aPerAux[1]), 0)

			Case (aCriterio[nI][1] == "RCE") // Sindicato
				lOk := cChave $ cSindChave
				nDias := If(lOk, NroDias("RCE", cChave, aPerAux[5], aPerAux[6], aPerAux[1]), 0)

			Case (aCriterio[nI][1] == "SQ3") // Cargo
				lOk := cChave $ cCargoChave
				nDias := If(lOk, NroDias("SQ3", cChave, aPerAux[5], aPerAux[6], aPerAux[1]), 0)

			Case (aCriterio[nI][1] == "SR6") // Turno
				lOk := cChave $ cTurnoChave
				nDias := If(lOk, NroDias("SR6", cChave, aPerAux[5], aPerAux[6], aPerAux[1]), 0)

			Case (aCriterio[nI][1] == "SRJ") // Fun็ใo
				lOk := cChave $ cFuncaoChave
				nDias := If(lOk, NroDias("SRJ", cChave, aPerAux[5], aPerAux[6], aPerAux[1]), 0)

			Case (aCriterio[nI][1] == "SWY") // Conven็ใo Coletiva de Trabalho
				If Empty(aCCTChave)
					aCCTChave := fLoadCCT(cChave, cFilChave)
				EndIf
				cCCTChave := ""
				aEval(aCCTChave, {|x| If(x $ cSindChave, cCCTChave += x + "/", Nil)})
				lOk := !Empty(cCCTChave)
				nDias := If(lOk, NroDias("SWY", cCCTChave, aPerAux[5], aPerAux[6], aPerAux[1]), 0)
			Otherwise
				lOk := .F.
		EndCase

		If nDias > 0 .And. nDias >= aCriterio[nI, 13]	//Se quantidade de dias maior que 0 e maior que o n๚mero minimo de dias informado no crit
			Aadd(aDiasAux, {nI, nDias, 0, 0, 0, 0, .F.})
		EndIf

	Next nI

	If Len(aDiasAux) > 0

		DbSelectArea("RG2")
		DbSetOrder(1)	// RG2_FILIAL + RG2_MAT + RG2_TPVALE + RG2_CODIGO + RG2_PERIOD + RG2_NROPGT + RG2_ROTEIR

		lSLDANT := RG2->(ColumnPos("RG2_SLDANT")) > 0

		cAliasRG2 := GetNextAlias()

		// Busca valor pago no m๊s anterior
		If lSLDANT
			cCampos := "%RG2_VALCAL, RG2_SLDANT, RG2_CODIGO%"
		Else
			cCampos := "%RG2_VALCAL, RG2_CODIGO%"
		EndIf

		If !Empty(cCodSM7)
			cWhereCod	:= "%AND RG2_CODIGO = '" + cCodSM7 + "'%"
		EndIf

		BeginSQL ALIAS cAliasRG2

			SELECT %Exp: cCampos%

			FROM %Table:RG2% RG2

			WHERE RG2_FILIAL = %Exp:SRA->RA_FILIAL%
			AND RG2_MAT = %Exp:SRA->RA_MAT%
			AND RG2_TPVALE = %Exp:cTipoVale%
			AND RG2_PERIOD = %Exp:aPerAux[1]%
			AND RG2_NROPGT = %Exp:aPerAux[2]%
			AND RG2_ROTEIR = %Exp:aPerAux[8]%
			AND RG2.%notDel%
			%Exp:cWhereCod%

		ENdSQL

		While ! (cAliasRG2)->(Eof())
			If(lSLDANT, nValPago += (cAliasRG2)->(RG2_VALCAL + RG2_SLDANT), nValPago += (cAliasRG2)->RG2_VALCAL)
			cCodBen 	:= (cAliasRG2)->RG2_CODIGO
			lAchouRG2 	:= .T.
			(cAliasRG2)->(dbSkip())
		EndDo

		(cAliasRG2)->(dbCloseArea())

		// Calcula valor devido para o funcionแrio
		If lAchouRG2
			For nX := 1 To Len(aDiasAux)

				nCrit := aDiasAux[nX, 1]
				cCodBen := Padr(aCriterio[nCrit, 3], nTamRg2Cd )

				nDiasFixos := Posicione("RFO", 1, xFilial("RFO", SRA->RA_FILIAL) + cTipoVale + cCodBen, "RFO_DIAFIX")
				nValUni := RFO->RFO_VALOR

				nValDevido += If( ! Empty(nDiasFixos), nDiasFixos * nValUni, aDiasAux[nX, 2] * nValUni)

			Next
		
			// calcula a diferen็a entre o que foi pago e o que ้ devido
			nDiferenca := nValDevido - nValpago

			aRet := {nDiferenca, cCodBen}
		EndIf
	EndIf

	_aDias := {} // Zera array com os dias do funcionแrio

Return aRet

/*/{Protheus.doc} fVldCrit
Valida criterios de servicos.
@author Leandro Drumond
@since 28/04/2015
/*/
Static Function fVldCrit(aAloc,aCriterio,aDiasMes)

Local lPaga := .F.

	lPaga := aAloc[Len(aAloc)] == 0 // Se nใo foi pago em um crit้rio anterior (Estarแ sempre na ๚ltima posi็ใo)
	If lPaga
		If (lPaga := ! aAloc[13] .or. aCriterio[08] == "1") // Se nใo for falta ou se estiver definido para pagar falta
			If (lPaga := ! aAloc[12] .or. aCriterio[07] == "1") // Se nao for feriado ou se estiver definido para pagar feriado
				If (lPaga := Dow(aAloc[02]) <> 1 .or. aCriterio[06] == "1") //Se nใo for domingo ou se estiver definido para pagar o domingo
					If (lPaga := Dow(aAloc[02]) <> 7 .or. aCriterio[05] == "1") //Se nใo for sabado ou se estiver definido para pagar o sabado
						If (lPaga := ! aAloc[14] .or. aCriterio[09] == "1") //Se nใo for substituto ou se estiver definido para pagar substitui็ใo
							If aCriterio[10] <> "1" //Nใo paga afastamento e f้rias
								If (nPos := Ascan( aDiasMes , { |X| X[1] == aAloc[02] } )) > 0
									If aDiasMes[nPos, 5] == "AFA" .or. aDiasMes[nPos, 5] == "FER"
										lPaga := .F.
									EndIf
								EndIf
							EndIf
						EndIf
						If lPaga
							If aCriterio[11] <> "1" //Nใo paga f้rias programadas
								If (nPos := Ascan( aDiasMes, { |X| X[1] == aAloc[02] } )) > 0
									If aDiasMes[nPos,5] == "PRG"
										lPaga := .F.
									EndIf
								EndIf
							EndIf
						EndIf
					EndIf
				EndIf
			EndIf
			If lPaga
				If DateWorkDay( aAloc[02] , aAloc[02], .F., .F., .F. ) > 0 .And. aCriterio[04] <> "1" //ษ dia ๚til e esta definido para nใo pagar dias ๚teis
					lPaga := .F.
				EndIf
			EndIf
		EndIf
	EndIf

Return lPaga

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณCargaCrit    บAutor  ณFlavio S Correa  บ Data ณ 22/02/2014  บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณCarrega beneficios por criterio			                      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณRoteiros de Calculo			                                  บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function CargaCrit(cTipo, aCriterio, dDt1, dDt2)

Local aArea			:= GetArea()
Local cQry			:= GetNextAlias()
Local cSGBD			:= AllTrim( Upper( TcGetDb() ) )
Local cWhere		:= ""
Local cLimite		:= ""
Local cWhereFil		:= "%%"
Local cSelect		:= ""

Default dDt1		:= aPeriodo[nPosSem,3] //Data inicial do periodo de calculo
Default dDt2		:= aPeriodo[nPosSem,4] //Data final do periodo de calculo

Do Case
	Case cSGBD $ "ORACLE/MYSQL/POSTGRES/DB2"
		cLimite += If(cSGBD $ "ORACLE", " WHERE ROWNUM <= 1 ", If(cSGBD $ "MYSQL/POSTGRES", " TSTALIAS LIMIT 1 ", " FETCH FIRST 1 ROW ONLY "))
	Case cSGBD $ "INFORMIX"
	Otherwise
		cSelect := " TOP 1 "
EndCase

cWhere  := "% " + cWhere + " %"

If !Empty( xFilial("SJQ") )
	cWhereFil	:= "% LY_FILIAL = '"+ xFilial("SLY") + "' AND %"
EndIf

If !Empty(cSelect) //MSSQLServer
	cSelect := "% " + cSelect + " %"

	BEGINSQL ALIAS cQry
		SELECT * FROM %table:SLY% SLY
		INNER JOIN %table:SJS% SJS on JS_FILIAL = %xfilial:SJS% AND JS_CDAGRUP = LY_AGRUP and JS_TABELA = LY_ALIAS and SJS.%notDel%
		WHERE %exp:cWhereFil%
		LY_TIPO IN (%exp:cTipo%)
		AND LY_AGRUP=(SELECT %exp:cSelect% JQ_CODIGO
						FROM %table:SJQ%  SJQ
						WHERE SJQ.%notDel%
						AND JQ_FILIAL = %xfilial:SJQ%
						AND JQ_STATUS = %exp:'1'%
						AND JQ_FILREF IN (%exp:SRA->RA_FILIAL%,'')
						AND ( %exp:AnoMes(dDt1)% >= JQ_PERINI AND (%exp:AnoMes(dDt2)% <= JQ_PERFIM OR JQ_PERFIM = %exp:''%) )
						%exp:cWhere% 
						ORDER BY JQ_FILREF DESC)
		AND ( %exp:dtos(dDt1)% >= LY_DTINI AND (%exp:dtos(dDt2)% <= LY_DTFIM or LY_DTFIM = %exp:''%))	and SLY.%notDel%
		ORDER BY JS_SEQ
	ENDSQL
Else
	cLimite := "% " + cLimite + " %"

	BEGINSQL ALIAS cQry
		SELECT * FROM %table:SLY% SLY
		INNER JOIN %table:SJS% SJS on JS_FILIAL = %xfilial:SJS% AND JS_CDAGRUP = LY_AGRUP and JS_TABELA = LY_ALIAS and SJS.%notDel%
		WHERE %exp:cWhereFil%
		LY_TIPO IN (%exp:cTipo%)
		AND LY_AGRUP=(SELECT CODIGO FROM 
			(SELECT JQ_CODIGO CODIGO
						FROM %table:SJQ%  SJQ
						WHERE SJQ.%notDel%
						AND JQ_FILIAL = %xfilial:SJQ%
						AND JQ_STATUS = %exp:'1'%
						AND JQ_FILREF IN (%exp:SRA->RA_FILIAL%,'')
						AND ( %exp:AnoMes(dDt1)% >= JQ_PERINI AND (%exp:AnoMes(dDt2)% <= JQ_PERFIM OR JQ_PERFIM = %exp:''%) )
						%exp:cWhere% 
						ORDER BY JQ_FILREF DESC) %exp:cLimite% )
		AND ( %exp:dtos(dDt1)% >= LY_DTINI AND (%exp:dtos(dDt2)% <= LY_DTFIM or LY_DTFIM = %exp:''%))	and SLY.%notDel%
		ORDER BY JS_SEQ
	ENDSQL
EndIf

aCriterio := {}
While !(cQry)->(Eof())
	(cQry)->(aAdd(aCriterio, { LY_ALIAS,LY_CHVENT,LY_CODIGO,LY_PGDUT,LY_PGSAB,LY_PGDOM,LY_PGFER,LY_PGFALT,LY_PGSUBS,LY_PGAFAS,LY_PGVAC,LY_TIPO,LY_DIAS,LY_DTINI,LY_DTFIM   } ))
	(cQry)->(dbSkip())
EndDo

(cQry)->(DbCloseArea())

RestArea(aArea)
Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออัอออออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFunctionณfCrgBenAut     บAutor  ณLeandro Drumond  บ Data ณ 05/05/2015  บฑฑ
ฑฑฬออออออออุอออออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.   ณValida criterios de servicos.  			                      บฑฑ
ฑฑบ        ณ                                                              บฑฑ
ฑฑฬออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso     ณRoteiros de Calculo			                                  บฑฑ
ฑฑศออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function fCrgBenAut(lGrava)
Local aArea		:= GetArea()
Local aTabBenef := {}
Local cTipo		:= ""
Local nX		:= 0
Local lCarga	:= .F.
Local lDiasMes	:= .F.

If Empty(aCriterio)
	fCarrTab( @aTabBenef,"S011", Nil,.T.)
	If !Empty(aTabBenef)
		For nX := 1 to Len(aTabBenef)
			cTipo += aTabBenef[nX,5]
			If nX < Len(aTabBenef)
				cTipo += "','"
			EndIf
		Next nX
	EndIf
	If Empty(aCriterio) .Or. cTipo <> cTipoCri
		CargaCrit(cTipo, @aCriterio)
		cTipoCri := cTipo
	EndIf
EndIf

If Len(aCriterio) > 0
	DbSelectArea("RI1")
	DbSetOrder(1)//RI1_FILIAL+RI1_MAT+RI1_BENEF+RI1_TABELA
	
	For nX := 1 to Len(aCriterio)
		If DbSeek(SRA->RA_FILIAL + SRA->RA_MAT + aCriterio[nX,12])
			If RI1->RI1_TPCALC == "2"
				RecLock("RI1",.F.)
				DbDelete()
				MsUnLock()
				lCarga := .T.
			EndIf
		Else
			lCarga := .T.
		EndIf
		If (aCriterio[nX][1] $ "ABS*SA1*TDX")
			lDiasMes := .T.
		EndIf
	Next nX
	
	If lCarga
		If lGrava
			//Carrega aDiasMes
			If lDiasMes
				fVlsAus(,,, @aDiasMes)
				fVlsFerPrg(,,@aDiasMes)
			EndIf
		EndIf
		fCrgAutom(cTipoCri,"OUT",lGrava)
		If !lGrava
			lCarga := !Empty(aBenRI1) //Se nใo houver crit้rios lan็ados para o funcionแrio, retorna .F. para carregar a defini็ใo padrใo.
			aBenRI1 := {}
		EndIf
	EndIf
EndIf

RestArea(aArea)

Return lCarga

/*/{Protheus.doc} fSVerbas
Funcao de soma as verbas definidas no cแlculo da folha
@author Allyson Mesashi
@since 12/07/2016
@version P12.1.07
@Param cCodigos, Caracter, C๓digo de verbas que serใo verificados
@Param nValor, Num้rico, Variแvel contendo o valor das verbas selecionadas
/*/
Static Function fSVerbas(cCodigos, nValor, aVerbas)

Local aPd		:= aClone( If(Type("aBkpPd") != "U" .and. !Empty(aBkpPd), aBkpPd, aVerbas) )
Local cVerbas 	:= ""
Local nFor		:= 0
Local nGarantia	:= 0

// Separa os Codigos das verbas solicitadas a listar
For nFor := 1 To Len(ALLTRIM(cCodigos)) Step 3
	cVerbas += Subs(cCodigos,nFor,3)
	If Len(ALLTRIM(cCodigos)) > ( nFor+3 )
		cVerbas += "/"
	Endif
Next nFor

For nFor := 1 To Len(aPd)
	If aPd[nFor, 3] != cSemana
		Loop
	EndIf
	If aPd[nFor, 1] $ cVerbas
		If RetValSRV( aPd[nFor, 1], SRA->RA_FILIAL, "RV_TIPOCOD" ) == "1"//Proventos
			nValor += aPd[nFor, 5]
		ElseIf RetValSRV( aPd[nFor, 1], SRA->RA_FILIAL, "RV_TIPOCOD" ) == "2"//Descontos
			nValor -= aPd[nFor, 5]
		EndIf
	EndIf
Next nFor

fPosReg("RCE", 1, xFilial("RCE", SRA->RA_FILIAL)+SRA->RA_SINDICA)
nGarantia := RCE->RCE_GCOMIS

If nValor < nGarantia .AND. SRA->RA_CATFUNC $ "C"
	nValor := nGarantia
EndIf

Return Nil


Static Function GetNroPed()

Local aAreaSR0 	:= {}
Local cAliasTab	:= ""

If Empty(cNroPed) .or. cFilPed <> SRA->RA_FILIAL
	aAreaSR0 	:= SR0->( GetArea() )
	cAliasTab	:= GetNextAlias()

	BeginSql Alias cAliasTab
		SELECT MAX(R0_NROPED) NROPED FROM %table:SR0% SR0
		WHERE SR0.R0_FILIAL = %Exp:(xFilial("SR0", SRA->RA_FILIAL))%
			AND %NotDel%
	EndSql

	cNroPed := StrZero( Val((cAliasTab)->NROPED)+1, nTamPed )

	(cAliasTab)->( dbCloseArea() )

	cFilPed := SRA->RA_FILIAL

	RestArea( aAreaSR0 )
	
EndIf

Return cNroPed

/*/{Protheus.doc} CustoVales
Como podemos ter mais de um pedido por periodo ้ preciso recalcular os custos
@author Flavio Correa
@since 12/08/2016
@version P12.1.07
/*/
Static Function CustoVales(cTipo)
Local aArea			:= GetArea()
Local nCustFunc		:= 0
Local nCalc			:= 0
Local nFechF		:= 0
Local nCustAux		:= 0
Local nI			:= 1
Local aCalc			:= {}
Local cCod			:= ""
Local nTeto			:= 0
Local cAliasQry		:= ""

Private nPercentual	:= RetValSRV(aCodFol[051, 1], SRA->RA_FILIAL, "RV_PERC")

DEFAULT lGp210Sal	:= ExistBlock("GP210SAL")

If cTipo == "0"
	If lGp210Sal
		EXECBLOCK("GP210SAL", .F. , .F. ) //Retorna nPercentual
		If ValType(nPercentual) <> "N"
			nPercentual := 6
		EndIf
	EndIf

	If SRA->RA_CATFUNC $ "E*G"
		nCustFunc := nPercentual := 0
	Else
		nCustFunc := Round(NSALBSVT * ( nPercentual / 100 ),2) //Custo Mensal do Funcionario
	EndIf

	DbSelectArea("SR0")
	SR0->(DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO+R0_PEDIDO")))
	SR0->(DbSeek(SRA->RA_FILIAL+SRA->RA_MAT+cTipo+cPeriodo))
	While !SR0->(Eof()) .And. SR0->(R0_FILIAL+R0_MAT+R0_TPVALE+R0_PERIOD) == SRA->RA_FILIAL+SRA->RA_MAT+cTipo+cPeriodo
		If SR0->R0_PEDIDO == "1"
			nCalc += SR0->R0_VALCAL
			Aadd(aCalc,SR0->(Recno()))
		Else
			nCustFunc := Max(nCustFunc - SR0->R0_VLRFUNC,0)
		EndIf
		SR0->(dbSkip())
	EndDo

	For nI := 1 To Len(aCalc)
		SR0->(dbGoto(aCalc[nI]))
		RecLock("SR0",.F.)
		If nCustFunc > nCalc
			SR0->R0_VLRFUNC := SR0->R0_VALCAL
			SR0->R0_VLREMP := 0
		Else
			If nI == Len(aCalc)
				SR0->R0_VLRFUNC := nCustFunc - nCustAux
			Else
				SR0->R0_VLRFUNC := Round(nCustFunc / nCalc * SR0->R0_VALCAL, 2) //Rateia custo do VT
				nCustAux += SR0->R0_VLRFUNC
			EndIf
			SR0->R0_VLREMP := SR0->R0_VALCAL - SR0->R0_VLRFUNC  //Custo Empresa
		EndIf

		SR0->(msUnlock())

	Next nI
Else
	cAliasQry := GetNextAlias()
	BeginSql alias cAliasQry
		SELECT SR0.R_E_C_N_O_ AS RECSR0,SR0.*
		FROM %table:SR0% SR0
		WHERE R0_FILIAL = %exp:SRA->RA_FILIAL%
		AND R0_MAT = %exp:SRA->RA_MAT%
		AND R0_TPVALE = %exp:cTipo%
 	    AND SR0.%notDel%
 	    ORDER BY R0_PEDIDO DESC
	EndSql
	cCod := ""
	While !(cAliasQry)->(Eof()) .And. (cAliasQry)->(R0_FILIAL+R0_MAT+R0_TPVALE) == SRA->RA_FILIAL+SRA->RA_MAT+cTipo
		SR0->(dbGoto( (cAliasQry)->RECSR0))
		If cCod <> (cAliasQry)->R0_CODIGO
			nTeto := 0
			nFechF := 0
			If RFO->(DbSeek(xFilial("RFO")+(cAliasQry)->(R0_TPVALE + R0_CODIGO)))
		    	nTeto	 := RFO->RFO_TETO
		    EndIf
	    EndIf

	    If nTeto <= 0
	    	cCod := (cAliasQry)->R0_CODIGO
	    	(cAliasQry)->(dbSkip())
	    	Loop
	    EndIf


	    If (nFechF + (cAliasQry)->R0_VLRFUNC) >= nTeto
	    	nVal :=  nTeto  - nFechF
	    	If nVal > 0
		    	RecLock("SR0",.F.)
		    		SR0->R0_VLRFUNC := nVal
		    		SR0->R0_VLREMP  := (cAliasQry)->R0_VALCAL - nVal
		    	SR0->(msUnlock())
		    Else
		    	RecLock("SR0",.F.)
		    		SR0->R0_VLRFUNC := 0
		    		SR0->R0_VLREMP  := (cAliasQry)->R0_VALCAL
		    	SR0->(msUnlock())
		    EndIf
	    Endif
	    nFechF += (cAliasQry)->R0_VLRFUNC

		cCod := (cAliasQry)->R0_CODIGO
		(cAliasQry)->(dbSkip())
	EndDo
	(cAliasQry)->(dbCloseArea())
EndIf
RestArea(aArea)
Return

/*/{Protheus.doc} FaltasDes
faltas ja descontadas da tabela SP7 = faltas da tabela SR0 com pedido = 2
@author Flavio Correa
@since 12/08/2016
@version P12.1.07
/*/
Static Function FaltasDes(cPeriodo,dDt1,dDt2)
Local aArea		:= GetArea()
Local nDescont 	:= 0
Local cAliasQry	:= GetNextAlias()
Local cTipoVale := If(cTipoRot == "8","0",(If(cTipoRot == "D","1","2"))) //0-Vale Transporte / 1-Vale Refei็ใo / 2-Vale Alimenta็ใo

DEFAULT dDt1 	:= aPeriodo[nPosSem,3] //Data inicial do periodo de calculo
DEFAULT dDt2 	:= aPeriodo[nPosSem,4] //Data final do periodo de calculo

dbSelectArea("SP7")

//nao pegar faltas que sao do mesmo periodo(inicio/Fim), pois se for considerar recalculo e sobreescrever as faltas
BeginSql alias cAliasQry
	SELECT SUM(P7_DFALT) TOTALFAL
	FROM %table:SP7% SP7
	WHERE SP7.%notDel%
    AND SP7.P7_FILIAL = %exp:SRA->RA_FILIAL%
    AND SP7.P7_MAT = %exp:SRA->RA_MAT%
    AND SP7.P7_PERIOD = %exp:cPeriodo%
    AND SP7.P7_DATAINI <> %exp:dDt1%
    AND SP7.P7_DATAFIM <> %exp:dDt2%
EndSql
nDescont := (cAliasQry)->TOTALFAL
(cAliasQry)->(DbCloseArea())

BeginSql alias cAliasQry
	SELECT DISTINCT R0_NROPED, R0_FALTAS
	FROM %table:SR0% SR0
	WHERE SR0.%notDel%
    AND SR0.R0_FILIAL = %exp:SRA->RA_FILIAL%
    AND SR0.R0_MAT = %exp:SRA->RA_MAT%
    AND SR0.R0_PERIOD = %exp:cPeriodo%
    AND SR0.R0_TPVALE = %exp:cTipoVale%
    AND SR0.R0_PEDIDO ='2'
EndSql
While !(cAliasQry)->(Eof())
	nDescont += (cAliasQry)->R0_FALTAS
	(cAliasQry)->(dbskip())
EndDo
(cAliasQry)->(DbCloseArea())


RestArea(aArea)
Return nDescont

/*/{Protheus.doc} GravaSP7
Grava faltas descontadas
@author Flavio Correa
@since 12/08/2016
@version P12.1.07
/*/
Static Function GravaSP7(nValor,dDt1,dDt2)
Local aArea	:= GetArea()
Local lNovo	:= .T.

DEFAULT dDt1 	:= aPeriodo[nPosSem,3] //Data inicial do periodo de calculo
DEFAULT dDt2 	:= aPeriodo[nPosSem,4] //Data final do periodo de calculo


dbSelectArea("SP7")

lNovo := !(SP7->(dbSeek(SRA->RA_FILIAL+ SRA->RA_MAT+cPeriodo+dtos(dDt1)+dtos(dDt2))))
RecLock("SP7",lNovo)
	SP7->P7_FILIAL 	:= SRA->RA_FILIAL
	SP7->P7_MAT		:= SRA->RA_MAT
	SP7->P7_PERIOD	:= cPeriodo
	SP7->P7_DFALT	:= nValor
	SP7->P7_DATAINI	:= dDt1
	SP7->P7_DATAFIM	:= dDt2
SP7->(msUnlock())

RestArea(aArea)
Return

/*/{Protheus.doc} NovoCalcBEN
Usa o calculo novo ou nao do beneficios
@author Flavio Correa
@since 12/08/2016
@version P12.1.07
@see http://tdn.totvs.com/x/ikyZDg
/*/
Function NovoCalcBEN()

	Local lNovo := .F.

	If GetRPORelease() >= "12.1.027"
		lNovo := .T.
	Else
		lNovo := GetMvRH("MV_CALCBEN",,.F.)
	EndIf

Return lNovo

/*/{Protheus.doc} fRetBenAnt
Verifica o ๚ltimo m๊s de pagamento de beneficio para o funcionแrio
@author Leandro Drumond
@since 19/08/2017
@version P12.1.17
/*/
Function fRetBenAnt(cFilFunc, cMatFunc, cProcFunc, cTipoVale, aPerAux)

	Local aArea		:= GetArea()
	Local cAnoAux	:= ""
	Local cMesAux	:= ""
	Local dDtIni	:= Ctod("//")
	Local cAliasQuey	:= GetNextAlias()
	Local cTipo		:= If(cTipoVale == "2", "VA", If(cTipoVale == "1", "VR", ""))

	BeginSQL Alias cAliasQuey

		COLUMN RCH_DTINI AS DATE
		COLUMN RCH_DTFIM AS DATE

		SELECT RCH_PER, RCH_NUMPAG, RCH_MES, RCH_ANO, RCH_DTINI, RCH_DTFIM, RCH_DTFECH

		FROM %Table:RCH% RCH

		WHERE RCH.RCH_FILIAL = %Exp:xFilial("RCH", cFilFunc)%
		AND RCH.RCH_PROCES = %Exp:cProcFunc%
		AND RCH.RCH_ROTEIR = %Exp:fGetCalcRot(cTipoRot)%
		AND RCH.%notDel%

		ORDER BY RCH_DTINI DESC

	EndSQL

	// ฺltimo perํodo fechado
	While ! (cAliasQuey)->(Eof())
		If ! Empty((cAliasQuey)->RCH_DTFECH)
			cAnoAux := (cAliasQuey)->RCH_ANO
			cMesAux := (cAliasQuey)->RCH_MES
			dDtIni	:= (cAliasQuey)->RCH_DTINI
			Exit
		EndIf
		(cAliasQuey)->(dbSkip())
	EndDo

	(cAliasQuey)->(dbCloseArea())

	BeginSQL Alias cAliasQuey

		SELECT LY_AGRUP

		FROM %Table:SLY% SLY

		WHERE SLY.LY_FILIAL = %Exp:xFilial("SLY", cFilFunc)%
		AND SLY.LY_TIPO = %Exp:cTipo%
		AND SLY.LY_DTINI <= %Exp:dTos(dDtIni)%
		AND SLY.%notDel%

	EndSQL

	// S๓ faz o recalculo se no perํdo havia calculo automแtico configurado
	If ! (cAliasQuey)->(Eof())
		fRetPerComp(cMesAux, cAnoAux, , cProcFunc, fGetCalcRot(cTipoRot), , , @aPerAux)
	EndIf

	(cAliasQuey)->(dbCloseArea())

	RestArea(aArea)

Return Nil

/*/{Protheus.doc} NroDias
Calcula o n๚mero de dias que deve ser pago para o benefํcio em determinada entidade
@author cicero.pereira
@since 30/09/2017
@param cEntidade, characters, Entidade do crit้rio que estแ sendo calculada
@param cChave, characters, C๓digo referente a entidade informada
@param dDataDe, date, Inํcio do perํodo
@return nDias, numerico
@param dDataAte, date, Fim do perํodo
@sample
	NroDias("CTT", "000000001", cTod("01/09/2017"), cTod("30/09/2017"))
	Verifica quantos dias o funcionแrio ficou no Centro de custo 000000001, verificando transfer๊ncias
	e retorna a quantidades de dias vแlidos para o pagamento do benefํcio
/*/
Static Function NroDias(cEntidade, cChave, dDataDe, dDataAte, cPeriod, lRepete)

	Local nDias		:= 0
	Local nI		:= 0
	Local nPosDe	:= 0
	Local nPosPara	:= 0
	Local nPosData	:= 7
	Local aTransf	:= {}
	Local dAuxPara	:= Ctod("//")

	Default cPeriod := ""
	Default lRepete := .F.

	If Empty(_aDias)
		fCarrAdias(dDataDe, dDataAte, cPeriod)
	EndIf

	If cEntidade == "CTT" // Centro de Custo
		fTransf(@aTransf, MesAno(dDataDe), , , .T., , , , , , , , SRA->RA_FILIAL, SRA->RA_MAT)
		nPosDe		:= 3
		nPosPara	:= 6
	ElseIf cEntidade == "SQB" // Departamento
		fTransf(@aTransf, MesAno(dDataDe), , , , , , , , .T., , , SRA->RA_FILIAL, SRA->RA_MAT)
		nPosDe		:= 16
		nPosPara	:= 17
	ElseIf cEntidade == "RCL" // Posto
		fTransf(@aTransf, MesAno(dDataDe), , , , , , , , , .T., , SRA->RA_FILIAL, SRA->RA_MAT)
		nPosDe		:= 18
		nPosPara	:= 19
	ElseIf cEntidade == "SM0" // Filial
		fTransf(@aTransf, MesAno(dDataDe), , .T., , , , , , , , , SRA->RA_FILIAL, SRA->RA_MAT)
		nPosDe		:= 2
		nPosPara	:= 5
		cChave += SRA->RA_MAT
	ElseIf cEntidade == "RCE" .or.  cEntidade == "SWY"// Sindicato
		aTransf		:= HistSRA("RCE")
		nPosData	:= 1
		nPosDe		:= 2
		nPosPara	:= 3
	ElseIf cEntidade == "SQ3" // Cargo
		aTransf		:= HistSRA("SQ3")
		nPosData	:= 1
		nPosDe		:= 2
		nPosPara	:= 3
	ElseIf cEntidade == "SR6" // Turno
		aTransf		:= HistSPF(dDataDe, dDataAte)
		nPosData	:= 1
		nPosDe		:= 2
		nPosPara	:= 3
	ElseIf cEntidade == "SRJ" // Fun็ใo
		aTransf		:= HistSR7()
		nPosData	:= 1
		nPosDe		:= 2
		nPosPara	:= 3
	EndIf

	If ! Empty(aTransf)
		For nI := 1 To Len(aTransf)
			If ( cEntidade == "SWY" .and. aTransf[nI][nPosDe] $ cChave ) .or. ( cEntidade <> "SWY" .and. aTransf[nI][nPosDe] == cChave ) // Saindo
				If ! Empty(dAuxPara)
					nDias += VldDiasRH(dAuxPara, aTransf[nI][nPosData], lRepete)
					dAuxPara := Ctod("//")
				Else
					nDias += VldDiasRH(dDataDe, aTransf[nI][nPosData], lRepete)
				EndIf
			ElseIf ( cEntidade == "SWY" .and. aTransf[nI][nPosPara] $ cChave ) .or. ( cEntidade <> "SWY" .and. aTransf[nI][nPosPara] == cChave ) // Entrando
				dAuxPara := aTransf[nI][nPosData]
			EndIf
		Next
		If ! Empty(dAuxPara)
			nDias += VldDiasRH(dAuxPara, dDataAte + 1, lRepete)
		EndIf
	Else
		nDias += VldDiasRH(dDataDe, dDataAte + 1, lRepete)
	EndIf

Return nDias

/*/{Protheus.doc} HistSPF
Busca hist๓rico de altera็๕es do turno de trabalho
@author cicero.pereira
@since 02/10/2017
@param dInicio, date, Data inicial da pesquisa
@param dFim, date, Data final da pesquisa
/*/
Function HistSPF(dInicio, dFim)

	Local cAliasSPF := GetNextAlias()
	Local aAlt		:= {}

	BeginSQL Alias cAliasSPF
		Column  PF_DATA As Date
		SELECT PF_DATA, PF_TURNODE, PF_TURNOPA
		FROM %Table:SPF% SPF
		WHERE PF_FILIAL = %Exp:xFilial("SPF", SRA->RA_FILIAL)%
		AND SPF.PF_MAT = %Exp:SRA->RA_MAT%
		AND SPF.PF_DATA >= %Exp: dTos(dInicio)%
		AND SPF.PF_DATA <= %Exp: dTos(dFim)%
		AND SPF.%notDel%
	EndSQL

	While ! (cAliasSPF)->(Eof())
		Aadd(aAlt, {(cAliasSPF)->PF_DATA, Alltrim((cAliasSPF)->PF_TURNODE), Alltrim((cAliasSPF)->PF_TURNOPA)})
		(cAliasSPF)->(dbSkip())
	EndDo

	(cAliasSPF)->(dbCloseArea())

Return aAlt

/*/{Protheus.doc} VldDiasRH
Retorna quanto dias vแlidos para o pagamento do benefํcio, atualiza o _aDias
@author cicero.pereira
@since 30/09/2017
@param dInicio, date, Inํcio do perํodo
@param dFim, date, Fim do perํodo
/*/
Static Function VldDiasRH(dInicio, dFim, lRepete)

	Local nDias	:= 0
	Local nI	:= 0
	Local lGS   := SuperGetMV("MV_TECXRH", NIL, .F. ) .AND. SuperGetMV("MV_GSBENAG", NIL, .F. ) .AND. FindFunction("TecBVldBnf")

	Default lRepete := .F.

	For nI := 1 To Len(_aDias)
		If _aDias[nI][1] >= dInicio .And. _aDias[nI][1] < dFim .And. (_aDias[nI][Len(_aDias[nI])] == 0 .Or. lRepete)
			If lGS
				IF TecBVldBnf(_aDias[nI],SRA->RA_FILIAL, SRA->RA_MAT)
					nDias++
					If cTipoRot != "I"
						_aDias[nI][Len(_aDias[nI])] := 1
					EndIf
				EndIf
			ElseIf _aDias[nI][4]
				nDias++
				If cTipoRot != "I"
					_aDias[nI][Len(_aDias[nI])] := 1
				EndIf
			EndIf
		EndIf
	Next

Return nDias

/*/{Protheus.doc} HistSRA
Busca o Hist๓rico de altera็๕es de um campo da SRA em determinado perํodo
@author cicero.pereira
@since 29/09/2017
@param cEntidade, characters, Define qual campo serแ buscado. Ex.: Entidade RCE retorna o hit๓rico do campo RA_SINDICA
@param dInicio, date, Data para inicio da busca
@return aAlt, array
@example
	HistSRA("RCE", cTod("01/09/2017"))

	Retorna o array aAlt com as altera็๕es do campo RA_SINDICA sendo:
	aAlt[nI][1] = Data da altera็ใo
	aAlt[nI][1] = Conte๚do De
	aAlt[nI][1] = Conte๚do Para
/*/
Static Function HistSRA(cEntidade)

	Local cAliasSR9 := GetNextAlias()
	Local aAlt		:= {}
	Local cCampo	:= ""
	Local cCodAux	:= ""
	Local lFrist	:= .T.

	If cEntidade == "RCE"
		cCampo := "RA_SINDICA"
	ElseIf cEntidade == "SQ3"
		cCampo := "RA_CARGO"
	EndIf

	BeginSQL Alias cAliasSR9
		Column  R9_DATA As Date
		SELECT R9_DATA, R9_DESC
		FROM %Table:SR9% SR9
		WHERE SR9.R9_FILIAL = %Exp:SRA->RA_FILIAL%
		AND SR9.R9_MAT = %Exp:SRA->RA_MAT%
		AND SR9.R9_CAMPO = %Exp:cCampo%
		AND SR9.%notDel%
	EndSQL

	While ! (cAliasSR9)->(Eof())
		If lFrist
			cCodAux := Alltrim((cAliasSR9)->R9_DESC)
			lFrist := .F.
		Else
			Aadd(aAlt, {(cAliasSR9)->R9_DATA, cCodAux, Alltrim((cAliasSR9)->R9_DESC)})
			cCodAux := Alltrim((cAliasSR9)->R9_DESC)
		EndIf
		(cAliasSR9)->(dbSkip())
	EndDo
	(cAliasSR9)->(dbCloseArea())

Return aAlt

/*/{Protheus.doc} HistSR7
Busca o hist๓rico das altera็๕es de fun็ใo do funcionแrio posicionado
@author cicero.pereira
@since 29/09/2017
@return aAlt, Array, Array contendo todas as altera็๕es de fun็ใo do funcionแrio
@sample
	HistSR7()

	Retorna aAlt sendo:
	aAlt[nI][1] = Data da altera็ใo
	aAlt[nI][2] = Fun็ใo De
	aAlt[nI][2] = Fun็ใo Para
/*/
Static Function HistSR7()

	Local cAliasSR7 := GetNextAlias()
	Local aAlt		:= {}
	Local lFrist	:= .T.
	Local cFuncAux	:= ""

	BeginSQL Alias cAliasSR7
		Column  R7_DATA As Date
		SELECT R7_DATA, R7_FUNCAO
		FROM %Table:SR7% SR7
		WHERE SR7.R7_FILIAL = %Exp:SRA->RA_FILIAL%
		AND SR7.R7_MAT = %Exp:SRA->RA_MAT%
		AND SR7.%notDel%
	EndSQL

	While ! (cAliasSR7)->(Eof())
		If lFrist
			cFuncAux := Alltrim((cAliasSR7)->R7_FUNCAO)
			lFrist := .F.
		ElseIf cFuncAux != Alltrim((cAliasSR7)->R7_FUNCAO)
			Aadd(aAlt, {(cAliasSR7)->R7_DATA, cFuncAux, Alltrim((cAliasSR7)->R7_FUNCAO)})
			cFuncAux := Alltrim((cAliasSR7)->R7_FUNCAO)
		EndIf
		(cAliasSR7)->(dbSkip())
	EndDo
	(cAliasSR7)->(dbCloseArea())

Return aAlt

/*/{Protheus.doc} fCarrAdias
Carrega os dias do m๊s validando em quais dias devem ser pagos os benefํcios
@author cicero.pereira
@since 30/09/2017
@param dInicio, date, Inicio do perํodo
@param dFim, date, Fim do perํodo
@param cPeriod, caracter, C๓digo do perํodo que serแ carregado
/*/
Static Function fCarrAdias(dInicio, dFim, cPeriod)

	Local nI, nJ		:= 0
	Local lPropAdm		:= .F. //APERGUNTE[1,3] == 1	// Se calcula proporcional a admissใo
	Local lDesAfast		:= .F. //APERGUNTE[2,3] <> 2	// Se desconta f้rias/astamentos
	Local lDesFerProg	:= .F. //APERGUNTE[3,3] == 1	// Se calcula proporcional a f้rias programadas
	Local lDesFaltas	:= .F. //APERGUNTE[4,3] == 1	// Se desconta faltas
	Local cTipoAfas		:= ""
	Local aAfast		:= {}
	Local aPerAnt		:= {}
	//Local nAfaFer	:= If(cTipoRot == "I", 0, If(cTipoRot == '8', aPergunte[3,3], aPergunte[2,3]))
	Static _aDias		:= {}

	Default cPeriod := ""
	DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Mem๓ria de Cแlculo

	If cTipoRot == "8"
		lPropAdm	:= APERGUNTE[2,3] == 1	// Se calcula proporcional a admissใo
		lDesAfast	:= APERGUNTE[3,3] <> 2	// Se desconta f้rias/astamentos
		lDesFerProg	:= APERGUNTE[4,3] == 1	// Se calcula proporcional a f้rias programadas
		lDesFaltas	:= APERGUNTE[5,3] == 1	// Se desconta faltas
	ElseIf cTipoRot $ "D*E"
		lPropAdm	:= APERGUNTE[1,3] == 1	// Se calcula proporcional a admissใo
		lDesAfast	:= APERGUNTE[2,3] <> 2	// Se desconta f้rias/astamentos
		lDesFerProg	:= APERGUNTE[3,3] == 1	// Se calcula proporcional a f้rias programadas
		lDesFaltas	:= APERGUNTE[4,3] == 1	// Se desconta faltas
	EndIf

	If __lMemCalc
		fAddMemLog("Proporcional a f้rias : " + If(lDesFerProg,"Sim","Nใo") ,1,1)
		fAddMemLog("Proporcional a admissใo : " + If(lPropAdm,"Sim","Nใo") ,1,1)
		fAddMemLog("Desconta faltas  : " + If(lDesFaltas,"Sim","Nใo") ,1,1)
		fAddMemLog("Desconta afastamentos  : " + If(lDesAfast,"Sim","Nใo") ,1,1)
	EndIf

	If !Empty(cPeriod) // Se estiver preenchido estแ recalculando o m๊s anterior
		fCarPeriodo( cPeriod , cRot, @aPerAnt, , , ! IsBlind(), )
		fCarDiasMes(@_aDias, lPropAdm, aPerAnt)
	Else
		fCarDiasMes(@_aDias, lPropAdm)
	EndIf

	If lDesAfast
		If cTipoRot == "8"
			If aPergunte[2,3] > 1
				cTipoAfas := If(aPergunte[2,3] == 3, '4','123')
			EndIf
		Else
			If aPergunte[2,3] > 1
				cTipoAfas := If(aPergunte[3,3] == 3, '4','123')
			EndIf
		EndIf

		fRetAfas(dInicio, dFim, , , ,cTipoAfas , @aAfast)
		For nI := 1 To Len(aAfast)
			For nJ := 1 To Len(_aDias)
				If _aDias[nJ][1] >= aAfast[nI][3] .And. (_aDias[nJ][1] <= aAfast[nI][4] .Or. Empty(aAfast[nI][4]))
					_aDias[nJ][4] := .F.
				EndIf
			Next
		Next
	EndIf

	If lDesFerProg
		fVlsFerPrg(dInicio, dFim, @_aDias, .T.)
	EndIf

	For nI := 1 To Len(_aDias)
		Aadd(_aDias[nI], 0)
	Next

Return

/*/{Protheus.doc} AtuSM7Aut
Grava Registro na SM7
@author cicero.pereira
@since 09/10/2017
@version 12.1.17
@param cTpVale, characters, Tipo do vale: 1 Vale Refei็ใo; 2 Vale Alimenta็ใo
@param cCodBen, characters, C๓digo do benefํcio
/*/
Static Function AtuSM7Aut(cTpVale, cCodBen)

	Local aArea	:= GetArea()

	dbSelectArea("SM7")

	RecLock("SM7", .T.)
		SM7->M7_FILIAL := xFilial("SM7", SRA->RA_FILIAL)
		SM7->M7_MAT := SRA->RA_MAT
		SM7->M7_TPCALC := "2"	// Cแlculo automแtico
		SM7->M7_TPVALE := cTpVale
		SM7->M7_CODIGO := cCodBen
		SM7->M7_COMPL := "2"
	SM7->(MSUnlock())

	RestArea(aArea)

Return

/*/{Protheus.doc} fVlDemMs
Verifica demissao mes seguinte para pagamento de beneficios
@author Esther de Viveiro
@since 03/10/2017
@version P12.1.14
/*/
Function fVlDemMs()
	Local aArea		:= GetArea()
	Local lGpem040 	:= FunName() == "GPEM040" .or. FunName() == "GPEM040B"

	If !lGpem040 //Se esta sendo calculado pela rescisใo, a data de demissใo jแ esta preenchida, mas a SRG ainda nใo foi gravada.
		dDataDem	:= StoD("//")

		//verifica se ha calculo de rescisao para o periodo de processamento do beneficio
		SRG->( DbSetOrder(1) )	// Ordem 1 - RetOrdem( "SRG", "RG_FILIAL+RG_MAT+DTOS(RG_DTGERAR)")
		If SRG->(DbSeek( SRA->RA_FILIAL + SRA->RA_MAT))
			While SRG->(!Eof()) .and. SRG->(RG_FILIAL+RG_MAT) == SRA->(RA_FILIAL + RA_MAT)
				If SRG->RG_EFETIVA == 'S' .AND. AnoMes(SRG->RG_DATADEM) <= cPeriodo
					dDataDem := (SRG->RG_DATADEM)
				EndIf
				SRG->(dbSkip())
			EndDo
		EndIf

		RestArea(aArea)
	EndIf
Return Nil

/*/{Protheus.doc} RestXCBEN
Reinicia variแveis Static do fonte GPEXCBEN
@author cicero.pereira
@since 14/11/2017
/*/
Function RestXCBEN()

	nFaltasAut	:= 0
	nDiasRef	:= 0
	aCriterio 	:= {}
	lUsaCPer	:= GetMvRH("MV_USACPER",, .F.) //.T.=Usa periodo folha/.F.= usa calendario ponto
	cTipoCri	:= ""

Return

/*/{Protheus.doc} RestXCBEN
Reinicia cNroPed
@author Allyson Mesashi
@since 30/07/2021
/*/
Function RstNroPed()

cNroPed := ""

Return

/*/{Protheus.doc} fLoadCCT
Carrega sindicatos associados a CCT
@author Leandro Drumond
@since 23/05/2022
/*/
Static Function fLoadCCT(cCodCCT, cFilSRA)
Local aRet  		:= {"##"}
Local cAliasQry 	:= GetNextAlias()
Local cFilRCE       := xFilial("RCE", cFilSRA)

BeginSql alias cAliasQry
	SELECT RCE_CODIGO
	FROM %table:RCE% RCE
	WHERE RCE_CCT = %exp:cCodCCT% AND
	RCE_FILIAL = %exp:cFilRCE% AND
	RCE.%notDel%
EndSql

If !(cAliasQry)->( EOF() )
	aRet := {}
	While !(cAliasQry)->( EOF() )
		aAdd(aRet, (cAliasQry)->RCE_CODIGO)
		(cAliasQry)->( DbSkip() )
	EndDo
EndIf

(cAliasQry)->(DbCloseArea())

Return aRet

/*/{Protheus.doc} fBuscaCCT
Busca o c๓digo da CCT associado ao sindicato
@author Leandro Drumond
@since 02/06/2022
/*/
Static Function fBuscaCCT(cSindicato)
Local aArea		:= GetArea()
Local cRet		:= ""

DbSelectArea("RCE")
RCE->(DbSetOrder(1))

If RCE->(DbSeek(xFilial("RCE") + cSindicato))
	cRet := RCE->RCE_CCT
EndIf

RestArea(aArea)

Return cRet

/*{Protheus.doc} GetDiasTab
Carrega os dias de beneficio de acordo com a tabela de horแrio padrใo
@author Leandro Drumond
@since 23/08/2022
*/
Static Function GetDiasTab(aTabCalend,aDiaVales,lPropFerias,lFeriado,lVT,nDUtil,nDiaNUtil,nDiasSeg,nDiasTer,nDiasQua,nDiasQui,nDiasSex,nDiasSab,nDiasDom,nDias,nDiaNTran)
Local dDtAux	:= CtoD("")
Local nX 		:= 0
Local nDiaSem 	:= 0
Local nPos 		:= 0
Local lSoma 	:= .F.
Local cDiaSem	:= ""

For nX := 1 to Len(aTabcalend)
	lSoma	:= .F.
	cDiaSem := Alltrim( Upper(cDow( aTabCalend[nX][1] ) ))

	Do Case
		Case cDiaSem == "MONDAY"
			nDiaSem := 1
		Case cDiaSem == "TUESDAY"
			nDiaSem := 2
		Case cDiaSem == "WEDNESDAY"
			nDiaSem := 3
		Case cDiaSem == "THURSDAY"
			nDiaSem := 4
		Case cDiaSem == "FRIDAY"
			nDiaSem := 5
		Case cDiaSem == "SATURDAY"
			nDiaSem := 6
		Otherwise
			nDiaSem := 7
	EndCase

	If aDiaVales[nDiaSem] > 0
		If aTabCalend[nX][6] == "S" .and. nDUtil > 0
			nDias++
		ElseIf aTabCalend[nX][6] <> "S" .and. nDiaNUtil > 0
			nDiaNTran++
		EndIf
	EndIf

	dDtAux := aTabCalend[nX][1]

	If lVT .or. ( aDiasMes[Ascan( aDiasMes, { |y| y[1] == dDtAux })][4] .or.(  aTabCalend[nX][19] == .F. .And. nDiaNUtil > 0 .And. aDiaVales[nDiaSem] > 0 ) )
		If lPropFerias			
			nPos := aScan(aDiasMes,{|y| y[1] == dDtAux})
			If !lVT .or. ( nPos == 0 .or. aDiasMes[nPos,5] <> 'PRG' ) //Se for vale transporte ou o dia estiver configurado para pagamento de beneficio
				If (Empty(dDataDem) .OR. aTabCalend[nX][1] < dDataDem)
					If ( aTabCalend[nX][6] == "S" .And. !aTabCalend[nX][19] ) ; //Se tipo de dia ้ trabalhado e nใo ้ feriado
						.or. ;
					   ( !aTabCalend[nX][19] .And. nDiaNUtil > 0 .And. aDiaVales[nDiaSem] > 0) ; //Se dia nใo for trabalhado, nใo for feriado, pagar dias nใo ๚teis e pagar vale no dia especifico da semana
						.or. ;
						( lFeriado .and. ( aTabCalend[nX][6] == "F" .And. !aTabCalend[nX][24] .And. aTabCalend[nX][36] == "S") ) ; //Se for feriado, pagar feriado  e funcionแrio nใo estiver afastado
						.or. ;
						( lFeriado .and.  ( aTabCalend[nX][6] == "S" .And. !aTabCalend[nX][24] .And. aTabCalend[nX][36] == "S" .And. aTabCalend[nX][19]) )
							lSoma := .T.
					EndIf 
				EndIf 
			EndIf
		Else
			If (Empty(dDataDem) .OR. aTabCalend[nX][1] < dDataDem)
				If ( aTabCalend[nX][6] == "S" .And. !aTabCalend[nX][19] ) ;
					.or. ;
				( !aTabCalend[nX][19] .And. nDiaNUtil > 0 .And. aDiaVales[nDiaSem] > 0) ;
					.or. ;
				( lFeriado .and. (aTabCalend[nX][6] == "F" .And. !aTabCalend[nX][24] .And. aTabCalend[nX][36] == "S" ) ) ;
					.or. ;
				( lFeriado .and. (aTabCalend[nX][6] == "S" .And. !aTabCalend[nX][24] .And. aTabCalend[nX][36] == "S" .And. aTabCalend[nX][19]) ) ;
					.or. ;
				( aTabCalend[nX][6] == "N" .And. !aTabCalend[nX][19] .And. aTabCalend[nX][24] .And. aTabCalend[nX][36] == "S" ) 
					lSoma := .T.
				EndIf 
			EndIf 
		EndIf

		If lSoma
			Do Case
				Case cDiaSem == "MONDAY"
					nDiasSeg++
				Case cDiaSem == "TUESDAY"
					nDiasTer++
				Case cDiaSem == "WEDNESDAY"
					nDiasQua++
				Case cDiaSem == "THURSDAY"
					nDiasQui++
				Case cDiaSem == "FRIDAY"
					nDiasSex++
				Case cDiaSem == "SATURDAY"
					nDiasSab++
				Otherwise
					nDiasDom++
			EndCase	
		EndIf
	EndIf
Next nX

If __lMemCalc
	fAddMemLog("Qtd dias de Segunda : " +  cValtoChar(nDiasSeg) ,1,1)
	fAddMemLog("Qtd dias de Ter็a   : " +  cValtoChar(nDiasTer) ,1,1)
	fAddMemLog("Qtd dias de Quarta  : " +  cValtoChar(nDiasQua) ,1,1)
	fAddMemLog("Qtd dias de Quinta  : " +  cValtoChar(nDiasQui) ,1,1)
	fAddMemLog("Qtd dias de Sexta   : " +  cValtoChar(nDiasSex) ,1,1)
	fAddMemLog("Qtd dias de Sabado  : " +  cValtoChar(nDiasSab) ,1,1)
	fAddMemLog("Qtd dias de Domingo : " +  cValtoChar(nDiasDom) ,1,1)
EndIf

Return Nil
