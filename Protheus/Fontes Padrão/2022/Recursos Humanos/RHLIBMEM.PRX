#INCLUDE "PROTHEUS.CH"

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInRhLibMemExecЁAutor ЁMarinaldo de Jesus   Ё Data Ё14/05/2003Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de RHLIBMEM                          Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInRhLibMemExec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InRhLibMemExec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁMsMmObtemRec	ЁAutorЁMarinaldo de Jesus Ё Data Ё22/07/2003Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁObtem os Recnos e as Chaves correspondentes no SYP			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                      									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function MsMmObtemRec(	cAlias 			,;	//Alias do Arquivo que contem o campo memo para Msmm
						aAliasRecnos	,;	//Array com os Recnos 
						aCposMemo		,;	//Array contendo os campos memo (1->Conteudo;2->Codigo)
						aSYPRecnos		,;	//Array com os Recnos do SYP ( por referencia )
						aSYPRecKeys		,;	//Array com as Chaves referente aos Recnos ( por referencia )
						lGetKeys		,;	//Se carrega o Array com as Chaves
						cAliasMsMm		 ;	//Alias para a MsMm
					)

Local cSYPFil
Local cKey
Local cFieldFil
Local cFieldChv
Local cFieldSeq
Local cPrefixoCpo
Local nFieldFil
Local nFieldChv
Local nRecno
Local nMemo
Local nRecnos
Local nMemos

DEFAULT cAlias			:= Alias()
DEFAULT aAliasRecnos	:= {}
DEFAULT aCposMemo		:= {}
DEFAULT lGetKeys		:= .F.
DEFAULT cAliasMsMm		:= "SYP"

cAliasMsMm	:= Upper( AllTrim( cAliasMsMm ) )
cSYPFil		:= xFilial( cAliasMsMm )
cPrefixoCpo	:= ( PrefixoCpo( cAliasMsMm ) + "_" )
cFieldFil	:= ( cPrefixoCpo + "FILIAL" )
cFieldChv	:= ( cPrefixoCpo + "CHAVE"  )
cFieldSeq	:= ( cPrefixoCpo + "SEQ"  	)
nFieldFil	:= ( cAliasMsMm )->( FieldPos( cFieldFil ) )
nFieldChv	:= ( cAliasMsMm )->( FieldPos( cFieldChv ) )
nFieldSeq	:= ( cAliasMsMm )->( FieldPos( cFieldSeq ) )

aSYPRecnos	:= {}
aSYPRecKeys := {}

nRecnos	:= Len( aAliasRecnos )
nMemos	:= Len( aCposMemo )
For nRecno := 1 To nRecnos
	( cAlias )->( dbGoto( aAliasRecnos[ nRecno ] ) )
	For nMemo := 1 To nMemos
		cKey := ( cSYPFil + ( cAlias )->( __ExecMacro( aCposMemo[nMemo,02] ) ) )
		IF ( cAliasMsMm )->( dbSeek( cKey , .F. ) )
			While ( cAliasMsMm )->( !Eof() .and. cKey == ( FieldGet( nFieldFil ) + FieldGet( nFieldChv ) ) )
				( cAliasMsMm )->( aAdd( aSYPRecnos , Recno() ) )
				IF ( lGetKeys )
					( cAliasMsMm )->( aAdd( aSYPRecKeys , ( cKey + FieldGet( nFieldSeq ) ) ) )
				EndIF
				( cAliasMsMm )->( dbSkip() )
			End While
		EndIF
	Next nMemo
Next nRecno

Return( NIL )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁSetMemoFields	 ЁAutorЁMarinaldo de JesusЁ Data Ё12/04/2004Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSeta o Array aMemo para carga dos Memos Virtuais			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function SetMemoFields( cAlias , cMemoDb )

Local cKeyaMemo
Local nLoop
Local nLoops

Static __cKeyaMemo__
Static __aMemosHeader__
Static __aMemosVirtua__
Static __aMemos__

DEFAULT cMemoDb				:= "SYP"

DEFAULT __cKeyaMemo__		:= "__cKeyaMemo__"
DEFAULT __aMemosHeader__	:= {}
DEFAULT __aMemosVirtua__	:= {}
DEFAULT __aMemos__			:= {}
cKeyaMemo := ( FwCodFil() + FwCodEmp("SYP") + cAlias )
IF ( !( __cKeyaMemo__ == cKeyaMemo ) )
	__aMemosVirtua__	:= {}
	__aMemos__ 			:= {}
	__aMemosHeader__	:= GdMontaHeader(	NIL					,;	//01 -> Por Referencia contera o numero de campos em Uso
											@__aMemosVirtua__	,;	//02 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Virtuais
											NIL					,;	//03 -> Por Referencia contera os Campos do Cabecalho da GetDados que sao Visuais
											cAlias				,;	//04 -> Opcional, Alias do Arquivo Para Montagem do aHeader
											NIL					,;	//05 -> Opcional, Campos que nao Deverao constar no aHeader
											.T.					,;	//06 -> Opcional, Carregar Todos os Campos
											.F.			 		,;	//07 -> Nao Carrega os Campos Virtuais
											.F.					,;	//08 -> Carregar Coluna Fantasma
											.F.					,;	//09 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
											.F.					,;	//10 -> Verifica se Deve verificar se o campo eh usado
											.F.					 ;	//11 -> Verifica se Deve verificar o nivel do usuario
			   							)
	nLoops := Len( __aMemosHeader__ )
	For nLoop := 1 To nLoops
		IF ( __aMemosHeader__[ nLoop , 08 ] == "M" )
			IF ( aScan( __aMemosVirtua__ , { |x| ( x == __aMemosHeader__[ nLoop , 02 ] ) } ) > 0 )
				aAdd( __aMemos__ , { NIL , __aMemosHeader__[ nLoop , 02 ] , cMemoDb } )
			EndIF
		EndIF
	Next nLoop
EndIF
_SetOwnerPrvt( "aMemos" , aClone( __aMemos__ ) )

Return( NIL )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGetMemoDb		 ЁAutorЁMarinaldo de JesusЁ Data Ё12/04/2004Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁObtem o Alias para o arquivo de Memos Virtuais   			Ё	
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function GetMemoDb( cAlias , cModName )

Local cMemoDb		:= "SYP"

DEFAULT cAlias		:= ""
DEFAULT cModName	:= cModulo

IF (;
		( cModName == "APD" );
		.or.;
		(;
			( cAlias >= "RD0" );
			.and.;
			( cAlias <= "RDY" );
		 );
	)
	cMemoDb := "RDY"
EndIF

Return( cMemoDb )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁMsMmDel			ЁAutorЁMarinaldo de Jesus Ё Data Ё22/07/2003Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁExclusao de Memos Gerados pela MsMm         				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                      									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function MsMmDel( cAlias , aAliasRecnos , aCposMemo , cAliasMsMm )
                
Local nRecno			:= 0
Local nRecnos			:= 0
Local nCpoMemo			:= 0
Local nCposMemo			:= 0

DEFAULT cAlias			:= Alias()
DEFAULT aAliasRecnos	:= {}
DEFAULT aCposMemo		:= {}
DEFAULT cAliasMsMm		:= "RDY"

IF ( ( nCposMemo := Len( aCposMemo ) ) > 0 )
	IF ( ( nRecnos := Len( aAliasRecnos ) ) > 0 )
		For nRecno := 1 To nRecnos
			( cAlias )->( dbGoto( aAliasRecnos[ nRecno ] ) )
			For nCpoMemo := 1 To nCposMemo
				IF ( ( cAlias )->( FieldPos( aCposMemo[nCpoMemo,02] ) ) > 0 )
					MsMm(	( cAlias )->( __ExecMacro( aCposMemo[nCpoMemo,02] ) )	,;	
							NIL											,;
							NIL											,;
							NIL											,;
							2											,;
							NIL											,;
							NIL											,;
							cAlias										,;
							aCposMemo[nCpoMemo,02]						,;
							cAliasMsMm									 ;
						)
				EndIF
			Next nCpoMemo
		Next nRecno
	EndIF	
EndIF

Return( NIL )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁfGravaMemoЁ Autor Ё Equipe RH             Ё Data Ё 11/10/05 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁGrava campos Memo.                      					  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe	 Ё fGravaMemo()												  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function fGravaMemos(aMemos,cAlias)
Local aArea := GetArea()
Local nY,cVar,cVar1

dbSelectArea( cAlias )

If Len(aMemos) > 0
	For nY := 1 to Len(aMemos)
		cVar       := aMemos[nY][2]
		cVar1      := aMemos[nY][1]
		cAliasMemo := If(len(aMemos[nY]) == 3,aMemos[nY][3],Nil)
		MSMM(&cVar1,TamSx3(aMemos[nY][2])[1],,&cVar,1,,,cAlias,aMemos[nY][1],cAliasMemo)
	Next nY
EndIf

RestArea( aArea )

Return