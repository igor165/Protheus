#INCLUDE "PROTHEUS.CH"
#INCLUDE "APDA150.CH"

/*/
зддддддддддбддддддддддбдддддбдддддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁAPDA150   ЁAutorЁMarinaldo de Jesus       Ё Data Ё26/04/2004Ё
цддддддддддеддддддддддадддддадддддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Periodos                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                    Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё            ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL           Ё
цддддддддддддбддддддддддбдддддддддддбддддддддддддддддддддддддддддддддддд╢
ЁProgramador ЁData      ЁBOPS       ЁMotivo da Alteracao                Ё
цддддддддддддеддддддддддедддддддддддеддддддддддддддддддддддддддддддддддд╢
ЁCecilia Car.Ё03/07/2014ЁTPZWBQ     ЁIncluido o fonte da 11 para a 12 e Ё
Ё            Ё          Ё           Ёefetuada a limpeza.                Ё  
юддддддддддддаддддддддддадддддддддддаддддддддддддддддддддддддддддддддддды/*/
Function APDA150( cAlias , nReg , nOpc , lExecAuto )

Local aArea 	:= GetArea()
Local aAreaRdu	:= RDU->( GetArea() )
Local lExistOpc	:= ( ValType( nOpc ) == "N" )

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSo Executa se o Modo de Acesso dos Arquivos do Modulo APD estiЁ
	Ёverem OK													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( ApdRelationFile() )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁRedefine o Alias                                              Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cAlias	:= "RDU"

	Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina
	
	Private cCadastro	:= OemToAnsi( STR0006 ) //"Cadastro de Rede"

	IF ( lExistOpc )
    
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁGarante o Posicinamento do Recno                              Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		DEFAULT nReg	:= ( cAlias )->( Recno() )
		IF !Empty( nReg )
			( cAlias )->( MsGoto( nReg ) )
		EndIF

		DEFAULT lExecAuto := .F.
		IF ( lExecAuto )

			nPos := aScan( aRotina , { |x| x[4] == nOpc } )
			IF ( nPos == 0 )
				Break
			EndIF
			bBlock := &( "{ |a,b,c,d| " + aRotina[ nPos , 2 ] + "(a,b,c,d) }" )
			Eval( bBlock , cAlias , nReg , nPos )
		
		Else
		
			Apda150Mnt( cAlias , nReg , nOpc , .T. )
		
		EndIF	

	Else
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Chama a Funcao de Montagem do Browse                                   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		mBrowse( 6 , 1 , 22 , 75 , cAlias )

	EndIF		

End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Coloca o Ponteiro do Mouse em Estado de Espera			   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()
		
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura os Dados de Entrada 											 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aAreaRdu )
RestArea( aArea    )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura o Estado do Ponteiro do Mouse						   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( NIL )

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInApda150Exec ЁAutor ЁMarinaldo de Jesus   Ё Data Ё24/08/2004Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de APDA150                           Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInApda150Exec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InApda150Exec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= &( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda150VisЁ Autor ЁMarinaldo de Jesus     Ё Data Ё26/04/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Rede ( Visualizar )								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁApda150()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda150Vis( cAlias , nReg )
Return( APDA150( cAlias , nReg , 2 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda150IncЁ Autor ЁMarinaldo de Jesus     Ё Data Ё26/04/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Rede ( Incluir )								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁApda150()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda150Inc( cAlias , nReg )
Return( APDA150( cAlias , nReg , 3 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda150AltЁ Autor ЁMarinaldo de Jesus     Ё Data Ё26/04/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Rede ( Alterar )								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁApda150()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda150Alt( cAlias , nReg )
Return( APDA150( cAlias , nReg , 4 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda150DelЁ Autor ЁMarinaldo de Jesus     Ё Data Ё26/04/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Rede ( Excluir )								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁApda150()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda150Del( cAlias , nReg )
Return( APDA150( cAlias , nReg , 5 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda150MntЁ Autor ЁMarinaldo de Jesus     Ё Data Ё26/04/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Rede (Manutencao)  								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁApda150Mnt( cAlias , nReg , nOpc , lDlgPadSiga )			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias 		= Alias do arquivo                              Ё
Ё          ЁnReg   		= Numero do registro                            Ё
Ё          ЁnOpc   		= Numero da opcao selecionada                   Ё
Ё          ЁlDlgPadSiga = Numero da opcao selecionada                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁApda150()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda150Mnt( cAlias , nReg , nOpc , lDlgPadSiga )

Local aArea			:= GetArea(Alias())
Local aSvKeys		:= GetKeys()
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aRduHeader	:= {}
Local aRduCols		:= {}
Local aSvRduCols	:= {}
Local aRduFields	:= {}
Local aRduAltera	:= {}
Local aRduNaoAlt	:= {}
Local aRduVirtEn	:= {}
Local aRduNotFields	:= {}
Local aRduRecnos	:= {}
Local aRduKeys		:= {}
Local aRduVisuEn	:= {}
Local aFreeLocks	:= {}
Local bSet15		:= { || NIL }
Local bSet24		:= { || NIL }
Local cFilRDU		:= ""
Local cCodRDU		:= ""
Local cRDUKeySeek	:= ""
Local lLocks		:= .T.	
Local lExecLock		:= ( ( nOpc <> 2 ) .and. ( nOpc <> 3 ) )
Local nLoop			:= 0
Local nLoops		:= 0
Local nOpcAlt		:= 0
Local nRDUUsado		:= 0
Local oDlg			:= NIL
Local oEnRdu		:= NIL	

Private aGets
Private aTela

Private nGetSX8Len	:= GetSX8Len()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁColoca o Ponteiro do Mouse em Estado de Espera				   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁCheca a Opcao Selecionada									   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aRotSetOpc( cAlias , @nReg , nOpc )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta os Dados para a Enchoice							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aRduNotFields	:= { "RDU_FILIAL" }
	bGetRdu			:= { |lLock,lExclu|	IF( lExecLock , ( lLock := .T. , lExclu	:= .T. ) , aRduKeys := NIL ),;
										aRduCols := RDU->(;
															GdBuildCols(	@aRduHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
																			@nRduUsado		,;	//02 -> Numero de Campos em Uso
																			@aRduVirtEn		,;	//03 -> [@]Array com os Campos Virtuais
																			@aRduVisuEn		,;	//04 -> [@]Array com os Campos Visuais
																			"RDU"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
																			aRduNotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
																			@aRduRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
																			"RDU"		   	,;	//08 -> Alias do Arquivo Pai
																			NIL				,;	//09 -> Chave para o Posicionamento no Alias Filho
																			NIL				,;	//10 -> Bloco para condicao de Loop While
																			NIL				,;	//11 -> Bloco para Skip no Loop While
																			NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols 
																			NIL				,;	//13 -> Se Sera considerado o Inicializador Padrao
																			NIL				,;	//14 -> Opcional, Carregar Todos os Campos
																			NIL				,;	//15 -> Opcional, Nao Carregar os Campos Virtuais
																			NIL				,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
																			NIL				,;	//17 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
																			NIL				,;	//18 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
																			NIL				,;	//19 -> Carregar Coluna Fantasma
																			NIL				,;	//20 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
																			NIL				,;	//21 -> Verifica se Deve Checar se o campo eh usado
																			NIL				,;	//22 -> Verifica se Deve Checar o nivel do usuario
																			NIL				,;	//23 -> Verifica se Deve Carregar o Elemento Vazio no aCols
																			@aRduKeys		,;	//24 -> [@]Array que contera as chaves conforme recnos
																			@lLock			,;	//25 -> [@]Se devera efetuar o Lock dos Registros
																			@lExclu			 ;	//26 -> [@]Se devera obter a Exclusividade nas chaves dos registros
																	    );
														  ),;
										IF( lExecLock , ( lLock .and. lExclu ) , .T. );
	  					} 
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁLock do Registro do RDU									   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( lLocks := WhileNoLock( "RDU" , NIL , NIL , 1 , 1 , .T. , 1 , 5 , bGetRdu ) )
		Break
	EndIF
	CursorWait()
	aSvRduCols		:= aClone( aRduCols )
	cFilRDU			:= RDU->RDU_FILIAL
	cCodRDU			:= RDU->RDU_CODIGO
	cRDUKeySeek		:= ( cFilRDU + cCodRDU )
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Cria as Variaveis de Memoria e Carrega os Dados Conforme o arЁ
	Ё quivo														   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	For nLoop := 1 To nRDUUsado
		aAdd( aRduFields , aRduHeader[ nLoop , 02 ] )
		SetMemVar( aRduHeader[ nLoop , 02 ] , aRduCols[ 01 , nLoop ] , .T. )
	Next nLoop
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine os Campos Editaveis na Enchoice Apenas na Inclusao( 3 )Ё
	Ёou Alteracao(4)											   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define os Campos Editaveis								   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		nLoops := Len( aRduVisuEn )
		For nLoop := 1 To nLoops
			aAdd( aRduNaoAlt , aRduVisuEn[ nLoop ] )
		Next nLoop
		IF ( nOpc == 4 )
			aAdd( aRduNaoAlt , "RDU_CODIGO" )
		EndIF
		nLoops := Len( aRduFields )
		For nLoop := 1 To nLoops
			IF ( aScan( aRduNaoAlt , { |cNaoA| cNaoA == aRduFields[ nLoop ] } ) == 0 )
				aAdd( aRduAltera , aRduFields[ nLoop ] )
			EndIF
		Next nLoop
	
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁVerifica se Pode Efetuar a Delecao dos Registros			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( nOpc == 5 )
		IF !( ApdChkDel( cAlias , nReg , nOpc , cCodRDU ) )
			Break
		EndIF
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFAULT lDlgPadSiga	:= .F.
	aAdvSize		:= MsAdvSize( NIL , lDlgPadSiga )
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Tecla <CTRL-O> 						   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet15		:= { || IF(; 
								( ( nOpc == 3 ) .or. ( nOpc == 4 ) );			//Inclusao ou Alteracao
								.and.;
								Apda150TEncOk( nOpc , oEnRdu ),;				//Valida Todos os Campos da Enchoice
								(;
									nOpcAlt := 1 ,;
									RestKeys( aSvKeys , .T. ),;
									oDlg:End();
							 	),;
							 	IF(; 
							 		( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) ,;		//Inclusao ou Visualizacao
							 			(;
							 				nOpcAlt := 0 ,;
							 				.F.;
							 			 ),;	
									(;
										nOpcAlt := IF( nOpc == 2 , 0 , 1 ) ,;	//Visualizacao ou Exclusao
										RestKeys( aSvKeys , .T. ),;
										oDlg:End();
							 		);
							 	  );
						   );
					 }
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Teclas <CTRL-X>     	   			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet24		:= { || ( nOpcAlt := 0 , RestKeys( aSvKeys , .T. ) , oDlg:End() ) }
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta o Dialogo Principal para a Manutencao das Formulas	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0006 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF GetWndDefault() PIXEL
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto Enchoice para o RDU                      	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oEnRdu	:= MsmGet():New(	cAlias		,;
									nReg		,;
									nOpc		,;
									NIL			,;
									NIL			,;
									NIL			,;
									aRduFields	,;
									aObjSize[1]	,;
									aRduAltera	,;
									NIL			,;
									NIL			,;
									NIL			,;
									oDlg		,;
									NIL			,;
									.F.			,;
									NIL			,;
									.F.			 ;
								)
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 ) CENTERED

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Coloca o Ponteiro do Mouse em Estado de Espera			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorWait()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁQuando Confirmada a Opcao e Nao for Visualizacao Grava ou   ExЁ
	Ёclui as Informacoes do RDU e RD2							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF( nOpcAlt == 1 )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Apenas se nao For Visualizacao              				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
 		IF ( nOpc != 2 )
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Gravando/Incluido ou Excluindo Informacoes do SRY/RD2        Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			MsAguarde(;
						{ || Apda150Grava(;
											nOpc		,;	//Opcao de Acordo com aRotina
							 				nReg		,;	//Numero do Registro do RDU)
							 				aRduHeader	,;	//Campos do Arquivo RDU
							 				aRduCols	,;	//Array com o Conteudo Atual do RDU
							 				aSvRduCols	,;	//Array com o Conteudo de entrado do RDU
							 				aRduVirtEn	 ;	//Campos Virtuais do Arquivo RDU
							 			 );
						 };
					  )
		EndIF
	Else
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё RollBack da Numeracao Automatica            				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		While ( GetSX8Len() > nGetSX8Len )
			RollBackSX8()
		End While
	EndIF

End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁLibera os Locks             								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
aAdd( aFreeLocks , { "RDU" , aRduRecnos , aRduKeys } )
ApdFreeLocks( aFreeLocks )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura os Dados de Entrada								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aArea )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura as Teclas de Atalho								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura o Ponteiro do Mouse                  				   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( nOpcAlt )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda150TEncOkЁAutorЁMarinaldo de Jesus    Ё Data Ё26/04/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁTudoOk para a Enchoice                                      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁApda150TEncOk( nOpc , oEnRdu )								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA150()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apda150TEncOk( nOpc , oEnRdu )

Local lTudoOk := .T.

If ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )

	If GetMemVar( "RDU_TIPO" ) $ "2/3" //Plano de Desenvolvimento ou Plano de Metas
		Begin Sequence
			
			IF Empty( GetMemVar( "RDU_INIINC" ) ) .and. !Empty( GetMemVar( "RDU_FIMINC" ) ) 
				MsgInfo( OemToAnsi( STR0023 ) , OemToAnsi( STR0013 ) ) //"As datas de Inicio e Fim de InclusЦo de Metas devem ficar em branco ou ambas preenchidas."
				lTudoOk := .F.
			ElseIF Empty( GetMemVar( "RDU_FIMINC" ) ) .and. !Empty( GetMemVar( "RDU_INIINC" ) ) 
				MsgInfo( OemToAnsi( STR0023 ) , OemToAnsi( STR0013 ) ) //"As datas de Inicio e Fim de InclusЦo de Metas devem ficar em branco ou ambas preenchidas."
				lTudoOk := .F.
			ElseIF Empty( GetMemVar( "RDU_INIRES" ) ) .and. !Empty( GetMemVar( "RDU_FIMRES" ) ) 
				MsgInfo( OemToAnsi( STR0024 ) , OemToAnsi( STR0013 ) ) //"As datas de Inicio e Fim de InclusЦo de Resultados devem ficar em branco ou ambas preenchidas."
				lTudoOk := .F.
			ElseIF Empty( GetMemVar( "RDU_FIMRES" ) ) .and. !Empty( GetMemVar( "RDU_INIRES" ) )
				MsgInfo( OemToAnsi( STR0024 ) , OemToAnsi( STR0013 ) ) //"As datas de Inicio e Fim de InclusЦo de Resultados devem ficar em branco ou ambas preenchidas."
				lTudoOk := .F.
			EndIf
		End Sequence
	
	Else
	
		If !Empty( GetMemVar( "RDU_INIINC" ) ) .or. !Empty( GetMemVar( "RDU_FIMINC" ) ) .or. ;
			!Empty( GetMemVar( "RDU_INIRES" ) ) .or. !Empty( GetMemVar( "RDU_FIMRES" ) )
				MsgInfo( OemToAnsi( STR0025 ) , OemToAnsi( STR0013 ) ) //"Para o tipo selecionado, os datas de Inicio e Fim de InclusЦo de Metas e Resultados devem ficar em branco. Altere tipo para 2 ou 3, limpe as datas e volte o tipo para o cСdigo desejado."
				lTudoOk := .F.
		EndIf
	
	EndIf
	                
	IF lTudoOk 
		lTudoOk := EnchoTudOk( oEnRdu )
	EndIF

EndIf

Return( lTudoOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda150Grava ЁAutorЁMarinaldo de Jesus    Ё Data Ё26/04/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁApda150()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apda150Grava(	nOpc		,;	//Opcao de Acordo com aRotina
							 	nReg		,;	//Numero do Registro do RDU)
							 	aRduHeader	,;	//Campos do Arquivo RDU
							 	aRduCols	,;	//Array com o Conteudo Atual do RDU
							 	aSvRduCols	,;	//Array com o Conteudo de entrado do RDU
							 	aRduVirtEn	 ;	//Campos Virtuais do Arquivo RDU
							 )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis de Inicializacao Obrigatoria					  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local aMestre	:= GdPutIStrMestre( 01 )
Local cOpcao	:= IF( ( nOpc == 5 ) , "DELETE" , IF( ( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) , "PUT" , NIL ) )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis que serao inicializadas no Corpo da Funcao		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local nLoop
Local nLoops

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Atualiza aRdmCols para Verificar se Houveram Alteracoes	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	nLoops := Len( aRduHeader )
	For nLoop := 1 To nLoops
		aRduCols[ 01 , nLoop ] := GetMemVar( aRduHeader[ nLoop , 02 ] )
	Next nLoop

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Seta a Gravacao ou Exclusao Apenas se Houveram Alteracoes  ouЁ
	Ё se foi Selecionada a Exclusao								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aMestre[ 01 , 01 ]	:= "RDU"
	aMestre[ 01 , 02 ]	:= nReg
	aMestre[ 01 , 03 ]	:= ( ( nOpc == 5 ) .or. !( ArrayCompare( aRduCols , aSvRduCols ) ) )
	aMestre[ 01 , 04 ]	:= aClone( aRduHeader )
	aMestre[ 01 , 05 ]	:= aClone( aRduVirtEn )
	aMestre[ 01 , 06 ]	:= {}
	aMestre[ 01 , 07 ]	:= {}

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Grava as Informacoes                        				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	GdPutInfoData( aMestre , cOpcao )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Confirmando a Numeracao Automatica          				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	While ( GetSX8Len() > nGetSX8Len )
		ConfirmSX8()
	End While

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduCodigoVld ЁAutorЁMarinaldo de Jesus    Ё Data Ё26/04/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDU_CODIGO          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDU_CODIGO                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduCodigoVld()

Local cRduCodigo	:= GetMemVar( "RDU_CODIGO" )
Local lRduCodigoOk	:= .T.

Begin Sequence

	IF !( lRduCodigoOk := RduGetCodigo( @cRduCodigo , .F. , .T. ) )
 		Break
    EndIF

	SetMemVar( "RDU_CODIGO" , cRduCodigo )

End Sequence

Return( lRduCodigoOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduGetCodigo ЁAutorЁMarinaldo de Jesus    Ё Data Ё26/04/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁObtem Numeracao Valida para o RDU_CODIGO                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁObter Numeracao valida para o RDU_CODIGO                 	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduGetCodigo( cRduCodigo , lExistChav , lShowHelp )
Return(;
			GetNrExclOk(	@cRduCodigo 			,;
							"RDU"					,;
							"RDU_CODIGO"			,;
							"RDU_FILIAL+RDU_CODIGO" ,;
							NIL						,;
							lExistChav				,;
							lShowHelp	 			 ;
						);
		)

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduCodigoInitЁAutorЁMarinaldo de Jesus    Ё Data Ё26/04/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Inicializar o Conteudo do Campo RDU_CODIGO      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RDU_CODIGO                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduCodigoInit()
Local cRduCodigo
RduGetCodigo( @cRduCodigo , .F. , .F. )
Return( cRduCodigo )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduDescVld ЁAutorЁMarinaldo de Jesus      Ё Data Ё26/04/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDU_DESC	        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDU_DESC	                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduDescVld()

Local lRduDescOk := .T.

Begin Sequence

	IF !( lRduDescOk := NaoVazio() )
		Break
	EndIF

	IF !( lRduDescOk := Texto() )
    	Break
    EndIF

End Sequence

Return( lRduDescOk )


/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduInit()	 ЁAutorЁMarcos Alves 		  Ё Data Ё31/01/2005Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Inicializar o Conteudo dos campos da tabela RDU Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para os campos da tebela RDU						Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduInit()
Local cRet		:= Ctod("//")
/****************************
Caso necessite fazer alguma inicializaГЦo especifica para um determinado campo
usar este trecho de progrma.
Local cVar		:= Upper( AllTrim( ReadVar() ) )
If cVar$"RDU_DATINI#RDU_EDITIN#RDU_DATFIM#"
	cRet:=Ctod("//")
EndIf	
*/
Return cRet

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduCodigoVld ЁAutorЁMarinaldo de Jesus    Ё Data Ё26/04/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo dos campos de data         Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDU_DATAS                     	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduValid()
Local cVar			:= Upper( AllTrim( ReadVar() ) )
Local cMsgInfo		:= ""
Local lDateOk		:= .T.


Begin Sequence
	
	IF ( "RDU_DATINI" $ cVar )
		IF !( lDateOk := NaoVazio() )
			Break
		EndIF
		IF ( !Empty( GetMemVar( "RDU_DATFIM" ) ) )
			IF !( lDateOk := ( GetMemVar( "RDU_DATINI" ) <= GetMemVar( "RDU_DATFIM" ) ) )
				cMsgInfo := ( STR0010 + " " + STR0011 ) //"Data Nao Pode Ser Vazia ou"###"Maior que Data Final."
				Break
			EndIF
		EndIF
	ElseIF ( "RDU_DATFIM" $ cVar )
		IF !( lDateOk := NaoVazio() )
			Break
		EndIF
		IF ( !Empty( GetMemVar( "RDU_DATINI" ) ) )
			IF !( lDateOk := ( GetMemVar( "RDU_DATFIM" ) >= GetMemVar( "RDU_DATINI" ) ) )
				cMsgInfo := ( STR0010 + " " + STR0012 ) //"Data Nao Pode Ser Vazia ou"###"Menor que Data Inicial."
				Break
			EndIF
		EndIF
	EndIF
	
	
End Sequence


IF !( lDateOk )
	IF !( Empty( cMsgInfo ) )
		MsgInfo( OemToAnsi( cMsgInfo ) , OemToAnsi( STR0013 ) )	//"Data InvАlida"
	EndIF	
EndIF

Return( lDateOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduDtVld ЁAutorЁEduardo Ferreira    Ё Data Ё30/05/2016Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo dos campos de data range    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDU_INIINC/RDU_FIMINC                 	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduDtVld()
Local cVar			:= Upper( AllTrim( ReadVar() ) )
Local cMsgInfo		:= ""
Local lDateOk		:= .T.

If GetMemVar( "RDU_TIPO" ) $ "2/3"
	Begin Sequence
		
		If ( "RDU_INIINC" $ cVar )
			IF ( !Empty( GetMemVar( "RDU_FIMINC" ) ) )
				IF !( lDateOk := ( GetMemVar( "RDU_INIINC" ) <= GetMemVar( "RDU_FIMINC" ) ) )
					cMsgInfo := ( STR0020) //"Esta data tem que ser inferior Ю data final de inclusЦo de metas."
					Break
				EndIF
			EndIF
		ElseIF ( "RDU_FIMINC" $ cVar )
			IF ( !Empty( GetMemVar( "RDU_INIINC" )) )
				IF !( lDateOk := ( GetMemVar( "RDU_FIMINC" ) >= GetMemVar( "RDU_INIINC" ) ) )
					cMsgInfo := ( STR0021 ) //"Esta data tem que ser superior Ю data inМcio da inclusЦo de metas."
					Break
				EndIF
			EndIf
		ElseIF ( "RDU_INIRES" $ cVar )
			IF ( !Empty( GetMemVar( "RDU_FIMRES" ) ) )
				IF !( lDateOk := ( GetMemVar( "RDU_INIRES" ) <= GetMemVar( "RDU_FIMRES" ) ) )
					cMsgInfo := ( STR0019 ) //"Esta data tem que ser inferior Ю data final de inclusЦo de resultados."
					Break
				EndIF
			EndIf
		ElseIF ( "RDU_FIMRES" $ cVar )
			IF ( !Empty( GetMemVar( "RDU_INIRES" ) ) )
				IF !( lDateOk := ( GetMemVar( "RDU_FIMRES" ) >= GetMemVar( "RDU_INIRES" ) ) )
					cMsgInfo := ( STR0022 ) //"Esta data tem que ser superior Ю data inМcio da inclusЦo de resultados."
					Break
				EndIF
			EndIf
		EndIF
		
		
	End Sequence
EndIf


IF !( lDateOk )
	IF !( Empty( cMsgInfo ) )
		MsgInfo( OemToAnsi( cMsgInfo ) , OemToAnsi( STR0013 ) )	//"Data InvАlida"
	EndIF	
EndIF

Return( lDateOk )
/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduTipoVld   ЁAutorЁEmerson Grassi Rocha  Ё Data Ё13/06/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDU_TIPO           	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDU_TIPO                           	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduTipoVld()

Local lRduTipoOK := .T.

Begin Sequence

	IF !( lRduTipoOK := NaoVazio() )
		Break
	EndIF

	IF !( lRduTipoOK := Pertence( RduTipoBox( .T. ) ) )
		Break
	EndIF

End Sequence

Return( lRduTipoOK )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduTipoInit 	 ЁAutorЁEmerson Grassi    Ё Data Ё13/06/2006Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInit do Campo RDU_TIPO										Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO do Campo RDU_TIPO								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduTipoInit()
Return( SubStr( RduTipoBox( .T. ) , 1 , 1 ) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd5TipoBox   ЁAutorЁEmerson Grassi Rocha  Ё Data Ё13/06/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Retornar as Opcoes do Campo RDU_TIPO           	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_CBOX para o campo RDU_TIPO                           	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduTipoBox( lValid , lRetDesc , cOpcDesc )

Local cRduTipoBox 	:= ""

DEFAULT lValid		:= .F.
DEFAULT lRetDesc	:= .F.

IF !( lValid )

	IF !( lRetDesc )

		cRduTipoBox := "1="+STR0014+";"	//"Mapa do Conhecimento"
		cRduTipoBox += "2="+STR0015+";"	//"Plano de Desenvolvimento"
		cRduTipoBox += "3="+STR0016		//"Plano de Metas"
		If FunName() == "APDA270"
			cRduTipoBox := ";4="+STR0017+";"	//"AvaliaГЦo"
		Else
			cRduTipoBox += ";4="+STR0017+";"	//"AvaliaГЦo
		EndIf
		If FunName() == "APDA130"
			cRduTipoBox := "5="+STR0018	   		//"Projeto"	
		Else
			cRduTipoBox += "5="+STR0018	   		//"Projeto"	
		EndIf	
		
	Else

 			Do Case
			Case ( cOpcDesc == "1" ) ; ( cRduTipoBox := STR0014 )	//"Mapa do Conhecimento"
			Case ( cOpcDesc == "2" ) ; ( cRduTipoBox := STR0015 )	//"Plano de Desenvolvimento"
			Case ( cOpcDesc == "3" ) ; ( cRduTipoBox := STR0016 )	//"Plano de Metas"
			Case ( cOpcDesc == "4" ) ; ( cRduTipoBox := STR0017 )	//"AvaliaГЦo"
			Case ( cOpcDesc == "5" ) ; ( cRduTipoBox := STR0018 )	//"Projeto"
		End Case

	EndIF

Else

	cRduTipoBox := "12345"

EndIF
	
Return( cRduTipoBox )
                     

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRduSxbFilt       ЁAutorЁEmerson Grassi    Ё Data Ё13/06/2006Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁCondicao de Filtro		     								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁFiltro da Consulta Padrao RDU no SXB						Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RduSxbFilt()
Local cFilt		:= "@#.T.@#"  
Local cCmpPer	:= ReadVar()
Local cTipo		:= "" 
Local cFunName	:= FunName() 
Local cCodObj	:= ""
Local nPosObj	:= 0

//1-Mapa de Conhecimento
If "RBO_PERIOD"$cCmpPer									
	cTipo := "1" 

//2-PDP 	  
ElseIf cFunName == "APDM031"
	cTipo := "2" 	

//3-PM	
ElseIf cFunName == "APDM032"
	cTipo := "3"

//Cabecalho - PDP / Plano de Metas	
ElseIf "RDV_CODPER"$cCmpPer //.Or. "RDJ_PERIOD"$cCmpPer
	nPosObj := GdFieldPos("RDV_CODOBJ")
	cCodObj	:= aCols[n][nPosObj]		
	RDI->( dbSetOrder(1) )
	If ( RDI->( dbSeek(xFilial("RDI")+cCodObj) ) )
		If RDI->RDI_TIPO == "1"
			cTipo := "2"	//2-PDP 	  
		Else 
			cTipo := "3"	//3-PM			
		EndIf
	EndIf
//4-AvaliaГЦo / 5-Projeto
ElseIf "RD6_CODPER"$cCmpPer									
	cTipo := ""
	cFilt:= "@#RDU->RDU_TIPO == '4' .OR. RDU->RDU_TIPO == '5'@#"
//5-Projeto APDA130
ElseIf "RDN_PERIOD"$cCmpPer
	cTipo := "5"	//5-Projeto	
EndIf
    
If !Empty(cTipo)
	cFilt:= "@#RDU->RDU_TIPO == '"+ cTipo +"'@#"    
EndIf
	
Return( cFilt )

/*                                	
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Gustavo     Ё Data Ё21/12/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas bibliotecas Framework da Versao 9.12 .      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁAPDA150                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/   

Static Function MenuDef()
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define Array contendo as Rotinas a executar do programa      Ё
	Ё ----------- Elementos contidos por dimensao ------------     Ё
	Ё 1. Nome a aparecer no cabecalho                              Ё
	Ё 2. Nome da Rotina associada                                  Ё
	Ё 3. Usado pela rotina                                         Ё
	Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
	Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
	Ё    2 - Simplesmente Mostra os Campos                         Ё
	Ё    3 - Inclui registros no Bancos de Dados                   Ё
	Ё    4 - Altera o registro corrente                            Ё
	Ё    5 - Remove o registro corrente do Banco de Dados          Ё
	Ё    6 - Copiar                                                Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
 Local aRotina := {;
								{ STR0001 , "AxPesqui"	 , 0 , 01,,.F.} ,; //"Pesquisar"
								{ STR0002 , "Apda150Mnt" , 0 , 02 } ,; //"Visualizar"
								{ STR0003 , "Apda150Mnt" , 0 , 03 } ,; //"Incluir"
								{ STR0004 , "Apda150Mnt" , 0 , 04 } ,; //"Alterar"
								{ STR0005 , "Apda150Mnt" , 0 , 05 }  ; //"Excluir"
							}
Return aRotina