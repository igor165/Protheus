#INCLUDE "PROTHEUS.CH"


/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Static Utilizadas na Funcao fVerbas() 					   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static __afVerbas__
Static oHash_SRV

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁGPEXSRV   Ё Autor Ё Marinaldo de Jesus    Ё Data Ё21/12/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁBiblioteca de Funcoes Genericas para uso em Formulas no SRV Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      Ё Generico                                                   Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё
цддддддддддддбддддддддддбдддддддбддддддддддддддддддддддддддддддддддддддд╢
ЁProgramador Ё Data     ЁCHAMADOЁMotivo da Alteracao                    Ё
цддддддддддддеддддддддддедддддддеддддддддддддддддддддддддддддддддддддддд╢
ЁMohanad OdehЁ28/06/2012ЁTFGBHRЁInclusЦo de novo parБmetro (cCpoFiltro) Ё
Ё            Ё          Ё      Ё na funГЦo fVerbas() para ser utilizado Ё
Ё            Ё          Ё      Ёcomo filtro ao Passar o nome de um campoЁ
Ё            Ё          Ё      Ё com conteЗdo "S" ou "1" ou "N" ou "2". Ё
Ё            Ё          Ё      ЁEx: cCpoFiltro == "SRV->RV_LEEINC", serАЁ
Ё            Ё          Ё      Ё filtrado somente as verbas que Leem    Ё
Ё            Ё          Ё      ЁIncidencias                             Ё
ЁRenan BorgesЁ20/01/2017ЁMRH-  ЁAjuste para nЦo passar pelo campo sem   Ё
Ё            Ё          Ё269   Ёinformar verbas seja preenchido com *.  Ё
юддддддддддддаддддддддддадддддддаддддддддддддддддддддддддддддддддддддддды/*/

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁPosSrv	Ё Autor ЁMauro 	   ( Versao I )	Ё Data Ё23/03/1995Ё╠╠
╠╠Ё       	 Ё       	Ё       ЁMarinaldo ( Versao II )Ё Data Ё17/01/2003Ё╠╠
╠╠Ё       	 Ё       	Ё       ЁMarinaldo ( Versao III)Ё Data Ё06/09/2004Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁPosiciona na Verba desejada no SRV						  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё<vide parametros formais>									  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ<vide parametros formais>									  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso		 ЁGenerico 												  	  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function PosSrv( cCodigo , cFil , uCampo , nOrdem , lMsGoto )

Local uRet
Local Rst

DEFAULT lMsGoto	:= .T.

Rst		:= RstPosAlias( "SRV", cFil ) // alteraГЦo incluida.
uRet	:= PosAlias( "SRV" , cCodigo , cFil , uCampo , nOrdem , lMsGoto )

IF ( uRet == NIL )
	uRet := " "	//Quando NIL retorna sempre String vazia. Quem chamou deve testar o retorno.
ElseIF (;
			( ValType( uRet ) == "L" );
			.and.;
			!( uRet );
			.and.;
			( uCampo <> NIL );
		)
	uRet := "@" //Apenas por compatibilidade com funcoes que testam este retorno.
EndIF


Return( uRet )

/*/{Protheus.doc} RetValSrv
Busca campos do cadastro de verbas
@author Leandro/Paulo
@since 09/04/2019 - V2
@type function
/*/
Function RetValSrv( cCodigo , cFil , uCampo , nOrdem )
Local aArea
Local aAreaSRV
Local cKey	:= cFil + cCodigo + uCampo
Local uRet

DEFAULT nOrdem := 1
DEFAULT oHash_SRV := HMNew() //Cria objeto hash para melhoria de performance na busca

If !(HMGet(oHash_SRV, cKey, uRet))

	aArea		:= GetArea()
	aAreaSRV	:= SRV->(GetArea())

	dbSelectArea("SRV")
	dbSetOrder(nOrdem)

	MsSeek(xFilial("SRV",cFil) + cCodigo)
	uRet := SRV->(&uCampo)
	uRet := Iif(uCampo == "RV_INCIRF", AllTrim(uRet), uRet)

	HMSet(oHash_SRV, cKey, uRet)

	RestArea(aAreaSRV)
	RestArea(aArea)

EndIf

Return( uRet )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfSrvStaticЁ Autor Ё Mauro				  Ё Data Ё 25.11.02 Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁReinicializa as Staticas para PosSrv()					  	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfSrvStatic()												Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                      					  	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   ЁReinicializar as Static utilizadas no GPEXSRV				Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function fSrvStatic()

IF FindFunction( "RstPosAlias" )
	RstPosAlias("SRV")
EndIF

Return( NIL )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁfVerbas   Ё Autor ЁEquipe Advanced RH	  Ё Data Ё 18/04/01 Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSelecionar Verbas no SRV                              	  	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё fVErbas()  												Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   Ё Generico 													Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function fVerbas( l1Elem , cPreSelect, nMaxSel, cCpoFiltro )

Local aNewSelect		:= {}
Local aPreSelect		:= {}
Local aArea				:= {}
Local cFilSRV			:= xFilial("SRV")
Local cTitulo			:= ""
Local MvParDef			:= ""
Local MvRetor			:= ""
Local MvParam			:= ""
Local cValAnt			:= ""
Local lRet				:= .T.
Local nFor				:= 0
Local nAuxFor			:= 1
Local MvPar     		:= NIL

DEFAULT cPreSelect		:= ""
DEFAULT l1Elem			:= .F.
DEFAULT nMaxSel			:= 10
DEFAULT cCpoFiltro		:= ""
Begin Sequence

	For nFor := 1 To Len( cPreSelect ) Step 3
		aAdd( aPreSelect , SubStr( cPreSelect , nFor , 3 ) )
	Next nFor

	cAlias 	:= Alias()
	MvPar	:= &(Alltrim(ReadVar()))
	mvRet	:= Alltrim(ReadVar())
	cValAnt := MvPar

	If !( l1Elem )
		For nFor := 1 TO Len(alltrim(MvPar))
			Mvparam += Subs(MvPar,nAuxFor,3)
			MvParam += Replicate("*",3)
			nAuxFor := (nFor * 3) + 1
		Next
		mvPar := MvParam
	Endif


	IF Empty( __afVerbas__ )
		__afVerbas__	:= BldafVerbas( cFilSRV )
	EndIF

	IF !( lRet := !Empty( __afVerbas__ ) )
		Help(" ",1,"GPENFILIAL")	//Cadastro de verbas nao existe para a filial
		Break
	EndIF

	For nFor := 1 To Len( __afVerbas__ )
		IF ( aScan( aPreSelect , SubStr( __afVerbas__[ nFor ] , 1 , 3 ) ) == 0.00 )
			If !Empty(cCpoFiltro)
				aArea:= getArea()
				dbSelectArea("SRV")
				//POSICIONA O SRV
				PosSRV(Left(__afVerbas__[ nFor ],3), cFilSRV)
				RestArea(aArea)
				If &cCpoFiltro $ "1/S"
					MvParDef+=Left(__afVerbas__[ nFor ],3)
					aAdd(aNewSelect , __afVerbas__[nFor])
				EndIf
			Else
				MvParDef+=Left(__afVerbas__[ nFor ],3)
				aAdd( aNewSelect , __afVerbas__[ nFor ] )
			EndIf
		EndIF
	Next nFor

	IF f_Opcoes(@MvPar,cTitulo,aNewSelect,MvParDef,12,49,l1Elem,3,nMaxSel)
		For nFor := 1 To Len( mVpar ) Step 3
			IF ( SubStr( mVpar , nFor , 3 ) # "***" )
				mvRetor += SubStr( mVpar , nFor , 3 )
			Endif
		Next nFor
		If !Empty(Mvretor)
			&MvRet := PADR(Mvretor,60)
		Else
			&MvRet := '*'
		EndIf
	Else
		&MvRet := cValAnt
	EndIF

	If AllTrim(cValAnt) == "*" .and. Empty(&MvRet)
		&MvRet := cValAnt
	EndIf

End Sequence

dbSelectArea(cAlias)

Return( lRet )

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁBldafVerbas   Ё Autor ЁMarinaldo de Jesus     Ё Data Ё12/09/2003Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCarregar as Verbas do SRV conforme Filial                    	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcFilSRV - Filial do SRV											Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁaVerbas - Array com a String de Codigo + Descricao da Verba	    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function BldafVerbas( cFilSRV )

Local aArea		:= GetArea()
Local aQuery	:= {}
Local aVerbas	:= {}
Local bSkip		:= { || aAdd( aVerbas , ( RV_COD + " - " + RV_DESC ) ) , .F. }

#IFDEF TOP
	aQuery		:= Array( 03 )
	aQuery[01]	:= "RV_FILIAL='"+cFilSRV+"'"
	aQuery[02]	:= " AND "
	aQuery[03]	:= "D_E_L_E_T_=' ' "
#ENDIF

SRV->( GdMontaCols(	NIL				,;
					NIL				,;
					NIL				,;
					NIL				,;
					NIL				,;
					{				 ;
						"RV_FILIAL"	,;
						"RV_COD"	,;
						"RV_DESC"	 ;
					}				,;
					NIL				,;
					"SRV"			,;
					cFilSRV			,;
					NIL				,;
					bSkip			,;
					.F.				,;
					.F.				,;
					.F.				,;
					NIL				,;
					.F.				,;
					.F.				,;
					aQuery			,;
					.F.				,;
					.T.				,;
					.F.				,;
					.T.				,;
					.F.				,;
					.F.				,;
					.F.				 ;
				   );
	  )

RestArea( aArea )

Return( aClone( aVerbas ) )

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRstfVerbas    Ё Autor ЁMarinaldo de Jesus     Ё Data Ё11/09/2003Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁReinicializa as Statics em fVerbas()                         	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ																Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL																Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function RstfVerbas()
__afVerbas__ := {}

RstHashSRV()

Return( ( __afVerbas__ ) )

/*/{Protheus.doc} RstHashSRV
Reseta hash map com dados da SRV
@author Leandro Drumond
@since 11/04/2019
@type function
/*/
Function RstHashSRV()

If ValType(oHash_SRV) == "O"
	HMClean(oHash_SRV)
	FreeObj(oHash_SRV)
	oHash_SRV := Nil
EndIf

Return .T.
