#include "rwmake.ch"        // incluido pelo assistente de conversao do AP5 IDE em 05/07/00
#INCLUDE "IMPRESZ.CH"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para armazenar as informacoes das perguntes:   ³
//³                                                                     ³
//³ nTipo 		- Tipo de Formulario ?                 					³ 
//³ cFilialDe	- De Sucursal ?                         				³
//³ cFilialAte	- Hasta Sucursal ?                      				³
//³ cMatDe		- De Matricula ?                        				³
//³ cMatAte		- Hasta Matricula ?                     				³
//³ cCCDe		- De Centro de Costo ?                  				³
//³ cCCAte		- Hasta Centro de Costo ?               				³
//³ cTipoRes	- Tipo de Liq. Final ?                  				³
//³ cImprCGC 	- Imprime Sello RFC ?                   				³
//³ dDtDemDe	- De Fecha Despido ?                    				³
//³ dDtDemAte	- Hasta Fecha Despido ?                 				³
//³ dDtGerDe	- De Fecha Generacion ?                 				³
//³ dDtGerAte	- Hasta Fecha Generacion ?              				³
//³ nImprDtHom	- Imprime Fecha Homologacion ?          				³
//³ nNumVias	- Numero de Copias ?                    				³
//³ cImprFerias	- Imprime Vacaciones ?              					³
//³ dDtHomDe	- De Fecha Homologa ?                   				³
//³ dDtHomAte	- Hasta Fecha Homologa ?                				³
//³ cImpr13Sal	- Imprime Aguinaldo ?                   				³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

User Function ImpResDom()       

Local aVerbas	:= {}  
Local nValor	:= 0
Local nReg		:= 0

Private nPagina := 0     
Private cMensRec:= ""
Private nChar   := 15

//Definicao do array de Titulo aTitulos.
#Define CGC			1		
#Define FILIAL 		2
#Define CC     		3 
#Define NOME     	4
#Define MAT			5
#Define ENDERE		6
#Define BAIRRO		7
#Define MUNICIP		8
#Define ESTADO		9
#Define CEP			10
#Define CIC			11
#Define NASC    	12
#Define	 MAE		13
#Define SALMEN		14
#Define	 ADMISSA	15
#Define DTAVISO		16
#Define DTDEM		17
#Define RG			18
#Define CURP		19
#Define LOCALIDAD	20
#DEFINE IMP_DISCO 1
#DEFINE IMP_SPOOL 2
#DEFINE IMP_PORTA 3
#DEFINE IMP_EMAIL 4


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de variaveis utilizadas no programa atraves da funcao    ³
//³ SetPrvt, que criara somente as variaveis definidas pelo usuario,    ³
//³ identificando as variaveis publicas do sistema utilizadas no codigo ³
//³ Incluido pelo assistente de conversao do AP5 IDE                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SetPrvt("LNULL,NCONTAR,NLI,DET,X,cCodSaqIOB")

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³RdMake    ³ ImpResDom³ Autor ³ Alfredo Medrano       ³ Data ³ 05.06.2017 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao da Rescisao em Papel Zebrado para Rep Dominicana   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ImpResDom                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Alf. Medrano³12/05/2017³      ³Se incluye Fuente para Rep. Dominicana    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/

lNull   := .F.
nContar := 0
nLi     := 0

nEpoca:= SET(5,1910)
//--MUDAR ANO PARA 4 DIGITOS
SET CENTURY ON

// redefine el array de Titulo aTitulos.
aTitulo[4] := "NOMBRE"
aTitulo[6] := "CARGO"
aTitulo[7] := "DEPARTAMENTO"
aTitulo[8] := "SUPERVISOR"
aTitulo[9] := "SALARIO BASE"
aTitulo[10] := "SALARIO PROMEDIO"
aTitulo[11] := "COMISIÓN PROMEDIO"
aTitulo[12] := "SALARIO PROMEDIO MI"
aTitulo[13] := "SALARIO PROMEDIO DI"
aTitulo[14] := "MOTIVO DE SALIDA"
aTitulo[15] := "FECHA INGRESO"
aTitulo[17] := "FECHA SALIDA"
                  
fCabecalho()

/* Impresión de conceptos primero Percepciones y abajo descuentos
// 1= Proventos
// 2= Descontos
For nReg := 1 to 2
	aVerbas := IIF(nReg==1,aClone(aPdv),aClone(aPdd))
	nValor := IIF(nReg==1, nProv, nDesc)
	fCabecVerbas(nReg)
	fDetalhe(nReg, aVerbas)	
	fRodapeVerbas(nReg, nValor)
	nLi++    
Next nReg
*/

// Impresión de conceptos Percepciones y descuentos a la par
fCabecVerbas(nReg)
fDetalhe(nReg, aVerbas)	
fRodapeVerbas(nReg, nValor)

fRodape()

Set(5,nEpoca)
If nTdata > 8
	SET CENTURY ON
Else
	SET CENTURY OFF
Endif

nChar := If(aReturn[4]==1,15,18)

Return Nil   

//-------------------
Static Function fCabecalho()
//-------------------
Local nTam1 := 0
Local nTam2 := 0
Local nAnoAct := 0
Local nMesAct := 0
Local nDiaAct := 0
Local nAnoIng := 0
Local nMesing := 0
Local nDiaing := 0
Local nAnoAnt := 0
Local nMesAnt := 0
Local dMesAnt := CTOD("  /  /  ")
Local dAniv := CTOD("  /  /  ")
Local dFecAct := CTOD("  /  /  ")

Local aDriver := ReadDriver()
local nTamanho:= 'M'
Local lWin    := .f.

If TYPE("__DRIVER") == "C"
	If "DEFAULT"$__DRIVER
		lWin := .t.
	EndIf
EndIf

IF aReturn[5] == IMP_DISCO
   lWin := .f.    // Se eh disco , nao eh windows
Endif

	If aReturn[5] <> IMP_SPOOL // Se não for via Windows manda os caracteres para setar a impressora
		If nChar == NIL .and. !lWin .and. __cInternet == Nil
			@ 0,0 PSAY &(If(nTamanho=="P",aDriver[1],If(nTamanho=="G",aDriver[5],(If(aReturn[4]=1,aDriver[3],aDriver[4])))))
		ElseIf !lWin .and. __cInternet == Nil
			If nChar == 15
				@ 0,0 PSAY &(If(nTamanho=="P",aDriver[1],If(nTamanho=="G",aDriver[5],(If(aReturn[4]=1,aDriver[3],aDriver[4])))))
			Else
				@ 0,0 PSAY &(If(nTamanho=="P",aDriver[2],If(nTamanho=="G",aDriver[6],aDriver[4])))
			EndIf
		EndIf
	EndIF

nPagina++
If nPagina > 1
	@ 076,001 PSAY REPLICATE("_",131)
	@ 077,121 PSAY "CONTINUA..."
	@ 078,001 PSAY REPLICATE("_",131)
EndIf

nLi := 01
@ nLi,060 PSAY "CORPORACIÓN AVICOLA DEL CARIBE"
@ nLi,123 PSAY "PAG. " + AllTrim(Str(nPagina))

nLi++

If cTipoRes == 1
	@ nLi,060 PSAY "CÁLCULO DE PRESTACIONES LABORALES"
Else   // Complementar
	@ nLi,060 PSAY "CÁLCULO DE PRESTACIONES LABORALES ( COMPLEMENTAR )"
Endif                       

nLi := nLi + 2

nTam1 := Max(Len(Alltrim(aTitulo[10])), Len(Alltrim(aTitulo[11])))
nTam1 := If(nTam1 > Max(Len(Alltrim(aTitulo[12])), Len(Alltrim(aTitulo[13]))), nTam1, Max(Len(Alltrim(aTitulo[12])), Len(Alltrim(aTitulo[13]))))
                                   
nEspacos := nTam1 - Len(AllTrim(aTitulo[4]))
@ nLi,001 PSAY AllTrim(aTitulo[4])  + Space(nEspacos) + " : " + SRA->RA_MAT + " " + SRA->RA_NOME

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[6]))
@ nLi,001 PSAY AllTrim(aTitulo[6]) + Space(nEspacos) + " : " + fDesc("SQ3",SRA->RA_CARGO,"Q3_DESCSUM")

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[7]))
@ nLi,001 PSAY AllTrim(aTitulo[7]) + Space(nEspacos) + " : " + fDesc("SQB",SRA->RA_DEPTO,"QB_DESCRIC")

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[15]))
@ nLi,001 PSAY AllTrim(aTitulo[15]) + Space(nEspacos) + " : " + DTOC(SRA->RA_ADMISSA)

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[17]))
@ nLi,001 PSAY AllTrim(aTitulo[17]) + Space(nEspacos) + " : " + DTOC(SRG->RG_DATADEM)

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[9]))
@ nLi,001 PSAY AllTrim(aTitulo[9]) + Space(nEspacos) + " : " + Transform(SRA->RA_SALARIO,"99,999,999.99")

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[10]))
@ nLi,001 PSAY AllTrim(aTitulo[10]) + Space(nEspacos) + " : " + Transform(TraeValor("605",SRA->RA_MAT),"99,999,999.99")

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[11]))
@ nLi,001 PSAY AllTrim(aTitulo[11]) + Space(nEspacos) + " : " + Transform(TraeValor("606",SRA->RA_MAT),"99,999,999.99")

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[12]))
@ nLi,001 PSAY AllTrim(aTitulo[12]) + Space(nEspacos) + " : " + Transform(TraeValor("608",SRA->RA_MAT),"99,999,999.99")

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[13]))
@ nLi,001 PSAY AllTrim(aTitulo[13]) + Space(nEspacos) + " : " + Transform(TraeValor("607",SRA->RA_MAT),"99,999,999.99")

nLi++
nEspacos := nTam1 - Len(AllTrim(aTitulo[14]))
@ nLi,001 PSAY AllTrim(aTitulo[14]) + Space(nEspacos) + " : " + FDESCRCC("S010",SRG->RG_TIPORES,1,2,3,30)

nLi++
@ nLi,001 PSAY "    TIEMPO"

nLi++
@ nLi,001 PSAY "DIAS       MESES      AÑOS"

// La antigüedad se calcula en Años, Meses y Días Cumplidos
	dAniv := STOD(SUBSTR(DTOS(SRG->RG_DATADEM),1,4)+SUBSTR(DTOS(SRA->RA_ADMISSA),5,2)+SUBSTR(DTOS(SRA->RA_ADMISSA),7,2))
	dFecAct := SRG->RG_DATADEM

	nAnoAct := YEAR(SRG->RG_DATADEM)
	nMesAct := MONTH(SRG->RG_DATADEM)
	nDiaAct := DAY(SRG->RG_DATADEM)
	nAnoIng := YEAR(SRA->RA_ADMISSA)
	nMesing := MONTH(SRA->RA_ADMISSA)
	nDiaing := DAY(SRA->RA_ADMISSA)

	nAnoAnt := nAnoAct
	nMesAnt := nMesAct - 1
	IF nMesAnt = 0
		nMesAnt := 12
		nAnoAnt := nAnoAnt -1
	ENDIF

	dMesAnt := STOD(ALLTRIM(STR((nAnoAnt*10000)+(nMesAnt*100)+01)))

	IF dFecAct >= dAniv
		nAnios := (nAnoAct - nAnoIng)
		nMeses := IIF(nDiaAct>=nDiaIng,nMesAct-nMesIng,nMesAct-nMesIng-1)
		nDias  := IIF(nDiaAct>=nDiaIng,nDiaAct-nDiaIng,LAST_DAY(dMesAnt)-(nDiaIng-nDiaAct))
	ELSE
		nAnios := (nAnoAct - nAnoIng) - 1
        nMeses := IIF(nDiaAct>=nDiaIng,12-(nMesIng-nMesAct),12-(nMesIng-nMesAct+1))  //B5 Actua; B11 Ingreso
        nDias  := IIF(nDiaAct>=nDiaIng,nDiaAct-nDiaIng,LAST_DAY(dMesAnt)-(nDiaIng-nDiaAct))
	ENDIF

nLi++
@ nLi,003 PSAY ALLTRIM(STR(nDias)) + Space(10) +  ALLTRIM(STR(nMeses)) + Space(9) + ALLTRIM(STR(nAnios))

nLi++
Return Nil

//-------------------
Static Function fCabecVerbas(nReg)
//-------------------

If nLi > 69
	fCabecalho()     
EndIf

@ nLi,001 PSAY REPLICATE("_",131)
nLi++

@ nLi,023 PSAY "PERCEPCIONES" + Space(50) +  "DEDUCCIONES"

nLi++
@ nLi,001 PSAY REPLICATE("_",131)

nLi++
@ nLi,004 PSAY "CANTIDAD"
@ nLi,019 PSAY "CONCEPTOS"
@ nLi,051 PSAY "IMPORTE"
@ nLi,069 PSAY "CANTIDAD"
@ nLi,084 PSAY "CONCEPTOS"
@ nLi,116 PSAY "IMPORTE"

nLi++
Return Nil

//-------------------
Static Function fRodapeVerbas(nReg, nValor)
//-------------------

If nLi > 74
	fCabecalho()     
	fCabecVerbas(nReg)
EndIf

/*  Impresión Percepciones primero y Descuentos despues
@ nLI,014 PSAY "TOTAL DE " + IIF(nReg==1, "PERCEPCIONES", "DEDUCCIONES")
@ nLI,090 PSAY "$ " + Transform(nValor,"99,999,999.99")
*/

@ nLI,046 PSAY "_______________"
@ nLI,111 PSAY "_______________"
nLi++
@ nLI,004 PSAY "TOTAL DE PERCEPCIONES" + Space(21) + "$ " + Transform(nProv,"99,999,999.99")
@ nLI,069 PSAY "TOTAL DE DEDUCCIONES"  + Space(22) + "$ " + Transform(nDesc,"99,999,999.99")
nLi := nLi + 2
@ nLI,111 PSAY "_______________"
nLi++
@ nLI,069 PSAY "TOTAL A RECIBIR"  + Space(27) + "$ " + Transform(nProv - nDesc,"99,999,999.99")

Return Nil

//-------------------
Static Function fDetalhe(nReg, aVerbas)
//-------------------
Local Imprime	:= 0
 
/* Impresión Percepciones primero y Descuentos despues
For Imprime := 1 to Len(aVerbas)
	If nLi > 74
		fCabecalho()     
		fCabecVerbas(nReg)
	EndIf
	@ nLi,014 PSAY aVerbas[Imprime,1] + Space(10) + OemToAnsi(aVerbas[Imprime,2]) + Space(40) + "$ " + aVerbas[Imprime,3]
	nLi++    
Next Imprime
*/

// Impresión Percepciones y Deducciones en paralelo
For Imprime := 1 to Max(Len(aPdv), Len(aPdd)) 
	If nLi > 74
		fCabecalho()     
		fCabecVerbas(nReg)
	EndIf
	If Imprime <= Len(aPdv)
	@ nLi,005 PSAY aPdv[Imprime,1] + Space(5) + OemToAnsi(aPdv[Imprime,2]) + Space(10) + "$ " + aPdv[Imprime,3]
	End If
	If Imprime <= Len(aPdd)
	@ nLi,070 PSAY aPdd[Imprime,1] + Space(5) + OemToAnsi(aPdd[Imprime,2]) + Space(10) + "$ " + aPdd[Imprime,3]
	End If
nLi++	
Next Imprime

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºfRodape   ³IMPRESZ   ºAutor  ³Microsiga           º Data ³  10/30/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³fRodape - Impressao do Rodape da Rescisao.                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fRodape()

Local bCabec 	:= {|| nLi++, If(nLi > 74, fCabecalho(), .T.) }
Local nCont		:= 0
Local nWord		:= 0
Local nRecc		:= 0
Local cWord		:= ""

nLi++
Eval(bCabec)
@ nLI,001 PSAY " El (la) suscrito(a), " + SRA->RA_NOME + ", Dominicano, mayor de edad, portador  de la cedula de identidad y electoral"
Eval(bCabec)
@ nLI,001 PSAY " No." + ALLTRIM(SRA->RA_CIC) + ", domiciliado y residente en esta ciudad, por el presente documento declara y reconoce haber recibido a su entera"
Eval(bCabec)
@ nLI,001 PSAY " satisfacción  y en moneda de curso  legal de la Corporación Avícola del Caribe LTD, entidad comercial organizada de acuerdo con"
Eval(bCabec)
@ nLI,001 PSAY " las leyes de la República Dominicana, con domicilio y asiento social en la ciudad de Santo Domingo, la suma de"
Eval(bCabec)
@ nLI,001 PSAY " " + Extenso(nProv - nDesc) + "(RD$ " + Transform(nProv - nDesc,"99,999,999.99") + ")"
Eval(bCabec)
@ nLI,001 PSAY " por concepto de pago transaccional de los salarios, prestaciones laborales, asistencia económica y todo tipo de derechos que le"
Eval(bCabec)
@ nLI,001 PSAY " corresponden  o  pudieren  corresponderle  con  motivo de la terminación en fecha " + Str(Day(SRG->RG_DATADEM),2) + " de " + MesExtenso(Month(SRG->RG_DATADEM)) + " de " + Str(Year(SRG->RG_DATADEM),4) + ", a requerimiento del"
Eval(bCabec)
@ nLI,001 PSAY " suscrito,  de  la  relación  de  trabajo  que  existía  entre  las  partes;  que compañía, sus representantes, administradores,"
Eval(bCabec)
@ nLI,001 PSAY " accionistas  y causahabientes, declarando y  reconociendo que no tiene ninguna suma que reclamarle ni en el presente ni  en  el"
Eval(bCabec)
@ nLI,001 PSAY " futuro, legal ni contractualmente  ni por ningún otro concepto y que consecuentemente, renuncia a cualquier derecho o acción de"
Eval(bCabec)
@ nLI,001 PSAY " cualquier naturaleza por haber sido total y definitivamente desinteresado(a). El presente recibo ha sido formulado en presencia"
Eval(bCabec)
@ nLI,001 PSAY " de los testigos instrumentales requeridos al efecto, señores, _______________________________ y _______________________________"
Eval(bCabec)
@ nLI,001 PSAY " Nos. ____________________________ y ___________________________, domiciliados y residentes en esta ciudad de Santiago."

nLi := nLi + 2
Eval(bCabec)
@ nLi,010 PSAY "HECHO Y FIRMADO en a los"+Space(10)+ Str(Day(SRG->RG_DATADEM),2) + " de " + MesExtenso(Month(SRG->RG_DATADEM)) + " de " + Str(Year(SRG->RG_DATADEM),4)

nLi++
Eval(bCabec)
@ nLi,036 PSAY REPLICATE("_",60)
nLi++         
@ nLi,053 PSAY SRA->RA_NOME
nLi++
Eval(bCabec)
@ nLi,001 PSAY REPLICATE("_",40)
@ nLi,080 PSAY REPLICATE("_",40)
Eval(bCabec)
@ nLi,016 PSAY "TESTIGO"
@ nLi,096 PSAY "TESTIGO"

nLi++
Eval(bCabec)
@ nLI,001 PSAY " Yo, ________________________________________, Abogado Notario Público de los del Número Matricula __________________,"
Eval(bCabec)
@ nLI,001 PSAY " CERTIFICO  Y  DOY  FE,  que  la  firma  que  antecede  fue  puesta  en  mi  presencia  por  el  (la)  señor(a)"
Eval(bCabec)
@ nLI,001 PSAY " ________________________________________, ________________________________________ y _________________________________________"
Eval(bCabec)
@ nLI,001 PSAY " quienes me manifestaron, libre y voluntariamente, bajo la fe del juramento ser estas la firmas que acostumbran a usar en todos"
Eval(bCabec)
@ nLI,001 PSAY " los actos de su vida pública y privada por lo que debe dársele a la misma entera fe y crédito. En Santiago,  provincia  de  la"
Eval(bCabec)
@ nLI,001 PSAY " República Dominicana, a los ____________________ (     ) días del mes de ___________________ del año _____________."

nLi := nLi + 3
Eval(bCabec)
@ nLI,080 PSAY " ________________________________________"
nLi++
@ nLI,089 PSAY " ABOGADO-NOTARIO PÚBLICO"

If Type("cRodape") <> "U"
	
	nLi := nLi+2
	
	For nCont := 1 To Len(cRodape)
			@ nLi++,001 PSAY SubStr( cRodape, nCont,  132 )
			nCont := nCont+132
	Next
	
EndIf

Return Nil

Static Function TraeValor(Concepto,cNumEmp)
Local nValSRC :=	0
Local cConSRC	:=	Concepto
Local cNumSRC	:=	cNumEmp
Local cAliasT1 	:= GetNextAlias()
Local aArea		:=	GetArea()
Local cPer		:= SRG->RG_PERIODO
Local cNumPag	:= SRG->RG_SEMANA
Local cTipNom	:= SRG->RG_ROTEIR
Local cPRoces	:= SRG->RG_PROCES

If SRG->RG_EFETIVA = '3'
	cPer	:= SRG->RG_APLPER
	cNumPag	:= SRG->RG_APLNPG
	cTipNom	:= SRG->RG_APLROT
EndIf


BeginSql alias cAliasT1
	//%noparser%
	SELECT
	SRC.RC_VALOR
	FROM %table:SRC% SRC
	WHERE   SRC.%notDel%
	AND SRC.RC_FILIAL>=%exp:cFilialDe%
	AND SRC.RC_FILIAL<=%exp:cFilialAte%
	AND SRC.RC_PERIODO=%exp:cPer%
	AND SRC.RC_SEMANA=%exp:cNumPag%
	AND SRC.RC_ROTEIR=%exp:cTipNom%
	AND SRC.RC_PROCES=%exp:cProces%
	AND SRC.RC_MAT=%exp:cNumSRC%
	AND SRC.RC_PD=%exp:cConSRC%
	ORDER BY SRC.RC_MAT,SRC.RC_PD
EndSql

nValSRC	:= (cAliasT1)->RC_VALOR
DBCloseArea()
RestArea(aArea)

Return(nValSRC)
