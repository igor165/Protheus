#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPER860.CH"
#INCLUDE "report.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER860  ³ Autor ³ R.H. - Abel Ribeiro   ³ Data ³  12.01.2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Dotacao - Colombia                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER860(void)                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ BOPS/FNC      ³  Motivo da Alteracao            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Alfredo Med.³25/10/13  ³Prj.:COL11.8_RH³-MEX-Se incluyen las validaciones³±±
±±³            ³          ³Req.:239722    ³ contrato de trabajo permanente, ³±±
±±³            ³          ³               ³ 3 meses de forma ininterrumpida ³±± 
±±³            ³          ³               ³ salario mensual inferior a 2    ³±± 
±±³            ³          ³               ³ salarios mínimos mensuales      ³±±
±±³M.Camargo   ³04/03/14  ³TIKXSP         ³Se modifica query para que       ³±±
±±³            ³          ³               ³considere ra_silfolh normal      ³±±
±±³Alex Hdez.  ³23/02/16  ³PCREQ-9393     ³Merge para 12.1.9 para Col       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function GPER860()
Local	oReport   
Local	aArea 	:= GetArea()
Private cTitulo	:= OemToAnsi(STR0022)
Private aOrd    := {OemToAnsi(STR0004),OemToAnsi(STR0021)}	//"Matr¡cula"###"CC. + Matrícula"
Private cPerg   := "GP860R"
Private	cString	:= "SRA"				// alias do arquivo principal (Base)

If FindFunction("TRepInUse") .And. TRepInUse()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as perguntas selecionadas       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	pergunte(cPerg,.F.) 
	
    oReport := ReportDef()
    oReport:PrintDialog()
EndIF    

RestArea( aArea )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef  ³ Autor ³ Abel Ribeiro          ³ Data ³12/01/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³  Relatorio de Dotacao                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER860                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER860 - Generico - Release 4                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportDef()
Local oReport 
Local oSection1 
Local cDesc1	:= OemToAnsi(STR0001) + OemToAnsi(STR0002) + OemToAnsi(STR0003)	
	//"Relatorio de Dotacao ### "Ser  impresso de acordo com os parametros solicitados pelo"  ### "usu rio."
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao dos componentes de impressao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE REPORT oReport NAME "GPER860" TITLE cTitulo PARAMETER cPerg ACTION {|oReport| R860Imp(oReport)} DESCRIPTION OemtoAnsi(STR0017) TOTAL IN COLUMN	 
//"Este programa emite Relatorio de Dotação "

	DEFINE SECTION oSection1 OF oReport TITLE OemToAnsi(STR0016) TABLES "SRA" TOTAL IN COLUMN ORDERS aOrd
		DEFINE CELL NAME "FILIAL_CC" 	OF oSection1 ALIAS " " 	   	TITLE " " BLOCK {|| cFILCC}
		DEFINE CELL NAME "RA_FILIAL" 	OF oSection1 ALIAS cString
		DEFINE CELL NAME "RA_CC" 	 	OF oSection1 ALIAS cString
		DEFINE CELL NAME "CTT_DESC01" 	OF oSection1 ALIAS " "       	TITLE " " BLOCK {|| CDESCRIP}
		DEFINE CELL NAME "RA_MAT" 	 	OF oSection1 ALIAS cString
		DEFINE CELL NAME "RA_NOME" 	 	OF oSection1 ALIAS cString   
		DEFINE CELL NAME "RA_TPCIC" 	OF oSection1 ALIAS cString
		DEFINE CELL NAME "RA_CIC" 	 	OF oSection1 ALIAS cString
		DEFINE CELL NAME "RA_ADMISSA"	OF oSection1 ALIAS cString
		DEFINE CELL NAME "SALMEDIA"		OF oSection1 ALIAS " "		TITLE OemToAnsi(STR0020)	PICTURE "@E 999,999,999.99" BLOCK {|| NSALMED}
		DEFINE FUNCTION FROM oSection1:Cell("RA_MAT")		FUNCTION COUNT NO END SECTION 
	                                                                                              
Return(oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER860   ºAutor  ³Microsiga           º Data ³  12/01/2009 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R860Imp(oReport)

Local oSection  	:= oReport:Section(1)
Local cFiltro 		:= "" 
Local cAliasQry	:= ""
Local cSitQuery	:= ""
Local cCatQuery	:= ""  
Local cTitCC		:= "" 
Local cTitFil		:= ""
Local cAuxPrc		:= ""
Local nReg			:= 0
Local nTamCod		:= 0
Local X         	:= 0 
Local cAnioCuat	:=""
Local cTipoCont   :=""
Local cFechBF 		:= ""
Local dFechBaseF 	:= CTOD(" / / ")
Local cFecha		:=""
Local cFiltroC		:=""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Variaveis de Acesso do Usuario                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cAcessaSRA	:= &( " { || " + ChkRH( "GPER860" , "SRA" , "2" ) + " } " )
Private nOrdem		:= oSection:GetOrder()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
//³ mv_par01        //  Filial                                   ³
//³ mv_par02        //  Centro de Custo                          ³
//³ mv_par03        //  Matricula                                ³
//³ mv_par04        //  Nome                                     ³
//³ mv_par05        //  Situacoes                                ³
//³ mv_par06        //  Categorias                               ³
//³ mv_par07        //  Nome De                                  ³
//³ mv_par08        //  Nome Ate                                 ³
//³ mv_par09        //  Anio y Cuatrimestre                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cSituacao  	:= mv_par05
Private cCategoria 	:= mv_par06
Private dDataDe    	:= dDataBase - 120       //Periodo de Inicial para Busca SRD para Calculo da Média
Private cTipoSal   	:= 1              //Tipo de Salario no Relatorio
Private aInfo       	:= {}
Private cVerbasMed 	:= ""               
Private cTipoVerba 	:= '1'                   //Tipo De Verbas = "1"
Private cRoteiro		:= " "   
Private nValACL		:= 0
Private nSalMed		:= 0
Private cDESCRIP		:= ""
private cFILCC			:= ""
private nSalMin 		:= FTABELA("S007",1,6,NIL) // Salario Minimo

Private cTitulo    := OemToAnsi(STR0009)	//" RELACAO DE DOTACAO"	

cTitulo	+=	" - " + OemToAnsi(STR0018) + PADR(DtoC(dDataBase),10)		//"DATA REFERENCIA: "
                                                       
//-- Altera o titulo do relatorio
oReport:SetTitle(if(AllTrim(oReport:Title()) == AllTrim(cTitulo), OemToAnsi(cTitulo), oReport:Title()))

   If nOrdem==1                                                                                        
	//-- Quebrar e Totalizar por Filial
	DEFINE BREAK oBreakFil OF oSection WHEN oSection:Cell("RA_FILIAL")  TITLE OemToAnsi(STR0013)		// "TOTAL FILIAL -> "
		DEFINE FUNCTION FROM oSection:Cell("RA_MAT")		FUNCTION COUNT BREAK oBreakFil NO END REPORT
		oBreakFil:OnBreak({|x,y|cTitFil:=OemToAnsi(STR0013)+x,fInfo(@aInfo,y)})	//"TOTAL FILIAL -> "
	    oBreakFil:SetTotalText({||cTitFil}) 
	ElseIf nOrdem==2
	//-- Quebrar e Totalizar por C.COSTO
	DEFINE BREAK oBreakFil OF oSection WHEN oSection:Cell("FILIAL_CC")  TITLE OemToAnsi(STR0012)		// "TOTAL C.COSTO -> "
		DEFINE FUNCTION FROM oSection:Cell("RA_MAT")		FUNCTION COUNT BREAK oBreakFil NO END REPORT
		oBreakFil:OnBreak({|x,y|cTitFil:=OemToAnsi(STR0012)+x,fInfo(@aInfo,y)})	//"TOTAL C.COSTO -> "
	    oBreakFil:SetTotalText({||cTitFil}) 
	EndIF

    //-- Condicao de impressao do Funcionario
    oSection:SetLineCondition({|| fGP860Cond(cAliasQry) })
    
    //-- Obtiene código de tipo de contrato permanente 
    cTipoCont := ObtTipConP()

#IFDEF TOP
	cAliasQry := GetNextAlias()

	//-- Modifica variaveis para a Query 
	cSitQuery := ""
	For nReg:=1 to Len(cSituacao)
		cSitQuery += "'"+Subs(cSituacao,nReg,1)+"'"
		If ( nReg+1 ) <= Len(cSituacao)
			cSitQuery += "," 
		Endif
	Next nReg        
	cSitQuery := "%" + cSitQuery + "%"
	
	cCatQuery := ""
	For nReg:=1 to Len(cCategoria)
		cCatQuery += "'"+Subs(cCategoria,nReg,1)+"'"
		If ( nReg+1 ) <= Len(cCategoria)
			cCatQuery += "," 
		Endif
	Next nReg        
	cCatQuery := "%" + cCatQuery + "%"

	//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
	MakeSqlExpr(cPerg)
		
	BEGIN REPORT QUERY oSection
	
	If nOrdem == 1
		cOrdem := "%SRA.RA_FILIAL,SRA.RA_MAT%"
	ElseIf nOrdem == 2
		cOrdem := "%SRA.RA_FILIAL,SRA.RA_CC,SRA.RA_MAT%"
	Endif
	
	IF cTipoCont!=""
		cTipoCont := "%" + SubStr(cTipoCont,1,len(cTipoCont)-1) + "%"
		Else
		cTipoCont := "%%"
	End IF
	
	cFecha := RegFechaC() // Obtiene la fecha base final 
	If cFecha !=""
		dFechBaseF := CTOD(cFecha) - 90
		cFechBF := "%'" + DTOS(dFechBaseF) + "'%"
		Else
		cFecha := "%%"
	EndIf 
	
		
	BeginSql alias cAliasQry
		SELECT	SRA.RA_FILIAL,  SRA.RA_CC,      SRA.RA_MAT,     SRA.RA_NOME, SRA.RA_TPCIC,SRA.RA_CIC,  SRA.RA_SITFOLH, SRA.RA_CATFUNC, 
				SRA.RA_ADMISSA, SRA.RA_SALARIO,SRA.RA_PROCES, SRA.RA_TIPOSAL
		FROM %table:SRA% SRA
		WHERE SRA.RA_SITFOLH	IN	(%exp:Upper(cSitQuery)%) 	AND
			  SRA.RA_CATFUNC	IN	(%exp:Upper(cCatQuery)%)	AND
	 	      SRA.%notDel% 	AND 
	 	      SRA.RA_TIPOCO IN (%exp:cTipoCont%)   				AND
	 	      SRA.RA_ADMISSA < %exp:cFechBF%   
			      
		ORDER BY %exp:cOrdem%
	EndSql
	/*
	Prepara relatorio para executar a query gerada pelo Embedded SQL passando como 
	parametro a pergunta ou vetor com perguntas do tipo Range que foram alterados 
	pela funcao MakeSqlExpr para serem adicionados a query
	*/
	END REPORT QUERY oSection PARAM mv_par01, mv_par02, mv_par03, mv_par04
	TCSetField(cAliasQry,"RA_ADMISSA","D",8,0) // Formato de fecha

#ENDIF	

//-- Define o total da regua da tela de processamento do relatorio
oReport:SetMeter( 100 )  
oSection:Print()	 //Imprimir

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Termino do Relatorio                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFNDEF TOP
	dbSelectArea( "SRA" )
	Set Filter to
	dbSetOrder(1)
	Set Device To Screen
#ENDIF

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fGP860Cond    ³ Autor ³ Alfredo Medrano  ³ Data ³12/09/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica Condicao para Impressao da Linha do Relatorio     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER860 - A partir do Release 4                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
STATIC Function fGP860Cond(cAliasQry)    
Local 	  lRet	    := .T.
Local	  nCTT		:= RETORDEM("CTT","CTT_FILIAL+CTT_CUSTO") // regresa el índice 
Local 	  nAcum		:= 0
Local dFechBaseF  := CTOD(" / / ") 
Local nMesAnti		:= 0
Default cAliasQry	:= "SRA"                 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Consiste Filiais e Acessos                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF !( (cAliasQry)->RA_FILIAL $ fValidFil() ) .or. !Eval( cAcessaSRA )
	lRet	:= .F.	
EndIF

cDESCRIP := POSICIONE( "CTT", nCTT,XFILIAL("CTT") + (cAliasQry)->RA_CC, "CTT_DESC01" ) //Retorna el centro de costos  
cFILCC = (cAliasQry)->RA_FILIAL + " " + (cAliasQry)->RA_CC + " " + cDESCRIP  // Sucursal + Centro Costos + descripción C.C.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula Salario Base de Cotizacion					   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   IF (cAliasQry)->RA_TIPOSAL == "1"        // Tipo de Salario  Base Fijo
      nSalTeto:= (nSalMin * 2)				//	2 Salarios minimos
      
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Solo se imprimirá el registro si el Salario Base de ³
//³ Cotización es menor al tope de 2 salarios mínimos   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	  IF (cAliasQry)->RA_SALARIO > nSalTeto    
         lRet := .F.
      ELSE
         nSALMED := (cAliasQry)->RA_SALARIO 
      ENDIF  
       
   ElseIF  (cAliasQry)->RA_TIPOSAL == "3"  // Tipo de Salario  Base Variable 
   		
   		nAcum	:= fBuscaAcum ((cAliasQry)->RA_MAT,(cAliasQry)->RA_PROCES ) // Suma de Acumnulados
   		cFecha := RegFechaC() // obtiene Fecha Base Final
   		
   		If cFecha!="" 
   			dFechBaseF := ctod(cFecha)//(CTOD(cFecha) - 120) + 1
   			If (cAliasQry)->RA_ADMISSA <= ((dFechBaseF - 120) +1 ) 
   				nMesAnti := 4
   			Else
   				nMesAnti := (dFechBaseF - (cAliasQry)->RA_ADMISSA + 1)/30
   			EndIf
   			nSalBasCot := (nAcum/nMesAnti) + (cAliasQry)->RA_SALARIO
   			nSalTeto:= (nSalMin * 2)
   			
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Solo se imprimirá el registro si el Salario Base de ³
//³ Cotización es menor al tope de 2 salarios mínimos   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   			
   			IF  nSalBasCot > nSalTeto
         		lRet := .F.
      		ELSE
        		 nSALMED := nSalBasCot
     		ENDIF   
   		Else
   			lRet := .F.
   		EndIf		 		
   Endif	
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ObtTipConP ³ Autor ³ Alfredo Medrano    ³ Data ³ 23/10/2013 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ •Obtiene código de tipo de contrato permanente             ³±±  
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ObtTipConP()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³			   ³		³      ³            							 		    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ObtTipConP() 
	Local 	aArea	  := getArea()        
	Local	cTmpPer  := CriaTrab(Nil,.F.)
	Local  cQuery	  := ""    
	Local  cTipoCont:= ""
	
	cQuery := " SELECT RCC_CONTEU "
	CQuery += " FROM " + RetSqlName("RCC") +" RCC "
 	cQuery += " WHERE RCC_CODIGO='S018'"  
  	cQuery += " AND D_E_L_E_T_ = ' ' "
  	cQuery := ChangeQuery(cQuery)   
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cTmpPer,.T.,.T.) 

	 While  (cTmpPer)->(!EOF())
		IF SubStr((cTmpPer)->RCC_CONTEU,70,1) $ "1/2"
      		cTipoCont := cTipoCont + "'" + SubStr((cTmpPer)->RCC_CONTEU,1,2) + "',"
   		END IF
		(cTmpPer)-> (dbskip())
	EndDo
	
	(cTmpPer)->( dbCloseArea())
	restArea(aArea) 
		
Return cTipoCont  
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ RegFechaC    ³ Autor ³ Alfredo Medrano   ³ Data ³23/10/2013³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Arma Fecha Base Final según el cuatrimestre                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Caracter                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Localizado Colombia                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function RegFechaC()
Local nAnioCuat  	:= mv_par07   //Año y cuatrimestre
Local  cAnio	:=""         
Local cFechBF	:=""
Local cCuatri	:=""

cAnioCuat := alltrim(str(nAnioCuat))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ 1er cuatrimestre "1" 30/04 del año seleccionado     ³
//³ 2do cuatrimestre "2" 31/08 del año seleccionado     ³
//³ 3er cuatrimestre "3" 20/12 del año seleccionado     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cAnioCuat != "" .And. Len(cAnioCuat) = 5
	cAnio 	 := substr(cAnioCuat, 1,4)
	cCuatri := substr(cAnioCuat, 5,1)
	Do Case
	   Case  cCuatri == "1"
	   		  	cFechBF :="30/04/" + cAnio
	   Case  cCuatri == "2"
	 			cFechBF :="31/08/" + cAnio
	   Case cCuatri  == "3"
				cFechBF :="20/12/" + cAnio
	End Case		
EndIf

return  cFechBF 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  |fBuscaAcum ºAutor  ³Alfredo Medrano  º Data ³  		24/10/2013 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Proceso para obtener Suma de acumulados      						 º±±
±±º          ³					                      							 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ Número                                            				 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpO1:Caracter Matricula 											 º±±
±±º				 ExpO2:Caracter Proceso                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Localizado Colombia                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fBuscaAcum(cMat, cProces)

	Local 	aAreaC	  	:= getArea()        
	Local	cTmpPerC 	:= CriaTrab(Nil,.F.)
	Local  cQuery		:= ""    
	Local 	cAcumula 	:= ""
	Local  nAnioCuat  := mv_par07   //Año y cuatrimestre
	Local  cAnio		:= ""         
	Local  cCuatri		:= ""
	Local  nTotal		:= 0
	Default cMat 		:= ""
	Default cProces	:= "" 
	
	cAnioCuat	:= alltrim(str(nAnioCuat))
	cAnio		:= substr(cAnioCuat, 1,4) // obtiene el año
	cCuatri	:= substr(cAnioCuat, 5,1) // obtiene el cuatrimestre
	
	If cCuatri == "1"		
		cAcumula := " SUM(RG7_ACUM01 + RG7_ACUM02 + RG7_ACUM03 + RG7_ACUM04) AS Total "
	ElseIf cCuatri == "2"
		cAcumula := " SUM(RG7_ACUM05 + RG7_ACUM06 + RG7_ACUM07 + RG7_ACUM08) AS Total "
	ElseIf cCuatri == "3"
		cAcumula := " SUM(RG7_ACUM09 + RG7_ACUM10 + RG7_ACUM11 + RG7_ACUM12) AS Total "
	EndIf
	
	If cAcumula !=""
	
		cQuery := " SELECT" + cAcumula
		CQuery += " FROM " + RetSqlName("RG7") + " RG7 "
		cQuery += " INNER JOIN " +  RetSqlName("SRV") + " SRV "
		cQuery += " ON RG7_PD=RV_COD "
	 	cQuery += " WHERE RV_INCORP ='S' "
	 	cQuery += " AND RG7_MAT = '" + cMat + "' " 
	  	cQuery += " AND RG7_PROCES = '" + cProces + "' " 
	  	cQuery += " AND RG7_ANOINI = '" + cAnio + "' " 
	  	cQuery += " AND RG7_ANOFIM = '" + cAnio + "' " 
	  	cQuery += " AND RG7.D_E_L_E_T_ = ' ' "
	   cQuery += " AND SRV.D_E_L_E_T_ = ' ' "
	  	cQuery := ChangeQuery(cQuery)   
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cTmpPerC,.T.,.T.) 
	
		 If  (cTmpPerC)->(!EOF())
			  nTotal :=  (cTmpPerC)->Total
			 (cTmpPerC)-> (dbskip())
		EndIf
		
		(cTmpPerC)->( dbCloseArea())
		restArea(aAreaC)
		
	EndIF
	
 Return nTotal   
 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fFechaCuatr    ³ Autor ³ Alfredo Medrano ³ Data ³28/10/2013³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida cuatrimestre							                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Logico                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Localizado Colombia                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fFechaCuatr(nFechCua)
Local lRet 	  := .T.
Local cAnioCuat := ""
Local cCuatri	  := ""
Local cHelp	  := ""

cHelp 		:= OemToAnsi(STR0023)
cAnioCuat	:= alltrim(str(nFechCua))
If Len(cAnioCuat) = 5
cCuatri := substr(cAnioCuat, 5,1)
	If cCuatri $ "1/2/3" 
		lRet := .T.
	Else
		lRet := .F.
	EndIf
Else
lRet := .F.
EndIf

If lRet == .F.
Help( ' ' , 1 , "FECHCUATR" , , cHelp , 1 , 0 )
EndIf

Return( lRet ) 


                                                                        
