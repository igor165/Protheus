#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEA410.CH"
#INCLUDE "PONCALEN.CH"

Static cRclPostoInit

/*/
зддддддддддбддддддддддбдддддбдддддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁGPEA410   ЁAutorЁGuadalupe Santacruz      Ё Data Ё22/06/2004Ё
цддддддддддеддддддддддадддддадддддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Postos   										Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                    Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё            ACTUALIZACIONES DESDE LA CONTRUCCION INICIAL               Ё
цддддддддддддбддддддддддбдддддддддддбддддддддддддддддддддддддддддддддддд╢
ЁProgramador ЁData      ЁBOPS 	    ЁMotivo da Alteracao                Ё
цддддддддддддеддддддддддедддддддддддеддддддддддддддддддддддддддддддддддд╢
ЁEmerson G.R.Ё 12/09/06 Ё00000106234ЁEstrutura Organizacional(Release 4)Ё
ЁLuiz Gustavo| 10/01/07 ЁMelhoria   ЁInclusao da funcao MenuDef()       Ё
Ё			 Ё		    Ё           Ёpara versao 9.12.		        	Ё
Ё------------Ё----------Ё-----------Ё versao 11.5-----------------------Ё
ЁEmerson     Ё 22/06/11 Ё00000012463ЁAtender a Nova Interface Totvs 11.5Ё
ЁCampos      Ё          Ё      /2011Ёutilizando a funcao MsAdvSize( p/  Ё
Ё            Ё          Ё           Ёflexibilidade da tela de acordo comЁ
Ё            Ё          Ё           Ёa resolucao).                      Ё
ЁClaudinei S.Ё16/01/2013Ё     TGMC80ЁAjuste na Gpea410Mnt para nao      Ё
Ё			 Ё		    Ё           Ёocorrer error.log quando existir  	Ё
Ё			 Ё		    Ё           Ёcampo de usuario na tabela SQB.   	Ё
юддддддддддддаддддддддддадддддддддддаддддддддддддддддддддддддддддддддддды/*/
Function GPEA410( cAlias , nReg , nOpc , aNewAdvSize , lMinSize )

Local aSvKeys		:= GetKeys()
Local aArea			:= GetArea()
Local aAreaSQB		:= SQB->( GetArea() )
Local aAreaRCL		:= RCL->( GetArea() )
Local aAreaSRA		:= SRA->( GetArea() )
Local aAreaSRJ		:= SRJ->( GetArea() )
Local aAreaSQ3		:= SQ3->( GetArea() )
Local aIndexSQB		:= {}
Local cFiltraSQB	:= ""
Local cFunName		:= Upper( AllTrim( FunName() ) )

DEFAULT nOpc		:= 5
DEFAULT aNewAdvSize	:= MsAdvSize()
DEFAULT lMinSize	:= .F.

Private lIsGpea410	:= ( cFunName == "GPEA410" )
Private lReduzSize	:= lMinSize

/*
VALIDAгцO DE COMPARTILHAMENTO DE TABELAS RCL E SQB
*/
If FindFunction("fVldCompt")
	If !fVldCompt()
		Return .F.
	EndIf
EndIf	
Begin Sequence

    Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina														}

	Private bFiltraBrw	:= { || NIL }
	if cPaisLoc=="RUS"
		Private cCadastro   := OemToAnsi( STR0025 )
	Else
		Private cCadastro   := OemToAnsi( STR0006 )
	Endif
	
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Inicializa o filtro utilizando a funcao FilBrowse                      Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cFiltraRh	:= ChkRh( "GPEA410" , "SQB" , "1" ) 
	bFiltraBrw 	:= { || FilBrowse( "SQB" , @aIndexSQB , @cFiltraRH ) }
   	Eval( bFiltraBrw )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Chama a Funcao de Montagem do Browse                                   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	mBrowse( 6 , 1 , 22 , 75 , "SQB"  , NIL , NIL , NIL , NIL , NIL , nil )

    /*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Deleta o filtro utilizando a funcao FilBrowse                     	 Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	EndFilBrw( "SQB" , aIndexSQB ) 
	
End Sequence
		
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura os Dados de Entrada 											 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aArea )
RestKeys( aSvKeys )
RestArea( aAreaRCL )
RestArea( aAreaSQB )                                                    
RestArea( aAreaSRA )
RestArea( aAreaSRJ )
RestArea( aAreaSQ3 )
Return( NIL )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGpea410MntЁ Autor ЁGpe Santacruz          Ё Data Ё22/06/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁGpea410Mnt( cAlias , nReg , nOpc )							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias = Alias do arquivo                                   Ё
Ё          ЁnReg   = Numero do registro                                 Ё
Ё          ЁnOpc   = Numero da opcao selecionada                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGpea410()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Gpea410Mnt( cAlias , nReg , nOpc )

Local aArea				:= GetArea()
Local aSvKeys			:= GetKeys()
Local aButtons			:= {}
Local aAdvSize			:= {}
Local aInfoAdvSize		:= {}
Local aObjSize			:= {}
Local aObjCoords		:= {}
Local aSQBHeader		:= {}
Local aSQBCols			:= {}
Local aSQBFields		:= {}
Local aSQBAltera		:= {}
Local aSQBNotFields		:= {}
Local aSQBRecnos		:= {}
Local aRCLGdAltera  	:= {}
Local aRCLGdNaoAlt		:= {}
Local aRCLVirtGd		:= {}
Local aRCLVisuGd		:= {}
Local aRCLMemoGd		:= {}
Local aRCLHeader		:= {}
Local aRCLCols			:= {}
Local aLogGer			:= {}
Local aLog				:= {}
Local aLogTitle			:= {}
Local aLogGerTitle		:= {}
Local aPos				:= {}
Local bRCLGdDelOk		:= { || .F. }
Local bRCLKeySkip		:= { || .F. }
Local bSet15			:= { || NIL }
Local bSet24			:= { || NIL }
Local cFilSQB			:= ""
Local cCodSQB			:= ""
Local nOpcAlt			:= 0
Local nSQBUsado			:= 0
Local nRCLUsado			:= 0
Local nLoop				:= 0
Local nLoops			:= 0
Local nOpcNewGd			:= 0
Local nCnt				:= 0
Local oDlg				:= NIL
Local oSQBEnchoice		:= NIL	
Local oRclGetDados		:= NIL

Private nGetSX8Len		:= GetSX8Len()
Private aRCLQuery		:= {}
Private aColsAnt		:= {}
Private aRCLRecnos		:= {}
Private aRCLNotFields	:= {}
Private cSQBKeySeek		:= ""

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta os Dados para Enchoice							   	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd( aSQBNotFields , "QB_DEPTO"		)
	aAdd( aSQBNotFields , "QB_DESCRIC"		)

	aSQBCols		:= SQB->(GdMontaCols(@aSQBHeader,@nSQBUsado,NIL,NIL,NIL,aSQBNotFields,@aSQBRecnos,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,.T.))
	
//	cFilSQB			:= xFilial("SQB")
	cFilSQB			:= xFilial("RCL")
	cCodSQB			:= SQB->QB_DEPTO
	cSQBKeySeek		:= ( cFilSQB + 	cCodSQB )
	

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Cria as Variaveis de Memoria e Carrega os Dados Conforme o arЁ
	Ё quivo														   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	For nLoop := 1 To nSQBUsado
		aAdd( aSQBFields , aSQBHeader[ nLoop , 02 ] )
		SetMemVar( aSQBHeader[ nLoop , 02 ] , aSQBCols[ 01 , nLoop ] , .T. )
	Next nLoop

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Tratamento dos campos de usuario no aheader                  Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	
	aAdd( aSQBFields , "NOUSER" )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define los Campos que no seran cargados en el grid		   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd( aRCLNotFields , "RCL_FILIAL"	)
	aAdd( aRCLNotFields , "RCL_ESTRUT"	)
	aAdd( aRCLNotFields , "RCL_DEPTO"	)
	aAdd( aRCLNotFields , "RCL_DDEPTO"	)

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta os Dados para a GetDados							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	#IFDEF TOP
		aRCLQuery		:= Array( 05 )
		aRCLQuery[01]	:= "RCL_FILIAL='"+cFilSQB+"'"
		aRCLQuery[02]	:= " AND "
		aRCLQuery[03]	:= "RCL_DEPTO='"+cCodSQB+"'"
		aRCLQuery[04]	:= " AND "
		aRCLQuery[05]	:= "D_E_L_E_T_=' ' "
	#ENDIF

	bRCLKeySkip := NIL

	RCL->( dbSetOrder( RetOrdem( "RCL" , "RCL_FILIAL+RCL_DEPTO" ) ) )
	aRCLCols	:= RCL->(GdMontaCols(@aRCLHeader,@nRCLUsado,@aRCLVirtGd,@aRCLVisuGd,NIL,aRCLNotFields,@aRCLRecnos,"RCL",cSQBKeySeek,NIL,bRCLKeySkip,NIL,NIL,NIL,NIL,NIL,NIL,aRCLQuery,.F.,.F.,.T.))
	aColsAnt	:= aClone( aRCLCols )           

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁInicializa o Ultimo Valor do RCL_POSTO  				   	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF (;
			(;
				Empty( RclPostoGet() );
				.or.;
				Empty( StrTran( RclPostoGet() , "0" , "" ) );
			);
			.and.;
			!Empty( aRCLRecnos );
		)	
		RclPostoSet( RclPostoInit() )
	Else
		RclPostoSet( GdFieldGet( "RCL_POSTO" , 1 , .F. , aRCLHeader , aRCLCols ) )	
	EndIF
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define los Campos que no se alteraran solo se visualizaran   
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd( aRCLGdNaoAlt , "RCL_POSTO"	) //Consecutivo de plaza  
	aAdd( aRCLGdNaoAlt , "RCL_OPOSTO"	) //Numero de plazas ocupadas

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carga los Campos Editables para a GetDados				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	For nLoop := 1	To nRCLUsado
		SetMemVar( aRCLHeader[ nLoop , 02 ] , GetValType( aRCLHeader[ nLoop , 08 ] , aRCLHeader[ nLoop , 04 ] ) , .T. )
		IF (;
				( aScan( aRCLVirtGd		, aRCLHeader[ nLoop , 02 ] ) == 0 );
				.and.;
		   	 	( aScan( aRCLVisuGd		, aRCLHeader[ nLoop , 02 ] ) == 0 );
		   	 	.and.;
		   	 	( aScan( aRCLNotFields	, aRCLHeader[ nLoop , 02 ] ) == 0 );
		   	 	.and.	;
		   	 	( aScan( aRCLGdNaoAlt	, aRCLHeader[ nLoop , 02 ] ) == 0 );
		   	)
			aAdd( aRCLGdAltera , aRCLHeader[ nLoop , 02 ] )
		EndIF
	Next nLoop

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensiones los Objetos         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/   
	aAdvSize		:= MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
	aAdd( aObjCoords , { 000 , 030 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
    aPos 			:= {aObjSize[1,1],aObjSize[1,2],aObjSize[1,3],aObjSize[1,4]}
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define Bloque Para tecla de CONFIRMACION                     Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
 	bSet15	:= { || ( nOpcAlt := 1 , RestKeys( aSvKeys ) , oDlg:End() ) } //Confirmacion  

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define  Bloque para tecla de ELIMINAR						   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet24		:= { || ( nOpcAlt := 0 , RestKeys( aSvKeys ) , oDlg:End() ) } //Cancela

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta o Dialogo Principal para a Manutencao das Marcacoes    Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0006 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta o Objeto Enchoice para o SQB                      	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	oSQBEnchoice:= MsmGet():New(	"SQB"		,;
									nReg		,;
									2			,;
									NIL			,;
									NIL			,;
									NIL			,;
									aSQBFields	,;
									aPos		,;
									aSQBAltera	,;
									NIL			,;
									NIL			,;
									NIL			,;
									oDlg		,;
									NIL			,;
									.T.			 ;
								)
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta o Objeto GetDados para o RCL						   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	oRclGetDados	:= MsNewGetDados():New(	aObjSize[2,1]							,;
										aObjSize[2,2]								,;
										aObjSize[2,3]								,;
										aObjSize[2,4]								,;
										nOpcNewGd									,;
										Nil											,;
										Nil											,;			
										""											,;
										aRCLGdAltera								,;
										0											,;
										NIL                                         ,;											
										NIL  							    		,; 
										NIL											,;
									    bRCLGdDelOk        							,;
										oDlg										,;
										aRCLHeader									,;
										aRCLCols									 ;
									 )                 

	Aadd(aButtons,{"S4WB011N" , {|| ORGA030Mnt( "RCL" , oRclGetDados:aCols[oRclGetDados:nAt,(Len(oRclGetDados:aCols[oRclGetDados:nAt])-1)], 2 ) }, OemToAnsi(STR0020 ),OemToAnsi(STR0021) } ) //"HistСrico"###"Hist."

	If nOpc == 3	//Atualizar
		Aadd(aButtons,{"DESTINOS" , {|| ORGA030Mnt( "RCL" , oRclGetDados:aCols[oRclGetDados:nAt,(Len(oRclGetDados:aCols[oRclGetDados:nAt])-1)], 3 ),Gp410AtuGet(oRclGetDados) }, OemToAnsi(STR0022 ),OemToAnsi(STR0023) } ) //"Movimento"###"Mov."
	Endif
										                     
	ACTIVATE MSDIALOG oDlg CENTERED ON INIT	EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁRestaura as Teclas de Atalho								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	RestKeys( aSvKeys )
	

End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura os Dados de Entrada								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aArea )

Return( nOpcAlt )


/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRclPostoInit	ЁAutorЁMarinaldo de Jesus Ё Data Ё31/08/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao de Inicializacao da Numeracao Automatica do RCL_POSTOЁ
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO do campo RCL_POSTO		                        Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RclPostoInit()

Local IsInGetDados := IsInGetDados( { "RCL_POSTO" } )

Local cNewPosto
Local cNumRclPosto

If !IsInCallStack('GPEA410') .or. IsInCallStack('ORGA030MNT')

	If ( IsInGetDados )
		CursorWait()
		cNumRclPosto	:= GdNumItem( "RCL_POSTO" , RclPostoGet() )
		cNewPosto		:= cNumRclPosto
	EndIf
	cNumRclPosto := GetNewCodigo("RCL","RCL_POSTO","RCL_FILIAL+RCL_POSTO",NIL,.F.,.F.,xFilial("RCL"),"P",cNewPosto)
	RclPostoSet( cNumRclPosto )
	
	If ( IsInGetDados )
		CursorArrow()
	EndIf
EndIf

Return( cNumRclPosto )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRclPostoSet  ЁAutorЁMarinaldo de Jesus    Ё Data Ё31/08/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSetar a Variavel Static cRclPostoInit						Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                            	   	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RclPostoSet( cNewItem )

IF Empty( cNewItem )
	cNewItem := Replicate( "0" , GetSx3Cache( "RCL_POSTO" , "X3_TAMANHO" ) )
EndIF	

cRclPostoInit := cNewItem

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRclPostoGet  ЁAutorЁMarinaldo de Jesus    Ё Data Ё31/08/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁObter o Conteudo da Variavel Static cRclPostoInit			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                            	   	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RclPostoGet()

IF Empty( cRclPostoInit )
	RclPostoSet( NIL )
EndIF

Return( cRclPostoInit )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRclPostoClearЁAutorЁMarinaldo de Jesus    Ё Data Ё31/08/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁLimpar a Variavel Static cRclPostoInit						Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                            	   	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RclPostoClear()

cRclPostoInit := NIL

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGp410AtuGet  ЁAutorЁLeandro Drumond		  Ё Data Ё30/09/2014Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁAtualiza getdados											Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                            	   	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Gp410AtuGet(oGet)

oGet:SetArray( RCL->(GdMontaCols(Nil,Nil,Nil,Nil,NIL,aRCLNotFields,Nil,"RCL",cSQBKeySeek,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,aRCLQuery,.F.,.F.,.T.)) )

oGet:Refresh(.T.)

Return .T.

/*                                	
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Gustavo     Ё Data Ё10/01/2007Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas bibliotecas Framework da Versao 9.12 .      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGPEA410                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/   

Static Function MenuDef()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define Array q contiene las Rutinas a executar do programa   Ё
	Ё ----------- Elementos contidos por dimensao ------------     Ё
	Ё 1. Nome a aparecer no cabecalho                              Ё
	Ё 2. Nome da Rotina associada                                  Ё
	Ё 3. Usado pela rotina                                         Ё
	Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
	Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
	Ё    2 - Simplesmente Mostra os Campos                         Ё
	Ё    3 - Inclui registros no Bancos de Dados                   Ё
	Ё    4 - Altera o registro corrente                            Ё
	Ё    5 - Remove o registro corrente do Banco de Dados          Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	Private aRotina := {	{ STR0001 , "PesqBrw"			, 0 , 01, ,.F. } ,; 	//"Pesquisar"
							{ STR0002 , "Gpea410Mnt"		, 0 , 02 } ,; 	//"Visualizar"
							{ STR0004 , "Gpea410Mnt"		, 0 , 04 } ; 	//"Atualizar"
															}

Return aRotina
