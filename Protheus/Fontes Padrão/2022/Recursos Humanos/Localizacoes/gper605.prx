#Include "PROTHEUS.CH"
#INCLUDE "GPER605.CH"
#INCLUDE "REPORT.CH"

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥ GPER605  ≥ Autor   ≥ Ricardo Berti  		  ≥ Data ≥ 11.04.13 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Relatorio Controle de Ferias para a Bolivia                  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ GpeR605() 	                                                ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥                                                              ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ Generico                                                     ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥         ATUALIZACOES SOFRIDAS DESDE A CONSTRUÄAO INICIAL                ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Programador ≥ Data   ≥ Chamado≥  Motivo da Alteracao                     ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥            ≥        ≥        ≥                                          ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function GPER605()
Local oReport 
Local cMsgDic		:= ""		//Mensagem para validacao de dicionario de dados

Private cAliasMov	:= "SRA"
Private cAliasCc   	:= IF( SuperGetMv("MV_MCONTAB") == "CTB" , "CTT" , "SI3" )

/*⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø  
//≥ Checa atualiz.obrigatoria e informa ao usuario	 	  ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
If !CpoUsado( "RF_FERPAGA" )
	cMsgDic := fRhUpdMsg( 215 )
	MsgAlert( cMsgDic )
	Return Nil
EndIf

If FindFunction("TRepInUse") .And. TRepInUse()

	//-- Interface de impressao
	Pergunte("GPE605R",.F.)
   	oReport := ReportDef()
	oReport:PrintDialog()

EndIf

Return Nil


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥ ReportDef≥ Autor ≥ Ricardo Berti  		≥ Data ≥ 11.04.13 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Definicao do relatorio                                     ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function ReportDef()

//-- Objeto Relatorio
Local oReport  

//-- Objeto Section
Local oSection1   
Local oSection2   
Local oSection3
Local oSection4
Local oSection5     
Local oSection6
Local oSection7
Local oSection8

Local	cDesc		:=	STR0001 + ". "+STR0002		// "Ferias Vencidas no Periodo"	# "Ser† impresso de acordo com os parametros solicitados pelo usuario."

Local aOrd     := {	STR0003,; 			//"Matricula"
				 	STR0004,;			//"Nome"
				 	STR0005,;			//"C.Custo"
				 	STR0006}			//"Processo"

//-- Inicio definicao do Relatorio
DEFINE REPORT oReport NAME "GPER605" TITLE OemToAnsi(STR0009) PARAMETER "GPE605R" ACTION {|oReport| PrintReport(oReport)} DESCRIPTION cDesc TOTAL IN COLUMN  //"CONTROLE DE FERIAS"
                                     
//-- Utilizada essa section para as ordens 1=Matricula e 2=Nome
//-- Section de Funcionario
DEFINE SECTION oSection1 OF oReport TABLES "SRA" TITLE STR0010 ORDERS aOrd  TOTAL IN COLUMN
oSection1:SetHeaderBreak(.T.)
				
	DEFINE CELL NAME "RA_FILIAL"	OF oSection1 ALIAS "SRA"
	DEFINE CELL NAME "RA_MAT" 		OF oSection1 ALIAS "SRA" SIZE 10
	DEFINE CELL NAME "RA_NOME"	 	OF oSection1 ALIAS "SRA"

	oSection1:Cell("RA_FILIAL"):Disable()
     
     //-- Section de Ferias no Periodo do Funcionario
	DEFINE SECTION oSection2 OF oSection1 TABLES "SRF" TITLE STR0001 TOTAL IN COLUMN
	oSection2:SetHeaderBreak(.T.)

		DEFINE CELL NAME "RF_DATABAS" 	OF oSection2 ALIAS "SRF" TITLE STR0011+CRLF+STR0012
		DEFINE CELL NAME "RF_DATAFIM" 	OF oSection2 ALIAS "SRF" TITLE CRLF+STR0031
		DEFINE CELL NAME "GESTAO" 		OF oSection2 TITLE CRLF+STR0014 BLOCK {|| Str(Year(oSection2:Cell("RF_DATABAS"):GetValue(.T.)),4) }
		DEFINE CELL NAME "DIASDIR" 		OF oSection2 TITLE STR0032+CRLF+STR0033 PICTURE PesqPict("SRF","RF_DIASDIR")  // DIAS HABILES
		DEFINE CELL NAME "D_USADOS"		OF oSection2 TITLE STR0032+CRLF+STR0034 PICTURE PesqPict("SRF","RF_DFERANT")  // DIAS USADOS
		DEFINE CELL NAME "D_PAGLIQ"		OF oSection2 TITLE STR0032+CRLF+STR0035 PICTURE PesqPict("SRF","RF_FERPAGA")  // DIAS PAGADOS
		DEFINE CELL NAME "DETALHE" 		OF oSection2 TITLE CRLF+STR0016 SIZE 50
		DEFINE CELL NAME "FER_DTINI" 	OF oSection2 TITLE STR0007+CRLF+STR0030 SIZE 10 HEADER ALIGN LEFT
		DEFINE CELL NAME "FER_DTFIM" 	OF oSection2 TITLE STR0008+CRLF+STR0031 SIZE 10 HEADER ALIGN RIGHT
		DEFINE CELL NAME "FIRMARESP" 	OF oSection2 TITLE CRLF+STR0018 BLOCK {|| Replic("_",15) }

//-- Utilizada essa section para as ordens 3=Centro de Custo
//-- Section de Centro de Custo
DEFINE SECTION oSection3 OF oReport TABLES "SRA" TITLE STR0005 TOTAL IN COLUMN
oSection3:SetHeaderBreak(.T.)

	DEFINE CELL NAME "RA_FILIAL" 	OF oSection3 ALIAS "SRA"
	DEFINE CELL NAME "RA_CC" 		OF oSection3 ALIAS "SRA"  // SIZE (TamSx3("RA_CC")[1]+3)
	DEFINE CELL NAME "DESCRICAO" 	OF oSection3 ALIAS "   " BLOCK {|| DescCc((cAliasMov)->RA_CC,cFilaInfo) } SIZE 30

	//-- Section de Funcionarios do Centro de Custo
	DEFINE SECTION oSection4 OF oSection3 TABLES "SRA" TITLE STR0010 TOTAL IN COLUMN
	oSection4:SetHeaderBreak(.T.)

		DEFINE CELL NAME "RA_MAT" 	OF oSection4 ALIAS "SRA" SIZE 10
		DEFINE CELL NAME "RA_NOME" 	OF oSection4 ALIAS "SRA"


      //-- Section de Ferias Vencidas no Periodo do Funcionario
		DEFINE SECTION oSection5 OF oSection4 TABLES "SRF" TITLE STR0001 TOTAL IN COLUMN
		oSection5:SetHeaderBreak(.T.)

		DEFINE CELL NAME "RF_DATABAS" 	OF oSection5 ALIAS "SRF" TITLE STR0011+CRLF+STR0012
		DEFINE CELL NAME "RF_DATAFIM" 	OF oSection5 ALIAS "SRF" TITLE CRLF+STR0031
		DEFINE CELL NAME "GESTAO" 		OF oSection5 TITLE CRLF+STR0014 BLOCK {|| Str(Year(oSection5:Cell("RF_DATABAS"):GetValue(.T.)),4) }
		DEFINE CELL NAME "DIASDIR" 		OF oSection5 TITLE STR0032+CRLF+STR0033 PICTURE PesqPict("SRF","RF_DIASDIR")  // DIAS HABILES
		DEFINE CELL NAME "D_USADOS"		OF oSection5 TITLE STR0032+CRLF+STR0034 PICTURE PesqPict("SRF","RF_DFERANT")  // DIAS USADOS
		DEFINE CELL NAME "D_PAGLIQ"		OF oSection5 TITLE STR0032+CRLF+STR0035 PICTURE PesqPict("SRF","RF_FERPAGA")  // DIAS PAGADOS
		DEFINE CELL NAME "DETALHE" 		OF oSection5 TITLE CRLF+STR0016 SIZE 50
		DEFINE CELL NAME "FER_DTINI" 	OF oSection5 TITLE STR0007+CRLF+STR0030 SIZE 10 HEADER ALIGN LEFT
		DEFINE CELL NAME "FER_DTFIM" 	OF oSection5 TITLE STR0008+CRLF+STR0031 SIZE 10 HEADER ALIGN RIGHT
		DEFINE CELL NAME "FIRMARESP" 	OF oSection5 TITLE CRLF+STR0018 BLOCK {|| Replic("_",15) }

//-- Utilizada essa section para as ordens 4=Processo
//-- Section de Processo
DEFINE SECTION oSection6 OF oReport TABLES "SRA", "RCJ" TITLE STR0006 TOTAL IN COLUMN	// "Processo"
oSection6:SetHeaderBreak(.T.)
				
	DEFINE CELL NAME "RA_FILIAL"	OF oSection6 ALIAS "SRA"
	DEFINE CELL NAME "RA_PROCES" 	OF oSection6 ALIAS "SRA"
	DEFINE CELL NAME "RCJ_DESCRI"	OF oSection6 ALIAS "RCJ"
		
	oSection6:Cell("RA_FILIAL"):Disable()

	//-- Section de Funcionarios do Centro de Custo
	DEFINE SECTION oSection7 OF oSection6 TABLES "SRA" TITLE STR0010 TOTAL IN COLUMN
	oSection7:SetHeaderBreak(.T.)
				
		DEFINE CELL NAME "RA_MAT" 	OF oSection7 ALIAS "SRA" SIZE 10
		DEFINE CELL NAME "RA_NOME" 	OF oSection7 ALIAS "SRA"

      //-- Section de Ferias Vencidas no Periodo do Funcionario
		DEFINE SECTION oSection8 OF oSection7 TABLES "SRF" TITLE STR0001 TOTAL IN COLUMN
		oSection8:SetHeaderBreak(.T.)
	
		DEFINE CELL NAME "RF_DATABAS" 	OF oSection8 ALIAS "SRF" TITLE STR0011+CRLF+STR0012
		DEFINE CELL NAME "RF_DATAFIM" 	OF oSection8 ALIAS "SRF" TITLE CRLF+STR0031
		DEFINE CELL NAME "GESTAO" 		OF oSection8 TITLE CRLF+STR0014 BLOCK {|| Str(Year(oSection8:Cell("RF_DATABAS"):GetValue(.T.)),4) }
		DEFINE CELL NAME "DIASDIR" 		OF oSection8 TITLE STR0032+CRLF+STR0033 PICTURE PesqPict("SRF","RF_DIASDIR")  // DIAS HABILES
		DEFINE CELL NAME "D_USADOS"		OF oSection8 TITLE STR0032+CRLF+STR0034 PICTURE PesqPict("SRF","RF_DFERANT")  // DIAS USADOS
		DEFINE CELL NAME "D_PAGLIQ"		OF oSection8 TITLE STR0032+CRLF+STR0035 PICTURE PesqPict("SRF","RF_FERPAGA")  // DIAS PAGADOS
		DEFINE CELL NAME "DETALHE" 		OF oSection8 TITLE CRLF+STR0016 SIZE 50
		DEFINE CELL NAME "FER_DTINI" 	OF oSection8 TITLE STR0007+CRLF+STR0030 SIZE 10 HEADER ALIGN LEFT
		DEFINE CELL NAME "FER_DTFIM" 	OF oSection8 TITLE STR0008+CRLF+STR0031 SIZE 10 HEADER ALIGN RIGHT
		DEFINE CELL NAME "FIRMARESP" 	OF oSection8 TITLE CRLF+STR0018 BLOCK {|| Replic("_",15) }

oSection1:SetReadOnly() 
oSection2:SetReadOnly()
oSection3:SetReadOnly()
oSection4:SetReadOnly()
oSection5:SetReadOnly()
oSection6:SetReadOnly()
oSection7:SetReadOnly()
oSection8:SetReadOnly()

oSection1:SetNoFilter("SRA")
oSection2:SetNoFilter("SRF")
oSection3:SetNoFilter("SRA")
oSection4:SetNoFilter("SRA")
oSection5:SetNoFilter("SRF")
oSection6:SetNoFilter("SRA")
oSection6:SetNoFilter("RCJ")
oSection7:SetNoFilter("SRA")
oSection8:SetNoFilter("SRA")

Return oReport

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥PrintReport≥ Autor ≥ Ricardo Berti  		   ≥ Data ≥ 11.04.13 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Relacao de Liquidos - Relatorio Personalizavel                ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function PrintReport(oReport)     

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Declaracao de Variaveis Locais                               ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
//-- Numerico
Local nOrdem	   	:= oReport:Section(1):GetOrder()
Local nCont			:= 0
Local nTamProc		:= GetSx3Cache( "RA_PROCES", "X3_TAMANHO" )

//-- Objeto                
Local oSection1 	:= IIf(nOrdem == 3, oReport:Section(2), IIf(nOrdem == 4, oReport:Section(3) , NIL)) 	//Centro de Custo ou Processo
Local oSection2 	:= IIf(nOrdem == 3 .or. nOrdem == 4, oSection1:Section(1), oReport:Section(1))			// Funcionario
Local oSection3 	:= oSection2:Section(1)																						// Ferias Vencidas

//-- String
Local cCodPD		:= fGetCodFol("0072")  // Verba Ferias
Local cChave		:= ""
Local cFilMat		:= ""
Local cFiltro		:= ""
Local cSitQuery		:= ""
Local cCatQuery		:= ""
Local cSvProcesso	:= ""
Local cMaxProces	:= ""
Local cMinProces	:= "ZZZZZZ"

//-- Logico
Local lAllProc		:= .F.

//--Data
Local cCposSel		:= ""

//-- Array
Local aMovSR8		:= {}


//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Declaracao de Variaveis Privadas                             ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
//--String
Private cAliasSRF	:= "SRF"
Private cFilaInfo	:= ""

//-- Array
Private aInfo   	:= {}
Private aStruSR8	:= SR8->( dbStruct() )

/*
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Variaveis utilizadas para parametros                         ≥
≥ mv_par01		// Filial   								   ≥
≥ mv_par02		// Centro de Custo   						   ≥
≥ mv_par03		// Matricula   								   ≥
≥ mv_par04		// Nome   									   ≥
≥ mv_par05		// Processo									   ≥
≥ mv_par06		// Situacoes a Imprimir						   ≥
≥ mv_par07		// Categorias a Imprimir					   ≥
≥ mv_par08 		// Centro de custo em outra pagina			   ≥
≥ mv_par09 		// Processo em outra pagina 				   ≥
≥ mv_par10 		// Data Inicio de Referencia				   ≥
≥ mv_par11 		// Data Final de Referencia	   				   ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/

cProcesso	:= mv_par05
cSit     	:= mv_par06
cCat     	:= mv_par07
lSaltCC  	:= mv_par08 == 1
lSaltProc	:= mv_par09 == 1
dDataIni 	:= mv_par10
dDataFim 	:= mv_par11

	If nOrdem == 1 .or. nOrdem == 2
		//Total de Funcionarios da Filial                    
		DEFINE BREAK oBreakFil OF oReport WHEN oSection2:Cell("RA_FILIAL") TITLE STR0024
		DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") FUNCTION COUNT BREAK oBreakFil NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		
		//Total da Filial                    
		DEFINE BREAK oBreakFil2 OF oReport WHEN oSection2:Cell("RA_FILIAL") TITLE STR0027
		oBreakFil:SetPageBreak(.T.)
		
	Else
		If nOrdem == 3
			//-- Total de Funcionarios da Section Centro de Custo
			DEFINE BREAK oBreakCC OF oReport WHEN oSection1:Cell("RA_CC") TITLE STR0019
			DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT BREAK oBreakCC NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
				
			//-- Total da Section Centro de Custo
			DEFINE BREAK oBreakCC2 OF oReport WHEN oSection1:Cell("RA_CC") TITLE STR0028
			If lSaltCC
				oBreakCC2:SetPageBreak(.T.)
			EndIf 

		ElseIf nOrdem == 4
			//-- Total de Funcionarios da Section Processo
			DEFINE BREAK oBreakProc OF oReport WHEN oSection1:Cell("RA_PROCES") TITLE STR0020
			DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT BREAK oBreakProc NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
					
			//-- Total da Section Processo
			DEFINE BREAK oBreakProc2 OF oReport WHEN oSection1:Cell("RA_PROCES") TITLE STR0029
			If lSaltProc
				oBreakProc2:SetPageBreak(.T.)
			EndIf 

		EndIf

		//Total de Funcionarios da Filial                    
		DEFINE BREAK oBreakCCFil OF oReport WHEN oSection1:Cell("RA_FILIAL") TITLE STR0024 PAGE BREAK
		DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") FUNCTION COUNT BREAK oBreakCCFil NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"

		//Total da Filial                    
		DEFINE BREAK oBreakCCFil2 OF oReport WHEN oSection1:Cell("RA_FILIAL") TITLE STR0027

	EndIf

	lAllProc 	:= Iif(AllTrim( cProcesso ) == "*" .Or. Empty(cProcesso), .T., .F.)

	//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	//≥ Faz filtro no arquivo...                                                 ≥
	//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ

		cAliasMov := GetNextAlias()
		cAliasSRF := cAliasMov

		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeSqlExpr("GPE605R")

		//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		//≥ Modifica variaveis para a Query                                          ≥
		//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
		//-- Situacao do Funcionario
		cSitQuery := "" 
		For nCont:=1 to Len(cSit)
			cSitQuery += "'"+Subs(cSit,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cSit)
				cSitQuery += "," 
			Endif
		Next nCont     
		cSitQuery := "%" + cSitQuery + "%"
		
		//-- Categoria do Funcionario
		cCatQuery := ""
		For nCont:=1 to Len(cCat)
			cCatQuery += "'"+Subs(cCat,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cCat)
				cCatQuery += "," 
			Endif
		Next nCont
		cCatQuery := "%" + cCatQuery + "%"
                  
		cFiltro := ""
		/*
		⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		≥Gerar lista de Itens dos Processos.  										  ≥
		¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
		If !lAllProc 
			cSvProcesso := cProcesso
			cProcesso 	:= ""                           
			For nCont:= 1 to Len(Alltrim(cSvProcesso)) Step nTamProc
				cProcesso += "'" + Substr(cSvProcesso, nCont, nTamProc) + "',"
				If Substr(cSvProcesso, nCont, nTamProc) > cMaxProces 
					cMaxProces := Substr(cSvProcesso, nCont, nTamProc)
				EndIf
				If Substr(cSvProcesso, nCont, nTamProc) < cMinProces
					cMinProces := Substr(cSvProcesso, nCont, nTamProc)
				EndIf
			Next nCont
			cProcesso := Substr( cProcesso, 1, Len(cProcesso)-1)
			cFiltro := " SRA.RA_PROCES  IN (" + cProcesso + ") AND "
		EndIf         
		
		cFiltro := "%" + cFiltro + "%"
		
		//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		//≥ Monta string de ordenacao                                          		  ≥
		//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
		If nOrdem == 1												// Matricula
			cOrdem := "%RA_FILIAL, RA_MAT, RF_DATABAS%"
		ElseIf nOrdem == 2										// Nome
			cOrdem := "%RA_FILIAL, RA_NOME, RA_MAT, RF_DATABAS%"
		ElseIf nOrdem == 3										// Centro de Custo
			cOrdem := "%RA_FILIAL, RA_CC, RA_MAT, RF_DATABAS%"
		ElseIf nOrdem == 4										// Processo
			cOrdem := "%RA_FILIAL, RA_PROCES, RA_MAT, RF_DATABAS%"
		EndIf

		cCposSel := "%"
		cCposSel += "RA_FILIAL, RA_MAT, RA_NOME, RA_CC, RA_PROCES, RA_TNOTRAB, RA_SEQTURN, "
		cCposSel += "RCJ_DESCRI, RF_FILIAL, RF_MAT,"
		cCposSel += "RF_DATABAS, RF_DATAFIM, RF_DIASDIR, RF_DIASANT, RF_DFERVAT, RF_DFERAAT, RF_DFERANT, RF_STATUS, RF_FERPAGA"
		cCposSel += "%"
	
		//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		//≥ Inicializa Query                                                  		  ≥
		//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
		If nOrdem == 3 .or. nOrdem == 4
			BEGIN REPORT QUERY oSection1
		Else
			BEGIN REPORT QUERY oSection2
		EndIf
	
		BeginSql alias cAliasMov   
			SELECT 					 
				%exp:cCposSel%
			FROM   %table:SRA% SRA, %table:RCJ% RCJ, %table:SRF% SRF
			WHERE  	SRA.RA_FILIAL = SRF.RF_FILIAL	AND
			       	SRA.RA_MAT	 = SRF.RF_MAT		AND
			       	SRA.RA_PROCES = RCJ.RCJ_CODIGO	AND
					SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%)	AND
				    SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%)	AND
				    %exp:cFiltro%
					SRF.%notDel%	AND 
					SRA.%notDel%
			ORDER BY %exp:cOrdem%
		EndSql
	
		/*
		Prepara relatorio para executar a query gerada pelo Embedded SQL passando como 
		parametro a pergunta ou vetor com perguntas do tipo Range que foram alterados 
		pela funcao MakeSqlExpr para serem adicionados a query
		*/
		If nOrdem == 3 .or. nOrdem == 4
			END REPORT QUERY oSection1 PARAM mv_par01, mv_par02, mv_par03, mv_par04
		Else
			END REPORT QUERY oSection2 PARAM mv_par01, mv_par02, mv_par03, mv_par04
		EndIf
		
		If nOrdem == 3
			oSection2:SetParentQuery()
		ElseIf nOrdem == 4
			oSection2:SetParentQuery()
		EndIf

		//-- Utiliza a query do Pai (Section2) - Funcionarios
		oSection3:SetParentQuery()


	//-- Define o total da regua da tela de processamento do relatorio
	oReport:SetMeter((cAliasMov)->( RecCount() ))  

	//-- Impressao na quebra de pagina - Impressao das informacoes da Empresa e Filial
 	oReport:OnPageBreak({|| fCabec(oReport) })

  	If nOrdem == 3 .or. nOrdem == 4
		//-- Condicao de impressao do Funcionario
  		If nOrdem == 3
  			oSection1:SetLineCondition({|| fCondSRA(oSection2) })
	  		oSection2:SetLineCondition({|| fCondSRA(oSection2)}) 
  		Else
  			oSection1:SetLineCondition({|| fCondSRA(oSection1) })
	  		oSection2:SetLineCondition({|| fCondSRA(oSection1)}) 
  		EndIf
  	Else
		//-- Condicao de impressao do Funcionario
		oSection2:SetLineCondition({|| fCondSRA(oSection2) }) 
  	EndIf

//-- Impressao do Relatorio
	
While (cAliasMov)->(!Eof())
	//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	//≥ Incrementa Regua Processamento                               ≥
	//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
	oReport:IncMeter()
                                
	If oReport:Cancel()
		Exit
	EndIf 

	If !fCondSRA()
		// Nao encontrou SRF 
		DbSelectArea(cAliasMov)
		(cAliasMov)->( DbSkip() )
		Loop
	EndIf
		
	cFilMat := (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT	

  	If (nOrdem == 3 .And. (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC <> cChave ) .Or. ;
  	   (nOrdem == 4 .And. (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES <> cChave )

		oSection1:Init(.T.)
		oSection1:PrintLine()

		If nOrdem == 3
			cChave := (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC
		Else
			cChave := (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES
		EndIf
	
	EndIf

	oSection2:Init(.T.)
	oSection2:PrintLine()

	oSection3:Init(.T.)

	While (cAliasSRF)->(!Eof()) .And. (cAliasSRF)->RF_FILIAL + (cAliasSRF)->RF_MAT == cFilMat
		
		/*⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		  ≥ Buscar lancamentos do Funcionario na Tabela SR8 			 |
		  ¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
	  	aMovSR8	:= RetornaMovSR8( 	(cAliasMov)->RA_FILIAL	,; 	// Filial do funcionario corrente
									(cAliasMov)->RA_MAT	  	,; 	// Matricula do funcionario corrente
									(cAliasSRF)->RF_DATABAS ,; 	// Data de (SRF)
									(cAliasSRF)->RF_DATAFIM	,; 	// Data Ate (SRF)
									cCodPD	 				 ; 	// Cod.Verba Ferias
									)
		oSection3:Cell("RF_DATABAS"):SetValue((cAliasSRF)->RF_DATABAS)
		oSection3:Cell("RF_DATAFIM"):SetValue((cAliasSRF)->RF_DATAFIM)
		oSection3:Cell("D_USADOS"):SetValue(0)
		oSection3:Cell("D_PAGLIQ"):SetValue(0)
		oSection3:Cell("DETALHE"):SetValue("")

		If !Empty(aMovSR8)
			oSection3:Cell("FER_DTINI"):Show()
			oSection3:Cell("FER_DTFIM"):Show()
				
			nSaldo := (cAliasSRF)->RF_DIASDIR
			For nCont :=1 to Len(aMovSR8)
				
				oSection3:Cell("DIASDIR"):SetValue(nSaldo)
				oSection3:Cell("D_USADOS"):SetValue(aMovSR8[nCont,12])
				nSaldo -= aMovSR8[nCont,12]
				
				If nSaldo > 0
					cMsg := "Vacacion Parcial"
				Else
					cMsg := "Vacacion Total"
					nSaldo := 0
				EndIf
				                
				oSection3:Cell("DETALHE"):SetValue(cMsg)
				oSection3:Cell("FER_DTINI"):SetValue(aMovSR8[nCont,5])
				oSection3:Cell("FER_DTFIM"):SetValue(aMovSR8[nCont,6])
				
				oSection3:PrintLine()

				oSection3:Cell("RF_DATABAS"):Hide()
				oSection3:Cell("RF_DATAFIM"):Hide()
				oSection3:Cell("GESTAO"):Hide()
			Next
			oSection3:Cell("RF_DATABAS"):Show()
			oSection3:Cell("RF_DATAFIM"):Show()
			oSection3:Cell("GESTAO"):Show()

		Else
			oSection3:Cell("DIASDIR"):SetValue((cAliasSRF)->RF_DIASDIR)
			oSection3:Cell("FER_DTINI"):Hide()
			oSection3:Cell("FER_DTFIM"):Hide()

            If (cAliasSRF)->RF_STATUS=="3" .And. (cAliasSRF)->(RF_FERPAGA) > 0
				oSection3:Cell("D_PAGLIQ"):SetValue((cAliasSRF)->RF_FERPAGA)
				oSection3:Cell("DETALHE"):SetValue("Pagada por conv.Partes y/o en Liq. Benef. Sociales")
            ElseIf (cAliasSRF)->RF_STATUS=="2" // Prescrito
				oSection3:Cell("D_PAGLIQ"):SetValue(0)
				oSection3:Cell("DETALHE"):SetValue("Prescrito/Caducado")
            EndIf

			oSection3:PrintLine()

		EndIf

		(cAliasSRF)->( DbSkip() )
	  	If (nOrdem == 3 .And. (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC <> cChave ) .Or. ;
  		   (nOrdem == 4 .And. (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES <> cChave )
			Exit
		EndIf
		
	EndDo

	oSection3:Finish()
	oReport:SkipLine()
	
	oSection2:Finish()
	If nOrdem == 3 .or. nOrdem == 4
		oSection1:Finish()
	EndIf
EndDo

Return NIL

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥fCondSRA   ≥ Autor ≥ R.H. - Tatiane Matias   ≥ Data ≥ 04.07.06 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Verifica se a linha deve ser impressa                         ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ fCondSRA()                                                    ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥                                                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ Generico                                                      ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function fCondSRA(oSection)

Local lRetorno		:= .T.
Local cAcessaSRA	:= &("{ || " + ChkRH("GPER605","SRA","2") + "}")
Local dDataSRF
	
	If lRetorno
		/*
		⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		≥ Verifica Quebra de Filial                                    ≥
		¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
		If (cAliasMov)->RA_FILIAL # cFilaInfo
			If !fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
				//-- Registro nao deve ser impressao (Nao acessa Filial ou nao carregado os identificadores de calculo) 
				lRetorno := .F.                                    

				//-- Carrega os arrays com as informacoes da Filial anterior.
				fInfo(@aInfo,cFilaInfo)
			Else
				cFilaInfo := (cAliasMov)->RA_FILIAL
			EndIf
		EndIf

		/*
		⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		≥ Consiste controle de acessos e filiais validas			   |
		¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
		If !((cAliasMov)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
			//-- Registro nao deve ser impressao (Nao acessa tabela de Funcionarios ou Filial invalida) 
			lRetorno := .F.
		EndIf        
                                                    
		If lRetorno
			dDataSRF := ( cAliasSRF )->( RF_DATAFIM ) 
	  		If !(dDataSRF >= dDataIni .and. dDataSRF <= dDataFim)
				lRetorno := .F.
			EndIf   	
		EndIf
	Endif
		
Return lRetorno

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥fCabec     ≥ Autor ≥ R.H. - Tatiane Matias   ≥ Data ≥ 07.07.06 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Cabecalho do relatorio                                        ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ fCabec()                                                      ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥                                                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ Generico                                                      ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function fCabec(oReport)

	If !Empty((cAliasMov)->RA_FILIAL)
		//-- Carrega informacoes da Filial
		cFilaInfo := (cAliasMov)->RA_FILIAL
		fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
		
		oReport:PrintText(" "+STR0026+aInfo[3]+Space(20)+STR0021+cFilaInfo+" - "+aInfo[1])  //"Empresa: "###"Sucursal: "
		oReport:SkipLine()
	Else
		oReport:PrintText(" "+STR0026+Space(35)+STR0021)			// "Empresa: "###"Sucursal: "
		oReport:SkipLine()
	EndIf                
	
Return NIL


/*/
⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø
≥FunáÖo	   ≥RetornaMovSR8≥ Autor ≥ Mauricio Takakura  ≥ Data ≥07/10/2005≥
√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥
≥DescriáÖo ≥ Retorna o Movimento em SR8 do funcionario no periodo       ≥
√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥
≥Sintaxe   ≥RetornaMovSR8(cFilial, cMatricula)             			    ≥
√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥
≥Parametros≥                                                            ≥
√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥
≥ Uso	   ≥ Gper605  												    ≥
¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
Static Function RetornaMovSR8(	cFil	  	,;
		 				   		cMatricula 	,;
		 				   		dDtDe		,;
		 				   		dDtAte		,;
		 				   		cCodPD	 	;
						   )
Local aArea			:= GetArea()
Local aAreaSR8
Local aMovSR8		:= {}
Local cAliasSR8		:= "SR8"
Local cAliasQSR8	:= "SR8"
Local dDataIniPer
Local dDataFimPer
Local nDias
Local cQuery	:= ""
Local nCont
	
Begin Sequence

	(cAliasSR8)->(DbSetOrder(1)) // R8_FILIAL+R8_MAT+DTOS(R8_DATAINI)+R8_TIPOAFA+STR(R8_DIASEMP)
	
	cAliasQSR8:= GetNextAlias()

	cQuery := "SELECT * "
        	cQuery += "FROM "+RetSqlName("SR8")+ " SR8 "
	cQuery += "WHERE SR8.R8_FILIAL='"+cFil+"' AND "
	cQuery += "SR8.R8_MAT='"+cMatricula+"' AND "
	//cQuery += "SR8.R8_TIPOAFA IN "+cCodAfas+" AND "
	cQuery += "SR8.R8_PD IN ("+cCodPD+") AND "
	cQuery += "SR8.R8_DATAFIM >= '"+DtoS(dDtDe)+"'" + " AND SR8.R8_DATAINI <= '"+DtoS(dDtAte)+"' AND "
	cQuery += " SR8.D_E_L_E_T_ = ' ' "
   		cQuery += "ORDER BY "+SqlOrder(SR8->( IndexKey() ))
	
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliasQSR8, .F., .T.)
	For nCont := 1 To Len(aStruSR8)
		If ( aStruSR8[nCont][2] <> "C" )
			TcSetField(cAliasQSR8,aStruSR8[nCont][1],aStruSR8[nCont][2],aStruSR8[nCont][3],aStruSR8[nCont][4])
		EndIf
	Next nCont
	cAliasSR8:= cAliasQSR8

	While ( cAliasSR8 )->( !Eof() ) .and. ( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT == (cAliasSR8)->R8_FILIAL + (cAliasSR8)->R8_MAT )

		/*⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		  ≥ Consistencia do Codigo da Verba da Ausencia                  ≥
		  ¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
		If !((cAliasSR8)->R8_PD $ cCodPD )
			(cAliasSR8)->( DbSkip() )
			Loop
		EndIf
              
		/*⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		  ≥ Consistencia do Periodo Informado                            ≥
		  ¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
		If (cAliasSR8)->R8_DATAFIM >= dDtDe .And. (cAliasSR8)->R8_DATAINI <= dDtAte
		   
		   	fPosReg("RCM",1,xFilial("RCM")+(cAliasSR8)->R8_TIPOAFA)
		   	
   			/*
			⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
			≥ Dias do Periodo                          					   ≥
			¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
			dDataIniPer := If( dDtDe  < (cAliasSR8)->R8_DATAINI, (cAliasSR8)->R8_DATAINI, dDtDe )
			dDataFimPer := If( dDtAte > (cAliasSR8)->R8_DATAFIM, (cAliasSR8)->R8_DATAFIM, dDtAte )
			aAreaSR8 := (cAliasSR8)->( GetArea() )
			nDias 		:= fDiasAfasPeriodo( (cAliasMov)->RA_MAT,, RCM->RCM_TIPODI, dDataIniPer, dDataFimPer )
			RestArea( aAreaSR8 )
	   
			aAdd(aMovSR8, {	(cAliasSR8)->R8_TIPOAFA		, RCM->RCM_DESCRI			,;
							(cAliasSR8)->R8_PD			, PosSRV( (cAliasSR8)->R8_PD,(cAliasSR8)->R8_FILIAL, "RV_DESC") ,;
							(cAliasSR8)->R8_DATAINI		, (cAliasSR8)->R8_DATAFIM	,;
							(cAliasSR8)->R8_DURACAO		, (cAliasSR8)->R8_CONTINU	,;
							""	,;
							""	,;
							RCM->RCM_TIPODI				, nDias					     ;
				         })

		EndIf
		(cAliasSR8)->( DbSkip() )
	Enddo

	( cAliasQSR8 )->( dbCloseArea() )

End Sequence

//Ordenar por Filial, MAtricula e Verba
aSort( aMovSR8,,, { |x,y| x[1] + x[3] + DTOS(x[5]) < y[1] + y[3] + DTOS(y[5]) } )

RestArea( aArea )

Return ( aMovSR8 )

/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo	 ≥ DescCc 	≥ Autor ≥ Kleber Dias Gomes     ≥ Data ≥ 13/07/99 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Retorna a Descricao do Centro de Custo 					  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso		 ≥ Generico 												  ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ/*/
Static Function DescCc( cCodigo , cFil , nBytes , lEmpDif )
Local cRet		:= ""
Local cAlias 	:= Alias()

DEFAULT lEmpDif := .F.

cFil	:= xFilial( cAliasCc , cFil )
If nBytes = Nil
	nBytes := 20
Endif
If nBytes > 25
	nBytes := 25
Endif
If lEmpDif .And. !lSI3Igual
	dbSelectArea("GPE"+cAliasCc)
Else
	dbSelectArea(cAliasCc)
	__cFil := xFilial(cAliasCc)
Endif
If dbSeek( cFil + cCodigo )
	cRet := Left( IF( cAliasCc == "CTT" , CTT_DESC01 , I3_DESC ) + Space( nBytes ) , nBytes )
Else
	cRet := Left(STR0026+Space(nBytes),nBytes) //"** Nao Cadastrado **"
Endif
dbSelectArea( cAlias )

Return( cRet )
