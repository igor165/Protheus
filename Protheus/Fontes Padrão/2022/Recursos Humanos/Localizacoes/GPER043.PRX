#Include "protheus.CH"
#INCLUDE "REPORT.CH"
#INCLUDE "GPER043.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER043   ºAutor  ³Alex Sandro Fagundesº 		Data ³  03/08/2010     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Relatorio DGI - Imposto de Renda - Uruguai                           º±±
±±º          ³                                                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                                  º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄº±±
±±º			ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.		    	       º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ºProgramador ³ Data	³ BOPS ³  Motivo da Alteracao 				     	       º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ºAlex        |03/08/2010|00017636/2010|Criacao do relatorio de imposto de renda  º±±
±±º            |          |             |Localizacao Uruguai.                      º±±
±±³Christiane V|12/08/2010|00017741/2010|Verificação dos campos criados pelo updateº±±
±±³Alex        |13/09/2010|00017636/2010|Leitura do SRC sem o DbSeek correspondenteº±±  
±±³Christiane V|23/09/2010|00021890/2010|Correção da apresentação das strings      º±±  
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function GPER043()

	Private cFilAnterior 	:= Replicate("!", FWGETTAMFILIAL)
	Private cFilQuebra 		:= Replicate("!", FWGETTAMFILIAL)
	Private aPeriodo	 	:= {}
	
		
	If cPaisLoc == "URU"
		DbSelectArea("SRA")	
		IF !( SRA->( FieldPos( "RA_TIPTKAL" ) ) > 0 )  //Verificar se o campo existe, caso não exista não foi executado o update
			Aviso(STR0002, STR0003, { "OK" } )         //e os novos perguntes não foram criados.
		    Return
		Endif
	Endif

	oFont05	:= TFont():New("Courier New",05,05,,.F.,,,,.T.,.F.)
	oFont06	:= TFont():New("Courier New",06,06,,.F.,,,,.T.,.F.)
	oFont07	:= TFont():New("Courier New",07,07,,.F.,,,,.T.,.F.)
	oFont08	:= TFont():New("Courier New",08,08,,.F.,,,,.T.,.F.)
	oFont25n:= TFont():New("Courier New",25,25,,.T.,,,,.T.,.F.)     //Negrito// 
	oFont11n:= TFont():New("Courier New",11,11,,.T.,,,,.T.,.F.)     //Negrito//
	oFont17n:= TFont():New("Courier New",17,17,,.T.,,,,.T.,.F.)     //Negrito//
	oFont11	:= TFont():New("Courier New",11,11,,.F.,,,,.T.,.F.) 

	GPER043R3()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER043   ºAutor  ³Microsiga           º Data ³  04/08/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function GPER043R3()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Variaveis Locais (Basicas)                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local cDesc1 	:= STR0001		//"DGI - DIRECCION GENERAL IMPOSITIVA - Imposto de Renda."
	Local cDesc2 	:= STR0004		//" Será  impresso de acordo com os parametros solicitados pelo usuario."
	Local cDesc3 	:= " "		   
	Local cString	:= "SRA"       // alias do arquivo principal (Base)
	Local cMesAnoRef

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Variaveis Private(Basicas)                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cExercicio	:= mv_par01
	Private cFilDe    	:= mv_par02
	Private cFilAte   	:= mv_par03
	Private cCcDe     	:= mv_par04
	Private cCcAte    	:= mv_par05
	Private cMatDe    	:= mv_par06
	Private cMatAte   	:= mv_par07
	Private cNomDe    	:= mv_par08
	Private cNomAte   	:= mv_par09
	Private cResolucao	:= mv_par12
	Private aReturn 	:= { "", 1,"", 1, 2, 1,"",1 }	
	Private nomeprog	:= "GPER043"
	Private nTipo   	:= "P"
	Private aLinha  	:= {},nLastKey := 0
	Private cPerg   	:= "GPR043" 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis Utilizadas na funcao IMPR                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private Titulo		:= STR0001		
	Private AT_PRG  	:= "GPER043"
	Private nQtdeFolhas	:= 1
	Private Tamanho		:= "M"
	Private cCabec
	Private nOrdem
	Private aInfo   	:= {}
	Private lBse13
	Private oPrint
	Private aCodFol   	:= {}
	Private aCodLido  	:= {}
	Private aTotais		:=	{0,0,0,0,0}


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as perguntas selecionadas                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	pergunte("GPR043",.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01        //  Exercicio                                ³
	//³ mv_par02        //  Filial De                                ³
	//³ mv_par03        //  Filial Ate                               ³
	//³ mv_par04        //  Centro de Custo De                       ³		
	//³ mv_par05        //  Centro de Custo Ate                      ³
	//³ mv_par06        //  Matricula de                             ³
	//³ mv_par07        //  Matricula ate                            ³		
	//³ mv_par08        //  Nome de                                  ³
	//³ mv_par09        //  Nome ate                                 ³		
	//³ mv_par10        //  Situacao                                 ³		
	//³ mv_par11        //  Inf.a data limite de entrega             ³		
	//³ mv_par12        //  Inf.a resolucao atual                    ³		
	//³ mv_par13        //  Periodo de                               ³			
	//³ mv_par14        //  Periodo Ate                              ³				
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cFilDe    := mv_par02
	cFilAte   := mv_par03
	cCcDe     := mv_par04
	cCcAte    := mv_par05
	cMatDe    := mv_par06
	cMatAte   := mv_par07
	cNomDe    := mv_par08
	cNomAte   := mv_par09
	cPeriodoDe   := Substr(MesAno( mv_par13 ),5,2)+ "/"+ Substr(MesAno( mv_par13 ),1,4)
	cPeriodoAte  := Substr(MesAno( mv_par14 ),5,2)+ "/"+ Substr(MesAno( mv_par14 ),1,4)
	
	Private cDesde	:= "01/01/" + mv_par01
	Private cAte	:= "31/12/" + mv_par01
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel:="GPER043"            //Nome Default do relatorio em Disco
	wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,Nil,.T.,Tamanho)
	
	If nLastKey = 27
		Return
	Endif
	
	SetDefault(aReturn,cString)
	oPrint := TMSPrinter():New( "DGI - DIRECCION GENERAL IMPOSITIVA - Imposto de Renda" )
	oPrint:SetPortrait()
	
	If nLastKey = 27
		Return
	Endif

	nOrdem    	:= 1         // Ordem de Centro de Custo do Movimento
	nRelat	 	:= 2

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para geracao do SRZ via procedure     	 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lFolPgto    := .T.
	lFol13Sl    := lBse13
	RptStatus({|lEnd| R043ImpR3(@lEnd,wnRel,cString,cMesAnoRef)},STR0001)
	                           
	oPrint:Preview()  							// Visualiza impressao grafica antes de imprimir

Return 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R043ImpR3³ Autor ³ Alex Sandro Fagundes  ³ Data ³ 05.08.10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processamento Para emissao do Relatorio DGI - IR           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ R043ImpR3(lEnd,Wnrel,cString)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function R043ImpR3(lEnd,WnRel,cString,cMesAnoRef)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Variaveis Locais (Basicas)                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local cMesArqRef  
	Local nX		:=	0
	Local cPicVlr   := TM(99999999,12,MsDecimais(1))
	Local Tamanho   := "M"                                        
	Local aPolizas	:=	{}
	Local lRet		:=	.F.
	Local aFiliais	:=	{}
	Local aPeriodo	:=	{}
	Local aVerbas	:=	{}
	Local cbtxt 	:= SPACE(10)
	Local cbcont 	:= 0
	Local cabec1	:=	""
	Local cabec2	:=	""
	Local nV		:= 0
	Local nC		:= 0
	Local nF       	:= 0
	Local nP 		:= 0
	Local lRet13   	:= .F.
	Local cAliq		:= 0
	Local cPremio	:= 0
	Local cImpLey4	:= 0
	Local cAliqLey	:= 0
	Local cSubTotal	:= 0
	Local cIVA		:= 0
	Local cAliqIVA	:= 0
	Local cTotal	:= 0
	Local dDataIni	:= ""
	Local cChave	:= ""
	Local cAcessaSRC := &("{ || " + ChkRH("GPER043","SRC","2") + "}")
	Local cOutAportes := 0

	Private cAliasMov := ""
	Private aFuncsBSE	:=	{} 
	cMesArqRef := cMesAnoRef

	//Carrega informações das filiais
	DbSelectArea('SM0')
	aAreaSM0	:=	GetArea()
	DbSeek(cEmpAnt)
	While SM0->M0_CODIGO==cEmpAnt .And. !SM0->(EOF())
		Aadd(aFiliais,FWGETCODFILIAL)
		DbSkip()
	Enddo	
	RestArea(aAreaSM0)

	dbSelectArea("SRA")
	dbGoTop()  
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cria indice para Relatorio de IR.                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSetOrder(1)
	dbSeek(cFilDe + cMatDe,.T.)
	cInicio  := "SRA->RA_FILIAL + SRA->RA_MAT"
	cFim     := cFilAte + cMatAte

	cFilAnterior 	:= Replicate("!", FWGETTAMFILIAL)
	aTotais[1] := 0

	While !EOF() .And. &cInicio <= cFim
	
		If SRA->RA_FILIAL # cFilQuebra
			If !FP_CODFOL(@aCodFol)
				Exit
			Endif
			fInfo(@aInfo,SRA->RA_FILIAL)
		EndIf
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste Parametrizacao do Intervalo de Impressao            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (SRA->RA_NOME < cNomDe)   .Or. (SRA->RA_NOME > cNomAte)  .Or. ;
		   (SRA->RA_MAT < cMatDe)    .Or. (SRA->RA_MAT > cMatAte)   .Or. ;
		   (SRA->RA_CC < cCcDe)      .Or. (SRA->RA_CC > cCcAte)     .Or. ;
		   (SRA->RA_FILIAL < cFilDe) .Or. (SRA->RA_FILIAL > cFilAte)
			Loop
		EndIf
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste controle de acessos e filiais validas               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !(SRA->RA_FILIAL $ fValidFil())
			dbSelectArea( "SRA" )
			dbSkip()
			Loop
		EndIf                          

		//Carregamento do período inicial
		If SubStr(MesAno(SRA->RA_ADMISSA),1,4) > mv_par01 .AND. Alltrim(SRA->RA_ADMISSA) != ""
			cDesde		:= SRA->RA_ADMISSA
		EndIf

		dbSelectArea("SRC")
		dbSetOrder(1)
		cChave	:= SRA->RA_FILIAL + SRA->RA_MAT + aCodFol[15,1]
		dbSeek(cChave,.T.)
		
		aTotais[1]	:=	0
		aTotais[2]	:=	0
		aTotais[3]	:=	0
		aTotais[4]	:=	0
		
		While !Eof() .And. (SRC->RC_FILIAL+SRC->RC_MAT) == (SRA->RA_FILIAL+SRA->RA_MAT)
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Consiste controle de acessos								 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Eval(cAcessaSRC)
				dbSkip()
				Loop
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Base de IR        											 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SRC->RC_PD = aCodFol[15,1]
				aTotais[1]	+=	SRC->RC_VALOR
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Base de Montepio											 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SRC->RC_PD = aCodFol[64,1]
				aTotais[2]	+=	SRC->RC_VALOR
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Otros aportes "Considera o Total de Descontos deduzindo o BPS³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If PosSrv( SRC->RC_PD , SRA->RA_FILIAL , "RV_IR" ) == "S" .AND. SRC->RC_PD = aCodFol[15,1] .AND. PosSrv( SRC->RC_PD , SRA->RA_FILIAL , "RV_TIPOCOD" ) == "2"
				aTotais[3]	+=	SRC->RC_VALOR
			EndIf
			
			dbSkip()
		EndDo
        
		/*
		cOutAportes := aTotais[1] - aTotais[3]
		aTotais[3]	:= cOutAportes
		*/
                        
		If aTotais[1] != 0
			RelatorioPeriodo()
		EndIf
			
		dbSelectArea("SRA")
		dbSkip()
	EndDo	

	dbSelectArea("SRA")
	Set Filter To
	RetIndex( "SRA" )
	dbSetOrder(1)

Return         

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ RelatorioPeriodo³ Autor ³ Alex Fagundes  ³ Data ³ 05.08.10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processamento Para emissao do Relatorio DGI - IR           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ RelatorioPeriodo(cPeriodo)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function RelatorioPeriodo()
	Local cEmpresa		:= SM0->M0_NOME
	Local cRUC			:= SM0->M0_CGC
	Local cFuncionario	:= SRA->RA_NOME
	Local cCI			:= SRA->RA_RG
	Local cLinhaPeriodos := ""

	oPrint:StartPage() 							//Inicia uma nova pagina   
		oPrint:Box ( 0020, 0035, 1630, 2280 ) 	//BOX PRINCIPAL

		oPrint:say ( 0040, 0100, "Datos a suministrar al contribuyente - Resolucion Nº 662/007 Num. 37 bis) y ", oFont17n )
		oPrint:say ( 0140, 0870, "Resolución Nº 353/"+StrZero(mv_par12,3), oFont17n )

		oPrint:say ( 0250, 0060, "Sujeto pasivo responsable", oFont11 )
		oPrint:say ( 0250, 0600, cEmpresa, oFont11n )

		oPrint:say ( 0250, 1300, "RUC: ", oFont11 )
		oPrint:say ( 0250, 1400, cRUC, oFont11n )
		
		oPrint:say ( 0350, 1300, "Periodo  Desde", oFont11 )
		oPrint:say ( 0350, 1600, cDesde, oFont11n )
					
		oPrint:say ( 0450, 1300, "         Hasta", oFont11 )
		oPrint:say ( 0450, 1600, cAte, oFont11n )

		oPrint:say ( 0550, 0060, "Nombre del contribuyente", oFont11 )
		oPrint:say ( 0550, 0545, cFuncionario, oFont11n )
					
		oPrint:say ( 0550, 1400, "CI", oFont11 )
		oPrint:say ( 0550, 1480, cCI, oFont11n )

		oPrint:Box ( 0650, 0060, 750, 1450 ) 	//BOX Datos a suministrar
		oPrint:Box ( 0650, 1450, 750, 2180 ) 	//BOX Importe$			

		oPrint:say ( 0680, 0420, "Datos a suministrar", oFont17n )
		oPrint:say ( 0680, 1750, "Importe$", oFont17n )

		oPrint:Box ( 0750, 0060, 850, 1450 ) 	//BOX Ingresos
		oPrint:Box ( 0750, 1450, 850, 2180 ) 	//BOX Campo 07			
		oPrint:say ( 0780, 0100, "Ingresos totales gravados por IRPF", oFont11 )
		oPrint:say ( 0780, 1750, Transform(aTotais[1], "99,999,999.99"), oFont17n )		
		
		oPrint:Box ( 0850, 0060, 1050, 1450 ) 	//BOX Deducciones prop
		oPrint:say ( 0870, 0100, "Deducciones ", oFont11 )
		oPrint:say ( 0890, 0100, "proporcionales:", oFont11 )
		oPrint:say ( 0880, 0600, "Aportes jubilatorios", oFont11 )			
		oPrint:say ( 0880, 1750, Transform(aTotais[2], "99,999,999.99"), oFont17n )

		oPrint:say ( 0950, 0600, "Otros aportes (ex Disse, FRL,", oFont11 )
		oPrint:say ( 0950, 1750, Transform(aTotais[3], "99,999,999.99"), oFont17n )
		
		oPrint:say ( 0970, 0600, "Cajas de Auxilio)", oFont11 )       

		oPrint:Box ( 1050, 0060, 1150, 1450 ) 	//BOX Retenciones efectuadas
		oPrint:say ( 1080, 0100, "Retenciones efectuadas", oFont11 )
		oPrint:say ( 1080, 1750, Transform(aTotais[4], "99,999,999.99"), oFont17n )		

		oPrint:Box ( 0850, 1450, 950, 2180 ) 	//BOX Campo 08			
		oPrint:Box ( 0950, 1450, 1050, 2180 ) 	//BOX Campo 09
		oPrint:Box ( 1050, 1450, 1150, 2180 ) 	//BOX Campo 10

		oPrint:say ( 1080, 0100, "Retenciones efectuadas", oFont11 )

		oPrint:say ( 1300, 0060, "Nota: Este comprobante no tendrá carácter de resguardo y deberá estar a disposicion de los contribuyentes antes del 30 de abril de 2008 por la", oFont08 )
		
		cLinhaPeriodos := "informacion correspondiente al período " + cPeriodoDe + " A " + cPeriodoAte
		oPrint:say ( 1350, 0060, cLinhaPeriodos, oFont08 )
	oPrint:EndPage()
Return