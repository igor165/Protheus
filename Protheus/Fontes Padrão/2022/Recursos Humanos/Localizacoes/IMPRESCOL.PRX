#INCLUDE "Protheus.CH"
#INCLUDE "IMPRESCOL.CH"
#DEFINE   nColMax	2300
#DEFINE   nColIni	100
#DEFINE   nLinMax  2870

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³IMPRESCOL ºAutor  ³Tiago Malta                  º Data ³  01/08/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Impressao da Rescisao em modo Grafico.                              º±±
±±º          ³                                                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Colombia                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±  
±±³                ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ FNC / Chamado  ³  Motivo da Alteracao                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Ademar Jr.  ³04/01/2011³Chamado TDLDOT  ³-Ajuste de picture na implementacao que³±±
±±³            ³          ³                ³ o Tiago fez.                          ³±±  
±±³Raquel Hager³06/06/2012³00000005813/2012³-Remocao de campos Cedula e Classe de  ³±±
±±³            ³          ³          TEQJRN³ Salario.                              ³±±
±±³Ademar Jr.  ³01/11/2012³00000006603/2012³-Ajuste das pictures para impressao dos³±±
±±³            ³          ³Chamado: TERISZ ³ valores da rescisao.                  ³±±
±±³Cecilia     ³06/02/2013³00000002737/2013³-implementação da impressão do campo   ³±±
±±³            ³          ³Chamado: TGPEBF ³ RA_CIC.                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function IMPRESCOL()

Local nCont		:= 2
Local Imprime	:= 1   				//LINHA DE IMPRESSAO DO RELATORIO GRAFICO 
Local nImpr2    := 1
Local n			:= 0 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Define Variaveis PRIVATE utilizadas para Impressao Grafica³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private nPos		:= 0				//LINHA DE IMPRESSAO DO RELATORIO GRAFICO
Private nTot		:= 0
Private nNumMax		:= 18				//Numero maximo de verbas impressas no Detalhe da rescisao 
Private nImprime	:= 1 				//Variavel Auxiliar 
Private nImpre		:= 1 
Private CONTFL		:= 1				//CONTA PAGINA

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Objetos para Impressao Grafica - Declaracao das Fontes Utilizadas.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private oFont11n,oFont14n, oFont09, oFont09n,oFont07 

oFont07	:= TFont():New("Courier New",07,07,,.F.,,,,.T.,.F.)
oFont09	:= TFont():New("Courier New",09,09,,.F.,,,,.T.,.F.)
oFont09n:= TFont():New("Courier New",09,09,,.T.,,,,.T.,.F.)     //Negrito//
oFont11n:= TFont():New("Courier New",11,11,,.T.,,,,.T.,.F.)     //Negrito//
oFont14n:= TFont():New("Courier New",14,14,,.T.,,,,.T.,.F.)     //Negrito//

nEpoca:= SET(5,1910)
//-- MUDAR ANO PARA 4 DIGITOS 
SET CENTURY ON 

nTot 	:= Max( Len(aPdv) , Len(aPdd) )
nTot	:= Max( nTot , Len(aPdpm) )

fCabecalho()

While Imprime <=  nTot
	//-- Impressao do rodape e quebra de pagina 
	If nImpre > ( nNumMax ) .or. nImprime > nNumMax .or. nImpr2 > nNumMax
		fRodape() 
		oPrint:EndPage() 
		oPrint:StartPage()  
		ContFl++  
		fCabecalho() 
		nCont += 2
		nNumMax := ( nNumMax * nCont ) 
		nImpre	:= ( nImpre + n)
	Endif 

	If nImpre <= nNumMax .or. Imprime <= nNumMax .or. nImpr2 <= nNumMax
		
		//--Coluna de Proventos  
		If nImpre <= Len(aPdv) 
			//--Primeira coluna de Provento 
			oPrint:say (nlin,nColIni+20 ,Space(1) + Subs(aPdv[nImpre],01,20),oFont07)
			oPrint:say (nlin,450 		,Space(1) + Subs(aPdv[nImpre],21,06),oFont07) 
			oPrint:say (nlin,550 		,Space(1) + Subs(aPdv[nImpre],27,16),oFont07) 
			nImpre	+= 1
		Endif
		//-- Coluna de Descontos
		If nImprime <= Len(aPdd)  
			oPrint:say (nlin,0820,Space(1) +       Subs(aPdd[nImprime],01,20)    , oFont07)
			oPrint:say (nlin,1140,Space(1) + Right(Subs(aPdd[nImprime],21,06),6) , oFont07)
			oPrint:say (nlin,1300,Space(1) + Right(Subs(aPdd[nImprime],27,16),14), oFont07)
			nImprime	++
		Endif
		//-- Coluna de Base 
		If nImpr2 <= Len(aPdpm)  
			oPrint:say (nlin,1570,Space(1) +       Subs(aPdpm[nImpr2],01,20)    , oFont07)
			oPrint:say (nlin,1870,Space(1) + Right(Subs(aPdpm[nImpr2],21,06),6) , oFont07)
			oPrint:say (nlin,2020,Space(1) + Right(Subs(aPdpm[nImpr2],27,16),14), oFont07)
			nImpr2	++
		Endif
					
	Endif  
	
	Imprime 	+= 1
	nlin +=40 
	
Enddo
fRodape()

Set(5,nEpoca)
If nTdata > 8
	SET CENTURY ON
Else
	SET CENTURY OFF
Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fCabecalho³ Autor ³ Recursos Humanos      ³ Data ³ 04.11.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao do CABECALHO Modo Grafico                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ RdMake                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fCabecalho()

oPrint:StartPage() 			//Inicia uma nova pagina  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³TERMO DE RESCISAO DO CONTRATO DE TRABALHO                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nlin := 40
oPrint:say (nlin,620,STR0001,oFont14n)			 	//"TERMO DE RESCISAO DO CONTRATO DE TRABALHO"

////////////////////////////////////////////
nlin += 110
oPrint:say (nlin , nColIni , "1. "+STR0002, oFont11n)

nlin += 60
oPrint:Box( nlin , nColIni , nlin+120 ,nColMax ) 					 	  			 	//-- Box Identificacao da Empresa
oPrint:line(nlin, 1500 , nlin+120 ,1500    )													//Linha Vertical Meio

nlin += 20
oPrint:say (nlin , nColIni+20 , STR0003 , oFont09) 
oPrint:say (nlin , 1520 , "NIT.", oFont09) 

nlin += 50
oPrint:say (nlin , nColIni+35 , aInfo[3], oFont09) 
oPrint:say (nlin , 1535 , aInfo[8], oFont09) 

////////////////////////////////////////////
nlin += 90
oPrint:say (nlin , nColIni , "2. "+STR0004, oFont11n)

nlin += 60
oPrint:Box( nlin , nColIni , nlin+450 ,nColMax )					 	  			 	//-- Box Identificacao do Trabalhador

oPrint:line(nlin,800 ,nlin+450,800)													//Linha Vertical Meio
oPrint:line(nlin,1600,nlin+450,1600)													//Linha Vertical Meio

nlin +=20
oPrint:say (nlin,nColIni+20,STR0005, oFont09) 		 							
oPrint:say (nlin,0820,STR0006, oFont09)								   
oPrint:say (nlin,1620,STR0007, oFont09) 						  	  		

nlin += 50                                 
oPrint:say (nlin,nColIni+35 ,SRA->RA_MAT , oFont10 ) 			
oPrint:say (nlin,835 ,SRA->RA_NOME, oFont10 )						   		
oPrint:say (nlin,1635,SRA->RA_FILIAL+" - "+aInfo[1], oFont10 )

nlin += 40
oPrint:line(nlin,nColIni ,nlin,nColMax) 					 	  						//Linha Horizontal

nlin += 20
oPrint:say (nlin,nColIni+20 ,STR0009, oFont09)  								
oPrint:say (nlin,0820,STR0010, oFont09)								   	
oPrint:say (nlin,1620,STR0011, oFont09) 						  	  		

nlin += 50
oPrint:say (nlin,nColIni+35 ,SRA->RA_CARGO+" - "+Posicione("SQ3",1,xFilial("SQ3")+SRA->RA_CARGO,"Q3_DESCSUM"), oFont09)			 	  						
oPrint:say (nlin,835 ,SRA->RA_CC+" - "+Posicione("CTT",1,xFilial("CTT")+SRA->RA_CC,"CTT_DESC01"), oFont09)				   	   				
oPrint:say (nlin,1635,DTOC(SRA->RA_ADMISSA), oFont09)	   								  

nlin += 40
oPrint:line(nlin,nColIni ,nlin,nColMax) 					 	  			 			//Linha Horizontal

nlin += 20
oPrint:say (nlin,nColIni+20 ,STR0012, oFont09)  									
oPrint:say (nlin,0820,STR0013, oFont09)								   	
oPrint:say (nlin,1620,STR0014, oFont09) 						  	  			

nlin += 50
oPrint:say (nlin,nColIni+35 ,DTOC(SRA->RA_DEMISSA), oFont09)				 	  					
oPrint:say (nlin,835 ,Transform(SRA->RA_SALARIO        ,"@E 999,999,999.99"), oFont09)	 				   	   				
oPrint:say (nlin,1635,SRA->RA_TIPOCO +" - "+Alltrim(fDescRCC("S022",SRA->RA_TIPOCO,1,2,3,50)) , oFont09)	   								   

nlin += 40
oPrint:line(nlin,nColIni ,nlin,nColMax) 					 	  			 			//Linha Horizontal

nlin += 20
oPrint:say (nlin,nColIni+20 ,STR0016, oFont09)  	 
oPrint:say (nlin,0820,STR0041, oFont09)								   	
oPrint:say (nlin,1620 ,STR0028, oFont09)												  	  		

nlin += 50
oPrint:say (nlin,nColIni+35,SRG->RG_TIPORES+" - "+Alltrim(fDescRCC("S024",SRG->RG_TIPORES,1,2,3,100)), oFont09)    
oPrint:say (nlin,835 , SRA->RA_CIC , oFont09)
oPrint:say (nlin,1635 , Alltrim(Str(DateDiffDay( SRA->RA_ADMISSA , SRA->RA_DEMISSA ))) , oFont09)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Discriminacao das Verbas Rescisorias                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nlin += 90
oPrint:say (nlin , nColIni , "3. "+STR0017, oFont11n)

nlin += 60
oPrint:Box( nlin, nColIni, nlin+970, nColMax )
oPrint:line(nlin,0800,nlin+970,0800) 							//-- Linha Vertical Meio 
oPrint:line(nlin,1550,nlin+970,1550)							//-- Linha Vertical Meio 

nlin += 15
oPrint:say (nlin,nColIni+20 ,oemtoansi(STR0018), oFont09n)		//-- Proventos
oPrint:say (nlin,820 ,oemtoansi(STR0019), oFont09n)		//-- Descontos
oPrint:say (nlin,1570,oemtoansi(STR0020), oFont09n)		//-- Bases

nlin += 40
oPrint:line(nlin,nColIni,nlin,nColMax) 						//-- Linha Horizontal Proventos/Descontos

nlin += 30

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³fRodape   ºAutor  ³Microsiga           º Data ³  04/11/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³fRodape - Impressao do Rodape da Rescisao (Grafico).        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fRodape()

Local aLey := {}
Local i
Local cTexto    := ""  
Local cValext   := ""
Local nLinTotal := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Totais das Verbas Rescisorias                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

nlin := 2030           


IF Len(aPdd) > nNumMax .or. Len(aPdv) > nNumMax .or. Len(aPdpm) > nNumMax

	nlin += 35 
	oPrint:say(nlin,1900, oemtoAnsi(STR0021), oFont09n ) 	//--  " Continua ... "
	
ELSE

	nlin += 35
	oPrint:say (nlin,nColIni , STR0022, oFont09n)											//"|30-TOTAL BRUTO"
    oPrint:say (nlin,0450 , Transform(nProv        ,"@E 999,999,999.99"), oFont09n)
	oPrint:say (nlin,0850 , STR0023, oFont09n)											//"|TOTAL DESCONTOS "
	oPrint:say (nlin,1240 , Transform(nDesc        ,"@E 999,999,999.99"), oFont09n)	
	
	nlin += 35
	oPrint:Line(nlin,nColIni , nlin , nColMax)						//-- Linha Horizontal dos Totais das Verbas
	nlin += 25
	oPrint:say (nlin,nColIni , STR0024, oFont09n)											//"|LIQUIDO A RECEBER "
	oPrint:say (nlin,0450 , Transform(nProv - nDesc,"@E 999,999,999.99"), oFont09n)
	
	                     
	cValext := Alltrim(Extenso(nProv - nDesc))
	nLinTotal := MlCount(cValext,100)
	
	For i:=1 To nLinTotal     
	
		cTexto:= Memoline(cValext,100,i)
		oPrint:say (nlin,0800 ,cTexto, oFont07)
		
		IF nLinTotal > 1 .AND. i <> nLinTotal
			nlin += 25
		ENDIF
		
	Next i
		
Endif
                               
nlin += 50
oPrint:Box( nlin, nColIni, nlin+430, nColMax )
  
aLey := {STR0029,STR0030,STR0031,STR0032,STR0033,STR0034,STR0035,STR0042,STR0036,STR0037,STR0038,STR0039,STR0040}

nlin += 20

For i:=1 to len(aLey)
	oPrint:say( nlin,nColIni+20 ,aLey[i], oFont07)
	nlin += 30
Next i

nlin += 350
oPrint:say( nlin,nColIni+20 ,"_____________________________", oFont09n)	
oPrint:say( nlin,850  ,"_____________________________", oFont09n)	
oPrint:say( nlin,1510 ,"_____________________________", oFont09n)	
nlin += 30
oPrint:say( nlin,330 ,STR0025, oFont09n)	
oPrint:say( nlin,1030 ,STR0026, oFont09n)	
oPrint:say( nlin,1650 ,STR0027, oFont09n)	 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³FIM DO RELATORIO                                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oPrint:EndPage()
CONTFL:=1

Return 