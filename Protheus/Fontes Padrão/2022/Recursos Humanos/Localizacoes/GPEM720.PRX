#INCLUDE "Protheus.CH"
#INCLUDE "GPEM720.CH"

Static lIncluir:= .F.
               
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEM720   ºAutor  ³Silvia Taguti       	 º Data ³  12/08/06    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Manutencao do Arquivo da Declaracao Anual                  	   º±±
±±º          ³ Mexico                                                     	   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             	   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ FNC/BOPS  ³ Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ºSilvia T    ³08/02/08³140541		³Acerto na gravacao quando os valores sao  º±±
±±º            ³        ³      		³modificados                               º±±
±±³Rogerio R.  ³08/10/08³      		³Inclusao da funcao setBrwCHGAll( .F. )    ³±±
±±³            ³        ³      		³antes da mBrowse,para nao apresentar a    ³±±
±±³            ³        ³      		³tela de inclusao de filial ao incluir no- ³±±
±±³            ³        ³      		³vo Registro.                              ³±±
±±ºIgor Franzoi³22/09/09³21020/2009	³Ajuste da consulta Padrao no RGC_KEYLOC   º±±
±±º			   ³22/09/09³21020/2009	³RGC_KEYLOC - Descricao deve ser Estado	   º±±
±±ºTiago Malta ³29/12/09³30785/2009	³Retirado a funcao AjustSx3.               º±±
±±º------------³--------³-----------³ versao 11.5------------------------------º±±
±±ºEmerson     ³26/06/11³015139/2011³Atender a Nova Interface Totvs 11.5 utili-º±±
±±ºCampos      ³        ³           ³zando a funcao MsAdvSize( p/ flexibilidadeº±±
±±º            ³        ³           ³da tela de acordo com a resolucao).       º±±
±±ºGustavo M.  ³22/08/12³TFOVBR 	|Ajuste para permitir que seja alterado    º±±
±±º			   ³	    ³	     	|um registro da filial nao corrente.       º±±
±±ºLaura Medina³21/05/13³THGXPI  	³Ajuste para que NO duplique registros en  º±±
±±º            ³        ³      		³RCV (Encabezado de acumulados) y actualiceº±±  
±±º            ³        ³      		³los datos y campo RCV_STATUS='U-Usuario'. º±±
±±ºLaura Medina³13/06/13³THKT38  	³Se agrego el alias para el bloqueo de la  º±±
±±º            ³        ³      		³tabla y se quito del bloqueo un parametro.º±±  
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function GPEM720()

LOCAL cFiltraRCV					//Variavel para filtro
LOCAL aIndexRCV	:= {}				//Variavel Para Filtro

Private bFiltraBrw := {|| Nil}		//Variavel para Filtro

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private nVez 		:= 1
Private cArqCompl	:=	"GPCMPL"+cEmpAnt
Private aColsComp 	:= {}
Private aColsRec

cCadastro := OeMtoAnsi(STR0001) //"Mantenimiento de la Declaracion Anual"

If  RCV->(FieldPos("RCV_STATUS")) == 0
	cHelp := OemToAnsi( STR0027 )//"El campo RCV_STATUS no existe, ejecute la rutina RHUPDMOD."
	Help( ' ' , 1 , "GP710NOCPO" , , cHelp , 1 , 0 )
	Return Nil
Endif 

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Funcao verifica se existe alguma restrição de acesso para o³
³usuário que impeça a execução da rotina.                   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If !(fValidFun({"RCV","RCW","RCC"}))
	Return( nil ) 
Endif


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aRotina :=	{	{ STR0009 ,"AxPesqui"  , 0 , 1},; //"Pesquisar"
						{ STR0010 ,"gp720Man"  , 0 , 2},; //"Visualizar"
						{ STR0011 ,"gp720Aux"  , 0 , 4},; //"Incluir" 
						{ STR0012 ,"gp720Man"  , 0 , 6},; //"Alterar"
						{ STR0013 ,"gp720Man"  , 0 , 5} } //"Excluir"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a existencia do arquivo de informacoes complementares ³
//³e cria um novo caso ele nao exista                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ChkInfComp()           

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea( "RCV" )
dbSetOrder( 1 )
dbGoTop()
/* 
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carregando Filtro de BROWSE                                  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/ 
cFiltraRCV	:= ChkRh( "GPEM720" , "RCV" , "1" ) 
bFiltraBrw	:= { || FilBrowse( "RCV" , @aIndexRCV , @cFiltraRCV ) }
Eval( bFiltraBrw ) 
SetBrwCHGAll( .T. )
mBrowse( 6, 1,22,75,"RCV")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Deleta o filtro utilizando a funcao FilBrowse                     	   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
EndFilBrw("RCV",aIndexRCV)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp720Man  ³ Autor ³ J. Ricardo            ³ Data ³ 20.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Visualizacao,Inclusao,Altereacao do Arquivo da ³±±
±±³          ³ Declaracao anual                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ gp720Vis(ExpC1,ExpN1,ExpN2)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ gpeM720                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp720MAN(cAlias,nReg,nOpc)

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aButton		:= {}
Local bSet15		:= { || NIL }
Local bSet24		:= { || NIL }
Local nOpca			:= 0.00
Local oDlg
Local oGet			
Local aArea			:= GetArea()
Local nDel			:= 0.00
Local cX3Campo
Local cMCampo
Local aColsAnt		:= {}
Local nx			:= 0

Private nUsado	:= 0
Private aCols 	:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Matriz de Cabecalho para Enchoice³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aM720Obrig 	:= {}
Private aM720Nao   	:= {"RCV_FILIAL"}
Private aM720Dif   	:= {"RCV_MAT" }
Private aLetra := {}

If lIncluir
	nOpcx := 3
	lIncluir :=.F.
Endif 

cAlias 		:= "RCV"
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do Arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If nOpc == 3
	dbSelectArea("SX3")
	SX3->(dbSeek( "RCV"))
	While SX3->(!Eof()) .and. (SX3->X3_ARQUIVO == "RCV")
		cX3Campo := "RCV->"+(SX3->X3_CAMPO)
		cMCampo  := "M->"+(SX3->X3_CAMPO)
		If Ascan(aM720Nao,ALLTRIM(Upper(SX3->X3_CAMPO))) = 0
			If SX3->X3_CAMPO = "RCV_RFCFON"
				&(cMCampo) := SM0->M0_CGC
			Elseif SX3->X3_CAMPO = "RCV_NOMFON"
				&(cMCampo) := SM0->M0_NOMECOM
			Elseif SX3->X3_CAMPO = "RCV_PROAPL"
				&(cMCampo) := 0            
			Else
				If ValType(cX3Campo) = "C"
					&(cMCampo) := SPACE(LEN(&(cX3Campo)))
				ElseIf ValType(cX3Campo) = "N"
					&(cMCampo) := 0
				ElseIf ValType(cX3Campo) = "D"
					&(cMCampo) := CtoD("  /  /  ")
				ElseIf ValType(cX3Campo) = "L"
					&(cMCampo) := .F.
				Endif
			Endif                        
			AADD(aM720Obrig,SX3->X3_CAMPO)
		Endif
		SX3->( dbSkip())
	Enddo
ElseIf nOpc == 4
	dbSelectArea("SX3")
	SX3->( dbSeek( "RCV"))
	While SX3->(!Eof()) .and. (SX3->X3_ARQUIVO == "RCV")
		If Ascan(aM720Nao,ALLTRIM(Upper(SX3->X3_CAMPO))) == 0
			cX3Campo := "RCV->"+(SX3->X3_CAMPO)
			cMCampo  := "M->"+(SX3->X3_CAMPO)
			&cMCampo := &(cX3Campo) 
			If &cMCampo == Nil
				If ValType(cX3Campo) == "C"
					&(cMCampo) := SPACE(LEN(RCV->&(SX3->X3_CAMPO)))
				ElseIf ValType(cX3Campo) == "N"
					&(cMCampo) := 0
				ElseIf ValType(cX3Campo) == "D"
					&(cMCampo) := CtoD("  /  /  ")
				ElseIf ValType(cX3Campo) == "L"
					&(cMCampo) := .F.
				Endif
			Endif
			If Ascan(aM720Dif,ALLTRIM(Upper(SX3->X3_CAMPO))) = 0
				AADD(aM720Obrig,x3_campo)
			Endif
		Endif
		SX3->( dbSkip())
	Enddo
EndIf

Private aTELA[0][0],aGETS[0],aHeader[0],Continua:=.F.

nUsado := 0

Gera720(cAlias,@aHeader,@aCols,@nUsado,nOpc)

aAlter := Array(Len(aHeader))
For nx:= 2 to Len(aHeader)
	aAlter[nx] := aHeader[nx,2] 
Next

aColsAnt := Aclone(aCols)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdvSize		:= MsAdvSize()
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
aAdd( aObjCoords , { 000 , 080 , .T. , .F. } )
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

DEFINE MSDIALOG oDlg TITLE cCadastro From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL 
	nOpca := EnChoice( cAlias , nReg , nOpc , NIL , NIL , NIL , NIL , aObjSize[1] ,If(nOpc==2.or. nopc==6,NIL,aM720Obrig),3 )
	dbSelectArea("RCW")
  	oGet	:= MsGetDados():New(aObjSize[2,1],aObjSize[2,2],aObjSize[2,3],aObjSize[2,4],nOpc,"gp720LinOk","gp720LinOk"," ",,,,,12)
	bSet15	:= {||nOpca:=1,If(If(nOpc==3,Obrigatorio(aGets,aTela),.T.).and.oGet:TudoOk(),oDlg:End(),nOpca:=0)}
	bSet24	:= {||oDlg:End()}

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 , NIL  )  
	
If nOpcA == 1 .and. nOpc # 2
	Begin Transaction
		If nOpc == 5 //Exclusao
			nDel := 0
			FDel720(@nDel)
	    Else
			gp720Grava("RCW",aColsAnt)				
			EvalTrigger()
		EndIf	
	End Transaction
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da janela                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aColsComp 	:= {}
nOpca 		:= 0
dbSelectArea(cAlias)
RestArea( aArea )
Return nOpca          

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp720Grava³ Autor ³ J. Ricardo            ³ Data ³ 20.02.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava no arquivo de Acumulados Anuais                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ gp720Grava                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function gp720Grava(cAlias,aColsAnt)
Local nX 		:= 0
Local nY 		:= 0
Local aAreaRCV 	:= RCV->( GetArea() )

Begin Transaction
	dbSelectArea("RCV")
	dbSetOrder(1)            
	//If dbSeek(xFilial("RCV")+M->RCV_MAT+M->RCV_RFC+M->RCV_ANO+M->RCV_MESINI+M->RCV_MESFIN)
	If  RCV->(dbSeek(xFilial("RCV")+M->RCV_MAT+M->RCV_RFC+M->RCV_ANO)) //THGXPI
		RecLock("RCV",.F.)
	Else
		RecLock("RCV",.T.)
	Endif 
	
	RCV->RCV_FILIAL   := xFilial("RCV")
	RCV->RCV_MAT      := M->RCV_MAT
	RCV->RCV_RFCFONT  := M->RCV_RFCFONT        
	RCV->RCV_RFC      := M->RCV_RFC
	RCV->RCV_CURP     := M->RCV_CURP
	RCV->RCV_NOMFONT  := M->RCV_NOMFONT
	RCV->RCV_CVEZON   := M->RCV_CVEZON
	RCV->RCV_EMCALA   := M->RCV_EMCALA
	RCV->RCV_SINDIC   := M->RCV_SINDIC
	RCV->RCV_KEYLOC   := M->RCV_KEYLOC
	RCV->RCV_CLASIM   := M->RCV_CLASIM
	RCV->RCV_ANO      := M->RCV_ANO
	RCV->RCV_MESINI   := M->RCV_MESINI
	RCV->RCV_MESFIN   := M->RCV_MESFIN
	RCV->RCV_PRINOM  	:= M->RCV_PRINOM
	RCV->RCV_SEGNOM  	:= M->RCV_SEGNOM
	RCV->RCV_PRISOB  	:= M->RCV_PRISOB
	RCV->RCV_SEGSOB  	:= M->RCV_SEGSOB
	RCV->RCV_NOMREP   := M->RCV_NOMREP
	RCV->RCV_RFCREP   := M->RCV_RFCREP
	RCV->RCV_CURPRE   := M->RCV_CURPRE
	RCV->RCV_NUMDIA   := M->RCV_NUMDIA
	RCV->RCV_NUMANO   := M->RCV_NUMANO
   RCV->RCV_PROAPL	:= M->RCV_PROAPL
   RCV->RCV_RFCOT1   := M->RCV_RFCOT1
   RCV->RCV_RFCOT2   := M->RCV_RFCOT2
   RCV->RCV_RFCOT3   := M->RCV_RFCOT3
   RCV->RCV_RFCOT4   := M->RCV_RFCOT4
   RCV->RCV_RFCOT5   := M->RCV_RFCOT5
   RCV->RCV_RFCOT6   := M->RCV_RFCOT6
   RCV->RCV_RFCOT7   := M->RCV_RFCOT7
   RCV->RCV_RFCOT8   := M->RCV_RFCOT8
   RCV->RCV_RFCOT9   := M->RCV_RFCOT9
   RCV->RCV_RFCOTA   := M->RCV_RFCOTA  
   RCV->RCV_STATUS   := "U"
   RCV->(MsUnlock())

	For nX := 1 TO 12
        For nY := 2 to nUsado
  			If aCols[nX,nY] # aColsAnt[nX,nY] //.And. aCols[nX,nY] >= 0.00
				cTipDirf := aLetra[nY-1]
				dbSelectArea("RCW")
				If dbSeek(RCV->RCV_FILIAL+RCV->RCV_MAT+RCV->RCV_RFC+RCV->RCV_ANO+RCV->RCV_MESINI+RCV->RCV_MESFIN+STRZERO(nX,2)+ALLTRIM(cTipDirf))
					RecLock("RCW",.F.)
				Else
					RecLock("RCW",.T.)
				Endif
				RCW->RCW_FILIAL  := RCV->RCV_FILIAL
				RCW->RCW_MAT     := RCV->RCV_MAT
				RCW->RCW_RFC  	  := RCV->RCV_RFC
				RCW->RCW_ANO     := RCV->RCV_ANO
				RCW->RCW_MESINI  := RCV->RCV_MESINI
				RCW->RCW_MESFIN  := RCV->RCV_MESFIN
				RCW->RCW_MES     := STRZERO(nX,2)
				RCW->RCW_TIPORE  := cTipDirf
				RCW->RCW_VALOR   := aCols[nX,nY]
				MsUnLock()
			Endif
		Next nY 
	Next nX
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Grava informacoes complementares³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//	m720GravInf(RCV->RCV_MAT)	

End Transaction
RestArea( aAreaRCV )
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp720LinOk³ Autor ³ J. Ricardo            ³ Data ³ 20.02.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Critica linha digitada                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function gp720LinOk(o)
//Local nx
Local lRet 	:= .T.

/*For nx := 2 To Len(aHeader)
	If aCols[n,nx] < 0.00 .And. lRet
		Help(" ",1,"M720SVALOR")
		lRet := .F.
		Exit
	Endif
Next nx*/

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp720TudOk³ Autor ³ J. Ricardo            ³ Data ³ 20.02/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function gp720TudOk(o)
Local lRetorna  := .T.
Continua        := .F.

Return lRetorna

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³F720DEL   ³ Autor ³ J. Ricardo            ³ Data ³ 20.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava no arquivo de Acumulados Anuais                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ F720DEL                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function FDel720(nDel)
Begin Transaction 
	dbSelectArea("RCV")
	RecLock("RCV",.F.,.T.)
	dbDelete()

	dbSelectArea("RCW")
	dbSeek( RCV->RCV_FILIAL+RCV->RCV_MAT+RCV->RCV_RFC+RCV->RCV_ANO+RCV->RCV_MESINI+RCV->RCV_MESFIN )
	While ! Eof() .And. RCV->RCV_FILIAL+RCV->RCV_MAT+RCV->RCV_RFC+RCV->RCV_ANO+RCV->RCV_MESINI+RCV->RCV_MESFIN == RCW->RCW_FILIAL+RCW->RCW_MAT+RCW->RCW_RFC+RCW->RCW_ANO+RCW->RCW_MESINI+RCW->RCW_MESFIN
		RecLock("RCW",.F.,.T.)
		dbDelete()
		nDel ++
      dbSkip()
	EndDo

	dbSelectArea("RCV")
	RecLock("RCV",.F.,.T.)
	dbDelete()
	aColsComp := {}
End Transaction
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gera720   ³ Autor ³                       ³ Data ³ 20.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera o aHeader e aCols                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gera720                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gera720(cAlias,aHeader,aCols,nUsado,nOpc)

Local aMes   := {STR0014,STR0015,STR0016,STR0017,STR0018,STR0019,; //"Janeiro    R$"###"Fevereiro  R$"###"Marco      R$"###"Abril      R$"###"Maio       R$"###"Junho      R$"
                 STR0020,STR0021,STR0022,STR0023,STR0024,STR0025}  //"Julho      R$"###"Agosto     R$"###"Setembro   R$"###"Outubro    R$"###"Novembro   R$"###"Dezembro   R$"
Local aDirfv:= {}
Local nX
Local nY
Local nL    := 1           
Local aChave := {}
Local cTipo := ""
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Guarda as posicoes inicio e tamanho, dentro das vaiaves com os respectivos conteudos.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

AADD(aHeader,{STR0026 ,"M710MES"  ,"@!",13, 0,"! EMPTY()","û", "C" ," "}) //"Mes            "
cFilRCC:=xFilial("RCC")
DbSelectArea("RCC")
DbSetOrder(1)      
DbSeek(xFilial()+"S020")
Do While cFilRCC == RCC_FILIAL .and. RCC_CODIGO == "S020"    
	If !( AllTrim(SubStr(RCC_CONTEU,1,3)) $ "1B,2A,2C,2D,2E,2F,2O,2P,3b,3e,41m,6Q1,6R1" )
		
		cChave := Alltrim(SubStr(RCC_CONTEU, 1, 4 ))
		If nL >= 67
		   cTipo := "."
		Endif   
		cDescr	:= AllTrim(SubStr(RCC_CONTEU,1,2)) + " - " + Alltrim(SubStr(RCC_CONTEU, 5,58))+cTipo
		//cDescr := Alltrim(SubStr(RCC_CONTEU, 5,30))+cTipo
		cPict	 := Alltrim(Substr(RCC_CONTEU,105,17)) 
		nTam   := Val(Alltrim(Substr(RCC_CONTEU,122, 2)))
		nDec   := Val(Alltrim(Substr(RCC_CONTEU,124, 1))) 
		cAlter := "M710"+ StrZero(nL,3)
		AAdd(aChave, {cChave} )
		AAdd(aHeader,{cDescr,cAlter,cPict,nTam ,nDec,"", "û","N"," " })                '
		nL += 1
	EndIf
	DbSkip()	
EndDo    

nUsado := Len(aHeader)
aLetra := Array(Len(aChave))
aDirfV := Array(12,nUsado)

For nx := 1 to Len(aChave)
	aLetra[nx] := aChave[nx,1]
Next nx

For nX := 1 to 12
    For nY := 1 TO nUsado
		If nY == 1
			aDirfv[nX,nY] := aMes[nX]
		Else
			aDirfv[nX,nY] := 0.00
		Endif
	Next nY
Next nX

If nOpc # 3
	dbSelectArea("RCW")
	dbSeek(RCV->RCV_FILIAL+RCV->RCV_MAT+RCV->RCV_RFC+RCV->RCV_ANO+RCV->RCV_MESINI+RCV->RCV_MESFIN)
	While !Eof() .And. RCV->RCV_FILIAL+RCV->RCV_MAT+RCV->RCV_RFC+RCV->RCV_ANO+RCV->RCV_MESINI+RCV->RCV_MESFIN == RCW->RCW_FILIAL+RCW->RCW_MAT+RCW->RCW_RFC+RCW->RCW_ANO+RCW->RCW_MESINI+RCW->RCW_MESFIN
		nMes   := Val(RCW->RCW_MES)
		nElem  := Ascan(aLetra, ALLTRIM(RCW->RCW_TIPORE))  
		If nElem >0
			adirfv[nMes,nElem+1] := RCW->RCW_VALOR
		EndIf	
		dbSkip(1)
	Enddo
Endif
aCols := Aclone(aDirfv)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GP720LOK  ºAutor  ³Andreia dos Santos  º Data ³  01/24/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida a linha da msGetdados                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM720                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION GP720LOK()
Local lRet := .T.

if  (aCols[n,5] == .F.) .and. (empty(aCols[n,1]) .or. aCols[n,3]==0) 
	lRet := .F.
EndIf	
RETURN( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³m720GravInf ºAutor  ³Andreia dos Santos º Data ³ 01/24/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Faz a gravacao no arquivo de informacoes complementares     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM720                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/                     
Function m720GravInf(cMatr)
Local nX := 1

For nX := 1 to len(aColsComp)       
	if !empty(aColsComp[nx][01])
		//--Verifica se Nao esta Deletado no aCols
		If nx <= Len(aColsRec)               		// Altera Linha existente
			(cArqCompl)->(dbGoto(aColsRec[nx]))
			RecLock(cArqCompl,.F.)
			//--Verifica se esta deletado
			If aColsComp[nx][Len(aColsComp[nx])] 
				(cArqCompl)->(dbDelete())
			Else	
				(cArqCompl)->INF_NOME		:= aColsComp[nX,01]
				(cArqCompl)->INF_CIC		:= aColsComp[nX,02]
				(cArqCompl)->INF_VALOR		:= aColsComp[nX,03]
				(cArqCompl)->INF_VERBA		:= aColsComp[nX,04]
		   	MsUnlock()
			EndIf
		Else                                  		// Nova Linha da GetDados
			If !aColsComp[nx][Len(aColsComp[nx])]  
				RecLock(cArqCompl,.T.)
				(cArqCompl)->INF_FILIAL		:= xFilial("RCV")
				(cArqCompl)->INF_MAT 		:= cMATR
				(cArqCompl)->INF_NOME		:= aColsComp[nX,01]
				(cArqCompl)->INF_CIC		:= aColsComp[nX,02]
				(cArqCompl)->INF_VALOR		:= aColsComp[nX,03]
				(cArqCompl)->INF_VERBA		:= aColsComp[nX,04]
			   	MsUnlock()
		    Endif
		EndIf
	EndIf	
Next      
aColsComp := {}

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³m720AltInfºAutor  ³Microsiga           º Data ³  01/30/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Altera dados das informacoes complementares atraves da     º±±
±±º          ³ verba informada                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
function m720AltInf()
lOCAL cVerba:= SPACE(03)
Local cRFC 	:= SPACE(14)
Local cNome := SPACE(20)
Local lRet
Local nOpca
Local nX 
Local aArea := GetArea()
Local cAltera                  

Local oDlg
Local oVerba
Local oRFC
Local oNOme
Local oAltera

Private aItem := {"Sim","Nao"}

DEFINE MSDIALOG oDlg FROM  15,1 TO 200,474 TITLE OemToAnsi(STR0004) PIXEL  //"Alteracao das Informacoes Complementares"
nOpca := 2
@ 07, 007 TO 68, 229 LABEL ""                 OF oDlg PIXEL
@ 16, 013 SAY STR0007             SIZE 053,07 OF oDlg PIXEL   //VERBA
@ 14, 045 MSGET oVerba VAR cVerba SIZE 016,10 OF oDlg PIXEL PICTURE "999"  F3 "SRV"  VALID (lRet := ( mPoeDesc(cVerba,@cNome) ),oNome:Refresh(),lRet) 
@ 14, 075 MSGET oNome  VAR cNome  SIZE 150,10 OF oDlg PIXEL PICTURE "@!" 
@ 36, 013 SAY STR0008  SIZE 055,07 OF oDlg PIXEL  //"Altera RFC?"
@ 34, 075 COMBOBOX oAltera VAR cAltera ITEMS aItem  SIZE 040,10 OF oDlg PIXEL 
@ 52, 013 SAY STR0005          SIZE 055,07 OF oDlg PIXEL  //"RFC
@ 50, 045 MSGET oRFC   VAR cRFC   SIZE 070,10 OF oDlg PIXEL PICTURE "@!"  VALID CGC( cRFC ) WHEN (cAltera=="Sim")
DEFINE SBUTTON FROM 75, 173 TYPE 1 ENABLE OF oDlg ACTION( nOpcA :=1 ,oDlg:End() )
DEFINE SBUTTON FROM 75, 201 TYPE 2 ENABLE OF oDlg ACTION( nOpcA :=2 ,oDlg:End() )
ACTIVATE MSDIALOG oDlg CENTERED

If nOpca == 1
  
	(cArqCompl)->(dbGotop() )
	While (cArqCompl)->(!eof())
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Nao altera se a verba estiver em branco porque e referente ³
		//³ao beneficiario.                                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		if (cArqCompl)->INF_VERBA == cVerba .and. !empty(cVerba)
			Reclock(cArqCompl,.F.)
			(cArqCompl)->INF_NOME 	:= cNome
			(cArqCompl)->INF_RFC 	:= cRFC
			MsUnlock()
		Endif
		(cArqCompl)->(dbskip())
	EndDo
Endif

RestArea( aArea )

Return

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ AjustSx3		³Autor³  Igor Franzoi     ³ Data ³22/09/2009³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Ajusta os campos do SX3										³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fEstPag														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM720														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³															³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function ValidState( cEst )

Local lRet 		:= .T.

Local cEstado	:= ""

cEstado := fDesc( "SX5","12"+cEst, "X5_DESCSPA", 30 )

If Empty(cEstado)
	lRet := .F.
	If !lRet
		Help(" ",1,"NOTAB")
	Endif
EndIf

Return ( lRet )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ gp720Aux		³Autor³  Gustavo M.	      ³ Data ³22/08/2012³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Utilizada para identificar quando é selecionado a opçao de  ³
³          ³incluir    													³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA720                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³gp720Man													³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function gp720Aux(cAlias,nReg,nOpcX)

lIncluir := .T.   

Return(gp720Man(cAlias,nReg,nOpcX))  
