#Include "PROTHEUS.CH"
#INCLUDE "GPER600.CH"
#INCLUDE "REPORT.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER600  ³ Autor   ³ Maurico T. Takakura   ³ Data ³ 07.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Ferias para o Mexico                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GpeR600(void)                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL  .             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS   ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Tatiane    ³05/07/06³100547  |Conversao para relatorio personalizavel   ³±± 
±±³ Mauricio T.³23/11/06³--------|Tratamento para AS/400 e correcao de var. ³±± 
±±³Erika K.    ³02/06/08³        ³Alteracao dos nomes dos perguntes para com³±±
±±³            ³        ³        ³patibilizar dicionarios Mexico e R 1.2.   ³±±
±±³Renata E.   ³10/11/08³32122008³Ajuste para emissao em outros paises.		³±±
±±³Renata E.   ³18/11/08³154280  ³Ajuste no help, espanhol, das perguntas	³±±
±±³            ³        ³        ³do R4 e R3.							    ³±±
±±³Renata E.   ³25/11/08³32122008³Ajuste no help espanhol da descricao do   ³±±
±±³            ³        ³        ³relatorio (R3 e R4); da pergunta "Filial" ³±±
±±³            ³        ³        ³(R4 )e da pergunta processo (R3 e R4)	    ³±±
±±³Erika K.    ³30/12/09³296670/ ³Ajustes para permitir impressao quando 	³±±
±±³            ³        ³    2009³Processo estiver em branco.               ³±±
±±³Francisco Jr³01/03/10³00003848³Alterado variaveis lSaltCC,lSaltProc = .T.³±±
±±³Mohanad Odeh³16/03/12³0005871/³COL: Correção da query com a inclusão     ³±±
±±³            ³        ³    2012³do comando "Distinct" e "RCJ.%notDel%"    ³±±
±±³            ³        ³  TEQM42³afim de evitar resultados repetidos       ³±±
±±³            ³        ³        ³                                          ³±±
±±³Miguel Rojas³28/10/13³COL11.8_RH³Para Colombia se agrego la columna de   ³±±
±±³            ³        ³Req 239723³fecha de caducidad.                     ³±±
±±³Paulo O.    ³22/05/17³Ajuste na declaração da variavel nMultVigVa para   ³±±
±±³Inzonha     ³        ³que seja visivel no relatório não personalizável   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GpeR600()
Local oReport

Private nMultVigVa := If(cPaisLoc == "MEX", 365, 1)
Private cAliasMov	:= "SRA"

	If FindFunction("TRepInUse") .And. TRepInUse()

		//-- Interface de impressao
		Pergunte("GPE600R",.F.)
	   	oReport := ReportDef()
   		oReport:PrintDialog()
 	Else
 		GpeR600R3()
	EndIF

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ R.H. - Tatiane Matias ³ Data ³ 05.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definicao do relatorio                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

//-- Objeto Relatorio
Local oReport

//-- Objeto Section
Local oSection1   
Local oSection2   
Local oSection3
Local oSection4
Local oSection5     
Local oSection6
Local oSection7
Local oSection8

Local	cDesc		:=	STR0001 + ". "+STR0002		// "Ferias Vencidas no Periodo"	# "Ser  impresso de acordo com os parametros solicitados pelo usuario."

Local aOrd     := {	STR0003,; 			//"Matricula"
						 	STR0004,;			//"Nome"
						 	STR0005,;			//"Centro de Custo"
						 	STR0006 }				//"Processo"
						 	
						 	

	//-- Inicio definicao do Relatorio
	DEFINE REPORT oReport NAME "GPER600" TITLE OemToAnsi(STR0009) PARAMETER "GPE600R" ACTION {|oReport| PrintReport(oReport)} DESCRIPTION cDesc TOTAL IN COLUMN

		//-- Utilizada essa section para as ordens 1=Matricula e 2=Nome
		//-- Section de Funcionario
		DEFINE SECTION oSection1 OF oReport TABLES "SRA", "RCJ" TITLE STR0010 ORDERS aOrd  TOTAL IN COLUMN
		oSection1:SetHeaderBreak(.T.)
				
			DEFINE CELL NAME "RA_FILIAL"	OF oSection1 ALIAS "SRA"
			DEFINE CELL NAME "RA_MAT" 		OF oSection1 ALIAS "SRA"
			DEFINE CELL NAME "RA_NOME" 	OF oSection1 ALIAS "SRA"

			If cPaisLoc == "MEX"
				DEFINE CELL NAME "RCJ_VIGVAC" OF oSection1 ALIAS "RCJ"
				oSection1:Cell("RCJ_VIGVAC"):Disable()
 			EndIf
 			
		   	If cPaisLoc == "COL"
				  DEFINE CELL NAME "dFcCaduca" OF oSection1 ALIAS "SRF" title oemToansi(STR0029)SIZE 16 BLOCK {|| dFcCaduca :=(cAliasMov)->RF_DATAFIM + ((cAliasMov)->RCJ_VIGVAC*365)}
		          oSection1:Cell("dFcCaduca"):Disable()	
			EndIf

			oSection1:Cell("RA_FILIAL"):Disable()


	      //-- Section de Ferias Vencidas no Periodo do Funcionario
			DEFINE SECTION oSection2 OF oSection1 TABLES "SRF" TITLE STR0001 TOTAL IN COLUMN
			oSection2:SetHeaderBreak(.T.)

				DEFINE CELL NAME "RF_DATABAS" 	OF oSection2 ALIAS "SRF"
				DEFINE CELL NAME "RF_DATAFIM" 	OF oSection2 ALIAS "SRF"
				DEFINE CELL NAME "RF_DIASDIR" 	OF oSection2 ALIAS "SRF"
				DEFINE CELL NAME "RF_DIASANT" 	OF oSection2 ALIAS "SRF"
				DEFINE CELL NAME "RF_DFERVAT" 	OF oSection2 ALIAS "SRF"
				DEFINE CELL NAME "RF_DFERAAT" 	OF oSection2 ALIAS "SRF"
				DEFINE CELL NAME "RF_DFERANT" 	OF oSection2 ALIAS "SRF"
				
				If cPaisLoc == "COL"
				      DEFINE CELL NAME "dFcCaduca" OF oSection2 ALIAS "SRF" title oemToansi(STR0030) BLOCK {|| dFcCaduca :=(cAliasMov)->RF_DATAFIM + ((cAliasMov)->RCJ_VIGVAC*365)} 			         
 			    EndIf
				
				
		//-- Utilizada essa section para as ordens 3=Centro de Custo
		//-- Section de Centro de Custo
		DEFINE SECTION oSection3 OF oReport TABLES "SRA" TITLE STR0005 TOTAL IN COLUMN
		oSection3:SetHeaderBreak(.T.)
				
			DEFINE CELL NAME "RA_FILIAL" 	OF oSection3 ALIAS "SRA"
			DEFINE CELL NAME "RA_CC" 		OF oSection3 ALIAS "SRA"
			DEFINE CELL NAME "DESCRICAO" 	OF oSection3 ALIAS "   " BLOCK {|| DescCc((cAliasMov)->RA_CC,cFilaInfo) } SIZE 30
			                                          
			//-- Section de Funcionarios do Centro de Custo
			DEFINE SECTION oSection4 OF oSection3 TABLES "SRA", "RCJ" TITLE STR0010 TOTAL IN COLUMN
			oSection4:SetHeaderBreak(.T.)
				
				DEFINE CELL NAME "RA_MAT" 		OF oSection4 ALIAS "SRA"
				DEFINE CELL NAME "RA_NOME" 	OF oSection4 ALIAS "SRA"

				

				If cPaisLoc == "MEX"
					DEFINE CELL NAME "RCJ_VIGVAC" OF oSection4 ALIAS "RCJ"

					oSection4:Cell("RCJ_VIGVAC"):Disable()
	    		EndIf
	    		
   	    		If cPaisLoc == "COL"
   				     DEFINE CELL NAME "dFcCaduca" OF oSection4 ALIAS "SRF" title oemToansi(STR0030)SIZE 16 BLOCK {|| dFcCaduca :=(cAliasMov)->RF_DATAFIM + ((cAliasMov)->RCJ_VIGVAC*365)}
   			         oSection4:Cell("dFcCaduca"):Disable()
			    EndIf
	    		
	    		  
		      //-- Section de Ferias Vencidas no Periodo do Funcionario
				DEFINE SECTION oSection5 OF oSection4 TABLES "SRF" TITLE STR0001 TOTAL IN COLUMN
				oSection5:SetHeaderBreak(.T.)
	
					DEFINE CELL NAME "RF_DATABAS" 	OF oSection5 ALIAS "SRF"
					DEFINE CELL NAME "RF_DATAFIM" 	OF oSection5 ALIAS "SRF"
					DEFINE CELL NAME "RF_DIASDIR" 	OF oSection5 ALIAS "SRF"
					DEFINE CELL NAME "RF_DIASANT" 	OF oSection5 ALIAS "SRF"
					DEFINE CELL NAME "RF_DFERVAT" 	OF oSection5 ALIAS "SRF"
					DEFINE CELL NAME "RF_DFERAAT" 	OF oSection5 ALIAS "SRF"
					DEFINE CELL NAME "RF_DFERANT" 	OF oSection5 ALIAS "SRF"
					
					If cPaisLoc == "COL"
				       DEFINE CELL NAME "dFcCaduca" OF oSection5 ALIAS "SRF" title oemToansi(STR0030) BLOCK {|| dFcCaduca :=(cAliasMov)->RF_DATAFIM + ((cAliasMov)->RCJ_VIGVAC*365)} 			         
 			        EndIf
			    					
					
		//-- Utilizada essa section para as ordens 4=Processo
		//-- Section de Processo
		DEFINE SECTION oSection6 OF oReport TABLES "SRA", "RCJ" TITLE STR0006 TOTAL IN COLUMN	// "Processo"
		oSection6:SetHeaderBreak(.T.)
				
			DEFINE CELL NAME "RA_FILIAL"	OF oSection6 ALIAS "SRA"
			DEFINE CELL NAME "RA_PROCES" 	OF oSection6 ALIAS "SRA"
			DEFINE CELL NAME "RCJ_DESCRI" OF oSection6 ALIAS "RCJ"

			If cPaisLoc == "MEX"
				DEFINE CELL NAME "RCJ_VIGVAC" OF oSection6 ALIAS "RCJ"

				oSection6:Cell("RCJ_VIGVAC"):Disable()
			EndIf
   			If cPaisLoc == "COL"
   		       DEFINE CELL NAME "dFcCaduca" OF oSection6 ALIAS "SRF" title oemToansi(STR0030) BLOCK {|| dFcCaduca :=(cAliasMov)->RF_DATAFIM + ((cAliasMov)->RCJ_VIGVAC*365)} 			         
   		       oSection6:Cell("dFcCaduca"):Disable()
   		    EndIf

						
			oSection6:Cell("RA_FILIAL"):Disable()

						
			//-- Section de Funcionarios do Centro de Custo
			DEFINE SECTION oSection7 OF oSection6 TABLES "SRA" TITLE STR0010 TOTAL IN COLUMN
			oSection7:SetHeaderBreak(.T.)
				
				DEFINE CELL NAME "RA_MAT" 		OF oSection7 ALIAS "SRA"
				DEFINE CELL NAME "RA_NOME" 	OF oSection7 ALIAS "SRA"
   				If cPaisLoc == "COL"
   				       DEFINE CELL NAME "dFcCaduca" OF oSection7 ALIAS "SRF" title oemToansi(STR0030) BLOCK {|| dFcCaduca :=(cAliasMov)->RF_DATAFIM + ((cAliasMov)->RCJ_VIGVAC*365)} 			         
   				       oSection7:Cell("dFcCaduca"):Disable()
   			    EndIf
			

		      //-- Section de Ferias Vencidas no Periodo do Funcionario
				DEFINE SECTION oSection8 OF oSection7 TABLES "SRF" TITLE STR0001 TOTAL IN COLUMN
				oSection8:SetHeaderBreak(.T.)
	
					DEFINE CELL NAME "RF_DATABAS" 	OF oSection8 ALIAS "SRF"
					DEFINE CELL NAME "RF_DATAFIM" 	OF oSection8 ALIAS "SRF"
					DEFINE CELL NAME "RF_DIASDIR" 	OF oSection8 ALIAS "SRF"
					DEFINE CELL NAME "RF_DIASANT" 	OF oSection8 ALIAS "SRF"
					DEFINE CELL NAME "RF_DFERVAT" 	OF oSection8 ALIAS "SRF"
					DEFINE CELL NAME "RF_DFERAAT" 	OF oSection8 ALIAS "SRF"
					DEFINE CELL NAME "RF_DFERANT" 	OF oSection8 ALIAS "SRF"
					
					If cPaisLoc == "COL"
				       DEFINE CELL NAME "dFcCaduca" OF oSection8 ALIAS "SRF" title oemToansi(STR0030) BLOCK {|| dFcCaduca :=(cAliasMov)->RF_DATAFIM + ((cAliasMov)->RCJ_VIGVAC*365)} 			         
 			        EndIf
			
Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³PrintReport³ Autor ³ R.H. - Tatiane Matias   ³ Data ³ 05.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao de Liquidos - Relatorio Personalizavel                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport)     

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis Locais                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//-- Numerico
Local nOrdem   	:= oReport:Section(1):GetOrder()
Local nCont			:= 0
Local nTamProc    := GetSx3Cache( "RA_PROCES", "X3_TAMANHO" )

//-- Objeto
Local oSection1 	:= IIf(nOrdem == 3, oReport:Section(2), IIf(nOrdem == 4, oReport:Section(3) , NIL)) 	//Centro de Custo ou Processo
Local oSection2 	:= IIf(nOrdem == 3 .or. nOrdem == 4, oSection1:Section(1), oReport:Section(1))			// Funcionario
Local oSection3 	:= oSection2:Section(1)																						// Ferias Vencidas

//-- String
Local cTitulo		:= ""
Local cFiltro		:= ""
Local cSitQuery	:= ""
Local cCatQuery	:= ""
Local cSvProcesso	:= ""
Local cMaxProces	:= ""
Local cMinProces	:= "ZZZZZZ"

//-- Logico
Local lAllProc		:= .F.

//--Data
Local dDataSRF
Local cCposSel		:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis Privadas                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//--String
Private cAliasSRF	:= "SRF"
Private cFilaInfo := ""

//-- Numerico
Private nVigVac
                 
//-- Array
Private aInfo   	:= {}

	cProcesso	:= mv_par05
	cSit     	:= mv_par06
	cCat     	:= mv_par07
	cLanca	 	:= mv_par08
	lSaltCC  	:= mv_par09 == 1
	lSaltProc	:= mv_par10 == 1
	dDataIni 	:= mv_par11
	dDataFim 	:= mv_par12

	If nOrdem == 1 .or. nOrdem == 2
		//Total de Funcionarios da Filial                    
		DEFINE BREAK oBreakFil OF oReport WHEN oSection2:Cell("RA_FILIAL") TITLE STR0024
		DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") FUNCTION COUNT BREAK oBreakFil NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
		
		//Total da Filial                    
		DEFINE BREAK oBreakFil2 OF oReport WHEN oSection2:Cell("RA_FILIAL") TITLE STR0027
		DEFINE FUNCTION FROM oSection3:Cell("RF_DIASDIR") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RF_DIASANT") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RF_DFERVAT") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RF_DFERAAT") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RF_DFERANT") 	FUNCTION SUM 	BREAK oBreakFil2 NO END SECTION PICTURE "999,999,999,999.99"
		
	    // Totales Por Matricula y Nombre solo Para Colombia
		 If cPaisLoc == "COL"
		  If lSaltCC                              // Validación para Colombia
		    oBreakFil:SetPageBreak(.T.)
		  Endif
		 Else
		    oBreakFil:SetPageBreak(.T.) 		  // Línea Original.
		 endIF
		 
	Else
		If nOrdem == 3
			//-- Total de Funcionarios da Section Centro de Custo
			DEFINE BREAK oBreakCC OF oReport WHEN oSection1:Cell("RA_CC") TITLE STR0019
		
			DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT BREAK oBreakCC NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
				
			//-- Total da Section Centro de Custo
			DEFINE BREAK oBreakCC2 OF oReport WHEN oSection1:Cell("RA_CC") TITLE STR0028
			DEFINE FUNCTION FROM oSection3:Cell("RF_DIASDIR") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION  NO END REPORT PICTURE "999,999,999,999.99"
			DEFINE FUNCTION FROM oSection3:Cell("RF_DIASANT") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
			DEFINE FUNCTION FROM oSection3:Cell("RF_DFERVAT") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
			DEFINE FUNCTION FROM oSection3:Cell("RF_DFERAAT") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
			DEFINE FUNCTION FROM oSection3:Cell("RF_DFERANT") 	FUNCTION SUM BREAK oBreakCC2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
	
			If lSaltCC
				oBreakCC2:SetPageBreak(.T.)
			EndIf 

		ElseIf nOrdem == 4
			//-- Total de Funcionarios da Section Processo
			DEFINE BREAK oBreakProc OF oReport WHEN oSection1:Cell("RA_PROCES") TITLE STR0020
			DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT BREAK oBreakProc NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"
					
			//-- Total da Section Processo
			DEFINE BREAK oBreakProc2 OF oReport WHEN oSection1:Cell("RA_PROCES") TITLE STR0029
			DEFINE FUNCTION FROM oSection3:Cell("RF_DIASDIR") 	FUNCTION SUM BREAK oBreakProc2 NO END SECTION  NO END REPORT PICTURE "999,999,999,999.99"
			DEFINE FUNCTION FROM oSection3:Cell("RF_DIASANT") 	FUNCTION SUM BREAK oBreakProc2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
			DEFINE FUNCTION FROM oSection3:Cell("RF_DFERVAT") 	FUNCTION SUM BREAK oBreakProc2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
			DEFINE FUNCTION FROM oSection3:Cell("RF_DFERAAT") 	FUNCTION SUM BREAK oBreakProc2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
			DEFINE FUNCTION FROM oSection3:Cell("RF_DFERANT") 	FUNCTION SUM BREAK oBreakProc2 NO END SECTION 	NO END REPORT PICTURE "999,999,999,999.99"
			
			If lSaltProc
				oBreakProc2:SetPageBreak(.T.)
			EndIf 

		EndIf

		//Total de Funcionarios da Filial                    
		DEFINE BREAK oBreakCCFil OF oReport WHEN oSection1:Cell("RA_FILIAL") TITLE STR0024
		DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") FUNCTION COUNT BREAK oBreakCCFil NO END SECTION NO END REPORT PICTURE "999,999,999,999.99"

		//Total da Filial                    
		DEFINE BREAK oBreakCCFil2 OF oReport WHEN oSection1:Cell("RA_FILIAL") TITLE STR0027
		DEFINE FUNCTION FROM oSection3:Cell("RF_DIASDIR") 	FUNCTION SUM 	BREAK oBreakCCFil2 NO END SECTION PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RF_DIASANT") 	FUNCTION SUM 	BREAK oBreakCCFil2 NO END SECTION PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RF_DFERVAT") 	FUNCTION SUM 	BREAK oBreakCCFil2 NO END SECTION PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RF_DFERAAT") 	FUNCTION SUM 	BREAK oBreakCCFil2 NO END SECTION PICTURE "999,999,999,999.99"
		DEFINE FUNCTION FROM oSection3:Cell("RF_DFERANT") 	FUNCTION SUM 	BREAK oBreakCCFil2 NO END SECTION PICTURE "999,999,999,999.99" 
Endif	

	
	lAllProc 	:= Iif(AllTrim( cProcesso ) == "*" .Or. Empty(cProcesso), .T., .F.)

	cTitulo := oReport:Title() + ": " + Dtoc(dDataIni) + STR0018 + Dtoc(dDataFim)
	oReport:SetTitle(cTitulo)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz filtro no arquivo...                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP

		cAliasMov := GetNextAlias()
		cAliasSRF := cAliasMov

		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeSqlExpr("GPE600R")

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Modifica variaveis para a Query                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//-- Situacao do Funcionario
		cSitQuery := "" 
		For nCont:=1 to Len(cSit)
			cSitQuery += "'"+Subs(cSit,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cSit)
				cSitQuery += "," 
			Endif
		Next nCont     
		cSitQuery := "%" + cSitQuery + "%"
		

		//-- Categoria do Funcionario
		cCatQuery := ""
		For nCont:=1 to Len(cCat)
			cCatQuery += "'"+Subs(cCat,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cCat)
				cCatQuery += "," 
			Endif
		Next nCont
		cCatQuery := "%" + cCatQuery + "%"
                  
		cFiltro := ""
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Gerar lista de Itens dos Processos.  										  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !lAllProc
			cSvProcesso := cProcesso
			cProcesso 	:= ""
			For nCont:= 1 to Len(Alltrim(cSvProcesso)) Step nTamProc
				cProcesso += "'" + Substr(cSvProcesso, nCont, nTamProc) + "',"
				If Substr(cSvProcesso, nCont, nTamProc) > cMaxProces 
					cMaxProces := Substr(cSvProcesso, nCont, nTamProc)
				EndIf
				If Substr(cSvProcesso, nCont, nTamProc) < cMinProces
					cMinProces := Substr(cSvProcesso, nCont, nTamProc)
				EndIf
			Next nCont
			cProcesso := Substr( cProcesso, 1, Len(cProcesso)-1)
			cFiltro := " SRA.RA_PROCES  IN (" + cProcesso + ") AND "
		EndIf         
		
		If cLanca == 1
			cFiltro += 	" (SRF.RF_DFERVAT <> SRF.RF_DFERANT OR SRF.RF_DFERVAT = 0) AND "
		ElseIf cLanca == 2
			cFiltro +=	" (SRF.RF_DFERVAT = SRF.RF_DFERANT AND SRF.RF_DFERVAT > 0) AND "
		EndIf

		cFiltro := "%" + cFiltro + "%"
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta string de ordenacao                                          		  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nOrdem == 1												// Matricula
			cOrdem := "%RA_FILIAL, RA_MAT, RF_DATABAS,RF_DATAFIM%"
		ElseIf nOrdem == 2										// Nome
			cOrdem := "%RA_FILIAL, RA_NOME, RA_MAT, RF_DATABAS,RF_DATAFIM%"
		ElseIf nOrdem == 3										// Centro de Custo
			cOrdem := "%RA_FILIAL, RA_CC, RA_MAT, RF_DATABAS,RF_DATAFIM%"
		ElseIf nOrdem == 4										// Processo
			cOrdem := "%RA_FILIAL, RA_PROCES, RA_MAT, RF_DATABAS,RF_DATAFIM%"
		EndIf

		cCposSel := "%"
		cCposSel += " DISTINCT RA_FILIAL, RA_MAT, RA_NOME, RA_CC, RA_PROCES,"
		cCposSel += "RCJ_DESCRI, RF_FILIAL, RF_MAT,"
		cCposSel += "RF_DATABAS, RF_DATAFIM, RF_DIASDIR, RF_DIASANT, RF_DFERVAT, RF_DFERAAT, RF_DFERANT"

		If	cPaisLoc == "MEX" .OR. cPaisLoc == "COL"
			cCposSel += ", RCJ_VIGVAC"
		EndIf
		cCposSel += "%"

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa Query                                                  		  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nOrdem == 3 .or. nOrdem == 4
			BEGIN REPORT QUERY oSection1
		Else
			BEGIN REPORT QUERY oSection2
		EndIf

		BeginSql alias cAliasMov   
			SELECT 					 
				%exp:cCposSel%
			FROM   %table:SRA% SRA, %table:RCJ% RCJ, %table:SRF% SRF
			WHERE  SRA.RA_FILIAL = SRF.RF_FILIAL			AND
			       SRA.RA_MAT 	= SRF.RF_MAT				AND
			       SRA.RA_PROCES	= RCJ.RCJ_CODIGO			AND
					 SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%) 	AND
				    SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) 	AND
				    %exp:cFiltro%
					 SRF.%notDel% AND 
					 SRA.%notDel% AND
					 RCJ.%notDel%
			ORDER BY %exp:cOrdem%
		EndSql

		/*
		Prepara relatorio para executar a query gerada pelo Embedded SQL passando como 
		parametro a pergunta ou vetor com perguntas do tipo Range que foram alterados 
		pela funcao MakeSqlExpr para serem adicionados a query
		*/

		If nOrdem == 3 .or. nOrdem == 4
			END REPORT QUERY oSection1 PARAM mv_par01, mv_par02, mv_par03, mv_par04
		Else
			END REPORT QUERY oSection2 PARAM mv_par01, mv_par02, mv_par03, mv_par04
		EndIf
		
		If nOrdem == 3
			oSection2:SetParentQuery()

			//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
			//-- Filtro por Filial e Centro de Custo
			oSection2:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC })

		ElseIf nOrdem == 4
			oSection2:SetParentQuery()

			//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
			//-- Filtro por Filial e Centro de Custo
			oSection2:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES })
		EndIf
		



		//-- Utiliza a query do Pai (Section2) - Funcionarios
		oSection3:SetParentQuery()

		//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
		//-- Filial e matricula da tabela de funcionarios (SRA) com a tabela de ausencias (SR8)
		oSection3:SetParentFilter({|cParam| (cAliasSRF)->RF_FILIAL + (cAliasSRF)->RF_MAT == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT })


		
		
		
		

	#ELSE
		cAliasMov   := "SRA"  
		cAliasSRF	:= "SRF"

		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeAdvplExpr("GPE600R")

		DbSelectArea(cAliasMov)

		//-- Adiciona no filtro o parametro tipo Range
		//-- Filial
		If !Empty(mv_par01)
			cFiltro := mv_par01 + " .AND. "
		EndIf                 
		
		//-- Centro de Custo
		If !Empty(mv_par02)
			cFiltro += mv_par02 + " .AND. "
		EndIf                 

		//-- Matricula
		If !Empty(mv_par03)
			cFiltro += mv_par03 + " .AND. "
		EndIf                 

		//-- Nome
		If !Empty(mv_par04)
			cFiltro += mv_par04 + " .AND. "
		EndIf                 
                                   
		If !lAllProc
			cFiltro += 'RA_PROCES $ "' + cProcesso + '" .AND. '
		EndIf

	  	cFiltro += 'RA_SITFOLH $ "' + cSit + '" .AND. '
		cFiltro += 'RA_CATFUNC $ "' + cCat + '"'      

		//-- Filtra a tabela de Funcionarios conforme perguntes
		If nOrdem == 1	.or. nOrdem == 2
		  	oSection2:SetFilter(cFiltro)

		  	If nOrdem == 1                  	// Matricula        
				oSection2:SetIdxOrder ( 1 )
			ElseIf nOrdem == 2					// Nome
				oSection2:SetIdxOrder ( 3 )
			EndIf              
			
		ElseIf nOrdem == 3 .or. nOrdem == 4				// Centro de Custo
		  	oSection1:SetFilter(cFiltro) 
      
			If nOrdem == 3
				oSection1:SetIdxOrder ( 2 )  

				//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
				//-- Filtro por Filial e Centro de Custo
				oSection2:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC })

			ElseIf nOrdem == 4					// Processo
				oSection1:SetIdxOrder( RetOrder( "SRA", "RA_FILIAL+RA_PROCES+RA_MAT") )  

				//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
				//-- Filtro por Filial e Centro de Custo
				oSection2:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES })
			EndIf
		EndIf
              
		cFiltro := ""
		If cLanca == 1
			cFiltro := 	" (cAliasSRF)->RF_DFERVAT <> (cAliasSRF)->RF_DFERANT .Or. (cAliasSRF)->RF_DFERVAT = 0 "
		ElseIf cLanca == 2
			cFiltro :=	" (cAliasSRF)->RF_DFERVAT == (cAliasSRF)->RF_DFERANT .and. (cAliasSRF)->RF_DFERVAT > 0 "
		EndIf
                                  
		//-- Filtra a tabela de Funcionarios conforme perguntes
	  	oSection3:SetFilter(cFiltro) 

		//-- Relaciona a Section Filha (Ausencia) com a Section Pai (Funcionario)
		oSection3:SetRelation({|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT },"SRF",1,.T.)

		//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
		//-- Filial e matricula da tabela de funcionarios (SRA) com a tabela de ausencias (SR8)
		oSection3:SetParentFilter({|cParam| (cAliasSRF)->RF_FILIAL + (cAliasSRF)->RF_MAT == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT })
	  	
		If nOrdem == 4
			TRPosition():New(oSection1,"RCJ",1,{|| xFilial("RCJ")+(cAliasMov)->RA_PROCES})
		Else
			TRPosition():New(oSection2,"RCJ",1,{|| xFilial("RCJ")+(cAliasMov)->RA_PROCES})
		EndIf

		(cAliasMov)->( DbGoTop() )

	#Endif	

	//-- Define o total da regua da tela de processamento do relatorio
	oReport:SetMeter((cAliasMov)->( RecCount() ))  

	//-- Impressao na quebra de pagina - Impressao das informacoes da Empresa e Filial
 	oReport:OnPageBreak({|| fCabec(oReport) })

  	If nOrdem == 3 .or. nOrdem == 4                 
		//-- Condicao de impressao do Funcionario
  		If nOrdem == 3
  			oSection1:SetLineCondition({|| fCondSRA(oSection2) })
	  		oSection2:SetLineCondition({|| fCondSRA(oSection2)}) 
	  		If cPaisLoc == "COL"
				oSection2:ASECTION[1]:SetLineCondition({|| fCondSRA(	oSection2:ASECTION[1]) })	  		
	  		Endif
  		Else
  			oSection1:SetLineCondition({|| fCondSRA(oSection1) })
	  		oSection2:SetLineCondition({|| fCondSRA(oSection1)}) 


  		EndIf
  		
 		If cPaisLoc == "COL"
			oSection2:ASECTION[1]:SetLineCondition({|| fCondSRA(	oSection2:ASECTION[1]) })	  		
  		Endif

		//-- Impressao do Relatorio
		oSection1:Print()
  	Else
		//-- Condicao de impressao do Funcionario
		oSection2:SetLineCondition({|| fCondSRA(oSection2) })            
		
		If cPaisLoc == "COL"
		   oSection2:ASECTION[1]:SetLineCondition({|| fCondSRA(	oSection2:ASECTION[1]) })
		ENDIF	

		//-- Impressao do Relatorio
		oSection2:Print()
  	EndIf

	#IFNDEF TOP 
		dbSelectArea(cAliasMov)
		DbClearFilter()
		dbSetOrder(1)
	#Endif	

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fCondSRA   ³ Autor ³ R.H. - Tatiane Matias   ³ Data ³ 04.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se a linha deve ser impressa                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fCondSRA()                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fCondSRA(oSection)

Local lRetorno
Local cAcessaSRA  := &("{ || " + ChkRH("GPER600","SRA","2") + "}")

	lRetorno := .T.

	#IFNDEF TOP     
		
		//-- Registro nao deve ser impressao (Funcionario não possui registro a ser impresso) 
  		If !(SRF->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT , .T.) ))
  			lRetorno := .F.
  		EndIf
		
	#Endif	
             
	If lRetorno
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Quebra de Filial                                    ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If (cAliasMov)->RA_FILIAL # cFilaInfo
			If !fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
				//-- Registro nao deve ser impressao (Nao acessa Filial ou nao carregado os identificadores de calculo) 
				lRetorno := .F.                                    

				//-- Carrega os arrays com as informacoes da Filial anterior.
				fInfo(@aInfo,cFilaInfo)
			Else
				cFilaInfo 		:= (cAliasMov)->RA_FILIAL
			EndIf
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste controle de acessos e filiais validas			   |
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !((cAliasMov)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
			//-- Registro nao deve ser impressao (Nao acessa tabela de Funcionarios ou Filial invalida) 
			lRetorno := .F.
		EndIf        
                                                    
		If lRetorno
			If cPaisLoc == "MEX"
				oSection:Cell("RCJ_VIGVAC"):Execute()
				nVigVac := Val(oSection:Cell("RCJ_VIGVAC"):GetText())
		                       
				dDataSRF := ( cAliasSRF )->( RF_DATAFIM ) + (nVigVac * nMultVigVa)
	  	   
	  		 Else
			    IF cPaisLoc == "COL"
			      	oSection:Cell("dFcCaduca"):Execute()
				  	dDataSRF := cTod(oSection:Cell("dFcCaduca"):GetText())
			     ELSE		
					dDataSRF := ( cAliasSRF )->( RF_DATAFIM )
				 ENDIF  
	  		EndIf
	  		
	  		If !(dDataSRF >= dDataIni .and. dDataSRF <= dDataFim)
				lRetorno := .F.
			EndIf   	
		EndIf
	Endif
		
Return lRetorno

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fCabec     ³ Autor ³ R.H. - Tatiane Matias   ³ Data ³ 07.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cabecalho do relatorio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fCabec()                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fCabec(oReport)

	If !Empty((cAliasMov)->RA_FILIAL)
		//-- Carrega informacoes da Filial
		cFilaInfo := (cAliasMov)->RA_FILIAL
		fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
		
		oReport:PrintText(STR0026+aInfo[3]+Space(20)+STR0021+cFilaInfo+" - "+aInfo[1])
		oReport:SkipLine()
	Else
		oReport:PrintText(STR0026+Space(35)+STR0021)
		oReport:SkipLine()
	EndIf                
	
Return NIL



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER600R3³ Autor ³ Maurico T. Takakura   ³ Data ³ 07.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Ferias para o Mexico                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GpeR600R3(void)                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GpeR600R3()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define Variaveis Locais (Basicas)                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local cString		:= "SRA"        // alias do arquivo principal (Base)
Local cDesc1 		:= STR0001		// "Ferias Vencidas no Periodo"
Local cDesc2 		:= STR0002		// "Ser  impresso de acordo com os parametros solicitados pelo usuario."
Local cDesc3		:= ""
Local cSvProcesso

Local nCont

Local aOrd      	:= {STR0003,STR0004,STR0005,STR0006}	//"Matricula"###"Nome"###"Centro de Custo"###"Processo"

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define Variaveis Private(Basicas)                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private aInfo   	:= {}
Private aReturn		:= { STR0007, 1,STR0008, 2, 2, 1,"",1 }	//"Zebrado"###"Administra‡„o"
Private cFilaInfo   := ""
Private cPerg   	:= "GPER600"
Private Titulo		:= STR0009		//"IMPRESSAO DE FERIAS VENCIDAS NO PERIODO"

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis Utilizadas na funcao IMPR                          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private aLinha  	:= {}
Private nOrdem
Private nTamanho	:= "M"
Private nLastKey 	:= 0

Private Li	        := 0

Private wCabec1 := ""
Private wCabec2 := ""

Private lAllProc
Private nTamProc    := GetSx3Cache( "RA_PROCES", "X3_TAMANHO" )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis de Controle 			                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
#IFDEF TOP
	Private aSRFFields := SRF->( dbStruct() )
#ENDIF
	
Private cMinProces	:= "ZZZZZZ"
Private cMaxProces	:= ""

Private cCcAnt
Private cFilAnterior
Private cProcesAnt
Private cFim
Private cInicio

Private nTotFunQueb		// Totalizador de Funcionario por Quebra de Ordem
Private nTotFunFil		// Totalizador de Funcionario por Filial
Private nTotFunEmp		// Totalizador de Funcionario por Empresa

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Atualizar o Pergunte do Relatorio                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica as perguntas selecionadas                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Pergunte("GPER600",.F.)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Envia controle para a funcao SETPRINT                        ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
wnrel:="GPER600"           //Nome Default do relatorio em Disco
wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)

If nLastKey = 27
	Return
EndIf

SetDefault(aReturn,cString)

If nLastKey = 27
	Return
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis utilizadas para parametros                         ³
³ mv_par01		// Filial De								   ³
³ mv_par02		// Filial Ate								   ³
³ mv_par03		// Centro de Custo De						   ³
³ mv_par04		// Centro de Custo Ate						   ³
³ mv_par05		// Processo									   ³
³ mv_par06		// Matricula De								   ³
³ mv_par07		// Matricula Ate							   ³
³ mv_par08		// Nome De									   ³
³ mv_par09		// Nome Ate									   ³
³ mv_par10		// Situacoes a Imprimir						   ³
³ mv_par11		// Categorias a Imprimir					   ³
³ mv_par12		// Situacao do Lancamento					   ³
³ mv_par13		// Centro de custo em outra pagina			   ³
³ mv_par14		// Processo em outra pagina 				   ³
³ mv_par15		// Data Inicio de Referencia				   ³
³ mv_par16		// Data Final de Referencia	   				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nOrdem   := aReturn[8]

cFilDe   := mv_par01
cFilAte  := mv_par02
cCcDe    := mv_par03
cCcAte   := mv_par04
cProcesso:= mv_par05
cMatDe   := mv_par06
cMatAte  := mv_par07 
cNomDe   := mv_par08
cNomAte  := mv_par09
cSit     := mv_par10
cCat     := mv_par11
cLanca	 := mv_par12
lSaltCC  := mv_par13 == 1
lSaltProc:= mv_par14 == 1
dDataIni := mv_par15
dDataFim := mv_par16

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Gerar lista de Itens dos Processos.  										  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
lAllProc 	:=Iif(AllTrim( cProcesso ) == "*" .Or. Empty(cProcesso), .T., .F.)
If !lAllProc
	cSvProcesso := cProcesso
	cProcesso 	:= "("
	For nCont:= 1 to Len(Alltrim(cSvProcesso)) Step nTamProc
		cProcesso += "'" + Substr(cSvProcesso, nCont, nTamProc) + "',"
		If Substr(cSvProcesso, nCont, nTamProc) > cMaxProces 
			cMaxProces := Substr(cSvProcesso, nCont, nTamProc)
		EndIf
		If Substr(cSvProcesso, nCont, nTamProc) < cMinProces
			cMinProces := Substr(cSvProcesso, nCont, nTamProc)
		EndIf
	Next nCont
	cProcesso := Substr( cProcesso, 1, Len(cProcesso)-1) + ")"
EndIf

RptStatus({|lEnd| GR600Imp(@lEnd,wnRel,cString)},Capital(Titulo))

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GR600Imp ³ Autor ³ Mauricio Takakura     ³ Data ³ 07.03.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Ferias Vencidas no Mes                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ GR600Imp(lEnd,wnRel,cString)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd        - A‡ao do Codelock                             ³±±
±±³          ³ wnRel       - T¡tulo do relat¢rio                          ³±±
±±³          ³ cString     - Mensagem                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gper600                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GR600Imp(lEnd,WnRel,cString)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define Variaveis Private			                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private aEmp     := {}  		// Empresa
Private aFil     := {}  		// Filial
Private aCc      := {}  		// Centro de Custo
Private aProces  := {}  		// Processos

Private cPDFerias	:= fGetCodFol( "0072" )	// Verba de Ferias
Private cAliasMov   := "SRA"

Titulo += ": " + Dtoc(dDataIni) + STR0018 + Dtoc(dDataFim)

fImpFerias(lEnd)

dbSelectArea("SRA")
DbClearFilter()
dbSetOrder(1)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Termino do relatorio                                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
//--Gerar Rodape no final da Impressao
Li := 58
Impr("","F")
Set Device To Screen
If aReturn[5] = 1
	Set Printer To
	Commit
	ourspool(wnrel)
Endif
MS_FLUSH()

Return(.T.)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpFerias   ³ Autor ³ Mauricio Takakura  ³ Data ³07/03/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Impressao de Ferias Vencidas no Mes                        ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpFerias(lEnd)                              			    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper600  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpFerias(lEnd)
   
Local aMovSRF

Local cChave
Local cItens
Local cAcessaSRA  	:= &("{ || " + ChkRH("GPER600","SRA","2") + "}")

Local nX
Local nItens
Local nReg			:= 0
Local nReg1			:= 0
Local nTotRegPrc	:= 0

#IFDEF TOP 
	Local aSRAFilter	:= {}
	Local aSRAFields

	Local cSRAQuery
	Local cSvSRAQuery
#ENDIF

Private cAntProc
Private nVigVac

Begin Sequence

	dbSelectArea( "SRA" )
	dbGoTop()

	If nOrdem == 1			// Matricula
		dbSetOrder( 1 )
		cChave := cFilDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_MAT"
		cFim     := cFilAte + cMatAte
	ElseIf nOrdem == 2		// Nome
		dbSetOrder( 3 )
		cChave := cFilDe + cNomDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT"
		cFim     := cFilAte + cNomAte + cMatAte
	ElseIf nOrdem == 3		// Centro de Custo
		dbSetOrder( 2 )
		cChave := cFilDe + cCcDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT"
		cFim     := cFilAte + cCcAte + cMatAte
	ElseIf nOrdem == 4		// Processo
		dbSetOrder( RetOrder( "SRA", "RA_FILIAL+RA_PROCES+RA_MAT"))
		If !lAllProc
			cChave   := cFilDe + cMinProces
			cFim     := cFilAte + cMaxProces
		Else
			cChave := cFilDe
			cFim     := cFilAte
		EndIf
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_PROCES"
	EndIf

	#IFDEF TOP                    
		aSRAFields := SRA->( dbStruct() )
		cAliasMov := If(Substr(cAliasMov,1,1) = "Q", Substr(cAliasMov, 2), cAliasMov)
		If Empty( aSRAFilter )
			aAdd( aSRAFilter, { "RA_FILIAL"		, ">="	, cFilDe	, cFilDe 	} )
			aAdd( aSRAFilter, { "RA_FILIAL"		, "<="	, cFilAte	, cFilAte 	} )
			aAdd( aSRAFilter, { "RA_CC"			, ">="	, cCcDe		, cCcDe 	} )
			aAdd( aSRAFilter, { "RA_CC"			, "<="	, cCcAte	, cCcAte 	} )
			aAdd( aSRAFilter, { "RA_MAT"		, ">="	, cMatDe	, cMatDe 	} )
			aAdd( aSRAFilter, { "RA_MAT"		, "<="	, cMatAte	, cMatAte 	} )
			If !lAllProc
				aAdd( aSRAFilter, { "RA_PROCES"	, "IN"	, cProcesso	, cProcesso } )
			EndIf

			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Inserir o filtro para Situacao e Categoria                   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			nItens := Len( cSit )
			cItens  := "(' ',"
			For nX := 1 To nItens
				cItens += "'" + Substr(cSit,nX,1)+"',"
			Next nX
			cItens := Substr( cItens, 1, Len(cItens)-1) + ")"
			aAdd( aSRAFilter, { "RA_SITFOLH"	, "IN"	, cItens	, cItens } )
			
			nItens := Len( cCat )
			cItens  := "("
			For nX := 1 To nItens
				cItens += "'" + Substr(cCat,nX,1)+"',"
			Next nX
			cItens := Substr( cItens, 1, Len(cItens)-1) + ")"
			aAdd( aSRAFilter, { "RA_CATFUNC"	, "IN"	, cItens	, cItens } )

		EndIf
		If !CreateQry( 	aSRAFields	 ,;			// Array com os campos para a Query
						@aSRAFilter	 ,;			// Filtro para query
						cAliasMov	 ,;			// Alias da Query
						@cSRAQuery	 ,;			// String com a Query Montada
						@cSvSRAQuery  ;			// Copia da Query
					 )
			( cAliasMov )->( DbSeek( cChave, .T.) )
		Else
			cAliasMov := "Q" + cAliasMov
		EndIf
	#ENDIF

	cFilAnterior := Space(FWGETTAMFILIAL)
	cCcAnt  	 := Space(09)
	cProcesAnt	 := Space(nTamProc)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis de Totalizacao.                                    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nTotFunQueb		:= 0
	nTotFunFil		:= 0
	nTotFunEmp		:= 0

	nTotRegPrc := If( Upper( cMatAte ) == 'ZZZZZZ' .Or. Val( cMatAte ) == 999999 , SRA->( Reccount() ), If( Val( cMatAte ) - Val( cMatDe ) <= 0, 1, Val( cMatAte ) - Val( cMatDe )) )
	SetRegua(nTotRegPrc)

	While (cAliasMov)->( !EOF() .And. &cInicio <= cFim )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Movimenta Regua de Processamento                             ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IncRegua()

		If lEnd
			@Prow()+1,0 PSAY cCancel
			Exit
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Quebra de Filial                                    ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If (cAliasMov)->RA_FILIAL # cFilAnterior
			If !fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
				fTestaTotal()
				Exit
			EndIf
			cFilaInfo := (cAliasMov)->RA_FILIAL
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste Parametrizacao do Intervalo de Impressao            ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If ( (cAliasMov)->RA_NOME < cNomDe ) .Or. ( (cAliasMov)->RA_NOME > cNomAte ) .Or. ;
			( (cAliasMov)->RA_MAT < cMatDe ) .Or. ( (cAliasMov)->RA_MAT > cMatAte ) .Or. ;
			( (cAliasMov)->RA_CC < cCcDe) .Or. ( (cAliasMov)->RA_CC > cCcAte)
			fTestaTotal()
			Loop
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Situacao e Categoria do Funcionario                 ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !((cAliasMov)->RA_SITFOLH $ cSit) .Or. !((cAliasMov)->RA_CATFUNC $ cCat) .Or. ;
			( !lAllProc .and. !((cAliasMov)->RA_PROCES $ cProcesso) )
			fTestaTotal()
			Loop
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste controle de acessos e filiais validas			   |
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !((cAliasMov)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
			fTestaTotal()
			Loop
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Buscar o Nro de Dias Vigente de Ferias  - Tabela Processos   |
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If cAntProc <> (cAliasMov)->RA_PROCES
			RCJ->( DbSetOrder(1) )	// RCJ_FILIAL+RCJ_CODIGO
			RCJ->( DbSeek( xFilial("RCJ") + (cAliasMov)->RA_PROCES, .F. ) )
			If RCJ->( !Eof() ) .and. cPaisLoc == "MEX"
				nVigVac := RCJ->RCJ_VIGVAC
			Else
				nVigVac := 0
			EndIf
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Buscar lancamentos do Funcionario na Tabela SR8 			   |
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/		
		aMovSRF	:= RetornaMovSRF( 	(cAliasMov)->RA_FILIAL	,; 	// Filial do funcionario corrente
									(cAliasMov)->RA_MAT	  	 ; 	// Matricula do funcionario corrente
								)

		If Empty(aMovSRF)
			fTestaTotal()
			Loop
		EndIf

		nReg1 := Len(aMovSRF)
		For nReg := 1 to nReg1 
			fSoma(@aProces	, aMovSRF, nReg)
			fSoma(@aCc 	 	, aMovSRF, nReg)
			fSoma(@aFil 	, aMovSRF, nReg)
			fSoma(@aEmp 	, aMovSRF, nReg)
		Next nReg

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Imprime Funcionarios                                         ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		fImpFun(@aMovSRF)
		fTestaTotal()
	Enddo

End Sequence

#IFDEF TOP
	( cAliasMov )->( dbCloseArea() )
#ENDIF

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³RetornaMovSRF³ Autor ³ Mauricio Takakura  ³ Data ³07/03/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Retorna os lancamentos de ferias vencidas no periodo       ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³RetornaMovSRF(cFilial, cMatricula)             			    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper600  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function RetornaMovSRF(	cFil	  	 ,;
							   	cMatricula 	  ;
			   			     )

Local aArea			:= GetArea()
Local aAreaSRF
Local aMovSRF		:= {}

Local cAliasSRF		:= "SRF"

Local dDataSRF

#IFDEF TOP
	Local aSRFFilter := {}
	Local cSRFQuery
	Local cSvSRFQuery
#ENDIF

Begin Sequence

	(cAliasSRF)->(DbSetOrder(1)) // RF_FILIAL+RF_MAT+DTOS(RF_DATABAS)+RF_PD

	#IFDEF TOP
		cAliasSRF 	:= If( Substr(cAliasSRF,1,1) = "Q", Substr(cAliasSRF, 2), cAliasSRF)
		If Empty( aSRFFilter )
			aAdd( aSRFFilter, { "RF_FILIAL"	, "="	, cFil		, cFil } )
			aAdd( aSRFFilter, { "RF_MAT"	, "="	, cMatricula, cMatricula } )
			aAdd( aSRFFilter, { "RF_PD"		, "="	, cPDFerias	, cPDFerias } )
		EndIf

		If !CreateQry( 	aSRFFields	 ,;			// Array com os campos para a Query
						@aSRFFilter	 ,;			// Filtro para query
						cAliasSRF	 ,;			// Alias da Query
						@cSRFQuery	 ,;			// String com a Query Montada
						@cSvSRFQuery  ;			// Copia da Query
					 )
			(cAliasSRF)->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT, .T.) )
		Else
			cAliasSRF := "Q" + cAliasSRF
		EndIf
	#ELSE
		(cAliasSRF)->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT, .T.) )
	#ENDIF

	While ( cAliasSRF )->( !Eof() ) .and. ( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT == (cAliasSRF)->RF_FILIAL + (cAliasSRF)->RF_MAT )

        dDataSRF := ( cAliasSRF )->( RF_DATAFIM ) + (nVigVac * nMultVigVa)

		If dDataSRF >= dDataIni .and. dDataSRF <= dDataFim .and. ;
			(( cLanca == 1 .and. ((cAliasSRF)->RF_DFERVAT <> (cAliasSRF)->RF_DFERANT .Or. (cAliasSRF)->RF_DFERVAT = 0)) .Or. (cLanca == 2 .and. (cAliasSRF)->RF_DFERVAT == (cAliasSRF)->RF_DFERANT .and. (cAliasSRF)->RF_DFERVAT > 0 ) .Or. ( cLanca == 3))
			aAdd(aMovSRF, {	(cAliasSRF)->RF_DATABAS		, (cAliasSRF)->RF_DATAFIM	,;
							(cAliasSRF)->RF_DIASDIR		, (cAliasSRF)->RF_DIASANT	,;
							(cAliasSRF)->RF_DFERVAT		, (cAliasSRF)->RF_DFERAAT	,;
							(cAliasSRF)->RF_DFERANT		;
				         })
		EndIf

		(cAliasSRF)->( DbSkip() )
	Enddo

End Sequence

//Ordenar por Sequencia
aSort( aMovSRF,,, { |x,y| Dtos(x[1]) < Dtos(y[1]) } )

#IFDEF TOP
	( cAliasSRF )->( dbCloseArea() )
#ENDIF

RestArea( aArea )

Return ( aMovSRF )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fSoma        ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fSoma(aMatriz,cArq,cCod,nValor,aDadosFunc,nReg)     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fSoma(	aMatriz		,;	// @Array com as opcoes a serem gravadas a totalizacao
						aDadosFunc	,;	// Array com as informacoes a serem processadas
						nReg		 ;	// Posicao do array acima para busca das informacoes.
					  )
Local nRet

DEFAULT aDadosFunc := {}

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Geracao do array com as totalizacoes                         ³ 
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nRet := Len( aMatriz )
If	nRet == 0
	Aadd (aMatriz,{aDadosFunc[nReg,3],aDadosFunc[nReg,4],aDadosFunc[nReg,5],aDadosFunc[nReg,6],aDadosFunc[nReg,7],1})  		// se nao cria elemento
Else
	aMatriz[nRet,1] += aDadosFunc[nReg,3]			// Qtde de Ref. do Periodo
	aMatriz[nRet,2] += aDadosFunc[nReg,4]           // Dias de Antecipacao do Periodo
	aMatriz[nRet,3] += aDadosFunc[nReg,5]			// Dias Vencidos
	aMatriz[nRet,4] += aDadosFunc[nReg,6]			// Dias Proporcionais
	aMatriz[nRet,5] += aDadosFunc[nReg,7]			// Dias Pagos
	aMatriz[nRet,6] ++								// Numero de registros
EndIf

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fTestaTotal  ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Executa Quebras                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fTestaTotal()                                        		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper590  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fTestaTotal()

cFilAnterior 	:= (cAliasMov)->RA_FILIAL
cAntProc		:= (cAliasMov)->RA_PROCES
cProcesAnt		:= (cAliasMov)->RA_PROCES
dbSelectArea( cAliasMov )
(cAliasMov)->(dbSkip())

If (cAliasMov)->(Eof()) .Or. (cAliasMov)->(&cInicio) > cFim
	fImpProc()
	fImpCc()
	fImpFil()
	fImpEmp()
ElseIf cFilAnterior # (cAliasMov)->RA_FILIAL
	fImpProc()
	fImpCc()
	fImpFil()
ElseIf nOrdem == 3 .and. cCcAnt # (cAliasMov)->RA_CC .And. !Eof()
	fImpCc()
ElseIf nOrdem == 4 .and. cProcesAnt # (cAliasMov)->RA_PROCES .And. !Eof()
	fImpProc()
EndIf

dbSelectArea(cAliasMov)

Return(Nil)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpEmp      ³ Autor ³ Mauricio Takakura  ³ Data ³04/11/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Empresa		                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpEmp()										     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpEmp()

If !Empty(aEmp)

	fImprime(aEmp,4)
	aEmp 		:= {}
	nTotFunEmp	:= 0

EndIf                                       

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpFil      ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Filial			                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpFil()										     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpFil()

If !Empty(aFil)

	fImprime(aFil,3)
	aFil 		:= {}
	nTotFunFil	:= 0

EndIf

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpFun      ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime um Funcionario                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpFun(aMovSR8) 			                           		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpFun(aMovSRF)

If Len(aMovSRF) == 0
	Return Nil
EndIf

fImprime(aMovSRF,1)

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpCc       ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Centro de Custo                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpCC()				                            		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpCc()

If Len(aCc) == 0
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Zera variaveis do c.c. anterior						       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aCc := {}

	Return Nil

EndIf
If nOrdem == 3
	fImprime(aCc,2)
EndIf

aCc := {}

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpProc     ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Quebra de Processos                                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpProc()				                            		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpProc()

If Len(aProces) == 0
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Zera variaveis do c.c. anterior						       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aProces := {}

	Return Nil

EndIf
If nOrdem == 4
	fImprime(aProces,5)
EndIf

aProces := {}

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImprime     ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ 						                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImprime()										     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper600  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImprime( aMovSRF	,;		// Array com os movimentos a imprimir
						  nTipo		 ;		// 1-Funcionario; 2-Centro de Custo; 3-Filial; 4-Empresa
 						 )
Local cFunc

Local DET

Local nConta
Local nMaximo

nMaximo:= Len(aMovSRF)
												   				
//Funcionario                                     Data Inicial  Data Final  Qtd Ref  Dias Ant  Dias Venc  Dias Prop  Dias Pagos
//xxxxxx-xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xx/xx/xxxx    xx/xx/xxxx   xxxx,x    xxxx,x     xxxx,x     xxxx,x      xxxx,x
WCabec1 := STR0026+aInfo[3]+Space(20)+If(nTipo#4,STR0026+cFilaInfo+" - "+aInfo[1],Space(26))	//"Empresa: "###" 
WCabec2 := 	Substr(STR0010,1,48) + Space(If(48-Len(STR0010) >= 48, 1, 49-Len(STR0010))) +	; 	// funcionario
  			Substr(STR0011,1,13) + Space(If(13-Len(STR0011) >= 13, 1, 14-Len(STR0011))) +	; 	// Data Inicial
  			Substr(STR0012,1,11) + Space(If(11-Len(STR0012) >= 11, 1, 12-Len(STR0012))) +	; 	// Data Final
  			Substr(STR0013,1,08) + Space(If(08-Len(STR0013) >= 08, 1, 09-Len(STR0013))) +	; 	// Qtd ref
  			Substr(STR0014,1,09) + Space(If(09-Len(STR0014) >= 09, 1, 10-Len(STR0014))) +	; 	// Dias Ant
  			Substr(STR0015,1,10) + Space(If(10-Len(STR0015) >= 10, 1, 11-Len(STR0015))) +	; 	// Dias Venc
  			Substr(STR0016,1,10) + Space(If(10-Len(STR0016) >= 10, 1, 11-Len(STR0016))) +	; 	// Dias Prop
  			Substr(STR0017,1,11) + Space(If(11-Len(STR0017) >= 11, 1, 12-Len(STR0017))) 	 	// Dias Pagos

If nTipo == 1 .and. cFilAnterior <> (cAliasMov)->RA_FILIAL
	IMPR(DET, "P")

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Reimprimir Sub-Cabecalho.                           		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If nOrdem == 3
		cCcAnt := "" // Centro de Custo
	ElseIf nOrdem = 4
		cProcesAnt := ""	// Processo
	EndIf

EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Imprimir o sub-cabecalho.                           		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If nOrdem <> 1 .and. nOrdem <> 2 .and. nTipo == 1 .and. !Empty(aMovSRF)
	DET := ""
	If nOrdem == 3 .and. (cAliasMov)->RA_CC <> cCcAnt // Centro de Custo
		//Filial: + "" + Centro de Custo 
		DET := STR0021 + cFilAnterior + "  " + STR0022 + (cAliasMov)->RA_CC + DescCc((cAliasMov)->RA_CC,cFilAnterior)			//"Filial: "###" C.CUSTO: "
	ElseIf nOrdem == 4 .and. (cAliasMov)->RA_PROCES <> cProcesAnt // Processo
		//Processo
		DET := STR0023 + (cAliasMov)->RA_PROCES + " - " + RCJ->RCJ_DESCRI
	EndIf
	If !Empty(DET)
		IMPR(DET, "C")
		DET := ""
		IMPR(DET, "C")
	EndIf
EndIf

If nTipo == 1
	nTotFunQueb++							// Totalizador de Funcionario por Quebra de Ordem
	nTotFunFil++							// Totalizador de Funcionario por Filial
	nTotFunEmp++							// Totalizador de Funcionario por Empresa
	cFunc   := (cAliasMov)->RA_MAT + "-" + Substr((cAliasMov)->RA_NOME,1,40)
   	DET     := AllTrim(cFunc) + Space(49-Len(AllTrim(cFunc)))	// Nome do Funcionario
	For nConta := 1 TO nMaximo
		If nConta > 1
			DET := Space(49)
		EndIf

		DET 	+= Dtoc(aMovSRF[nConta, 1]) + Space(6)			// Data Inicial
		DET 	+= Dtoc(aMovSRF[nConta, 2]) + Space(2)			// Data Final
		DET 	+= Str(aMovSRF[nConta, 3],6,1) + Space(3)		// Qtd Ref.
		DET 	+= Str(aMovSRF[nConta, 4],6,1) + Space(4)		// Dias Ant
		DET 	+= Str(aMovSRF[nConta, 5],6,1) + Space(5)		// Dias Venc
		DET 	+= Str(aMovSRF[nConta, 6],6,1) + Space(5)		// Dias Prop
		DET 	+= Str(aMovSRF[nConta, 7],6,1)					// Dias Pagos
		Impr(Det, "C")

		cCcAnt 			:= (cAliasMov)->RA_CC
	Next nConta
EndIf

If nTipo # 1
	If !Empty( aMovSRF )
		If nTipo == 2

			// "Total de Funcionarios do C. Custo: "
			DET := Substr(STR0019,1,48) + Str(nTotFunQueb, 4, 0)
			DET += Space(73-Len(DET))
			DET += Str(aMovSRF[1, 1],6,1) + Space(3)									// Qtd Ref. 
			DET += Str(aMovSRF[1, 2],6,1) + Space(4)									// Dias Ant
			DET += Str(aMovSRF[1, 3],6,1) + Space(5)									// Dias Venc
			DET += Str(aMovSRF[1, 4],6,1) + Space(5)									// Dias Prop
			DET += Str(aMovSRF[1, 5],6,1)												// Dias Pagos
	
	    	nTotFunQueb := 0

			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			Impr(DET,"C")
			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			If lSaltCC 
				Impr("","P")
			EndIf
		ElseIf nTipo == 5
			// "Total de Funcionario do Processo: "
			DET := Substr(STR0020,1,48) + Str(nTotFunQueb, 4, 0)
			DET += Space(73-Len(DET))
			DET += Str(aMovSRF[1, 1],6,1) + Space(3)									// Qtd Ref. 
			DET += Str(aMovSRF[1, 2],6,1) + Space(4)									// Dias Ant
			DET += Str(aMovSRF[1, 3],6,1) + Space(5)									// Dias Venc
			DET += Str(aMovSRF[1, 4],6,1) + Space(5)									// Dias Prop
			DET += Str(aMovSRF[1, 5],6,1)												// Dias Pagos
	
	    	nTotFunQueb := 0

			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			Impr(DET,"C")
			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			If lSaltProc
				Impr("","P")
			EndIf			
	  	EndIf
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Se Impressao de Filial ou Empresa, ejetar Pagina    		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If nTipo == 3 .Or. nTipo == 4

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Resumo de Filial e/ou Empresa.                      		   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If nTipo == 3 .Or. nTipo == 4
		
			If nTipo == 3
				// "Total da Filial:" ####
				DET := Substr(STR0024,1,48) + Str(nTotFunFil, 4, 0)
				DET += Space(73-Len(DET))
				DET += Str(aMovSRF[1, 1],6,1) + Space(3)									// Qtd Ref. 
				DET += Str(aMovSRF[1, 2],6,1) + Space(4)									// Dias Ant
				DET += Str(aMovSRF[1, 3],6,1) + Space(5)									// Dias Venc
				DET += Str(aMovSRF[1, 4],6,1) + Space(5)									// Dias Prop
				DET += Str(aMovSRF[1, 5],6,1)												// Dias Pagos
				nTotFunFil := 0
			ElseIf nTipo == 4
				// "Total da Empresa:" ####
				DET := Substr(STR0025,1,48) + Str(nTotFunEmp, 4, 0)
				DET += Space(73-Len(DET))
				DET += Str(aMovSRF[1, 1],6,1) + Space(3)									// Qtd Ref. 
				DET += Str(aMovSRF[1, 2],6,1) + Space(4)									// Dias Ant
				DET += Str(aMovSRF[1, 3],6,1) + Space(5)									// Dias Venc
				DET += Str(aMovSRF[1, 4],6,1) + Space(5)									// Dias Prop
				DET += Str(aMovSRF[1, 5],6,1)												// Dias Pagos
				nTotFunEmp := 0
			EndIf
			Impr("","C")
			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			Impr(DET,"C")
			Impr(__PrtThinLine(), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			Impr("","C")
		EndIf
	EndIf

EndIf

Return Nil

