#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPER027CHI.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER027CHI ³ Autor ³ Jesus Peñaloza           ³ Data ³ 05.09.14   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Reporte de Reliquidacion Chile                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador   ³ Data   ³ BOPS/FNC  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³M.Camargo     ³11/12/15³TUAEX2     ³Se agrega total por Empleado.             ³±±
±±³              ³        ³           ³Se ordenan conceptos alfabéticamente.     ³±±
±±³              ³        ³           ³Se quitan decimales del picture.          ³±±
±±³M.Camargo     ³20/04/16³TVAZK7     ³Se reestructura generacion conc dinamicos ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GPER027CHI()

Local oReport
Local aArea := GetArea()
Private cPergunte := 'GPER027CHI'
Private aConcepto := {}
Private aPeriodos := {}
Private cPeriodos := ''
Private aEmpleados := {}
Private aOrd	  := {OemToAnsi(STR0008)}
Static  cReliq    := TipoReliq()
oReport := ReportDef()
oReport:PrintDialog()
RestArea(aArea)
Return (Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³ ArmaPeriodo³ Autor ³ Jesus Peñaloza        ³ Data ³08/09/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Obtiene los periodos hijo del calculo de reliquidacion       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER027CHI                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ArmaPeriodo()
Local cPeriodos:= '('
Local cQuery   := ''
Local cTempF   := CriaTrab(Nil, .F.)
Pergunte(cPergunte, .F.)
cQuery += "SELECT RCH_PER, RCH_MES, RCH_ANO FROM "+RetSQLName("RCH")+" "
cQuery += "WHERE RCH_FILIAL = '"+xFilial("RCH")+"' "
cQuery += "AND RCH_PROCES = '"+MV_PAR01+"' "
cQuery += "AND RCH_ROTEIR = '"+cReliq+"' "
cQuery += "AND RCH_NUMPAG = '"+MV_PAR04+"' "
cQuery += "AND RCH_PER <> '"+MV_PAR03+"' "
cQuery += "AND RCH_PERREL = '"+MV_PAR03+"' "
cQuery += "AND D_E_L_E_T_ = '' "
cQuery += "ORDER BY RCH_PER ASC "
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cTempF,.T.,.T.)
(cTempF)->(dbGoTop())
While (!(cTempF)->(EOF()))
	cPeriodos += "'"+(cTempF)->RCH_PER+"',"
	(cTempF)->(dbSkip())
EndDo
cPeriodos += "'"+(cTempF)->RCH_PER+"')"
(cTempF)->(dbCloseArea())
Return cPeriodos

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³ListaConceptos³ Autor ³ Jesus Peñaloza        ³ Data ³05/09/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Obtiene los conceptos de los parametros mv_par09 y mv_par10    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER027CHI                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ListaConceptos(cFiltroPD)
Local nX := 0
Local nY := 0
Local nZ := 0
Local nPos := 0
Local aLista := {}
Local aConcep := {}
Local cConc := ''
Local bSeek
Local nTam := TamSX3("RV_COD")[1]
Default cFiltroPD := ""

Pergunte(cPergunte, .F.)
For nX := 1 to len(Alltrim(MV_PAR09))
	If nX % nTam == 0
		cConc := substr(MV_PAR09,nX-2,3)
		bSeek := {|x|x == cConc}
		nPos := aScan(aLista, bSeek)
		If nPos == 0
			aAdd(aLista, cConc)
		EndIf
	EndIf
Next nX
For nX := 1 to len(Alltrim(MV_PAR10))
	If nX % nTam == 0
		cConc := substr(MV_PAR10,nX-2,3)
		bSeek := {|x|x == cConc}
		nPos := aScan(aLista, bSeek)
		If nPos == 0
			aAdd(aLista, cConc)
		EndIf
	EndIf
Next nX

//For nX := 1 to len(aConcepto)
	For nY := 1 to len(aLista)
		//If aConcepto[nX] == aLista[nY]
			aAdd(aConcep, aLista[nY])
			cFiltroPD += "'" + aLista[nY] +"'"

			If nY <> len(aLista)
				cFiltroPD += ","
			EndIF

		//EndIf
	Next nY
//Next nX
aSort(aConcep)
Return aConcep

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³ ReportDef  ³ Autor ³ Jesus Peñaloza        ³ Data ³05/09/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Definicion reporte de reliquidacion                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER027CHI                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local 	oReport
Local cTitulo := STR0001
Local	oSection1
Local 	oSection2
Local 	oSection3
Local 	oSection4
Local oSection5
Local nX := 0

oReport:=TReport():New(cPergunte,OemToAnsi(cTitulo), cPergunte ,{|oReport| PrintReport(oReport)})
oReport:SetPortrait()     // Define a orientacao de pagina do relatorio como retrato.
oReport:nColSpace	:= 1
oReport:nFontBody	:= 6 // Define el tamaño de la fuente
oReport:nLineHeight := 25 // Define a altura da linea
oReport:cFontBody	:= "COURIER NEW"
oReport:Setlandscape(.T.)//Pag Horizontal
oReport:SetTotalInLine(.F.)

oSection1:= TRSection():New(oReport,oemtoansi(STR0012),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"Encabezado"
oSection1:SetHeaderPage(.T.)	//Muestra le titulo de la seccion
oSection1:SetLineStyle(.T.)   //Pone titulo del campo y aun lado el y valor
oSection1:SetCharSeparator("")
oSection1:SetHeaderBreak(.T.)

TRCell():New(oSection1,"PROCESO",,STR0002,,TamSX3("RCJ_CODIGO")[1]+1,.T.,,"LEFT") //Proceso
TRCell():New(oSection1,"DESCRIP",,"",,12,.T.,,"LEFT")
TRCell():New(oSection1,"TIPONOM",,STR0003,,TamSX3("RY_CALCULO")[1]+1,.T.,,"LEFT") //T. Nomina
TRCell():New(oSection1,"DESCRIT",,"",,14,.T.,,"LEFT")
TRCell():New(oSection1,"PERIODO",,STR0004,,TamSX3("RCH_PER")[1]+1,.T.,,"LEFT") //Periodo
TRCell():New(oSection1,"NUMPAGO",,STR0005,,TamSX3("RCH_NUMPAG")[1]+1,.T.,,"LEFT") //Numero de Pago
TRCell():New(oSection1,"FECHINI",,STR0006,,TamSX3("RCH_DTINI")[1]+4,.T.,,"LEFT") //Fecha de Inicio
TRCell():New(oSection1,"FECHFIN",,STR0007,,TamSX3("RCH_DTFIM")[1]+3,.T.,,"LEFT") //Fecha de Fin

oSection2:= TRSection():New(oReport,oemtoansi(STR0013),,aOrd,,) //Conceptos
oSection2:SetTotalInLine(.f.)
oSection2:SetHeaderSection(.f.)	//Muestra le titulo de la seccion
oSection2:SetHeaderPage(.f.)	//Exibe Cabecalho da Secao
//oSection2:SetLineStyle(.T.)   //Pone titulo del campo y aun lado el y valor
oSection2:SetCharSeparator("")
oSection2:SetHeaderBreak(.T.)

TRCell():New(oSection2,"MATRICULA",,"",,12,.T.,,"LEFT")
TRCell():New(oSection2,"NOMBRE",,"",,12,.T.,,"LEFT")

oSection3:= TRSection():New(oReport,oemtoansi(STR0014),,aOrd,,) //Datos Empleado
oSection3:SetTotalInLine(.f.)
oSection3:SetHeaderSection(.f.)	//Muestra le titulo de la seccion
oSection3:SetHeaderPage(.f.)	//Exibe Cabecalho da Secao
//oSection3:SetLineStyle(.T.)   //Pone titulo del campo y aun lado el y valor
oSection3:SetCharSeparator("")
oSection3:SetHeaderBreak(.T.)

TRCell():New(oSection3,"MATRICULA",,STR0008+":",,8,.T.,,"LEFT")
TRCell():New(oSection3,"NOMBRE",,"",,TamSX3("RA_NOME")[1],.T.,,"LEFT")

oSection4:= TRSection():New(oReport,oemtoansi(STR0015),,aOrd,,) //Detalle Conceptos
//oSection4:= TRSection():New(oSection3,oemtoansi(STR0015),,aOrd,,) //Detalle Conceptos
oSection4:SetTotalInLine(.f.)
oSection4:SetHeaderSection(.f.)	//Muestra le titulo de la seccion
oSection4:SetHeaderPage(.f.)	//Exibe Cabecalho da Secao
//oSection4:SetLineStyle(.T.)   //Pone titulo del campo y aun lado el y valor
oSection4:SetCharSeparator(" ")
oSection4:SetHeaderBreak(.T.)

TRCell():New(oSection4,"PERIODO",,"",,10,.T.,,"LEFT")


oSection5:= TRSection():New(oReport,oemtoansi(STR0016),,aOrd,,) //Totales
oSection5:SetTotalInLine(.f.)
oSection5:SetHeaderSection(.f.)	//Muestra le titulo de la seccion
oSection5:SetHeaderPage(.f.)	//Exibe Cabecalho da Secao
//oSection5:SetLineStyle(.T.)   //Pone titulo del campo y aun lado el y valor
oSection5:SetCharSeparator("")
oSection5:SetHeaderBreak(.T.)

TRCell():New(oSection5,"MATRICULA",,"",,10,.T.,,"LEFT")

oSection6:= TRSection():New(oReport,oemtoansi("Conceptos"),,aOrd,,) //Totales
oSection6:SetTotalInLine(.f.)
oSection6:SetHeaderSection(.f.)	//Muestra le titulo de la seccion
oSection6:SetHeaderPage(.f.)	//Exibe Cabecalho da Secao
//oSection6:SetLineStyle(.T.)   //Pone titulo del campo y aun lado el y valor
oSection6:SetCharSeparator("")
oSection6:SetHeaderBreak(.T.)

TRCell():New(oSection6,"PERIODO",,"",,10,.T.,,"LEFT")

OSECTION1:NLINESBEFORE:=0
OSECTION2:NLINESBEFORE:=0
OSECTION3:NLINESBEFORE:=0
OSECTION4:NLINESBEFORE:=0
OSECTION5:NLINESBEFORE:=0
OSECTION6:NLINESBEFORE:=0

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³PrintReport ³ Autor ³ Jesus Peñaloza        ³ Data ³05/09/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Llenado del reporte de Reliquidacion                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ReportDef                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function PrintReport(oReport)

Local oSection1  := oReport:Section(1)
Local oSection2  := oReport:Section(2)
Local oSection3  := oReport:Section(3)
Local oSection4  := oReport:Section(4)
Local oSection5  := oReport:Section(5)
Local oSection6  := oReport:Section(6)
Local nEmp := 0
Local nX := 0
Local nY := 0
Local nTotal := 0
Local nVal := 0
Local nTam := TamSX3("RC_VALOR")[1]
Local nAnchoTot := oReport:PageWidth()
Local nAncho := 0
Local aTotal := {}
Local cTempF     := CriaTrab(Nil, .F.)
Local cFechaFin  := FechaFin()
Local cFechaCierre:=FechaCierre()
Local cFilialSR := xFilial("SRD")
Local cQuery     := ''
Local cMat := ''
Local cPer := ''
Local cPict := "999,999,999"//PesqPict("SRC", "RC_VALOR")
Local lContinua := .T.
Local aTotEmp := {}
Local cFilPD 		:= ""
Local nTotCel		:=0
Local nTamCell 	:= 0
Local nTotCon		:= 0
Local nCont		:= 1
Local nPos		:= 0
Private lAbierto := IIf(Empty(cFechaCierre),.T.,.F.)

Pergunte(oReport:GetParam(),.F.)

cPeriodos := ArmaPeriodo()
If MV_PAR11 == 2
	aConcepto := ListaConceptos(@cFilPD)
EndIf
MakeSqlExpr(oReport:uParam)
cQuery += "SELECT RA_FILIAL, RA_MAT, RA_NOME, RV_DESC, "
If lAbierto
	cQuery += "RC_PD CONC, SUM(RC_VALOR) VALOR, RC_PERIODO PERIODO "
Else
	cQuery += "RD_PD CONC, SUM(RD_VALOR) VALOR, RD_PERIODO PERIODO "
EndIf
cQuery += "FROM "+RetSQLName("SRA")+" SRA, "+RetSQLName("SRV")+" SRV, "
If lAbierto
	cQuery+=RetSQLName("SRC")+" SRC "
Else
	cQuery+=RetSQLName("SRD")+" SRD "
EndIf
cQuery += "WHERE RV_FILIAL = '"+xFilial("SRV")+"'  "

If !Empty(MV_PAR05)
	cQuery += "AND " +MV_PAR05+" "
EndIf

If !Empty(MV_PAR06)
	cQuery += "AND "+MV_PAR06+" "
EndIF

If !Empty(MV_PAR07)
	cQuery += "AND "+MV_PAR07+" "
EndIf

If !Empty(MV_PAR08)
	cQuery += "AND "+MV_PAR08+" "
EndIf

If lAbierto
	cQuery += "AND RC_FILIAL = '"+xFilial("SRC", xFilial("SRA"))+"' "
	cQuery += "AND RC_MAT = RA_MAT "
	cQuery += "AND RC_PROCES = '"+MV_PAR01+"' "
	cQuery += "AND RC_ROTEIR = '"+cReliq+"' "
	cQuery += "AND RV_COD = RC_PD "
	cQuery += "AND RC_PERIODO IN "+cPeriodos
	cQuery += "AND RC_SEMANA IN ('"+MV_PAR04+"') "
	If MV_PAR11 == 2
		cQuery += "AND RC_PD IN (" + cFilPD + ") "
	EndIf
	cQuery += " AND SRC.D_E_L_E_T_ = '' "
Else
	cQuery += "AND RD_FILIAL = '"+xFilial("SRD", xFilial("SRA"))+"' "
	cQuery += "AND RD_MAT = RA_MAT "
	cQuery += "AND RD_PROCES = '"+MV_PAR01+"' "
	cQuery += "AND RD_ROTEIR = '"+cReliq+"' "
	cQuery += "AND RV_COD = RD_PD "
	cQuery += "AND RD_PERIODO IN "+cPeriodos
	cQuery += "AND RD_SEMANA IN ('"+MV_PAR04+"') "
	If MV_PAR11 == 2
		cQuery += "AND RD_PD IN (" + cFilPD + ") "
	EndIf
	cQuery += " AND SRD.D_E_L_E_T_ = '' "
EndIf
cQuery += "AND SRA.D_E_L_E_T_ = '' "
cQuery += "AND SRV.D_E_L_E_T_ = '' "
cQuery += "GROUP BY RA_FILIAL, RA_MAT, RA_CC, RA_DEPTO, RA_NOME, RV_DESC,"
If lAbierto
	cQuery += "RC_PD, RC_PERIODO "
Else
	cQuery += "RD_PD, RD_PERIODO "
EndIf
cQuery += "ORDER BY RA_FILIAL, RA_MAT, RA_CC, RA_DEPTO, "
If lAbierto
	cQuery += "RC_PERIODO, RC_PD ASC"
Else
	cQuery += "RD_PERIODO, RD_PD ASC"
EndIf
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cTempF,.T.,.T.)
count to nTotal
(cTempF)->(dbGoTop())
If MV_PAR11 == 1
	While (!(cTempF)->(EOF()))
		If aScan(aConcepto, {|x|x == (cTempF)->CONC}) == 0
			aAdd(aConcepto, (cTempF)->CONC)
		EndIf
		(cTempF)->(dbSkip())
	EndDo
EndIf
For nX:= 1 to len(aConcepto)
	aAdd(aTotEmp, 0)
	aAdd(aTotal, 0)
Next

nAncho := oSection4:Cell("PERIODO"):GetWidth()

dbSelectArea("SRV")
SRV->(dbSetOrder(1))
For nX := 1 to len(aConcepto)
	SRV->(dbSeek(xFilial("SRV")+aConcepto[nX]))
	If nX == 1
		TRCell():New(oSection6,"CONCEP"+Alltrim(str(nX)),,"",,nTam+2,.T.,,"LEFT")
		oSection6:Cell("CONCEP"+Alltrim(str(nX))):SetValue(SRV->RV_COD+"-"+SRV->RV_DESC)
		TRCell():New(oSection4,"CONCEP"+Alltrim(str(nX)),,"",,nTam+2,.T.,,"RIGHT")
		//nAncho += oSection4:Cell("CONCEP"+Alltrim(str(nX))):GetWidth()
		TRCell():New(oSection5,"CONCEP"+Alltrim(str(nX)),,"",,nTam+2,.T.,,"RIGHT")
	Else
		nTamCel :=  oSection4:Cell("CONCEP"+Alltrim(str(nX-1))):GetWidth()
		nTotCel := Int(nAnchoTot/nTamCel)

		If nX < nTotCel
			TRCell():New(oSection6,"CONCEP"+Alltrim(str(nX)),,"",,nTam+3,.T.,,"LEFT")
			oSection6:Cell("CONCEP"+Alltrim(str(nX))):SetValue(SRV->RV_COD+"-"+SRV->RV_DESC)
			TRCell():New(oSection4,"CONCEP"+Alltrim(str(nX)),,"",,nTam+3,.T.,,"RIGHT")

			TRCell():New(oSection5,"CONCEP"+Alltrim(str(nX)),,"",,nTam+3,.T.,,"RIGHT")
		Else
			nAncho += (oSection4:Cell("CONCEP"+Alltrim(str(nX-1))):GetWidth()	* nTotCel)
			Exit
		EndIf
	EndIF

Next nX

If nAncho > nAnchoTot

	lContinua := MsgYesNo(STR0017, STR0018) //La cantidad de conceptos seleccionados rebasa el ancho de la pagina,selecione menos concepto o la visualización del informe no será uniforme ¿continuar?
EndIf
If !lContinua
	oReport:CancelPrint()
EndIf
oSection1:Init()
oSection1:Cell("PROCESO"):SetValue(MV_PAR01)
oSection1:Cell("DESCRIP"):SetValue(DescriProc())
oSection1:Cell("TIPONOM"):SetValue(MV_PAR02)
oSection1:Cell("DESCRIT"):SetValue(DescriNom())
oSection1:Cell("PERIODO"):SetValue(MV_PAR03)
oSection1:Cell("NUMPAGO"):SetValue(MV_PAR04)
oSection1:Cell("FECHINI"):SetValue(FechaIni())
oSection1:Cell("FECHFIN"):SetValue(cFechaFin)
oSection1:printline()
oReport:Fatline()
oSection1:Finish()

oSection2:Init()
oSection2:Cell("MATRICULA"):SetValue(STR0008) //"Matricula"
oSection2:Cell("NOMBRE"):SetValue(STR0009) //"Nombre"
oSection2:Printline()
//oReport:Fatline()
oSection2:Finish()

oSection6:Init()
oSection6:Cell("PERIODO"):SetValue("  "+STR0010) //"Mes/Año"
oSection6:PrintLine()
oReport:Fatline()
oSection6:Finish()

oSection3:Init()
oReport:SetMeter(nTotal)
(cTempF)->(dbGoTop())
While (!(cTempF)->(EOF()))
	cMat := (cTempF)->RA_MAT
	oSection3:Cell("MATRICULA"):SetValue((cTempF)->RA_MAT)
	oSection3:Cell("NOMBRE"):SetValue((cTempF)->RA_NOME)
	oSection3:PrintLine()
	While (!(cTempF)->(EOF()) .and. (cTempF)->RA_MAT == cMat)
		cPer := (cTempF)->PERIODO
		nCont := 1
		While (!(cTempF)->(EOF()) .and. (cTempF)->RA_MAT == cMat .and. (cTempF)->PERIODO == cPer)
			nCont := 1
				oSection4:Init()
				oSection4:Cell("PERIODO"):SetValue("  "+substr((cTempF)->PERIODO,5,2)+"/"+substr((cTempF)->PERIODO,1,4))
				//For nX := 1 to len(aConcepto)
					//If aConcepto[nX] == (cTempF)->CONC
					nPos := aScan(aConcepto,{|x| x == (cTempF)->CONC })
					If nPos < nTotCel .and. nPos > 0
						oSection4:Cell("CONCEP"+Alltrim(Str(nPos))):SetValue(Alltrim(Transform((cTempF)->VALOR, cPict)))
						aTotal[nPos]:= aTotal[nPos] + (cTempF)->VALOR
						atotEmp[nPos]:= atotEmp[nPos] + (cTempF)->VALOR
					EndIF
					nCont++
					//EndIf
				//Next nX
				(cTempF)->(dbSkip())
				oReport:IncMeter()
		EndDo
		oSection4:Printline()
		For nX := 1 to len(aConcepto)
			If nX < nTotCel
				oSection4:Cell("CONCEP"+Alltrim(Str(nX))):SetValue("")
			Else
				Exit
			EndIF
		Next nX
	EndDo
	oSection4:Cell("PERIODO"):SetValue(STR0019)
	iF len(aTotEmp) > 0
		For nX := 1 to len(aToteMP)
			If nX < nTotCel
				oSection4:Cell("CONCEP"+Alltrim(Str(nX))):SetValue(Alltrim(Transform(atotEmp[nX], cPict)))
			Else
				Exit
			EndIf
		Next nX
	EndIF
	oSection4:Printline()
	aTotEmp := {}
	oSection4:Cell("PERIODO"):SetValue(' ')
	For nX := 1 to len(aConcepto)
		aAdd(aTotEmp,0)
		If nX < nTotCel
			oSection4:Cell("CONCEP"+Alltrim(Str(nX))):SetValue("")
		eLSE
			eXIT
		EndIf
	Next nX

	oSection4:Printline()

EndDo
oSection3:Finish()
oSection4:Finish()
oReport:Fatline()

oSection5:Init()
oSection5:Cell("MATRICULA"):SetValue(STR0011)

For nY := 1 to len(aTotal)
	If nY < nTotCel
		nVal:=aTotal[nY]
		oSection5:Cell("CONCEP"+Alltrim(Str(nY))):SetValue(Alltrim(Transform(nVal, cPict)))
	endIf
Next nY

oSection5:PrintLine()
oSection5:Finish()
(cTempF)->(dbCloseArea())
Return ( Nil )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³ DescriProc ³ Autor ³ Jesus Peñaloza        ³ Data ³08/09/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Obtiene la descripcion del Proceso                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PrintReport                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function DescriProc()
Local cDescri := ''
Pergunte(cPergunte, .F.)
dbSelectArea("RCJ")
RCJ->(dbSetOrder(1))
If RCJ->(dbSeek(xFilial("RCJ")+MV_PAR01))
	cDescri := Alltrim(RCJ->RCJ_DESCRI)
EndIf
Return cDescri

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³ DescriNom  ³ Autor ³ Jesus Peñaloza        ³ Data ³08/09/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Obtiene la descripcion del Tipo de Nomina                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PrintReport                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function DescriNom()
Local cDescri := ''
Pergunte(cPergunte, .F.)
dbSelectArea("SRY")
SRY->(dbSetOrder(1))
If SRY->(dbSeek(xFilial("SRY")+MV_PAR02))
	cDescri := Alltrim(SRY->RY_DESC)
EndIf
Return cDescri

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³ FechaIni   ³ Autor ³ Jesus Peñaloza        ³ Data ³08/09/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Obtiene la fecha de inicio del periodo                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PrintReport                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function FechaIni()
Local cFecha := ''
dbSelectArea("RCH")
RCH->(dbSetOrder(1))
If RCH->(dbSeek(xFilial("RCH")+MV_PAR01+MV_PAR03+MV_PAR04+MV_PAR02))
	cFecha := dtoc(RCH->RCH_DTINI)
EndIf
Return cFecha

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³ FechaFin   ³ Autor ³ Jesus Peñaloza        ³ Data ³08/09/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Obtiene la fecha de fin del periodo                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PrintReport                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function FechaFin()
Local cFecha := ''
Pergunte(cPergunte, .F.)
dbSelectArea("RCH")
RCH->(dbSetOrder(1))
If RCH->(dbSeek(xFilial("RCH")+MV_PAR01+MV_PAR03+MV_PAR04+MV_PAR02))
	cFecha := dtoc(RCH->RCH_DTFIM)
EndIf
Return cFecha

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³ FechaCierre³ Autor ³ Jesus Peñaloza        ³ Data ³08/09/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Obtiene la fecha de cierre del periodo                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PrintReport                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function FechaCierre()
Local cFecha := ''
Pergunte(cPergunte, .F.)
dbSelectArea("RCH")
RCH->(dbSetOrder(1))
If RCH->(dbSeek(xFilial("RCH")+MV_PAR01+MV_PAR03+MV_PAR04+MV_PAR02))
	cFecha := RCH->RCH_DTFECH
EndIf
Return cFecha

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcion   ³ TipoReliq  ³ Autor ³ Jesus Peñaloza        ³ Data ³10/12/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Obtiene el tipo de nomina que cumpla RY_TIPO = 'T'           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PrintReport                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function TipoReliq()
Local cCalculo := ''
dbSelectArea("SRY")
SRY->(dbSetOrder(1))
While (!SRY->(EOF()))
	If SRY->RY_TIPO = 'T'
		cCalculo := SRY->RY_CALCULO
		exit
	EndIf
	SRY->(dbSkip())
EndDo
Return cCalculo
