#INCLUDE "Protheus.ch"
#INCLUDE "GPEM881MEX.CH"

#DEFINE PROM_VID_A 28
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³GPEM881MEX³ Autor ³ Jesus Peñaloza        ³ Data ³ 24/03/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ CALCULO DE PRIMA DE RIESGO DE TRABAJO      (MEXICO)        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPEM881MEX                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/


Function GPEM881MEX

Local aSays			:={ }
Local aButtons		:= { }
Local aGetArea		:= GetArea()
Local nOpca 		:= 0
Local cPerg			:="GPEM881MEX"

Private aCodRpat	:= {}  
Private cCadastro 	:= STR0001  //"Calculo de Prima de Riesgo de Trabajo"
Private cMes:=''
Private cAnio:=''
Private cLisPat:=''
Private cLisReg:=''
Private cLisMat:=''
Private cLisSuc:=''
Private cMats:=''
Private cSucs:=''
Private aList:={}
Private nFactPr:=0
Private nPriMin:=0
Private nTrabPr:=0
Private lHayReg:=.T.
Private aErr := {}
Private dFecIni:=ctod("  /  /  ") //Fecha de inicio del mes que se calcula
Private dFecFin:=ctod("  /  /  ") //Fecha de final del mes que se calcula

dbSelectArea("SRA")  
DbSetOrder(1)

dbSelectArea("RCO")   
DbSetOrder(1)

dbSelectArea("SR8")  
dbSelectArea("RIV") // Incapacidad Parcial Permantente y Muerte
dbSelectArea("RIU") //Caratula de la Determinación
dbSelectArea("RIT") //Casos  Riesgo
dbSelectArea("RS2") //Detalle de Casos de Riesgo

AADD(aSays,OemToAnsi(STR0002))//Esta rutina ejecuta los calculos necesarios para generar los Casos de Riesgo
AADD(aSays,OemToAnsi(STR0003))//de Trabajo, asi como el calculo de la prima. Tomando como base el ausentismo
AADD(aSays,OemToAnsi(STR0004))//registrado en la opcion de Asientos
AADD(aSays,OemToAnsi(STR0005))//Considere tener a la mano el numero de Trabajadores promedio expuestos 
AADD(aSays,OemToAnsi(STR0006))//al riesgo, lo puedeobtener del informe 'Reporte de días cotizados y
AADD(aSays,OemToAnsi(STR0007))//"trabajadores promedio expuestos al riesgo'."
AADD(aSays,OemToAnsi(STR0008))//Después de este proceso, podrá generar los informes de:
AADD(aSays,OemToAnsi(STR0009))//Riesgo de Trabajo, Relaciones de Casos de Riesgo, la Caratula de Determinacion
AADD(aSays,OemToAnsi(STR0010))//y el archivo de Determinacion de la prima.

AADD(aButtons, {5,.T.,{|| Pergunte(cPerg,.T.)}})
AADD(aButtons, {1,.T.,{|o| nOpca := 1,IF(TodoOK(cPerg),FechaBatch(),nOpca:=0)}})
AADD(aButtons, {2,.T.,{|o| FechaBatch()}})

oDlg01:=MSDialog():New(000,000,270,460,STR0001,,,,,,,,,.T.)
@005,015 To 110,215 prompt "" Pixel Of oDlg01//Generacion de Archivo TXT
oSay01 := tSay():New(010,017,{||aSays[1]},oDlg01,,,,,,.T.,,,500,10)
oSay02 := tSay():New(018,017,{||aSays[2]},oDlg01,,,,,,.T.,,,500,10)
oSay03 := tSay():New(026,017,{||aSays[3]},oDlg01,,,,,,.T.,,,500,10)
oSay04 := tSay():New(034,017,{||""},oDlg01,,,,,,.T.,,,500,10)
oSay05 := tSay():New(042,017,{||aSays[4]},oDlg01,,,,,,.T.,,,500,10)
oSay06 := tSay():New(050,017,{||aSays[5]},oDlg01,,,,,,.T.,,,500,10)
oSay07 := tSay():New(058,017,{||aSays[6]},oDlg01,,,,,,.T.,,,500,10)
oSay08 := tSay():New(066,017,{||""},oDlg01,,,,,,.T.,,,500,10)
oSay09 := tSay():New(074,017,{||aSays[7]},oDlg01,,,,,,.T.,,,500,10)
oSay10 := tSay():New(082,017,{||""},oDlg01,,,,,,.T.,,,500,10)
oSay11 := tSay():New(090,017,{||aSays[8]},oDlg01,,,,,,.T.,,,500,10)
oSay12 := tSay():New(098,017,{||aSays[9]},oDlg01,,,,,,.T.,,,500,10)
oBtn01:=sButton():New(115,115,5,{|| Pergunte(cPerg,.T.)},oDlg01,.T.,,)
oBtn02:=sButton():New(115,150,1,{|o| nOpca := 1,IF(TodoOK(cPerg),oDlg01:End(),nOpca:=0)},oDlg01,.T.,,)
oBtn03:=sButton():New(115,185,2,{|o| oDlg01:End()},oDlg01,.T.,,)
oDlg01:Activate(,,,.T.,,,)

If nOpca == 1 //Ejecuta el proceso
	Processa({|| GPM881GERA() },OemToAnsi(STR0011)) //"Procesando..." 
Endif

RestArea(aGetArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³TodoOK    ºAutor  ³Microsiga           º Data ³  24/03/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Validacion de los datos antes de Ejecutar el proceso        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM881MEX                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function TodoOK(cPerg)
Local nCont:=0                                     
Local nTamReg:= TamSX3("RCO_CODIGO")[1]

Pergunte(cPerg,.F.)

cAnio	:= ALLTRIM(STR(MV_PAR01))
cLisReg	:= MV_PAR02
nFactPr:=MV_PAR03
nPriMin:=MV_PAR04
nTrabPr:=MV_PAR05

If Val(cAnio) < 1900
	Msginfo(STR0012)//"El año debe ser mayor a 1900!"
	Return .f.
Endif	             

If Empty(cLisReg)
	Msginfo(STR0013)//"Debe seleccionar al menos un registro patronal!"
	Return .f.
Endif	             

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Genera lista de registros patronales para usar despues en Query³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cLisPat:=""

For nCont := 1 To Len( cLisReg ) Step nTamReg       
	IF EMPTY(SubStr( cLisReg , nCont , nTamReg ))
	   EXIT
	ENDIF   
    cLisPat+="'"+SubStr( cLisReg , nCont , nTamReg )+"',"
    aAdd(aList, SubStr( cLisReg , nCont , nTamReg ))
Next       

cLisPat:=substr(cLisPat,1,len(cLisPat)-1)                                   
dFecIni := Ctod("01/01/" +cAnio+"/")
dFecFin := Ctod("31/12/"+cAnio+"/")

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GPM881GERA³ Autor ³ Gpe Santacruz         ³ Data ³24/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo del Riesgo de Trabajo                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPM881GERA()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Ninguno                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPEM881MEX                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function GPM881GERA()

Local cQuery	:= ''
Local cAliasSR8	:= criatrab(nil,.f.)
Local nMax		:=0
Local cLlave:=''
Local nCont := 0
Local cItem := ''
Local lHayInic:= .T.
/*
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Selecciona Incapacidades
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
BorraCasos()
IncProc(STR0014) //"Seleccionando Incapacidades..."
ProcRegua(0) // Inicio de barra de avance

cQuery := "SELECT COUNT(R8_MAT) AS TOTAL"
cQuery += " FROM "+initsqlname("SRA") + " SRA, "+ initsqlname("SR8") + " SR8 "
cQuery += " WHERE RA_MAT=R8_MAT AND RA_FILIAL = R8_FILIAL  "
cQuery += " AND (R8_DATAINI BETWEEN  '"+DTOS(dFecini)+"' AND '"+DTOS(dFecFin)+"' or R8_DATAFIM BETWEEN  '"+DTOS(dFecini)+"' AND '"+DTOS(dFecFin)+"'  "
cQuery += " OR R8_DATAINI <  '"+DTOS(dFecini)+"' AND R8_DATAFIM >'"+DTOS(dFecini)+"')  "
cQuery += " AND R8_CODRPAT IN ("+CLISPAT+") AND R8_TIPORSC  IN ('1','2','3') "
cQuery += " AND SR8.D_E_L_E_T_ = ' ' AND  SRA.D_E_L_E_T_ = ' '"
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSR8,.T.,.T.)
(cAliasSR8)->(dbGoTop())
If (cAliasSR8)->TOTAL == 0
	lHayReg := .F.
EndIf
(cAliasSR8)->(dbclosearea())
If lHayReg
	cQuery := "SELECT R8_FILIAL,R8_CODRPAT,R8_MAT,	R8_NCERINC,	R8_DATAINI,	R8_DURACAO,	R8_DATAFIM,	R8_CONTINU,	R8_PRORSC,	R8_TIPORSC,	R8_RESINC,	R8_CONTINC "
	cQuery += " FROM "+initsqlname("SRA") + " SRA, "+ initsqlname("SR8") + " SR8 "
	cQuery += " WHERE RA_MAT=R8_MAT AND RA_FILIAL = R8_FILIAL  "
	cQuery += " AND (R8_DATAINI BETWEEN  '"+DTOS(dFecini)+"' AND '"+DTOS(dFecFin)+"' or R8_DATAFIM BETWEEN  '"+DTOS(dFecini)+"' AND '"+DTOS(dFecFin)+"'  "
	cQuery += " OR R8_DATAINI <  '"+DTOS(dFecini)+"' AND R8_DATAFIM >'"+DTOS(dFecini)+"')  " //Si es una ausencia que inicia antes del mes seleccionado y termina despues del mes seleccionado
	cQuery += " AND R8_CODRPAT IN ("+CLISPAT+") AND R8_TIPORSC  IN ('1','2','3') "
	cQuery += " AND SR8.D_E_L_E_T_ = ' ' AND  SRA.D_E_L_E_T_ = ' '"
	cQuery += " order by   R8_FILIAL,R8_CODRPAT, R8_MAT, R8_DATAINI, R8_CONTINC  "
	
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSR8,.T.,.T.)
	TCSetField(cAliasSR8,"R8_DATAINI","D")
	TCSetField(cAliasSR8,"R8_DATAFIM","D")
	
	COUNT TO nMax
	
	ProcRegua(nMax) // Número de registros a procesar
	
	(cAliasSR8)->(dbgotop())
	nMax:=0
	
	Do While !(cAliasSR8)->(Eof())
		
		//Analiza el ausentismo de cada empleado
		aMovs:={}
		cLlave:=(cAliasSR8)->R8_FILIAL+(cAliasSR8)->R8_CODRPAT+(cAliasSR8)->R8_MAT
		nDias:=0
		dFecAcc:=ctod("  /  /  ")
		cTipo:=''
		nCont := 0
		lHayInic := .T.
		Do While !(cAliasSR8)->(Eof()) .AND. cLlave==(cAliasSR8)->R8_FILIAL+(cAliasSR8)->R8_CODRPAT+(cAliasSR8)->R8_MAT
			cTipo:=(cAliasSR8)->r8_continc
			cRsc:=(cAliasSR8)->R8_TIPORSC
			dFecTer:=(cAliasSR8)->r8_datafim
			If (cAliasSR8)->R8_RESINC =='5' .and. (cAliasSR8)->r8_continc<> '4' .and. (cAliasSR8)->r8_continc<> '2' .and. (cAliasSR8)->r8_continc<> '3' //recaida y no esta terminada y no es inicial
				nCont++
				cItem := padl(nCont,6,'0')
				DetailGQ(@aMovs,(cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,(cAliasSR8)->r8_ncerinc,(cAliasSR8)->r8_dataini,(cAliasSR8)->r8_datafim,(cAliasSR8)->r8_continu,(cAliasSR8)->r8_prorsc,(cAliasSR8)->r8_tiporsc,'5',(cAliasSR8)->r8_continc)
		   		GeraCaso((cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,(cAliasSR8)->r8_dataini,"*",(cAliasSR8)->R8_TIPORSC,'5',0,"",(cAliasSR8)->R8_DATAFIM,"2",'u',aMovs)
			else
				Do Case
					Case(cAliasSR8)->r8_continc=='1'  // Unica'
						nCont++
						cItem := padl(nCont,6,'0')
						DetailGQ(@aMovs,(cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,(cAliasSR8)->r8_ncerinc,(cAliasSR8)->r8_dataini,(cAliasSR8)->r8_datafim,(cAliasSR8)->r8_continu,(cAliasSR8)->r8_prorsc,(cAliasSR8)->r8_tiporsc,'1','1')
						GeraCaso((cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,(cAliasSR8)->r8_dataini,"",(cAliasSR8)->R8_TIPORSC,'1',0,"",(cAliasSR8)->R8_DATAFIM,"1",'1',aMovs)
					Case (cAliasSR8)->r8_continc=='2'   //Inicial
						cMat := (cAliasSR8)->r8_mat
						cRec := (cAliasSR8)->r8_RESINC
						nDias:=(cAliasSR8)->R8_DURACAO
						dFecAcc:=(cAliasSR8)->R8_DATAINI
						cPro :=(cAliasSR8)->r8_prorsc
						dFecAl := (cAliasSR8)->R8_DATAFIM
					Case (cAliasSR8)->r8_continc=='3' //Subsiguiente
						If nDias==0//Subsiguiente El inicio del caso comenzo el año anterior al del proceso
							BuscaInicial((cAliasSR8)->r8_filial, (cAliasSR8)->r8_mat, @dFecAcc,@nDias,@lHayInic) // busca la incapacidad inicial y obtiene fecha de inicio y  dias
						EndIf
						If lHayInic == .F.
							aAdd(aErr, (cAliasSR8)->r8_mat)
						EndIf
						cFil := (cAliasSR8)->r8_filial
						cReg := (cAliasSR8)->r8_codrpat
						cRec := (cAliasSR8)->r8_RESINC
						cMat := (cAliasSR8)->r8_mat
						ndias+=(cAliasSR8)->R8_duracao
						cNcer:= (cAliasSR8)->r8_ncerinc
						cContinu:=(cAliasSR8)->r8_continu
						cTipr:=(cAliasSR8)->r8_tiporsc
						cPro :=(cAliasSR8)->r8_prorsc
						dFecAl := (cAliasSR8)->R8_DATAFIM
					Case (cAliasSR8)->r8_continc=='4'
						If nDias==0
							BuscaInicial((cAliasSR8)->r8_filial, (cAliasSR8)->r8_mat, @dFecAcc,@nDias)
						EndIf
						if (cAliasSR8)->R8_RESINC =='5' //recaida
							ndias+=(cAliasSR8)->R8_duracao
							nCont++
							cItem := padl(nCont,6,'0')
							DetailGQ(@aMovs,(cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,(cAliasSR8)->r8_ncerinc,IF (!EMPTY(dFecAcc),dFecAcc,(cAliasSR8)->r8_dataini),(cAliasSR8)->r8_datafim,(cAliasSR8)->r8_continu,(cAliasSR8)->r8_prorsc,(cAliasSR8)->r8_tiporsc,'5','4')
							GeraCaso((cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,IF (!EMPTY(dFecAcc),dFecAcc,(cAliasSR8)->r8_dataini),  "*",(cAliasSR8)->R8_TIPORSC,'5',0,"",(cAliasSR8)->R8_DATAFIM,"1",'4',aMovs)
						else
							ndias+=(cAliasSR8)->R8_duracao
							nCont++
							cItem := padl(nCont,6,'0')
							DetailGQ(@aMovs,(cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,(cAliasSR8)->r8_ncerinc,IF (!EMPTY(dFecAcc),dFecAcc,(cAliasSR8)->r8_dataini),(cAliasSR8)->r8_datafim,(cAliasSR8)->r8_continu,(cAliasSR8)->r8_prorsc,(cAliasSR8)->r8_tiporsc,'1','4')
							GeraCaso((cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,IF (!EMPTY(dFecAcc),dFecAcc,(cAliasSR8)->r8_dataini),  "",(cAliasSR8)->R8_TIPORSC,'1',0,"",(cAliasSR8)->R8_DATAFIM,"1",'4',aMovs)
						endif
						nDias:=0
						dFecAcc:=ctod("  /  /  ")
					Case (cAliasSR8)->r8_continc=='5'
						nPor:=BuscaIPP((cAliasSR8)->R8_FILIAL, (cAliasSR8)->R8_MAT, (cAliasSR8)->R8_NCERINC)
						ndias+=(cAliasSR8)->R8_duracao
						nCont++
						cItem := padl(nCont,6,'0')
						DetailGQ(@aMovs,(cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,(cAliasSR8)->r8_ncerinc,IF (!EMPTY(dFecAcc),dFecAcc,(cAliasSR8)->r8_dataini),(cAliasSR8)->r8_datafim,(cAliasSR8)->r8_continu,(cAliasSR8)->r8_prorsc,(cAliasSR8)->r8_tiporsc,'2','5')
						GeraCaso((cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,IF (!EMPTY(dFecAcc),dFecAcc,(cAliasSR8)->r8_dataini),  "",(cAliasSR8)->R8_TIPORSC,'2',nPor,"",(cAliasSR8)->R8_DATAFIM,"1",'5',aMovs)
						nDias:=0
						dFecAcc:=ctod("  /  /  ")
					Case (cAliasSR8)->r8_continc=='6' //defunsion
						If nDias==0
							BuscaInicial((cAliasSR8)->r8_filial, (cAliasSR8)->r8_mat, @dFecAcc,@nDias)
						EndIf
						ndias+=(cAliasSR8)->R8_duracao
						nCont++
						cItem := padl(nCont,6,'0')
						DetailGQ(@aMovs,(cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,(cAliasSR8)->r8_ncerinc,IF (!EMPTY(dFecAcc),dFecAcc,(cAliasSR8)->r8_dataini),(cAliasSR8)->r8_datafim,(cAliasSR8)->r8_continu,(cAliasSR8)->r8_prorsc,(cAliasSR8)->r8_tiporsc,'4','6')
						GeraCaso((cAliasSR8)->r8_filial,(cAliasSR8)->r8_codrpat,cAnio,(cAliasSR8)->r8_mat,cItem,IF (!EMPTY(dFecAcc),dFecAcc,(cAliasSR8)->r8_dataini),  "",(cAliasSR8)->R8_TIPORSC,'4',0,"D",(cAliasSR8)->R8_DATAFIM,"1",'6',aMovs,(cAliasSR8)->r8_ncerinc)
				EndCase
			EndIf
			nMax++                                                                            
			IncProc(STR0015) //"Generando Incapacidades..."
			(cAliasSR8)->(dbSkip())
		EndDo
	    If cTipo=='3' .and. lHayInic //subsiguiente
	    	nCont++
	    	cItem := padl(nCont,6,'0')
	    	DetailGQ(@aMovs,cFil,cReg,cAnio,cMat,cItem,cNcer,dFecAcc,dFecAl,cContinu,cPro,cTipr,'1','3')
	    	If cRec == '5'
	    		GeraCaso(cFil,cReg,cAnio,cMat,cItem,dFecAcc,"*",cRsc,"1",0,"",dFecAl,"2",'3',aMovs)   //pendiente de concluir
	    	Else
	    		GeraCaso(cFil,cReg,cAnio,cMat,cItem,dFecAcc,"",cRsc,"1",0,"",dFecAl,"2",'3',aMovs)   //pendiente de concluir
	    	EndIf
		ElseIf cTipo == '2'
			nCont++
			cItem := padl(nCont,6,'0')
			DetailGQ(@aMovs,cFil,cReg,cAnio,cMat,cItem,cNcer,dFecAcc,dFecAl,cContinu,cPro,cTipr,'1','2')
			If cRec == '5'
				GeraCaso(cFil,cReg,cAnio,cMat,cItem,dFecAcc,"*",cRsc,"1",0,"",dFecAl,"2",'2',aMovs)   //pendiente de concluir
			Else
				GeraCaso(cFil,cReg,cAnio,cMat,cItem,dFecAcc,"",cRsc,"1",0,"",dFecAl,"2",'2',aMovs)   //pendiente de concluir
			EndIf
		EndIf
	EndDo
	(cAliasSR8)->(dbclosearea())
	GenPrima(xFilial("RIU"),aList,cAnio) //Genera la prima de riesgo (tabla RIU9)
Else
	Aviso(STR0016, STR0019, {STR0018})
EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GeraCaso  ³ Autor ³ Jesus Peñaloza        ³ Data ³24/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Graba el caso de riesgo de trabajo                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GeraCaso(cExp1,cExp2,cExp3,cExp4,cExp5,dExp6,cExp7,cExp8   ³±±
±±³          ³          cExp9,nExp10,cExp11,dExp12,cExp13,cExp14,aExp15)  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³  											              ³±±
±±³          ³ cExp1.- Filial                                             ³±±
±±³          ³ cExp2.- Registro patronal                                  ³±±
±±³          ³ cExp3.- Año de proceso                                     ³±±
±±³          ³ cExp4.- Matricula                                          ³±±
±±³          ³ cExp5.- Consecutivo por matricula                          ³±±
±±³          ³ dExp6.- Fecha de accidente                                 ³±±
±±³          ³ cExp7.- Indica si es una recaida (*)                       ³±±
±±³          ³ cExp8.- Tipo de riesgo                                     ³±±
±±³          ³ cExp9.- Resultado de la incapacidad                        ³±±
±±³          ³ nExp10.-Porcentaje de Incapacidad Parcial Permanente       ³±±
±±³          ³ cExp11.-Indica si es una defuncion(D)                      ³±±
±±³          ³ dExp12.-Fecha de alta                                      ³±±
±±³          ³ cExp13.-Observaciones (2.-Caso pendiente 1.-Caso terminado)³±±
±±³          ³ cExp14.-Tipo de caso, si es 2 o 3 es un caso pendiente     ³±±
±±³          ³ aExp15.-Arreglo para ingresar el detalle del caso de riesgo³±±
±±³          ³ cExp16.-Numero de incapacidad                              ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPM881GERA                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function GeraCaso(cFil,cRegp,cAnio,cMat,cCon,dFecAcc,cRec,cTipRie,cConsec,nPorIPP,cDefun,dFecter,cObs,cTip,aMovs,cNcerin)
//Guarda en RIT
//Guarda en RS2 el contenido de aMovs
dbSelectArea("RIT")

RecLock("RIT", .T.)
RIT->RIT_FILIAL := cFil
RIT->RIT_CODRPAT:= cRegp
RIT->RIT_ANIO   := cAnio
RIT->RIT_MAT    := cMat
RIT->RIT_ITEM   := cCon
RIT->RIT_REC    := cRec
RIT->RIT_FECACC := dFecAcc
RIT->RIT_TIPRIE := cTipRie
RIT->RIT_CONSEC := cConsec
If Alltrim(cDefun) == 'D' .and. Len(Alltrim(cNcerin)) == 0
	RIT->RIT_DIAS := 0
Else
	RIT->RIT_DIAS := dFecter-dFecAcc+1
EndIf
RIT->RIT_PORINC := nPorIPP
RIT->RIT_DEFUNC := cDefun
If cTip != '2' .and. cTip != '3'
	RIT->RIT_FECALT := dFecter
EndIf
RIT->RIT_OBSERV := cObs
MsUnlock()

dbSelectArea("RS2")
RecLock("RS2", .T.)
RS2_FILIAL := aMovs[1]
RS2_CODRPA := aMovs[2]
RS2_ANIO   := aMovs[3]
RS2_MAT    := aMovs[4]
RS2_ITEM   := aMovs[5]
RS2_NCERIN := aMovs[6]
RS2_DATAIN := aMovs[7]
RS2_DURACA := dFecter-dFecAcc+1
If cTip != '2' .and. cTip != '3'
	RS2_DATAFI := aMovs[9]
EndIf
RS2_CONTIN := aMovs[10]
RS2_PRORSC := aMovs[11]
RS2_TIPORS := aMovs[12]
RS2_RESINC := aMovs[13]
RS2_CONINC := aMovs[14]
MsUnlock()
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BorraCasos³ Autor ³ Jesus Peñaloza        ³ Data ³25/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Borra los casos generados anteriormente, de acuerdo al     ³±±
±±³          ³ año y registro patronal seleccionado                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ BorraCasos()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPM881GERA                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function BorraCasos()
Local nX := 0
Local cFilRit := xFilial("RIT")
Local cFilRs2 := xFilial("RS2")
dbSelectArea("RIT")
RIT->(dbSetOrder(1))
For nX := 1 to Len(aList)
	RIT->(dbSeek(xFilial("RIT")+(aList[nX])+cAnio))
	While (!RIT->(Eof()) .and. RIT->RIT_FILIAL == cFilRit .and. RIT->RIT_CODRPA == aList[nX] .and. RIT->RIT_ANIO == cAnio)
		RecLock("RIT", .F.)
		RIT->(dbDelete())
		MsUnlock()
		RIT->(dbSkip())
	EndDo
Next nX

dbSelectArea("RS2")
RS2->(dbSetOrder(1))
For nX := 1 to Len(aList)
	RS2->(dbSeek(xFilial("RS2")+(aList[nX])+cAnio))
	While (!RS2->(Eof()) .and. RS2->RS2_FILIAL == cFilRs2 .and. RS2->RS2_CODRPA == aList[nX] .and. RS2->RS2_ANIO == cAnio)
		RecLock("RS2", .F.)
		RS2->(dbDelete())
		MsUnlock()
		RS2->(dbSkip())
	EndDo
Next nX
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BuscaInicial³ Autor ³ Jesus Peñaloza      ³ Data ³25/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Busca la fecha inicial del accidente, en caso de que este  ³±±
±±³          ³ se haya dado el año pasado                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ BuscaInicial(cExp1, cExp2, dExp3, nExp4)                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³  											              ³±±
±±³          ³ cExp1.- Filial                                             ³±±
±±³          ³ cExp2.- Matricula                                          ³±±
±±³          ³ dExp3.- Fecha de inicio del accidente                      ³±±
±±³          ³ nExp4.- Duracion desde la fecha del accidente hasta la     ³±±
±±³          ³         fecha final                                        ³±±
±±³          ³ lExp5.- Indica si se encuentra una incapacidad inicial     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPM881GERA                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function BuscaInicial(cFil, cMat, dFec, nDias, lHayIn)
Local cQuery := "SELECT R8_DATAINI, R8_DURACAO, R8_CONTINC"
Local cFecIn := AllTrim(STR(Val(cAnio)-1))+"0101"
Local cFecFi := AllTrim(STR(Val(cAnio)-1))+"1231"
lHayIn := .F.
cQuery += " FROM "+RetSqlName("SR8")+" SR8 WHERE R8_FILIAL = '"+cFil+"' AND R8_MAT = '"+cMat+"'"
cQuery += " AND (R8_DATAINI BETWEEN '"+cFecIn+"' AND '"+cFecFi+"' OR R8_DATAFIM BETWEEN '"+cFecIn+"' AND '"+cFecFi+"' "
cQuery += " OR R8_DATAINI < '"+cFecIn+"' AND R8_DATAFIM < '"+cFecIn+"') "
cQuery += " AND D_E_L_E_T_ = ' ' ORDER BY R8_DATAINI DESC"
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"cSR8",.T.,.T.)
TCSetField("cSR8","R8_DATAINI","D")
cSR8->(dbgotop())
While !cSR8->(Eof())
	If cSR8->(R8_CONTINC) == '2'
		dFec := cSR8->(R8_DATAINI)
		lHayIn := .T.
		exit
	EndIf
	nDias += cSR8->(R8_DURACAO)
	cSR8->(dbSkip())
EndDo
cSR8->(dbclosearea())
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BuscaIPP    ³ Autor ³ Jesus Peñaloza      ³ Data ³25/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Busca el porcentaje relacionado a la IPP, en caso de haber ³±±
±±³          ³ IPPs reevaluadas, retorna dicho porcentaje                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ BuscaIPP(cExp1, cExp2, cExp3)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³  											              ³±±
±±³          ³ cExp1.- Filial                                             ³±±
±±³          ³ cExp2.- Matricula del empleado a buscar                    ³±±
±±³          ³ cExp3.- Numero de Incapacidad a buscar                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPM881GERA                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function BuscaIPP(cFil, cMat, cNcer)
Local nPorc := 0
dbSelectArea("RIV")
RIV->(dbSetOrder(1))
If RIV->(dbSeek(cFil+cMat+cNcer))
	nPorc := RIV->RIV_PROCEN
	While !RIV->(EOF()) .and. RIV->RIV_FILIAL == cFil .and. RIV->RIV_MAT == cMat .and. RIV->RIV_NCERIN == cNcer
		If RIV->RIV_RESINC == '7' .OR. RIV->RIV_RESINC == '9'
			nPorc := RIV->RIV_PROCEN
		EndIf
		RIV->(dbSkip())
	EndDo
EndIf
Return nPorc

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GenPrima    ³ Autor ³                     ³ Data ³25/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Genera la prima de riesgo resultante del ejercicio          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GenPrima(cExp1, aExp2, cExp3)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³  											              ³±±
±±³          ³ cExp1.- Filial                                             ³±±
±±³          ³ aExp2.- Lista de registros patronales indicados            ³±±
±±³          ³ cExp3.- Año que se esta procesando                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPM881GERA                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GenPrima(cFil,aList,cAnio)
Local nX := 0
Local nSumaDias := 0
Local nSumaPorc := 0
Local nTotalDef := 0
Local nPriRes   := 0
Local nPriAnt   := 0
Local nPriNue   := 0
Local cObserv   := STR0017+CHR(13)+CHR(10)

dbSelectArea("RIU")
RIU->(dbSetOrder(1))
For nX := 1 to Len(aList)
	nSumaDias := SumaDiasInc(cFil,aList[nX])
	nSumaPorc := SumaPorcInc(cFil,aList[nX])
	nTotalDef := TotalDefunc(cFil,aList[nX])
	nPriRes := (((nSumaDias/365)+PROM_VID_A*(nSumaPorc+nTotalDef))*(nFactPr/nTrabPr)+nPriMin)*100
	dbSelectArea("RCO")
	RCO->(dbSetOrder(1))
	If RCO->(dbSeek(xFilial("RCO")+aList[nX]))
		nPriAnt := RCO->RCO_FATRSC
	EndIf
	If RIU->(dbSeek(cFil+aList[nX]+cAnio))
		If nPriRes > nPriAnt*1.01
			nPriNue := nPriAnt*1.01
		ElseIf nPriRes < nPriAnt-(nPriAnt*.01)
			nPriNue := nPriAnt-(nPriAnt*.01)
		Else
			nPriNue := nPriRes
		EndIf
		RecLock("RIU", .F.) //Actualizacion de Prima de riesgo existente
		RIU->RIU_FACTOR := nFactPr
		RIU->RIU_PRIMIN := nPriMin
		RIU->RIU_PRIANT := nPriAnt
		RIU->RIU_S      := nSumaDias
		RIU->RIU_I      := nSumaPorc
		RIU->RIU_D      := nTotalDef
		RIU->RIU_N      := nTrabPr
		RIU->RIU_V      := PROM_VID_A
		RIU->RIU_PRIRES := nPriRes
		RIU->RIU_PRINVA := nPriNue
		MsUnlock()
	Else
		If nPriRes > nPriAnt*1.01
			nPriNue := nPriAnt*1.01
		ElseIf nPriRes < nPriAnt-(nPriAnt*.01)
			nPriNue := nPriAnt-(nPriAnt*.01)
		Else
			nPriNue := nPriRes
		EndIf
		RecLock("RIU", .T.) //Nueva prima de riesgo
		RIU->RIU_FILIAL := cFil
		RIU->RIU_CODRPA := aList[nX]
		RIU->RIU_ANIO   := cAnio
		RIU->RIU_FACTOR := nFactPr
		RIU->RIU_PRIMIN := nPriMin
		RIU->RIU_PRIANT := nPriAnt
		RIU->RIU_S      := nSumaDias
		RIU->RIU_I      := nSumaPorc
		RIU->RIU_D      := nTotalDef
		RIU->RIU_N      := nTrabPr
		RIU->RIU_V      := PROM_VID_A
		RIU->RIU_PRIRES := nPriRes
		RIU->RIU_PRINVA := nPriNue
		MsUnlock()
	EndIf
Next nX
If Len(aErr) > 0
	For nX := 1 to len(aErr)
		cObserv+=CHR(13)+CHR(10)+STR0020+aErr[nX]+STR0021
	Next nX
EndIf
Aviso(STR0016, cObserv, {STR0018})
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³SumaDiasInc ³ Autor ³ Jesus Peñaloza      ³ Data ³27/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna el numero de dias subsidiados por riesgo de trabajo³±±
±±³          ³ durante el año del ejercicio                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SumaDiasInc(cExp1, cExp2)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³  											              ³±±
±±³          ³ cExp1.- Filial                                             ³±±
±±³          ³ cExp2.- Registro patronal                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GenPrima                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SumaDiasInc(cFil, cReg)
Local nDias := 0
Local cQuery := "SELECT SUM(RIT_DIAS) AS DIAS FROM "+RetSQLName("RIT")+" WHERE RIT_OBSERV = '1' AND RIT_TIPRIE IN ('1','3')"
cQuery += " AND RIT_FILIAL = '"+cFil+"'"
cQuery += " AND RIT_CODRPA = '"+cReg+"'"
cQuery += " AND RIT_ANIO = '"+cAnio+"'"
cQuery += " AND D_E_L_E_T_ = ''"
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"cSum",.T.,.T.)
cSum->(dbgotop())
nDias := cSum->(DIAS)
cSum->(dbclosearea())
Return nDias

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³SumaPorcInc ³ Autor ³ Jesus Peñaloza      ³ Data ³27/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³ Retorna la suma de los porcentajes de las valuaciones      ³±±
±±³ Descri‡„o³ por IPP otorgadas a los trabajadores durante el año de     ³±±
±±³          ³ ejercicio                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SumaPorcInc()                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³  											              ³±±
±±³          ³ cFil.- Filial                                              ³±±
±±³          ³ cReg.- Registro patronal                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GenPrima                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SumaPorcInc(cFil, cReg)
Local nPorc := 0
Local cQuery := "SELECT SUM(RIT_PORINC) AS PORC FROM "+RetSQLName("RIT")+" WHERE RIT_OBSERV = '1' AND RIT_TIPRIE IN ('1','3')"
cQuery += " AND RIT_FILIAL = '"+cFil+"'"
cQuery += " AND RIT_CODRPA = '"+cReg+"'"
cQuery += " AND RIT_ANIO = '"+cAnio+"'"
cQuery += " AND D_E_L_E_T_ = ''"
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"cPor",.T.,.T.)
cPor->(dbgotop())
nPorc := cPor->(PORC)
cPor->(dbclosearea())
Return (nPorc/100)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³TotalDefunc ³ Autor ³ Jesus Peñaloza      ³ Data ³27/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna el total de defunciones ocurridas durante el año   ³±±
±±³          ³ de ejercicio                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ TotalDefunc()                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³  											              ³±±
±±³          ³ cFil.- Filial                                              ³±±
±±³          ³ cReg.- Registro patronal                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GenPrima                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function TotalDefunc(cFil, cReg)
Local nDefun := 0
Local cQuery := "SELECT COUNT(RIT_DEFUNC) AS DEFUNC FROM "+RetSQLName("RIT")+" WHERE RIT_OBSERV = '1' AND RIT_TIPRIE IN ('1','3')"
cQuery += " AND RIT_FILIAL = '"+cFil+"'"
cQuery += " AND RIT_CODRPA = '"+cReg+"'"
cQuery += " AND RIT_ANIO = '"+cAnio+"'"
cQuery += " AND RIT_DEFUNC = 'D'"
cQuery += " AND D_E_L_E_T_ = ''"
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"cDef",.T.,.T.)
cDef->(dbgotop())
nDefun := cDef->(DEFUNC)
cDef->(dbclosearea())
Return nDefun

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³DetailGQ    ³ Autor ³ Jesus Peñaloza      ³ Data ³27/03/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Genera el detalle de cada caso de riesgo generado          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ DetailGQ(aExp1,cExp2,cExp3,cExp4,cExp5,cExp6,cExp7,dExp8,  ³±±
±±³          ³          dExp9,cExp10,cExp11,cExp12,cExp13,cExp14)         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³  											              ³±±
±±³          ³ aExp1.- Arreglo que contendra los datos del detalle        ³±±
±±³          ³ cExp2.- Filial                                             ³±±
±±³          ³ cExp3.- Registro patronal                                  ³±±
±±³          ³ cExp4.- Año que se esta procesando                         ³±±
±±³          ³ cExp5.- Matricula del empleado                             ³±±
±±³          ³ cExp6.- Consecutivo por matricula                          ³±±
±±³          ³ cExp7.- Numero de incapacidad parcial permanente           ³±±
±±³          ³ dExp8.- Fecha del accidente                                ³±±
±±³          ³ dExp9.- Fecha de alta                                      ³±±
±±³          ³ cExp10.-Continuacion de incapacidad anterior               ³±±
±±³          ³ cExp11.-Riesgo probable                                    ³±±
±±³          ³ cExp12.-Tipo de riesgo                                     ³±±
±±³          ³ cExp13.-Resultado de la incapacidad                        ³±±
±±³          ³ cExp14.-Control de incapacidad                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPM881GERA                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function DetailGQ(aDat,cFil,cReg,cAnio,cMat,cItem,cInc,dFecI,dFecF,cCont,cRies,cTipr,cResin,cContin)
	aDat := {}
	aAdd(aDat, cFil)
	aAdd(aDat, cReg)
	aAdd(aDat, cAnio)
	aAdd(aDat, cMat)
	aAdd(aDat, cItem)
	aAdd(aDat, cInc)
	aAdd(aDat, dFecI)
	aAdd(aDat, STR(dFecF-dFecI+1))
	aAdd(aDat, dFecF)
	aAdd(aDat, cCont)
	aAdd(aDat, cRies)
	aAdd(aDat, cTipr)
	aAdd(aDat, cResin)
	aAdd(aDat, cContin)
Return
