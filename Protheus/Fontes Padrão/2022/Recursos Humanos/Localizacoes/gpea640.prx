#INCLUDE "GPEA640.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PONCALEN.CH"
#INCLUDE "HEADERGD.CH"

Static lIncluir:= .F.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Gpea640  ³ Autor ³ Tatiane Matias              ³ Data ³ 01/08/05   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Tempo Extra                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³   Data   ³ BOPS/FNC ³  Motivo da Alteracao                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Luiz Gustavo  ³08/01/2007³Melhor    ³Inclusao da funcao MenuDef() para versao  ³±± 
±±³--------------³----------³----------³9.12.                                     ³±±
±±³Silvia        ³31/08/2007³129102    ³Ajuste na selecao/modificacao do roteiro  ³±±
±±³              ³          ³----------³de calculo.                               ³±±
±±³Silvia Tag.   ³22/11/2007³136531    ³Inclusao da funcao MenuDef()              ³±± 
±±³Valdeci       ³19/06/2008³----------³Ajustes para calcular corretamente a seq. ³±±
±±³              ³          ³          ³dos lancamentos (REF. mudanca Tempo Extra)³±±
±±³              ³          ³          ³e lancamento do Tempo Extra na Tab. RGB   ³±±
±±³Igor F.       ³08/10/2008³----------³Possibilitar a exclusao dos registros de  ³±±
±±³              ³          ³          ³tempo extra para periodos abertos.        ³±±
±±³              ³          ³          ³Retirada validacao na inclusao/modificacao³±±
±±³              ³          ³          ³de registros. Para nao enviar mensagem    ³±±
±±³              ³          ³          ³desnecessaria.                            ³±±
±±³Rogerio R.    ³08/10/2008³          ³Inclusao da funcao setBrwCHGAll( .F. )    ³±±
±±³              ³          ³          ³antes da mBrowse,para nao apresentar a    ³±±
±±³              ³          ³          ³tela de inclusao de filial ao incluir no- ³±±
±±³              ³          ³          ³vo Registro.                              ³±±
±±³Valdeci Lira  ³03/02/2009³08709/2008³Ajuste na chamada de GDDELETED() para evt.³±±
±±³              ³          ³          ³error.log                                 ³±±  
±±³Valdeci Lira  ³07/04/2009³----------³Tratamento para chave duplicada consid.   ³±±
±±³              ³          ³----------³super filtro.                             ³±±
±±³Valdeci Lira  ³12/05/2009³----------³Retirada da funcao chkvazio. Quando       ³±±
±±³              ³          ³          ³a tabela e exclusiva e o usuario nao tem  ³±±
±±³              ³          ³          ³nada na filial corrente, mesmo tendo a    ³±±
±±³              ³          ³          ³permissao para visualizar todas filiais   ³±±
±±³              ³          ³          ³concedidas atraves do configurador a      ³±±
±±³              ³          ³          ³funcao nao encontrava nada.               ³±± 
±±³Valdeci Lira  ³23/06/2009³08708/2008³Alteracao do da consulta padrao SRA04     ³±±
±±³              ³          ³          ³e preenchimento do campo descricao do     ³±±
±±³        	   ³          ³          ³concepto no modo edicao por linha.        ³±±
±±³Marcelo       ³21/01/2010³00886/2010³Ajuste na gravacao dos campos que obtem os³±±
±±³              ³          ³          ³valores com base no inicializador padrao. ³±±
±±³Erika K.      ³15/06/2011³11792/2011³Inclusao de tratamento para chamada da    ³±±
±±³              ³          ³          ³rotina atraves da rescisao.(modelo 2)     ³±±
±±³--------------³----------³----------³ versao 11.5------------------------------³±±
±±³Emerson       ³07/07/2011³12463/2011³Atender a Nova Interface Totvs 11.5 utili-³±±
±±³Campos        ³          ³          ³zando a funcao MsAdvSize( p/ flexibilidade³±±
±±³              ³          ³          ³da tela de acordo com a resolucao).       ³±±
±±³Emerson Camp  ³ 07/10/11 ³0000025569³Inclusao de tratamento para chamada do    ³±±
±±³              ³          ³      2011³AjustSX3, pois o mesmo so e relativo a    ³±±
±±³              ³          ³          ³localizacao Mexico e estava provocando    ³±±
±±³              ³          ³          ³erro na localizacao Equador.              ³±±
±±³Gustavo M.    ³22/08/12  ³TFOVBR    ³Ajuste para permitir que seja alterado    ³±±
±±³              ³	       ³          ³um registro da filial nao corrente.       ³±±
±±³M. Silveira   ³26/03/2013³    TGYCJB³Ajuste na a640AtualFunc() para pesquisar o³±±
±±³              ³          ³          ³pelo roteiro corretamente na alteracao.   ³±±
±±³Gustavo M.    ³15/07/2013³THMELF    ³Ajuste para posicionar corretamente na SRA³±±
±±³Esther Viveiro³30/06/2014³TPXL14    ³Limpeza do fonte para uso na V12. Retirada³±±
±±³              ³          ³          ³da funcao AjustSX3().  				      ³±±
±±³Christiane V. ³24/07/2014³TQBAHS    ³Retirada da chamada da func. Gp520AjustSXB³±±
±±³Gsantacruz    ³27/11/2015³pcreq-7944³Se a grego a Chile en la validacion de la ³±±
±±³              ³          ³          ³funcion Gp640ValCpos,Gpea640Grava,        ³±±
±±³              ³          ³          ³Gp640GrInc                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Function Gpea640(nOpcAuto,nOpAuto)
	Local aSvRotSetOpc	:= {}
	Local aIndexSRA		:= {}
	Local cFiltraSRA	:= ""
	
	Private bFiltraBrw	:= {|| NIL }		//Variavel para Filtro
	
	Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis para a consulta padrao SRV-RM                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cCodRoteiro	
	Private cCodProcesso
	Private cProcesso
	Private cCategoria 
	Private aIndexSrv	:= {}
	Private lSetF3		:= .F.		//Variavel para saber se passou no f3 da matricula 
	Private cLancDemi	:= Upper( AllTrim( GetMv("MV_DEMISRC") ) )
	
	Private nOption		:= 0
	Private lItemClVl		:= SuperGetMv("MV_ITMCLVL",NIL,"2")  $ "1*3" //variavel que define se os campos "Item Contabil" e Classe de Valor" estão ativos   
	
	cCadastro := OemToAnsi(STR0006) //"Cadastro de Tempo Extra"

	If !ExistSixRGB()
		return( Nil )
	Endif 

	//Tratamento para chamada da rotina atraves de tela de rescisão (Modelo 2)	
	lResM2 := If( Type("lResM2") <> "U" .And. lResM2, .T., .F. )
    
	If lResM2
		If FWChkFuncAccess( "GPEA640", 4, .T.)
			gp640Mnt( "SRA" , 1 , 4 ) 
		Elseif FWChkFuncAccess( "GPEA640", 2, .T.)  
			Aviso( STR0017, STR0030, {STR0031}) //"Anteção" ## "Permissão somente de visualização dos registros." ## "OK"   
			gp640Mnt( "SRA" , 1 , 2 ) 
		Else
			Aviso( STR0017, STR0032, {STR0031}) //"Anteção" ## "O usuário não tem permissão de acesso a esta rotina." ## "OK"
		Endif 
	Endif
		
	If nOpcAuto <> Nil
		aSvRotSetOpc := aRotSetOpc( NIL , NIL , nOpcAuto )
		dbSelectArea('SRA')
		nPos := Ascan(aRotina,{|x| x[4]== nOpcAuto},nOpAuto)
		If ( nPos <> 0 )
			bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nPos,2 ] + "(a,b,c,d,e) }" )
			Eval( bBlock, Alias(), (Alias())->(Recno()),nPos)
		EndIf
		aRotReSetOpc( aSvRotSetOpc )
	Elseif !lResM2
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa o filtro utilizando a funcao FilBrowse                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cFiltraRh 	:= CHKRH("GPEA640","SRA","1")
		bFiltraBrw 	:= {|| FilBrowse("SRA",@aIndexSRA,@cFiltraRH) }
		Eval(bFiltraBrw)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Endereca a funcao de BROWSE                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetBrwCHGAll( .T. ) 
		dbSelectArea("SRA")
		mBrowse( 6, 1,22,75,"SRA",,,,,,fCriaCor() )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Deleta o filtro utilizando a funcao FilBrowse                     	   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		EndFilBrw("SRA",aIndexSra)
	EndIf
		
Return( NIL )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp640Mnt  ³ Autor ³ Tatiane Matias        ³ Data ³ 01/08/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Tempo Extra                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ gp640Mnt(ExpC1,ExpN1,ExpN2)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPE                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
FUNCTION gp640Mnt(cAlias,nReg,nOpcx)
	Local aAdvSize		:= {}
	Local aInfoAdvSize	:= {}
	Local aObjSize		:= {}
	Local aObjCoords	:= {}
	Local bSet15		:= { || NIL }
	Local bSet24		:= { || NIL }
	Local nSavRec
	Local aPesqIdx 		:= {}
	Local aPesqOrd		:= {}
	Local lSeeAll		:= .T.
	Local lOk			:= .T.
	Local cPesqOrd
	Local cPesqCampo	:= space(40)
	Local cMat			:= SRA->RA_MAT
	Local lAxPesqui 	:=.F.
	Local aButtons	 	:= {}
	Local bGravar	 	:= {}
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Objetos          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	Local oFont
	Local oGroup
	Local oDlg
	Local oBtnPgNext
	Local oBtnPgPrev
	Local oBntNext
	Local oBntPrev
	Local oPesqCbx
	Local oPesqGet
	Local oFunc
	Local oPeriodo
	Local oSemana
	Local oRoteiro
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Blocos de Codigo                                            ³
	³================                                            ³
	³Serao utilizados na acao do botoes de movimentacao, "Avanca"³
	³"Retrocede", "Avanca Todos" e "Retrocede Todos".            ³
	³O Bloco "bFica" sera utilizado quando forem alterados os    ³
	³campos data de referencia, semana e tipo de lancamento.     ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	Local bRetrocOne := {|| SRA->( dbskip(-1))	 ,Gp640VerifAlt(cAlias,SRA->RA_FILIAL,cMat,nOpcx), aCols:= {}, a640AtualFunc(cAlias,@cMat),oGet:oBrowse:Refresh(),oDLG:Refresh()}
	Local bRetrocAll := {|| SRA->( dbGoTop())	 ,Gp640VerifAlt(cAlias,SRA->RA_FILIAL,cMat,nOpcx), aCols:= {}, a640AtualFunc(cAlias,@cMat),oGet:oBrowse:Refresh(),oDLG:Refresh()}
	Local bAvancaOne := {|| SRA->( dbskip())	 ,Gp640VerifAlt(cAlias,SRA->RA_FILIAL,cMat,nOpcx), If( SRA->(! EOF()),aCols:= {},.T.), a640AtualFunc(cAlias,@cMat),oGet:oBrowse:Refresh(),oDLG:Refresh()}
	Local bAvancaAll := {|| SRA->( dbGoBottom()) ,Gp640VerifAlt(cAlias,SRA->RA_FILIAL,cMat,nOpcx), aCols:= {}, a640AtualFunc(cAlias,@cMat),oGet:oBrowse:Refresh(),oDLG:Refresh()}
	Local bFica      := {|| a640AtualFunc(cAlias,@cMat),If(lDaRefresh,(oGet:oBrowse:Refresh(),oDLG:Refresh()),.t.)}
	Local lValPer		:= .T.
	
	Private oGet
	Private cDataIni	:= ""
	Private cDataFim	:= ""
	Private aColsRec	:= {} 
	Private aGhColsRec	:= {}
	Private aVirtual	:= {}
	Private aVisual		:= {}
	Private aInfo		:= {}
	Private aCodFol		:= {}
	Private aCols   	:= {}
	Private aColsAnt   	:= {}
	Private aGhCols		:= {}  
	Private aGhostCols	:= {}
	Private aGhHeader	:= {} 
	Private aGhVirtual	:= {}
	Private aGdAltera	:= {}
	Private cFilAnterior:= ""
	Private lDaRefresh 	:= .F.
	Private cPeriodo	:= Space(GetSx3Cache("RCH_PER", "X3_TAMANHO"))
	Private cPerAnt		:= Space(GetSx3Cache("RCH_PER", "X3_TAMANHO"))
	Private cNumPagto 	:= Space(GetSx3Cache("RCH_NUMPAG", "X3_TAMANHO"))
	Private cSemAnt		:= Space(GetSx3Cache("RCH_NUMPAG", "X3_TAMANHO"))
	Private cRoteiro	:= Space(GetSx3Cache("RCH_ROTEIR", "X3_TAMANHO"))
	Private cRotAnt		:= Space(GetSx3Cache("RCH_ROTEIR", "X3_TAMANHO"))
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a entrada de dados do arquivo                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private aTELA[0,0],aGETS[0],aHeader[0],nUsado:=0
	
	If lIncluir
   		nOpcx := 3
   		lIncluir :=.F.
	Endif 

	_SetOwnerPrvt( "nOpcNewGd", IF( nOpcX == 2 .or. nOpcX == 5 , 0 , GD_INSERT + GD_UPDATE + GD_DELETE))
	
	cProcesso	:= SRA->RA_PROCES
	cCategoria	:= SRA->RA_CATFUNC
	
	nOption	:= nOpcX

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³O botao gravar so aparecera quando for inclusao ou alteracao		   ³	
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If (nOpcx = 3) .OR. (nOpcx = 4)
		bGravar	:= { || iIf(Gp640TudOk(), MsAguarde( { || Gpea640Grava(cAlias,SRA->RA_FILIAL,cMat,nOpcx, .T.) }, OemToAnsi(STR0016) ), .F.)}
		aAdd(aButtons, {"SALVAR", bGravar, OemToAnsi( STR0019 ), OemToAnsi( STR0019 )}) //"Gravar"
	EndIf    	 

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Salva a integridade dos dados                                         ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nSavRec         := SRA->(Recno())  
	If( MV_MODFOL == "2" )
		cAlias := "RGB"
	Else
		cAlias := "RCR"
	EndIf  

	
	//-- Carrega o aInfo com os dados do SIGAMAT.EMP
	If fInfo(@aInfo,SRA->RA_FILIAL)
		cFilAnterior:= SRA->RA_FILIAL
	EndIf
	
	If ! Fp_CodFol(@aCodFol,SRA->RA_FILIAL) 
		Return
	Endif
	                                                            
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Inicializar Roteiro, Periodo e Numero de Pagamento            		   ³	
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cRoteiro := fGetRotOrdinar()   	// Roteiro Ordinario para o periodo de admissao e aumento	
	cRotAnt  := cRoteiro
	                    
	dbSelectArea( "RCH" )                                       
	dbSetOrder(5)
	dbSeek(xFilial("RCH")+ cProcesso + cRoteiro + Space(8), .F.)
	If Eof()
		cRoteiro := Space( GetSx3Cache( "RCH_ROTEIR", "X3_TAMANHO" ) )
		dbSeek( xFilial("RCH") + cProcesso + cRoteiro + Space(8), .F.)
	EndIf	
	While !Eof() 
		If Empty(RCH->RCH_DTFECH) .AND. (RCH->RCH_PROCES == cProcesso) .And. RCH->RCH_ROTEIR == cRoteiro
			cPeriodo := RCH->RCH_PER
			cPerAnt	 := cPeriodo
			cNumPagto:= RCH->RCH_NUMPAG
			cSemAnt	 := cNumPagto
			Exit
		Endif
		dbSkip()
	EndDo
	If Empty( cRoteiro )
		cRoteiro := cRotAnt
	EndIf
    
	If (MV_MODFOL == "2")     
		dbSelectArea( "RGB" )
		RGB->( dbSetOrder(1) )
	Else
		dbSelectArea( "RCR" )
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Atualizar a GetDados                                         		   ³	
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	a640MontaCols(cAlias,SRA->RA_FILIAL,cMat)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta as Dimensoes dos Objetos         					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aAdvSize		:= MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5}
	aAdd( aObjCoords , {  000 , 020 , .T. , .F. } )
	aAdd( aObjCoords , {  000 , 020 , .T. , .F. } )
	aAdd( aObjCoords , {  000 , 000 , .T. , .T. } )
	aAdd( aObjCoords , {  000 , 020 , .T. , .F. } )
	aAdd( aObjCoords , {  000 , 005 , .T. , .F. } )
	aObjSize := MsObjSize( aInfoAdvSize , aObjCoords )
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Mostra o corpo dos valores variaveis                 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nOpca := 0
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0006) FROM aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Define grupo do cabecalho       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	 /*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
       ³ Matricula, Nome, Admissao, Processo e Posto       ³
	   ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	@ aObjSize[1,1], aObjSize[1,2] 		GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]*0.15 	LABEL OemToAnsi(STR0007) OF oDlg PIXEL // Matricula:
	oGroup:oFont:= oFont
	@ aObjSize[1,1], aObjSize[1,4]*0.155 	GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]*0.67 	LABEL OemToAnsi(STR0008) OF oDlg PIXEL // Nome:
	oGroup:oFont:= oFont
	@ aObjSize[1,1], aObjSize[1,4]*0.675 	GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]*0.8	LABEL OemToAnsi(STR0009) OF oDlg PIXEL	// Admiss„o:
	oGroup:oFont:= oFont
	@ aObjSize[1,1], aObjSize[1,4]*0.805 	GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]*0.9 	LABEL OemToAnsi(STR0010) OF oDlg PIXEL // Processo:
	oGroup:oFont:= oFont
	@ aObjSize[1,1], aObjSize[1,4]*0.905 	GROUP oGroup TO aObjSize[1,3], aObjSize[1,4]		LABEL OemToAnsi(STR0011) OF oDlg PIXEL // Posto:
	oGroup:oFont:= oFont
		
	@ aObjSize[1,1]+10, aObjSize[1,2]*2.5	SAY oFunc Var cMat 					SIZE 050,10 OF oDlg PIXEL FONT oFont
	@ aObjSize[1,1]+10, aObjSize[1,4]*0.17	SAY OemToAnsi(SRA->RA_NOME)			SIZE 146,10 OF oDlg PIXEL FONT oFont
	@ aObjSize[1,1]+10, aObjSize[1,4]*0.69	SAY Dtoc(SRA->RA_ADMISSA) 			SIZE 050,10 OF oDlg PIXEL FONT oFont
	@ aObjSize[1,1]+10, aObjSize[1,4]*0.82	SAY OemToAnsi(cProcesso) 			SIZE 050,10 OF oDlg PIXEL FONT oFont
	@ aObjSize[1,1]+10, aObjSize[1,4]*0.92	SAY OemToAnsi(SRA->RA_POSTO)		SIZE 050,10 OF oDlg PIXEL FONT oFont
		
	 /*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
       ³ Codigo do Periodo, Numero de Pagamento, Roteiro, Data Inicio e Data Fim do periodo ³
	   ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	@ aObjSize[2,1], aObjSize[2,2] 		GROUP oGroup TO aObjSize[2,3], aObjSize[2,4]*0.18	LABEL OemToAnsi(STR0012) OF oDlg PIXEL	// Codigo do Periodo:
	oGroup:oFont:= oFont
	@ aObjSize[2,1], aObjSize[2,4]*0.185 	GROUP oGroup TO aObjSize[2,3], aObjSize[2,4]*0.36 	LABEL OemToAnsi(STR0013) OF oDlg PIXEL	// Numero de Pagamento:
	oGroup:oFont:= oFont
	@ aObjSize[2,1], aObjSize[2,4]*0.365	GROUP oGroup TO aObjSize[2,3], aObjSize[2,4]*0.54 	LABEL OemToAnsi(STR0014) OF oDlg PIXEL	// Roteiro:
	oGroup:oFont:= oFont
	@ aObjSize[2,1], aObjSize[2,4]*0.545 	GROUP oGroup TO aObjSize[2,3], aObjSize[2,4]*0.74	LABEL OemToAnsi(STR0015) OF oDlg PIXEL	// Periodo:
	oGroup:oFont:= oFont

	@ aObjSize[2,1]+7, aObjSize[2,2]*2.5	MSGET oPeriodo 	Var cPeriodo PICTURE GetSx3Cache( "RCH_PER"   , "X3_PICTURE" ) F3 "RCH"   VALID ExistCpo("RCH", cProcesso + cPeriodo, 1) .and. Gp640VerifAlt(cAlias,SRA->RA_FILIAL,cMat,nOpcx,bFica)  				SIZE 050,10 OF oDlg PIXEL FONT oFont HASBUTTON
	@ aObjSize[2,1]+7, aObjSize[2,4]*0.20 	MSGET oSemana 	Var cNumPagto  PICTURE GetSx3Cache( "RCH_NUMPAG", "X3_PICTURE" ) F3 "RCH01" VALID ExistCpo("RCH", cProcesso + cPeriodo + cNumPagto, 1) .and. Gp640VerifAlt(cAlias,SRA->RA_FILIAL,cMat,nOpcx,bFica)	SIZE 050,10 OF oDlg PIXEL FONT oFont HASBUTTON
	@ aObjSize[2,1]+7, aObjSize[2,4]*0.38 	MSGET oRoteiro 	Var cRoteiro PICTURE GetSx3Cache( "RCH_ROTEIR", "X3_PICTURE" ) F3 "SRY" ;
			VALID If( lValPer, lValPer := (Vazio() .OR. ExistCpo("SRY", cRoteiro)), lValPer := .T. ) .and. ;
	  	    If(cRoteiro <> cRotAnt, Gp640VerifAlt(cAlias,SRA->RA_FILIAL,cMat,nOpcx,bFica),.T.) SIZE 050,10 OF oDlg PIXEL FONT oFont HASBUTTON

	@ aObjSize[2,1]+10,aObjSize[2,4]*0.56		SAY DTOC(cDataIni) + " - "  + DTOC(cDataFim) SIZE 100,10 OF oDlg PIXEL FONT oFont

	/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  ³ Define grupo do rodape          ³
	  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	@ aObjSize[4,1], (aObjSize[4,2]) GROUP oGroup TO (aObjSize[4,3]+ 6), (aObjSize[4,4]) LABEL OemToAnsi(STR0021) OF oDlg PIXEL	 //"Pesquisar Funcionarios"
	oGroup:oFont:= oFont 

	/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	  ³ Lista Botoes de posicionamento do funcionario ³
	  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	
	@ aObjSize[4,1]+10, aObjSize[4,2]+08.96 	BUTTON oBtnPgPrev 	PROMPT OemToAnsi("<<")	 SIZE 021.5, 10 DESIGN ACTION Gp640Movimenta(bRetrocAll) OF oDlg PIXEL
	@ aObjSize[4,1]+10, aObjSize[4,2]+29.96	BUTTON oBtnPrev		PROMPT OemToAnsi("<")	 SIZE 021.5, 10 DESIGN ACTION Gp640Movimenta(bRetrocOne) OF oDlg PIXEL
	@ aObjSize[4,1]+10, aObjSize[4,2]+50.96	BUTTON oBntNext 	PROMPT OemToAnsi(">")	 SIZE 021.5, 10 DESIGN ACTION Gp640Movimenta(bAvancaOne) OF oDlg PIXEL
	@ aObjSize[4,1]+10, aObjSize[4,2]+71.96	BUTTON oBtnPgNext 	PROMPT OemToAnsi(">>")	 SIZE 021.5, 10 DESIGN ACTION Gp640Movimenta(bAvancaALL) OF oDlg PIXEL

    AxPesqOrd("SRA",@aPesqIdx,,lSeeAll,@aPesqOrd)
    cPesqOrd := aPesqOrd[1]

	@ aObjSize[4,1]+10, aObjSize[4,4]*0.28 		COMBOBOX oPesqCbx 	VAR cPesqOrd ITEMS aPesqOrd SIZE 100,	36 	PIXEL OF oDlg ON CHANGE If(lAxPesqui,(DbSelectArea(cAlias),DbSetOrder(aPesqIdx[oPesqCbx:nAt][1]), ,__oObjBrow:Refresh()),)     //Ajuste para Funciones no Compiladas en RPO
  	@ aObjSize[4,1]+10, aObjSize[4,4]*0.28 + 110 	MSGET oPesqGet 		VAR cPesqCampo 				SIZE 100,	09 	PIXEL OF oDlg 
    @ aObjSize[4,1]+10, aObjSize[4,4]*0.28 + 220 	BUTTON "Ok" 									SIZE 021.5, 10 	PIXEL OF oDlg ACTION (Gp640VerifAlt(cAlias,SRA->RA_FILIAL,cMat,nOpcx,,.T.), AxPesqSeek("SRA",.F.,cPesqCampo,,aPesqIdx,oPesqCbx:nAt,lSeeAll,),Gp640Movimenta(bFica))

	oGet := MsNewGetDados():New(	aObjSize[3,1],;
									aObjSize[3,2],;
									aObjSize[3,3],;
									aObjSize[3,4],;
									nOpcNewGd,;
									"Gp640LinOk",;
									"Gp640TudOk",;
									NIL,;
									aGdAltera,;
									0,;
									99999,;
									'Gp640ValCpos()',;
									NIL,;
									If(nOpcx=2.Or.nOpcx=5,.F.,.T.),;
									@oDlg,;
									@aHeader,;
									@aCols)
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Validar a linha atual quando a GetDados perder o foco          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
 	oGet:oBrowse:bLostFocus := { || If(!Gp640LinOk(), oGet:oBrowse:SetFocus(), .T.) }

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verificar se o roteiro foi informado antes de incluir a linha  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
 	oGet:oBrowse:bGotFocus := { || If(Empty(cRoteiro), oRoteiro:SetFocus(), If( lValPer := GpValidPer( NIL, cProcesso, cPeriodo, cNumPagto, cRoteiro), .T., (oRoteiro:SetFocus(), cRoteiro := Space(3), oRoteiro:Refresh()))) }

	bSet15	:= { || nOpcA:=1, If( Gp640TudOk(), oDlg:End(),.F. ) }
	bSet24	:= { || nOpcA:=0,oDlg:End(), lOk := .F. }
    
	//Variaveis para a funcao montaF3Srv em Gpea520, usada na consulta padrao SRV-RM
	cCodRoteiro := cRoteiro
	cCodProcesso:= cProcesso   

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24, NIL , aButtons )

	If ( ( nOpcA == 1 ) .and. ( nOpcX <> 2 ) ) .or. ( nOpcX == 5 .and. lOk )
       	Gpea640Grava(cAlias,SRA->RA_FILIAL,cMat,nOpcx, .F.)
	EndIF

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura a integridade da janela                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cAlias := "SRA"
	dbSelectArea(cAlias)
	dbGoTo( nSavRec )
	
Return ( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³a640MontaColsºAutor  ³Tatiane Matias      º Data ³  01/08/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Monta o aCols com base nos dados do Tempo Extra                º±±
±±º          ³                                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a640MontaCols(cAlias,cFil,cMatricula)                         
	Local cAcessaSRA  	:= &("{ || " + ChkRH("GPEA640","SRA","2") + "}")
	Local cAcessaRCR 	:= ""
	Local cAcessaRGB 	:= ""
	Local cPrefixTab	:= iIf(MV_MODFOL =="2", "RGB_", "RCR_")
	Local aPosHeader	:= {} 
	Local aTmpCols		:= {}
	Local aGdNaoAltera  := {}
	Local cQueryCond	:= ""  
	Local cQueryGh		:= ""
	Local nGhRows		:= 0
	Local nRowAcols		:= 0
	Local nPosSemana
	Local nI 			:= 0  
	Local nJ			:= 0
	Local nX            := 0
	Local nPosPD	
	Local nPosDESCPD    
	
	If (MV_MODFOL =="2")
		cAcessaRGB 	:= &("{ || " + ChkRH("GPEA640","RGB","2") + "}")	
	Else
		cAcessaRCR 	:= &("{ || " + ChkRH("GPEA640","SRC","2") + "}")
	End If
	
	aCols := {}
                      
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Recupera a data de inicio e a data fim do periodo informado    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cDataIni := RCH->RCH_DTINI
	cDataFim := RCH->RCH_DTFIM

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Carrega aCodFol                  |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cFil # cFilAnterior
		If ! Fp_CodFol(@aCodFol,cFil) 
			Return
		Endif             
		cFilAnterior:= cFil
	Endif

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta a chave para Pesquisa em GdMontaCols                   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/    
	If (MV_MODFOL=="2")
	   ( cAlias )->(dbSetOrder( RetOrder("RGB", "RGB_FILIAL+RGB_PROCES+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_MAT+RGB_PD+RGB_SEQ") ))
		cKeySeek := cFil + cProcesso + cPeriodo + cNumPagto + cRoteiro + cMatricula
	Else
		cKeySeek := ( cFil + cMatricula + cPeriodo + cNumPagto + cRoteiro )
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta Query para a Selecao das Informacoes em GdMontaCols    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	#IFDEF TOP                 
		cQueryCond	:= cPrefixTab+"FILIAL='"+cFil+"'"
		cQueryCond	+= " AND "
		cQueryCond	+= cPrefixTab+"MAT='"+cMatricula+"'"
		cQueryCond	+= " AND "
		cQueryCond	+= cPrefixTab+"PROCES='"+cProcesso+"'"
		cQueryCond	+= " AND "
		cQueryCond	+= cPrefixTab+"PERIOD='"+cPeriodo+"'"
		cQueryCond	+= " AND "   
		cQueryCond	+= cPrefixTab+"ROTEIR='"+cRoteiro+"'"
		cQueryCond	+= " AND "
		cQueryCond	+= "D_E_L_E_T_<>'*' "   
		
		If (MV_MODFOL=="2")                            
			cQueryCond	+= "AND RGB_SEMANA='"+cNumPagto+"' " 
			
			cQueryGh := cQueryCond
			
			//Seleciona somente registros que tiverem algum valor lancado 
			cQueryCond	+= "AND ((RGB_DUM 	!= 0) OR "  
			cQueryCond	+= "	(RGB_DDOIS 	!= 0) OR "  
			cQueryCond	+= "	(RGB_DTRES 	!= 0) OR "  
			cQueryCond	+= "	(RGB_DQUATR	!= 0) OR "  
			cQueryCond	+= "	(RGB_DCINCO	!= 0) OR "  
			cQueryCond	+= "	(RGB_DSEIS 	!= 0) OR "  
			cQueryCond	+= "	(RGB_DSETE 	!= 0) )"  
			cQueryCond	+= "AND RGB_TIPO1='H' "  			

			//Condicao para a query do acols Ghost			
			cQueryGh	+= "AND ((RGB_DUM 	= 0) AND "  
			cQueryGh	+= "	(RGB_DDOIS 	= 0) AND "  
			cQueryGh	+= "	(RGB_DTRES 	= 0) AND "  
			cQueryGh	+= "	(RGB_DQUATR	= 0) AND "  
			cQueryGh	+= "	(RGB_DCINCO	= 0) AND "  
			cQueryGh	+= "	(RGB_DSEIS 	= 0) AND "  
			cQueryGh	+= "	(RGB_DSETE 	= 0) )"  			
		Else
			cQueryCond	+= "RCR_NPAGTO='"+cNumPagto+"'"
		End If
	#ENDIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta Bloco para Skip em GdMontaCols                         ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	bSkip := { || .F. }

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Define os Campos que nao serao Mostrados                     ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If (MV_MODFOL == "2") 
		/*aFields	:= {"RGB_FILIAL" , "RGB_MAT"   , "RGB_NOME"  , "RGB_QTDSEM", "RGB_HORAS", "RGB_VALOR", ;
					"RGB_DTREF"  , "RGB_PARCEL", "RGB_TIPO2" , "RGB_NUMID", ;
					"RGB_CODFUN" , "RGB_POSTO" , "RGB_DEPTO" , ;
					"RGB_PROCES" , "RGB_PERIOD", "RGB_ROTEIR"}*/

		aFields	:= {"RGB_FILIAL" , "RGB_MAT"   , "RGB_NOME"  , "RGB_QTDSEM", "RGB_VALOR", ;
					 "RGB_PARCEL", "RGB_TIPO2" , "RGB_NUMID", ;
					 "RGB_POSTO" , "RGB_PROCES" , "RGB_PERIOD", "RGB_ROTEIR"}					
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Define os Campos que nao Serao Alterados                     ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		aGdNaoAltera := { "RGB_TIPO1", "RGB_HORAS" }

	Else
		aFields	:= {"RCR_FILIAL", "RCR_MAT", "RCR_PROCES", "RCR_PERIOD", "RCR_NPAGTO","RCR_ROTEIR"}
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Define os Campos que nao Serao Alterados                     ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		aGdNaoAltera := {}
	End If
 
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Recupera os dados da tabela                                  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aCols 		:= GdMontaCols(	@aHeader,;
								@nUsado,;
								@aVirtual,;
								@aVisual,;
								cAlias,;
								aFields,;
								@aColsRec,;
								cAlias,;
								cKeySeek,;
								NIL,;
								bSkip,;
								NIL,;
								NIL,;
								NIL,;
								NIL,;
								NIL,;
								NIL,;
								cQueryCond,;
								.F.,;
								.F.)

	nPosSemana		:= GdFieldPos("RGB_SEMANA", aHeader)
								
	If nPosSemana > 0	
		aAdd(aVisual, "RGB_SEMANA")
		aHeader[nPosSemana, __AHEADER_VISUAL__] := "V"
		aHeader[nPosSemana, __AHEADER_INITPAD__] := "cNumPagto"
	
		For nX:= 1 To Len(aCols)
			If Empty(aCols[nX, nPosSemana])
				aCols[nX, nPosSemana] := cNumPagto
			EndIf
		Next
	EndIf									

	/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
 	³ Carrega, apenas, os Campos Editaveis            			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aGdAltera := {}
	For nX := 1	To nUsado
		IF (;
				( aScan( aVirtual 		, aHeader[ nX , 02 ] ) == 0.00 ) .and. ;
		   		( aScan( aVisual 		, aHeader[ nX , 02 ] ) == 0.00 ) .and. ;
		   		( aScan( aFields    	, aHeader[ nX , 02 ] ) == 0.00 ) .and. ;
		   		( aScan( aGdNaoAltera	, aHeader[ nX , 02 ] ) == 0.00 ) 	   ;
		  	)
			aAdd( aGdAltera , aHeader[ nX , 02 ] )
		EndIF			   
	Next nX
							 
	If MV_MODFOL=="2" 
		nPosPd		:= GdFieldPos("RGB_PD", aHeader)
		aHeader [ nPosPd , __AHEADER_INITPAD__] := ""
		
		aIndexSrv := {}
		aSize(aIndexSrv, 0)
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega acols com todos os campos para usar na gravacao      ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
		aGHCols := GdMontaCols(@aGHHeader,@nUsado,@aGhVirtual,@aVisual,cAlias,{},@aGhColsRec,cAlias,cKeySeek,NIL,bSkip,NIL,NIL,NIL,NIL,NIL,NIL,cQueryGh, .F., .F.)		
		
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Compatibiliza o aGhostCols com o aCols para usar no fmontaseq³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		nI:=1   
		nRowAcols := Len(aHeader)
		While nI <= nRowAcols
			aAdd(aPosHeader, GdFieldPos( aHeader[nI, 2],aGhHeader ))			
			nI++                 
		EndDo                                                                   
		aAdd(aPosHeader, Len(aGhHeader)+1/*posicao do GdDeleted*/)			
		
		aGhostCols 	:= {}
		aTmpCols   	:= {}
		nRowAcols 	:= Len(aPosHeader)
		nGhRows 	:= Len(aGhCols)
		nJ 			:= 1
		While nJ<= nGhRows
			nI:=1   
			While nI <= nRowAcols
				aAdd(aTmpCols, aGhCols[nJ, aPosHeader[nI]])
				nI++
			EndDo		 
			iIf(Len(aTmpCols)>0, aAdd(aGhostCols, aClone(aTmpCols)), .F.)
			aTmpCols:= {}
			nJ++
		EndDo     
		
	    If !(Inclui)                                                                                                                             							  
			nPosPd		:= GdFieldPos("RGB_PD", aHeader)
			nPosDescPd	:= GdFieldPos("RGB_DESCPD", aHeader)
			nRowAcols = Len(aCols)
			If ( nPosPd > 0 ) .And. (nPosDescPd > 0) .And. (nRowAcols > 0 )
				nI:=1
				While nI <= nRowAcols 	                    
					aCols[nI, nPosDescPd] := fDesc("SRV", aCols[nI, nPosPd], "RV_DESC")
			 		nI++
				EndDo
			EndIf
		EndIf
	EndIf
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Ordena por sequencia                                         ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/ 
	If cAlias != "RGB"
		aSort(aCols,,,{|x,y| x[1]<y[1]})	
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Atualiza a getdados                                          ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If !(oGet == nil)
		oGet:aCols := aClone(aCols)
		oGet:Refresh()
	EndIf
	
	aColsAnt := aClone(aCols)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao  ³Gp640MovimentaºAutor  ³Tatiane Matias      º Data ³  01/08/05   º±±
±±ÌÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.   ³ Executa bloco de codigo a cada movimentacao dos botoes de      º±±
±±º        ³ movimentacao.                                                  º±±
±±ÌÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso     ³ AP                                                             º±±
±±ÈÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Gp640Movimenta( bFunc )

Eval(bFunc)

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao  ³a640AtualFuncºAutor  ³Tatiane Matias      º Data ³  01/08/05   º±±
±±ÌÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.   ³ Atualiza os dados do funcionario na tela e chama a rotina paraº±±
±±º        ³ refazer o aCols.                                              º±±
±±ÌÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso     ³ AP                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a640AtualFunc(cAlias,cMat)

//Dados da Filial
If SRA->( EOF() )  
    Aviso( STR0017,STR0018, { "Ok" },, ) //"Atencao", "Nao ha mais funcionarios para consulta"
	SRA->( dbSkip(-1))
	return
EndIf 
                   
If SRA->RA_FILIAL # cFilAnterior
	If fInfo(@aInfo,SRA->RA_FILIAL)
		cFilAnterior:= SRA->RA_FILIAL
	EnDif	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se mudou as informacoes de periodo, nro pagamento ou roteiro ³
//³atualiza as informacoes e dá refresh na janela.              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lDaRefresh := .F.
If (cPeriodo#cPerAnt) .or. (cNumPagto#cSemAnt) .or. (cRoteiro#cRotAnt)
	lDaRefresh 	:= .T.

	If (cPeriodo <> cPerAnt ) .or. (cNumPagto <> cSemAnt) .Or. (cRoteiro <> cRotAnt)
		If (MV_MODFOL == "2")     
			DbSelectArea( "RGB" )
		Else
			DbSelectArea( "RCR" )
		EndIf
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Recupera o roteiro do periodo/numero de pagamento informado  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If (cRoteiro <> RCH->RCH_ROTEIR .and. !Empty(RCH->RCH_ROTEIR)) .or. Empty(cRoteiro) 
			cRoteiro	:= gp240RetCont("RCH", 1, xFilial("RCH") + cProcesso + cPeriodo + cNumPagto + cRoteiro, "RCH_ROTEIR")

			If Empty(cRoteiro)
				cRoteiro := fGetRotOrdinar()
			EndIf
		ElseIf cRoteiro <> RCH->RCH_ROTEIR .and. Empty(RCH->RCH_ROTEIR)
			// somente posicionar na tabela RCH e nao atualizar o roteiro
			gp240RetCont("RCH", 1, xFilial("RCH") + cProcesso + cPeriodo + cNumPagto, "RCH_ROTEIR")
		Else           
			gp240RetCont("RCH", 1, xFilial("RCH") + cProcesso + cPeriodo + cNumPagto + cRoteiro, "RCH_ROTEIR")
		EndIf
	EndIf

	cPerAnt		:=	cPeriodo
	cSemAnt		:= 	cNumPagto                                 
	cRotAnt		:= 	cRoteiro
EndIf		

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiza os dados do Funcionario.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cMat		:= SRA->RA_MAT
cProcesso 	:= SRA->RA_PROCES

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Chama funcao para montagem do acols.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
a640MontaCols(cAlias, SRA->RA_FILIAL,cMat)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp640LinOk  ³ Autor ³ Tatiane Matias      ³ Data ³01/08/05  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Critica linha digitada                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³GPEA640                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gp640LinOk()

	Local lRet 			:= .T.   
	Local nPosPd    	:= 0
	Local nPosDia1 		:= 0
	Local nPosDia2 		:= 0
	Local nPosDia3 		:= 0
	Local nPosDia4 		:= 0
	Local nPosDia5 		:= 0
	Local nPosDia6 		:= 0
	Local nPosDia7 		:= 0
	Local nPosTotal		:= 0
	Local nValor   		:= 0
	Local nPosDtRef 	:= 0
	Local nPosNrPagto	:= 0
	Local nNumHoras		:= 0
	Local n         	:= 0
	Local cDtFim    	:= ""
   	Local lOnSpFilter	:= .F.
   	
	If (MV_MODFOL=="2")
		nPosHoras	:= GdFieldPos("RGB_HORAS"  )
		nPosDia1	:= GdFieldPos("RGB_DUM"    )
		nPosDia2	:= GdFieldPos("RGB_DDOIS"  )
		nPosDia3	:= GdFieldPos("RGB_DTRES"  )
		nPosDia4	:= GdFieldPos("RGB_DQUATR" )
		nPosDia5	:= GdFieldPos("RGB_DCINCO" )
		nPosDia6	:= GdFieldPos("RGB_DSEIS"  )
		nPosDia7	:= GdFieldPos("RGB_DSETE"  )
        nPosPd		:= GdFieldPos("RGB_PD" 	   )
        nPosDtRef	:= GdFieldPos("RGB_DTREF"  )
        nPosNrPagto := GdFieldPos("RGB_SEMANA" )
		nPosSq      := GdFieldPos("RGB_SEQ" )
		n           := oGet:nat
				 
		If ArrayCompare( oGet:aCols , aColsAnt ) 
			Return .T.
		Endif
		
		If Empty(oGet:aCols[n, nPosPD])	     
			msgInfo(STR0024)//Campo cod verba vazio
			Return .F.
		EndIf	
	
  		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Se a data de referencia foi alterada. So podera ser ³ 
		³ alterada se a verba tiver opcao de lancamento diario. Senao  ³ 
		³ a data deve ser sempre o ultimo dia do periodo informado.    ³ 
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If nPosDtRef > 0 
			cDtFim := oGet:aCols[n, nPosDtRef]
		else
			cDtFim := cDataFim
		endif
		If !Empty(oGet:aCols[n, nPosPD]) .AND. !Empty(cDtFim)
			If ( cDtFim <> gp090RetDtRef() )             
				If ( PosSRV(oGet:aCols[n, nPosPD] , SRA->RA_FILIAL , "RV_LCTODIA" ) == "N" ) 
					Help(" ",1,"GPA120REF")
					lRet := .F.
				EndIf
			EndIf
		EndIf  

        //Verifica se existe algum dos campos especificos na getDados
        nPosTotal	:= nPosDia1 + nPosDia2 + nPosDia3 + nPosDia4 + nPosDia5 + nPosDia6 + nPosDia7
		If (nPosTotal > 0)          
			//Verifica se existe algum valor nos campos especificos	    	
			aCols  := oGet:aCols
			
			nNumHoras := oGet:aCols[n, nPosHoras]
			
			nValor := oGet:aCols[n, nPosDia1] + oGet:aCols[n, nPosDia2] + oGet:aCols[n, nPosDia3] + ;
			 	  		oGet:aCols[n, nPosDia4] + oGet:aCols[n, nPosDia5] + oGet:aCols[n, nPosDia6] + ;
				  		oGet:aCols[n, nPosDia7]  
			                              
			If nValor > 0  

				If nValor # nNumHoras
					Alert( STR0033 ) //"O valor do campo HORAS nao corresponde ao total de horas informadas")
					Return( .F. )
				EndIf

				/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				  ³Monta a Sequencia da Verba "Chave Inicial Codigo da Verba"    ³
				  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/   
				IF !( lRet := fMontaSeq( "RGB_PD", oGet:aCols[n, nPosPd],NIL, aGhostCols ) )
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³Para Linhas Deletadas o Retorno devera ser "True"             ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					IF ( GdDeleted(n) )
						lRet:= .T.
					EndIF		
				EndIF
			Else 
				Help(" ",1,"A040SVAHOR")
				lRet:=.F.
			EndIf
		Else
			lRet := .F.
		EndIf
		        
		//Verifica se a chave existe na tabela 
		dbSelectArea("RGB")  
		If FWOnSPFilter("RGB") 
			SPFilTerOff( { "RGB" } ) 
			lOnSpFilter := .T.                                                                                                                        
		EndIf
		
		RGB->( dbSetOrder(RetOrdem("RGB","RGB_FILIAL + RGB_PROCES + RGB_PERIOD + RGB_SEMANA + RGB_ROTEIR + RGB_MAT + RGB_PD + RGB_SEQ")) )
		If RGB->( dbSeek(xFilial("RGB") + cProcesso + cPeriodo + cNumPagto + cRoteiro + ;
		                 SRA->RA_MAT + oGet:aCols[n, nPosPD] + oGet:aCols[n, nPosSq]) )
			If n <= len(aColsAnt)
				If oGet:aCols[n, nPosPD] != aColsAnt[n, nPosPd]
					lRet := .F.
				EndIf
			Else
				lRet := .F.
			EndIf    
			If !lRet
	    		Aviso( STR0017, STR0025, { "Ok" },, ) //"Atencao", "Verba informada já existe em incidencias"
	  		EndIf
		EndIf
		
		If lOnSpFilter
		   SPFilTerOff( { "RGB" }, .F. )
		EndIf
			   	
		If lRet                  
			// Se o campo de filtro numero de pagamento estiver preenchido, o 
			// campo RC_SEMANA da GetDados devera ser o informado no filtro.
			// Se o campo de filtro numero de pagamento nao estiver preenchido, o 
			// campo RC_SEMANA da GetDados podera ser qualquer numero de pagamento 
			// da consulta padrao.
			lRet := gp090NrPagtoValid(oGet:aCols[n, nPosNrPagto], "RGB_")
		EndIf
	Else
		nPosDia1	:= GdFieldPos("RCR_DIA1")
		nPosDia2	:= GdFieldPos("RCR_DIA2")
		nPosDia3	:= GdFieldPos("RCR_DIA3")
		nPosDia4	:= GdFieldPos("RCR_DIA4")
		nPosDia5	:= GdFieldPos("RCR_DIA5")
		nPosDia6	:= GdFieldPos("RCR_DIA6")
		nPosDia7	:= GdFieldPos("RCR_DIA7")
		nPosTotal	:= GdFieldPos("RCR_TOTAL")
	
		n := If(Type("n")="U",oGet:nat, n)
	
		aCols  := oGet:aCols
	
		nValor := oGet:aCols[n, nPosDia1] + oGet:aCols[n, nPosDia2] + oGet:aCols[n, nPosDia3] + ;
			 	  oGet:aCols[n, nPosDia4] + oGet:aCols[n, nPosDia5] + oGet:aCols[n, nPosDia6] + ;
				  oGet:aCols[n, nPosDia7]
	
		oGet:aCols[n, nPosTotal] := nValor
		oGet:oBrowse:Refresh()    
	EndIf
Return lRet
                        
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp640TudOk  ³ Autor ³Tatiane Matias       ³ Data ³01/08/05  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica o Tudo OK                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³GPEA640                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gp640TudOk()

Local lRet := .T.

//Se o periodo estiver fechado nao podera efetuar exclusao
If ( !Empty(RCH->RCH_DTFECH) .and. (nOption == 5))
	MsgAlert( OemToAnsi(STR0026), OemToAnsi(STR0017) )		//Periodo Fechado
	lRet := .F.
Else
	lRet := Gp640LinOk()
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp640VerifAlt³ Autor ³Tatiane Matias       ³ Data ³01/08/05  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se houve alguma alteracao na getdados               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³GPEA640                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gp640VerifAlt(cAlias,cFil,cMat,nOpc,bFica,lBotaoOK)

DEFAULT lBotaoOK := .F.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se mudou as informacoes de periodo, nro pagamento, roteiro,  ³
	//³matricula, verifica se os dados foram alterados e pergunta se³
	//³deseja salvar as alteracoes                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (cPeriodo#cPerAnt) .or. (cNumPagto#cSemAnt) .or. (cRoteiro#cRotAnt) .or. (cMat#SRA->RA_MAT) .or. (lBotaoOK)
		If !ArrayCompare( oGet:aCols , aColsAnt )
			If (MsgYesNo( OemToAnsi( STR0020 ) ) )	//"Deseja salvar as alteracoes?"
				Gpea640Grava(cAlias, cFil, cMat, nOpc, .F.)
			EndIf
		EndIf
		If !Empty(bFica)
			Gp640Movimenta(bFica)
		EndIf    

		cPerAnt := cPeriodo                                                                           
		cSemAnt := cNumPagto
		cRotAnt := cRoteiro
	EndIf
	
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gpea640Grava³ Autor ³ Tatiane Matias      ³ Data ³01/08/05  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grava os dados na tabela RCR           	                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³GPEA640                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gpea640Grava(cAlias, cFil, cMat, nOpc, lAtualizGet)
	Local nLenRegAlt	:= Len(aColsRec)
	Local nX			:= 0
	Local nY			:= 0
	Local nPosDeleted	:= GdFieldPos( "GDDELETED" )
	Local nPosRecno		:= GdFieldPos( "RGB_REC_WT", aGhHeader )
	Local nLenHeader	:= Len( aHeader )
	Local nLenGhHeader	:= 0
	Local nLenCols		:= 0
	Local cCampo		:= ""
	Local nSequencia	:= 0
	Local cGhNoFields	:= "RGB_FILIAL/RGB_MAT/RGB_PROCES/RGB_PERIOD/RGB_ROTEIR/RGB_SEMANA"	
	Local nHoras
	Local nPosReg		:= 0
	Local aDias			:= {}		
	
	If cPaisLoc $ "ARG|MEX|CHI"	
		If MV_MODFOL=="2"                	
			AAdd(aDias, GdFieldPos("RGB_DUM"))
			AAdd(aDias, GdFieldPos("RGB_DDOIS"))
			AAdd(aDias, GdFieldPos("RGB_DTRES"))
			AAdd(aDias, GdFieldPos("RGB_DQUATR"))
			AAdd(aDias, GdFieldPos("RGB_DCINCO"))
			AAdd(aDias, GdFieldPos("RGB_DSEIS"))
			AAdd(aDias, GdFieldPos("RGB_DSETE"))
			
			//cGhNoFields+= "/RGB_HORAS"			
		/*Else		
			AAdd(aDias, GdFieldPos("RCR_DIA1"))
			AAdd(aDias, GdFieldPos("RCR_DIA2"))
			AAdd(aDias, GdFieldPos("RCR_DIA3"))
			AAdd(aDias, GdFieldPos("RCR_DIA4"))
			AAdd(aDias, GdFieldPos("RCR_DIA5"))
			AAdd(aDias, GdFieldPos("RCR_DIA6"))
			AAdd(aDias, GdFieldPos("RCR_DIA7"))*/
		EndIf
	EndIf
		
	If MV_MODFOL == "2"                
		nLenGhHeader	:= Len( aGhHeader )
		If ( nOpc != 5 ) 
			If ArrayCompare(oGet:aCols, aColsAnt)
				Return .T.
			EndIf
		EndIf
	EndIf     
	
	aCols 		:= ( oGet:aCols )
	nLenCols	:= Len( aCols )
	Begin Transaction
		If ( nOpc == 5 ) //Exclusao
			PonDelRecnos( cAlias , aColsRec )
			( cAlias )->( EvalTrigger() )
		Else //Inclusao ou Alteracao
			If ( nLenRegAlt > 0 )
				For nX := 1 To nLenRegAlt
					(cAlias)->( dbGoto( aColsRec[nX] ) )
					(cAlias)->( RecLock( cAlias , .F. , .T. ) )
	
					IF ( aCols[nX,nPosDeleted] )
						(cAlias)->(dbDelete())
					Else
						nSequencia++
						(cAlias)->&(cAlias+"_FILIAL")	:= cFil
						(cAlias)->&(cAlias+"_MAT")		:= cMat
						(cAlias)->&(cAlias+"_PROCES")	:= cProcesso
						(cAlias)->&(cAlias+"_PERIOD	")	:= cPerAnt
					          
						If (MV_MODFOL=="2")                            
							RGB->RGB_SEMANA	:= cSemAnt
						   
							If cPaisLoc $ "ARG|MEX|CHI"
								nHoras:= 0		
								AEval(aDias, {|nPos| nHoras+= aCols[nX, nPos] } )
															
								RGB->RGB_HORAS	:= nHoras
							EndIf
						Else
							RCR->RCR_NPAGTO	:= cSemAnt
							
						EndIf					
						
						(cAlias)->&(cAlias+"_ROTEIR")	:= cRotAnt
						For nY := 1 To nLenHeader
							/*
							ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							³Obtem o Campo e o COnteudo para Gravacao  				       ³
							ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
							cCampo 		:= aHeader[nY,2]
							If cCampo = cAlias+"_SEQ" .AND. cAlias!="RGB"
								xConteudo := nSequencia
							Else
								xConteudo 	:= aCols[nX,nY]
							EndIf
							
							/*
							ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							³Nao Grava Campo Virtual									   ³
							ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/ 
							If MV_MODFOL == "2"
								If ( aScan(aGhVirtual,cCampo) # 0 )
									Loop
								EndIf
							Else
								If ( aScan(aVirtual,cCampo) # 0 )
									Loop
								EndIf                                       
							End If
							
							(cAlias)->( &cCampo ) := xConteudo
		
						Next nY  
						
						//Se for folha mexico grava os demais campos nao exibidos
						//na tela mais contidos na tabela RGB
						If MV_MODFOL=="2"
							For nY := 1 To nLenGhHeader
								cCampo := aGhHeader[nY,2]
								If (											;
										(aScan(aHeader,{ |x| x[2]==cCampo})==0	);
										.And. ( aScan(aGhVirtual,cCampo)== 0 ) ;
										.And. !(cCampo $ cGhNoFields);
									)
									 
										nPosReg := aScan( aGhCols, { |x| x[nPosRecno] == (cAlias)->(Recno()) } )
										If ( nPosReg == 0 )
											If	cCampo == "RGB_TIPO2" 
												xConteudo := "I"
											ElseIf cCampo == "RGB_DTREF"
												xConteudo := cDataFim
											ElseIf cCampo == "RGB_PARCEL"
												xConteudo := 1
											Else
												If aGhHeader[ nY, __AHEADER_TYPE__ ] == "N"
													xConteudo := Val(aGhHeader[ nY, __AHEADER_INITPAD__ ])
												Else
													xConteudo := aGhHeader[ nY, __AHEADER_INITPAD__ ]								
												EndIf								
											EndIf
										Else
											If	cCampo == "RGB_TIPO2" 
												xConteudo := "I"
											Else
												xConteudo := aGhCols[ nPosReg, nY ]
											EndIf
										EndIf
										(cAlias)->( &cCampo ) := If( "->" $ cValToChar(xConteudo), &xConteudo, xConteudo )
								EndIf
							Next nY					
						EndIf
					EndIF
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³Destrava o Registro										   ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					( cAlias )->( MsUnLock() )
				Next nX
				If ( nLenCols > nLenRegAlt ) // Se Trata de Inclusao de Itens
					Gp640GrInc(cAlias, cFil, cMat, (nLenRegAlt + 1), nSequencia)
				EndIf
			Else
				Gp640GrInc(cAlias, cFil, cMat, 1, 0)
			EndIf
			( cAlias )->( EvalTrigger() )
		EndIf
		aColsAnt := aClone(aCols)
	
		// atualiza a getdados
		If lAtualizGet     
			a640MontaCols(cAlias,SRA->RA_FILIAL,cMat)
		EndIf
	End Transaction  
	
	If lResM2  //Tratamento para chamada da rotina atraves de tela de rescisão (Modelo 2)
		lM2Modif:= .T.
	Endif
	
Return (.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp640GrInc  ³ Autor ³Tatiane Matias       ³ Data ³ 01/08/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grava os registros Incluidos na GetDados	                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Gpea640                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gp640GrInc(cAlias, cFil, cMat, nIniciar, nSequencia)
	Local nX			:= 0
	Local nY 			:= 0
	Local nLenHeader	:= Len( aHeader )
	Local nLenGhHeader	:= 0
	Local nLenCols		:= Len( aCols )
	Local nPosDeleted	:= GdFieldPos( "GDDELETED" )
	Local cCampo		:= ""
	Local cGhNoFields	:= "RGB_FILIAL/RGB_MAT/RGB_PROCES/RGB_PERIOD/RGB_ROTEIR/RGB_SEMANA"
	Local nHoras	
	Local aDias			:= {}		  
	



	
	If cPaisLoc $ "ARG|MEX|CHI"	
		If MV_MODFOL=="2"                	
			AAdd(aDias, GdFieldPos("RGB_DUM"))
			AAdd(aDias, GdFieldPos("RGB_DDOIS"))
			AAdd(aDias, GdFieldPos("RGB_DTRES"))
			AAdd(aDias, GdFieldPos("RGB_DQUATR"))
			AAdd(aDias, GdFieldPos("RGB_DCINCO"))
			AAdd(aDias, GdFieldPos("RGB_DSEIS"))
			AAdd(aDias, GdFieldPos("RGB_DSETE"))
			
			//cGhNoFields+= "/RGB_HORAS"
		//Else		
		//	AAdd(aDias, GdFieldPos("RCR_DIA1"))
		//	AAdd(aDias, GdFieldPos("RCR_DIA2"))
		//	AAdd(aDias, GdFieldPos("RCR_DIA3"))
		//	AAdd(aDias, GdFieldPos("RCR_DIA4"))
		//	AAdd(aDias, GdFieldPos("RCR_DIA5"))
		//	AAdd(aDias, GdFieldPos("RCR_DIA6"))
		//	AAdd(aDias, GdFieldPos("RCR_DIA7"))
		EndIf
	EndIf
                        
	If MV_MODFOL=="2"
		nLenGhHeader := Len( aGhHeader )
	EndIf  
	
	For nX := nIniciar To nLenCols
		IF !( aCols[nX,nPosDeleted] )
			(cAlias)->( RecLock(cAlias,.T.) )
			nSequencia++
			(cAlias)->&(cAlias+"_FILIAL")	:= cFil
			(cAlias)->&(cAlias+"_MAT")		:= cMat
			(cAlias)->&(cAlias+"_PROCES")	:= cProcesso
			(cAlias)->&(cAlias+"_PERIOD")	:= cPerAnt
			
			If MV_MODFOL=="2"             
				RGB->RGB_SEMANA	:= cSemAnt
	
				If cPaisLoc $ "ARG|MEX|CHI"
					nHoras:= 0		
					AEval(aDias, {|nPos| nHoras+= aCols[nX, nPos] } )
												
					RGB->RGB_HORAS	:= nHoras
				EndIf
			Else
				RCR->RCR_NPAGTO	:= cSemAnt
			EndIf
			
			(cAlias)->&(cAlias+"_ROTEIR")	:= cRotAnt
			For nY := 1 To nLenHeader
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Obtem o Campo para Gravacao  							       ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cCampo 		:= aHeader[nY,2]
				If cCampo = cAlias+"_SEQ" .AND. cAlias != "RGB"
					xConteudo := nSequencia
				Else
					xConteudo 	:= aCols[nX,nY]
				EndIf
	
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Nao Grava Campo Virtual									   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				IF ( aScan(aVirtual,cCampo) # 0 )
					Loop
				EndIF
				(cAlias)->( &cCampo ) := xConteudo
			Next nY 
			
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Grava os demais campos para a tabela RGB, caso necessario     |
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/		
			If MV_MODFOL=="2"
				For nY := 1 To nLenGhHeader
					cCampo := aGhHeader[nY,2]
					If (											;
							(aScan(aHeader,{ |x| x[2]==cCampo})==0	);
							.And. (aScan(aGhVirtual,cCampo)== 0 ) ;
							.And. !(cCampo $ cGhNoFields);
						)
							If	cCampo == "RGB_TIPO2" 
								xConteudo := "I"
							ElseIf cCampo == "RGB_DTREF"
								xConteudo := cDataFim
							ElseIf cCampo == "RGB_PARCEL"
								xConteudo := 1
							Else
								If aGhHeader[ nY, __AHEADER_TYPE__ ] == "N"
									xConteudo := Val(aGhHeader[ nY, __AHEADER_INITPAD__ ])
								Else
									xConteudo := aGhHeader[ nY, __AHEADER_INITPAD__ ]								
								EndIf								
							EndIf
							(cAlias)->( &cCampo ) := If( "->" $ cValToChar(xConteudo), &xConteudo, xConteudo )
					EndIf
				Next nY					           
			EndIf
			
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Destrava o Registro										   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			( cAlias )->( MsUnLock() )
		EndIF
	Next nX

Return (.T.)  

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ MenuDef		³Autor³  Luiz Gustavo     ³ Data ³04/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Isola opcoes de menu para que as opcoes da rotina possam    ³
³          ³ser lidas pelas bibliotecas Framework da Versao 9.12 .      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA080                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/         

Static Function MenuDef()	

	Local aRotina :=     {;
							{ STR0001 , "PesqBrw" 	, 0 , 1},;	//"Pesquisar" 
							{ STR0002 , "gp640Mnt"	, 0 , 2},;	//"Visualizar"
							{ STR0003 , "gp640Aux"	, 0 , 4},;	//"Alterar"
							{ STR0004 , "gp640Mnt"	, 0 , 4},;	//"Alterar"
							{ STR0005 , "gp640Mnt"	, 0 , 5},;	//"Excluir"
							{ STR0022 , "gper590" 	, 0 , 6},;	//"Imprimir"
							{ STR0023 , "GpLegend"	, 0 , 7} ;  //"Legenda"
						 }	 
Return aRotina

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEA640   ºAutor  ³Valdeci Lira        º Data ³  04/08/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica a existencia do indice em SIX para poder executar º±±
±±º          ³a validacao de super filtro corretamente                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ExistSixRGB()
	Local lRet := .F.
	Local aArea := getArea()
	Local cMsg := ""               
	If cPaisLoc == "BRA"
		lRet := .T.
	Else
		dbSelectArea("SIX")
		SIX->(dbSetOrder(1))	
		lRet := SIX->(dbSeek("RGBC"))  
	EndIf  
	If (!lRet)      
		cMsg := OemToAnsi(STR0027) + CRLF //"Foi aplicado o Path de Restrição de acesso e nao foi executado o"
		cMsg += OemToAnsi(STR0028) + CRLF //"programa de Atualizacao de Bases (RHUPDMOD)!" 
		cMsg += OemToAnsi(STR0029)			//"Consulte o Administrador do Sistema." 
		msgAlert(cMsg, OemToAnsi(STR0017))//"Atenção"                                                                                                                                                                                                                                                                                                                                                                                                                                          
	endif
	
	restArea(aArea)
Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp640ValCpos ºAutor  ³Marcelo Silveira º Data ³  21/03/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Validacao dos campos digitados na GetDados.                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA110                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gp640ValCpos()
                   
Local nHoras		:= 0
Local nPosHoras		:= 0
Local cCposHoras	:= "RGB_DUM|RGB_DDOIS|RGB_DTRES|RGB_DQUATR|RGB_DCINCO|RGB_DSEIS|RGB_DSETE"
Local aDias			:= {}

If cPaisLoc $ "ARG|MEX|CHI"

	If SubStr(ReadVar(),4,10) $ cCposHoras
	
		nPosHoras := GdFieldPos("RGB_HORAS")
		
		AAdd(aDias, GdFieldPos("RGB_DUM"))  
		AAdd(aDias, GdFieldPos("RGB_DDOIS"))
		AAdd(aDias, GdFieldPos("RGB_DTRES"))
		AAdd(aDias, GdFieldPos("RGB_DQUATR"))
		AAdd(aDias, GdFieldPos("RGB_DCINCO"))
		AAdd(aDias, GdFieldPos("RGB_DSEIS"))
		AAdd(aDias, GdFieldPos("RGB_DSETE"))
	
		AEval(aDias, {|nPos| nHoras += If( nPos # oGet:oBrowse:ColPos, aCols[n, nPos], 0 ) } )
		nHoras += &(ReadVar())
		
		//Atualiza o campo RGB_HORAS se ao menos um campo diario tiver valor
		If nHoras > 0
			aCols[n, nPosHoras]:= nHoras
		EndIf
		
	EndIf
	
EndIf
	
Return(.T.)


/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ gp640Aux		³Autor³  Gustavo M.	      ³ Data ³22/08/2012³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Utilizada para identificar quando é selecionado a opçao de  ³
³          ³incluir    													³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA640                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³gp640Mnt													³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function gp640Aux(cAlias,nReg,nOpcX)

lIncluir := .T.   

Return(gp640Mnt(cAlias,nReg,nOpcX))  
