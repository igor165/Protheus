#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPER650.CH" 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER650  ³ Autor ³ Flor Monroy           ³ Data ³  10.05.2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Informe mensual SUA                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ (void)                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ gsantacruz ³ 12/10/11 ³ Mej  ³ Correcciones a espaciados                ³±±
±±³            ³          ³ Corr ³ Valor de Sal Min DF                      ³±±
±±³ flor.monroy³ 22/11/11 ³ Mej  ³ Correcciones a ordenar por Nombre        ³±±
±±³            ³          ³ Corr ³ por el campo RA_NOME.                    ³±±
±±³ gsantacruz ³ 13/12/11 ³ Corr ³ Dias traba = Dias trab-incap-faltas      ³±±
±±³            ³          ³ Mej  ³ No debe imprimir fecha si tipo mov ='00' ³±±
±±³ flor.monroy³ 05/01/12 ³ Corr ³ Correcion de formato a fecha vacia.      ³±±
±±³ flor.monroy³ 05/01/12 ³ Corr ³ Correcion quitar de filtro  de query     ³±±
±±³            ³          ³ Corr ³ RHC_CFPAT <> 0                           ³±±
±±³ flor.monroy³ 29/02/12 ³ Corr ³ Cambios en la mascara RHC_NDTRAB         ³±±
±±³            ³          ³ Corr ³                                          ³±±
±±³R.Berti     ³13/09/2012³TFTLRJ³Creacion del fuente GPER650MEX;		      ³±±
±±³            ³          ³      ³Se corrije funciones para estaticas.      ³±±
±±³M.Camargo   ³28/10/15  ³TTQUO9³Se elimina fuente GPER650MEX quedando sola³±±
±±³            ³          ³      ³mente este fuente como el válido.         ³±±
±±³            ³          ³      ³Se cambia a function GPER65001.           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/


Function GPER650()

Local	oReport   
Local	aArea 	:= GetArea()
Local 	nTamReg	:= TamSX3("RCO_CODIGO")[1]
Local   nCont	:= 0

Private cNomeProg:= "GPER650"
Private cPerg    := "GPR650" 
Private _cPar01  := '' //Mes/Anio
Private _cPar02  := '' //Registro Patronal
Private _cPar03  := '' //Tipo de Reporte
Private cLisPat  := ""

If !TRepInUse()	//verifica se a opção relatórios personalizáveis está disponível
	MsgStop(OemToAnsi(STR0083) )//"Para usar este inform debe tener MP8 R4 en adelante!"
	RestArea(aArea)
	Return
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Pergunte(cPerg,.T.) 

//	_cPar01 := Val(MV_PAR01) //Mes/Anio
	_cPar01 := MV_PAR01 //Mes/Anio	
	_cPar02 := alltrim(MV_PAR02) //Registro Patronal
	_cPar03 := MV_PAR03 //Tipo de Reporte
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Genera lista de registros patronales para usar despues en Query³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	For nCont := 1 To Len( _cPar02 ) Step nTamReg
	    cLisPat += "'" + SubStr( _cPar02 , nCont , nTamReg ) + "',"
	Next       
	cLisPat := Substr(cLisPat,1,Len(cLisPat)-1)

	If _cPar03 == 1 //Informe Analitico
		oReport := ReportDef()
	Else  //Informe Resumén
		oReport := ReportDef_hor()
	EndIf
	oReport:PrintDialog()

EndIf

RestArea(aArea)

Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef  ³ Autor ³ FMonroy               ³ Data ³12/01/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³  Relatorio de Dotacao                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

Local aArea      := GetArea() 

Local oReport
Local oSection1
Local oSection2
Local oSection3
Local oSection4
Local oSection5
Local oSection6
Local oSection7

Private cTitulo	:=OEMTOANSI(STR0082) //"CEDULA DE DETERMINACION DE CUOTAS OBRERO-PATRONALES"
Private aOrd    := {OemToAnsi(STR0103),OemToAnsi(STR0006),OemToAnsi(STR0004)}	//"Apellido paterno" "Nombre Completo"###""Nombre"###"Matricula"

cTitulo := Trim(cTitulo)
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Criacao do componente de impressao                                      ³
³TReport():New                                                           ³
³ExpC1 : Nome do relatorio                                               ³
³ExpC2 : Titulo                                                          ³
³ExpC3 : Pergunte                                                        ³
³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
³ExpC5 : Descricao                                                       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
oReport := TReport():New(cNomeProg,OemToAnsi(cTitulo),/* cNomeProg */ ,{|oReport| PrintReport(oReport)})

oReport:SetPortrait()     	// Define a orientacao de pagina do relatorio como retrato.
oReport:nColSpace 	:= 1
oReport:nFontBody	:= 5 	// Define o tamanho da fonte.
oReport:nLineHeight	:= 40 	// Define a altura da linha.
oReport:cFontBody	:= "COURIER NEW"
oReport:Setlandscape(.T.)	//Pag Horizontal
oReport:SetTotalInLine(.F.)
 
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Criacao da celulas da secao do relatorio                                ³
³TRCell():New                                                            ³
³ExpO1 : Objeto TSection que a secao pertence                            ³
³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
³ExpC3 : Nome da tabela de referencia da celula                          ³
³ExpC4 : Titulo da celula                                                ³
³        Default : X3Titulo()                                            ³
³ExpC5 : Picture                                                         ³
³        Default : X3_PICTURE                                            ³
³ExpC6 : Tamanho                                                         ³
³        Default : X3_TAMANHO                                            ³
³ExpL7 : Informe se o tamanho esta em pixel                              ³
³        Default : False                                                 ³
³ExpB8 : Bloco de código para impressao.                                 ³
³        Default : ExpC2                                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Criacao da Primeira Secao: Encabezado                                  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
oSection1 := TRSection():New(oReport,OemToAnsi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/)
oSection1:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
oSection1:SetLineStyle(.t.)   	//Pone titulo del campo y aun lado el y valor
oSection1:SetCharSeparator(" ")    
oSection1:SetHeaderBreak(.T.) 
oSection1:nColSpace:=5   


TRCell():New(oSection1,"RCO_NREPAT","QRYRHD"	, STR0012 , PesqPict("RHD","RCO_NREPAT"), TamSx3("RCO_NREPAT")[1])	//Registro Patronal
TRCell():New(oSection1,"SM0_CGC","QRYRHD" 		, STR0013 , ,TamSx3("RA_CIC")[1])	//"RFC:"                                              
TRCell():New(oSection1,"RCO_ATIVID","QRYRHD"	, STR0102  , PesqPict("RCO","RCO_ATIVID"), TamSx3("RCO_ATIVID")[1])	//"Actividad: "
TRCell():New(oSection1,"RCO_CVEZON","QRYRHD"	, STR0015 , PesqPict("RCO","RCO_CVEZON"), TamSx3("RCO_CVEZON")[1])	//"Area Geografica:  "
oSection1:Cell("RCO_CVEZON"):SetCellBreak(.T.)
TRCell():New(oSection1,"RCO_NOME",	 	"QRYRHD", STR0016, PesqPict("RCO","RCO_NOME"),	TamSx3("RCO_NOME")[1])	//"Nombre o Razón Social: "
oSection1:Cell("RCO_NOME"):SetCellBreak(.T.)
TRCell():New(oSection1,"RHD_FATRSC",	"QRYRHD", STR0018, PesqPict("RHD","RHD_FATRSC"),TamSx3("RHD_FATRSC")[1]) //"Prima de R.T.: "
TRCell():New(oSection1,"MESANO",	 	"QRYRHD", STR0019,, 10)//"Mes y Año de Proceso" 

//Seccion 2 sin uso 
oSection2 := TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/)
//TRCell():New(oSection2,"RHD_FATRSC",	"QRYRHD", STR0018, PesqPict("RHD","RHD_FATRSC"),TamSx3("RHD_FATRSC")[1]) //"Prima de R.T.: "
//TRCell():New(oSection2,"MESANO",	 	"QRYRHD", STR0019,, 10)//"Mes y Año de Proceso" 
  
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Criacao da 3ra.     Secao:Encabezado Empleado                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ */
oSection3 := TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection3:SetTotalInLine(.F.)  
oSection3:SetHeaderSection(.T.)	//Exibe Cabecalho da Secao
oSection3:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
oSection3:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor
oSection3:nColSpace := 10

TRCell():New(oSection3, "RA_RG",	"QRYRHD",	STR0021, PesqPict("SRA","RA_RG"),	TamSx3("RA_RG")[1]+3)	//"Numero de Seguridad Social"
TRCell():New(oSection3, "NOMECOM",		    ,	STR0022, PesqPict("SRA","RA_NOME"),	TamSx3("RA_NOME")[1]+10)//"Nombre"
TRCell():New(oSection3, "RA_CIC",	"QRYRHD",	STR0024, PesqPict("SRA","RA_CIC"),	TamSx3("RA_CIC")[1])	//"R.F.C."
TRCell():New(oSection3, "RA_CURP",	"QRYRHD",	STR0025, PesqPict("SRA","RA_CURP"),	TamSx3("RA_CURP")[1])	//"C.U.R.P."

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Criacao da 4Ta.     Secao: Detalle Empleado                              ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
oSection4 := TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection4:SetTotalInLine(.F.)   
oSection4:SetTotalText(" ")  
oSection4:SetHeaderSection(.T.)	//Exibe Cabecalho da Secao
oSection4:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
oSection4:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor

TRCell():New(oSection4,"RHC_TPMOV",  "QRYRHD", STR0026, PesqPict("RHC","RHC_TPMOV"),	5)//Clave
TRCell():New(oSection4,"RHC_DTMOV",  "QRYRHD", STR0027, PesqPict("RHC","RHC_DTMOV"),	TamSx3("RHC_DTMOV")[1],,/*{||IIF((cAliasRHD)->RHC_TPMOV<>'00',(cAliasRHD)->RHC_DTMOV,CTOD(''))}*/)	//Fecha
TRCell():New(oSection4,"RHC_NDTRAB", "QRYRHD", STR0028, PesqPict("RHC","RHC_NDTRAB"),	5,,/*{||(cAliasRHD)->RHC_NDTRAB-(cAliasRHD)->RHC_NDINC-(cAliasRHD)->RHC_NDFAL}*/)//"Dias"
TRCell():New(oSection4,"RHC_SALDII", "QRYRHD", STR0029, PesqPict("RHC","RHC_SALDII"),	TamSx3("RHC_SALDII")[1])//SDI
TRCell():New(oSection4,"RHC_NDINC",  "QRYRHD", STR0084, PesqPict("RHC","RHC_NDINC"),	TamSx3("RHC_NDINC")[1])	//Inc.
TRCell():New(oSection4,"RHC_NDFAL",  "QRYRHD", STR0085, PesqPict("RHC","RHC_NDFAL"),	TamSx3("RHC_NDFAL")[1])	//"Aus." 
TRCell():New(oSection4,"RHC_CFPAT",  "QRYRHD", STR0031, PesqPict("RHC","RHC_CFPAT"),	TamSx3("RHC_CFPAT")[1])	//cf.
TRCell():New(oSection4,"RHC_EXEPAT", "QRYRHD", STR0086, PesqPict("RHC","RHC_EXEPAT"),	TamSx3("RHC_EXEPAT")[1])//Enfermedad y Maternidad, Excedente Pat.
TRCell():New(oSection4,"RHC_EXETRA", "QRYRHD", STR0087, PesqPict("RHC","RHC_EXETRA"),	TamSx3("RHC_EXETRA")[1])//Enfermedad y Maternidad, Excedente Pat.
TRCell():New(oSection4,"RHC_PDPAT",  "QRYRHD", STR0088, PesqPict("RHC","RHC_PDPAT"),	TamSx3("RHC_PDPAT")[1])	//P.D. Pat
TRCell():New(oSection4,"RHC_PDTRA",  "QRYRHD", STR0089, PesqPict("RHC","RHC_PDTRA"),	TamSx3("RHC_PDTRA")[1])	//P.D. Obr.
TRCell():New(oSection4,"RHC_GMPPAT", "QRYRHD", STR0090, PesqPict("RHC","RHC_GMPPAT"),	TamSx3("RHC_GMPPAT")[1])//G.M.P. Pat.
TRCell():New(oSection4,"RHC_GMPTRA", "QRYRHD", STR0091, PesqPict("RHC","RHC_GMPTRA"),	TamSx3("RHC_GMPTRA")[1])//G.M.P. Obr.
TRCell():New(oSection4,"RHC_RTPAT",  "QRYRHD", STR0035, PesqPict("RHC","RHC_RTPAT"),	TamSx3("RHC_RTPAT")[1])	//R.T.	
TRCell():New(oSection4,"RHC_IVPAT",  "QRYRHD", STR0092, PesqPict("RHC","RHC_IVPAT"),	TamSx3("RHC_IVPAT")[1])	//I.V Pat.
TRCell():New(oSection4,"RHC_IVTRA",  "QRYRHD", STR0093, PesqPict("RHC","RHC_IVTRA"),	TamSx3("RHC_IVTRA")[1])	//I.V. Obr.
TRCell():New(oSection4,"RHC_GPSPAT", "QRYRHD", STR0037, PesqPict("RHC","RHC_GPSPAT"),	TamSx3("RHC_GPSPAT")[1])//G.P.S.

TRCell():New(oSection4,"SUMAPAT",	"QRYRHD",	STR0094,	"999,999,999,999.99", 15)//Suma Pat.
TRCell():New(oSection4,"SUMAOBR",	"QRYRHD",	STR0095,	"999,999,999,999.99", 15)//Suma Obr.
TRCell():New(oSection4,"TOTAL",,			  	STR0039,	"999,999,999,999.99", 15)//Total

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Criacao da 5ta.     Secao: Totales por Reg. Pat.                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
oSection5 := TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection5:SetTotalInLine(.F.) 
  
oSection5:SetTotalText(" ")  
oSection5:SetHeaderSection(.F.)	//Exibe Cabecalho da Secao
oSection5:SetHeaderPage(.F.)	//Exibe Cabecalho da Secao
oSection5:SetLineStyle(.F.)   	//Pone titulo del campo y aun lado el y valor

TRCell():New(oSection5, "RHC_TPMOV",,	STR0026, PesqPict("RHC","RHC_TPMOV"),	5)//Clave
TRCell():New(oSection5, "RHC_DTMOV",,	STR0027, PesqPict("RHC","RHC_DTMOV"),	TamSx3("RHC_DTMOV")[1])//Fecha
TRCell():New(oSection5, "RHC_NDTRAB",,	STR0028, "@E 99999", 	/*TamSx3("RHC_NDTRAB")[1]*/5)//"Dias"
TRCell():New(oSection5, "RHC_SALDII",,	STR0029, PesqPict("RHC","RHC_SALDII"),	TamSx3("RHC_SALDII")[1])//SDI
TRCell():New(oSection5, "RHC_NDINC",,	STR0084, PesqPict("RHC","RHC_NDINC"),	TamSx3("RHC_NDINC")[1])//Inc.
TRCell():New(oSection5, "RHC_NDFAL",,	STR0085, PesqPict("RHC","RHC_NDFAL"),	TamSx3("RHC_NDFAL")[1])//"Aus." 
TRCell():New(oSection5, "RHC_CFPAT",,	STR0031, PesqPict("RHC","RHC_CFPAT"),	TamSx3("RHC_CFPAT")[1])//cf.
TRCell():New(oSection5, "RHC_EXEPAT",,	STR0086, PesqPict("RHC","RHC_EXEPAT"),	TamSx3("RHC_EXEPAT")[1])//Enfermedad y Maternidad, Excedente Pat.
TRCell():New(oSection5, "RHC_EXETRA",,	STR0087, PesqPict("RHC","RHC_EXETRA"),	TamSx3("RHC_EXETRA")[1])//Enfermedad y Maternidad, Excedente Obr.
TRCell():New(oSection5, "RHC_PDPAT",,	STR0088, PesqPict("RHC","RHC_PDPAT"),	TamSx3("RHC_PDPAT")[1])//P.D. Pat
TRCell():New(oSection5, "RHC_PDTRA",,	STR0089, PesqPict("RHC","RHC_PDTRA"),	TamSx3("RHC_PDTRA")[1])//P.D. Obr.
TRCell():New(oSection5, "RHC_GMPPAT",,	STR0090, PesqPict("RHC","RHC_GMPPAT"),	TamSx3("RHC_GMPPAT")[1])//G.M.P. Pat.
TRCell():New(oSection5, "RHC_GMPTRA",,	STR0091, PesqPict("RHC","RHC_GMPTRA"),	TamSx3("RHC_GMPTRA")[1])//G.M.P. Obr.
TRCell():New(oSection5, "RHC_RTPAT",,	STR0035, PesqPict("RHC","RHC_RTPAT"),	TamSx3("RHC_RTPAT")[1])//R.T.	
TRCell():New(oSection5, "RHC_IVPAT",,	STR0092, PesqPict("RHC","RHC_IVPAT"),	TamSx3("RHC_IVPAT")[1])//I.V Pat.
TRCell():New(oSection5, "RHC_IVTRA",,	STR0093, PesqPict("RHC","RHC_IVTRA"),	TamSx3("RHC_IVTRA")[1])//I.V. Obr.
TRCell():New(oSection5, "RHC_GPSPAT",,	STR0037, PesqPict("RHC","RHC_GPSPAT"),	TamSx3("RHC_GPSPAT")[1])//G.P.S.
TRCell():New(oSection5, "SUMAPAT",,		STR0094, "999,999,999,999.99", 15)//Suma Pat."
TRCell():New(oSection5, "SUMAOBR",,		STR0095, "999,999,999,999.99", 15)//Suma Obr." 
TRCell():New(oSection5, "TOTAL",,		STR0039, "999,999,999,999.99", 15)//Total

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Criacao da 6ta.     Secao: Total Grles.                                  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
oSection6 := TRSection():New(oReport, OemToAnsi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection6:SetTotalInLine(.F.)  
oSection6:SetHeaderSection(.T.)	//Exibe Cabecalho da Secao
oSection6:SetHeaderPage(.F.)	//Exibe Cabecalho da Secao
oSection6:SetLineStyle(.t.)   	//Pone titulo del campo y aun lado el y valor
oSection6:SetCharSeparator(":")    
oSection6:SetHeaderBreak(.T.)      
oSection6:nColSpace := 10

TRCell():New(oSection6, "VALTOT",, STR0096, "999,999,999,999.99", 15)
TRCell():New(oSection6, "TOTCOT",, STR0097, "999,999,999,999.99", 15)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Criacao da 7ma.     Secao: Pie de Pag.                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ */
oSection7:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection7:SetHeaderSection(.F.)	//Exibe Cabecalho da Secao
oSection7:SetHeaderPage(.F.)	//Exibe Cabecalho da Secao
oSection7:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor
oSection7:SetHeaderBreak(.F.) 

TRCell():New(oSection7, "DESC1",,,, 40)
TRCell():New(oSection7, "DESC2",,,, 40)
TRCell():New(oSection7, "DESC3",,,, 40)

Return(oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef_hor³ Autor ³ FMonroy             ³ Data ³12/01/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³  Definición del Informe Resumén                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef_hor()

Local aArea      := GetArea() 
Local oReport
Local oSection1
Local oSection2
Local oSection3

Private cTitulo	:= OemToAnsi(STR0082) //"CEDULA DE DETERMINACION DE CUOTAS OBRERO-PATRONALES"
Private aOrd    := {OemToAnsi(STR0006),OemToAnsi(STR0004)}	//"Nombre"###"Matricula"

cTitulo := Trim(cTitulo)

If _cPar03 == 2
	cTitulo := Trim(OemToAnsi(STR0011)) //"RESUMEN DE LIQUIDACION"
Endif	

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Criacao do componente de impressao                                      ³
³TReport():New                                                           ³
³ExpC1 : Nome do relatorio                                               ³
³ExpC2 : Titulo                                                          ³
³ExpC3 : Pergunte                                                        ³
³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
³ExpC5 : Descricao                                                       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
oReport := TReport():New(cNomeProg,OemToAnsi(cTitulo),/* cNomeProg */ ,{|oReport| PrintReport(oReport)})

oReport:nColSpace	:= 1
oReport:nFontBody	:= 6 // Define o tamanho da fonte.
oReport:nLineHeight	:= 45 // Define a altura da linha.
oReport:cFontBody	:= "COURIER NEW" 

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Criacao da celulas da secao do relatorio                                ³
³TRCell():New                                                            ³
³ExpO1 : Objeto TSection que a secao pertence                            ³
³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
³ExpC3 : Nome da tabela de referencia da celula                          ³
³ExpC4 : Titulo da celula                                                ³
³        Default : X3Titulo()                                            ³
³ExpC5 : Picture                                                         ³
³        Default : X3_PICTURE                                            ³
³ExpC6 : Tamanho                                                         ³
³        Default : X3_TAMANHO                                            ³
³ExpL7 : Informe se o tamanho esta em pixel                              ³
³        Default : False                                                 ³
³ExpB8 : Bloco de código para impressao.                                 ³
³        Default : ExpC2                                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Criacao da Primeira Secao: Encabezado del Reg. Pat.                    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
oSection1 := TRSection():New(oReport, OemToAnsi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/)

oSection1:SetLineStyle(.t.)   //Pone titulo del campo y aun lado el y valor
oSection1:SetReadOnly() 
oSection1:SetCharSeparator(" ")    
oSection1:SetHeaderBreak(.T.)
oSection1:SetLeftMargin(25)
oSection1:nColSpace := 1

TRCell():New(oSection1, "MESANO",      "QRYRHD",   STR0019,, 10)//"Mes y Año de Proceso"
oSection1:Cell("MESANO"):SetCellBreak(.T.)
TRCell():New(oSection1, "RCO_NREPAT",  "QRYRHD",   STR0012, PesqPict("RCO","RCO_NREPAT"),	TamSx3("RCO_NREPAT")[1])//TamSx3("RCO_NREPAT")[1])//Registro Patronal
TRCell():New(oSection1, "SM0_CGC",     "QRYRHD",   STR0013,,								TamSx3("RA_CIC")[1])//"RFC:"
oSection1:Cell("SM0_CGC"):SetCellBreak(.T.)
TRCell():New(oSection1, "RCO_NOME",    "QRYRHD",   STR0016,,								TamSx3("RCO_NOME")[1])//"Nombre o Razón Social: "
//TRCell():New(oSection1, "RHD_FATRSC",  "QRYRHD",   STR0018,PesqPict("RHD","RHD_FATRSC"),	TamSx3("RHD_FATRSC")[1])//"Prima de R.T.: "


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Criacao da 2da.     Secao:Detalle del Total                              ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
oSection2:= TRSection():New(oReport, OemToAnsi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection2:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor
oSection2:SetHeaderSection(.T.)	//Exibe Cabecalho da Secao
oSection2:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
oSection2:SetLeftMargin(30)
 
TRCell():New(oSection2,"DESC",,		STR0047,, 50)//DESCRIPCION
TRCell():New(oSection2,"IMPORT",,	STR0048, "999,999,999,999.99", 15)//IMPORTE

Return(oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao     ³ PrintReport³ Autor ³ Flor Monroy        ³ Data ³ 10.05.11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descripción³ Función para impresion del Reporte                        ³±± 
±±³           ³                                                           ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxis   ³ PrintReport()                                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport)

Local oSection1  	:= oReport:Section(1)//Encabezado
Local oSection2  	:= oReport:Section(2)//Encabezado Empleado
Local oSection3  	:= oReport:Section(3)//Detalle Empleado
Local oSection4  	:= oReport:Section(4)// Tráiler 
Local oSection5  	:= oReport:Section(5)//Informe Resumén
Local oSection6  	:= oReport:Section(6)//Informe Resumén
Local oSection7  	:= oReport:Section(7)//Pie de Pag.

Local cTitle    	:= "" 
Local cFilRHC		:= xFilial("RHC")
Local cFilRCO		:= xFilial("RCO")
Local cFilSRA		:= xFilial("SRA")
Local cFilRHD		:= xFilial("RHD")
Local cOrdem     	:= "" 
Local cAliasRHD  	:= "QRYRHD" 
Local cCposQuery 	:= ""
Local cCposFrom  	:= ""
Local cWhere 		:= ""
Local cMes			:= SubStr(StrZero( _cPar01,6),1,2)
Local cAnio			:= SubStr(Strzero( _cPar01,6),3,4)
Local cRegPAT    	:=""
Local cGroup  
Local cZona

Local nloop      	:= 0
Local nOrdem	  	:= oSection1:GetOrder()
Local nTotal 		:= 0
Local nTTotal		:= 0
Local nTEmp			:= 0
Local nPageW		:= oReport:PageWidth()//Tamaño de la pagina definida por el usuario
Local nSMGDF 		:= If((fPosTab("S006","A","=",4)) > 0, fTabela("S006", fPosTab("S006","A","=",4),5), 0)

Local aStot			:= {}

Pergunte(oReport:GetParam(),.F.)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³  Altera o Titulo do Relatorio conforme o parametro selecionado   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cTitle 	:= If(AllTrim(oReport:Title())==AllTrim(cTitle), OemToansi(cTitulo), oReport:Title())
oReport:SetTitle(cTitle)

#IFDEF TOP

	//Transforma los parametros de tipo Rango en expresion SQL para ser utilizada en query 
	If _cPar03 ==1
		cCposQuery := "% RCO_NREPAT,RA_NOME,RCO_ATIVID,RCO_CVEZON,RCO_NOME,RCO_ATIVID,RHD_FATRSC,RA_RG,RA_PRISOBR,RA_SECSOBR,RA_PRINOME,RA_SECNOME,RA_CIC,RA_CURP, RHC.*,(RHC_CFPAT + RHC_EXEPAT + RHC_PDPAT + RHC_GMPPAT + RHC_RTPAT + RHC_IVPAT + RHC_GPSPAT) SUMAPAT,(RHC_EXETRA + RHC_PDTRA + RHC_GMPTRA + RHC_IVTRA) SUMAOBR %"
	Else
		cCposQuery := "% RCO_NREPAT,RCO_CVEZON,RCO_NOME,RHD_FATRSC, SUM(RHC_NDINC) SRHC_NDINC ,SUM(RHC_NDFAL) SRHC_NDFAL,SUM(RHC_CFPAT) SRHC_CFPAT,SUM(RHC_EXEPAT + RHC_EXETRA) SRHC_EXPPAT,SUM(RHC_PDPAT + RHC_PDTRA) SRHC_PDPAT,SUM(RHC_GMPPAT + RHC_GMPTRA) SRHC_GMPPAT,SUM(RHC_RTPAT) SRHC_RTPAT, SUM(RHC_IVPAT + RHC_IVTRA) SRHC_IVPAT, SUM(RHC_GPSPAT) SRHC_GPSPAT, (SUM(RHC_NDTRAB)-SUM(RHC_NDFAL)-SUM(RHC_NDINC)) SRHC_NDTRAB, COUNT(DISTINCT RA_MAT) EMP %"
	EndIf
	cCposFrom   := "%" + RetSqlName("RHD") + " RHD, " 
	cCposFrom   +=  RetSqlName("RCO")+ " RCO, "
	cCposFrom   +=  RetSqlName("SRA")+ " SRA, "
	cCposFrom   +=  RetSqlName("RHC")+ " RHC  %"
		
	cWhere :="%        RCO_CODIGO=RHD_CODRPA"
	cWhere +="     AND RA_MAT=RHD_MAT"
	cWhere +="     AND RA_FILIAL=RHD_FILIAL"
	cWhere +="     AND RHD_FILIAL=RHC_FILIAL"
	cWhere +="     AND RHC_MAT=RHD_MAT"
	cWhere +="     AND RHC_ANOMES=RHD_ANOMES"
    cWhere +="     AND RHD_CODRPA IN ("+CLISPAT+")" 
    cWhere +="     AND RHC_ANOMES ='"+cAnio+cMes+"'"
	cWhere +="     AND RHC_FILIAL  ='"+cFilRHC+"'"
	cWhere +="     AND RCO_FILIAL= '" + xFilial("RCO" , cFilRHD) +"' "
	cWhere +="     AND RA_FILIAL  ='"+cFilSRA+"'"
	cWhere +="     AND RHD_FILIAL  ='"+cFilRHD+"'"
	//cWhere +="     AND RHC_CFPAT <> 0"
	
    If ( TcSrvType()=="AS/400" )
		cWhere+="     AND RHD.@DELETED@  = ' ' " 
		cWhere+="     AND RCO.@DELETED@  = ' ' " 
		cWhere+="     AND SRA.@DELETED@  = ' ' " 
		cWhere+="     AND RHC.@DELETED@  = ' ' %" 
	Else 
		cWhere+="     AND RHD.D_E_L_E_T_  = ' ' " 
		cWhere+="     AND RCO.D_E_L_E_T_  = ' ' " 		
		cWhere+="     AND SRA.D_E_L_E_T_  = ' ' " 						
		cWhere+="     AND RHC.D_E_L_E_T_  = ' ' %" 				
	EndIf                   

	If _cPar03 ==1
		If nOrdem == 2
			cOrdem := "%RCO_NREPAT,RA_NOME,RHC_SEQMVT%"
		ElseIf nOrdem == 3
			cOrdem := "%RCO_NREPAT,RCO_CVEZON,RHC_MAT,RHC_SEQMVT%"
		ElseIf nOrdem == 1
			cOrdem := "%RCO_NREPAT,RA_PRISOBR,RA_SECSOBR,RA_PRINOME,RA_SECNOME,RHC_SEQMVT%"
			
			
		EndIf
		
		BeginSql alias cAliasRHD 
			SELECT  %exp:cCposQuery%
			FROM  %exp:cCposFrom% 
			WHERE %exp:cWhere%  
			ORDER BY %exp:cOrdem%
		EndSql
	Else
		cOrdem := "%RCO_NREPAT,RCO_CVEZON,SRHC_NDINC%"
		cGroup := "%RCO_NREPAT,RCO_CVEZON,RCO_NOME,RHD_FATRSC%"
	 	BeginSql alias cAliasRHD 
			SELECT  %exp:cCposQuery%
			FROM  %exp:cCposFrom% 
			WHERE %exp:cWhere% 
			GROUP BY %exp:cGroup%    
			ORDER BY %exp:cOrdem%
		EndSql
	EndIf
   
    //Los campo tipo fecha los deje como tipo fecha, pues por query son recuperados como string
   	If _cPar03 ==1
	 	TCSetField(cAliasRHD,"RHC_DTMOV","D")  
	endif 	

#ELSE

	MsgError(STR0100) //"No esta disponible para DBF"
	
#ENDIF	

Begin Sequence
	DbSelectArea( cAliasRHD )
	Count to nTotal
	oReport:SetMeter(nTotal)
	
	(cAliasRHD)->(DbGoTop())
	If (cAliasRHD)->(!Eof())
		If _cPar03 == 1
		
			oSection1:Init()
  //			oSection2:Init()		
			oSection3:Init()
			oSection4:Init()
			oSection4:hIDE()
			oSection4:PrintLine()
			oSection4:Show()
			While (cAliasRHD)->(!Eof())
				cRegPAT	:=	(cAliasRHD)->RCO_NREPAT   
				cZona	:=	(cAliasRHD)->RCO_CVEZON                         
				oSection1:Cell("SM0_CGC"):SetValue(" "+Alltrim(SM0->M0_CGC))
				oSection1:Cell("MESANO"):SetValue(" "+cMes+"/"+cAnio)//Mes y anio de proceso
				oSection1:PrintLine()
				//Osection2:PrintLine()
				oReport:FatLine()	//Imprime linea
				
				//Inicializa Avumuladores
				aStot	:= {0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0}
				nTTotal	:= 0
				nTEmp	:= 0
				
				While (cAliasRHD)->(!Eof()) .and. cRegPAT == (cAliasRHD)->RCO_NREPAT  
					oSection3:Cell("NOMECOM"):SetValue(rtrim((cAliasRHD)->RA_PRISOBR)+" "+rtrim((cAliasRHD)->RA_SECSOBR)+" "+rtrim((cAliasRHD)->RA_PRINOME)+" "+rtrim((cAliasRHD)->RA_SECNOME))//RFC.
					oSection3:PrintLine()
					cMat := (cAliasRHD)->RHC_MAT
					nTEmp++

					While (cAliasRHD)->(!Eof()).And. cRegPAT == (cAliasRHD)->RCO_NREPAT .and. cMat == (cAliasRHD)->RHC_MAT

						If (cAliasRHD)->RHC_CFPAT <> 0 .Or.(cAliasRHD)->RHC_TPMOV == "02"
							//Total
							oSection4:Init()
							oSection4:Cell("TOTAL"):SetValue((cAliasRHD)->SUMAPAT+(cAliasRHD)->SUMAOBR)
					
							oSection4:Cell("RHC_NDTRAB"):SetValue((cAliasRHD)->RHC_NDTRAB)
							oSection4:Cell("RHC_SALDII"):SetValue((cAliasRHD)->RHC_SALDII)
							oSection4:Cell("RHC_NDINC"):SetValue((cAliasRHD)->RHC_NDINC)
							oSection4:Cell("RHC_NDFAL"):SetValue((cAliasRHD)->RHC_NDFAL)
							oSection4:Cell("RHC_CFPAT"):SetValue((cAliasRHD)->RHC_CFPAT)
							oSection4:Cell("RHC_EXEPAT"):SetValue((cAliasRHD)->RHC_EXEPAT)
							oSection4:Cell("RHC_EXETRA"):SetValue((cAliasRHD)->RHC_EXETRA)
							oSection4:Cell("RHC_PDPAT"):SetValue((cAliasRHD)->RHC_PDPAT)
							oSection4:Cell("RHC_PDTRA"):SetValue((cAliasRHD)->RHC_PDTRA)
							oSection4:Cell("RHC_GMPPAT"):SetValue((cAliasRHD)->RHC_GMPPAT)
							oSection4:Cell("RHC_GMPTRA"):SetValue((cAliasRHD)->RHC_GMPTRA)
							oSection4:Cell("RHC_RTPAT"):SetValue((cAliasRHD)->RHC_RTPAT)
							oSection4:Cell("RHC_IVPAT"):SetValue((cAliasRHD)->RHC_IVPAT)
							oSection4:Cell("RHC_IVTRA"):SetValue((cAliasRHD)->RHC_IVTRA)
							oSection4:Cell("RHC_GPSPAT"):SetValue((cAliasRHD)->RHC_GPSPAT)
							oSection4:Cell("SUMAPAT"):SetValue((cAliasRHD)->SUMAPAT)//SumaPat.
							oSection4:Cell("SUMAOBR"):SetValue((cAliasRHD)->SUMAOBR)// Suma Obr.
							oSection4:Cell("RHC_NDTRAB"):SetValue((cAliasRHD)->RHC_NDTRAB-(cAliasRHD)->RHC_NDINC-(cAliasRHD)->RHC_NDFAL)// Suma Obr.
							oSection4:Cell("RHC_DTMOV"):SetValue(IIF((cAliasRHD)->RHC_TPMOV<>'00',(cAliasRHD)->RHC_DTMOV,CTOD('')))// Suma Obr.
						 	//oSection4:cell("RHC_NDTRAB"):SetBlock({||(cAliasRHD)->RHC_NDTRAB-(cAliasRHD)->RHC_NDINC-(cAliasRHD)->RHC_NDFAL})
							//oSection4:cell("RHC_DTMOV"):SetBlock({||IIF((cAliasRHD)->RHC_TPMOV<>'00',(cAliasRHD)->RHC_DTMOV,CTOD(''))})
					
					
							/*
							ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ------ÄÄÄÄ¿
							³  Si es baja "02" todas las columnas quedan en blanco excepto "Fecha"   ³
							ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ------ÄÙ*/
							Do Case
								Case (cAliasRHD)->RHC_TPMOV == "00"
									oSection4:Cell("RHC_TPMOV"):SetValue(Space(3))//Clave
								Case (cAliasRHD)->RHC_TPMOV == "01"
									oSection4:Cell("RHC_TPMOV"):SetValue("A")//Clave
								Case (cAliasRHD)->RHC_TPMOV == "02"
									oSection4:Cell("RHC_NDTRAB"):SetValue(0)
									oSection4:Cell("RHC_SALDII"):SetValue(0)
									oSection4:Cell("RHC_NDINC"):SetValue(0)
									oSection4:Cell("RHC_NDFAL"):SetValue(0)
									oSection4:Cell("RHC_CFPAT"):SetValue(0)
									oSection4:Cell("RHC_EXEPAT"):SetValue(0)
									oSection4:Cell("RHC_EXETRA"):SetValue(0)
									oSection4:Cell("RHC_PDPAT"):SetValue(0)
									oSection4:Cell("RHC_PDTRA"):SetValue(0)
									oSection4:Cell("RHC_GMPPAT"):SetValue(0)
									oSection4:Cell("RHC_GMPTRA"):SetValue(0)
									oSection4:Cell("RHC_RTPAT"):SetValue(0)
									oSection4:Cell("RHC_IVPAT"):SetValue(0)
									oSection4:Cell("RHC_IVTRA"):SetValue(0)
									oSection4:Cell("RHC_GPSPAT"):SetValue(0)
									oSection4:Cell("SUMAPAT"):SetValue(0)//SumaPat.
									oSection4:Cell("SUMAOBR"):SetValue(0)// Suma Obr.
									oSection4:Cell("TOTAL"):SetValue(0) //Total= SumaPat+SumaObr
									oSection4:Cell("RHC_TPMOV"):SetValue("B")//Clave
									nTTotal += (-(cAliasRHD)->SUMAPAT-(cAliasRHD)->SUMAOBR)
								Case (cAliasRHD)->RHC_TPMOV == "05"
									oSection4:Cell("RHC_TPMOV"):SetValue("M/S")//Clave
								Case (cAliasRHD)->RHC_TPMOV=="06"
									oSection4:Cell("RHC_TPMOV"):SetValue("R")//Clave
							End Case
													   
							If !((cAliasRHD)->RHC_TPMOV == "02")
								aStot[1]  += (cAliasRHD)->RHC_NDTRAB-(cAliasRHD)->RHC_NDINC-(cAliasRHD)->RHC_NDFAL
								aStot[2]  += (cAliasRHD)->RHC_NDINC
								aStot[3]  += (cAliasRHD)->RHC_NDFAL
								aStot[4]  += (cAliasRHD)->RHC_CFPAT
								aStot[5]  += (cAliasRHD)->RHC_EXEPAT
								aStot[6]  += (cAliasRHD)->RHC_EXETRA
								aStot[7]  += (cAliasRHD)->RHC_PDPAT
								aStot[8]  += (cAliasRHD)->RHC_PDTRA
								aStot[9]  += (cAliasRHD)->RHC_GMPPAT
								aStot[10] += (cAliasRHD)->RHC_GMPTRA
								aStot[11] += (cAliasRHD)->RHC_RTPAT
								aStot[12] += (cAliasRHD)->RHC_IVPAT
								aStot[13] += (cAliasRHD)->RHC_IVTRA
								aStot[14] += (cAliasRHD)->RHC_GPSPAT
								aStot[15] += (cAliasRHD)->SUMAPAT
								aStot[16] += (cAliasRHD)->SUMAOBR
								aStot[17] += (cAliasRHD)->SUMAPAT+(cAliasRHD)->SUMAOBR
							EndIf
							oSection4:PrintLine()
							nTTotal += (cAliasRHD)->SUMAPAT+(cAliasRHD)->SUMAOBR
							(cAliasRHD)->(dbSkip())
							oReport:IncMeter()   //regla de avance
						Else
							(cAliasRHD)->(dbSkip())
						EndIf //RHC_CFPAT <> 0
					EndDo //Matricula     
				EndDo //Registro patronal  
				
				oSection5:Init()
				oSection5:Cell("RHC_NDTRAB"):SetValue(aStot[1])
				oSection5:Cell("RHC_NDINC"):SetValue(aStot[2])
				oSection5:Cell("RHC_NDFAL"):SetValue(aStot[3])
				oSection5:Cell("RHC_CFPAT"):SetValue(aStot[4])
				oSection5:Cell("RHC_EXEPAT"):SetValue(aStot[5])
				oSection5:Cell("RHC_EXETRA"):SetValue(aStot[6])
				oSection5:Cell("RHC_PDPAT"):SetValue(aStot[7])
				oSection5:Cell("RHC_PDTRA"):SetValue(aStot[8])
				oSection5:Cell("RHC_GMPPAT"):SetValue(aStot[9])
				oSection5:Cell("RHC_GMPTRA"):SetValue(aStot[10])
				oSection5:Cell("RHC_RTPAT"):SetValue(aStot[11])
				oSection5:Cell("RHC_IVPAT"):SetValue(aStot[12])
				oSection5:Cell("RHC_IVTRA"):SetValue(aStot[13])
				oSection5:Cell("RHC_GPSPAT"):SetValue(aStot[14])
				oSection5:Cell("SUMAPAT"):SetValue(aStot[15])//SumaPat.
				oSection5:Cell("SUMAOBR"):SetValue(aStot[16])// Suma Obr.
				oSection5:Cell("TOTAL"):SetValue(aStot[17]) //Total= SumaPat+SumaObr
				oSection5:Printline()
				oSection5:Finish()
				
				//Trailer
				oSection6:Init()
				oSection6:Cell("VALTOT"):SetValue(nTTotal)//Sum. Col. Total
				oSection6:Cell("TOTCOT"):SetValue(nTEmp)//Total de Empleados
				oSection6:PrintLine()
				oSection6:Finish()
				
				oSection4:Finish()
				
				oSection7:Init()     
				oSection7:Cell("DESC2"):Show()
				oSection7:Cell("DESC3"):Show()
				oSection7:Cell("DESC1"):SetValue(STR0052)	//"# Trabajadores con Articulo 33"
				oSection7:Cell("DESC2"):SetValue(STR0060)	// "C.F.   - Cuota Fijo"
				oSection7:Cell("DESC3"):SetValue(STR0101+TRANSFORM(nSMGDF,"999.99"))// "SMGDF"
				oSection7:PrintLine()
				
				oSection7:Cell("DESC1"):SetValue(STR0053)// "& Trabajadores Pensionados IV"
				oSection7:Cell("DESC2"):SetValue(STR0061)// "Exc.   - Excedente 3 SMGDF"
				oSection7:Cell("DESC3"):HIDE()
				oSection7:PrintLine()
				
				oSection7:Cell("DESC1"):SetValue(STR0054)// "% Trabajadores Pensionados CV"
				oSection7:Cell("DESC2"):SetValue(STR0062)// "P.D.   - Prestaciones en Dinero"
				oSection7:PrintLine()
				
				oSection7:Cell("DESC1"):SetValue(STR0055)//"* Trabajadores con Semana Reducida"
				oSection7:Cell("DESC2"):SetValue(STR0063)//"G.M.P. - Gastos Medicos Pens.(Art. 25)"				
				oSection7:PrintLine()
				
				oSection7:Cell("DESC1"):SetValue(STR0056)//"** Trabajadores con Jornada Reducida"
				oSection7:Cell("DESC2"):SetValue(STR0064)//"R.T.   - Riesgo de Trabajo"
				oSection7:PrintLine()
				
				oSection7:Cell("DESC1"):SetValue(STR0057)// "SDI    - Salario Diario Integrado"
				oSection7:Cell("DESC2"):SetValue(STR0065)// "I.V.   - Invalidez y vida"
				oSection7:PrintLine()
				
				oSection7:Cell("DESC1"):SetValue(STR0058)// "Inc.   - Incapacidades"
				oSection7:Cell("DESC2"):SetValue(STR0066)// "G.P.S. - Guarderia y Prest. Sociales"
				oSection7:PrintLine()
				
				oSection7:Cell("DESC1"):SetValue(STR0059)// "Aus.   - Ausencias"
				oSection7:Cell("DESC2"):HIDE()
				oSection7:PrintLine()
				
				oSection7:Finish()
				oReport:EndPage() //-- Salta Pagina
			EndDo
	
		Else

			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ------ÄÄÄÄ¿
			³  Informe Resumén                                                       ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ------ÄÙ*/
			oSection1:Init()
			oSection2:Init()
			While (cAliasRHD)->(!Eof())
				oReport:SkipLine()
				oSection1:Cell("SM0_CGC"):SetValue(" "+Alltrim(SM0->M0_CGC)) 
				oSection1:Cell("MESANO"):SetValue(" "+cMes+"/"+cAnio)//Mes y anio de proces
				oSection1:PrintLine() 
				
				oReport:FatLine()	
				
				nTTotal := (cAliasRHD)->SRHC_CFPAT+(cAliasRHD)->SRHC_EXPPAT+(cAliasRHD)->SRHC_PDPAT+(cAliasRHD)->SRHC_GMPPAT+(cAliasRHD)->SRHC_RTPAT+(cAliasRHD)->SRHC_IVPAT+(cAliasRHD)->SRHC_GPSPAT
				oReport:SkipLine()
				oSection2:Cell("DESC"):SetValue(STR0068) //"PARA ABONO EN CUENTA DEL IMSS"
				oSection2:Cell("IMPORT"):Hide()
				oSection2:PrintLine()
				oReport:SkipLine()

				oSection2:Cell("IMPORT"):Show()
				oSection2:Cell("DESC"):SetValue(STR0069)// "EMPLEADOS"
				oSection2:Cell("IMPORT"):SetValue((cAliasRHD)->EMP)
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0070)//DIAS COTIZADOS
				oSection2:Cell("IMPORT"):SetValue((cAliasRHD)->SRHC_NDTRAB)
				oSection2:PrintLine()
				oReport:SkipLine()

				oSection2:Cell("DESC"):SetValue(STR0071)//CUOTA FIJA
				oSection2:Cell("IMPORT"):SetValue((cAliasRHD)->SRHC_CFPAT)
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0072)//EXCEDENTE 3 SMGDF
				oSection2:Cell("IMPORT"):SetValue((cAliasRHD)->SRHC_EXPPAT)
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0073)//PRESTACIONES EN DINERO
				oSection2:Cell("IMPORT"):SetValue((cAliasRHD)->SRHC_PDPAT)
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0074)//GASTOS MEDICOS PENSIONADOS
				oSection2:Cell("IMPORT"):SetValue((cAliasRHD)->SRHC_GMPPAT)
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0075)//RIESGO DE TRABAJO
				oSection2:Cell("IMPORT"):SetValue((cAliasRHD)->SRHC_RTPAT)
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0076)//INVALIDEZ Y VIDA
				oSection2:Cell("IMPORT"):SetValue((cAliasRHD)->SRHC_IVPAT)
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0077)//GUARDERIAS Y PRES. SOC.
				oSection2:Cell("IMPORT"):SetValue((cAliasRHD)->SRHC_GPSPAT)
				oSection2:PrintLine()
				oReport:SkipLine()

				oSection2:Cell("DESC"):SetValue(STR0078)//TOTAL	
				oSection2:Cell("IMPORT"):SetValue(nTTotal)	
				oSection2:PrintLine()			
				
				oReport:EndPage() //-- Salta Pagina
				
				(cAliasRHD)->(dbSkip())
				oReport:IncMeter()   //regla de avance
			EndDo
			oSection1:Finish()
			oSection2:Finish()	
		EndIf
	EndIf
End Sequence
(cAliasRHD)->(dbCloseArea())

Return ( Nil )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GPER65001 ³ Autor ³ FMonroy               ³ Data ³24/05/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validacion de las preguntas                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER65001()											      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Ninguno						                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ X1_VALID - GPER650 En X1_ORDEM =01                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPER65001() 
                                   
Local cMes	:= Substr(StrZero( MV_PAR01,6),1,2)
Local cAnio	:= Substr(StrZero( MV_PAR01,6),3,4)
	
//Local cMes	:= StrZero(Val(Left(MV_PAR01,2)),2)
//Local cAnio	:= Right(MV_PAR01,4)


	If Val(cMes) < 1 .Or. Val(cMes) > 12
		MsgInfo(STR0098)//"El mes debe ser de 1 a 12!"
		Return( .F. )
	EndIf                  

	If Val(cAnio) <= 1900
		MsgInfo(STR0099)//"El año debe ser mayor a 1900!"
		Return( .F. )
	EndIf

Return (.T.)

//////////////////////////////////////////////////////////////////////
// Version 11 - Borrar cuando generado el UPDATE para la version 11. 
/////////////////////////////////////////////////////////////////////

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ 	GPER650 | Autor ³ Flor Monroy           ³ Data ³  10.05.2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Informe mensual SUA                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ (void)                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPER650_OLD()  

Local cDesc1	:= STR0001							// "Demonstrativo Mensal"
Local cDesc2	:= STR0002							// Será impresso de acordo com os parametros solicitados pelo usuario."
Local cDesc3	:= STR0003							// "A geracao deve ser executada previamente"
Local aArea		:= RCQ->( GetArea() )				// Ponteiros atuais do arquivo RCQ
Local nLin      := 80

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis privadas para SetPrint.								³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cString		:= 'RCQ'
Private aReturn		:= { STR0007, 1, STR0008, 2, 2, 1, '', 1 }
Private NomeProg	:= 'GPER650'
Private nLastKey	:= 0
Private cPerg		:= 'GPER650'
Private Titulo		
Private AT_PRG		:= 'GPER650'
Private wCabec0		:= 8
Private wCabec1 ,wCabec2, wCabec3, wCabec4, wCabec5, wCabec6, wCabec7, wCabec8		
Private nTamanho	:= 'G'
PRIVATE nChar	 	:= 15
PRIVATE COLUNAS  	:= 220
Private cCabec		:= ''
Private aInfo		:= {}
Private Li			:= 0
Private cSize		:= 'G'
Private contfl		:= 1
Private aCodRpat  	:= {}
Private dDataRef    := ctod("//")
Private cMesAno			// Mes/Ano do Processamento
Private cAnoMes			// Ano/Mes do Processamento
Private cTpImp     

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega parametros de impressao.										³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte( cPerg, .F. )

wnRel := 'GPER650'
wnRel := SetPrint( cString, wnRel, cPerg, @Titulo, cDesc1, cDesc2, cDesc3, .F., , , nTamanho )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta parametros para impressao.										³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cMesAno 	:= mv_par01 //- Mes/Ano de Processamento
cAnoMes  	:= Right(cMesAno,4)+Left(cMesAno,2)
cTpImp   	:= mv_par03 
nAplDesc	:= mv_par04
cVersao		:= mv_par05

If cTpImp == 1
	Titulo		:= STR0009      		//Analitico - "CEDULA DE CUOTAS OBRERAS Y PATRONALES"
	cSize   	:= "G"
	nTamanho 	:= "G"
	Colunas  	:= 220
	aReturn[4] 	:= 2
	nChar 		:= 15
ElseIf cTpImp == 2				
	Titulo 		:= STR0010				//Sintetico - "CEDULA DE AUTODETERMINACION DE CUOTAS"
	cSize   	:= "M"
	nTamanho	:= "M"
	Colunas  	:= 132
	aReturn[4] 	:= 1
	nChar 		:= 15
Else
	Titulo := STR0011				//Resumo - "RESUMEN DE LIQUIDACION OBRERO PATRONAL"
	cSize   	:= "M"
	nTamanho	:= "M"
	Colunas  	:= 132
	aReturn[4] 	:= 1
	nChar 		:= 15
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa a impressao.													³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nLastKey = 27
	Return
Endif

SetDefault(aReturn,cString,,,cSize)
RptStatus( { |lEnd| Gp650Prn(nLin) },Titulo )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GP650Prn  ºAutor  ³Silvia Taguti       º Data ³  02/05/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao gp650Prn() - Efetua a impressao do relatorio de     º±±
±±º          ³                     Obrigacoes patronais                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function gp650Prn(nLin)

Local nX		:= 0
Local nCont 	:= 0
Local cCod 		:= ""
Local nA		:= 0
Local cFilant  	:= Replicate("!", FwGetTamFilial)
Local nTotPat 	:= 0
Local nTotEmp	:= 0
Local nTotal	:= 0
Local cMat		:= Space(6)
Local cImss
Local cCurp
Local cNome
Local cRfc 
Local nPos		:= 0
Local nDiaM    	:= 0
Local nPosB		:= 0
Local aArea 	:= GetArea()

Private cMatAnt := "!!"
Private cDias
Private cRFCEmp := Space(11)
Private cAtivi	:= Space(24)
Private cNomePat:= Space(40)
Private cEndPat := Space(40)
Private cNum	:= ""
Private cBairro := Space(20)
Private cMunic  := Space(06)
Private cCep    := Space(05)
Private cEntid  := Space(05)
Private nDiasInc:= 0
Private nDiasFal:= 0
Private nDiasMaxFal	:= 0
Private cFatRis := Space(12)

Private nPCFixPat
Private nPExCPat 
Private nPExCEmp	
Private nPEDinPat
Private nPGDinEmp
Private nPGMPePat
Private nPGMPeEmp
Private nPInViPat
Private nPInViEmp
Private nPGPSPat	
Private nPRTPat	
Private nSalDiaI	:= 0
Private nSalIVCV	:= 0

Private nCFPat		:= 0
Private nExPat 		:= 0
Private nExEmp		:= 0
Private nPDPat		:= 0
Private nPDEmp		:= 0
Private nGMPPat		:= 0
Private nGMPEmp		:= 0
Private nRTPat		:= 0
Private nIVPat		:= 0
Private nIVEmp		:= 0
Private nGPSPat		:= 0
Private nTotDias 	:= 0
Private nTotDiInc	:= 0
Private nTotDiFal	:= 0
Private nTotFunc	:= 0
Private nTotCF 		:= 0
Private nTotExPat	:= 0
Private nTotExEmp	:= 0
Private nTotPDPat	:= 0
Private nTotPDEmp	:= 0
Private nTotGMPPat	:= 0
Private nTotGMPEmp	:= 0 
Private nTotRtPat	:= 0
Private nTotIvPat	:= 0
Private nTotIvEmp 	:= 0
Private nTotGpsPat	:= 0
Private nTotGerPat	:= 0
Private nTotGerEmp	:= 0
Private nTotGeral	:= 0
Private nValDesc	:= 0
Private nSalMinimo	:= 0
Private dDataAd		:= ctod("")
Private cChave 		:= " "
Private aInfFunc	:= {}
Private aCabFunc  	:= {}
Private nDias		:= 0
Private dDAtaIni  	:= ctod("")
Private aReingre	:= {}
Private cRegPat		:= ""

cFilDe      := Space(FwGetTamFilial)
cFilAte     := Replicate("z", FwGetTamFilial)

dbSelectArea("RCO")
DbSetOrder(1)
	
For nCont := 1 To Len( mv_par02 ) Step 4
	If dbSeek(xFilial("RCO")+SubStr( mv_par02 , nCont , 4 ))
		AADD(aCodRPat,{SubStr( mv_par02 , nCont , 4 ),RCO->RCO_NREPAT,RCO_ATIVID,RCO_NOME,;
		RCO_END,RCO_NUMEXT,RCO_BAIRRO,RCO_CEP,RCO_UF,RCO_MUNIC,Alltrim(Str(RCO_FATRSC))})
	Endif
Next nCont
	
If Len(aCodRPat)== 0
	Return
Endif

For nA := 1 to Len(aCodRPat)
	cCod += "'"+Alltrim(aCodRPat[nA,2])+"',"
Next nA
cCod := Substr(cCod,1,(Len(cCod)-1) )

dDataRef := Ctod("01/"+Left(cMesAno,2)+"/"+ Right(cMesAno,4))           

If !fCarTab650() 
	cHelp := OemToAnsi( STR0049 )
	Help( ' ' , 1 , "INCONSIST" , , cHelp , 1 , 0 )
	Return
Endif

dbSelectArea( 'RCQ' )

cAliasRCQ := "RCQ"	   

For nx := 1 to Len(aCodRPat)
	aInfFunc	:= {}
	aCabFunc  	:= {}
	cCodRPat 	:= aCodRPat[nx,1]   	//Codigo Patronal
	cRegPat 	:= aCodRPat[nx,2]	   //Numero Patronal 
	cAtivi  	:= aCodRpat[nx,3]		//Atividade	
	cNomePat 	:= aCodRpat[nx,4]  	//Razao Social
	cEndPat		:= aCodRpat[nx,5]  	//Endereco
	cNum		:= aCodRpat[nx,6]		//Numero
	cBairro  	:= aCodRpat[nx,7] 	//Bairro
	cMunic  	:= aCodRpat[nx,10] 	//Munic 
	aArea 		:= GetArea()
	nPosB		:= FPOSTAB("S016",cMunic,"=",4) 
	If nPosB > 0
		cMunic 	:= If(FTABELA("S016",nPosB,5)<> Nil,FTABELA("S016",nPosB,5),cMunic)
	Endif
	RestArea(aArea)
	cCep    := aCodRpat[nx,8] 		//Cep 
	cEntid  := aCodRpat[nx,9] 		//Entidad Federativa
	cFatRis := aCodRpat[nx,11] 	//Fator de Risco      
	
	#IFDEF TOP
		IF TcSrvType() 	!= "AS/400"
			cAliasRCQ 	:= "QRCQ"
			cQuery 	  	:= "SELECT * "
			cQuery 		+= " FROM "+	RetSqlName("RCQ")
			cQuery 		+= " WHERE RCQ_FILIAL between '" + cFilDe + "' AND '" + cFilAte + "' AND "
			cQuery 		+= " RCQ_NREPAT ='"+ cRegPat+ "' AND "
		    cQuery 		+= " RCQ_DTMOV <='" + cAnoMes+'31' + "' AND "
			cQuery 		+= "  D_E_L_E_T_ = ' ' "
			cQuery 		+= " ORDER BY RCQ_NREPAT,RCQ_NOME,RCQ_DTMOV,RCQ_ANOMES,RCQ_SEQMVT, RCQ_TPMOV"
			cQuery 		:= ChangeQuery( cQuery )
			aStru  		:= RCQ->(dbStruct())
			
			If  MsOpenDbf(.T.,"TOPCONN",TcGenQry(, ,cQuery),cAliasRCQ,.T.,.T.)
				For nA := 1 To Len(aStru)
					If ( aStru[nA][2] <> "C" )
						TcSetField(cAliasRCQ,aStru[nA][1],aStru[nA][2],aStru[nA][3],aStru[nA][4])
					EndIf
				Next nA
			Endif         
		Else 
			dbSetOrder( 3 )                 
			DbSeek(cRegPat,.T.)
		Endif	
	#ELSE
		dbSetOrder( 3 )                 
		DbSeek(cRegPat,.T.)
	#ENDIF
	
	ProcRegua((cAliasRCQ)->(RecCount()))

	While (cAliasRCQ)->(!EOF()) .And. 	(cAliasRCQ)->RCQ_NREPAT == cRegPat
		If (cAliasRCQ)->RCQ_ANOMES  <> cAnoMes .or. (cAliasRCQ)->RCQ_TPMOV $ "15|16|17|18|19|20"
			(cAliasRCQ)->(dbSkip())
			Loop
		Endif
      	If (cAliasRCQ)->RCQ_TPMOV == "06" //.And. AnoMes((cAliasRCQ)->RCQ_DTMOV) <> cAnomes 
			If Ascan(aReingre,{|x| x[1] == (cAliasRCQ)->RCQ_MAT } ) == 0
	      		AADD(aReingre,{(cAliasRCQ)->RCQ_MAT,(cAliasRCQ)->RCQ_DTMOV })
		    Endif	
      	Endif
		IF (cAliasRCQ)->RCQ_FILIAL # cFilAnt
			cFilAnt := (cAliasRCQ)->RCQ_FILIAL
			fInfo(@aInfo,cFilAnt)
			cRFCEmp := aInfo[08]
		Endif
		fCabec650(nLin)

		If (cAliasRCQ)->RCQ_MAT <> cMatAnt
			dbSelectArea("SRA")
			dbSetOrder(1)
			If dbSeek((cAliasRCQ)->(RCQ_FILIAL+RCQ_MAT))
				cMat  	:= SRA->RA_MAT
	         	cImss 	:= SRA->RA_RG
		        cCurp 	:= SRA->RA_CURP
   	    		cNome 	:= Alltrim(SRA->RA_PRISOBR)+" "+Alltrim(SRA->RA_SECSOBR)+" "+Alltrim(SRA->RA_PRINOME)+;
							" "+Alltrim(SRA->RA_SECNOME)   	      			
	      	   	cRfc	:= SRA->RA_CIC
		      	nTotFunc ++
		      	AADD(aCabFunc,{cImss,cNome,cMat,cRFC,cCurp } )   
				cImss:= cNome:= cMat:= cRfc:= cCurp := " "
				nDiasMaxFal := 0
	      	Endif
	  	Endif    
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Tipos de Movimentos - Admissao³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nDias   	:= F_ULTDIA(dDataRef)
		nSalDiaI := 0
		nSalIVCV := 0
     	nDiaM := If((cAliasRCQ)->RCQ_TPMOV <> "05",1,0)
     	If !((cAliasRCQ)->RCQ_TPMOV $ "02|04")
	     	nSalDiaI := (cAliasRCQ)->RCQ_SALDII
	     	nSalIVCV := (cAliasRCQ)->RCQ_SALIVC
	   	Endif
      
      	If !((cAliasRCQ)->RCQ_TPMOV $ "11|12")
      		If AnoMes((cAliasRCQ)->RCQ_DTMOV) == cAnoMes
				If (cAliasRCQ)->RCQ_TPMOV $ "01|03"				
					cChave := "A"
					nDias := (nDias - Day((cAliasRCQ)->RCQ_DTMOV))+ nDiaM
				ElseIf (cAliasRCQ)->RCQ_TPMOV $ "02|04"
					cChave := "B"
				ElseIf (cAliasRCQ)->RCQ_TPMOV == "05"
					cChave := "M/S"
				ElseIf (cAliasRCQ)->RCQ_TPMOV == "06"			
					cChave := "R"
				Endif	
				dDataAd := (cAliasRCQ)->RCQ_DTMOV
			Endif	
	  	ElseIf (cAliasRCQ)->RCQ_TPMOV == "12"
			nDiasInc := (cAliasRCQ)->RCQ_NDIF
		ElseIf (cAliasRCQ)->RCQ_TPMOV == "11"
			If ( nDiasMaxFal < 7 )
				nDiasFal 	:= (cAliasRCQ)->RCQ_NDIF
				If nDiasMaxFal > 0 .And. ( nDiasMaxFal + nDiasFal ) > 7
					nDiasFal := 7 - nDiasMaxFal
				Else
					nDiasMaxFal += nDiasFal
				EndIf
			EndIf
		Endif		
		cDias := StrZero(nDias,2)
		dDataIni := Ctod("01/"+Left(cMesAno,2)+"/"+Right(cMesAno,4))
	
		If (cAliasRCQ)->RCQ_MAT == cMatAnt 
			If Len(aInfFunc) > 0 
				nPos := Len(aInfFunc)		
				If !((cAliasRCQ)->RCQ_TPMOV $ "11|12")
					If aInfFunc[nPos,22] $ "11|12"
						aInfFunc[nPos,2] := cChave
						aInfFunc[nPos,3] := If(dDataAd <> ctod("//"),dtoc(dDataAd)," ")
		         	Else
						nDias := (cAliasRCQ)->RCQ_DTMOV - If(Ctod(aInfFunc[nPos,3]) <> ctod("//"),;
						Ctod(aInfFunc[nPos,3]),dDataIni) - Val(aInfFunc[nPos,7])+ nDiaM
						aInfFunc[nPos,4] :=	StrZero(nDias,2)					
						nDias := (F_ULTDIA(dDataRef) - Day((cAliasRCQ)->RCQ_DTMOV)) + nDiaM
						If (cAliasRCQ)->RCQ_TPMOV == "05"
							nDias++
						Endif	
						cDias := StrZero(If((cAliasRCQ)->RCQ_TPMOV $ "02|04", 0, nDias),2)				      
				    	fGravFun()
		         	Endif
      			Else 
					If !(aInfFunc[nPos,22] $ "11|12")
						If (cAliasRCQ)->RCQ_TPMOV == "12"
					  		aInfFunc[nPos,6]:= Str(Val(aInfFunc[nPos,6])+ nDiasInc,3)
					  	Else
							aInfFunc[nPos,7]:= Str(Val(aInfFunc[nPos,7])+ nDiasFal,3)
							nDias := Val(aInfFunc[nPos,4]) - nDiasFal
							aInfFunc[nPos,4] :=	StrZero(nDias,2)
						Endif	
					Else
				 		fGravFun()
		         	Endif
			   	Endif      
			Endif	
		Else
			fGravFun()
	 	Endif   

      	cMatAnt := (cAliasRCQ)->RCQ_MAT	
		nDiasInc:= nDiasFal := 0
		cDias   := ""
		cChave  := " "
		dDataAd := ctod("")
		(cAliasRCQ)->(dbSkip())
	Enddo
	fCalcEmp(@nTotPat,@nTotEmp,@nTotal)

	If Len(aInfFunc) > 0
		If cTpImp == 1    
			fImprAna()
			fTotPatAna()
		ElseIf cTpImp == 2
			fImprSin()
			fTotPatSin()
		Else
			fTotPatRes()
		Endif		
	Endif
	dbSelectARea(cAliasRCQ)
	dbCloseArea()
	dbSelectArea("RCQ")
Next 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza impressao.														³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Impr( '', 'F' )
Set Device To Screen

If aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	OurSpool( WnRel )
Endif
MS_Flush()

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCarTab650ºAutor  ³Microsiga           º Data ³  05/11/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega Tabelas de Cotas de Obreros e Empresa               º±±
±±º          ³Carrega Valor do Salario Minimo                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fCarTab650()

Local lRet := .T.        
Local n    := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tabela de Percentuais de Obreros/Patronais	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Cota Fixa Empresa 
nPCFixPat	:= If( FTABELA("S007",1,4,dDataRef) <> Nil,FTABELA("S007",1,4,dDataRef),;
                FTABELA("S007",1,4 ))
//Excedente Empresa 	
nPExCPat  	:= If( FTABELA("S007",1,5,dDataRef) <> Nil,FTABELA("S007",1,5,dDataRef),;
					FTABELA("S007",1,5 ))    
//Excedente Empregado
nPExCEmp		:= If( FTABELA("S007",1,6,dDataRef) <> Nil,FTABELA("S007",1,6,dDataRef),;
					FTABELA("S007",1,6 ) )    
//Emprestimo em Dinheiro Empresa 					 
nPEDinPat	:= If( FTABELA("S007",1,7,dDataRef) <> Nil,FTABELA("S007",1,7,dDataRef),;
					FTABELA("S007",1,7 ))   
//Emprestimo em Dinheiro Empregado					  
nPGDinEmp	:= If( FTABELA("S007",1,8,dDataRef) <> Nil,FTABELA("S007",1,8,dDataRef),;     
					FTABELA("S007",1,8 ))                                                 
//Gastos Medicos Pensionados Empresa 					
nPGMPePat	:= If( FTABELA("S007",1,9,dDataRef) <> Nil,FTABELA("S007",1,9,dDataRef),;
					FTABELA("S007",1,9 ))            
//Gastos Medicos Pensionados Empregado 
nPGMPeEmp	:= If( FTABELA("S007",1,10,dDataRef) <> Nil,FTABELA("S007",1,10,dDataRef),;
					FTABELA("S007",1,10 ))           
//Invalidez e Vida Empresa					 
nPInViPat	:= If( FTABELA("S007",1,11,dDataRef) <> Nil,FTABELA("S007",1,11,dDataRef),;
					FTABELA("S007",1,11 ))           
//Invalidez e Vida Empregado					 
nPInViEmp	:= If( FTABELA("S007",1,12,dDataRef) <> Nil, FTABELA("S007",1,12,dDataRef),;
					FTABELA("S007",1,12 ))           
//Guarderia Empresa					
nPGPSPat		:= If( FTABELA("S007",1,13,dDataRef) <> Nil, FTABELA("S007",1,13,dDataRef),;
					FTABELA("S007",1,13 ))           
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tabela de Salario MInimo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

n:= FPOSTAB("S006","A","=",4)
If n > 0
	nSalMinimo := If( FTABELA("S006",n,5,dDataRef) <> Nil, FTABELA("S006",n,5,dDataRef),;
	  				FTABELA("S006",n,5))
Endif	

If nPCFixPat = Nil .or. nPExCPat = Nil .Or. nPExCEmp = Nil .Or. nPEDinPat = Nil .Or.;
	nPGDinEmp	= Nil .Or. nPGMPePat = Nil .Or. nPGMPeEmp = Nil .Or. nPInViPat = Nil .Or. ;
	nPInViEmp	= Nil .Or. nPGPSPat = Nil
	lRet		:= .F.
EndIf

If lRet .and. (nPCFixPat+nPExCPat+nPExCEmp+nPEDinPat+nPGDinEmp+nPGMPePat+nPGMPeEmp+nPInViPat+nPInViEmp+nPGPSPat == 0)
	lRet := .F.
Endif

Return lRet	

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FCABEC650 ºAutor  ³Silvia Taguti       º Data ³  09/05/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Imprime Cabecalhos - Relatorio: Analitico, Sintetico e     º±±
±±º          ³                      Resumo                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fCabec650()
              
If cTpImp == 1    
					//Registro Patronal + RFC + Atividade + Area Geografica
	wCabec1 :=	fRetTxt(STR0012+cRegPat,40) + fRetTxt(STR0013+cRFCEmp,40) + fRetTxt(STR0014+cAtivi,70)+ ;
					fRetTxt(STR0015,20)
					//Razao Social + Delegacao IMSS 
	wCabec2 := 	fRetTxt(STR0016+cNomePat ,130) + fRetTxt(STR0017,30)
					//Prima de RT + Mes e Ano de Processamento + Sub-Delegacao + Versao SUA					
	wCabec3 := 	fRetTxt(STR0018+cFatRis,80) + fRetTxt(STR0019+Substr(cMesAno,1,2)+" / "+Substr(cMesAno,3,4),50)+;
					fRetTxt(STR0020,30) + fRetTxt(OemToAnsi(STR0079) + " " + cVersao, 25)					
	wCabec4 := 	__PrtFatLine()
					//Nro Seguridade Social + Nome + Matricula + RFC + CURP
	wCabec5 := 	fRetTxt(STR0021,36) + fRetTxt(STR0022,70) + fRetTxt(STR0023,28) + fRetTxt(STR0024,40)+ ;
					fRetTxt(STR0025,20)
					//Enfermidades e Maternidade
 	wCabec6 := 	Space(57) + STR0046
					//Chave+Data+Dias+SDI+Inc.Aus+CF+EXC+PD+GMP+RT+IV+GPS+Soma+Total
	wCabec7 := 	fRetTxt(STR0026,7) + fRetTxt(STR0027,08) + fRetTxt(STR0028,08) + fRetTxt(STR0029,9)+;
					fRetTxt(STR0030,16) + fRetTxt(STR0031,20) + fRetTxt(STR0032,24) + fRetTxt(STR0033,25)+ ;
					fRetTxt(STR0034,19) + fRetTxt(STR0035,19) + fRetTxt(STR0036,18) + fRetTxt(STR0037,21)+ ;
					fRetTxt(STR0038,20) + fRetTxt(STR0039,15)
					//Pat+Obr
	wCabec8 := 	Space(61) + fRetTxt(STR0040,12) + fRetTxt(STR0041,13) + fRetTxt(STR0040,12)+ ;
					fRetTxt(STR0041,13) + fRetTxt(STR0040,12) + fRetTxt(STR0041,26) + fRetTxt(STR0040,12)+;
					fRetTxt(STR0041,27) + fRetTxt(STR0040,14) + fRetTxt(STR0041,14)                                          
ElseIf cTpImp == 2  //Sintetico
					//Registro Patronal + RFC + Atividade + CURP
	wCabec1 := 	fRetTxt(STR0012+cRegPat,30)+ " "+fRetTxt(STR0013+cRFCEmp,20)+ fRetTxt(STR0014+cAtivi,45)+;
			      fRetTxt(STR0015,20)
					//Razao Social + Delegacao IMSS
	wCabec2 :=  fRetTxt(STR0016+cNomePat,63) + fRetTxt(STR0017,30)
					//Endereco+Num+Bairro+Municipio
	wCabec3 :=  fRetTxt(STR0042+cEndPat+cNum,52)+ ","+Space(1)+fRetTxt(cBairro,20)+;
					fRetTxt(STR0043+cMunic,41)
					//Codido Postal + Entidade+ Prima RT + Ano e Mes de Processamento
	wCabec4 :=  fRetTxt(STR0044+cCep,35)+fRetTxt(STR0045+cEntid,25)+fRetTxt(STR0018+cFatRis,35)+;
					fRetTxt(STR0019+Substr(cMesAno,1,2)	+ " / "+ Substr(cMesAno,3,4),40)
	wCabec5 :=  __PrtFatLine()
					//Nro de Seguridade Social + Nome + Matricula + RFC + CURP
	wCabec6 :=  fRetTxt(STR0021,34) + fRetTxt(STR0022,40)+fRetTxt(STR0023,14)+;
					fRetTxt(STR0024,20) + fRetTxt(STR0025,10)
					//Enfermidades e Maternidade
 	wCabec7 :=  Space(30)+STR0046
					//Chave+Data+Dias+SDI+INC.AUS+CF+EXC+PD+GMP+RT+IV+GPS+Soma+Total
	wCabec8 :=  fRetTxt(STR0026,8) + fRetTxt(STR0027,8) + fRetTxt(STR0028,7) + fRetTxt(STR0029,8)+;
					fRetTxt(STR0030,13) + fRetTxt(STR0031,13) + fRetTxt(STR0032,13) + fRetTxt(STR0033,11)+ ;
					fRetTxt(STR0034,13) + fRetTxt(STR0035,12) + fRetTxt(STR0036,11) + fRetTxt(STR0037,11)+ ;
					fRetTxt(STR0038,13) 
Else           //Resumo
					//Mes e Ano do Processamento + Data de Emissao
	wCabec1 :=  Space(30) + STR0019 + Substr(cMesAno,1,2)+ " / "+Substr(cMesAno,3,4) + Space(15)+STR0027 +": "+dtoc(dDataBase)
					//Registro Patronal + RFC
	wCabec2 :=  Space(10)+fRetTxt(STR0012+cRegPat,35) + fRetTxt(STR0013+cRFCEmp,20)
					//Razao Social 
	wCabec3 :=  Space(10)+fRetTxt(STR0016 + cNomePat,63)
	wCabec4 :=  __PrtFatLine()
     				//Descricao Importe
   wCabec5 :=  Space(20)+STR0047+Space(50)+STR0048 
Endif	
				       					 	
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCalcEmp  ºAutor  ³Silvia Taguti       º Data ³  10/05/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Calcula Cuotas por Funcionario                              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                           	                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fCalcEmp(nTotPat,nTotEmp,nTotal)

Local nx 	 := 0
Local nDias1 := 0
Local nDias2 := 0
Local nDias3 := 0
Local n		 := 0

//Risco de Trabalho
nPRTPat		:= Val(cFatRis)

For n:= 1 to Len(aReingre) 
	For nx:= Len(aInfFunc) to 1 Step -1
		If aReingre[n,1] == aInfFunc[nx,1]
			If aReingre[n,1] == aInfFunc[nx,1] .And.	AnoMes(Ctod(aInfFunc[nx,3])) == cAnoMes .or.;
				(nx+1) <= Len(aInfFunc) .And. aInfFunc[nx+1,22] <> "06" .And.(aReingre[n,1] == aInfFunc[nx+1,1] .And.;
				AnoMes(Ctod(aInfFunc[nx+1,3])) == cAnoMes)
				Loop
			Endif
			aInfFunc[nx,25] := "D"
		Endif
	Next nx
Next n	 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Dias1 - Dias Naturais - Dias Incapacidades - Faltas        			 ³
//³Dias2 - Dias Naturais - Dias de Incapacidades         		       ³
//³Dias3 - Dias Naturais                                       		 ³
//³Faltas sao limitadas a 8 dias                               		 ³
//³obs. As faltas ja foram descontadas dos Dias Atuais (aInffunc[nx,4]³
//³por isso foram somadas novamente nos nDias2 e nDias3    			    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

For nx:= 1 to Len(aInfFunc)
	If aInfFunc[nx,25] == "D"
		Loop
	Endif	
	nDias1 := Val(aInfFunc[nx,4]) - Val(aInfFunc[nx,6])
	nDias2 := Val(aInfFunc[nx,4]) - Val(aInfFunc[nx,6])+ If(Val(aInfFunc[nx,7])>8,Val(aInfFunc[nx,7])-8,Val(aInfFunc[nx,7]))
	nDias3 := Val(aInfFunc[nx,4])	+ Val(aInfFunc[nx,7])

	nCfPat := If(aInfFunc[nx,2] <> "B", Round(((nSalMinimo * nDias2) * nPCFixPat) / 100,MsDecimais(1)),0)			//Cuota Fija
	If aInfFunc[nx,5] > (nSalMinimo * 3)
		nExPat := Round((((aInfFunc[nx,5]- (nSalMinimo*3)) * nDias2) * nPExCPat)/100,MsDecimais(1)) //Excedente Empresa
		nExEmp := Round((((aInfFunc[nx,5]- (nSalMinimo*3)) * nDias2) * nPExCEmp)/100,MsDecimais(1)) //Excedente Func.
	Endif
	nPDPat := Round(((aInfFunc[nx,5] * nDias2) * nPEDinPat) / 100,MsDecimais(1))  //Emprestimo em Dinheiro Empresa 					 
	nPDEmp := Round(((aInfFunc[nx,5] * nDias2) * nPGDinEmp) / 100,MsDecimais(1))  //Emprestimo em Dinheiro Empregado					  
	nGMPPat:= Round(((aInfFunc[nx,5] * nDias2) * nPGMPePat) / 100,MsDecimais(1))  //Gastos Medicos Pensionados Empresa 					
	nGMPEmp:= Round(((aInfFunc[nx,5] * nDias2) * nPGMPeEmp) / 100,MsDecimais(1))  //Gastos Medicos Pensionados Empregado 
	nRTPat := Round(((aInfFunc[nx,5] * nDias1) * nPRTPat )  / 100,MsDecimais(1))  //Risco Trabalho           
	nIVPat := Round(((aInfFunc[nx,24]* nDias1) * nPInViPat) / 100,MsDecimais(1))  //Invalidez e Vida Empresa					 
	nIVEmp := Round(((aInfFunc[nx,24]* nDias1) * nPInViEmp) / 100,MsDecimais(1))  //Invalidez e Vida Empregado					               
	nGPSPat:= Round(((aInfFunc[nx,5] * nDias1) * nPGPSPat)  / 100,MsDecimais(1))  //Guarderia Empresa					             

	// Aplicar os 5% de descontos // 
	If nAplDesc == 1 
		nValDesc += (If(aInfFunc[nx,2] <> "B", Round(((nSalMinimo * nDias2) * nPCFixPat) / 100,MsDecimais(1)),0))*0.05
		nCfPat := (If(aInfFunc[nx,2] <> "B", Round(((nSalMinimo * nDias2) * nPCFixPat) / 100,MsDecimais(1)),0))*0.95 //Cuota Fija
		If aInfFunc[nx,5] > (nSalMinimo * 3)
			nValDesc += (Round((((aInfFunc[nx,5]- (nSalMinimo*3)) * nDias2) * nPExCPat)/100,MsDecimais(1)))*0.05		
			nExPat := (Round((((aInfFunc[nx,5]- (nSalMinimo*3)) * nDias2) * nPExCPat)/100,MsDecimais(1)))*0.95 			 //Excedente Empresa
		EndIf
		nValDesc+= (Round(((aInfFunc[nx,5] * nDias2) * nPEDinPat) / 100,MsDecimais(1)))*0.05
		nPDPat 	:= (Round(((aInfFunc[nx,5] * nDias2) * nPEDinPat) / 100,MsDecimais(1)))*0.95							 //Emprestimo em Dinheiro Empresa
		nValDesc+= (Round(((aInfFunc[nx,5] * nDias2) * nPGMPePat) / 100,MsDecimais(1)))*0.05
		nGMPPat	:= (Round(((aInfFunc[nx,5] * nDias2) * nPGMPePat) / 100,MsDecimais(1)))*0.95  						 //Gastos Medicos Pensionados Empresa
		nValDesc+= (Round(((aInfFunc[nx,5] * nDias1) * nPRTPat )  / 100,MsDecimais(1)))*0.05
		nRTPat 	:= (Round(((aInfFunc[nx,5] * nDias1) * nPRTPat )  / 100,MsDecimais(1)))*0.95  						 //Risco Trabalho
		nValDesc+= (Round(((aInfFunc[nx,24]* nDias1) * nPInViPat) / 100,MsDecimais(1)))*0.05
		nIVPat 	:= (Round(((aInfFunc[nx,24]* nDias1) * nPInViPat) / 100,MsDecimais(1)))*0.95  						 //Invalidez e Vida Empresa
		nValDesc+= (Round(((aInfFunc[nx,5] * nDias1) * nPGPSPat)  / 100,MsDecimais(1)))*0.05
		nGPSPat	:= (Round(((aInfFunc[nx,5] * nDias1) * nPGPSPat)  / 100,MsDecimais(1)))*0.95  //Guarderia Empresa

		If aInfFunc[nx,5] <= nSalMinimo
			If aInfFunc[nx,5] > (nSalMinimo * 3)
				nValDesc += (Round((((aInfFunc[nx,5]- (nSalMinimo*3)) * nDias2) * nPExCEmp)/100,MsDecimais(1)))*0.05		
				nExEmp := (Round((((aInfFunc[nx,5]- (nSalMinimo*3)) * nDias2) * nPExCEmp)/100,MsDecimais(1)))*0.95 //Excedente Func.
			EndIf
			nValDesc += (Round(((aInfFunc[nx,5] * nDias2) * nPGDinEmp) / 100,MsDecimais(1)))*0.05
			nPDEmp := (Round(((aInfFunc[nx,5] * nDias2) * nPGDinEmp) / 100,MsDecimais(1)))*0.95  //Emprestimo em Dinheiro Empregado
			nValDesc += (Round(((aInfFunc[nx,5] * nDias2) * nPGMPeEmp) / 100,MsDecimais(1)))*0.05
			nGMPEmp:= (Round(((aInfFunc[nx,5] * nDias2) * nPGMPeEmp) / 100,MsDecimais(1)))*0.95  //Gastos Medicos Pensionados Empregado
			nValDesc += (Round(((aInfFunc[nx,24]* nDias1) * nPInViEmp) / 100,MsDecimais(1)))*0.05
			nIVEmp := (Round(((aInfFunc[nx,24]* nDias1) * nPInViEmp) / 100,MsDecimais(1)))*0.95  //Invalidez e Vida Empregado
		EndIf
	EndIf
    
	nTotPat := Round(nCfPat+nExPat+nPDPat+nGMPPat+nRTPat+nIVPat+nGPSPat,MsDecimais(1))
	nTotEmp := Round(nExEmp+nPDEmp+nGMPEmp+nIVEmp,MsDecimais(1))
	nTotal := Round(nTotPat+nTotEmp,MsDecimais(1))

	aInfFunc[nx,08]:=	Transform(nCfPat,"@E 999,999.99")
	aInfFunc[nx,09]:=	nExPat
	aInfFunc[nx,10]:=	nExEmp
	aInfFunc[nx,11]:=	nPDPat
	aInfFunc[nx,12]:=	nPDEmp
	aInfFunc[nx,13]:=	nGMPPat
	aInfFunc[nx,14]:=	nGMPEmp
	aInfFunc[nx,15]:=	Transform(nRTPat,"@E 999,999.99")
	aInfFunc[nx,16]:=	nIVPat
	aInfFunc[nx,17]:=	nIVEmp
	aInfFunc[nx,18]:=	Transform(nGPSPat,"@E 999,999.99")
	aInfFunc[nx,19]:=	Transform(nTotPat,"@E 999,999.99")
	aInfFunc[nx,20]:=	Transform(nTotEmp,"@E 999,999.99")
	aInfFunc[nx,21]:=	Transform(nTotal,"@E 999,999.99")

	nTotDias 	+= If(aInfFunc[nx,2] <> "B",Val(aInfFunc[nx,4]),0)
	nTotDiInc 	+= Val(aInfFunc[nx,6])
	nTotDiFal 	+= Val(aInfFunc[nx,7])
	nTotCF 		+= nCfPat
	nTotExPat 	+= nExPat
	nTotExEmp 	+= nExEmp
	nTotPDPat 	+= nPDPat
	nTotPDEmp 	+= nPDEmp
	nTotGMPPat  += nGMPPat
	nTotGMPEmp 	+= nGMPEmp
	nTotRtPat	+= nRTPat
	nTotIvPat	+= nIVPat
	nTotIvEmp 	+= nIVEmp
	nTotGpsPat	+= nGPSPat
	nTotGerPat 	+= nTotPat
	nTotGerEmp 	+= nTotEmp
	nTotGeral	+= nTotal

	//Caso o funcionario tenha uma ausencia, desconta dos dias totais
	If ( Val(aInfFunc[nx,6]) > 0 )
		aInfFunc[nx,4] := AllTrim(Str(Val(aInfFunc[nx,4]) - Val(aInfFunc[nx,6])))		
	EndIf
		
	nCfPat:= nExPat:= nExEmp:= nPDPat:= nPDEmp:=	nGMPPat:= nGMPEmp:= 0
	nRTPat:= nIVPat:=	 nIVEmp:= nGPSPat:= nTotPat:=nTotEmp:= nTotal:= 0
	nDias1:= nDias2:= nDias3 := 0
Next nx	

Return
          
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGravFun  ºAutor  ³Silvia Taguti       º Data ³  05/31/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Adiciona informacoes do funcionario do array               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fGravFun(nTotPat,nTotEmp,nTotal)
               
Local cTpJorn := Space(2)
                         
If (cAliasRCQ)->RCQ_TJRNDA $ "1|2|3|4|5"
	cTpJorn := "* "
ElseIf (cAliasRCQ)->RCQ_TJRNDA == "6"	
	cTpJorn := "**"
Else
	cTpJorn := "  "
Endif   
		
AADD(aInfFunc,{(cAliasRCQ)->RCQ_MAT,;										//01 Matricula
					cChave,;                                           //02 Chave
					If(dDataAd <> ctod("//"),dtoc(dDataAd)," "),;      //03 Data
					cDias,;                                            //04 Dias
					nSalDiaI,;                                         //05 Salario Dia.Int.
					Str(nDiasInc,3),;                                  //06 Dias Incap
					Str(nDiasFal,3),;                                  //07 Dias Faltas
					"",;                                               //08 Cota Fixa         
					"",;                                               //09 Excedente Patronal
					"",;                                               //10 Excedente Funcionario
					"",;                                               //11 Emprestimo Din.Patr
					"",;                                               //12 Emprestimo Din.Func.
					"",;					                           //13 Gastos Medicos Patr.
					"",;                                               //14 Gastos Medicos Empreg.
					"",;											   //15 Retiro
					"",;                                               //16 Iv patronal
					"",;                                               //17 Iv Empregado
					"",;                                               //18 GPS Patronal
					"",;                                               //19 Total Patronal
					"",;                                               //20 Total Empresa
					"",;                                               //21 Total
					(cAliasRCQ)->RCQ_TPMOV ,;                          //22 Tipo de Movimento
					cTpJorn,;                                          //23 Tipo de Jornada
					nSalIVCV,;   									   //24 Sal.Dia.IVCV
 					""})	                                           //25 Deletado
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fImprAna ³ Autor ³ Silvia Taguti         ³ Data ³ 09.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio por Funcionario - Analitico                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fImprAna()												  				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImprAna()

Local nx := 0
Local nPos := 0
Local cMatrAnt := "!!"

For nx:= 1 to Len(aInfFunc)
	If aInfFunc[nx,25] == "D"  .or. (aInfFunc[nx,4] == "00" .And. !(aInfFunc[nx,2] $ "A|B"))
		Loop
	Endif	

	If aInfFunc[nx,1] <> cMatrAnt
		nPos	:=	Ascan(aCabFunc,{|x| x[3] == aInfFunc[nx,1] } ) 
	   	If nPos > 0
 			DET := Space(4)+fRetTxt(aCabFunc[nPos,1],31)+fRetTxt(aCabFunc[nPos,2],70)+;
 					 fRetTxt(aCabFunc[nPos,3],28)+fRetTxt(aCabFunc[nPos,4],40)+;
 					 fRetTxt(aCabFunc[nPos,5],20) 
			IMPR(DET,"C") 
			DET := ""
		Endif		
	Endif	
   
	DET := Space(1) + fRetTxt(aInfFunc[nx,2],4)+ fRetTxt(aInfFunc[nx,3],11)+;
     	    If(aInfFunc[nx,2] <> "B",fRetTxt(aInfFunc[nx,4],4) + Transform(aInfFunc[nx,5],"@E 9,999.99")+;
        	 aInfFunc[nx,23]+ aInfFunc[nx,6]+ Space(2) + aInfFunc[nx,7]+ Space(4)+;
	       aInfFunc[nx,8]+"   "+ Transform(aInfFunc[nx,9],"@E 999,999.99")+ "   " + ;
	       Transform(aInfFunc[nx,10],"@E 999,999.99")+ "   "+;
          TransForm(aInfFunc[nx,11],"@E 99,999.99")+ "   " +;
          Transform(aInfFunc[nx,12],"@E 99,999.99")+ "   " + Transform(aInfFunc[nx,13],"@E 999,999.99")+;
     	    "  " + Transform(aInfFunc[nx,14],"@E 999,999.99") + "   " + aInfFunc[nx,15]+ "   " +;
     	    Transform(aInfFunc[nx,16],"@E 999,999.99")+;
        	 "   "+ Transform(aInfFunc[nx,17],"@E 99,999.99") + "   " + aInfFunc[nx,18]+ "    "+ aInfFunc[nx,19]+;
	       "    "+ aInfFunc[nx,20] + "    " + aInfFunc[nx,21],"")
	IMPR(DET,"C")                

	cMatrAnt := aInfFunc[nx,1]
	nPos := 0
	DET := ""
Next nx	
            
Return Nil
              

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ImprSin   ºAutor  ³Silvia Taguti       º Data ³  06/01/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Impressao do Relatorio - Sinterico                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fImprSin()

Local nx := 0
Local nPos := 0
Local cMatrAnt := "!!"

For nx:= 1 to Len(aInfFunc)
	If aInfFunc[nx,25] == "D"  .or. (aInfFunc[nx,4] == "00" .And. !(aInfFunc[nx,2] $ "A|B"))
		Loop
	Endif	
	If aInfFunc[nx,1] <> cMatrAnt
		nPos	:=	Ascan(aCabFunc,{|x| x[3] == aInfFunc[nx,1] } ) 
  		If nPos > 0
 			DET := Space(4)+fRetTxt(aCabFunc[nPos,1],30)+fRetTxt(aCabFunc[nPos,2],40)+;
					 fRetTxt(aCabFunc[nPos,3],10)+fRetTxt(aCabFunc[nPos,4],10)+;
					 fRetTxt(aCabFunc[nPos,5],20) 
			IMPR(DET,"C") 
			DET := ""
		Endif		
	Endif	
   
	DET := 	Space(1) + fRetTxt(aInfFunc[nx,2],4)+ fRetTxt(aInfFunc[nx,3],11)+;
  	       	If(aInfFunc[nx,2] <> "B",fRetTxt(aInfFunc[nx,4],4) + Transform(aInfFunc[nx,5],"@E 9,999.99")+;
     	    aInfFunc[nx,23]+ aInfFunc[nx,6]+ Space(1) + aInfFunc[nx,7]+ Space(2)+;
          	aInfFunc[nx,8]+Space(3)+ ;
  	       	Transform(aInfFunc[nx,9]+aInfFunc[nx,10],"@E 999,999.99")+;
     	    "  "+ Transform(aInfFunc[nx,11]+ aInfFunc[nx,12],"@E 999,999.99")+;
        	"  " +Transform(aInfFunc[nx,13]+ aInfFunc[nx,14],"@E 999,999.99")+;
          	"  " + aInfFunc[nx,15]+ "  " + ;
  	       	Transform(aInfFunc[nx,16]+aInfFunc[nx,17],"@E 999,999.99")+;
     	    " " + aInfFunc[nx,18]+ "  " + aInfFunc[nx,21],"")
	IMPR(DET,"C")                

	cMatrAnt := aInfFunc[nx,1]
	nPos := 0
	DET := ""
Next nx	
            
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ImprRes   ºAutor  ³Silvia Taguti       º Data ³  06/01/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Impressao do Relatorio - Resumo                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fImprRes()

Local nx := 0
Local nPos := 0
Local cMatrAnt := "!!"

For nx:= 1 to Len(aInfFunc)
	If aInfFunc[nx,25] == "D"
		Loop
	Endif	
	nTotDias 	+= If(aInfFunc[nx,2] <> "B",Val(aInfFunc[nx,4]),0)
	nTotCF 		+= Val(aInfFunc[nx,8])
	nTotExPat 	+= aInfFunc[nx,9]
	nTotExEmp 	+= aInfFunc[nx,10]
	nTotPDPat 	+= aInfFunc[nx,11]
	nTotPDEmp 	+= aInfFunc[nx,12]
	nTotGMPPat  += aInfFunc[nx,13]
	nTotGMPEmp 	+= aInfFunc[nx,14]
	nTotRtPat	+= Val(aInfFunc[nx,15])
	nTotIvPat	+= aInfFunc[nx,16]
	nTotIvEmp 	+= aInfFunc[nx,17]
	nTotGpsPat	+= Val(aInfFunc[nx,18])
	nTotGeral	+= Val(aInfFunc[nx,21])

	cMatrAnt := aInfFunc[nx,1]
	nPos := 0
	DET := ""
Next nx	
            
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fRetTxt  ³ Autor ³ Equipe R.H.           ³ Data ³ 26.01.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna o texto no tamanho indicado no segundo parametro	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fRetTxt(cTexto, nTamanho)								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Static Function fRetTxt(cTexto, nTamanho)

Return Left(cTexto + Space(nTamanho), nTamanho)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fTotPatAnaºAutor  ³Silvia Taguti       º Data ³  05/19/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime Total Geral do Registro Patronal Analitico          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fTotPatAna()

DET := 	Space(15)+ fRetTxt(Alltrim(Str(nTotDias)),4)+ Space(9)+Str(nTotDiInc,5)+ ;
		Space(1)+Str(nTotDiFal,4)+Space(1)+Transform(nTotCf,"@E 99,999,999.99")+""+;
		Transform(nTotExPat,"@E 99,999,999.99")+""+Transform(nTotExEmp,"@E 99,999,999.99")+""+ ; 
		Transform(nTotPDPat,"@E 9,999,999.99")+""+Transform(nTotPDEmp,"@E 9,999,999.99")+ ""+ ; 
		Transform(nTotGMPPat,"@E 99,999,999.99")+""+Transform(nTotGMPEmp,"@E 9,999,999.99")+ ""+ ; 
		Transform(nTotRTPat,"@E 99,999,999.99")+""+Transform(nTotIVPat,"@E 99,999,999.99")+""+ ; 
		Transform(nTotIVEmp,"@E 9,999,999.99")+""+Transform(nTotGPSPat,"@E 99,999,999.99")+""+ ;    
		Transform(nTotGerPat,"@E 999,999,999.99")+""+Transform(nTotGerEmp,"@E 999,999,999.99")+""+ ;
		Transform(nTotGeral,"@E 999,999,999.99")

Impr( __PrtThinLine(),"C")

IMPR(DET,"C")
Li+=2
//Total a Pagar#Total de Cotizantes  
DET := Space(2)+STR0081+Transform(nTotGeral+nValDesc,"@E 999,999,999.99")+"            "+STR0051+ " "+fRetTxt(Alltrim(Str(nTotFunc)),4)
IMPR(DET,"C")
DET := Space(2)+STR0080+Transform(nValDesc,"@E 999,999,999.99") // "Valor Desconto.....................:"
IMPR(DET,"C")
DET := Space(2)+STR0050+Transform(nTotGeral,"@E 999,999,999.99") // 
IMPR(DET,"C")
IMPR("","C")
DET := Space(2)+STR0052+Space(19)+STR0060         //Trabalhadores com Artigo  33#C.F.
IMPR(DET,"C")
DET := Space(2)+STR0053+Space(20)+STR0061        	//Trabalhadores Pensionados IV#Exc.
IMPR(DET,"C")			           
DET := Space(2)+STR0054+Space(20)+STR0062		   //Trabalhadores Pensionados CV#P.D.
IMPR(DET,"C")
DET := Space(2)+STR0055+Space(15)+STR0063        	//Trabajadores con Semana Reducida#G.M.P
IMPR(DET,"C")
DET := Space(2)+STR0056+Space(13)+STR0064			//** Trabajadores con Jornada Reducida#R.T.
IMPR(DET,"C")
DET := Space(2)+STR0057+Space(17)+STR0065         //SDI#I.V.
IMPR(DET,"C")
DET := Space(2)+STR0058+Space(29)+STR0066         //Inc.#GPS
IMPR(DET,"C")
DET := Space(2)+STR0059+Space(30)+STR0067+Transform(nSalMinimo,"@E 9,999.99") //Aux.#Salario Minimo Do  D.F.
IMPR(DET,"C")

IMPR("","F")

nTotDias	:= nTotDiInc:= nTotDiFal:= nTotCF := nTotExPat	:= nTotExEmp	:= 0
nTotPDPat:= nTotPDEmp:= nTotGMPPat:= nTotGMPEmp:= nTotRtPat:= nTotIvPat	:= 0
nTotIvEmp := nTotGpsPat:= nTotGerPat:= nTotGerEmp:= nTotGeral	:= nTotFunc := 0

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fTotPatSinºAutor  ³Silvia Taguti       º Data ³  05/19/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime Total Geral do Registro Patronal Sintetico          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fTotPatSin()

DET := Space(15)+ fRetTxt(Alltrim(Str(nTotDias)),4)+ Space(8)+Str(nTotDiInc,5)+ ;
       Space(1)+Str(nTotDiFal,4)+Transform(nTotCf,"@E 9,999,999.99")+""+;
		 Transform(nTotExPat+nTotExEmp,"@E 99,999,999.99")+""+ ; 
		 Transform(nTotPDPat+nTotPDEmp,"@E 9,999,999.99")+""+ ; 
		 Transform(nTotGMPPat+nTotGMPEmp,"@E 9,999,999.99")+""+ ; 
		 Transform(nTotRTPat,"@E 9,999,999.99")+""+;
		 Transform(nTotIVPat+nTotIVEmp,"@E 9,999,999.99")+""+;
		 Transform(nTotGPSPat,"@E 999,999.99")+""+Transform(nTotGeral,"@E 9,999,999.99")

Impr( __PrtThinLine(),"C")

IMPR(DET,"C")
Li+=2
//Total a Pagar#Total de Cotizantes
DET := Space(2)+STR0050+Transform(nTotGeral,"@E 999,999,999.99")+"  "+STR0051+ " "+fRetTxt(Alltrim(Str(nTotFunc)),4)
IMPR(DET,"C")               
If nAplDesc == 1
	DET := Space(2)+STR0080+Transform(nValDesc,"@E 999,999,999.99") // "Valor Desconto.....................:"
	IMPR(DET,"C")
EndIf

IMPR("","C")
DET := Space(2)+STR0052+Space(19)+STR0060         //Trabalhadores com Artigo  33#C.F.
IMPR(DET,"C")
DET := Space(2)+STR0053+Space(20)+STR0061        	//Trabalhadores Pensionados IV#Exc.
IMPR(DET,"C")			           
DET := Space(2)+STR0054+Space(20)+STR0062		   //Trabalhadores Pensionados CV#P.D.
IMPR(DET,"C")
DET := Space(2)+STR0055+Space(15)+STR0063        	//Trabajadores con Semana Reducida#G.M.P
IMPR(DET,"C")
DET := Space(2)+STR0056+Space(13)+STR0064			//** Trabajadores con Jornada Reducida#R.T.
IMPR(DET,"C")
DET := Space(2)+STR0057+Space(17)+STR0065         //SDI#I.V.
IMPR(DET,"C")
DET := Space(2)+STR0058+Space(29)+STR0066         //Inc.#GPS
IMPR(DET,"C")
DET := Space(2)+STR0059+Space(30)+STR0067+Transform(nSalMinimo,"@E 9,999.99") //Aux.#Salario Minimo Do  D.F.
IMPR(DET,"C")

IMPR("","F")

nTotDias	:= nTotDiInc:= nTotDiFal:= nTotCF := nTotExPat	:= nTotExEmp	:= 0
nTotPDPat:= nTotPDEmp:= nTotGMPPat:= nTotGMPEmp:= nTotRtPat:= nTotIvPat	:= 0
nTotIvEmp := nTotGpsPat:= nTotGerPat:= nTotGerEmp:= nTotGeral	:= nTotFunc := 0

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fTotPatResºAutor  ³Silvia Taguti       º Data ³  05/19/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime Total Geral do Registro Patronal Resumo             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fTotPatRes()

DET:= ""
IMPR(DET,"C")

DET := Space(20) + STR0068						//"PARA ABONO EN CUENTA DEL IMSS"
IMPR(DET,"C")
LI+=2
DET := Space(20) + PADR(STR0069,50) + PadL(Alltrim(Str(nTotFunc)),15)
IMPR(DET,"C")										//"EMPLEADOS 						"

DET := Space(20) + PADR(STR0070,50) + PadL(Alltrim(Str(nTotDias)),15)
IMPR(DET,"C")										//"DIAS COTIZADOS 					
LI+=3
DET := Space(20) + PADR(STR0071,50) + PadL(Transform(nTotCf,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"CUOTA FIJA     					

DET := Space(20) + PADR(STR0072,50) + PadL(Transform(nTotExPat+nTotExEmp,"@E 99,999,999.99"),15)
IMPR(DET,"C")										//"EXCEDENTE 3 SMGDF				

DET := Space(20) + PADR(STR0073,50) + PadL(Transform(nTotPDPat+nTotPDEmp,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"PRESTACIONES EN DINERO		"

DET := Space(20) + PADR(STR0074,50) + PadL(Transform(nTotGMPPat+nTotGMPEmp,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"GASTOS MEDICOS PENSIONADOS	"
                  
DET := Space(20) + PADR(STR0075,50) + PadL(Transform(nTotRTPat,"@E 99,999,999.99"),15)
IMPR(DET,"C")										//"RIESGO DE TRABAJO				"

DET := Space(20) + PADR(STR0076,50) + PadL(Transform(nTotIVPat+nTotIVEmp,"@E 9,999,999.99"),15)
IMPR(DET,"C")	                          	//"INVALIDEZ Y VIDA				"
             
DET := Space(20) + PADR(STR0077,50) + PadL(Transform(nTotGPSPat,"@E 9,999,999.99"),15)
IMPR(DET,"C")                            //"GUARDERIAS Y PRES. SOC.		"
LI++
DET:= Space(20) +  PADR(STR0078,50) + PadL(Transform(nTotGeral,"@E 999,999,999.99"),15)
IMPR(DET,"C")                            //"GUARDERIAS Y PRES. SOC.		"

If nAplDesc == 1
	DET := Space(20)+PADR(STR0080,50)+PadL(Transform(nValDesc,"@E 999,999,999.99"),15) // "Valor Desconto.....................:"
	IMPR(DET,"C")
EndIf

IMPR("","P")

nTotDias	:= nTotDiInc:= nTotDiFal:= nTotCF := nTotExPat	:= nTotExEmp	:= 0
nTotPDPat:= nTotPDEmp:= nTotGMPPat:= nTotGMPEmp:= nTotRtPat:= nTotIvPat	:= 0
nTotIvEmp := nTotGpsPat:= nTotGerPat:= nTotGerEmp:= nTotGeral	:= nTotFunc := 0

Return Nil
