#INCLUDE "PROTHEUS.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "GPER660.CH" 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ 	GPER660 ³ Autor ³ Flor Monroy           ³ Data ³  16.05.2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Informe Bimestral SUA                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ (void)                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ CHAM.³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ gsantacruz ³ 12/10/11 ³ Mej  ³ Correcciones a espaciados                ³±±
±±³            ³          ³ Corr ³ Valor de Sal Min DF                      ³±±
±±³ fmonroy    ³ 22/11/11 ³ Mej  ³ Correcciones a ordenar por Nombre        ³±±
±±³            ³          ³ Corr ³ por el campo RA_NOME                     ³±±
±±³ fmonroy    ³ 13/12/11 ³ Mej  ³ Correcciones de RHC_RTPAT-->RHC_RETPAT   ³±±
±±³            ³          ³ Corr ³ cambio en la decisión para sumarizar     ³±±
±±³            ³          ³      ³ renglones.                               ³±±
±±³ fmonroy    ³ 10/01/12 ³ Mej  ³ Correccion cell(RHC_TIPINF) y            ³±±
±±³            ³          ³ Corr ³  cell(RHC_NUMINF)                        ³±±
±±³ fmonroy    ³ 11/01/12 ³ Mej  ³ Leer RHB                                 ³±±
±±³            ³          ³ Corr ³                                          ³±±
±±³ fmonroy    ³ 13/01/12 ³ Mej  ³ Corecciones NDTRB-NDINC-NDFAL            ³±±
±±³            ³          ³ Corr ³ Picture RHD_VALINF                       ³±±
±±³ gsantacruz ³ 13/01/12 ³ Corr ³Cambio el picture de RHC_AMORCF RHC_INFONA³±±
±±³            ³          ³ Corr ³ Redondeo a 2 al sumar esas cantidades    ³±±
±±³ gsantacruz ³ 26/03/12 ³ Corr ³ Que imprima movoto 02 aun cuando no tenga³±±
±±³            ³          ³      ³ importes de INFONAVIT.                   ³±±
±±³ gsantacruz ³ 09/04/12 ³ Corr ³ NDTRB-NDFAL                              ³±±
±±³            ³          ³      ³ Se agrego C.F. en cuota fija             ³±±
±±³Flor        ³          ³      ³Correcao Retiro, C.Y V.Pat, C.Y V.Obr     ³±±
±±³M.Silveira  ³19/04/2012³TETGCZ³Ajuste para nao arredondar o valor do     ³±±
±±³            ³          ³      ³Infonavit informado.                      ³±±
±±³R.Berti     ³13/09/2012³TFTLRJ³Creacion del fuente GPER660MEX;           ³±±
±±³            ³          ³      ³Se corrije funciones para estaticas.      ³±±  
±±³Laura Medina³11/11/2013³THXPL9³Se corrigio error en el query que obtiene ³±±
±±³            ³          ³      ³datos del historico Modif infonatit (RHB) ³±±
±±³M.Camargo   ³28/10/2015³TTQUO9³Se elimina fuente GPEr660MEX quedando sola³±±
±±³            ³          ³      ³mente este fuente como el válido.         ³±±
±±³            ³          ³      ³Se cambia a function la función GPER660BIM³±±
±±³            ³          ³      ³ya que es llamada desde la SX1.           ³±±             
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/


Function GPER660()

Local	oReport   
Local	aArea 	 	:= GetArea()
Local   lSalir		:=.F.

Local 	nTamReg		:= TamSX3("RCO_CODIGO")[1]
Local   nCont		:=0

Private cNomeProg 	:= "GPER660"
Private cPerg   	:= "GPR660" 
Private _cPar01 //Mes/Anio
Private _cPar02 //Registro Patronal
Private _cPar03 //Tipo de Reporte
Private _cPar04 //Tipo de Reporte
Private cLisPat:=""

If !TRepInUse()	//verifica se a opção relatórios personalizáveis está disponível
	MSGSTOP(OEMTOANSI(STR0098) )//"Para usar este inform debe tener MP8 R4 en adelante!"
	lSalir:=.t.
endif          
if lSalir 
	RestArea(aArea)
	Return
endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Pergunte(CPERG,.T.) 

	_cPar01:=	MV_PAR01 //Bimestre de Proceso
	_cPar02:=	MV_PAR02 //Anio de Proceso
	_cPar03:=	ALLTRIM(MV_PAR03) //Registro patronal
	_cPar04:=	MV_PAR04 //Tipo de Informe

	/*
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Genera lista de registros patronales para usar despues en Query³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	*/

	For nCont := 1 To Len( _cPar03 ) Step nTamReg
		cLisPat+="'"+SubStr( _cPar03 , nCont , nTamReg )+"',"
	Next
	       
	cLisPat:=Substr(cLisPat,1,len(cLisPat)-1)    
	IF _cPar04==1 //Informe Analitico
		oReport := ReportDef()
	Else  //Informe Resumén
		oReport := ReportDef_hor()
	Endif
	oReport:PrintDialog()
	
Endif
RestArea(aArea)

Return ( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef  ³ Autor ³ FMonroy               ³ Data ³12/01/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³  Relatorio de Dotacao                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

Local aArea      := GetArea() 

Local 	oReport
Local	oSection1
Local 	oSection2
Local 	oSection3   
Local 	oSection4     
Local 	oSection5       
Local 	oSection6
Local 	oSection7
Local 	oSection8

Private cTitulo := OemToAnsi(STR0097) //"CEDULA DE DETERMINACION DE CUOTAS OBRERO-PATRONALES"

Private aOrd    := {OemToAnsi(STR0139),OemToAnsi(STR0006),OemToAnsi(STR0004)}	//"Apellido paterno" ### "Nombre"###"Matricula"

cTitulo := Trim(cTitulo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:=TReport():New(cNomeProg,OemToAnsi(cTitulo), /* cNomeProg*/ ,{|oReport| PrintReport(oReport)})	
oReport:SetPortrait()     // Define a orientacao de pagina do relatorio como retrato.
oReport:nColSpace	:=1
oReport:nFontBody	:= 6 // Define o tamanho da fonte.
oReport:nLineHeight	:= 40 // Define a altura da linha.
oReport:cFontBody	:="COURIER NEW"

oReport:Setlandscape(.T.)//Pag Horizontal
oReport:SetTotalInLine(.F.)
 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para Simpressao.                                ³
//³        Default : ExpC2                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Primeira Secao: Encabezado del Registro Patronal              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

oSection1:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"
oSection2:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"
oSection3:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection1:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
oSection1:SetLineStyle(.t.)   //Pone titulo del campo y aun lado el y valor
oSection1:SetCharSeparator(" ")    
oSection1:SetHeaderBreak(.T.)



TRCell():New(oSection1,"RCO_NREPAT",    ,STR0012 ,PesqPict("RHD","RCO_NREPAT")		,TamSx3("RCO_NREPAT")[1])//TamSx3("RCO_NREPAT")[1])//Registro Patronal
TRCell():New(oSection1,"SM0_CGC",       ,STR0013 ,                                	,TamSx3("RA_CIC")[1])//"RFC:"
TRCell():New(oSection1,"RCO_ATIVID",    ,STR0014 ,PesqPict("SRA","RCO_ATIVID")    	,TamSx3("RCO_ATIVID")[1])//"Actividad "
TRCell():New(oSection1,"RCO_CVEZON",    ,STR0015 ,PesqPict("RCO","RCO_CVEZON")    	,TamSx3("RCO_CVEZON")[1])//"Area Geografica: "
oSection1:Cell("RCO_CVEZON"):SetCellBreak(.T.)
TRCell():New(oSection1,"RCO_NOME",	     ,STR0016 ,PesqPict("RHD","RCO_NOME")     	,TamSx3("RCO_NOME")[1])//"Nombre o Razón Social "
oSection1:Cell("RCO_NOME"):SetCellBreak(.T.)
TRCell():New(oSection1,"MESANO",        ,STR0019 ,                                 ,10                      )//"Bimestre Y Ano de Proceso"
TRCell():New(oSection1,"APORPAT",       ,STR0039 ,   PesqPict("RHD","RHD_FATRSC")  ,                    )//"Aportacion Patronal"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da 2da.     Secao:Encabezado Empleado                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

oSection4:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection4:SetTotalInLine(.F.)  
oSection4:SetHeaderSection(.T.)	
oSection4:SetHeaderPage(.T.)	
oSection4:SetLineStyle(.F.)
 oSection4:nColSpace:=10    

TRCell():New(oSection4,"RA_RG",      ,STR0021 ,PesqPict("SRA","RA_RG")            ,TamSx3("RA_RG")[1]+3)//"Numero de Seguridad Social"
TRCell():New(oSection4,"NOMECOM",    ,STR0006 ,PesqPict("SRA","RA_NOME")          ,TamSx3("RA_NOME")[1]+10)//"Nombre"
TRCell():New(oSection4,"EMPLEADO",   ,STR0023 ,PesqPict("SRA","RA_MAT")           ,TamSx3("RA_MAT")[1])//"Empleado"
TRCell():New(oSection4,"RA_CIC",     ,STR0024 ,PesqPict("SRA","RA_CIC")           ,TamSx3("RA_CIC")[1])//"R.F.C."
TRCell():New(oSection4,"RA_CURP",    ,STR0025 ,PesqPict("SRA","RA_CURP")          ,TamSx3("RA_CURP")[1])//"C.U.R.P."

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da 3ra.     Secao: Detalle Empleado                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

oSection5:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection5:SetTotalInLine(.f.)  
oSection5:SetHeaderSection(.T.)	//Exibe Cabecalho da Secao
oSection5:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
oSection5:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor 
oSection5:SetTotalText(" ") 

TRCell():New(oSection5,"RHC_TPMOV",    ,STR0026 ,PesqPict("RHC","RHC_TPMOV")      ,5)//Clave
TRCell():New(oSection5,"RHC_DTMOV",    ,STR0027 ,PesqPict("RHC","RHC_DTMOV")      ,10)//Fecha
TRCell():New(oSection5,"RHC_NDTRAB",   ,STR0028 ,PesqPict("RHC","RHC_NDTRAB")     ,12)//"Dias"
TRCell():New(oSection5,"RHC_SALDII",   ,STR0029 ,PesqPict("RHC","RHC_SALDII")     ,TamSx3("RHC_SALDII")[1])//SDI
TRCell():New(oSection5,"RHC_NDINC",    ,STR0099 ,PesqPict("RHC","RHC_NDINC")      ,TamSx3("RHC_NDINC")[1])//Inc.
TRCell():New(oSection5,"RHC_NDFAL",    ,STR0100 ,PesqPict("RHC","RHC_NDFAL")      ,TamSx3("RHC_NDFAL")[1])//"Aus." 
TRCell():New(oSection5,"RETIRO",       ,STR0032 ,PesqPict("RHC","RHC_RETPAT")      ,TamSx3("RHC_RETPAT")[1])//Retiro
TRCell():New(oSection5,"CYVPAT",       ,STR0101 ,PesqPict("RHC","RHC_CYVPAT")     ,TamSx3("RHC_CYVPAT")[1])//C. y V. Pat
TRCell():New(oSection5,"CYVOBR",       ,STR0102 ,PesqPict("RHC","RHC_CYVPAT")     ,TamSx3("RHC_CYVPAT")[1])//C. Y V. Obr.
TRCell():New(oSection5,"SUMA",         ,STR0034 ,PesqPict("RHC","RHC_CYVPAT")     ,TamSx3("RHC_CYVPAT")[1])//Suma.
TRCell():New(oSection5,"APORTPAR",     ,STR0035 ,"9,999,999.99"     ,12)//"Aport. Par." RHC_INFONA
TRCell():New(oSection5,"PCFVSM",       ,STR0103 ,      ,15)	//"% o C.F. o VSM"
TRCell():New(oSection5,"AMORT",        ,STR0038 ,"9,999,999.99"     ,12)//Amortizacion  RHC_AMORCF
TRCell():New(oSection5,"SUMAI",        ,STR0034 ,PesqPict("RHC","RHC_CYVPAT")    ,TamSx3("RHC_CYVPAT")[1])//Suma
TRCell():New(oSection5,"RHC_NUMINF",   ,STR0018 ,PesqPict("RHC","RHC_NUMINF")     ,TamSx3("RHC_NUMINF")[1])//"Credito de vivienda" 
TRCell():New(oSection5,"MOTCRED",      ,STR0104 ,  ,13)//Tipo y Fecha de Motivo de credito

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da 6ra.     Secao: Totales en linea                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

oSection6:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection6:SetTotalInLine(.f.)  
oSection6:SetHeaderSection(.f.)	//Exibe Cabecalho da Secao
oSection6:SetHeaderPage(.f.)	//Exibe Cabecalho da Secao
oSection6:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor

TRCell():New(oSection6,"RHC_TPMOV",    ,STR0026 ,PesqPict("RHC","RHC_TPMOV")      	,5)//Clave
TRCell():New(oSection6,"RHC_DTMOV",    ,STR0027 ,PesqPict("RHC","RHC_DTMOV")      	,10)//Fecha
TRCell():New(oSection6,"RHC_NDTRAB",   ,STR0028 ,PesqPict("RHC","RHC_CYVPAT")   	,12)//"Dias"
TRCell():New(oSection6,"RHC_SALDII",   ,STR0029 ,PesqPict("RHC","RHC_SALDII")     	,TamSx3("RHC_SALDII")[1])//SDI
TRCell():New(oSection6,"RHC_NDINC",    ,STR0099 ,PesqPict("RHC","RHC_NDINC")      	,TamSx3("RHC_NDINC")[1])//Inc.
TRCell():New(oSection6,"RHC_NDFAL",    ,STR0100 ,PesqPict("RHC","RHC_NDFAL")      	,TamSx3("RHC_NDFAL")[1])//"Aus." 
TRCell():New(oSection6,"RETIRO",       ,STR0032 ,PesqPict("RHC","RHC_RETPAT")     	,TamSx3("RHC_RETPAT")[1])//Retiro
TRCell():New(oSection6,"CYVPAT",       ,STR0101 ,PesqPict("RHC","RHC_CYVPAT")     	,TamSx3("RHC_CYVPAT")[1])//C. y V. Pat
TRCell():New(oSection6,"CYVOBR",       ,STR0102 ,PesqPict("RHC","RHC_CYVPAT")     	,TamSx3("RHC_CYVPAT")[1])//C. Y V. Obr.
TRCell():New(oSection6,"SUMA",         ,STR0026 ,PesqPict("RHC","RHC_CYVPAT")      ,TamSx3("RHC_CYVPAT")[1])//Suma.
TRCell():New(oSection6,"APORTPAR",     ,STR0035 ,"9,999,999.99"     ,12)//"Aport. Par." RHC_INFONA
TRCell():New(oSection6,"PCFVSM",       ,STR0103 ,      ,30)//"% o C.F. o VSM"
TRCell():New(oSection6,"AMORT",        ,STR0038 ,"9,999,999.99"     ,12)//Amortizacion     RHC_AMORCF
TRCell():New(oSection6,"SUMAI",        ,STR0034 ,PesqPict("RHC","RHC_CYVPAT")     	,TamSx3("RHC_CYVPAT")[1])//Suma
TRCell():New(oSection6,"RHC_NUMINF",   ,STR0018 ,PesqPict("RHC","RHC_NUMINF")     	,TamSx3("RHC_NUMINF")[1])//"Credito de vivienda" 
TRCell():New(oSection6,"MOTCRED",      ,STR0104 ,  ,13)//Tipo y Fecha de Motivo de credito

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da 7ma.     Secao: Trailer--Totales                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
oSection7:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection7:SetTotalInLine(.F.)  
oSection7:SetHeaderSection(.F.)	//Exibe Cabecalho da Secao
oSection7:SetHeaderPage(.F.)	//Exibe Cabecalho da Secao
oSection7:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor
oSection7:SetHeaderBreak(.F.) 
oSection7:nColSpace	:=5

TRCell():New(oSection7,"DESC1",         ,STR0105 ,    ,30)//Total por pagard e RVC
TRCell():New(oSection7,"IMPORT1",  ,STR0106 ,"9,999,999.99"     ,12)//"Total por pagar Infonavit" RHC_AMORCF
TRCell():New(oSection7,"DESC2",      ,STR0107 ,    ,30)//"Total a Pagar"
TRCell():New(oSection7,"IMPORT2",     ,STR0108 ,"9,999,999.99"     ,12)//"Aportacion Patronal S/Credito" RHC_AMORCF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da 8va.     Secao: Pie de Pag.                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

oSection8:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection8:SetHeaderSection(.f.)	//Exibe Cabecalho da Secao
oSection8:SetHeaderPage(.F.)	//Exibe Cabecalho da Secao
oSection8:SetLineStyle(.f.)   //Pone titulo del campo y aun lado el y valor
oSection8:SetHeaderBreak(.f.) 

TRCell():New(oSection8,"DESC1",,,,40)
TRCell():New(oSection8,"DESC2",,,,40)
TRCell():New(oSection8,"DESC3",,,,80)
OSECTION1:NLINESBEFORE:=0
OSECTION2:NLINESBEFORE:=0
OSECTION3:NLINESBEFORE:=0
OSECTION4:NLINESBEFORE:=0
OSECTION5:NLINESBEFORE:=0
OSECTION6:NLINESBEFORE:=0
OSECTION7:NLINESBEFORE:=0
OSECTION8:NLINESBEFORE:=0


Return(oReport)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef_hor³ Autor ³ FMonroy             ³ Data ³12/01/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³  Definición del Informe Resumén -Bimestral SUA               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef_hor()

Local aArea      := GetArea() 

Local 	oReport
Local 	oSection1 
Local 	oSection2

Private cTitulo	:=OEMTOANSI(STR0097) //"CEDULA DE DETERMINACION DE CUOTAS OBRERO-PATRONALES"

Private aOrd    := {OemToAnsi(STR0006),OemToAnsi(STR0004)}	//"Nombre"###"Matricula"

cTitulo := Trim(cTitulo)

If _cPar04 == 2
	cTitulo := Trim(OemToAnsi(STR0011)) //"RESUMEN DE LIQUIDACION OBRERO PATRONAL"
Endif	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:=TReport():New(cNomeProg,OemToAnsi(cTitulo),/* cNomeProg */ ,{|oReport| PrintReport(oReport)})	
oReport:nColSpace:=1
oReport:nFontBody	:= 6 // Define o tamanho da fonte.
oReport:nLineHeight	:= 40 // Define a altura da linha.
oReport:cFontBody:="COURIER NEW"
  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Primeira Secao: Encabezado del Registro Patronal              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

oSection1:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/)
oSection1:SetHeaderSection(.t.)	//Exibe Cabecalho da Secao
oSection1:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
oSection1:SetLineStyle(.T.)   //Pone titulo del campo y aun lado el y valor
OSection1:SetCharSeparator(" ")    
oSection1:SetLeftMargin(45)

TRCell():New(oSection1,"MESANO",      ,STR0019 ,                                 ,10                      )//"Bimestre Y Ano de Proceso"
oSection1:Cell("MESANO"):SetCellBreak(.T.)
TRCell():New(oSection1,"RCO_NREPAT",  ,STR0012 ,PesqPict("RCO","RCO_NREPAT")     ,TamSx3("RCO_NREPAT")[1])//TamSx3("RCO_NREPAT")[1])//Registro Patronal
TRCell():New(oSection1,"SM0_CGC",     ,STR0013 ,                                 ,TamSx3("RA_CIC")[1])//"RFC:"
oSection1:Cell("SM0_CGC"):SetCellBreak(.T.)
//TRCell():New(oSection1,"RCO_ATIVID",   ,STR0014 ,PesqPict("RCO","RCO_ATIVID")      ,TamSx3("RCO_ATIVID")[1])//"Actividad "
//TRCell():New(oSection1,"RCO_CVEZON",   ,STR0015 ,PesqPict("RCO","RCO_NOME")       ,TamSx3("RCO_NOME")[1])//"Area Geografica: Zona"
TRCell():New(oSection1,"RCO_NOME",	  ,STR0016 ,PesqPict("RCO","RCO_NOME")     ,TamSx3("RCO_NOME")[1])//"Nombre o Razón Social "
oSection1:Cell("RCO_NOME"):SetCellBreak(.T.)
//TRCell():New(oSection1,"APORPAT",     ,STR0039 , PesqPict("RHC","RHC_INFONA")     ,TamSx3("RHC_INFONA")[1] )//"Aportacion Patronal"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da 2da.     Secao:Detalle del Total                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

oSection2:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

oSection2:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor
oSection2:SetHeaderSection(.T.)	//Exibe Cabecalho da Secao
oSection2:SetHeaderPage(.t.)	//Exibe Cabecalho da Secao
oSection2:SetLeftMargin(50)

TRCell():New(oSection2,"DESC","QRYRHD",STR0047 ,  ,50)//DESCRIPCION
TRCell():New(oSection2,"IMPORT","QRYRHD",STR0048 ,"999,999,999.99"     ,14)//IMPORTE
 
Return(oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao     ³ PrintReport³ Autor ³ Flor Monroy        ³ Data ³ 10.05.11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descripción³ Función para impresion del Reporte                        ³±± 
±±³           ³                                                           ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxis   ³ PrintReport()                                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport)

Local oSection1  := oReport:Section(1)
Local oSection2  := oReport:Section(2)
//Local oSection3  := oReport:Section(3)
Local oSection4  := oReport:Section(4)
Local oSection5  := oReport:Section(5)
Local oSection6  := oReport:Section(6)
Local oSection7  := oReport:Section(7)
Local oSection8  := oReport:Section(8)

Local cTitle	 := ""     
Local cOrdem     := "" 
Local cAliasRHC  := "QRYRHC" 
Local cFilRHC    :=xfilial("RHC")
//Local cFilRCO    :=xfilial("RCO")
Local cFilSRA    :=xfilial("SRA")
Local cFilRHD    :=xfilial("RHD")
Local cCposQuery := ""
Local cCposFrom  := ""
Local cWhere     := ""
Local cCons      :=""
Local cMesIni    :=""
Local cMesFin    := ""
Local cAnoMesIni :=""
Local cAnoMesFin :=""
Local cRegPAT    :=""

Local nOrdem	 := oSection1:GetOrder()
Local nTEmp      :=0
Local nDiaFin    :=0
Local nRCV       :=0
Local nSinf      :=0
Local nSscre     :=0
Local nSabono    :=0
Local nAmort     :=0
Local nAcredi    :=0 
Local nDiasT     :=0
Local nInc       :=0
Local nAus       :=0
Local nRetiro    :=0
Local nCyvtra    :=0
Local nCyvpat    :=0
Local nAmorti    :=0
Local nSuma      :=0
Local nSumaI     :=0
Local nApoPat    :=0
Local nSMGDF 	 :=IF ((FPOSTAB("S006","A","=",4))>0, FTABELA("S006",FPOSTAB("S006","A","=",4),5), 0)   
Local nAporPat   :=FTABELA("S007",1,15)   
Local nIRhc      :=1
Local nTa        :=0
Local nTb        :=0
Local nTc        :=0

Local dDataIni            
Local dDataFin 
	
Local aRHC       :={}
Local aTot       :={}
Local aRHBT      :={}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta parametros -Bimestre de Proceso									³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If _cPar01 == "1"
	cMesIni := "01"
	cMesFin := "02"
ElseIf _cPar01 == "2"
	cMesIni := "03"
	cMesFin := "04"
ElseIf _cPar01 == "3"
	cMesIni := "05"
	cMesFin := "06"
ElseIf _cPar01 == "4"
	cMesIni := "07"
	cMesFin := "08"
ElseIf _cPar01 == "5"
	cMesIni := "09"
	cMesFin := "10"
ElseIf _cPar01 == "6"
	cMesIni := "11"
	cMesFin := "12"
Else
	Return
Endif

cAnoMesIni := _cPar02+cMesIni
cAnoMesFin := _cPar02+cMesFin

dDataIni  := Ctod("01/"+cMesIni+"/"+ _cPar02)           
dDataFin  := Ctod("01/"+cMesFin+"/"+ _cPar02)           
nDiaFin	  := F_ULTDIA(dDataFin)

dDataFin  :=Ctod(SUBSTR(strZERO(nDiaFin,2),1,2)+"/"+cMesFin+"/"+_cPar02)

Pergunte(oReport:GetParam(),.F.)   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Altera o Titulo do Relatorio conforme o parametro selecionado   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cTitle 	:= If(AllTrim(oReport:Title())==AllTrim(cTitle), OemToansi(cTitulo),oReport:Title())
oReport:SetTitle(cTitle) 

#IFDEF TOP

	//Transforma los parametros de tipo Rango en expresion SQL para ser utilizada en query 
	cCposQuery := "% RCO_NREPAT,RA_NOME,RCO_ATIVID,RCO_CVEZON,RCO_NOME,RA_RG,RA_PRISOBR,RA_SECSOBR,RA_PRINOME,RA_SECNOME,RA_CIC,RA_CURP, RHC.*,RHD_VALINF,RHD_FATRSC %"
		
	cCposFrom   := "%" + RetSqlName("RHD") + " RHD, " 
	cCposFrom   +=  RetSqlName("RCO")+ " RCO, "
	cCposFrom   +=  RetSqlName("SRA")+ " SRA, "
	cCposFrom   +=  RetSqlName("RHC")+ " RHC  %"
		
	cWhere :="%    RCO_CODIGO=RHD_CODRPA"
	cWhere +="     AND RA_MAT=RHD_MAT"
	cWhere +="     AND RA_FILIAL=RHD_FILIAL"
	cWhere +="     AND RHD_FILIAL=RHC_FILIAL"
	cWhere +="     AND RHC_MAT=RHD_MAT"
	cWhere +="     AND RHC_ANOMES=RHD_ANOMES"
		
	cWhere +="     AND RHD_CODRPA IN ("+CLISPAT+")"//Registro Patronal 
	cWhere +="     AND RHC_DTMOV BETWEEN '"+Dtos(dDataIni)+"' AND '"+Dtos(dDataFin)+"' "//AnioMes
		
	cWhere +="     AND RHC_FILIAL  ='"+cFilRHC+"'"
	cWhere +="     AND RCO_FILIAL= '"  + xFilial("RCO" , RHD->RHD_FILIAL) +"' "
	cWhere +="     AND RA_FILIAL  ='"+cFilSRA+"'"
	cWhere +="     AND RHD_FILIAL  ='"+cFilRHD+"'"

    If ( TcSrvType()=="AS/400" )
		cWhere+="     AND RHD.@DELETED@  = ' ' " 
		cWhere+="     AND RCO.@DELETED@  = ' ' " 
		cWhere+="     AND SRA.@DELETED@  = ' ' " 
		cWhere+="     AND RHC.@DELETED@  = ' ' %" 
	ELSE 
		cWhere+="     AND RHD.D_E_L_E_T_  = ' ' " 
		cWhere+="     AND RCO.D_E_L_E_T_  = ' ' " 		
		cWhere+="     AND SRA.D_E_L_E_T_  = ' ' " 						
		cWhere+="     AND RHC.D_E_L_E_T_  = ' ' %" 				
	ENDIF                   

	If nOrdem == 2
		cOrdem := "% RCO_NREPAT,RA_NOME,RHC_DTMOV,RHC_SEQMVT%"
	ElseIf nOrdem == 3
		cOrdem := "% RCO_NREPAT,RHC_MAT,RHC_DTMOV,RHC_SEQMVT%"
	ElseIf nOrdem == 1
		cOrdem := "%RCO_NREPAT,RA_PRISOBR,RA_SECSOBR,RA_PRINOME,RA_SECNOME,RHC_DTMOV,RHC_SEQMVT%"
	Endif
	

	BeginSql alias cAliasRHC 
		SELECT  %exp:cCposQuery%
		FROM  %exp:cCposFrom% 
		WHERE %exp:cWhere%     
		ORDER BY %exp:cOrdem%
	EndSql
    //Los campo tipo fecha los deje como tipo fecha, pues por query son recuperados como string
	TCSetField(cAliasRHC,"RHC_DTMOV","D")
	TCSetField(cAliasRHC,"RHC_DTMINF","D")   

#ELSE

	MSGERROR("No esta disponible para DBF")
	
#ENDIF	

Begin Sequence   

	dbSelectArea( cAliasRHC )
	(cAliasRHC)->(DbGoTop())  
	If (cAliasRHC)->(!Eof())
		While (cAliasRHC )->(!Eof())
			
			aAdd(aRHC,{(cAliasRHC)->RA_NOME,;	 //1
			(cAliasRHC)->RCO_ATIVID,; //2
			(cAliasRHC)->RCO_CVEZON,; //3
			(cAliasRHC)->RCO_NOME,;//4
			(cAliasRHC)->RA_RG,;//5
			(cAliasRHC)->RA_PRISOBR,;//6
			(cAliasRHC)->RA_SECSOBR  ,; //7
			(cAliasRHC)->RA_PRINOME, ;//8
			(cAliasRHC)->RA_SECNOME, ; //9
			(cAliasRHC)->RA_CIC, ; //10
			(cAliasRHC)->RA_CURP, ; //11
			(cAliasRHC)->RHC_MAT, ; //12
			(cAliasRHC)->RHC_RTPAT, ; //13
			(cAliasRHC)->RHC_CYVPAT, ; //14
			(cAliasRHC)->RHC_CYVTRA, ; //15
			(cAliasRHC)->RHC_INFONA, ; //16
			(cAliasRHC)->RHC_AMORCF, ; //17
			(cAliasRHC)->RHC_VALINF,; //18
			(cAliasRHC)->RHC_TPMINF,;//19
			(cAliasRHC)->rhc_TIPINF,; //20
			(cAliasRHC)->RHC_NUMINF,; //21
			(cAliasRHC)->RHC_TPMOV,; //22
			(cAliasRHC)->RHC_DTMOV,;//23  			(cAliasRHC)->RHC_NDTRAB-(cAliasRHC)->RHC_NDINC-(cAliasRHC)->RHC_NDFAL,;//24
			(cAliasRHC)->RHC_NDTRAB-(cAliasRHC)->RHC_NDFAL,;//24
			(cAliasRHC)->RHC_SALDII,; //25
			(cAliasRHC)->RHC_NDINC,;//26
			(cAliasRHC)->RHC_NDFAL,;//27
			(cAliasRHC)->RCO_NREPAT,;//28
			(cAliasRHC)->RHD_VALINF,;//29
			(cAliasRHC)->RHD_FATRSC,;//30
			(cAliasRHC)->RHC_DTMINF,; //31
			(cAliasRHC)->RHC_CFPAT,; //32
			(cAliasRHC)->RHC_EXEPAT,; //33
			(cAliasRHC)->RHC_EXETRA,; //34
			(cAliasRHC)->RHC_PDTRA,; //35
			(cAliasRHC)->RHC_PDPAT,; //36
			(cAliasRHC)->RHC_GMPPAT,; //37
			(cAliasRHC)->RHC_GMPTRA,; //38
			(cAliasRHC)->RHC_IVPAT,; //39
			(cAliasRHC)->RHC_IVTRA,; //40
			(cAliasRHC)->RHC_GPSPAT,; //41
			(cAliasRHC)->RHC_GPSPAT,; //42//GPSTRA
			(cAliasRHC)->RHC_RETPAT	})//43
			If (cAliasRHC)->RHC_TPMOV=="00"
				aRHBT:=leeRHB((cAliasRHC)->RHC_MAT,(cAliasRHC)->RHC_FILIAL,dDataFin )
				IF!EMPTY(aRHBT)
					aRHC[len(aRHC)][31]:= aRHBT[1][3]
					aRHC[len(aRHC)][19]:= aRHBT[1][4]
				EndIf
			Endif
			
			(cAliasRHC)->(dbskip()) 
		EndDo
		oReport:SetMeter(len(aRHC)) 
	EndIf

	If  nIRhc <= len(aRHC)
				
		If _cPar04==1//Tipo de Informe
			oSection1:Init()  
//			oSection2:Init()  
//			oSection3:Init()  
			oSection4:Init()
			oSection5:Init()
			
			oSection5:hIDE()
			oSection5:PrintLine()
			oSection5:sHOW()
			
			While nIRhc <= len(aRHC)
			
				cRegPAT:=aRHC[nIRhc,28]
				
				oSection1:Cell("RCO_NREPAT"):SetValue(aRHC[nIRhc,28])//Registro Patronal
				oSection1:Cell("SM0_CGC"):SetValue(SM0->M0_CGC)//RFC.
				oSection1:Cell("RCO_ATIVID"):SetValue(aRHC[nIRhc,2])//"Actividad "

				oSection1:Cell("RCO_CVEZON"):SetValue(aRHC[nIRhc,3])//"Area Geografica: Zona "
				oSection1:Cell("RCO_NOME"):SetValue(aRHC[nIRhc,4])//"Nombre o Razón Social: "
				oSection1:Cell("MESANO"):SetValue(cMesIni+"-"+cMesFin+"/"+_cPar02)//Bimestre/Anio 

				oSection1:Cell("APORPAT"):SetValue(STR(nAporPat))//Aportacion patronal.

				oSection1:printline()
//				Osection2:Printline()
//				Osection3:Printline() 
				oReport:Fatline()		
				
				nDiasT:=0
				nInc:=0
				nAus:=0
				nRetiro:=0
				nCyvtra:=0
				nCyvpat:=0
				nAmorti:=0
				nSuma:=0
				nSumaI:=0
				nApoPat:=0
			
				nRCV:=0
				nSinf:=0

				nSscre:=0
				nSabono:=0
				nAmort:=0

				nAcredi:=0
				nTEmp:=0
				//nSMGDF:=0
			
				While nIRhc <= len(aRHC).And. cRegPAT==aRHC[nIRhc,28] 
					
					oSection4:Cell("RA_RG"):SetValue(aRHC[nIRhc,5])//"Numero de Seguridad Social"
					oSection4:Cell("NOMECOM"):SetValue(rtrim(aRHC[nIRhc,6])+" "+rtrim(aRHC[nIRhc,7])+" "+rtrim(aRHC[nIRhc,8])+" "+rtrim(aRHC[nIRhc,9]))//Nombre Completo
					oSection4:Cell("EMPLEADO"):SetValue(aRHC[nIRhc,12])//"Empleado"#Matricula
					oSection4:Cell("RA_CIC"):SetValue(aRHC[nIRhc,10])//"R.F.C."
					oSection4:Cell("RA_CURP"):SetValue(aRHC[nIRhc,11])//"C.U.R.P."
					oSection4:PrintLine()
					
					cMat:=aRHC[nIRhc,12] 
					 
					nTEmp++//Num de Empleados
					
					If (!Empty(ARHC[nIRhc,21]),nAcredi++,)//Num de Empleados con credito Infonavit---	

					While nIRhc <= len(aRHC).And. cMat==ARHC[nIRhc,12]  .And. cRegPAT==aRHC[nIRhc,28]  
						oSection5:Cell("RHC_TPMOV"):Show()
						oSection5:Cell("RHC_DTMOV"):Show()
						oSection5:Cell("RHC_NDTRAB"):Show()
						oSection5:Cell("RHC_SALDII"):Show()
						oSection5:Cell("RHC_NDINC"):Show()
						oSection5:Cell("RHC_NDFAL"):Show()
						oSection5:Cell("RETIRO"):Show()
						oSection5:Cell("CYVPAT"):Show()
						oSection5:Cell("CYVOBR"):Show()
						oSection5:Cell("SUMA"):Show()
						oSection5:Cell("APORTPAR"):Show()
						oSection5:Cell("PCFVSM"):Show()
						oSection5:Cell("AMORT"):Show()
						oSection5:Cell("SUMAI"):Show()
						oSection5:Cell("MOTCRED"):Show() 
						oSection5:Cell("RHC_NUMINF"):Show()
						
						
						//Inicializa Celdas
						oSection5:Cell("RHC_TPMOV"):SetValue(Space(1))
						oSection5:Cell("RHC_DTMOV"):SetValue(Space(1))
						oSection5:Cell("RHC_NDTRAB"):SetValue(0)
						oSection5:Cell("RHC_SALDII"):SetVALue(0)
						oSection5:Cell("RHC_NDINC"):SetValue(0)
						oSection5:Cell("RHC_NDFAL"):SetVALUE(0)
						oSection5:Cell("RETIRO"):SetValue(Space(1))
						oSection5:Cell("CYVPAT"):SetValue(Space(1))
						oSection5:Cell("CYVOBR"):SetValue(Space(1))
						oSection5:Cell("SUMA"):SetValue(0)
						oSection5:Cell("APORTPAR"):SetValue(0)
						oSection5:Cell("PCFVSM"):SetValue(Space(1))
						oSection5:Cell("AMORT"):SetValue(0)
						oSection5:Cell("SUMAI"):SetValue(0)
						oSection5:Cell("MOTCRED"):SetValue(Space(1)) 
						oSection5:Cell("RHC_NUMINF"):SetValue(Space(1))
						/////
						cCons:=""
						//Registro de Infonavit
						//Para determinar que el registro RHC es de Mov. de Créd. Infonavit, las siguientes columnas vienen en Cero:
						If ARHC[nIRhc,13] ==0 .And. ARHC[nIRhc,14]==0 .And. ARHC[nIRhc,15]==0 .AND.  ARHC[nIRhc,22]<>"02"
							oSection5:Cell("RHC_TPMOV"):Hide()
							oSection5:Cell("RHC_DTMOV"):Hide()
							oSection5:Cell("RHC_NDTRAB"):Hide()
							oSection5:Cell("RHC_SALDII"):Hide()
							oSection5:Cell("RHC_NDINC"):Hide()
							oSection5:Cell("RHC_NDFAL"):Hide()
							oSection5:Cell("RETIRO"):Hide()
							oSection5:Cell("CYVPAT"):Hide()
							oSection5:Cell("CYVOBR"):Hide()
							oSection5:Cell("SUMA"):Hide()
							nSinf += ARHC[nIRhc,16]+ARHC[nIRhc,17] //(cAliasRHC)->RHC_INFONA+(cAliasRHC)->RHC_AMORCF //Infonavit
						Else //Movimiento Imss
							If nIRhc < len (aRHC)
								If cMesIni== SUBSTR(DTOS(ARHC[nIRhc,23]),5,2) .AND. (ARHC[nIRhc+1,22]=="00" .and. cMesFin== SUBSTR(DTOS(ARHC[nIRhc+1,23]),5,2)) // RHC_TPMOV=="00"
									nIRhc++
									// Al registro actual (último del MES1) sumar las columnas del siguiente registro (primero del MES2) cuando:
									// RHC_TPMOV (primero del MES2) = ‘00’
									// Columnas a respetar del último registro del MES1:
									ARHC[nIRhc,22]:=ARHC[nIRhc-1,22]// RHC_TPMOV
									ARHC[nIRhc,23]:=ARHC[nIRhc-1,23]// RHC_DTMOV
									ARHC[nIRhc,25]:=ARHC[nIRhc-1,25]// RHC_SALDII
									// Las columnas a Sumar son:
									ARHC[nIRhc,24]+=ARHC[nIRhc-1,24]// RHC_NDTRAB
									ARHC[nIRhc,26]+=ARHC[nIRhc-1,26]// RHC_NDINC
									ARHC[nIRhc,27]+=ARHC[nIRhc-1,27]// RHC_NDFAL
									ARHC[nIRhc,43]+=ARHC[nIRhc-1,43]// RHC_RETPAT   
									ARHC[nIRhc,14]+=ARHC[nIRhc-1,14]// RHC_CYVPAT
									ARHC[nIRhc,15]+=ARHC[nIRhc-1,15]// RHC_CYVTRA
									ARHC[nIRhc,16]:=ROUND(	ARHC[nIRhc,16]+ARHC[nIRhc-1,16],2)// RHC_INFONA
									ARHC[nIRhc,17]:=ROUND(ARHC[nIRhc,17]+ARHC[nIRhc-1,17],2)// RHC_AMORCF
									// Campos a respetar del primer registro del MES2:
									// RHC_TPMINF
									// RHC_DTMINF
									// RHC_VALINF
									// RHC_TIPINF
									// RHC_NUMINF
								EndIf
							EndIf
						EndIf
						oSection5:Cell("RHC_DTMOV"):SetValue(ARHC[nIRhc,23])
						oSection5:Cell("RHC_NDTRAB"):SetValue(ARHC[nIRhc,24])
						oSection5:Cell("RHC_SALDII"):SetValue(ARHC[nIRhc,25])
						oSection5:Cell("RHC_NDINC"):SetValue(ARHC[nIRhc,26])
						oSection5:Cell("RHC_NDFAL"):SetValue(ARHC[nIRhc,27])
						oSection5:Cell("RETIRO"):SetValue(ARHC[nIRhc,43])
						oSection5:Cell("CYVPAT"):SetValue(ARHC[nIRhc,14])
						oSection5:Cell("CYVOBR"):SetValue(ARHC[nIRhc,15])//25
						oSection5:Cell("APORTPAR"):SetValue(ARHC[nIRhc,16])//aPORT pAT.
						oSection5:Cell("RHC_NUMINF"):SetValue(ARHC[nIRhc,21])//número de infonavit
						oSection5:Cell("AMORT"):SetValue(ARHC[nIRhc,17])//RHC_AMORCFs
						//Suma
						oSection5:Cell("SUMA"):SetValue(ARHC[nIRhc,43]+ARHC[nIRhc,14]+ARHC[nIRhc,15])//RHC_RTPAT+RHC_CYVPAT+RHC_CYVTRA
						//%o C.F. o VSM
						If ARHC[nIRhc,20]=="1" //RHC_TIPINF
							cCons:="%"
						ElseIf ARHC[nIRhc,20]=="2"
							cCons:="CF " //Space(3)
						ElseIf ARHC[nIRhc,20]=="3"
							cCons:="VSM"
						EndIf
						//oSection5:Cell("PCFVSM"):SetValue(ALLTRIM(str(ARHC[nIRhc,18]))+" "+cCons)//
						oSection5:Cell("PCFVSM"):SetValue(alltrim(transform(ARHC[nIRhc,18],PesqPict("RHD","RHD_VALINF"))+" "+cCons))//
						//Suma
						oSection5:Cell("SUMAI"):SetValue(ARHC[nIRhc,16]+ARHC[nIRhc,17])//(cAliasRHC)->RHC_INFONA+(cAliasRHC)->RHC_AMORCF
													//Movimiento de Credito
						If Empty(ARHC[nIRhc,19])
							//oSection5:Cell("MOTCRED"):SetValue(0)	//En blanco
							oSection5:Cell("MOTCRED"):Hide()	//En blanco
						ElseIf ARHC[nIRhc,19]=="15"
							oSection5:Cell("MOTCRED"):SetValue("ICV"+" "+Dtoc(ARHC[nIRhc,31]))
						ElseIf ARHC[nIRhc,19]=="16"
							oSection5:Cell("MOTCRED"):SetValue("FSD"+" "+Dtoc(ARHC[nIRhc,31]))
						ElseIf ARHC[nIRhc,19]=="17"
							oSection5:Cell("MOTCRED"):SetValue("RD "+" "+Dtoc(ARHC[nIRhc,31]))
						ElseIf ARHC[nIRhc,19]=="18"
							oSection5:Cell("MOTCRED"):SetValue("MTD"+" "+Dtoc(ARHC[nIRhc,31]))
						ElseIf ARHC[nIRhc,19]=="19"
							oSection5:Cell("MOTCRED"):SetValue("MVD"+" "+Dtoc(ARHC[nIRhc,31]))
						ElseIf ARHC[nIRhc,19]=="20"
							oSection5:Cell("MOTCRED"):SetValue("MNC"+" "+Dtoc(ARHC[nIRhc,31])	)
							//oSection5:Cell("MOTCRED"):SetValue("MNC"+" "+Dtoc((cAliasRHC)->RHC_DTMINF)
						EndIf
						//oSection5:Cell("MOTCRED"):SetValue(0)
						//Si es baja "02" todas las columnas quedan en blanco excepto "Fecha"
						Do Case
							Case ARHC[nIRhc,22]=="00"
								oSection5:Cell("RHC_TPMOV"):SetValue(Space(3))//Clave
							Case ARHC[nIRhc,22]=="01"
								oSection5:Cell("RHC_TPMOV"):SetValue("A")//Clave
							Case ARHC[nIRhc,22]=="02"//Baja
								oSection5:Cell("RHC_NDTRAB"):Hide()
								oSection5:Cell("RHC_SALDII"):Hide()
								oSection5:Cell("RHC_NDINC"):Hide()
								oSection5:Cell("RHC_NDFAL"):Hide()
								oSection5:Cell("RETIRO"):Hide()
								oSection5:Cell("CYVPAT"):Hide()
								oSection5:Cell("CYVOBR"):Hide()
								oSection5:Cell("SUMA"):Hide()
								oSection5:Cell("APORTPAR"):Hide()
								oSection5:Cell("PCFVSM"):Hide()
								oSection5:Cell("AMORT"):Hide()
								oSection5:Cell("SUMAI"):Hide()
								oSection5:Cell("MOTCRED"):Hide()
								oSection5:Cell("RHC_NUMINF"):Hide()
								oSection5:Cell("RHC_TPMOV"):SetValue("B")//Clave
								//nTTotal+=(-(cAliasRHC)->SUMAPAT-(cAliasRHC)->SUMAOBR)
							Case ARHC[nIRhc,22]=="05"
								oSection5:Cell("RHC_TPMOV"):SetValue("M/S")//Clave
							Case ARHC[nIRhc,22]=="06"
								oSection5:Cell("RHC_TPMOV"):SetValue("R")//Clave
						End Case
							
						//Acumuladores
						nRcv:=nRcv+ARHC[nIRhc,14]+ARHC[nIRhc,15]
						IIF(ARHC[nIRhc,17]==0,nSsCre+=ARHC[nIRhc,16],)//aportación patronal s/credito
						IIF(ARHC[nIRhc,17]<>0,nSabono+=ARHC[nIRhc,16],)//aportación patronal c/ credito
						
						nAmort+=ARHC[nIRhc,17]
						//Acumuladores para totales por reg. pat.
						nDiasT+=ARHC[nIRhc,24]
						nInc+=ARHC[nIRhc,26]
						nAus+=ARHC[nIRhc,27]
						nRetiro+=ARHC[nIRhc,43]
						nCyvtra+=ARHC[nIRhc,25]
						nCyvpat+=ARHC[nIRhc,24]
						nAmorti+=nAmort
						nSuma+=ARHC[nIRhc,13]+ARHC[nIRhc,14]+ARHC[nIRhc,15]
						nSumaI+=ARHC[nIRhc,16]+ARHC[nIRhc,17]
						nApoPat+=ARHC[nIRhc,16]
						
						oSection5:PrintLine()
						nIRhc++//(cAliasRHC)->(dbSkip())
						oReport:IncMeter()   //regla de avance
					EndDo //Matricula
				EndDo //Registro Pat.
				//Totales en linea
				oSection6:Init()
				oSection6:Cell("RHC_NDTRAB"):SetValue(nDiasT)  
				oSection6:Cell("RHC_NDINC"):SetValue(nInc)  
				oSection6:Cell("RHC_NDFAL"):SetValue(nAus)  
				oSection6:Cell("RETIRO"):SetValue(nRetiro)  
				oSection6:Cell("CYVPAT"):SetValue(nCyvpat)  
				oSection6:Cell("CYVOBR"):SetValue(nCYvtra)  
				oSection6:Cell("SUMA"):SetValue(nSuma)    
				oSection6:Cell("APORTPAR"):SetValue(nApoPat)  
				oSection6:Cell("AMORT"):SetValue(nAmorti)  
				oSection6:Cell("SUMAI"):SetValue(nSumai)  
				oSection6:Printline()
				oSection6:Finish()
				//Trailer
				oSection7:Init()
				oSection7:Cell("DESC1"):SHOW()
				oSection7:Cell("IMPORT1"):SHOW()
				oSection7:Cell("DESC1"):SetValue(STR0112)//"Total de Trabajadores...................:"
				oSection7:Cell("IMPORT1"):SetValue(nTEmp)
				oSection7:Cell("DESC2"):SetValue(STR0108)//"Aportacion Patronal S/Credito...........:"
				oSection7:Cell("IMPORT2"):SetValue(nSscre)
				oSection7:PrintLine()
	
						
				oSection7:Cell("DESC1"):SetValue(STR0105)// "Total por pagar de RVC..................:"
				oSection7:Cell("IMPORT1"):SetValue(nRCV)
				oSection7:Cell("DESC2"):SetValue(STR0109)//"Aportacion Patronal C/Credito.............:" 
				oSection7:Cell("IMPORT2"):SetValue(nSabono)
				oSection7:PrintLine()
						
				oSection7:Cell("DESC1"):SetValue(STR0106)//"Total por pagar Infonavit...............:"
				oSection7:Cell("IMPORT1"):SetValue(nSinf)
				oSection7:Cell("DESC2"):SetValue(STR0110)//"Amortizacion............................:" 
				oSection7:Cell("IMPORT2"):SetValue(nAmort)
				oSection7:PrintLine()
	
				oSection7:Cell("DESC1"):SetValue(STR0107)//"Total a Pagar...........................:" 
				oSection7:Cell("IMPORT1"):SetValue(nRCV+nSinf)
				oSection7:Cell("DESC2"):SetValue(STR0106)//"Total por pagar Infonavit...............:"
				oSection7:Cell("IMPORT2"):SetValue(nSscre+nSabono+nAmort)
				oSection7:PrintLine()
					
				oSection7:Cell("DESC1"):Hide()
				oSection7:Cell("IMPORT1"):Hide()
				oSection7:Cell("DESC2"):SetValue(STR0111)//"Acreditados.............................:"
				oSection7:Cell("IMPORT2"):SetValue(nAcredi)
				oSection7:PrintLine()
				oSection7:Finish()
				
				
				//Pie de Pagina
				oSection8:Init()
				oSection8:Cell("DESC1"):Show()
				oSection8:Cell("DESC2"):Show()
				oSection8:Cell("DESC1"):SetValue(STR0052)  //"# Trabajadores con Articulo 33"
				oSection8:Cell("DESC2"):SetValue(STR0060)  //"C.y V.- Cesantia y Vejez  			   "
				oSection8:Cell("DESC3"):SetValue(STR0128)  //"AMORTIZACION DE CREDITOS DE VIVIENDA"
				oSection8:PrintLine()
				oSection8:Cell("DESC1"):SetValue(STR0053)	//"& Trabajadores Pensionados IV     	"
				oSection8:Cell("DESC2"):SetValue(STR0061)  //"% o CF - Porcentaje o Cuota Fija"
				oSection8:Cell("DESC3"):SetValue(STR0129)  //"ICV Início de Crédito de Vivienda"
				oSection8:PrintLine()
				oSection8:Cell("DESC1"):SetValue(STR0054) 	//"% Trabajadores Pensionados CV"
				oSection8:Cell("DESC2"):SetValue(STR0062)  //"SDI    - Salario Diario Integrado"
				oSection8:Cell("DESC3"):SetValue(STR0130)   //"FSD Fecha de Suspención de Descuento"
				oSection8:PrintLine()
				oSection8:Cell("DESC1"):SetValue(STR0055)  //"* Trabajadores con Semana Reducida	"
				oSection8:Cell("DESC2"):SetValue(STR0058)  //"Inc.   - Incapacidades"
				oSection8:Cell("DESC3"):SetValue(STR0131)  //"RD Reinicio de Descuento"
				oSection8:PrintLine()
				oSection8:Cell("DESC1"):SetValue(STR0056)  //"** Trabajadores con Jornada Reducida"
				oSection8:Cell("DESC2"):SetValue(STR0059)   //"Aus.  - Ausentismos					 	"
				oSection8:Cell("DESC3"):SetValue(STR0132)   //"MTD Modificación de Tipo de Descuento"
				oSection8:PrintLine()
				oSection8:Cell("DESC1"):Hide()
				oSection8:Cell("DESC2"):SetValue(STR0067+TRANSFORM(nSMGDF,'999.99'))   //"Salario Minimo del D.F. " 
				oSection8:Cell("DESC3"):SetValue(STR0133)   //"MVD Modificación de Valor de Descuento"
				oSection8:PrintLine()
				oSection8:Cell("DESC1"):Hide()
				oSection8:Cell("DESC2"):Hide()
				oSection8:Cell("DESC3"):SetValue(STR0134)  //"MNC Modificación de Número de Crédito"
				oSection8:PrintLine()
				oReport:SkipLine(3)
				oSection8:Cell("DESC1"):Hide()
				oSection8:Cell("DESC2"):Hide()
				oSection8:Cell("DESC3"):SetValue(STR0137+ alltrim(TRANSFORM((FTABELA("S007",1,18)),'999,999.99'))+STR0138) //"* Se toma encuenta $"" por concepto de Seguro de Daños de Vivienda "
				oSection8:PrintLine()
				
				oSection8:Finish()
	            oReport:EndPage() //-- Salta Pagina
			EndDo   
			oSection5:Finish()
		
		Else //Imforme Resumen
			oSection1:Init()  
			oSection2:Init()
			While nIRhc <= LEN(aRHC)
		
				cRegPAT:=aRHC[nIRhc,28]//Registro Patronal
				aTot:={}
				Aadd(aTot,{0,0,0,0,0,0,0,0,0,0,0,0,0,0,0})
							
				oSection1:Cell("RCO_NREPAT"):SetValue(ARHC[nIRhc,28])//Registro Patronal
				oSection1:Cell("SM0_CGC"):SetValue(SM0->M0_CGC)//RFC.
				//oSection1:Cell("RCO_ATIVID"):SetValue(ARHC[nIRhc,2])//"Actividad "

				//oSection1:Cell("RCO_CVEZON"):SetValue(ARHC[nIRhc,3])//"Area Geografica: Zona "
				oSection1:Cell("RCO_NOME"):SetValue(ARHC[nIRhc,4])//"Nombre o Razón Social: "
				oSection1:Cell("MESANO"):SetValue(cMesIni+"-"+cMesFin+"/"+_cPar02)//Bimestre/Anio 

				//oSection1:Cell("APORPAT"):SetValue(nAporPat)//Aportacion patronal.

				oSection1:printline()
				oReport:Fatline()		
				
				While  nIRhc <= LEN(aRHC) .AND. cRegPAT==aRHC[nIRhc,28]
					cMat:=ARHC[nIRhc,12]
					
					//If (ARHC[nIRhc,20]!=" ",aTot[1,13]+=1,)//Num de Empleados con infonavit
					
					If (!EMPTY(ARHC[nIRhc,20]),aTot[1,13]++,)//Num de Empleados con infonavit					
					
					aTot[1,1]+=1//Num de Empleados
				
					While nIRhc <= len(aRHC).And. cMat==ARHC[nIRhc,12]   .And. cRegPAT==aRHC[nIRhc,28]
						aTot[1,2]+=ARHC[nIRhc,24]
						aTot[1,3]+=ARHC[nIRhc,32]
						aTot[1,4]+=ARHC[nIRhc,33]+ARHC[nIRhc,34]
						aTot[1,5]+=ARHC[nIRhc,35]+ARHC[nIRhc,36]
						aTot[1,6]+=ARHC[nIRhc,37]+ARHC[nIRhc,38]
						aTot[1,7]+=ARHC[nIRhc,13]
						aTot[1,8]+=ARHC[nIRhc,39]+ARHC[nIRhc,40]
						aTot[1,9]+=ARHC[nIRhc,41]//+ARHC[nIRhc,42]
						aTot[1,10]+=ARHC[nIRhc,43]
						aTot[1,11]+=ARHC[nIRhc,14]+ARHC[nIRhc,15]
						
						If (ARHC[nIRhc,17]<>0,aTot[1,12]+=ARHC[nIRhc,16],)//APORTACION PATRPONAL CON CREDITO
						If (ARHC[nIRhc,17]==0,aTot[1,14]+=ARHC[nIRhc,16],)//APORTACION PAT SIN CREDITO
						aTot[1,15]+=ARHC[nIRhc,17]//AMORTIZACION
						
						nIRhc++
						oReport:IncMeter()   //regla de avance
					EndDo
				EndDo
				nTa:=aTot[1,3]+aTot[1,4]+aTot[1,5]+aTot[1,6]+aTot[1,7]+aTot[1,8]+aTot[1,9]
			    nTb:=aTot[1,10]+aTot[1,11]
			    //nTc:=nTb+aTot[1,12]
			    nTc:=aTot[1,14]	+aTot[1,12]+aTot[1,15]			    

				oSection2:Cell("DESC"):SetValue(STR0068)//"PARA ABONO EN CUENTA DEL IMSS"
				oSection2:PrintLine()
				oReport:SkipLine()               

				oSection2:Cell("DESC"):SetValue(STR0113)//"EMPLEADOS                         ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,1])
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0114)//"DIAS COTIZADOS                    ------>"  
				oSection2:Cell("IMPORT"):SetValue(aTot[1,2])
				oSection2:PrintLine()
				oReport:SkipLine()

				oSection2:Cell("DESC"):SetValue(STR0115)//"CUOTA FIJA                        ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,3])
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0116)//"EXCEDENTE 3 SMGDF                 ------>" 
				oSection2:Cell("IMPORT"):SetValue()
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0117)//"PRESTACIONES EN DINERO            ------>" 
				oSection2:Cell("IMPORT"):SetValue(aTot[1,5])//,PesqPict("RHC","RHC_PDPAT")     ,TamSx3("RHC_PDPAT")[1])// RHC_PDPAT + RHC_PDTRA
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0118)//"GASTOS MEDICOS PENSIONADOS        ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,6])// ,PesqPict("RHC","RHC_GMPPAT")     ,TamSx3("RHC_GMPPAT")[1])//RHC_GMPPAT + RHC_GMPTRA
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0119) //"RIESGOS DE TRABAJO                ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,7])//,PesqPict("RHC","RHC_RTPAT")     ,TamSx3("RHC_RTPAT")[1])//RHC_RTPAT
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0120)//"INVALIDEZ Y VIDA                  ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,8])//,PesqPict("RHC","RHC_IVPAT")     ,TamSx3("RHC_IVPAT")[1])//RHC_IVPAT + RHC_IVTRA
				oSection2:PrintLine()

				oSection2:Cell("DESC"):SetValue(STR0121)//"GUARDERIAS Y PRES. SOC.           ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,9])// ,PesqPict("RHC","RHC_GPSPAT")     ,TamSx3("RHC_GPSPAT")[1])//RHC_GPSPAT
				oSection2:PrintLine()
				
				oReport:SkipLine()
				oSection2:Cell("DESC"):SetValue(STR0122) //"TOTAL                             ------>"	
				oSection2:Cell("IMPORT"):SetValue(nta)	
				oSection2:PrintLine()			

				oReport:SkipLine()
				oSection2:Cell("DESC"):SetValue(STR0079)//"PARA ABONO EN CUENTA INDIVIDUAL"
				oSection2:Cell("IMPORT"):SetValue(nil)	
				oSection2:PrintLine()
				oReport:SkipLine()

				oSection2:Cell("DESC"):SetValue(STR0123)	//"RETIRO                            ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,10])	
				oSection2:PrintLine()	

				oSection2:Cell("DESC"):SetValue(STR0124)//"CESANTIA EN EDAD AVANZADA Y VEJEZ ------>"	
				oSection2:Cell("IMPORT"):SetValue(aTot[1,11])	
				oSection2:PrintLine()	
				
				oReport:SkipLine()
				oSection2:Cell("DESC"):SetValue(STR0122)//"TOTAL                             ------>"	
				oSection2:Cell("IMPORT"):SetValue(ntb)	
				oSection2:PrintLine()	                      
				
				oReport:SkipLine()
				oSection2:Cell("DESC"):SetValue(STR0082)//"PARA ABONO EN CUENTA DEL INFONAVIT"	
				oSection2:Cell("IMPORT"):SetValue(nil)					
				oSection2:PrintLine()
				oReport:SkipLine()

				oSection2:Cell("DESC"):SetValue(STR0135)	//"APORTACION PATRONAL SIN CRÉDIT0   ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,14])	
				oSection2:PrintLine()	

				oSection2:Cell("DESC"):SetValue(STR0125)	//"APORTACION PATRONAL CON CRÉDIT0   ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,12])	
				oSection2:PrintLine()	

				oSection2:Cell("DESC"):SetValue(STR0136)	//"AMORIZACION                       ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,15])	
				oSection2:PrintLine()	
				
				oReport:SkipLine()
				oSection2:Cell("DESC"):SetValue(STR0122) //"TOTAL                             ------>"	
				oSection2:Cell("IMPORT"):SetValue(nTc)	
				oSection2:PrintLine()	
				
				oReport:SkipLine()
				oSection2:Cell("DESC"):SetValue(STR0126)	//"TOTAL ACREDITADOS                 ------>"
				oSection2:Cell("IMPORT"):SetValue(aTot[1,13])	
				oSection2:PrintLine()	
				
				oReport:SkipLine()
				oSection2:Cell("DESC"):SetValue(STR0127) //"TOTAL A PAGAR                     ------>"	
				oSection2:Cell("IMPORT"):SetValue(nTa+nTb+nTc)	
				oSection2:PrintLine()	
				oReport:EndPage() //-- Salta Pagina

			EndDo         
			oSection2:Finish()
		EndIf
						
		oSection1:Finish()
		//oSection2:Finish()
	EndIf
End Sequence

(cAliasRHC)->(dbCloseArea())   

Return ( Nil )
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao     ³ leeRHB       Autor ³ Flor Monroy        ³ Data ³ 11.01.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descripción³ Función para impresion del Reporte                        ³±± 
±±³           ³                                                           ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxis   ³ leeRHB()                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function leeRHB(cMat,cFilRHB,dFecha )

Local  cAliasRHB:=CriaTrab(Nil,.F.)
Local cQueryRHB :=""
Local aRHB:={}

cQueryRHB:= "select RHB_FILIAL,RHB_MAT,MAX(RHB_DTMINF) RHB_DTMINF ,RHB_TPMINF "
cQueryRHB+= " from "+RetSqlName("RHB") 
cQueryRHB+=" WHERE RHB_DTMINF<='"+dtos(dFecha)+"'"
cQueryRHB+=" AND RHB_FILIAL='"+cFilRHB+"'"
cQueryRHB+=" AND RHB_MAT='"+cMat+"'"
cQueryRHB+= " AND D_E_L_E_T_  = ' ' "
cQueryRHB+= " group by  RHB_FILIAL,RHB_MAT,RHB_DTMINF,RHB_TPMINF "
cQueryRHB := ChangeQuery(cQueryRHB)
DbUseArea( .T., 'TOPCONN', TCGENQRY(,, cQueryRHB), cAliasRHB, .T., .T. )
	TCSetField(cAliasRHB,"RHB_DTMINF","D")  

WHILE  !(cAliasRHB)->(EOF())
	//iF 

		AADD(aRHB,{(cAliasRHB)->RHB_FILIAL,(cAliasRHB)->RHB_MAT,(cAliasRHB)->RHB_DTMINF,(cAliasRHB)->RHB_TPMINF})
	//eLSE
	(cAliasRHB)->(dbskip()) 
enddo
(cAliasRHB)->(dbCloseArea())  
Return (aRHB)

        
/*      
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER660   ºAutor  ³Microsiga           º Data ³  04/26/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³  GPER660BIM()                                              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function GPER660BIM()
        
Local cTitulo:= STR0094
Local MvPar
Local MvParDef:=""
Local lRet
Local aBim := {}
Local ni:=0
        
MvPar:=&(Alltrim(ReadVar()))		 // Carrega Nome da Variavel do Get em Questao
mvRet:=Alltrim(ReadVar())			 // Iguala Nome da Variavel ao Nome variavel de Retorno
        
aBim := {;
			"1 - " + STR0088,;	//"Enero/Febrero"
			"2 - " + STR0089,;	//"Marzo/Abril"
			"3 - " + STR0090,;	//"Mayo/Junio"
			"4 - " + STR0091,;	//"Julio/Agosto"
			"5 - " + STR0092,;	//"Septiembre/Octubre"
			"6 - " + STR0093;	//"Noviembre/Diciembre"
		}  

IF f_Opcoes(@MvPar,cTitulo,aBim,MvParDef,,,.T.,1,1)  
	&MvRet := Substr(MvPar,1,1)						
EndIF   
        
Return lRet
                                                                                  

/*      
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER660  ³ Autor ³ Silvia Taguti         ³ Data ³ 07/06/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Demonstrativo Bimestral Calculo SUA                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER660()                                                  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function GPER660_OLD()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis locais.												³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cDesc1	:= STR0001							// "Demonstrativo Bimestral"
Local cDesc2	:= STR0002							// Será impresso de acordo com os parametros solicitados pelo usuario."
Local cDesc3	:= STR0003							// "A geracao deve ser executada previamente"
Local aArea		:= RCQ->( GetArea() )				// Ponteiros atuais do arquivo RCQ
Local nLin         := 80
Local nMes		:= 0
Local nAno 		:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis privadas para SetPrint.								³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cString		:= 'RCQ'
Private aReturn		:= { STR0007, 1, STR0008, 2, 2, 1, '', 1 }
Private NomeProg	:= 'GPER660'
Private nLastKey	:= 0
Private cPerg		:= 'GPER660'
Private Titulo		
Private AT_PRG		:= 'GPER660'
Private wCabec0		:= 8
Private wCabec1 ,wCabec2, wCabec3, wCabec4, wCabec5, wCabec6, wCabec7, wCabec8		
Private nTamanho	:= 'G'
PRIVATE nChar	 := 15
PRIVATE COLUNAS  := 132
Private cCabec		:= ''
Private aInfo		:= {}
Private Li			:= 0
Private cSize	:= 'G'
Private contfl		:= 1
Private aCodRpat  := {}
Private dDataRef    	:= ctod("//")
Private dDataRef1  	:= ctod("//")
Private cMesAnoIni// Mes/Ano do Processamento
Private cAnoMesIni// Ano/Mes do Processamento
Private cTpImp                                
Private cAnoMesFim
Private cMesAnoFim

dbSelectArea("RCO")
DbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega parametros de impressao.										³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte( cPerg, .F. )

wnRel := 'GPER660'
wnRel := SetPrint( cString, wnRel, cPerg, @Titulo, cDesc1, cDesc2, cDesc3, .F., , , nTamanho )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ajusta parametros para impressao.										³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par01 == "1"
	cMesAnoIni := "01"
	cMesAnoFim := "02"
ElseIf mv_par01 == "2"
	cMesAnoIni := "03"
	cMesAnoFim := "04"
ElseIf mv_par01 == "3"
	cMesAnoIni := "05"
	cMesAnoFim := "06"
ElseIf mv_par01 == "4"
	cMesAnoIni := "07"
	cMesAnoFim := "08"
ElseIf mv_par01 == "5"
	cMesAnoIni := "09"
	cMesAnoFim := "10"
ElseIf mv_par01 == "6"
	cMesAnoIni := "11"
	cMesAnoFim := "12"
Else
	Return
Endif		

cMesAnoIni := cMesAnoIni + mv_par02
cMesAnoFim := cMesAnoFim + mv_par02

cAnoMesIni  := Right(cMesAnoIni,4)+Left(cMesAnoIni,2)
cAnoMesFim := Right(cMesAnoFim,4)+Left(cMesAnoFim,2)

cTpImp   := mv_par04 

If cTpImp == 1
	Titulo		:= STR0009      		//Analitico - "CEDULA DE CUOTAS OBRERAS Y PATRONALES, APORTACIONES Y AMORTIZACIONES"
	cSize   		:= "G"
	nTamanho 	:= "G"
	Colunas  	:= 220
	aReturn[4] 	:= 2
	nChar 		:= 15
ElseIf cTpImp == 2				
	Titulo 		:= STR0010				//Sintetico - "CEDULA DE AUTODETERMINACION DE CUOTAS, APORTACIONES Y AMORTIZACIONES"
	cSize   		:= "M"
	nTamanho		:= "M"
	Colunas  	:= 132
	aReturn[4] 	:= 1
	nChar 		:= 15
Else
	Titulo := STR0011				//Resumo - "RESUMEN DE LIQUIDACION OBRERO PATRONAL"
	cSize   		:= "M"
	nTamanho		:= "M"
	Colunas  	:= 132
	aReturn[4] 	:= 1
	nChar 		:= 15
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa a impressao.													³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nLastKey = 27
	Return
Endif

SetDefault(aReturn,cString,,,cSize)
RptStatus( { |lEnd| Gp660Prn(nLin) },Titulo )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GP660Prn  ºAutor  ³Silvia Taguti       º Data ³  07/06/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao gp660Prn() - Efetua a impressao do relatorio de     º±±
±±º          ³                     Obrigacoes patronais Bimestrais        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function gp660Prn(nLin)

Local nX			:= 0
Local nCont 	:= 0
Local cCod 		:= ""
Local nA			:= 0
Local cFilant  := Replicate("!", FwGetTamFilial)
Local nTotPat 	:= 0
Local nTotEmp	:= 0
Local nTotal	:= 0
Local cMat		:= Space(6)
Local cImss
Local cCurp
Local cNome
Local cRfc 
Local nPos		:= 0
Local nDiaM    := 0
Local nPosB		:= 0
Local aArea 	:= GetArea()
Local nPosAl	:= 0

Private cMatAnt 	:= "!!"
Private cDias
Private cRFCEmp 	:= Space(11)
Private cAtivi		:= Space(24)
Private cNomePat  := Space(40)
Private cEndPat 	:= Space(40)
Private cNum		:= ""
Private cBairro   := Space(20)
Private cMunic    := Space(06)
Private cCep      := Space(05)
Private cEntid    := Space(05)
Private nDiasInc	:= 0
Private nDiasFal  := 0
Private cFatRis   := Space(12)

Private nSalDiaI 	:= 0
Private nSalIVCV	:= 0

Private nRetiroPat:= 0
Private nCVPat 	:= 0
Private nCVEmp		:= 0
Private nInfon		:= 0
Private nSegViv	:= 0

Private nTotDias 	:= 0
Private nTotDiInc	:= 0
Private nTotDiFal	:= 0
Private nTotFunc	:= 0
Private nTotGMPPat:= 0
Private nTotGMPEmp:= 0 
Private nTotIvPat	:= 0
Private nSalMinimo:= 0
Private dDataAd	:= ctod("")
Private cChave 	:= " "
Private aInfFunc	:= {}
Private aCabFunc  := {}
Private nDias		:= 0
Private dDataIni 	:= ctod("")  
Private dDataIni2	:= ctod("")  
Private dDAtaFim	:= ctod("")
Private aReingre	:= {}
Private nTotRet	:= 0
Private nTotCVPat := 0
Private nTotCVEmp := 0
Private nTotVlInf := 0
Private nTotSoma1 := 0
Private nTotApPat := 0
Private nTotCF   	:= 0
Private nTotCF1  	:= 0
Private nTotAmort	:= 0
Private nTotSoma2	:= 0
Private nTotAc		:= 0

Private nPCFixPat := 0
Private nPExCPat  := 0
Private nPExCEmp	:= 0
Private nPEDinPat := 0
Private nPGDinEmp := 0
Private nPGMPePat := 0
Private nPGMPeEmp := 0
Private nPInViPat := 0
Private nPInViEmp := 0
Private nPGPSPat	:= 0
Private nPRTPat	:= 0

Private nTotExPat	:= 0
Private nTotExEmp	:= 0
Private nTotPDPat	:= 0
Private nTotPDEmp	:= 0
Private nTotRtPat	:= 0
Private nTotIvEmp := 0
Private nTotGpsPat:= 0
Private nTotGerPat:= 0
Private nTotGerEmp:= 0
Private nTotGeral	:= 0
Private aMatInfo	:= {}
Private aPatrSCre := {}
Private nPatrSCr 	:= 0
Private nAmort	:= 0     
Private nCF		:= 0
Private cSimb		:= Space(3)
Private nDiasBim  := 0

dbSelectArea("RCO")
DbSetOrder(1)
	
For nCont := 1 To Len( mv_par03 ) Step 4
	If dbSeek(xFilial("RCO")+SubStr( mv_par03 , nCont , 4 ))
		AADD(aCodRPat,{SubStr( mv_par03 , nCont , 4 ),RCO->RCO_NREPAT,RCO_ATIVID,RCO_NOME,;
		RCO_END,RCO_NUMEXT,RCO_BAIRRO,RCO_CEP,RCO_UF,RCO_MUNIC,Alltrim(Str(RCO_FATRSC))})
	Endif
Next nCont
	
If Len(aCodRPat)== 0
	Return
Endif

For nA := 1 to Len(aCodRPat)
	cCod += "'"+Alltrim(aCodRPat[nA,2])+"',"
Next nA
cCod := Substr(cCod,1,(Len(cCod)-1) )

dDataRef  := Ctod("01/"+Left(cMesAnoFim,2)+"/"+ Right(cMesAnoFim,4))           
dDataRef1 := Ctod("01/"+Left(cMesAnoIni,2)+"/"+ Right(cMesAnoIni,4))           
nDiasBim	 := F_ULTDIA(dDataRef)+ F_ULTDIA(dDataRef1)

SRA->(dbGoTop())
If !fCarTab660() 
	cHelp := OemToAnsi( STR0049 )
	Help( ' ' , 1 , "INCONSIST" , , cHelp , 1 , 0 )
	Return
Endif

dbSelectArea( 'RCQ' )

cAliasRCQ := "RCQ"	   

For nx := 1 to Len(aCodRPat)
	aInfFunc		:= {}
	aCabFunc  	:= {}
	aPatrSCre   := {}
	cCodRPat 	:= aCodRPat[nx,1]  	//Codigo Patronal
	cRegPat 	:= aCodRPat[nx,2]	   //Numero Patronal 
	cAtivi  	:= aCodRpat[nx,3]		//Atividade	
	cNomePat := aCodRpat[nx,4]  	//Razao Social
	cEndPat	:= aCodRpat[nx,5]  	//Endereco
	cNum		:= aCodRpat[nx,6]		//Numero
	cBairro  := aCodRpat[nx,7] 	//Bairro
	cMunic  := aCodRpat[nx,10] 	//Munic 
	aArea := GetArea()
	nPosB		:= FPOSTAB("S016",cMunic,"=",4) 
	If nPosB > 0
		cMunic := If(FTABELA("S016",nPosB,5)<> Nil,FTABELA("S016",nPosB,5),cMunic)
	Endif
	RestArea(aArea)
	cCep    := aCodRpat[nx,8] 		//Cep 
	cEntid  := aCodRpat[nx,9] 		//Entidad Federativa
	cFatRis := aCodRpat[nx,11] 	//Fator de Risco      

	dbSetOrder( 3 )                 
	DbSeek(cRegPat,.T.)
	
	ProcRegua((cAliasRCQ)->(RecCount()))

	While (cAliasRCQ)->(!EOF()) .And. 	(cAliasRCQ)->RCQ_NREPAT == cRegPat
		If (cAliasRCQ)->RCQ_ANOMES < cAnoMesIni .Or. (cAliasRCQ)->RCQ_ANOMES > cAnoMesFim .Or.;
		    (cAliasRCQ)->RCQ_TPMOV $ "15|16|17|18|19|20"
		    If (cAliasRCQ)->RCQ_VALINF > 0 .and. (cAliasRCQ)->RCQ_TPMOV == "18"
		    	nPosInf := aScan( aInfFunc, { |x| x[01] == (cAliasRCQ)->RCQ_MAT .and. Ctod(x[03]) < (cAliasRCQ)->RCQ_DTMOV } )
		    	If ( nPosInf > 0 )
		    		aInfFunc[ nPosInf, 15 ] := (cAliasRCQ)->RCQ_VALINF
		    	EndIf
		    ElseIf ( (cAliasRCQ)->RCQ_VALINF > 0 .and. (cAliasRCQ)->RCQ_TPMOV == "15" )
		    	If ( ( nPosInf := aScan( aInfFunc, { |x| x[01] == (cAliasRCQ)->RCQ_MAT .and. Ctod(x[03]) < (cAliasRCQ)->RCQ_DTMOV .and. x[15] == 0 } ) ) > 0 )
		    		aInfFunc[ nPosInf, 15 ] := (cAliasRCQ)->RCQ_VALINF					
		    	EndIf
		    EndIf
			(cAliasRCQ)->(dbSkip())
			Loop
		Endif
		If cAnoMesFim <> cAnoMesIni .And. (cAliasRCQ)->RCQ_ANOMES == cAnoMesIni .And.;
			(cAliasRCQ)->RCQ_TPMOV == "01" 
			nSalDiaI := (cAliasRCQ)->RCQ_SALDII
	  		nSalIVCV := (cAliasRCQ)->RCQ_SALIVC
	  		nMatr := (cAliasRCQ)->RCQ_MAT
	  		
			(cAliasRCQ)->(dbSkip())

			If (cAliasRCQ)->RCQ_MAT == nMatr .And. (cAliasRCQ)->RCQ_TPMOV <> "01"
				(cAliasRCQ)->( dbSkip( -1 ) )
			Else	
				Loop
			Endif	
		Endif
        
		If cAnoMesFim <> cAnoMesIni .And. (cAliasRCQ)->RCQ_TPMOV == "01"  .And.;
			((cAliasRCQ)->RCQ_ANOMES == cAnoMesIni .OR. ((cAliasRCQ)->RCQ_ANOMES == cAnoMesFim .And.;
			(cAliasRCQ)->RCQ_ANOMES == AnoMes((cAliasRCQ)->RCQ_DTMOV)))
			nSalDiaI := (cAliasRCQ)->RCQ_SALDII
	  		nSalIVCV := (cAliasRCQ)->RCQ_SALIVC
	  		nMatr := (cAliasRCQ)->RCQ_MAT
	  		
			(cAliasRCQ)->(dbSkip())

			If ((cAliasRCQ)->RCQ_ANOMES > cAnoMesFim .And. (cAliasRCQ)->RCQ_MAT == nMatr .And. (cAliasRCQ)->RCQ_TPMOV == "01") .or.;
			   ((cAliasRCQ)->RCQ_MAT == nMatr .And.(cAliasRCQ)->RCQ_TPMOV <> "01" ) .Or. ;
			   ((cAliasRCQ)->RCQ_MAT <> nMatr .And.(cAliasRCQ)->RCQ_TPMOV == "01" ) .Or. (cAliasRCQ)->(EOF())
			   (cAliasRCQ)->( dbSkip( -1 ) )
			Else	
				Loop
			Endif	
		Endif
      If (cAliasRCQ)->RCQ_TPMOV == "06"
  			If Ascan(aReingre,{|x| x[1] == (cAliasRCQ)->RCQ_MAT } ) == 0 
	      	AADD(aReingre,{(cAliasRCQ)->RCQ_MAT,(cAliasRCQ)->RCQ_DTMOV })
	      Endif	
      Endif
		IF (cAliasRCQ)->RCQ_FILIAL # cFilAnt
			cFilAnt := (cAliasRCQ)->RCQ_FILIAL
			fInfo(@aInfo,cFilAnt)
			cRFCEmp := aInfo[08]
      Endif
		fCabec660(nLin)

		If (cAliasRCQ)->RCQ_MAT <> cMatAnt
			dbSelectArea("SRA")
			dbSetOrder(1)
			If dbSeek((cAliasRCQ)->(RCQ_FILIAL+RCQ_MAT))
				cMat  := SRA->RA_MAT
         	cImss := SRA->RA_RG
	         cCurp := SRA->RA_CURP
   	      cNome := Alltrim(SRA->RA_PRISOBR)+" "+Alltrim(SRA->RA_SECSOBR)+" "+Alltrim(SRA->RA_PRINOME)+;
							" "+Alltrim(SRA->RA_SECNOME)   	      			
      	   cRfc	:= SRA->RA_CIC
		      nTotFunc ++
		      AADD(aCabFunc,{cImss,cNome,cMat,cRFC,cCurp } )   
				cImss:= cNome:= cMat:= cRfc:= cCurp := " "
	      Endif
	  	Endif    
	  	fBuscaInfo()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Tipos de Movimentos - Admissao³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nDias   	:= F_ULTDIA(dDataRef)+ F_ULTDIA(dDataRef1)
     	//nDiaM := If((cAliasRCQ)->RCQ_TPMOV <> "05",1,0)
		If ( (cAliasRCQ)->RCQ_TPMOV <> "05" ) .and.;
			 ( (cAliasRCQ)->RCQ_TPMOV <> "15" ) .and.;
			 ( (cAliasRCQ)->RCQ_TPMOV <> "17" )
			nDiaM := 1
		Else 
			nDiaM := 0
		EndIf
		dDataIni := Ctod("01/"+Left(cMesAnoIni,2)+"/"+Right(cMesAnoIni,4))
		dDataIni2:= Ctod("01/"+Left(cMesAnoFim,2)+"/"+Right(cMesAnoFim,4))
		dDataFim := Ctod(StrZero(F_ULTDIA(dDataRef))+"/"+Left(cMesAnoFim,2)+"/"+Right(cMesAnoFim,4))

     	If !((cAliasRCQ)->RCQ_TPMOV $ "02|04")
			nSalDiaI := If((cAliasRCQ)->RCQ_TPMOV=="01" .And. nSaldiaI > 0,nSalDiaI,(cAliasRCQ)->RCQ_SALDII)
	  		nSalIVCV := If((cAliasRCQ)->RCQ_TPMOV=="01" .And. nSalIVCV > 0,nSalIVCV,(cAliasRCQ)->RCQ_SALIVC)
	   Endif
      
      If !((cAliasRCQ)->RCQ_TPMOV $ "11|12")
      	If AnoMes((cAliasRCQ)->RCQ_DTMOV) == cAnoMesIni .or. AnoMes((cAliasRCQ)->RCQ_DTMOV) == cAnoMesFim
				If (cAliasRCQ)->RCQ_TPMOV $ "01|03"
					cChave := "A"
					nDias := (dDataFim - (cAliasRCQ)->RCQ_DTMOV)+ nDiaM
				ElseIf (cAliasRCQ)->RCQ_TPMOV $ "02|04"
					cChave := "B" 
				ElseIf (cAliasRCQ)->RCQ_TPMOV == "05"
					cChave := "M/S"
				ElseIf (cAliasRCQ)->RCQ_TPMOV == "06"			
					cChave := "R"                  	
				Endif	
			Endif	
      ElseIf (cAliasRCQ)->RCQ_TPMOV == "12"
			nDiasInc := (cAliasRCQ)->RCQ_NDIF
		ElseIf (cAliasRCQ)->RCQ_TPMOV == "11"	
			nDiasFal := (cAliasRCQ)->RCQ_NDIF
		Endif		
		dDataAd := (cAliasRCQ)->RCQ_DTMOV
		cDias := StrZero(nDias,2)

		If (cAliasRCQ)->RCQ_MAT == cMatAnt 
			If Len(aInfFunc) > 0 
				nPos := Len(aInfFunc)		
				If !((cAliasRCQ)->RCQ_TPMOV $ "11|12")
					If aInfFunc[nPos,17] $ "11|12"
						aInfFunc[nPos,2] := cChave
						aInfFunc[nPos,3] := If(dDataAd <> ctod("//"),dtoc(dDataAd)," ")
		         Else                                                            
	 	         	If cAnoMesFim <> cAnoMesIni .And. (cAliasRCQ)->RCQ_ANOMES == cAnoMesFim .And. (cAliasRCQ)->RCQ_TPMOV == "01"
  							nPosAl := Ascan(aInfFunc,{|x| x[1] == (cAliasRCQ)->RCQ_MAT  .And. x[17] == "01" } ) 
							If nPosAl > 0 
								If nPosAl+1 <= Len(aInfFunc) .And. 	aInfFunc[nPosAl,1] == aInfFunc[nPosAl+1,1] .And. AnoMes(ctod(aInfFunc[nPosAl+1,3])) < cAnoMesIni
									aInfFunc[nPosAl,5] := (cAliasRCQ)->RCQ_SALDII
							  		aInfFunc[nPosAl,33]:= (cAliasRCQ)->RCQ_SALIVC
							  	Endif	
							cMatAnt := (cAliasRCQ)->RCQ_MAT	
							nDiasInc:= nDiasFal := 0
							cDias   := ""
							cChave  := " "
							dDataAd := ctod("")
	
							(cAliasRCQ)->(dbSkip()) 
							Loop
		         	Endif	 
			     		EndIf
		         	If cAnoMesFim <> cAnoMesIni .And. (cAliasRCQ)->RCQ_TPMOV <> "01" .And.;
		         		AnoMes((cAliasRCQ)->RCQ_DTMOV) == cAnoMesIni .or. AnoMes((cAliasRCQ)->RCQ_DTMOV) == cAnoMesFim 
              			If AnoMes(Ctod(aInfFunc[nPos,3])) == cAnoMesIni .or. AnoMes(Ctod(aInfFunc[nPos,3])) == cAnoMesFim
              					If ( (cAliasRCQ)->RCQ_TPMOV == "17" )
									nDias := (cAliasRCQ)->RCQ_DTMOV - Ctod(aInfFunc[nPos,3]) - 1
              					Else
              						nDias := (cAliasRCQ)->RCQ_DTMOV - Ctod(aInfFunc[nPos,3]) - Val(aInfFunc[nPos,7]) + nDiaM
              					EndIf
 							Else	
								If (cAliasRCQ)->RCQ_TPMOV <> "06"
									nDias := ((cAliasRCQ)->RCQ_DTMOV - dDataIni) - Val(aInfFunc[nPos,7])+ nDiaM								             			    
								Else
									nDias := ((cAliasRCQ)->RCQ_DTMOV - dDataIni2) - Val(aInfFunc[nPos,7])+ nDiaM								             			    
								Endif	
                     Endif

							aInfFunc[nPos,4] :=	StrZero(nDias,2)					
							If	( (cAliasRCQ)->RCQ_TPMOV $ "17" ) .or. ;
								( (cAliasRCQ)->RCQ_TPMOV $ "06" .and. cChave == "R")
								nDias := (dDataFim - (cAliasRCQ)->RCQ_DTMOV) + 1
							Else 
								nDias := (dDataFim - (cAliasRCQ)->RCQ_DTMOV)
							EndIf			
							
						Endif	
						If (cAliasRCQ)->RCQ_TPMOV $ "05/15"
							nDias++
						Endif	
						cDias := StrZero(If((cAliasRCQ)->RCQ_TPMOV $ "02|04", 0, nDias),2)				      
				      fGravFun()
		         Endif
      		Else 
					If !(aInfFunc[nPos,17] $ "11|12")
						If (cAliasRCQ)->RCQ_TPMOV == "12"
					  		aInfFunc[nPos,6]:= Str(Val(aInfFunc[nPos,6])+ nDiasInc,3)
							nDias := Val(aInfFunc[nPos,4]) - nDiasInc
							aInfFunc[nPos,4] :=	StrZero(nDias,2)
					  	Else	
							aInfFunc[nPos,7]:= Str(Val(aInfFunc[nPos,7])+ nDiasFal,3)
							nDias := Val(aInfFunc[nPos,4]) - nDiasFal
							aInfFunc[nPos,4] :=	StrZero(nDias,2)					
						Endif	
					Else
				      fGravFun()
		         Endif
		   	Endif      
			Endif	
		Else
	      fGravFun()
	   Endif   

      cMatAnt := (cAliasRCQ)->RCQ_MAT	
		nDiasInc:= nDiasFal := 0
		cDias   := ""
		cChave  := " "
		dDataAd := ctod("")

		(cAliasRCQ)->(dbSkip())
	Enddo
	fCalcEmp(@nTotPat,@nTotEmp,@nTotal)
	If Len(aInfFunc) > 0
		If cTpImp == 1    
			fImprAna()
			fTotPatAna()
		ElseIf cTpImp == 2
			fImprSin()
			fTotPatSin()
		Else
			fTotPatRes()
		Endif		
	Endif	
	nPatrSCr := 0
	dbSelectARea(cAliasRCQ)
	dbCloseArea()
	dbSelectArea("RCQ")
Next 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza impressao.														³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Impr( '', 'F' )
Set Device To Screen

If aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	OurSpool( WnRel )
Endif
MS_Flush()
Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fBuscaInfoºAutor  ³Microsiga           º Data ³  11/03/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Busca Tipo de Infonavit e Valor de Infonavit               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fBuscaInfo()	

If cAnoMesFim <> cAnoMesIni .And. (cAliasRCQ)->RCQ_ANOMES == cAnoMesFim 
   If !Empty(RCQ->RCQ_TIPINF) 
		If Ascan(aMatInfo,{|x| x[1] == (cAliasRCQ)->RCQ_MAT  } ) == 0
			AADD(aMatInfo,{(cAliasRCQ)->RCQ_MAT,(cAliasRCQ)->RCQ_TIPINF, (cAliasRCQ)->RCQ_VALINF})
		Endif
	Endif
//Igor
ElseIf ( cAnoMesFim <> cAnoMesIni .and. (cAliasRCQ)->RCQ_TPMOV $ "02/04" .and.;
			(cAliasRCQ)->RCQ_DTMOV >= dDataRef1 .and. (cAliasRCQ)->RCQ_DTMOV < dDataRef )
   If !Empty(RCQ->RCQ_TIPINF)
		If Ascan(aMatInfo,{|x| x[1] == (cAliasRCQ)->RCQ_MAT  } ) == 0
			AADD(aMatInfo,{(cAliasRCQ)->RCQ_MAT,(cAliasRCQ)->RCQ_TIPINF, (cAliasRCQ)->RCQ_VALINF } )
		Endif                                                                         
	Endif
Endif			

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCarTab660ºAutor  ³Microsiga           º Data ³  05/11/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega Tabelas de Cotas de Obreros e Empresa               º±±
±±º          ³Carrega Valor do Salario Minimo                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fCarTab660()

Local lRet := .T.        
Local n    := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tabela de Percentuais de Obreros/Patronais	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Retiro           					
nRetiroPat := If( FTABELA("S007",1,14,dDataRef) <> Nil, FTABELA("S007",1,14,dDataRef),;
					FTABELA("S007",1,14 ))           
//CV.Patronal
nCVPat 		:= If( FTABELA("S007",1,16,dDataRef) <> Nil, FTABELA("S007",1,16,dDataRef),;
					FTABELA("S007",1,16 ))           
//CV.Empregado
nCVEmp 		:= If( FTABELA("S007",1,17,dDataRef) <> Nil, FTABELA("S007",1,17,dDataRef),;
					FTABELA("S007",1,17 ))           
//Infona       
nInfon 		:= If( FTABELA("S007",1,15,dDataRef) <> Nil, FTABELA("S007",1,15,dDataRef),;
					FTABELA("S007",1,15 ))           
//Seguro Vivienda					
nSegViv		:= If( FTABELA("S007",1,18,dDataRef) <> Nil, FTABELA("S007",1,18,dDataRef),;
					FTABELA("S007",1,18 ))           

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tabela de Salario MInimo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

n:= FPOSTAB("S006","A","=",4)
If n > 0
	nSalMinimo := If( FTABELA("S006",n,5,dDataRef) <> Nil, FTABELA("S006",n,5,dDataRef),;
						FTABELA("S006",n,5))
Endif	

//Caso as variaveis estejam com valor null, zera as mesmas
If ( nRetiroPat == NIL )
	nRetiroPat := 0
EndIf

If ( nCVPat == NIL )
	nCVPat := 0
EndIf

If ( nCVEmp == NIL )
	nCVEmp := 0
EndIf

If ( nInfon == NIL )
	nInfon := 0
EndIf

If ( nSegViv == NIL )
	nSegViv := 0
EndIf

If ( nSalMinimo == NIL )
	nSalMinimo := 0
EndIf
If cTpImp == 3

	//Cota Fixa Empresa 
	nPCFixPat	:= If( FTABELA("S007",1,4,dDataRef) <> Nil,FTABELA("S007",1,4,dDataRef),;
	                FTABELA("S007",1,4 ))	
	//Excedente Empresa 	
	nPExCPat  	:= If( FTABELA("S007",1,5,dDataRef) <> Nil,FTABELA("S007",1,5,dDataRef),;
						FTABELA("S007",1,5 ))    
	//Excedente Empregado
	nPExCEmp		:= If( FTABELA("S007",1,6,dDataRef) <> Nil,FTABELA("S007",1,6,dDataRef),;
						FTABELA("S007",1,6 ) )    
	//Emprestimo em Dinheiro Empresa 					 
	nPEDinPat	:= If( FTABELA("S007",1,7,dDataRef) <> Nil,FTABELA("S007",1,7,dDataRef),;
						FTABELA("S007",1,7 ))   
	//Emprestimo em Dinheiro Empregado					  
	nPGDinEmp	:= If( FTABELA("S007",1,8,dDataRef) <> Nil,FTABELA("S007",1,8,dDataRef),;     
						FTABELA("S007",1,8 ))                                                 
	//Gastos Medicos Pensionados Empresa 					
	nPGMPePat	:= If( FTABELA("S007",1,9,dDataRef) <> Nil,FTABELA("S007",1,9,dDataRef),;
						FTABELA("S007",1,9 ))            
	//Gastos Medicos Pensionados Empregado 
	nPGMPeEmp	:= If( FTABELA("S007",1,10,dDataRef) <> Nil,FTABELA("S007",1,10,dDataRef),;
						FTABELA("S007",1,10 ))           
	//Invalidez e Vida Empresa					 
	nPInViPat	:= If( FTABELA("S007",1,11,dDataRef) <> Nil,FTABELA("S007",1,11,dDataRef),;
						FTABELA("S007",1,11 ))           
	//Invalidez e Vida Empregado					 
	nPInViEmp	:= If( FTABELA("S007",1,12,dDataRef) <> Nil, FTABELA("S007",1,12,dDataRef),;
						FTABELA("S007",1,12 ))           
	//Guarderia Empresa					
	nPGPSPat		:= If( FTABELA("S007",1,13,dDataRef) <> Nil, FTABELA("S007",1,13,dDataRef),;
						FTABELA("S007",1,13 ))           
	//Caso as variaveis estejam com valor null, zera as mesmas
	If ( nPCFixPat == NIL )
		nPCFixPat := 0
	EndIf
	
	If ( nPExCPat == NIL )
		nPExCPat := 0
	EndIf
	
	If ( nPExCEmp == NIL )
		nPExCEmp := 0
	EndIf
	
	If ( nPEDinPat == NIL )
		nPEDinPat := 0
	EndIf
	
	If ( nPGDinEmp == NIL)
		nPGDinEmp := 0
	EndIf
	
	If ( nPGMPePat == NIL )
		nPGMPePat := 0
	EndIf

	If ( nPGMPeEmp == NIL )
		nPGMPeEmp := 0
	EndIf

	If ( nPInViPat == NIL )
		nPInViPat := 0
	EndIf

	If ( nPInViEmp == NIL )
		nPInViEmp := 0
	EndIf
	
	If ( nPGPSPat == NIL )
		nPGPSPat := 0
	EndIf							
Endif

If nRetiroPat+nCVPat+nCVEmp+nInfon == 0
	lRet := .F.
Endif

Return lRet	

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FCABEC660 ºAutor  ³Silvia Taguti       º Data ³  09/05/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Imprime Cabecalhos - Relatorio: Analitico, Sintetico e     º±±
±±º          ³                      Resumo                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fCabec660()
              
If cTpImp == 1    
					//Registro Patronal + RFC + Atividade + Area Geografica
	wCabec1 :=	fRetTxt(STR0012+cRegPat,40) + fRetTxt(STR0013+cRFCEmp,50) + fRetTxt(STR0014+cAtivi,55)+ ;
					fRetTxt(STR0015,20)
					//Razao Social + Delegacao IMSS 
	wCabec2 := 	fRetTxt(STR0016+cNomePat ,145) + fRetTxt(STR0017,30)
					//Bimestre e Ano de Processamento##Aportacion Patronal
	wCabec3 := 	fRetTxt(STR0019+Substr(cMesAnoFim,1,2)+" / "+Substr(cMesAnoFim,3,4),145)+;
					STR0039+Space(2)+	Transform(nInfon,"@E 999.99")+"%"
	wCabec4 := 	__PrtFatLine()
					//Nro Seguridade Social + Nome + Matricula + Cred.Vivienda+ RFC + CURP
	wCabec5 := 	fRetTxt(STR0021,36) + fRetTxt(STR0022,60) + fRetTxt(STR0023,40)+fRetTxt(STR0018,34)+;
					fRetTxt(STR0024,35)+ fRetTxt(STR0025,20)
					//Movimientos  Cuotas IMSS               Aportaciones y Amortizaciones INFONAVIT
 	wCabec6 := 	Space(20) + STR0020 + Space(30)+STR0046 + Space(66)+STR0031 
					//Chave+Data+Dias+SDI+Inc.Aus+Retiro+C.Y.V.+Suma+Aporte Pat.+% o CF+ Amortizacion+Suma
	wCabec7 := 	fRetTxt(STR0026,7) + fRetTxt(STR0027,08) + fRetTxt(STR0028,12) + fRetTxt(STR0029,10)+;
					fRetTxt(STR0030,15) + fRetTxt(STR0032,26) + fRetTxt(STR0033,25)+ ;
					fRetTxt(STR0034,16) + fRetTxt(STR0035,18) + fRetTxt(STR0036,19) + ;
					fRetTxt(STR0038,21) + fRetTxt(STR0034,17)
					//Pat+Obr
	wCabec8 := 	Space(70) + fRetTxt(STR0040,15) + fRetTxt(STR0041,13) 
ElseIf cTpImp == 2  //Sintetico
					//Registro Patronal + RFC + Atividade + Area Geografica
	wCabec1 :=	fRetTxt(STR0012+cRegPat,40) + fRetTxt(STR0013+cRFCEmp,32) + fRetTxt(STR0014+cAtivi,38)+ ;
					fRetTxt(STR0015,20)
					//Razao Social + Delegacao IMSS 
	wCabec2 := 	fRetTxt(STR0016+cNomePat ,72) + fRetTxt(STR0017,30)
					//Endereco+Num+Bairro+Municipio
	wCabec3 :=  fRetTxt(STR0042+cEndPat+cNum,52)+ ","+Space(1)+fRetTxt(cBairro,18)+;
					fRetTxt(STR0043+cMunic,35)
					//Codigo Postal + Entidade+ Aportacion Patronal + Bimestre Ano Processamento
	wCabec4 :=  fRetTxt(STR0044+cCep,25)+fRetTxt(STR0045+cEntid,25)+STR0039+Space(2)+;
					Transform(nInfon,"@E 999.99")+"%"+"   "+;
					fRetTxt(STR0019+Substr(cMesAnoFim,1,2)	+ " / "+ Substr(cMesAnoFim,3,4),40)
	wCabec5 :=  __PrtFatLine()
					//Nro Seguridade Social + Nome + Matricula + Cred.Vivienda+ RFC + CURP
	wCabec6 := 	fRetTxt(STR0021,36) + fRetTxt(STR0022,38) + fRetTxt(STR0023,13)+fRetTxt(STR0018,15)+;
					fRetTxt(STR0024,15)+ fRetTxt(STR0025,20)
					//Movimientos  Cuotas IMSS               Aportaciones y Amortizaciones INFONAVIT
 	wCabec7 := 	Space(20) + STR0020 + Space(15)+STR0046 + Space(30)+STR0031 
					//Chave+Data+Dias+SDI+Inc.Aus+Retiro+C.Y.V.+Suma+Aporte Pat.+% o CF+ Amortizacion+Suma
	wCabec8 := 	fRetTxt(STR0026,7) + fRetTxt(STR0027,08) + fRetTxt(STR0028,08) + fRetTxt(STR0029,09)+;
					fRetTxt(STR0030,13) + fRetTxt(STR0032,14) + fRetTxt(STR0033,14)+ ;
					fRetTxt(STR0034,11) + fRetTxt(STR0035,13) + fRetTxt(STR0036,12) + ;
					fRetTxt(STR0038,18) + fRetTxt(STR0034,15)
Else           //Resumo
					//Mes e Ano do Processamento + Data de Emissao
	wCabec1 :=  Space(30) + STR0019 + Substr(cMesAnoFim,1,2)+ " / "+Substr(cMesAnoFim,3,4) + Space(15)+STR0027 +": "+dtoc(dDataBase)
					//Registro Patronal + RFC
	wCabec2 :=  Space(05)+fRetTxt(STR0012+cRegPat,40) + fRetTxt(STR0013+cRFCEmp,20)
					//Razao Social 
	wCabec3 :=  Space(05)+fRetTxt(STR0016 + cNomePat,63)
	wCabec4 :=  __PrtFatLine()
     				//Descricao Importe
   wCabec5 :=  Space(20)+STR0047+Space(50)+STR0048 
Endif	
				       					 	
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCalcEmp  ºAutor  ³Silvia Taguti       º Data ³  10/05/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Calcula Cuotas por Funcionario                              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                           	                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fCalcEmp(nTotPat,nTotEmp,nTotal)

Local nx 		:= 0
Local nRetPat 	:= 0
Local	nVlCVEmp	:= 0
Local	nVlInf 	:= 0
Local	nSoma1 	:= 0
Local	nApPatr 	:= 0
Local	nSoma2 	:= 0              
Local nCF1		:= 0
Local nCFPat	:= 0
Local nExPat 	:= 0
Local nExEmp	:= 0
Local nPDPat	:= 0
Local nPDEmp	:= 0
Local nGMPPat	:= 0
Local nGMPEmp	:= 0
Local nRTPat	:= 0
Local nIVPat	:= 0
Local nIVEmp	:= 0
Local nGPSPat	:= 0
Local nDias1 := 0
Local nDias2 := 0
Local nDias3 := 0
Local nDias4 := 0
Local n		 := 0
Local nPos1 := 0
Local nPosn := 0
Local lSegV		:= .T.		//Atualiza seguro de vida
Local nPosBj	:= 0		//Registro de baixa
Local nPosMat	:= 0
Local nValAux	:= 0

Local aFunSeg	:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando existe uma baixa, e essa esta fora do periodo do bimestre,  ³
//³exemplo: 														  ³
//³01/01/2009 - Alta (Credito de Infonavit)							  ³
//³20/01/2009 - Baixa de Funcionário 								  ³
//³Neste caso as datas estão fora do range de um bimestre, e deve-se  ³
//³efetuar o cálculo do INFONAVIT para os dias trabalhados			  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Risco de Trabalho
nPRTPat		:= Val(cFatRis)

For n:= 1 to Len(aReingre) 
	For nx:= Len(aInfFunc) to 1 Step -1
		If aReingre[n,1] == aInfFunc[nx,1]
			If aReingre[n,1] == aInfFunc[nx,1] .And.	(AnoMes(Ctod(aInfFunc[nx,3])) == cAnoMesIni .or. AnoMes(Ctod(aInfFunc[nx,3])) == cAnoMesFim) .or.;
				(nx+1) <= Len(aInfFunc) .And. aInfFunc[nx+1,17] <> "06" .And.(aReingre[n,1] == aInfFunc[nx+1,1] .And.;
				(AnoMes(Ctod(aInfFunc[nx+1,3])) == cAnoMesIni .or. AnoMes(Ctod(aInfFunc[nx+1,3])) == cAnoMesFim))
				Loop
			Endif
			aInfFunc[nx,35] := "D"
		Endif
	Next nx
Next n	 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula os valores de Infonavit e soma o valor do seguro de vivienda  no Valor da Amortizacao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For n:= 1 to Len(aMatInfo)
	nPos1 := Ascan(aInfFunc, {|x| x[1] == aMatInfo[n,1] .And. x[35] <> "D" .And. x[4] <> "00"})
	lSegV := .T. 
	If nPos1 > 0
		nPosBj := Ascan(aInfFunc, {|x| x[1] == aMatInfo[n,1] .And. x[02] == "B" .and. Ctod(x[3]) >=dDataRef1 .and. Ctod(x[3]) < dDataRef })
		
		If ( nPosBj > 0 )
			nAmort  := aInfFunc[nPosBj,15]		
		Else
			nAmort  := aInfFunc[nPos1,15]
		EndIf
		cTipInf := aMatInfo[n,2]
		fRetInfo(Val(aInfFunc[nPos1,4]), aInfFunc[nPos1,5])
		// Suspensao do Credito Infonavit - o calculo de dias esta no registro do tipo 01 //
		If aInfFunc[nPos1,17] <> "16"
			aInfFunc[nPos1,14] := nCf
			aInfFunc[nPos1,15] := nAmort
			aInfFunc[nPos1,34] := cSimb
		Else
			aInfFunc[nPos1,14] := 0
			aInfFunc[nPos1,15] := 0
			aInfFunc[nPos1,34] := cSimb
		EndIf
		If Val(aInfFunc[nPos1,4]) > 0
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³O desconto so pode ser realizado uma vez por bimestre - 23/07/2009 ³ Igor
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
			If nAmort > 0 .and. lSegV
				lSegV	:= .F.
				aInfFunc[nPos1,14] += nSegViv
			EndIf
		Endif
	For nx := nPos1+1 to Len(aInfFunc)
			    	
			If aInfFunc[nx,1] <> aInfFunc[nPos1,1]	
				Exit
			Endif
			nPosBj := Ascan(aInfFunc, {|x| x[1] == aMatInfo[n,1] .And. x[02] == "B" .and. Ctod(x[3]) >=dDataRef1 .and. Ctod(x[3]) < dDataRef })
			
			If ( nPosBj > 0 )
				nAmort  := aInfFunc[nPosBj,15]		
			Else
				nAmort  := aInfFunc[nX,15]
			EndIf
		fRetInfo(Val(aInfFunc[nx,4]), aInfFunc[nx,5])
			// Suspensao do Credito Infonavit - o calculo de dias esta no registro do tipo 01 //
			If aInfFunc[nX,17] <> "16"
				aInfFunc[nx,14] := nCf
				aInfFunc[nx,15] := nAmort
				aInfFunc[nx,34] := cSimb
			Else
				aInfFunc[nx,14] := 0
				aInfFunc[nx,15] := 0
				aInfFunc[nx,34] := cSimb
			EndIf
			
			If Val(aInfFunc[nX,4]) > 0
				If nAmort > 0 .and. lSegV           
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³O desconto so pode ser realizado uma vez por bimestre - 23/07/2009 ³ Igor
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					lSegV	:= .F.
					aInfFunc[nX,14] += nSegViv
				EndIf
			Endif
	Next nx
	EndIf
Next n
				
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Dias1 - Dias Natuais - Dias Incapacidades - Faltas        			 ³
//³Dias2 - Dias Naturais - Dias de Incapacidades         		       ³
//³Dias3 - Dias Naturais                                       		 ³
//³Dias4 - Dias Naturais - Faltas                              		 ³
//³Faltas sao limitadas a 8 dias                               		 ³
//³obs. As faltas ja foram descontadas dos Dias Atuais (aInffunc[nx,4]³
//³por isso foram somadas novamente nos nDias2 e nDias3    			    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

For nx:= 1 to Len(aInfFunc)     
	If aInfFunc[nx,35] == "D"
		Loop
	Endif	
	If AnoMes(Ctod(aInfFunc[nx,3])) <> cAnoMesIni .And. AnoMes(Ctod(aInfFunc[nx,3])) <> cAnoMesFim
		aInfFunc[nx,3] := " "
	Endif	
		
	//nDias1 := Val(aInfFunc[nx,4]) - Val(aInfFunc[nx,6]) 
	nDias1 := Val(aInfFunc[nx,4])
	//nDias2 := Val(aInfFunc[nx,4]) - Val(aInfFunc[nx,6])+ If(Val(aInfFunc[nx,7])>8,Val(aInfFunc[nx,7])-8,Val(aInfFunc[nx,7]))
	nDias2 := Val(aInfFunc[nx,4]) + If(Val(aInfFunc[nx,7])>8,Val(aInfFunc[nx,7])-8,Val(aInfFunc[nx,7]))	
	nDias3 := Val(aInfFunc[nx,4]) + Val(aInfFunc[nx,7])
   	nDias4 := Val(aInfFunc[nx,4])	

   	nPosBj := 0
   	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Em caso de existencia de afastamento por incapacidade, nao se deve ³
	// descontar os dias de incapacidade da Aportacion - 23/07/2009 Igor  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If ( Val(aInfFunc[ nX, 06 ]) > 0 )
		
		//Muda a forma de calculo, se tem uma baixa
		nPosBj := aScan( aInfFunc, { |x| x[1] == aInfFunc[nx,01] .and. AllTrim(x[2]) == "B" .and. x[3] > aInfFunc[nx,03] } )		
		
		If ( nPosBj > 0 )
			nVlInf	:= ((aInfFunc[nx,33] * nDias4) * nInfon) / 100    //Infonavit	
			nRetPat := ((aInfFunc[nx,5] * nDias4) * nRetiroPat) / 100
			nVlCVPat	:= ((aInfFunc[nx,33] * (nDias1-Val(aInfFunc[nX,06]))) * nCVPat) / 100      //Cesantia y Vejez - nao desconta incapacidades e faltas
			nVlCVEmp	:= ((aInfFunc[nx,33] * (nDias1-Val(aInfFunc[nX,06]))) * nCVEmp) / 100      //Cesantia y Vejez - nao desconta incapacidades e faltas
			If ( nVlInf > 0 )
				If ( AllTrim(aInfFunc[ nX, 34 ]) == "%" )
					nValAux := ( ((nVlInf * aInfFunc[ nPosBj, 15 ]) * Val(aInfFunc[ nX, 06 ])) / 100 )
				ElseIf ( AllTrim(aInfFunc[ nX, 34 ]) == "VSM" )
					nValAux := (( (( nSalMinimo * aInfFunc[ nPosBj, 15 ] ) * 2) / nDiasBim ) * (nDias4 - Val(aInfFunc[ nX, 06 ])))
				EndIf
				
				If ( nValAux > 0 .and. ( aScan( aMatInfo, { |x| x[01] == aInfFunc[ nx, 01 ] } ) > 0 ) )
					If ( aScan( aFunSeg, { |x| x[01] == aInfFunc[ nx, 01 ] } ) == 0 )
						aInfFunc[ nx, 14 ] := nValAux + nSegViv
						Aadd( aFunSeg, { aInfFunc[ nx, 01 ] } ) 
					Else
						aInfFunc[ nx, 14 ] := nValAux					
					EndIf
				ElseIf ( aScan( aMatInfo, { |x| x[01] == aInfFunc[ nx, 01 ] } ) > 0 )
					aInfFunc[ nx, 14 ] := nValAux
				EndIf                  
				
				aInfFunc[ nx, 15 ] := aInfFunc[ nPosBj, 15 ]
				aInfFunc[ nx, 34 ] := aInfFunc[ nPosBj, 34 ]								
			EndIf			
		Else
			nVlInf	:= ((aInfFunc[nx,33] * (nDias4+Val(aInfFunc[ nX, 06 ]))) * nInfon) / 100    //Infonavit	
			//Retiro - para Retiro NAO se desconta os dias de incapacidades - Igor (24/07/2009)
			nRetPat := ((aInfFunc[nx,5] * (nDias4+Val(aInfFunc[ nX, 06 ]))) * nRetiroPat) / 100
			nVlCVPat	:= ((aInfFunc[nx,33] * nDias1) * nCVPat) / 100      //Cesantia y Vejez - nao desconta incapacidades e faltas
			nVlCVEmp	:= ((aInfFunc[nx,33] * nDias1) * nCVEmp) / 100      //Cesantia y Vejez - nao desconta incapacidades e faltas
		Endif
		
	Else
		
		//Muda a forma de calculo, se tem uma baixa
		nPosBj := aScan( aInfFunc, { |x| x[1] == aInfFunc[nx,01] .and. AllTrim(x[2]) == "B" .and. x[3] >= aInfFunc[nx,03] } )		
		
		If ( nPosBj > 0 )
			nVlInf	:= ((aInfFunc[nx,33] * nDias4) * nInfon) / 100    //Infonavit	
			nRetPat := ((aInfFunc[nx,5] * nDias4) * nRetiroPat) / 100
			nVlCVPat	:= ((aInfFunc[nx,33] * (nDias1-Val(aInfFunc[nX,06]))) * nCVPat) / 100      //Cesantia y Vejez - nao desconta incapacidades e faltas
			nVlCVEmp	:= ((aInfFunc[nx,33] * (nDias1-Val(aInfFunc[nX,06]))) * nCVEmp) / 100      //Cesantia y Vejez - nao desconta incapacidades e faltas
			If ( nVlInf > 0 )
				If ( AllTrim(aInfFunc[ nX, 34 ]) == "%" )
					//nValAux := ( ((nVlInf * aInfFunc[ nPosBj, 15 ]) * Val(aInfFunc[ nX, 06 ])) / 100 )
					nValAux := ( ((nVlInf * aInfFunc[ nPosBj, 15 ])) / 100 )
				ElseIf ( AllTrim(aInfFunc[ nX, 34 ]) == "VSM" )
					//nValAux := (( (( nSalMinimo * aInfFunc[ nPosBj, 15 ] ) * 2) / nDiasBim ) * (nDias4 - Val(aInfFunc[ nX, 06 ])))
					nValAux := (( (( nSalMinimo * aInfFunc[ nPosBj, 15 ] ) * 2) / nDiasBim ) * nDias4)
				EndIf
				
				If ( nValAux > 0 .and. ( aScan( aMatInfo, { |x| x[01] == aInfFunc[ nx, 01 ] } ) > 0 ) )
					If ( aScan( aFunSeg, { |x| x[01] == aInfFunc[ nx, 01 ] } ) == 0 )
						aInfFunc[ nx, 14 ] := nValAux + nSegViv
						Aadd( aFunSeg, { aInfFunc[ nx, 01 ] } ) 						
					Else
						aInfFunc[ nx, 14 ] := nValAux					
					EndIf
				ElseIf ( aScan( aMatInfo, { |x| x[01] == aInfFunc[ nx, 01 ] } ) > 0 )
					aInfFunc[ nx, 14 ] := nValAux
				EndIf  
				
				aInfFunc[ nx, 15 ] := aInfFunc[ nPosBj, 15 ]
				aInfFunc[ nx, 34 ] := aInfFunc[ nPosBj, 34 ]								
			EndIf
		Else		
			nVlInf 		:= ((aInfFunc[nx,33] * nDias4) * nInfon) / 100      //Infonavit	
			nRetPat 	:= ((aInfFunc[nx,5] * nDias4) * nRetiroPat) / 100   //Retiro
			nVlCVPat	:= ((aInfFunc[nx,33] * nDias1) * nCVPat) / 100      //Cesantia y Vejez - nao desconta incapacidades e faltas
			nVlCVEmp	:= ((aInfFunc[nx,33] * nDias1) * nCVEmp) / 100      //Cesantia y Vejez - nao desconta incapacidades e faltas			
		EndIf
	EndIf

	nSoma1 		:= nRetPat+nVlCVPat+nVlCVEmp
	nApPatr 	:= 0                                  

	nCF1     := aInfFunc[nx,14]
	nSoma2 	:= nVlInf+nCF1   

	nCfPat := ((nSalMinimo * nDias2) * nPCFixPat) / 100			//Cuota Fija
	If aInfFunc[nx,5] > (nSalMinimo * 3)
		nExPat := (((aInfFunc[nx,5]- (nSalMinimo*3)) * nDias2) * nPExCPat) / 100   //Excedente Empresa
		nExEmp := (((aInfFunc[nx,5]- (nSalMinimo*3)) * nDias2) * nPExCEmp) / 100   //Excedente Func.
	Endif
	nPDPat := ((aInfFunc[nx,5] * nDias2) * nPEDinPat) / 100  //Emprestimo em Dinheiro Empresa 					 
	nPDEmp := ((aInfFunc[nx,5] * nDias2) * nPGDinEmp) / 100  //Emprestimo em Dinheiro Empregado					  
	nGMPPat:= ((aInfFunc[nx,5] * nDias2) * nPGMPePat) / 100  //Gastos Medicos Pensionados Empresa 					
	nGMPEmp:= ((aInfFunc[nx,5] * nDias2) * nPGMPeEmp) / 100  //Gastos Medicos Pensionados Empregado 
	nRTPat := ((aInfFunc[nx,5] * nDias2) * nPRTPat )  / 100  //Risco Trabalho           
	nIVPat := ((aInfFunc[nx,33]* nDias1) * nPInViPat) / 100  //Invalidez e Vida Empresa					 
	nIVEmp := ((aInfFunc[nx,33]* nDias1) * nPInViEmp) / 100  //Invalidez e Vida Empregado					               
	nGPSPat:= ((aInfFunc[nx,5] * nDias2) * nPGPSPat)  / 100  //Guarderia Empresa					             

	nTotPat := nCfPat+nExPat+nPDPat+nGMPPat+nRTPat+nIVPat+nGPSPat
	nTotEmp := nExEmp+nPDEmp+nGMPEmp+nIVEmp
	nTotal  := nTotPat+nTotEmp
	
	aInfFunc[nx,08]:=	Transform(nRetPat,"@E 99,999,999.99")
	aInfFunc[nx,09]:=nVlCVPat
	aInfFunc[nx,10]:=	nVlCVEmp
	aInfFunc[nx,11]:=	Transform(nVlInf,"@E 99,999,999.99")
 	aInfFunc[nx,12]:=	Transform(nSoma1,"@E 99,999,999.99")
	aInfFunc[nx,13]:=	Transform(nApPatr,"@E 99,999,999.99")
	aInfFunc[nx,16]:=	Transform(nSoma2,"@E 99,999,999.99")
   
	If cTpImp == 3

		aInfFunc[nx,19]:=	Transform(nCfPat,"@E 999,999.99")
		aInfFunc[nx,20]:=	Transform(nExPat,"@E 999,999.99")
		aInfFunc[nx,21]:=	Transform(nExEmp,"@E 999,999.99")
		aInfFunc[nx,22]:=	Transform(nPDPat,"@E 99,999.99")
		aInfFunc[nx,23]:=	Transform(nPDEmp,"@E 99,999.99")
		aInfFunc[nx,24]:=	Transform(nGMPPat,"@E 999,999.99")
		aInfFunc[nx,25]:=	Transform(nGMPEmp,"@E 999,999.99")
		aInfFunc[nx,26]:=	Transform(nRTPat,"@E 999,999.99")
		aInfFunc[nx,27]:=	Transform(nIVPat,"@E 999,999.99")
		aInfFunc[nx,28]:=	Transform(nIVEmp,"@E 99,999.99")
		aInfFunc[nx,29]:=	Transform(nGPSPat,"@E 999,999.99")
		aInfFunc[nx,30]:=	Transform(nTotPat,"@E 999,999.99")
		aInfFunc[nx,31]:=	Transform(nTotEmp,"@E 999,999.99")
		aInfFunc[nx,32]:=	Transform(nTotal,"@E 999,999.99")
	
		nTotCF1 	+= nCfPat
		nTotExPat 	+= nExPat
		nTotExEmp 	+= nExEmp
		nTotPDPat 	+= nPDPat
		nTotPDEmp 	+= nPDEmp
		nTotGMPPat  += nGMPPat
		nTotGMPEmp 	+= nGMPEmp
		nTotRtPat	+= nRTPat
		nTotIvPat	+= nIVPat
		nTotIvEmp 	+= nIVEmp
		nTotGpsPat	+= nGPSPat
		nTotGerPat 	+= nTotPat
		nTotGerEmp 	+= nTotEmp
		nTotGeral	+= nTotal
	Endif

	nTotDias 	+= If(aInfFunc[nx,2] <> "B",Val(aInfFunc[nx,4]),0)
	nTotDiInc 	+= Val(aInfFunc[nx,6])
	nTotDiFal 	+= Val(aInfFunc[nx,7])
	nTotRet		+= nRetPat
	nTotCVPat 	+= nVlCVPat
	nTotCVEmp 	+= nVlCVEmp
	nTotVlInf 	+= nVlInf 
	nTotSoma1 	+= nSoma1
	nTotApPat   += nApPatr
	nTotCF   	+= NoRound(aInfFunc[nx,15])
	nTotAmort	+= aInfFunc[nx,14]
	nTotSoma2	+= nSoma2  
	
	If Ascan(aPatrSCre,{|x| x[1] == aInfFunc[nx,1]}) > 0
		nPatrSCr 	+= nVlInf
	Endif	

   If aInfFunc[nx,15] > 0 
	  	nTotAc++
	Endif
	
	nRetPat := nVlCVPat:= nVlCVEmp:=nVlInf :=nSoma1:=nApPatr:=nCF1:=nSoma2 := 0
	nDias1:= nDias2:= nDias3 := 0

Next nx	

Return
          
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGravFun  ºAutor  ³Silvia Taguti       º Data ³  05/31/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Adiciona informacoes do funcionario do array               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fGravFun()
               
Local cTpJorn 	:= Space(2) 
                         
If (cAliasRCQ)->RCQ_TJRNDA $ "1|2|3|4|5"
	cTpJorn := "* "
ElseIf (cAliasRCQ)->RCQ_TJRNDA == "6"	
	cTpJorn := "**"
Else
	cTpJorn := "  "
Endif  

If (cAliasRCQ)->RCQ_TPMOV == "01"
	If nAmort > 0
		AADD(aPatrSCre,{(cAliasRCQ)->RCQ_MAT} )
	Endif	
Endif
If Ascan(aInfFunc,{|x| x[1]==(cAliasRCQ)->RCQ_MAT .And. x[2]==cChave .And. x[3] == dtoc(dDataAd) .And.;
 	x[17]==(cAliasRCQ)->RCQ_TPMOV } ) > 0
 	Return
Endif 	

AADD(aInfFunc,{(cAliasRCQ)->RCQ_MAT,;											//01 Matricula
					cChave,;                                              //02 Chave
					If(dDataAd <> ctod("//"),dtoc(dDataAd)," "),;        //03 Data
					cDias,;                                               //04 Dias
					nSalDiaI,;                                            //05 Salario Diario
					Str(nDiasInc,3),;                                    //06 Dias Incapa
					Str(nDiasFal,3),;                                    //07 Dias Faltas
					"",;                                                  //08 
					"",;                                                  //09
					"",;                                                  //10
					"",;                                                  //11
					"",;                                                  //12
					"",;                                                  //13
					0 ,;                                                  //14 Cota Fixa
					(cAliasRCQ)->RCQ_VALINF ,;                         	  //15 nAmort
					"",;                                                  //16
					(cAliasRCQ)->RCQ_TPMOV ,;	 									//17 Tipo de Movto
					cTpJorn,;															//18 Tipo Jornada
					"",;                                                  //19
					"",;                                                  //20
					"",;                                                  //21
					"",;                                                  //22
					"",;                                                  //23
					"",;                                                  //24
 					"",;                                                  //25
					"",;                                                  //26
					"",;                                                  //27
					"",;                                                  //28
					"",;                                                  //29
					"",;                                                  //30
					"",;                                                  //31
					"",;                                                  //32
					nSalIVCV,;															//33 Salario IVCV
					"",;                                                 //34 Simbolo
					""})                                                  //35 deletado
						   
nSalDiaI := 0
nSalIVCV	:= 0   
						
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FRETINFO  ºAutor  ³Microsiga           º Data ³  11/03/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna Valor do Infonavit                                 º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fRetInfo(nDiasIn, nSaldiaI)

If cTipInf == "1"                  //% SDI
	nCF	 := (nSalDiaI * nDiasIn * nAmort	) /100
	cSimb  := "%  "
ElseIf cTipInf == "2"              //Cuota Fija
	nCF 	 := If(cAnoMesIni <> cAnoMesFim,(nAmort * 2 * (nDiasIn/nDiasBim)),nAmort)
	cSimb  := "$  "
ElseIf cTipInf == "3"               //Salario Minimo
	nCF    := (nSalMinimo * nAmort * 2 *(nDiasIn/nDiasBim))
	cSimb  := "VSM"
Endif 

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fImprAna ³ Autor ³ Silvia Taguti         ³ Data ³ 09.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio por Funcionario - Analitico                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fImprAna()												  				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImprAna()

Local nx := 0
Local nPos := 0
Local cMatrAnt := "!!"               
Local nPos1 := 0

For nx:= 1 to Len(aInfFunc)
	If aInfFunc[nx,35] == "D"  .or. (aInfFunc[nx,4] == "00" .And. !(aInfFunc[nx,2] $ "A|B"))
		Loop
	Endif	

	If aInfFunc[nx,1] <> cMatrAnt
		nPos	:=	Ascan(aCabFunc,{|x| x[3] == aInfFunc[nx,1] } ) 
  		If nPos > 0
 			DET := Space(4)+fRetTxt(aCabFunc[nPos,1],31)+fRetTxt(aCabFunc[nPos,2],61)+;
					 fRetTxt(aCabFunc[nPos,3],70)+fRetTxt(aCabFunc[nPos,4],35)+;
					 fRetTxt(aCabFunc[nPos,5],20) 
			IMPR(DET,"C") 
			DET := ""
		Endif		
	Endif	
   
	DET := Space(1) + fRetTxt(aInfFunc[nx,2],4)+ ;										//Clave
							fRetTxt(aInfFunc[nx,3],11)+ ;									//Fecha
     	    				If(aInfFunc[nx,2] <> "B",fRetTxt(aInfFunc[nx,4],4) + ; //Dias
     	    				Transform(aInfFunc[nx,5],"@E 99,999,999.99")+ ; 			//Salario Diario Integrado
          				aInfFunc[nx,18]+"  "+ ;                                 //Tipo de Jornada
          				aInfFunc[nx,6]+ Space(1) + ;       							//Dias Incapacidade
          				aInfFunc[nx,7]+ Space(2) + ;										//Dias Faltas
  	       				aInfFunc[nx,8]+"   "+ ;												//Retiro
  	       				Transform(aInfFunc[nx,9],"@E 99,999,999.99")+ "    " +; //Cesantia e Vejez Patronal	
  	       				Transform(aInfFunc[nx,10],"@E 99,999,999.99")+ "    "+; //Cesantia e Vejez Obr
     	    				aInfFunc[nx,12]+ "    " + ;                              //Soma
     	    				aInfFunc[nx,11]+ "    " + ;										//Valor Infonavit	
     	    				Transform( NoRound(aInfFunc[nx,15]),"@E 99,999,999.99")+;         //Amortizacion
        	 				aInfFunc[nx,34]+ Space(3)+ ;                            //Simb
        	 				Transform(aInfFunc[nx,14],"@E 999,999,999.99")+Space(7)+ ; //Total Amort
        	 				aInfFunc[nx,16],"")                                        //Soma
	IMPR(DET,"C")                
	cMatrAnt := aInfFunc[nx,1]
	nPos := 0
	DET := ""
Next nx	
            
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ImprSin   ºAutor  ³Silvia Taguti       º Data ³  06/01/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Impressao do Relatorio - Sinterico                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fImprSin()

Local nx 		:= 0
Local nPos 		:= 0
Local cMatrAnt := "!!"

For nx:= 1 to Len(aInfFunc)
	If aInfFunc[nx,35] == "D"  .or. (aInfFunc[nx,4] == "00" .And. !(aInfFunc[nx,2] $ "A|B"))
		Loop
	Endif	
	If aInfFunc[nx,1] <> cMatrAnt
		nPos	:=	Ascan(aCabFunc,{|x| x[3] == aInfFunc[nx,1] } ) 
  		If nPos > 0
			DET := Space(4)+fRetTxt(aCabFunc[nPos,1],30)+fRetTxt(aCabFunc[nPos,2],40)+;
					 fRetTxt(aCabFunc[nPos,3],22)+fRetTxt(aCabFunc[nPos,4],15)+;
					 fRetTxt(aCabFunc[nPos,5],20) 
			IMPR(DET,"C") 
			DET := ""
		Endif		
	Endif	
   
	DET := Space(1) + fRetTxt(aInfFunc[nx,2],4)+ ;											//Clave
							 fRetTxt(aInfFunc[nx,3],11)+;										//Fecha
     	  					 If(aInfFunc[nx,2] <> "B",fRetTxt(aInfFunc[nx,4],4) + ;   //Dias
     	  					 Transform(aInfFunc[nx,5],"@E 9,999.99")+	;						//Salario Diario Integrado
        	 				 aInfFunc[nx,18]+ ;														//Tipo de Jornada
        	 				 aInfFunc[nx,6]+ Space(1) + ;										//Dias Incapacidade
        	 				 aInfFunc[nx,7]+ Space(1)+;                                //Dias Faltas
          				 aInfFunc[nx,8]+" "+ ;                                     //Retiro
          				 Transform(aInfFunc[nx,9]+;											//Cesantia y Vejez Patronal
          				 aInfFunc[nx,10],"@E 9,999,999.99")+" "+;                  //Cesantia y Vejez Obr
  	       				 aInfFunc[nx,12]+ "  " + ;                                 //Soma
  	       				 aInfFunc[nx,11]+ "  " + ;                                 //Valor Infonavit
  	       				 Transform(NoRound(aInfFunc[nx,15]),"@E 999,999.99")+;              //Amort
     	    				 aInfFunc[nx,34]+ ;                                        //Simb
     	    				 Transform(aInfFunc[nx,14],"@E 999,999.99")+ "" + ;        //Total Amort
     	    				 aInfFunc[nx,16],"")                                       //Soma
	IMPR(DET,"C")                
	cMatrAnt := aInfFunc[nx,1]
	nPos := 0
	DET := ""
Next nx	
            
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fRetTxt  ³ Autor ³ Equipe R.H.           ³ Data ³ 26.01.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna o texto no tamanho indicado no segundo parametro	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fRetTxt(cTexto, nTamanho)								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Static Function fRetTxt(cTexto, nTamanho)

Return Left(cTexto + Space(nTamanho), nTamanho)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fTotPatAnaºAutor  ³Silvia Taguti       º Data ³  05/19/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime Total Geral do Registro Patronal Analitico          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fTotPatAna()

DET := Space(15)+ fRetTxt(Alltrim(Str(nTotDias)),4)+ Space(16)+Str(nTotDiInc,5)+ ;
       Str(nTotDiFal,4)+" "+Transform(nTotRet,"@E 999,999,999.99")+"  "+;
		 Transform(nTotCVPat,"@E 999,999,999.99")+"   "+Transform(nTotCVEmp,"@E 999,999,999.99")+"   "+ ; 
		 Transform(nTotSoma1,"@E 999,999,999.99")+"   "+Transform(nTotVlInf,"@E 999,999,999.99")+"   "+ ; 
		 Transform(NoRound(nTotCF),"@E 999,999,999.99")+"      "+Transform(nTotAmort,"@E 999,999,999.99")+"    "+;
		 Transform(nTotSoma2,"@E 9,999,999,999.99")                         
Impr( __PrtThinLine(),"C")

IMPR(DET,"C")
Li+=2

DET := Space(2)+fRetTxt(STR0063,37)+Transform(nTotSoma1,"@E 9,999,999.99")+Space(2)+STR0066+" "+Transform(nTotVlInf-nPatrSCr,"@E 9,999,999.99") //Total a Pagar de RCV ##Aportacion Patronal S/Credito
IMPR(DET,"C")
DET := Space(2)+fRetTxt(STR0064,37)+Transform(nTotSoma2,"@E 9,999,999.99")+Space(2)+ STR0051+" "+Transform(nPatrSCr,"@E 9,999,999.99") //Total a Pagar de INFONAVIT##Aportacion Patronal C/Credito
IMPR(DET,"C")
DET := Space(2)+fRetTxt(STR0065,37)+Transform(nTotSoma1+nTotSoma2,"@E 9,999,999.99")+Space(2)+fRetTxt(STR0037,37)+Transform(nTotAmort,"@E 9,999,999.99")       	 //Total a Pagar ##Amortizacion
IMPR(DET,"C")
DET := Space(53)+fRetTxt(STR0064,37)+Transform(nTotSoma2,"@E 9,999,999.99")     	 						//Total a Pagar de INFONAVIT  
IMPR(DET,"C")
DET := Space(53)+fRetTxt(STR0057,37)+ Padl(Alltrim(Str(nTotAc)),12)//Total de AcreditadosFONAVIT  
IMPR(DET,"C")

Li+=2

//Total a Pagar#Total de Cotizantes
DET := Space(2)+STR0050+ " "+fRetTxt(Alltrim(Str(nTotFunc)),6)
IMPR(DET,"C")
DET := Space(2)+STR0052+Space(17)+STR0060         //Trabalhadores com Artigo  33#CC.y V.
IMPR(DET,"C")
DET := Space(2)+STR0053+Space(17)+STR0061        	//Trabalhadores Pensionados IV#% o CF.
IMPR(DET,"C")			           
DET := Space(2)+STR0054+Space(17)+STR0062		   //Trabalhadores Pensionados CV#P"SDI
IMPR(DET,"C")
DET := Space(2)+STR0055+Space(17)+STR0058        	//Trabajadores con Semana Reducida#Inc.
IMPR(DET,"C")
DET := Space(2)+STR0056+Space(15)+STR0059			//** Trabajadores con Jornada Reducida#Aus.
IMPR(DET,"C")
DET := Space(2)+STR0059+Space(31)+STR0067+Transform(nSalMinimo,"@E 9,999.99") //Aux.#Salario Minimo Do  D.F.
IMPR(DET,"C")

IMPR("","F")

nTotDias	:= nTotDiInc:= nTotDiFal:= nTotRet:=nTotCVPat:=nTotCVEmp:=nTotSoma1:= 0
nTotCF   :=nTotAmort:=nTotSoma2:= nTotFunc :=nTotAc	:= 0

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fTotPatSinºAutor  ³Silvia Taguti       º Data ³  05/19/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime Total Geral do Registro Patronal Sintetico          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fTotPatSin()

DET := Space(15)+ fRetTxt(Alltrim(Str(nTotDias)),4)+ Space(9)+Str(nTotDiInc,5)+ ;
       Str(nTotDiFal,4)+" "+Transform(nTotRet,"@E 99,999,999.99")+""+;
		 Transform(nTotCVPat+nTotCVEmp,"@E 99,999,999.99")+" "+ ; 
		 Transform(nTotSoma1,"@E 99,999,999.99")+"  "+Transform(nTotVlInf,"@E 99,999,999.99")+""+ ; 
		 Transform(NoRound(nTotCF),"@E 9,999,999.99")+""+Transform(nTotAmort,"@E 99,999,999.99")+""+;
		 Transform(nTotSoma2,"@E 99,999,999.99")                         
Impr( __PrtThinLine(),"C")

IMPR(DET,"C")
Li+=2

DET := Space(2)+fRetTxt(STR0063,37)+Transform(nTotSoma1,"@E 9,999,999.99")+Space(2)+STR0066 +" "+Transform(nTotVlInf-nPatrSCr,"@E 9,999,999.99")      //Total a Pagar de RCV ##Aportacion Patronal S/Credito
IMPR(DET,"C")
DET := Space(2)+fRetTxt(STR0064,37)+Transform(nTotSoma2,"@E 9,999,999.99")+Space(2)+ STR0051+" "+Transform(nPatrSCr,"@E 9,999,999.99")         //Total a Pagar de INFONAVIT##Aportacion Patronal C/Credito
IMPR(DET,"C")
DET := Space(2)+fRetTxt(STR0065,37)+Transform(nTotSoma1+nTotSoma2,"@E 9,999,999.99")+Space(2)+fRetTxt(STR0037,37)+Transform(nTotAmort,"@E 9,999,999.99")       	 //Total a Pagar ##Amortizacion
IMPR(DET,"C")
DET := Space(53)+fRetTxt(STR0064,37)+Transform(nTotSoma2,"@E 9,999,999.99")     	 						//Total a Pagar de INFONAVIT  
IMPR(DET,"C")
DET := Space(53)+fRetTxt(STR0057,37)+ Padl(Alltrim(Str(nTotAc)),12)//Total de AcreditadosFONAVIT  
IMPR(DET,"C")

Li+=2

//Total a Pagar#Total de Cotizantes
DET := Space(2)+STR0050+ " "+fRetTxt(Alltrim(Str(nTotFunc)),6)
IMPR(DET,"C")
DET := Space(2)+STR0052+Space(17)+STR0060         //Trabalhadores com Artigo  33#CC.y V.
IMPR(DET,"C")
DET := Space(2)+STR0053+Space(17)+STR0061        	//Trabalhadores Pensionados IV#% o CF.
IMPR(DET,"C")			           
DET := Space(2)+STR0054+Space(17)+STR0062		   //Trabalhadores Pensionados CV#P"SDI
IMPR(DET,"C")
DET := Space(2)+STR0055+Space(17)+STR0058        	//Trabajadores con Semana Reducida#Inc.
IMPR(DET,"C")
DET := Space(2)+STR0056+Space(15)+STR0059			//** Trabajadores con Jornada Reducida#Aus.
IMPR(DET,"C")
DET := Space(2)+STR0059+Space(31)+STR0067+Transform(nSalMinimo,"@E 9,999.99") //Aux.#Salario Minimo Do  D.F.
IMPR(DET,"C")

IMPR("","F")

nTotDias	:= nTotDiInc:= nTotDiFal:= nTotRet:=nTotCVPat:=nTotCVEmp:=nTotSoma1:= 0
nTotCF   :=nTotAmort:=nTotSoma2:= nTotFunc :=nTotAc	:= 0

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fTotPatResºAutor  ³Silvia Taguti       º Data ³  05/19/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime Total Geral do Registro Patronal Resumo             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fTotPatRes()

DET:= ""
IMPR(DET,"C")

DET := Space(20) + STR0068						//"PARA ABONO EN CUENTA DEL IMSS"
IMPR(DET,"C")
LI+=2
DET := Space(20) + PADR(STR0069,50) + "---->"+ PadL(Alltrim(Str(nTotFunc)),15)
IMPR(DET,"C")										//"EMPLEADOS 						"
DET := Space(20) + PADR(STR0070,50) + "---->"+ PadL(Alltrim(Str(nTotDias)),15)
IMPR(DET,"C")										//"DIAS COTIZADOS 					
LI+=3
DET := Space(20) + PADR(STR0071,50) + "---->"+ PadL(Transform(nTotCf1,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"CUOTA FIJA     					
DET := Space(20) + PADR(STR0072,50) + "---->"+ PadL(Transform(nTotExPat+nTotExEmp,"@E 99,999,999.99"),15)
IMPR(DET,"C")										//"EXCEDENTE 3 SMGDF				
DET := Space(20) + PADR(STR0073,50) + "---->"+ PadL(Transform(nTotPDPat+nTotPDEmp,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"PRESTACIONES EN DINERO		"
DET := Space(20) + PADR(STR0074,50) + "---->"+ PadL(Transform(nTotGMPPat+nTotGMPEmp,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"GASTOS MEDICOS PENSIONADOS	"
DET := Space(20) + PADR(STR0075,50) + "---->"+ PadL(Transform(nTotRTPat,"@E 99,999,999.99"),15)
IMPR(DET,"C")										//"RIESGO DE TRABAJO				"
DET := Space(20) + PADR(STR0076,50) + "---->"+ PadL(Transform(nTotIVPat+nTotIVEmp,"@E 9,999,999.99"),15)
IMPR(DET,"C")	                          	//"INVALIDEZ Y VIDA				"
DET := Space(20) + PADR(STR0077,50) + "---->"+ PadL(Transform(nTotGPSPat,"@E 9,999,999.99"),15)
IMPR(DET,"C")                            //"GUARDERIAS Y PRES. SOC.		"
LI++
DET:= Space(20) +  PADR(STR0078,65) + "---->"+ PadL(Transform(nTotGeral,"@E 999,999,999.99"),15)
IMPR(DET,"C")                            //"TOTAL                          

LI+=3
DET := Space(20) + STR0079						//"PARA ABONO EN CUENTA INDIVIDUAL
IMPR(DET,"C")
LI+=2
DET := Space(20) + PADR(STR0080,50) + "---->"+ PadL(Transform(nTotRet,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"RETIRO         					
DET := Space(20) + PADR(STR0081,50) + "---->"+ PadL(Transform(nTotCVPat+nTotCVEmp,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"CESANTIA Y VEJEZ				
LI++
DET:= Space(20) +  PADR(STR0078,65) + "---->"+ PadL(Transform(nTotSoma1,"@E 999,999,999.99"),15)
IMPR(DET,"C")                            //"TOTAL                    "
LI+=3
DET := Space(20) + STR0082						//"PARA ABONO EN CUENTA DE INFONAVIT
IMPR(DET,"C")
LI+=2
DET := Space(20) + PADR(STR0083,50) + "---->"+ PadL(Transform(nTotVlInf-nPatrSCr,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"APORTE PATRONAL SIN CREDITO
DET := Space(20) + PADR(STR0084,50) + "---->"+ PadL(Transform(nPatrSCr,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"APORTE PATRONAL CON CREDITO
DET := Space(20) + PADR(STR0085,50) + "---->"+ PadL(Transform(nTotAmort,"@E 9,999,999.99"),15)
IMPR(DET,"C")										//"AMORTIZACION                  
LI++
DET:= Space(20) +  PADR(STR0078,65) + "---->"+ PadL(Transform((nTotVlInf-nPatrSCr)+nPatrSCr+nTotAmort,"@E 999,999,999.99"),15)
IMPR(DET,"C")                            //"TOTAL                    "
LI++
DET := Space(20) + PADR(STR0086,50) + "---->"+ Padl(Alltrim(Str(nTotAc)),15)
IMPR(DET,"C")                            //"TOTAL ACREDITADOS                    "
LI+=3
DET := Space(20) + PADR(STR0087,65)+ "---->"+ PadL(Transform(nTotGeral+nTotSoma1+(nTotVlInf-nPatrSCr)+nPatrSCr+nTotAmort,"@E 9,999,999.99"),15)					
IMPR(DET,"C")                            //"TOTAL ACREDITADOS                    "
                                        //TOTAL 
IMPR("","P")

nTotDias	:= nTotDiInc	:= nTotDiFal	:= nTotCF1 		:= nTotExPat	:= nTotExEmp	:= 0
nTotPDPat:= nTotPDEmp	:= nTotGMPPat	:= nTotGMPEmp	:= nTotRtPat	:= nTotIvPat	:= 0
nTotIvEmp:= nTotGpsPat	:= nTotGerPat	:= nTotGerEmp	:= nTotGeral	:= nTotFunc 	:= 0
nTotDias	:= nTotDiInc	:= nTotDiFal	:= nTotRet		:= nTotCVPat	:= nTotCVEmp	:= 0
nTotCF   :=nTotAmort		:= nTotSoma2	:= nTotFunc 	:= nTotAc		:= nTotSoma1	:= 0

Return Nil
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPER660   ºAutor  ³Microsiga           º Data ³  04/26/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fBimestre()

Local cTitulo:= STR0094
Local MvPar
Local MvParDef:=""
Local lRet
Local aBim := {}

MvPar:=&(Alltrim(ReadVar()))		 // Carrega Nome da Variavel do Get em Questao
mvRet:=Alltrim(ReadVar())			 // Iguala Nome da Variavel ao Nome variavel de Retorno

aBim := {;
			"1 - " + STR0088,;	//"Janeiro/Fevereiro"
			"2 - " + STR0089,;	//"Marcço/Abril"
			"3 - " + STR0090,;	//"Maio/Junho"
			"4 - " + STR0091,;	//"Julho/Agosto"
			"5 - " + STR0092,;	//"Setembro/Outubro"
			"6 - " + STR0093;		//"Novembro/Dezembro"
		}  
MvParDef:=" "

IF f_Opcoes(@MvPar,cTitulo,aBim,MvParDef,,,.T.,1,1)  // Chama funcao f_Opcoes
	&MvRet := Substr(MvPar,1,1)										 // Devolve Resultado
EndIF

Return lRet
                                                                                  
