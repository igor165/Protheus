#INCLUDE "PROTHEUS.CH"

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁGPEXSRR   Ё Autor Ё Marinaldo de Jesus    Ё Data Ё06/12/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁBiblioteca de Funcoes Genericas para uso em Formulas no SRR Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      Ё Generico                                                   Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё
цддддддддддддбддддддддддбдддддддбддддддддддддддддддддддддддддддддддддддд╢
ЁProgramador Ё Data     Ё BOPS  ЁMotivo da Alteracao                    Ё
цддддддддддддеддддддддддедддддддеддддддддддддддддддддддддддддддддддддддд╢
ЁMauricio T. Ё05/10/2006Ё       ЁTratamento para AS/400.                Ё
ЁMauricio T. Ё18/01/2007Ё109389-ЁReutilizacao do Objeto SRR.            Ё
ЁWinstonCostaЁ04/01/2019Ё       ЁRetirada Tratamento para AS/400.       Ё
юддддддддддддаддддддддддадддддддаддддддддддддддддддддддддддддддддддддддды/*/
/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁGetSRR		    ЁAutorЁMarinaldo de Jesus Ё Data Ё06/12/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁObtem as Informacoes do SRR									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                      									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function GetSRR( oSrr, cQueryWhere , lSqlWhere , lTopFilter )

Local cKey
Local cRetOrder
Local lGetSRR
Local nSRROrder

IF Empty( cQueryWhere )
	cQueryWhere := WhereSRR()
	#IFDEF TOP
		IF !Empty( cQueryWhere )
			lSqlWhere	:= .T.
		EndIF	
	#ENDIF
EndIF

cRetOrder := "RR_FILIAL+RR_MAT+RR_PERIODO+RR_ROTEIR"
nSRROrder := RetOrder( "SRR" , cRetOrder , .T. )
IF ( nSRROrder == 0 )
	cRetOrder	:= "RR_FILIAL+RR_MAT"
	nSRROrder	:= RetOrder( "SRR" , cRetOrder , .F. )
EndIF

IF ( cRetOrder == "RR_FILIAL+RR_MAT+RR_PERIODO+RR_ROTEIR" )
	cKey	:= ( SRA->( RA_FILIAL + RA_MAT ) + RcPeriodoInit() + RcRoteirInit() )
Else
	cKey	:= SRA->( RA_FILIAL + RA_MAT )
EndIF

//Eh necessario ter o Mnemonico oSRR ( Tipo Private para reinicializar a cada registro )
IF (( ValType( oSRR ) == "O" ) .and.;
	( Len(oSRR:aHeader) > 0 ))
	oSRR:GetCols( nSRROrder , cKey , cQueryWhere , lSqlWhere )
Else
	oSRR	:= GetDetFormula():New( "SRR" , nSRROrder , cKey , cQueryWhere , @lSqlWhere , @lTopFilter )
EndIf

lGetSRR	:= oSRR:GetOk()
IF ( lGetSRR )
	oSRR :Del( { "RR_VALINFO" , "RR_HORINFO" , "RR_TIPO2" } , { 0 , 0 , "C" } )	//Calculado
	oSRR:Del( { "RR_VALINFO" , "RR_HORINFO" , "RR_TIPO2" } , { 0 , 0 , "K" } )	//Ferias
	oSRR:Del( { "RR_VALINFO" , "RR_HORINFO" , "RR_TIPO2" } , { 0 , 0 , "P" } )	//Primeira Parcela 13o.
	oSRR:Del( { "RR_VALINFO" , "RR_HORINFO" , "RR_TIPO2" } , { 0 , 0 , "S" } )	//Segunda Parcela 13o.
	oSRR:Default( { "RR_HORAS" , "RR_VALOR" , "RR_DATA" } )
EndIF

Return( lGetSRR )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁPutSRR			ЁAutorЁMarinaldo de Jesus Ё Data Ё06/12/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁGrava as Informacoes do SRR									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                      									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerica      										    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function PutSRR()

Local aFieldsDel	:= Array( 4 )
Local aCntsDel		:= Array( 4 )

aFieldsDel[1] := "RR_VALINFO"	; aCntsDel[1] := 0
aFieldsDel[2] := "RR_HORINFO"	; aCntsDel[2] := 0
aFieldsDel[3] := "RR_VALOR"		; aCntsDel[3] := 0
aFieldsDel[4] := "RR_HORAS"		; aCntsDel[4] := 0

Return( oSRR:Put( aFieldsDel , aCntsDel ) )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁWhereSRR		ЁAutorЁMarinaldo de Jesus Ё Data Ё23/12/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna a Clausula Where para o SRR							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                      									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁWhere para o SRR									    	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function WhereSRR()

Local cQueryWhere

#IFDEF TOP
	cQueryWhere := "( RR_FILIAL='" + SRA->RA_FILIAL + "' AND " + "RR_MAT='" + SRA->RA_MAT + "') "
	cQueryWhere += " AND "
	cQueryWhere += " RR_PERIODO='" + RcPeriodoInit() + "'"
	cQueryWhere += " AND "
	cQueryWhere += "RR_ROTEIR='" + RcRoteirInit() + "'"
	cQueryWhere += " AND "
	cQueryWhere += "D_E_L_E_T_<>'*' "
#ELSE
	cQueryWhere := " RR_PERIODO=='" + RcPeriodoInit() + "'"
	cQueryWhere += " .AND. "
	cQueryWhere += "RR_ROTEIR=='" + RcRoteirInit() + "'"
#ENDIF

Return( cQueryWhere )

/*
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   Ё fPosSRR  Ё Autor Ё Marinaldo de Jesus 	  Ё Data Ё31/07/2000Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Posicionar Cursor em aCols de Acordo com o Tipo da Verba   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё fPosSRR( cCampo , lPos )                   				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ cCampo - Campo em que o Cursor esta Posicionado            Ё
Ё          Ё lPos   - .T. para posicionar na Coluna Correspondente      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   Ё Generico 												    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function fPosSRR( cCampo , lPos )

Local cTipo		:= ""
Local cRetVar	:= &( ReadVar() )

Local nPosPd	:= 0
Local nPosTip	:= 0
Local nPosHrs	:= 0
Local nPosVal	:= 0
Local nPulos	:= 0

lPos := If(lPos == Nil .or. ValType( lPos ) != "L" ,.F.,lPos)

If !lPos 
	Return( .T. )
EndIf

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁVerifica o Tipo da Verba (Horas,Dias,Valor ou Fixo)           Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
If ( "RR_PD" $ Upper( AllTrim( cCampo ) ) )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe a Verba nao Estiver Cadastrada.                            Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If  ( cTipo := AllTrim(PosSRV(  cRetVar , SRA->RA_FILIAL , "RV_TIPO" )) ) == "@"
		Return( .T. )
	EndIf
Else
	cTipo := cRetVar
EndIf

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁVerifica a Posicao das Colunas em aHeader.                    Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
nPosPd	:= GdFieldPos( "RR_PD" )
nPosTip := GdFieldPos( "RR_TIPO1"  )
nPosHrs := GdFieldPos( "RR_HORINFO" )
nPosVal := GdFieldPos( "RR_VALINFO" )

If ( "RR_PD" $ Upper( AllTrim( cCampo ) ) )
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁAltera Tipo da Verba em aCols para Substituir na  Redigitacao Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aCols[ oGet:nAt, nPosTip ] := cTipo
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁQuantidade de Vezes a Saltar                                  Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If Empty(cTipo)				//Se o Tipo da Verba nao Estiver Definido
		nPulos :=  ( nPosTip - nPosPd ) - 1
	ElseIf ( cTipo $ "H_D" )	//Se o Tipo da Verba For Horas ou Dias
		nPulos := ( nPosHrs - nPosPd ) - 1
	ElseIf ( cTipo $ "F_V" )	//Se o Tipo da Verba For Fixo ou Valor
		nPulos :=  ( nPosVal - nPosPd ) - 1
	EndIf
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁGarante o Posicionamento na Coluna Chamadora da Funcao.       Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	While oGet:oBrowse:nColPos <> nPosPd
		If oGet:oBrowse:nColPos > nPosPd
			oGet:oBrowse:GoLeft()
		ElseIf oGet:oBrowse:nColPos < nPosPd
			oGet:oBrowse:GoRight()
		EndIf
	End While
	
ElseIf ( "RR_TIPO1" $ Upper( AllTrim( cCampo ) ) )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁQuantidade de Vezes a Saltar                                  Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If Empty(cTipo)				//Se o Tipo da Verba nao Estiver Definido
		Return( .T. )
	ElseIf ( cTipo $ "H_D" )	//Se o Tipo da Verba For Horas ou Dias
		nPulos := ( nPosHrs - nPosTip ) - 1
	ElseIf ( cTipo $ "F_V" )	//Se o Tipo da Verba For Fixo ou Valor
		nPulos :=  ( nPosVal - nPosTip ) - 1
	EndIf	
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁGarante o Posicionamento na Coluna Chamadora da Funcao.       Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	While oGet:oBrowse:nColPos <> nPosTip
		If oGet:oBrowse:nColPos > nPosTip
			oGet:oBrowse:GoLeft()
		ElseIf oGet:oBrowse:nColPos < nPosTip
			oGet:oBrowse:GoRight()
		EndIf
	End While
	
EndIf

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁPosiciona Ponteiro na Coluna Correspondente                   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
While nPulos <> 0
	If nPulos > 0
		oGet:oBrowse:GoRight()
		nPulos--
	ElseIf nPulos < 0
		oGet:oBrowse:GoLeft()
		nPulos++
	EndIf
End While

Return( .T. )

/*
зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o	   ЁSRRTipo2   Ё Autor Ё Marinaldo de Jesus   Ё Data Ё04/03/2002Ё
цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁAltera Tipo 2 caso tenha havido alteracao        			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL 														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso	   ЁValid do SRR											    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function SRRTipo2()

Local cVar		:= ReadVar()

Local nPosVar	:= GdFieldPos("RR_TIPO2")

Local uLastVal	:= GdFieldGet(SubStr(cVar,4))
Local uValAtu	:= &( cVar )

If !( uLastVal == uValAtu )
	GdFieldPut( "RR_TIPO2" , "I" )
EndIf

Return( .T. )