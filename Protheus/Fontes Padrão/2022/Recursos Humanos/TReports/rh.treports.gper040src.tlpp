#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "TREPORTS_GPER040SRC.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T.)
class GPER040SRCTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass
 
method new() as object class GPER040SRCTReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Informações da folha aberta"
return self
 
method getDescription() as character class GPER040SRCTReportsBusinessObject
return STR0003//"Objeto contendo informações da folha aberta, processo, periodo, centro de custo, funcionários, funções e verbas"
 
method getAreas() as array class GPER040SRCTReportsBusinessObject
return {STR0002}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER040SRCTReportsBusinessObject
    local cAlias as character
    local cCatFunc as character
    local cCodFunc as character
    local cDescFunc as character
    local cQuery as character
    local dDataFim as date
    local dDataIni as date
    local nStart as numeric
    local nEnd as numeric
    local nCount as numeric
 
    nCount := 0
 
    cQuery  := "SELECT SRC.RC_FILIAL, SRC.RC_MAT, SRC.RC_CC, SRC.RC_PD, SRC.RC_HORAS, SRC.RC_VALOR, SRC.RC_PROCES, SRC.RC_PERIODO, SRC.RC_SEMANA, SRC.RC_ROTEIR, RCJ.RCJ_DESCRI, RCH.RCH_DTINI, RCH.RCH_DTFIM, RCH.RCH_DTPAGO, CTT.CTT_DESC01, SRA.RA_NOME, SRA.RA_NOMECMP, SRA.RA_TPCONTR, SRA.RA_CODFUNC, SRA.RA_ADMISSA, SRA.RA_DEMISSA, SRA.RA_CATFUNC, SRA.RA_SALARIO, SRA.RA_DEPIR, SRA.RA_DEPSF, SRA.RA_PERCADT, SRA.RA_HRSMES, SRA.RA_SITFOLH, SRJ.RJ_DESC, SRV.RV_DESC, SRV.RV_TIPOCOD, SRV.RV_INSS, SRV.RV_IR, SRV.RV_FGTS FROM " + RetSqlName("SRC") + " SRC "
    cQuery  += "INNER JOIN " + RetSqlName("RCJ") + " RCJ ON " + FWJoinFilial( "RCJ", "SRC" ) + " AND RCJ.RCJ_CODIGO = SRC.RC_PROCES AND RCJ.D_E_L_E_T_ = ' ' "
    cQuery  += "INNER JOIN " + RetSqlName("RCH") + " RCH ON " + FWJoinFilial( "RCH", "SRC" ) + " AND RCH.RCH_PROCES = SRC.RC_PROCES AND RCH.RCH_PER = SRC.RC_PERIODO AND RCH.RCH_NUMPAG = SRC.RC_SEMANA AND RCH.RCH_ROTEIR = SRC.RC_ROTEIR AND RCH.D_E_L_E_T_ = ' ' "
    cQuery  += "INNER JOIN " + RetSqlName("CTT") + " CTT ON " + FWJoinFilial( "CTT", "SRC" ) + " AND CTT.CTT_CUSTO = SRC.RC_CC AND CTT.D_E_L_E_T_ = ' ' "
    cQuery  += "INNER JOIN " + RetSqlName("SRA") + " SRA ON " + FWJoinFilial( "SRA", "SRC" ) + " AND SRA.RA_MAT = SRC.RC_MAT AND SRA.D_E_L_E_T_ = ' ' "
    cQuery  += "INNER JOIN " + RetSqlName("SRJ") + " SRJ ON " + FWJoinFilial( "SRJ", "SRA" ) + " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC AND SRJ.D_E_L_E_T_ = ' ' "
    cQuery  += "INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRC" ) + " AND SRV.RV_COD = SRC.RC_PD AND SRV.D_E_L_E_T_ = ' ' "
    cQuery  += "WHERE SRC.D_E_L_E_T_ = ' '"

    //Os filtros serão setados na interface do novo TReports
    if oFilter:hasFilter()
        cQuery += " AND " + oFilter:getSQLExpression()
    endif

    self:setPageSize(100)

    //Seta o primeiro item e o último da página tual
    nStart := ((nPage - 1) * self:getPageSize())
    nEnd   := nPage * self:getPageSize()

    cAlias := MPSysOpenQuery(cQuery)

    //Posiciona no registro inicial 
    (cAlias)->( dbSkip(nStart) )

    while !(cAlias)->(Eof())
        dDataIni := sToD( (cAlias)->RCH_DTINI )
        dDataFim := sToD( (cAlias)->RCH_DTFIM )
        fBuscaFunc(dDataIni, @cCodFunc, @cDescFunc, @cCatFunc )

        self:oData:appendData({"RC_FILIAL": (cAlias)->RC_FILIAL,;
                "RC_MAT": (cAlias)->RC_MAT,;
                "RC_CC": (cAlias)->RC_CC,;
                "RC_PD": (cAlias)->RC_PD, ;
                "RC_HORAS": (cAlias)->RC_HORAS, ;
                "RC_VALOR": (cAlias)->RC_VALOR, ;
                "RC_PROCES": (cAlias)->RC_PROCES, ;
                "RC_PERIODO": (cAlias)->RC_PERIODO, ;
                "RC_SEMANA": (cAlias)->RC_SEMANA, ;
                "RC_ROTEIR": (cAlias)->RC_ROTEIR, ;
                "RCJ_DESCRI": (cAlias)->RCJ_DESCRI, ;
                "RCH_DTINI": dDataIni, ;
                "RCH_DTFIM": dDataFim, ;
                "RCH_DTPAGO": sToD( (cAlias)->RCH_DTPAGO ), ;
                "CTT_DESC01": (cAlias)->CTT_DESC01,;
                "RA_NOME": (cAlias)->RA_NOME,;
                "RA_NOMECMP": (cAlias)->RA_NOMECMP,;
                "RA_TPCONTR": (cAlias)->RA_TPCONTR,;
                "RA_CODFUNC": cCodFunc,;
                "RA_ADMISSA": sToD( (cAlias)->RA_ADMISSA ), ;
                "RA_DEMISSA": sToD( (cAlias)->RA_DEMISSA ), ;
                "RA_CATFUNC": cCatFunc,;
                "RA_SALARIO": fBuscaSal(dDataIni, Nil, Nil, .F.), ;
                "RA_DEPIR": SubStr( GPRETSR9("SRA", LastDay(dDataIni), "RA_DEPIR"), 1, 4 ), ;
                "RA_DEPSF": SubStr( GPRETSR9("SRA", LastDay(dDataIni), "RA_DEPSF"), 1, 4 ), ;
                "RA_PERCADT":(cAlias)->RA_PERCADT, ;
                "RA_HRSMES": (cAlias)->RA_HRSMES, ;
                "RA_SITFOLH": RetSituacao( (cAlias)->RC_FILIAL, (cAlias)->RC_MAT, .F., dDataFim, Nil, Nil, Nil, dDataIni )[1], ;
                "RJ_DESC": cDescFunc,;                
                "RV_DESC": (cAlias)->RV_DESC, ;
                "RV_TIPOCOD": (cAlias)->RV_TIPOCOD, ;
                "RV_INSS": (cAlias)->RV_INSS, ;
                "RV_IR": (cAlias)->RV_IR, ;
                "RV_FGTS": (cAlias)->RV_FGTS })
        (cAlias)->( dbSkip() )
        nCount++
    
        if nCount == nEnd
            exit
        endif
    enddo

//Se não for o último registro indica que terá próxima página
self:setHasNext( (cAlias)->( !Eof() ) )
 
(cAlias)->( DBCloseArea() )

return self:oData
 
method getSchema() as object class GPER040SRCTReportsBusinessObject
    local aFieldsRCH as array
    local aFieldsSRA as array
    local aFieldsSRC as array
    local aFieldsSRV as array
    
    aFieldsRCH := { "RCH_DTINI", "RCH_DTFIM", "RCH_DTPAGO" }
    aFieldsSRA := { "RA_NOME", "RA_NOMECMP", "RA_TPCONTR", "RA_CODFUNC", "RA_ADMISSA", "RA_DEMISSA", "RA_CATFUNC", "RA_SALARIO", "RA_DEPIR", "RA_DEPSF", "RA_PERCADT", "RA_HRSMES", "RA_SITFOLH" }
    aFieldsSRC := { "RC_FILIAL", "RC_MAT", "RC_CC", "RC_PD", "RC_HORAS", "RC_VALOR", "RC_PROCES", "RC_PERIODO", "RC_SEMANA", "RC_ROTEIR" }
    aFieldsSRV := { "RV_DESC", "RV_TIPOCOD", "RV_INSS", "RV_IR", "RV_FGTS" }
    
    self:oSchema:aliasToSchema("CTT", "CTT_DESC01")
    self:oSchema:aliasToSchema("RCH", aFieldsRCH)  
    self:oSchema:aliasToSchema("RCJ", "RCJ_DESCRI")  
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SRC", aFieldsSRC)
    self:oSchema:aliasToSchema("SRJ", "RJ_DESC")
    self:oSchema:aliasToSchema("SRV", aFieldsSRV)
return self:oSchema
