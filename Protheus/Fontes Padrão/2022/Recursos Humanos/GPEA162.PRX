#INCLUDE "GPEA160.CH"
#INCLUDE "PROTHEUS.CH"

Static cSeqFil

/*/{Protheus.doc} GPEA162
Cadastro de Tipos de Nomina
Uso - generico
@type function
@author Marinaldo de Jesus
@since 07/11/2007
@version 12

@history 11/04/2012, Mohanad Odeh,			- Correcao pontual na opcao excluir
/*/
Function Gpea162()

Local aArea			:= GetArea()
Local aAreaSRY
Local aIndex		:= {}
Local cFiltra		:= ""
Local cModAlias		:= ""

Private lManutence
Private cRotOrd
Private aSRYAux		:= {}
Private aSRYAuxHdr	:= {}

If cPaisLoc <> "BRA"
	If !ChkVazio("SRY",.F.)
		Processa( { || fCarSRY(@aSRYAux, @aSRYAuxHdr)} , OemToAnsi(STR0122) ) // "Carregando Tipos de Roteiro"
	EndIf
EndIf

cModAlias	:= 'SRY'
aAreaSRY 	:= (cModAlias)->( GetArea() )

// Verificar se ja possui um roteiro ordinario
cRotOrd		:= fGetRotOrdinar()

/*/ Define Array contendo as Rotinas a executar do programa
----------- Elementos contidos por dimensao ------------
1. Nome a aparecer no cabecalho
2. Nome da Rotina associada
3. Usado pela rotina
4. Tipo de Transacao a ser efetuada
	1 - Pesquisa e Posiciona em um Banco de Dados
	2 - Simplesmente Mostra os Campos
	3 - Inclui registros no Bancos de Dados
	4 - Altera o registro corrente
	5 - Remove o registro corrente do Banco de Dados
	6 - Copiar
	7 - Legenda
/*/
Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina
Private bFiltraBrw	:= { || NIL }
Private cCadastro   := OemToAnsi( STR0001 ) //"Cadastro de Roteiros de Calculo"

// Verifica o Modo de Acesso do SRY e SRM. Ambos Tem que ser Iguais
IF xRetModo( cModAlias )

	// Inicializa o filtro utilizando a funcao FilBrowse
	cFiltra		:= ChkRh( FunName() , cModAlias , "1" )
	bFiltraBrw 	:= { || FilBrowse( cModAlias , @aIndex , @cFiltra ) }
	Eval( bFiltraBrw )
	
	// Chama a Funcao de Montagem do Browse
	mBrowse( 6 , 1 , 22 , 75 , cModAlias,,,,,,,,,,,,,,,,,MV_MODFOL)
	
	// Deleta o filtro utilizando a funcao FilBrowse
	EndFilBrw( cModAlias , aIndex )

EndIF

// Restaura os Dados de Entrada
RestArea( aAreaSRY )
RestArea( aArea )

Return( NIL )

/*/{Protheus.doc} MenuDef
Isola opcoes de menu para que as opcoes da rotina possam
ser lidas pelas bibliotecas Framework da Versao 9.12.
@type function
@author Tatiane Matias
@since 07/11/2007
@return aRotina
/*/
Static Function MenuDef()

 Local aRotina :=     {;
						{ STR0002 , "PesqBrw"  , 0 , 1, ,.F. } ,; //"Pesquisar"
						{ STR0003 , "Gpea162Mnt" , 0 , 2       } ,; //"Visualizar"
						{ STR0004 , "Gpea162Mnt" , 0 , 3       } ,; //"Incluir"
						{ STR0005 , "Gpea162Mnt" , 0 , 4       } ,; //"Alterar"
						{ STR0006 , "Gpea162Mnt" , 0 , 5       }  ; //"Excluir"
					}

Return aRotina

/*/{Protheus.doc} Gpea162Mnt
@type function
@author Tatiane Matias
@since 12/11/2007

@param cAlias, Alias do arquivo
@param nReg, Numero do registro
@param nOpc, Numero da opcao selecionada
/*/
Function Gpea162Mnt( cAlias , nReg , nOpc )
Local lRegOK 	:= .T. 		// se esta tudo OK para exclusao
Local cKeyDel   := SRY->RY_CALCULO 	// chave para exclusao
Local lDelRus	:= .F.

cSeqFil	:= GetSrySeqFil(xFilial('SRY'))

If nOpc == 2 // Visualizar
	AxVisual(cAlias,nReg,2,,,,,,,MV_MODFOL)
ElseIf nOpc == 3 // Incluir   
	AxInclui(cAlias,nReg,3,,,,,,"Gp162LoadFil()",,,,,,MV_MODFOL)
ElseIf nOpc == 4 // Alterar
	AxAltera(cAlias,nReg,4,,,,,,"Gp162LoadFil()",,,,,,,MV_MODFOL)
ElseIf nOpc == 5 // Excluir
	If cPaisLoc == "RUS" .And. SRY->RY_ORIGEM == '1'
		Help('',1,'GPEA162DELORIG',,STR0159,1) //"It is not possible to delete the records created by the system. Only the records created by the user can be deleted."
	Else
		lRegOk  := ChkDelRegs(	cAlias				,;	// Alias do Arquivo Principal
									nReg				,;	// Registro do Arquivo Principal
									nOpc				,;	// Opcao para a AxDeleta
									xFilial( cAlias )	,;	// Filial do Arquivo principal para Delecao
									cKeyDel				,;	// Chave do Arquivo Principal para Delecao
									NIL			   		,;	// Array contendo informacoes dos arquivos a serem pesquisados
									NIL					,;	// Mensagem para MsgYesNo
									NIL					,;	// Titulo do Log de Delecao
									NIL					,;	// Mensagem para o corpo do Log
									.F.				 	,;	// Se executa AxDeleta
									.T.     			,;	// Se deve Mostrar o Log
									NIL					,;	// Array com o Log de Exclusao
									NIL				 	 ;	// Array com o Titulo do Log
								)

		If lRegOK
			AxDeleta(cAlias,nReg,5,,,,,,,MV_MODFOL)
		EndIf
	Endif
EndIf

Return(NIL)

/*/{Protheus.doc} Gp162LoadFil
@type function
@author Leandro Drumond
@since 11/12/2012
/*/
Function Gp162LoadFil()

	SRY->RY_SEQFIL := cSeqFil

Return Nil
