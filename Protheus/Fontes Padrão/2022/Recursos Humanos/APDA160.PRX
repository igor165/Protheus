#INCLUDE "PROTHEUS.CH"
#INCLUDE "APDA160.CH"

/*/
зддддддддддбддддддддддбдддддбдддддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁAPDA160   ЁAutorЁMarinaldo de Jesus       Ё Data Ё20/03/2004Ё
цддддддддддеддддддддддадддддадддддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Criterios                                       Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                    Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё            ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL           Ё
цддддддддддддбддддддддддбдддддддддддбддддддддддддддддддддддддддддддддддд╢
ЁProgramador ЁData      ЁBOPS       ЁMotivo da Alteracao                Ё
цддддддддддддеддддддддддедддддддддддеддддддддддддддддддддддддддддддддддд╢
ЁCecilia Car.Ё03/07/2014ЁTPZWBQ     ЁIncluido o fonte da 11 para a 12 e Ё
Ё            Ё          Ё           Ёefetuada a limpeza.                Ё  
юддддддддддддаддддддддддадддддддддддаддддддддддддддддддддддддддддддддддды/*/
Function APDA160( cAlias , nReg , nOpc , lExecAuto , lGetCriter )

Local aArea 	:= GetArea()
Local aAreaRds	:= RDS->( GetArea() )
Local aAreaRdt	:= RDT->( GetArea() )

Local lExistOpc	:= ( ValType( nOpc ) == "N" )

Local bBlock

Local nPos
Local uRet
Local aRdsIndex		:= {}
Local cRdsFiltra	:= ""

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSo Executa se o Modo de Acesso dos Arquivos do Modulo APD estiЁ
	Ёverem OK													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( ApdRelationFile() )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁRedefine o Alias                                              Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cAlias	:= "RDS"

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define Array contendo as Rotinas a executar do programa      Ё
	Ё ----------- Elementos contidos por dimensao ------------     Ё
	Ё 1. Nome a aparecer no cabecalho                              Ё
	Ё 2. Nome da Rotina associada                                  Ё
	Ё 3. Usado pela rotina                                         Ё
	Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
	Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
	Ё    2 - Simplesmente Mostra os Campos                         Ё
	Ё    3 - Inclui registros no Bancos de Dados                   Ё
	Ё    4 - Altera o registro corrente                            Ё
	Ё    5 - Remove o registro corrente do Banco de Dados          Ё
	Ё    6 - Copiar                                                Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina
	
	Private cCadastro   := OemToAnsi( STR0007 )	//"CritИrios"

	IF ( lExistOpc )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁGarante o Posicinamento do Recno                              Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		DEFAULT nReg	:= ( cAlias )->( Recno() )
		IF !Empty( nReg )
			( cAlias )->( MsGoto( nReg ) )
		EndIF

		DEFAULT lGetCriter := .F.

		DEFAULT lExecAuto := .F.
		IF ( lExecAuto )

			nPos := aScan( aRotina , { |x| x[4] == nOpc } )
			IF ( nPos == 0 )
				Break
			EndIF
			bBlock := __ExecMacro( "{ |a,b,c,d,e| " + aRotina[ nPos , 2 ] + "(a,b,c,d,e) }" )
			uRet := Eval( bBlock , cAlias , nReg , nPos )
		
		Else

			uRet := Apda160Mnt( cAlias , nReg , nOpc , .T. , lGetCriter )
		
		EndIF	
	
	Else
		If GetRpoRelease("R7")
			If nModulo == 53 //SIGAAPD 
		    	cRdsFiltra 	:= 'RDS_MODULO == 0 .OR. RDS_MODULO == 53'
		    ElseIf nModulo == 18 //SIGATCF
		    	cRdsFiltra	:= 'RDS_MODULO == 18'
		    EndIf
		    If Len(cRdsFiltra) > 0
		    	bFiltraBrw := {|| FilBrowse("RDS" ,@aRdsIndex ,@cRdsFiltra)}
		    	Eval(bFiltraBrw)
		    EndIf
		EndIf	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Chama a Funcao de Montagem do Browse                                   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		mBrowse( 6 , 1 , 22 , 75 , cAlias )
	
	EndIF
	
End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁColoca o Ponteiro do Mouse em Estado de Espera			   	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()
		
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura os Dados de Entrada 											 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aAreaRdt )
RestArea( aAreaRds )
RestArea( aArea )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura o Cursor do Mouse                				   	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( uRet )

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInApda160Exec ЁAutor ЁMarinaldo de Jesus   Ё Data Ё24/08/2004Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de APDA160                           Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInApda160Exec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InApda160Exec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda160VisЁ Autor ЁMarinaldo de Jesus     Ё Data Ё20/03/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de CritИrios (visualizar)							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda160Vis( cAlias , nReg )
Return( APDA160( cAlias , nReg , 2 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda160IncЁ Autor ЁMarinaldo de Jesus     Ё Data Ё20/03/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de CritИrios (Incluir)								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda160Inc( cAlias , nReg )
Return( APDA160( cAlias , nReg , 3 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda160AltЁ Autor ЁMarinaldo de Jesus     Ё Data Ё20/03/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de CritИrios (Alterar)								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda160Alt( cAlias , nReg )
Return( APDA160( cAlias , nReg , 4 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda160DelЁ Autor ЁMarinaldo de Jesus     Ё Data Ё20/03/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de CritИrios (Alterar)							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda160Del( cAlias , nReg )
Return( APDA160( cAlias , nReg , 5 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda160MntЁ Autor ЁMarinaldo de Jesus     Ё Data Ё20/03/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de CritИrios (Manutencao)							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁApda160Mnt( cAlias , nReg , nOpc , lDlgPadSiga )			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias 		= Alias do arquivo                              Ё
Ё          ЁnReg   		= Numero do registro                            Ё
Ё          ЁnOpc   		= Numero da opcao selecionada                   Ё
Ё          ЁlDlgPadSiga = Numero da opcao selecionada                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda160Mnt( cAlias , nReg , nOpc , lDlgPadSiga , lGetCriter )

Local aArea			:= GetArea(Alias())
Local aSvKeys		:= GetKeys()
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aRdsHeader	:= {}
Local aRdsCols		:= {}
Local aRdsFields	:= {}
Local aRdsAltera	:= {}
Local aRdsNaoAlt	:= {}
Local aRdsVirtEn	:= {}
Local aRdsNotFields	:= {}
Local aRdsRecnos	:= {}
Local aRdsKeys		:= {}
Local aRdsVisuEn	:= {}
Local aRdtGdAltera  := {}
Local aRdtGdNaoAlt	:= {}
Local aRdtRecnos	:= {}
Local aRdtKeys		:= {}
Local aRdtNotFields	:= {}
Local aRdtVirtGd	:= {}
Local aRdtVisuGd	:= {}
Local aRdtHeader	:= {}
Local aRdtCols		:= {}
Local aButtons		:= {}
Local aSvRdsCols	:= {}
Local aSvRdtCols	:= {}
Local aRelation		:= {}
Local aFreeLocks	:= {}

Local bRdtGdDelOk	:= { || RdtGdDelOk( cAlias , nReg , nOpc , cCodRDS ) }
Local bSet15		:= { || NIL }
Local bSet24		:= { || NIL }
Local bDialogInit	:= { || NIL }
Local bGdRdtSeek	:= { || NIL }
Local bSvblDblClick := { || NIL }
Local bGetRds		:= { || NIL } 
Local bGetRdt		:= { || NIL }
Local bRdtSort		:= { || NIL }

Local cFilRDS		:= ""
Local cCodRDS		:= ""
Local cRDSKeySeek	:= ""

Local lLocks		:= .F.
Local lExecLock		:= ( ( nOpc <> 2 ) .and. ( nOpc <> 3 ) )

Local nOpcAlt		:= 0
Local nRDSUsado		:= 0
Local nRDTUsado		:= 0
Local nLoop			:= 0
Local nLoops		:= 0
Local nOpcNewGd		:= IF( ( ( nOpc == 2 ) .or. ( nOpc == 5 ) ) , 0 , GD_INSERT + GD_UPDATE + GD_DELETE	)
Local nRdtGhostCol	:= 0

Local oDlg			:= NIL
Local oEnchoice		:= NIL
Local oGetDados		:= NIL

Private aGets
Private aTela

Private nGetSX8Len	:= GetSX8Len()

DEFAULT lGetCriter	:= .F.

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Coloca o ponteiro do Cursor do Mouse em Estado de Espera     Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorWait()

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁCheca a Opcao Selecionada									   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aRotSetOpc( cAlias , @nReg , nOpc )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta os Dados para a Enchoice							   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aRdsNotFields	:= { "RDS_FILIAL" }
		bGetRds			:= { |lLock,lExclu|	IF( lExecLock , ( lLock := .T. , lExclu	:= .T. ) , aRdsKeys := NIL ),;
											aRdsCols := RDS->(;
																GdBuildCols(	@aRdsHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
																				@nRdsUsado		,;	//02 -> Numero de Campos em Uso
																				@aRdsVirtEn		,;	//03 -> [@]Array com os Campos Virtuais
																				@aRdsVisuEn		,;	//04 -> [@]Array com os Campos Visuais
																				"RDS"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
																				aRdsNotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
																				@aRdsRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
																				"RDS"		   	,;	//08 -> Alias do Arquivo Pai
																				NIL				,;	//09 -> Chave para o Posicionamento no Alias Filho
																				NIL				,;	//10 -> Bloco para condicao de Loop While
																				NIL				,;	//11 -> Bloco para Skip no Loop While
																				NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols 
																				NIL				,;	//13 -> Se Sera considerado o Inicializador Padrao
																				NIL				,;	//14 -> Opcional, Carregar Todos os Campos
																				NIL				,;	//15 -> Opcional, Nao Carregar os Campos Virtuais
																				NIL				,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
																				NIL				,;	//17 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
																				NIL				,;	//18 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
																				NIL				,;	//19 -> Carregar Coluna Fantasma
																				NIL				,;	//20 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
																				NIL				,;	//21 -> Verifica se Deve Checar se o campo eh usado
																				NIL				,;	//22 -> Verifica se Deve Checar o nivel do usuario
																				NIL				,;	//23 -> Verifica se Deve Carregar o Elemento Vazio no aCols
																				@aRdsKeys		,;	//24 -> [@]Array que contera as chaves conforme recnos
																				@lLock			,;	//25 -> [@]Se devera efetuar o Lock dos Registros
																				@lExclu			 ;	//26 -> [@]Se devera obter a Exclusividade nas chaves dos registros
																		    );
															  ),;
											IF( lExecLock , ( lLock .and. lExclu ) , .T. );
		  					} 
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁLock do Registro do RDS									   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF !( lLocks := WhileNoLock( "RDS" , NIL , NIL , 1 , 1 , .T. , 1 , 5 , bGetRds ) )
			Break
		EndIF
		CursorWait()		
		aSvRdsCols		:= aClone( aRdsCols )
		cFilRDS			:= RDS->RDS_FILIAL
		cCodRDS			:= RDS->RDS_CODIGO
		cRDSKeySeek		:= ( cFilRDS + cCodRDS )
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Cria as Variaveis de Memoria e Carrega os Dados Conforme o arЁ
		Ё quivo														   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		For nLoop := 1 To nRDSUsado
			aAdd( aRdsFields , aRdsHeader[ nLoop , 02 ] )
			SetMemVar( aRdsHeader[ nLoop , 02 ] , aRdsCols[ 01 , nLoop ] , .T. )
		Next nLoop
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁDefine os Campos Editaveis na Enchoice Apenas na Inclusao( 3 )Ё
		Ёou Alteracao(4)											   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF (;
				( nOpc == 3 );
				.or.;
				( nOpc == 4 );
			)

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Carrega Array com os Relacionamentos com o RD0			   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			BldArrRdZRel( @aRelation )

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define os Campos Editaveis								   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			nLoops := Len( aRdsVisuEn )
			For nLoop := 1 To nLoops
				aAdd( aRdsNaoAlt , aRdsVisuEn[ nLoop ] )
			Next nLoop
			IF ( nOpc == 4 )
				aAdd( aRdsNaoAlt , "RDS_CODIGO" )
			EndIF
			nLoops := Len( aRdsFields )
			For nLoop := 1 To nLoops
				IF ( aScan( aRdsNaoAlt , { |cNaoA| cNaoA == aRdsFields[ nLoop ] } ) == 0 )
					aAdd( aRdsAltera , aRdsFields[ nLoop ] )
				EndIF
			Next nLoop
		
		EndIF
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define os Campos que nao serao Carregados					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aAdd( aRdtNotFields , "RDT_FILIAL"  )
		aAdd( aRdtNotFields , "RDT_CODIGO"	)
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Quando For Inclusao Posiciona o RDT No Final do Arquivo	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( nOpc == 3  ) //Inclusao
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Garante que na Inclusao o Ponteiro do RDT estara em Eof()    Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			PutFileInEof( "RDT" )
		EndIF
		bGetRdt	:= { |lLock,lExclu| IF( lExecLock , ( lLock := .T. , lExclu := .T. ) , aRdtKeys := NIL ),;
							 			aRdtCols := RDT->(;
															GdBuildCols(	@aRdtHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
																			@nRdtUsado		,;	//02 -> Numero de Campos em Uso
																			@aRdtVirtGd		,;	//03 -> [@]Array com os Campos Virtuais
																			@aRdtVisuGd		,;	//04 -> [@]Array com os Campos Visuais
																			"RDT"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
																			aRdtNotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
																			@aRdtRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
																			"RDS"		   	,;	//08 -> Alias do Arquivo Pai
																			cRDSKeySeek		,;	//09 -> Chave para o Posicionamento no Alias Filho
																			NIL				,;	//10 -> Bloco para condicao de Loop While
																			NIL				,;	//11 -> Bloco para Skip no Loop While
																			NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols 
																			NIL				,;	//13 -> Se Sera considerado o Inicializador Padrao
																			NIL				,;	//14 -> Opcional, Carregar Todos os Campos
																			NIL				,;	//15 -> Opcional, Nao Carregar os Campos Virtuais
																			NIL				,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
																			.T.				,;	//17 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
																			.T.				,;	//18 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
																			Altera			,;	//19 -> Carregar Coluna Fantasma
																			NIL				,;	//20 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
																			NIL				,;	//21 -> Verifica se Deve Checar se o campo eh usado
																			NIL				,;	//22 -> Verifica se Deve Checar o nivel do usuario
																			NIL				,;	//23 -> Verifica se Deve Carregar o Elemento Vazio no aCols
																			@aRdtKeys		,;	//24 -> [@]Array que contera as chaves conforme recnos
																			@lLock			,;	//25 -> [@]Se devera efetuar o Lock dos Registros
																			@lExclu			,;	//26 -> [@]Se devera obter a Exclusividade nas chaves dos registros
																			NIL				,;	//27 -> Numero maximo de Locks a ser efetuado
																			Altera			 ;	//28 -> Utiliza Numeracao na GhostCol
																	    );
															  ),;
										IF( lExecLock , ( lLock .and. lExclu ) , .T. );
		  		    }
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁLock do Registro do RDT									   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF !( lLocks := WhileNoLock( "RDT" , NIL , NIL , 1 , 1 , .T. , 1 , 5 , bGetRdt ) )
			Break
		EndIF
		CursorWait()

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁMonta Bloco para Sort antes da Gravacao					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( ( nRdtGhostCol := GdFieldPos( "GHOSTCOL" , aRdtHeader ) ) > 0 )
			bRdtSort := { |x,y| ( x[ nRdtGhostCol ] < y[ nRdtGhostCol ] ) }
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Salva o Conteudo do aRdtCols para comparacao na saida		   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aSvRdtCols	:= aClone( aRdtCols )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega os Campos Editaveis para a GetDados				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		For nLoop := 1	To nRDTUsado
			SetMemVar( aRdtHeader[ nLoop , 02 ] , GetValType( aRdtHeader[ nLoop , 08 ] , aRdtHeader[ nLoop , 04 ] ) , .T. )
			IF (;
					(;
						( aScan( aRdtVirtGd		, aRdtHeader[ nLoop , 02 ] ) == 0 );
						.and.;
			   			( aScan( aRdtVisuGd		, aRdtHeader[ nLoop , 02 ] ) == 0 );
			   			.and.;
			   			( aScan( aRdtNotFields	, aRdtHeader[ nLoop , 02 ] ) == 0 );
			   			.and.;
			   			( aScan( aRdtGdNaoAlt	, aRdtHeader[ nLoop , 02 ] ) == 0 );
			   		);
			  	)
				aAdd( aRdtGdAltera , aRdtHeader[ nLoop , 02 ] )
			EndIF			   
		Next nLoop

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁSe Esta apenas obtendo os criterios, abandona   			   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( lGetCriter )
			Break
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁVerifica se Pode Efetuar a Delecao dos Registros			   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( nOpc == 5 ) 
			IF !( ApdChkDel( cAlias , nReg , nOpc , cCodRDS , .T. , NIL , NIL , { "RDT" } ) )
				Break
			EndIF
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta as Dimensoes dos Objetos         					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		DEFAULT lDlgPadSiga	:= .F.
		aAdvSize		:= MsAdvSize( NIL , lDlgPadSiga )
		aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 2 , 2 }
		aAdd( aObjCoords , { 000 , 040 , .T. , .F. } )
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
		aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Botao de Pesquisa na GetDados					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		bGdRdtSeek := { ||	GdRdtSeek( oGetDados )			,;
							SetKey( VK_F4 , bGdRdtSeek )	 ;
				   }
		aAdd(;
				aButtons	,;
								{;
									"pesquisa",;
		   							bGdRdtSeek,;
		       	   					OemToAnsi( STR0001 + "...<F4>" ),;	//"Pesquisar"
		       	   					OemToAnsi( STR0001			   );	//"Pesquisar"
		           				};
		     )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Bloco para a Tecla <CTRL-O> 						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		bSet15		:= { || IF(; 
									(;
										( nOpc == 3 );	//Inclusao
										.or.;
										( nOpc == 4 );	//Alteracao
									);			
									.and.;
									Apda160TEncOk( nOpc , oEnchoice );				//Valida Todos os Campos da Enchoice
									.and.;
									oGetDados:TudoOk(),;							//Valida as Informacoes da GetDados
									(;
										nOpcAlt 	:= 1 ,;
										aRdtCols	:= oGetDados:aCols,;			//Redireciona o Ponteiro do aRdtCols
										RestKeys( aSvKeys , .T. ),;
										oDlg:End();
								 	),;
								 	IF(; 
								 		(;
								 			( nOpc == 3 );	//Inclusao
								 			.or.;
								 			( nOpc == 4 );	//Alteracao
								 		),;		
								 			(;
								 				nOpcAlt := 0 ,;
								 				.F.;
								 			 ),;	
										(;
											nOpcAlt := IF( nOpc == 2 , 0 , 1 ) ,;	//Visualizacao ou Exclusao
											RestKeys( aSvKeys , .T. ),;
											oDlg:End();
								 		);
								 	  );
							   );
						 }
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Bloco para a Teclas <CTRL-X>     	   			   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		bSet24		:= { || ( nOpcAlt := 0 , RestKeys( aSvKeys , .T. ) , oDlg:End() ) }
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Bloco para o Init do Dialog						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		bDialogInit := { ||;
								EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons ),;
								SetKey( VK_F4 , bGdRdtSeek  ),;
						}
		
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Restaura o Ponteiro do Cursor do Mouse                  	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorArrow()
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta o Dialogo Principal para a Manutencao das Formulas	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0007 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF GetWndDefault() PIXEL

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto Enchoice para o RDS                      	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oEnchoice	:= MsmGet():New(	cAlias		,;
										nReg		,;
										nOpc		,;
										NIL			,;
										NIL			,;
										NIL			,;
										aRdsFields	,;
										aObjSize[1]	,;
										aRdsAltera	,;
										NIL			,;
										NIL			,;
										NIL			,;
										oDlg		,;
										NIL			,;
										.F.			,;
										NIL			,;
										.F.			 ;
									)
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto GetDados para o RD4						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oGetDados	:= MsNewGetDados():New(	aObjSize[2,1]							,;
											aObjSize[2,2]								,;
											aObjSize[2,3]								,;
											aObjSize[2,4]								,;
											nOpcNewGd									,;
											"RdtGdLinOk"								,;
											"RdtGdTudOk"								,;
											""											,;
											aRdtGdAltera								,;
											0											,;
											Max( ( Len( aRelation ) + 1 ) , 1 )	,;
											NIL											,;
											NIL											,;
											bRdtGdDelOk								,;
											oDlg										,;
											aRdtHeader									,;
											aRdtCols									 ;
										  )
		
		bSvblDblClick := oGetDados:oBrowse:blDblClick
		oGetDados:oBrowse:blDblClick := { || Apda160EdtMem( oGetDados , bSvblDblClick , ( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) ) }
	
	ACTIVATE MSDIALOG oDlg ON INIT Eval( bDialogInit ) CENTERED

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Coloca o Ponteiro do Mouse em Estado de Espera			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorWait()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁQuando Confirmada a Opcao e Nao for Visualizacao Grava ou   ExЁ
	Ёclui as Informacoes do RDS e RDT							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF( nOpcAlt == 1 )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Apenas se nao For Visualizacao              				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
 		IF ( nOpc != 2 )
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Gravando/Incluido ou Excluindo Informacoes do SRY/RDT        Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			MsAguarde(;
						{ ||;
								aSort( aRdtCols , NIL , NIL , bRdtSort ),;	//Sorteia as Informacoes do RDT para Comparacao Antes da Gravacao
								Apda160Grava(;
												nOpc		,;	//Opcao de Acordo com aRotina
							 					nReg		,;	//Numero do Registro do Arquivo Pai ( RDS )
							 					aRdsHeader	,;	//Campos do Arquivo Pai ( RDS )
							 					aRdsCols	,;	//Conteudo Atual dos Campos do Arquivo Pai ( RDS )
							 					aSvRdsCols	,;	//Conteudo Anterior dos Campos do Arquivo Pai ( RDS )
							 					aRdsVirtEn	,;	//Campos Virtuais do Arquivo Pai ( RDS )
							 					aRdtHeader	,;	//Campos do Arquivo Filho ( RDT )
							 					aRdtCols	,;	//Itens Atual do Arquivo Filho ( RDT )
							 					aSvRdtCols	,;	//Itens Anterior do Arquivo Filho ( RDT )
							 					aRdtVirtGd	,;	//Campos Virtuais do Arquivo Filho ( RDT )
							 					aRdtRecnos	 ;	//Recnos do Arquivo Filho ( RDT )
							 				 );
						 };
					 )
		EndIF
	Else
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё RollBack da Numeracao Automatica            				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		While ( GetSX8Len() > nGetSX8Len )
			RollBackSX8()
		End While
	EndIF

End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁColoca o Ponteiro do Mouse em Estado de Espera			   	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁLibera os Locks             								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
aAdd( aFreeLocks , { "RDS" , aRdsRecnos , aRdsKeys } )
aAdd( aFreeLocks , { "RDT" , aRdtRecnos , aRdtKeys } )
ApdFreeLocks( aFreeLocks )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura os Dados de Entrada								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aArea )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura as Teclas de Atalho								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Ponteiro do Cursor do Mouse                  	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( IF( lGetCriter , { aRdsHeader , aRdsCols , aRdtHeader , aRdtCols } , nOpcAlt ) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda160TEncOkЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁTudoOk para a Enchoice                                      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁApda160TEncOk( nOpc , oEnchoice )							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apda160TEncOk( nOpc , oEnchoice )

Local lTudoOk := .T.
                
IF (;
		( nOpc == 3 );
		.or.;
		( nOpc == 4 );
	 )
	lTudoOk := EnchoTudOk( oEnchoice )
EndIF

Return( lTudoOk )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtGdLinOk	ЁAutorЁMarinaldo de Jesus     Ё Data Ё20/03/2004Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRdtGdLinOk( oBrowse )									    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdtGdLinOk( oBrowse )

Local aCposKey	:= {}
Local lLinOk	:= .T.

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	Begin Sequence

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Se a Linha da GetDados Nao Estiver Deletada				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF !( GdDeleted() )

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica Itens Duplicados na GetDados						   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			aCposKey := GetArrUniqe( "RDT" )
			IF !( lLinOk := GdCheckKey( aCposKey , 4 ) )
				Break
			EndIF

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica Se o Campos Estao Devidamente Preenchidos		   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			aCposKey := GdObrigat( aHeader )
			IF !( lLinOk := GdNoEmpty( aCposKey ) )
		    	Break
			EndIF

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Valiar o Criterio                                 		   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF !( lLinOk := GpVldFilter( GdFieldGet( "RDT_CRITER" ) , GdFieldGet( "RDT_ALIAS" ) ) )
				Break
			EndIF
	
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁReinicializa as Variaveis                                     Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SetMemVar( "RDT_ALIAS"	, Space( GetSx3Cache( "RDT_ALIAS" , "X3_TAMANHO" ) ) ) 
		SetMemVar( "RDT_CRITER"	, Space( GetSx3Cache( "RDT_CRITER" , "X3_TAMANHO" ) ) )		 
		SetMemVar( "RDT_DESCCT"	, Space( GetSx3Cache( "RDT_DESCCT" , "X3_TAMANHO" ) ) ) 
		
	End Sequence
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe Houver Alguma Inconsistencia na GetDados, Seta-lhe o Foco  Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( lLinOk )
		oBrowse:SetFocus()
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Garante que na Inclusao o Ponteiro do RDT estara em Eof()    Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	PutFileInEof( "RDT" )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()
	
Return( lLinOk )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtGdTudOk	ЁAutorЁMarinaldo de Jesus     Ё Data Ё20/03/2004Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRdtGdTudOk( oBrowse )									    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdtGdTudOk( oBrowse )

Local lTudoOk := .T.

Local nLoop
Local nLoops

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	Begin Sequence
	
	    /*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Percorre Todas as Linhas para verificar se Esta Tudo OK      Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		nLoops := Len( aCols )
		For nLoop := 1 To nLoops
			n := nLoop
			IF !( lTudoOk := RdtGdLinOk( oBrowse ) )
				oBrowse:Refresh()
				Break
			EndIF
		Next nLoop

	End Sequence
	
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( lTudoOk  )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtGdDelOk   ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValidar a Delecao na GetDados                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRdtGdDelOk( cAlias , nRecno , nOpc , cCodigo )				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function RdtGdDelOk( cAlias , nRecno , nOpc )
         
Local lDelOk 		:= .T.
Local lStatusDel	:= .F.

Static lFirstDelOk
Static lLstDelOk

DEFAULT lFirstDelOk	:= .T.
DEFAULT lLstDelOk	:= .T.

Begin Sequence

	//Quando for Visualizacao ou Exclusao Abandona
	IF (;
			( nOpc == 2 );	//Visualizacao
			.or.;
			( nOpc == 5 );	//Exclusao
		)
		Break
	EndIF

	//Apenas se for a primeira vez
	IF !( lFirstDelOk )
		lFirstDelOk	:= .T.
		lDelOk 		:= lLstDelOk
		lLstDelOk	:= .T.
		Break
	EndIF

	lStatusDel	:= !( GdDeleted() ) //Inverte o Estado
	
	IF ( lStatusDel )	//Deletar
		lLstDelOk := lDelOk
		//Ja Passou pela funcao
		lFirstDelOk := .F.
    	Break
	Else				//Restaurar
   		lLstDelOk := lDelOk
		//Ja Passou pela funcao
		lFirstDelOk := .F.
   		Break
	EndIF

	//Ja Passou pela funcao
	lFirstDelOk := .F.

End Sequence
	
Return( lDelOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda160Grava ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apda160Grava(	nOpc		,;	//Opcao de Acordo com aRotina
							 	nReg		,;	//Numero do Registro do Arquivo Pai ( RDS )
							 	aRdsHeader	,;	//Campos do Arquivo Pai ( RDS )
							 	aRdsCols	,;	//Conteudo Atual dos Campos do Arquivo Pai ( RDS )
							 	aSvRdsCols	,;	//Conteudo Anterior dos Campos do Arquivo Pai ( RDS )
							 	aRdsVirtEn	,;	//Campos Virtuais do Arquivo Pai ( RDS )
							 	aRdtHeader	,;	//Campos do Arquivo Filho ( RDT )
							 	aRdtCols	,;	//Itens Atual do Arquivo Filho ( RDT )
							 	aSvRdtCols	,;	//Itens Anterior do Arquivo Filho ( RDT )
							 	aRdtVirtGd	,;	//Campos Virtuais do Arquivo Filho ( RDT )
							 	aRdtRecnos	 ;	//Recnos do Arquivo Filho ( RDT )
							  )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis de Inicializacao Obrigatoria					  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local aMestre		:= GdPutIStrMestre( 01 )
Local aItens		:= {}

Local cOpcao		:= IF( ( nOpc == 5 ) , "DELETE" , IF( ( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) , "PUT" , NIL ) )

Local lAllModif		:= .F.
Local lRdsModif		:= .F.
Local lRdtModif		:= .F.
Local lRdtDelet		:= .F.

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis que serao inicializadas no Corpo da Funcao		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local aRdtColDel
Local aRdtRecDel

Local nLoop
Local nLoops
Local nItens

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	IF ( cOpcao <> "DELETE" )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica se Houveram Modificacoes no RDV					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( lRdtModif := !ArrayCompare( aRdtCols , aSvRdtCols ) )
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Deleto todos os Itens que nao Estao OK					   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			GdSuperDel( aRdtHeader , @aRdtCols , NIL , .T. , GdGetBlock( "RDT" , aRdtHeader , .F. ) ) 
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Separa os Itens que foram Deletados     					   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			lRdtDelet := GdSplitDel( aRdtHeader , @aRdtCols , @aRdtRecnos , @aRdtColDel , @aRdtRecDel  )
		EndIF
	Else
		lRdtModif := .T.
		lRdsModif := .T.
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carrega os Itens Apenas se Houveram Alteracoes ou na ExclusaoЁ
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( lRdtModif )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega as Informacoes deletadas RD4                   	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( lRdtDelet )

			aAdd( aItens , GdPutIStrItens() )
			nItens := Len( aItens )
		
			aItens[ nItens , 01 ] := "RDT"
			aItens[ nItens , 02 ] := NIL
			aItens[ nItens , 03 ] := aClone( aRdtHeader )
			aItens[ nItens , 04 ] := aClone( aRdtColDel )
			aItens[ nItens , 05 ] := aClone( aRdtVirtGd )
			aItens[ nItens , 06 ] := aClone( aRdtRecDel )

		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega as Informacoes do RD4                   			   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aAdd( aItens , GdPutIStrItens() )
		nItens := Len( aItens )

		aItens[ nItens , 01 ] := "RDT"
		aItens[ nItens , 02 ] := {;
									{ "FILIAL" , xFilial( "RDT" , xFilial( "RDS" ) ) },;
									{ "CODIGO" , GetMemVar( "RDS_CODIGO" ) };
							 	}  	
		aItens[ nItens , 03 ] := aClone( aRdtHeader )
		aItens[ nItens , 04 ] := aClone( aRdtCols   )
		aItens[ nItens , 05 ] := aClone( aRdtVirtGd )
		aItens[ nItens , 06 ] := aClone( aRdtRecnos )

	EndIF
	
	IF !( lRdsModif )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Atualiza aRdsCols para Verificar se Houveram Alteracoes	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		nLoops := Len( aRdsHeader )
		For nLoop := 1 To nLoops
			aRdsCols[ 01 , nLoop ] := GetMemVar( aRdsHeader[ nLoop , 02 ] )
		Next nLoop
		lRdsModif := !( ArrayCompare( aRdsCols , aSvRdsCols ) )
	EndIF	

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Seta a Gravacao ou Exclusao Apenas se Houveram Alteracoes  ouЁ
	Ё se foi Selecionada a Exclusao								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
 	lAllModif := ( ( lRdtModif ) .or. ( lRdsModif ) )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se Houveram Alteracoes, ou Exclusao efetua a Gravacao        Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( lAllModif )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Seta a Gravacao ou Exclusao Apenas se Houveram Alteracoes  ouЁ
		Ё se foi Selecionada a Exclusao								   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aMestre[ 01 , 01 ]	:= "RDS"
		aMestre[ 01 , 02 ]	:= nReg
		aMestre[ 01 , 03 ]	:= lRdsModif
		aMestre[ 01 , 04 ]	:= aClone( aRdsHeader )
		aMestre[ 01 , 05 ]	:= aClone( aRdsVirtEn )
		aMestre[ 01 , 06 ]	:= {}
		aMestre[ 01 , 07 ]	:= aClone( aItens )
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Grava as Informacoes                        				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		GdPutInfoData( aMestre , cOpcao , .F. , .F. )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Confirmando a Numeracao Automatica          				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		While ( GetSX8Len() > nGetSX8Len )
			ConfirmSX8()
		End While

	EndIF
	
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( NIL )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda160EdtMem	 ЁAutorЁMarinaldo de JesusЁ Data Ё20/03/2004Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁEdit de Campo Memo                       					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁoGetDados:oBrowse:blDblClick								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apda160EdtMem( oGetDados , blDblClick , lModify , cRdtCriter )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis de Inicializacao Obrigatoria					  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local nColRdtCriter	:= GdFieldPos( "RDT_CRITER" , oGetDados:aHeader )
Local nColRdtAlias	:= GdFieldPos( "RDT_ALIAS"	, oGetDados:aHeader )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis que serao inicializadas no Corpo da Funcao        Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local aSvKeys

Local cTitCompl
Local cRdtAlias
Local cSvRdtCriter

Local lConfOk

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se a Coluna Posicionada nao for a Coluna do Memo             Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF (;
			!( oGetDados:oBrowse:nColPos == nColRdtCriter );
			.or.;
			( nColRdtAlias == 0 );
		)	
		GdRstDblClick( oGetDados , blDblClick )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Obtem a Entidade                                             Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cRdtAlias	:= GdFieldGet( "RDT_ALIAS" , oGetDados:oBrowse:nAt , .F. , oGetDados:aHeader , oGetDados:aCols )
	SetMemVar( "RDT_ALIAS" , cRdtAlias )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Obtem o Memo                                                 Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF Empty( cRdtCriter )
		cRdtCriter := GdFieldGet( "RDT_CRITER" , oGetDados:oBrowse:nAt , .F. , oGetDados:aHeader , oGetDados:aCols )
	EndIF	
	cSvRdtCriter := cRdtCriter

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos                               Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdvSize		:= MsAdvSize( .T. , .T. )
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
	aObjCoords		:= { { 0 , 0 , .T. , .T. } }
	aObjSize := MsObjSize( aInfoAdvSize , aObjCoords )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Salva as Teclas de Atalho                                    Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aSvKeys := GetKeys()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carrega o Complemento do Titulo                              Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cTitCompl := AllTrim( oGetDados:aHeader[ GdFieldPos( "RDT_CRITER" , oGetDados:aHeader ) , 01 ] )
	cTitCompl += " -> "
	cTitCompl += oGetDados:aHeader[ GdFieldPos( "RDT_ALIAS"  , oGetDados:aHeader ) , 01 ]
	cTitCompl += ": "
	cTitCompl += AllTrim( GdFieldGet( "RDT_ALIAS" , oGetDados:oBrowse:nAt , .F. , oGetDados:aHeader , oGetDados:aCols ) )
	cTitCompl += " / "
	cTitCompl += AllTrim( oGetDados:aHeader[ GdFieldPos( "RDT_CRITER" , oGetDados:aHeader ) , 01 ] )
	cTitCompl += ": "
	cTitCompl += AllTrim( GdFieldGet( "RDT_CRITER" , oGetDados:oBrowse:nAt , .F. , oGetDados:aHeader , oGetDados:aCols ) )
	cTitCompl := ( cCadastro + OemToAnsi( " - " + cTitCompl ) )

	IF ( lModify )
		lConfOk	:= GpFltBldExp( cRdtAlias , oGetDados:oBrowse:oWnd , @cRdtCriter , cTitCompl )
		IF ( lConfOk )
			GdFieldPut( "RDT_CRITER" , cRdtCriter , oGetDados:oBrowse:nAt , oGetDados:aHeader , oGetDados:aCols , .F. )
			SetMemVar( "RDT_CRITER" , cRdtCriter )
			Private n		:= oGetDados:oBrowse:nAt
			Private aHeader := oGetDados:aHeader
			Private aCols	:= oGetDados:aCols
			RdtDescCtInit( cRdtAlias , cRdtCriter , .T. )
		ElseIF !( lConfOk )
			IF !( cSvRdtCriter == cRdtCriter )
				//"Abandonar as altera┤■es?"
				IF !( MsgNoYes( OemToAnsi( STR0018 ) , cCadastro ) )
					Apda160EdtMem( oGetDados , blDblClick , lModify , @cRdtCriter )
				EndIF
			EndIF	
		EndIF
	EndIF
	
End Sequence

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGdRdtSeek    ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁEfetuar Pesquisa na GetDados                                Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA160()                                                   Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function GdRdtSeek( oGetDados )

Local aSvKeys		:= GetKeys()

Local cProcName3	:= Upper( AllTrim( ProcName( 3 ) ) )
Local cProcName5	:= Upper( AllTrim( ProcName( 5 ) ) )

Begin Sequence

	IF !( "APDA160MNT" $ ( cProcName3 + cProcName5 ) )
		Break
	EndIF

	GdSeek( oGetDados , OemToAnsi( STR0001 ) )	//"Pesquisar"

End Sequence

RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdsCodigoVld ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDS_CODIGO          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDS_CODIGO                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdsCodigoVld()

Local cRdsCodigo	:= GetMemVar( "RDS_CODIGO" )

Local lRdsCodigoOk	:= .T.

Begin Sequence

	IF !( lRdsCodigoOk := RdsGetCodigo( @cRdsCodigo , .F. , .T. ) )
    	Break
    EndIF

	SetMemVar( "RDS_CODIGO" , cRdsCodigo )

End Sequence

Return( lRdsCodigoOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdsGetCodigo ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁObtem Numeracao Valida para o RDS_CODIGO                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁObter Numeracao valida para o RDS_CODIGO                 	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdsGetCodigo( cRdsCodigo , lExistChav , lShowHelp )
Return(;
			GetNrExclOk(	@cRdsCodigo 			,;
							"RDS"					,;
							"RDS_CODIGO"			,;
							"RDS_FILIAL+RDS_CODIGO" ,;
							NIL						,;
							lExistChav				,;
							lShowHelp	 			 ;
						);
		)

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdsCodigoInitЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializador Padrao para o Campo RDS_CODIGO                Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RDS_CODIGO                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdsCodigoInit()
Local cRdsCodigo
RdsGetCodigo( @cRdsCodigo , .F. , .F. )
Return( cRdsCodigo )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdsDescVld 	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDS_DESC          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDS_DESC	                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdsDescVld()

Local lRdsDescOk := .T.

Begin Sequence

	IF !( lRdsDescOk := NaoVazio() )
		Break
	EndIF

	IF !( lRdsDescOk := Texto() )
    	Break
    EndIF

End Sequence

Return( lRdsDescOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtCodigoVld ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDT_CODIGO          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDT_CODIGO                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdtCodigoVld()

Local lRdtCodigoOk := .T.

Begin Sequence

	IF !( lRdtCodigoOk := NaoVazio() )
		Break
	EndIF

	IF !( lRdtCodigoOk := ExistCpo("RDS") )
    	Break
    EndIF

End Sequence

Return( lRdtCodigoOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtItemVld 	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDT_ITEM          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDT_ITEM                         		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdtItemVld()

Local lRdtItemOk := .T.

Begin Sequence

	IF !( lRdtItemOk := NaoVazio() )
		Break
	EndIF

	IF !( lRdtItemOk := ExistChav( "RDT" , GetMemVar("RDT_CODIGO") + GetMemVar("RDT_ITEM") ) )
    	Break
    EndIF

End Sequence

Return( lRdtItemOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtDescAlInitЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDT_ALIAS          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDT_ALIAS                        		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdtAliasVld()

Local cReadVar		:= ReadVar()
Local cCntReadVar	:= __ExecMacro( cReadVar )

Local lRdtAliasVld	:= .T.

Local aRelation
Local nPos
//A consulta pelo SRA estarА disponivel apenas para o modulo de Terminal de Consulta do Funcionario SIGATCF
Local lModTCF		:= (nModulo == 18)

Begin Sequence

	IF ( lRdtAliasVld := If( (lModTCF .AND. GetRpoRelease("R7")) , ( cCntReadVar == "RD0" ) .OR. ( cCntReadVar == "SRA" ), ( cCntReadVar == "RD0" )  ) )
		Break
	EndIF

	IF !( lRdtAliasVld := BldArrRdZRel( @aRelation ) )
		Break
	EndIF

	nPos := aScan( aRelation , { |x| x[1] == cCntReadVar } )
	IF !( lRdtAliasVld := ( nPos > 0 ) )
		Break
	EndIF

End Sequence

Return( lRdtAliasVld )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtDescAlInitЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDT_ALIAS           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDT_ALIAS                             Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdtAliasBox()

Local aRelation

Local cRdtAliasBox

Local nLoop
Local nLoops

cRdtAliasBox := "RD0=" + fDesc( "SX2" , "RD0" , "X2Nome()" , 30 , "" , 1 , .F. )

//A consulta pelo SRA estarА disponivel apenas para o modulo de Terminal de Consulta do Funcionario SIGATCF
If (nModulo == 18 .AND. GetRpoRelease("R7"))
    cRdtAliasBox += ";SRA=" + fDesc( "SX2" , "SRA" , "X2Nome()" , 30 , "" , 1 , .F. )
else
    IF ( BldArrRdZRel( @aRelation ) )
    	cRdtAliasBox += ";"
    	IF ( ( nLoops := Len( aRelation ) ) > 0 )
    		For nLoop := 1 To nLoops
    			cRdtAliasBox += aRelation[ nLoop , 01 ] + "=" + fDesc( "SX2" , aRelation[ nLoop , 01 ] , "X2Nome()" , 30 , "" , 1 , .F. )
    			IF ( nLoop <= nLoops )
    				cRdtAliasBox += ";"
    			EndIF
    		Next nLoop
    	EndIF
    EndIF
EndIf

Return( cRdtAliasBox )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtDescAlInitЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Inicializar o Conteudo do campo RDT_DESCAL      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RDT_DESCAL                          Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdtAliasInit()
cRetTab := "RD0"
If (nModulo == 18 .AND. GetRpoRelease("R7"))
   cRetTab := ""
EndIF

Return( cRetTab )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtDescAlInitЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Inicializar o Conteudo do campo RDT_DESCCT      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RDT_DESCCT                          Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdtDescCtInit( cRdtAlias , cRdtCriter , lGdFieldPut )

Local cRdtDescCtInit	:= ""

Local lGetDados			:= .F.

DEFAULT lGdFieldPut := .F.

lGetDados := IsInGetDados( { "RDT_DESCCT" , "RDT_CRITER" , "RDT_ALIAS" } )

IF ( lGetDados )
	IF ( IsMemVar( "RDT_ALIAS" ) )
		DEFAULT cRdtAlias := GetMemVar( "RDT_ALIAS" )
	Else
		DEFAULT cRdtAlias := GdFieldGet( "RDT_ALIAS" )
	EndIF
	IF ( IsMemVar( "RDT_CRITER" ) )
		DEFAULT cRdtCriter := GetMemVar( "RDT_CRITER" )
	Else
		DEFAULT cRdtCriter := GdFieldGet( "RDT_CRITER" )
	EndIF
	cRdtDescCtInit	:= GpFltBldDesc( cRdtCriter , NIL , cRdtAlias )
	IF ( lGdFieldPut )
		GdFieldPut( "RDT_DESCCT" , cRdtDescCtInit )
	EndIF
	SetMemVar( "RDT_DESCCT" , cRdtDescCtInit )
Else
	DEFAULT cRdtAlias	:= RDT->RDT_ALIAS
	DEFAULT cRdtCriter	:= RDT->RDT_CRITER
	cRdtDescCtInit		:= GpFltBldDesc( cRdtCriter , NIL , cRdtAlias )
EndIF

Return( If(cRdtDescCtInit==NIL,"",cRdtDescCtInit) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdtDescCtBrw ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Inicializar o Browse do campo RDT_DESCCT        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_INIBRW para o campo RDT_DESCCT                           Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdtDescCtBrw()
Return( RdtDescCtInit() )

/*                                	
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef        ЁAutorЁ  Luiz Gustavo     Ё Data Ё21/12/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas bibliotecas Framework da Versao 9.12 .      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >                                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁAPDA160                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >                                 Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/   

Static Function MenuDef()

 Local aRotina := {;
								{ STR0001 , "AxPesqui"	 , 0 , 01,,.F.} ,; //"Pesquisar"
								{ STR0002 , "Apda160Mnt" , 0 , 02 } ,; //"Visualizar"
								{ STR0003 , "Apda160Mnt" , 0 , 03 } ,; //"Incluir"
								{ STR0004 , "Apda160Mnt" , 0 , 04 } ,; //"Alterar"
								{ STR0005 , "Apda160Mnt" , 0 , 05 }  ; //"Excluir"
							}
Return aRotina

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    Ё RdsModInit  ЁAutorЁEmerson Campos        Ё Data Ё26/04/2012Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Inicializar o Browse do campo com o modulo      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_INIBRW para o campo RDT_MODULO                           Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdsModInit() 
// Retorna o modulo de acesso para o cadastro de criterios
Return nModulo