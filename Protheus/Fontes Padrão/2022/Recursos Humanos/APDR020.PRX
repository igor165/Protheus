#INCLUDE "APDR020.CH"
#INCLUDE "Protheus.ch"
#DEFINE CHRCOMP If(aReturn[4]==1,15,18)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ APDR020  ³ Autor ³ Eduardo Ju            ³ Data ³ 19/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio Comparativo Global                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ APDR020                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cecilia Car.³04/07/14³TPZWHD³Incluido o fonte da 11 para a 12 e efetua-³±±
±±³            ³        ³      ³da a limpeza.                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function APDR020()

Local oReport
Local aArea := GetArea()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte("APD20R",.F.)
oReport := ReportDef()
oReport:PrintDialog()	
RestArea( aArea )

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef() ³ Autor ³ Eduardo Ju          ³ Data ³ 19.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Definicao do Componente de Impressao do Relatorio           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

Local oReport
Local oSection1
Local oSection2
Local oSection3	
Local oSection4
Local oSection5

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:=TReport():New("APDR020",STR0001,"APD20R",{|oReport| PrintReport(oReport)},(STR0001+STR0027))	//"Comparativo Global"#"Será impresso de acordo com os parametros solicitados pelo usuario"
Pergunte("APD20R",.F.) 
oReport:SetTotalInLine(.F.) //Totaliza em linha 
oReport:SetLandscape(.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ'ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Primeira Secao: Avaliacao ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
oSection1 := TRSection():New(oReport,STR0004,{"RDB","RD6","RD5","RD3","RDK"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Avaliacao"
oSection1:SetTotalInLine(.F.)    
oSection1:SetHeaderBreak(.T.)

TRCell():New(oSection1,"RDB_CODAVA","RDB",STR0004)	//Codigo da Avaliacao
TRCell():New(oSection1,"RD6_DESC","RD6","",,30)	//Descricao da Avaliacao
TRCell():New(oSection1,"RDB_CODTIP","RDB",STR0025)	//Codigo do Tipo da Avaliacao
TRCell():New(oSection1,"RD5_DESC","RD5","",,30)	//Descricao do Tipo da Avaliacao
TRCell():New(oSection1,"RDB_CODMOD","RDB",STR0026)	//Codigo do Modelo de Avaliacao
TRCell():New(oSection1,"RD3_DESC","RD3","",,30)	//Descricao do Modelo de Avaliacao  
TRCell():New(oSection1,"RD6_CODVIS","RD6",STR0006)	//Codigo da Visao Padrao
TRCell():New(oSection1,"RDK_DESC","RDK","",,30) 	//Descricao da Visao Padrao
TRCell():New(oSection1,"RDB_DTIAVA","RDB",STR0008)	//Data Inicio
TRCell():New(oSection1,"RDB_DTFAVA","RDB",STR0009)	//Data Fim

TRPosition():New(oSection1,"RD6",1,{|| RhFilial("RD6", RDB->RDB_FILIAL) + RDB->RDB_CODAVA+RDB->RDB_CODTIP+RDB->RDB_CODMOD}) 
TRPosition():New(oSection1,"RD5",1,{|| RhFilial("RD5", RDB->RDB_FILIAL) + RDB->RDB_CODTIP}) 
TRPosition():New(oSection1,"RD3",1,{|| RhFilial("RD3", RDB->RDB_FILIAL) + RDB->RDB_CODMOD})
TRPosition():New(oSection1,"RDK",1,{|| RhFilial("RDK", RDB->RDB_FILIAL) + RD6->RD6_CODVIS})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Segunda Secao: Avaliado ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection2 := TRSection():New(oSection1,STR0011,{"RDB","RD0","RDN"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Avaliado"
oSection2:SetTotalInLine(.F.)
oSection2:SetHeaderBreak(.T.)  
oSection2:SetAutoSize()		//Ajuste Automatico do Tamanho do Cabecalho da Secao
oSection2:SetLeftMargin(1)	//Identacao da Secao
 
TRCell():New(oSection2,"RDB_CODADO","RDB")				//Codigo do Avaliado
TRCell():New(oSection2,"RD0_NOME","RD0","",,,,{||FDesc("RD0",RDB->RDB_CODADO,"RD0_NOME") })	//Nome do Avaliado  
TRCell():New(oSection2,"RDB_CODNET","RDB",STR0013)		//Codigo da Rede
TRCell():New(oSection2,"RD1_DESC","RD1","") 			//Descricao da Rede
TRCell():New(oSection2,"RDB_CODDOR","RDB",STR0022)		//Codigo do Avaliador
TRCell():New(oSection2,"RD0_NOME","RD0","",,,,{||FDesc("RD0",RDB->RDB_CODDOR,"RD0_NOME") }) //Nome do Avaliador 
TRCell():New(oSection2,"RDB_CODPRO","RDB",STR0023)		//Codigo do Projeto
TRCell():New(oSection2,"RDN_DESC","RDN","",,30)		//Descricao do Projeto
TRCell():New(oSection2,"RDN_DTIPRO","RDN") 				//Data Inicio do Projeto    
TRCell():New(oSection2,"RDN_DTFPRO","RDN") 				//Data Fim do Projeto     

TRPosition():New(oSection2,"RD0",1,{|| RhFilial("RD0", RDB->RDB_FILIAL) + RDB->RDB_CODADO})
TRPosition():New(oSection2,"RD0",1,{|| RhFilial("RD0", RDB->RDB_FILIAL) + RDB->RDB_CODDOR})	
TRPosition():New(oSection2,"RDN",1,{|| RhFilial("RDN", RDB->RDB_FILIAL) + RDB->RDB_CODPRO})
TRPosition():New(oSection2,"RD1",1,{|| RhFilial("RD1", RDB->RDB_FILIAL) + RDB->RDB_CODNET})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Terceira Secao: Competencia ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection3 := TRSection():New(oSection2,STR0007,{"RDB","RDM"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Competencia"
oSection3:SetTotalInLine(.F.)    
oSection3:SetHeaderBreak(.T.)
oSection3:SetLeftMargin(2)	//Identacao da Secao

TRCell():New(oSection3,"RDB_CODCOM","RDB")		//Codigo da Competencia
TRCell():New(oSection3,"RDM_DESC","RDM","")	//Descricao da Competencia

TRPosition():New(oSection3,"RDM",1,{|| RhFilial("RDM", RDB->RDB_FILIAL) + RDB->RDB_CODCOM})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Quarta Secao: Item de Competencia ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection4 := TRSection():New(oSection3,STR0012,{"RDB","RD2"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Item de Competencia"
oSection4:SetTotalInLine(.F.)  
oSection4:SetHeaderBreak(.T.) 
oSection4:SetLeftMargin(3)	//Identacao da Secao

TRCell():New(oSection4,"RDB_ITECOM","RDB")			//Codigo do Item de Competencia
TRCell():New(oSection4,"RD2_DESC","RD2","")		//Descricao do Item de Competencia 

TRPosition():New(oSection4,"RD2",1,{|| RhFilial("RD2", RDB->RDB_FILIAL) + RDB->RDB_CODCOM+RDB->RDB_ITECOM})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criacao da Quinta Secao: Questoes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection5 := TRSection():New(oSection4,STR0028,{"RDB","RD2","RBL"},/*aOrdem*/,/*Campos do SX3*/,/*Campos do SIX*/)	//"Questoes"	
oSection5:SetTotalInLine(.F.)
oSection5:SetHeaderBreak(.T.)  
oSection5:SetLeftMargin(4)	//Identacao da Secao 

TRCell():New(oSection5,"RDB_CODQUE","RDB",STR0020)		//Codigo da Questao
TRCell():New(oSection5,"QO_QUEST","SQO","",,110)		//Descricao da Questao
TRCell():New(oSection5,"RESULT","   ",STR0021,"@R 999.99",6,,{|| Ap010Resu() })	//Resultado Esperado
TRCell():New(oSection5,"GRAUIM","   ",STR0014,"@R 999.99",6,,{|| Ap010Grau() })	//Grau de Importancia
TRCell():New(oSection5,"RDB_RESOBT","RDB",STR0015,"@R 999,999,999.99",12)	   		//Resultado da Questao
TRCell():New(oSection5,"PORCENTO","   ",STR0018,"@R 999.99",6,,{|| If (RBL->RBL_VALOR <> 0,(RDB->RDB_RESOBT/RBL->RBL_VALOR)*100,1) })	//Resultado Esperado

TRPosition():New(oSection5,"SQO",1,{|| RhFilial("SQO", RDB->RDB_FILIAL) + RDB->RDB_CODQUE})

oSection5:SetTotalText({|| STR0019 }) //Nome do Totalizador 
TRFunction():New(oSection5:Cell("RESULT"),/*cId*/,"SUM",/*oBreak*/,/*cTitle*/,"@R 999,999,999.99"/*cPicture*/,/*uFormula*/,/*lEndSection*/,.F./*lEndReport*/,/*lEndPage*/)
TRFunction():New(oSection5:Cell("RDB_RESOBT"),/*cId*/,"SUM",/*oBreak*/,/*cTitle*/,"@R 999.99"/*cPicture*/,/*uFormula*/,/*lEndSection*/,.F./*lEndReport*/,/*lEndPage*/)

Return oReport

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef() ³ Autor ³ Eduardo Ju          ³ Data ³ 19.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao do Relatorio (Resultado Global)                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport)

Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(1):Section(1)  
Local oSection3 := oSection2:Section(1) 	//Filho da oSection2
Local oSection4 := oSection3:Section(1)		//Filho da oSection3
Local oSection5 := oSection4:Section(1)		//Filho da oSection4
Local cFiltro := ""

Private cIteCom := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01        //  Filial                                   ³
//³ mv_par02        //  Avaliacao                                ³
//³ mv_par03        //  Avaliado                                 ³
//³ mv_par04        //  Competencia                              ³
//³ mv_par05        //  Item de Competencia                      ³
//³ mv_par06        //  Rede                                     ³
//³ mv_par07        //  Visao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Transforma parametros Range em expressao (intervalo) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MakeAdvplExpr("APD20R")	                                  

If !Empty(mv_par01) .AND. xFilial("RDB") <> Space(2) //RDB_FILIAL
	cFiltro:= mv_par01
EndIf  
	
If !Empty(mv_par02)	//RDB_CODAVA
	cFiltro += If(!Empty(cFiltro),' .And. ','') + mv_par02
EndIf  

If !Empty(mv_par03)	//RDB_CODADO
	cFiltro += If(!Empty(cFiltro),' .And. ','') + mv_par03
EndIf  
	
If !Empty(mv_par04)	//RDB_CODCOM
	cFiltro += If(!Empty(cFiltro),' .And. ','') + mv_par04
EndIf  

If !Empty(mv_par05)	//RDB_ITECOM
	cFiltro += If(!Empty(cFiltro),' .And. ','') + mv_par05
EndIf  

If !Empty(mv_par06)	//RDB_CODTIP
	cFiltro += If(!Empty(cFiltro),' .And. ','') + mv_par06
EndIf  
	
If !Empty(mv_par07)	//RDB_CODMOD
	cFiltro += If(!Empty(cFiltro),' .And. ','') + mv_par07
EndIf  

If !Empty(mv_par08)	//RDB_CODNET
	cFiltro += If(!Empty(cFiltro),' .And. ','') + mv_par08 
EndIf          

oSection1:SetFilter(cFiltro) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Condicao para Impressao   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
oSection2:SetLineCondition({|| RDB->RDB_TIPOAV == "3" })	//Somente Avaliacao e Consenso
oSection2:SetParentFilter({|cParam| RDB->RDB_FILIAL+RDB->RDB_CODAVA == cParam},{|| RDB->RDB_FILIAL+RDB->RDB_CODAVA})

oSection3:SetParentFilter({|cParam| RDB->RDB_FILIAL+RDB->RDB_CODAVA+RDB->RDB_CODADO == cParam},{|| RDB->RDB_FILIAL+RDB->RDB_CODAVA+RDB->RDB_CODADO}) 

oSection4:SetLineCondition({|| Ap010PrintI(oSection4) })	
oSection4:SetParentFilter({|cParam| RDB->RDB_FILIAL+RDB->RDB_CODAVA+RDB->RDB_CODADO+RDB->RDB_CODCOM == cParam},{|| RDB->RDB_FILIAL+RDB->RDB_CODAVA+RDB->RDB_CODADO+RDB->RDB_CODCOM}) 

oSection5:Cell("PORCENTO"):SetHeaderAlign("CENTER")	
oSection5:SetParentFilter({|cParam| RDB->RDB_FILIAL+RDB->RDB_CODAVA+RDB->RDB_CODADO+RDB->RDB_CODCOM+RDB->RDB_ITECOM == cParam},{|| RDB->RDB_FILIAL+RDB->RDB_CODAVA+RDB->RDB_CODADO+RDB->RDB_CODCOM+RDB->RDB_ITECOM}) 
oSection5:SetLineCondition({|| cIteCom := "", RDB->RDB_TIPOAV == "3" })	//Somente Avaliacao e Consenso
oSection5:Cell("QO_QUEST"):SetLineBreak()	//Impressao de Campo Memo

oReport:SetMeter(RDB->(LastRec()))
oSection1:Print()                

Return Nil

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ap010PrintI()³ Autor ³ Eduardo Ju         ³ Data ³ 18.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Tratamento da Impressao do Item de Competencia              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oSection4                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ APDR010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Ap010PrintI(oSection4)

If RDB->RDB_ITECOM == cIteCom 
	oSection4:Cell("RDB_ITECOM"):Hide()
	oSection4:Cell("RD2_DESC"):Hide()
Else
	oSection4:Cell("RDB_ITECOM"):Show()
	oSection4:Cell("RD2_DESC"):Show()   
	cIteCom := RDB->RDB_ITECOM 
EndIf

Return .T.                    

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ap010Resu() ³ Autor ³ Eduardo Ju          ³ Data ³ 26.08.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Resultado Esperado                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ APDR020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Ap010Resu() 

Local cKeySeek := RDB->( RDB_CODAVA + RDB_CODADO + RDB_CODCOM + RDB_ITECOM ) 
Local cSvAlias 	:= Alias()
Local nResult	:= 0 

If 	RD7->( dbSeek( xFilial("RD7", RDB->RDB_FILIAL ) + cKeySeek ) ) .And.;
	RBL->( dbSeek( xFilial("RBL", RD7->RD7_FILIAL ) + RD7->( RD7_ESCALA+RD7_ITEESC ) ) )   
	nResult := RBL->RBL_VALOR   
EndIf      

DbSelectArea(cSvAlias)

Return nResult

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ap010Grau() ³ Autor ³ Eduardo Ju          ³ Data ³ 18.07.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Valor do Grau de Importancia do Item de Competencia.        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ APDR020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Ap010Grau() 

Local cKeySeek := RDB->( RDB_CODMOD + RDB_CODCOM + RDB_ITECOM + RDB_CODQUE )
Local cSvAlias 	:= Alias()
Local nGrauIm	:= 0

If 	RD8->( dbSeek( xFilial("RD8", RDB->RDB_FILIAL ) + cKeySeek ) ) .And. ;
	RBL->( dbSeek( xFilial("RBL", RD8->RD8_FILIAL ) + RD8->( RD8_ESCALA+RD8_ITEESC ) ) )
	nGrauIm := RBL->RBL_VALOR       
EndIf    				

DbSelectArea(cSvAlias)

Return nGrauIm
