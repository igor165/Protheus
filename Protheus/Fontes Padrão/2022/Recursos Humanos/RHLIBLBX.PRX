#INCLUDE "PROTHEUS.CH"

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInRhLibLbxExecЁAutor ЁMarinaldo de Jesus   Ё Data Ё14/05/2003Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de RHLIBLBX                          Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInRhLibLbxExec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InRhLibLbxExec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁLineLstBox		ЁAutorЁMarinaldo de Jesus Ё Data Ё14/01/2003Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁMontar a Linha do ListBox no bLine              			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico													Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function LineLstBox( oListBox , lMarck , aOkNo , nPosMarck , uOk )

Local abLine	:= {}
Local lIsArray	:= .F.
Local nItem		:= 0
Local nItens	:= 0

Static __aOkNo__

DEFAULT lMarck		:= .F.
DEFAULT __aOkNo__	:= { LoadBitmap( GetResources(), "LBOK" ) , LoadBitmap( GetResources(), "LBNO" ) }
DEFAULT aOkNo		:= IF( lMarck , aClone( __aOkNo__ ) , NIL )
DEFAULT nPosMarck	:= 1

#DEFINE __LINEBUTTONOK__	1
#DEFINE __LINEBUTTONNO__	2

IF ( ( oListBox:nAt := Min( oListBox:nAt , Len( oListBox:aArray ) ) ) > 0 )
	nItens			:= Len( oListBox:aHeaders )
	For nItem := 1 To nItens
		IF ( lIsArray := ( ValType( oListBox:aArray[ oListBox:nAt ] ) == "A" ) )
			IF ( nItem <= Len( oListBox:aArray[ oListBox:nAt ] ) )
				aAdd( abLine , oListBox:aArray[ oListBox:nAt , nItem ] )
			Else
				aAdd( abLine , "" )
			EndIF	
			IF ( ( lMarck ) .and. nItem == ( nPosMarck ) )
				DEFAULT uOk := abLine[ nItem ]
				IF ( ValType( oListBox:aArray[ oListBox:nAt , nItem ] ) == "L" ) .or. ( oListBox:aArray[ oListBox:nAt , nItem ] == uOk )
					abLine[ nItem ] := IF( abLine[ nItem ] , aOkNo[__LINEBUTTONOK__] , aOkNo[__LINEBUTTONNO__] )
				EndIF
			EndIF
		Else
			IF ( nItem <= Len( oListBox:aArray ) )
				aAdd( abLine , oListBox:aArray[ oListBox:nAt ] )
			Else
				aAdd( abLine , "" )
			EndIF
		EndIF	
	Next nItem
Else
	oListBox:nAt := 1
	aAdd( abLine , "" )	
EndIF	

Return( aClone( abLine ) )

/*/
зддддддддддбдддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRetFatListBoxЁ Autor ЁMarinaldo de Jesus   Ё Data Ё03/06/2002Ё
цддддддддддедддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetornar Fator de Multiplicacao para o Correto  DimensionamenЁ
Ё          Ёto em Relacao ao Tamanho do ListBox							 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRetFatListBox( nDimension )									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁnFat                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RetFatListBox( nDimension )

Local nFat := 83.0

IF ( ( nDimension >= 296.6 ) .and. ( nDimension <= 332.5 ) )		//1152 X 0864
	nFat := 90.0
ElseIF ( ( nDimension >= 212.6 ) .and. ( nDimension <= 296.5 ) )	//1024 X 0768
	nFat := 88.5
ElseIF ( ( nDimension >= 198.6 ) .and. ( nDimension <= 212.5 ) )	//0800 x 0600
	nFat := 83.5
ElseIF ( ( nDimension >= 153.1 ) .and. ( nDimension <= 198.5 ) )	//0720 x 0576
	nFat := 82.5
ElseIF ( ( nDimension >= 140.0 ) .and. ( nDimension <= 153.0 ) )	//0640 x 0480 ou 0720 x 480
	nFat := 76.5
EndIF

Return( nFat )