#INCLUDE "PROTHEUS.CH"

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³GPEXSR8   ³ Autor ³ Marinaldo de Jesus    ³ Data ³06/12/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Biblioteca de Funcoes Genericas para uso em Formulas no SR8 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³ Generico                                                   ³
ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Programador ³ Data     ³ BOPS  ³Motivo da Alteracao                    ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Mauricio T. ³26/06/2006³-------³Inclusao do Dbskip para funcao fdiasaf-³
³            ³          ³       ³asperiodo, pois estava em Loop.        ³
³Mauricio T. ³05/10/2006³-------³Tratamento para AS/400.                ³
³Mauricio T. ³18/01/2007³109389-³Reutilizacao do Objeto SR8.            ³
³Erika       ³29/05/2008³-------³Ajuste nos indices da tabela SR8 para  ³
³            ³          ³       ³compatibilizar dicionario Mexico e R1.2|
³Luciana     ³14/05/2010³010544-³Tratamento para verificar    a condicao³ 
³            ³          ³  2010 ³do mnemonico P_FERPRIME para demonstrar³
³            ³          ³      -³ou nao as ferias no mes do inicio do   ³ 
³            ³          ³      -³gozo e calcular corretamente os dias de³
³            ³          ³      -³salario no mes seguinte.               ³
³Erika K.    ³16/06/2010³013184/³Ajuste em WhereSR8 para carregar mnemo-³
³            ³          ³   2010³nico P_FERPRIME somente para Brasil.   |    
³WinstonCosta³04/01/2019³-------³Retirada Tratamento para AS/400.       |
ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³GetSR8		    ³Autor³Marinaldo de Jesus ³ Data ³06/12/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Obtem as Informacoes do SR8									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function GetSR8( cQueryWhere , lSqlWhere , lTopFilter )

Local cKey
Local cRetOrder

Local lGetSR8
Local nSR8Order

IF Empty( cQueryWhere )
	cQueryWhere := WhereSR8()
	#IFDEF TOP
		IF !Empty( cQueryWhere )
			lSqlWhere	:= .T.
		EndIF	
	#ENDIF
EndIF

cRetOrder 	:= "R8_FILIAL+R8_MAT"
nSR8Order 	:= RetOrder( "SR8" , cRetOrder , .T. )
cKey		:= SRA->( RA_FILIAL + RA_MAT )

//Eh necessario ter o Mnemonico oSR8 ( Tipo Private para reinicializar a cada registro )
IF (( ValType( oSr8 ) == "O" ) .and.;
	( Len(oSr8:aHeader) > 0 ))
	oSR8:GetCols( nSR8Order , cKey , cQueryWhere , lSqlWhere )
Else
	oSR8 	:= GetDetFormula():New( "SR8" , nSR8Order , cKey , cQueryWhere , @lSqlWhere , @lTopFilter )
EndIf
lGetSR8	:= oSR8:GetOk()

Return( lGetSR8 )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³PutSR8			³Autor³Marinaldo de Jesus ³ Data ³06/12/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Grava as Informacoes do SR8									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function PutSR8( )

Local aFieldsDel
Local aCntsDel

Return( oSR8:Put( aFieldsDel , aCntsDel ) )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³WhereSR8		³Autor³Marinaldo de Jesus ³ Data ³23/12/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Retorna a Clausula Where para o SR8							³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Where para o SR8									    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function WhereSR8()

Local cQueryWhere	:= ""
Local cFerPrime		:= ""
Local cTipoAfa      := ""
Local cFil			:=	Space(FWGETTAMFILIAL)
Local cFilRcm		:= IF (xFilial("RCM") == cFil, cFil, SRA->RA_FILIAL)
             
If cPaisLoc == "BRA"
	SetMnemonicos("P_FERPRIME",NIL,.T.)  
	cFerPrime := P_FERPRIME

	dbSelectArea("RCM")
	dbSetOrder(3)
	If dbSeek( cFilRcm + fGetCodFol( "0072", .F. ) )
		cTipoAfa := RCM->RCM_TIPOAF
	Endif

Endif

#IFDEF TOP
	cQueryWhere := "( R8_FILIAL='" + SRA->RA_FILIAL + "' AND " + "R8_MAT='" + SRA->RA_MAT + "') "
	cQueryWhere += " AND "
	If (cFerPrime <> "S" .AND. cTipoAfa =="4") .Or. cPaisLoc <> "BRA"
		cQueryWhere += " ( R8_SDPAGAR > 0 OR R8_DURACAO <> R8_DPAGOS OR R8_DATAFIM = '')"
		cQueryWhere += " AND "
	Endif
	cQueryWhere += " D_E_L_E_T_=' ' "
#ELSE
	cQueryWhere += " ( R8_SDPAGAR > 0 .OR. R8_DURACAO <> R8_DPAGOS .OR. Empty(R8_DATAFIM))"
#ENDIF

Return( cQueryWhere )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fDiasAfasPeriodo³ Autor ³ Mauricio Takakura³ Data ³03/11/05 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Calcula Nro. de Dias do Afastamento em determinado periodo ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fDiasAfasPeriodo                                      		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Generico 												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fDiasAfasPeriodo(	cMat		,;	// Matricula do Funcionario
							cCodTipo	,;	// Codigo do Tipo de Afastamento - Opcional desde que informado o cTipoDia
							cTipoDia	,;	// Tipo de Afastamento
							dDataIni	,;	// Data Inicial do Periodo de Apuracao do Afastamento
							dDataFim	,;	// Data Final do Periodo de Apuracao do Afastamento
							cNumID		,;  // Numero do ID
							cAliasSR8	 ;  // Alias SR8
 						    )

Local aArea 	:= GetArea()
Local cFilSR8 	:= xFilial( "SR8" )

Local lFound 	:= .F.

Local nDias

DEFAULT cMat 	 := SRA->RA_MAT
DEFAULT cCodTipo := ""
DEFAULT cTipoDia := ""
DEFAULT cAliasSR8:= "SR8"

Begin Sequence

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Localizar o Tipo do Afastamento           				   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If Empty(cTipoDia)
		DbSelectArea( "RCM" )
		DbSetOrder(1) 			// RCM_FILIAL + RCM_TIPO
		DbSeek( xFilial("RCM")+cCodTipo,.F.)
		If RCM->( Eof() )
			nDias := 0
			Break
		EndIf
	EndIf
	
	If !Empty( cNumID )
		DbSelectArea( cAliasSR8 )
		If cNumID <> (cAliasSR8)->R8_NUMID
			DbSetOrder(RetOrdem( "SR8", "R8_FILIAL+R8_MAT+DTOS(R8_DATAINI)+R8_TIPOAFA+STR(R8_DIASEMP)" ) )
			DbSeek( cFilSR8 + cMat, .F. )
			While !Eof() .and. (cAliasSR8)->R8_FILIAL == cFilSR8 .and. (cAliasSR8)->R8_MAT == cMat
				If (cAliasSR8)->R8_NUMID == cNumID
					lFound := .T.
					Exit
				EndIf
				(cAliasSR8)->(DbSkip())
			EndDo
			If !lFound
				nDias := 0
				Break
			EndIf
		EndIf
		If dDataFim > (cAliasSR8)->R8_DATAFIM
			dDataFim := (cAliasSR8)->R8_DATAFIM
		EndIf
	EndIf
	
	If cTipoDia == "2"
		If Empty(dDataFim)
			dDataFim := dDataBase
		EndIf
		nDias := dDataFim - dDataIni + 1  
	Else 
		GpeCalend(	NIL			,; //[,	<@>cFil			] -> Opcional, Filial do Funcionario Para Montagem do Calendario
					NIL			,; //[,	<@>cMat			] -> Opcional, Matricula do Funcionario Para Montagem do Calendario
					NIL			,; //[,	<@>cTno			] -> Opcional, Turno do Funcionario Para Montagem do Calendario
					NIL			,; //[,	<@>cSeq			] -> Opcional, Sequencia do Funcionario Para Montagem do Calendario
					NIL			,; //[,	<@>cCc			] -> Opcional, Centro de Custo do Funcionario Para Montagem do Calendario
					dDataIni	,; //[	<@>dDataIni		] -> Opcional, Data Inicial Para Montagem do Calendario
					dDataFim	,; //[,	<@>dDataFim		] -> Opcional, Data Final Para Montagem do Calendario
					@nDias		,; //[,	<@>nDiasUteis	] -> Opcional, Dias Uteis Para Montagem do Calendario
					"D"			,; //[,	<@>cTipoRet  	] -> Tipo de Retorno - Numero de Dias Uteis ("D") ou Data Final do Afastamento ("F")
					NIL		    ,; // Campo para comparacao na mensagem
					.F.			,; // Se deseja mostrar a mensagem do calendario
					cAliasMov   )  // Alias da Tabela SRA
	EndIf

End Sequence

RestArea( aArea )

Return( nDias )
