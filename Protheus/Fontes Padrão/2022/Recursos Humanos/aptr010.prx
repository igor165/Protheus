#INCLUDE "APTR010.CH"
#INCLUDE "protheus.ch"      
#INCLUDE "REPORT.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma     ³APTR010   º Autor ³ Andreia Santos      º Data ³  19/08/2004 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao    ³ Resumo dos Processos.                                       º±±
±±º             ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso          ³ Processos Trabalhistas                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Cecilia Car. ³04/08/2014³TQEQ39³Incluido o fonte da 11 para a 12 e efetuda ³±±  
±±³             ³          ³      ³a limpeza.                                 ³±±
±³Matheus M. 	³29/07/2015³TSZK68³Ajuste na ReportDef para utilizar 	      ³±±  
±±³             ³          ³      ³a variável da query para correta impressão.³±±
±³Matheus M. 	³02/12/2015³TTRKOC³V12 Ajuste na ReportDef para utilizar 	  ³±±  
±±³             ³          ³      ³a variável da query para correta impressão.³±±  
±±ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function APTR010()
Local	oReport   
Local	aArea 	:= GetArea()
Private	cString	:= "RE0"				// alias do arquivo principal (Base)
Private cPerg	:= "APT10R"
Private aOrd    := {OemToAnsi(STR0009)}		//"Processo"
Private cTitulo	:= OemToAnsi(STR0001)		//"Resumo dos Processos"
Private Inclui	:= .F.
Private cAliasQry := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte(cPerg,.F.) 
oReport := ReportDef()
oReport:PrintDialog()

RestArea( aArea )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef  ³ Autor ³ Tania Bronzeri        ³ Data ³21/08/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Resumo dos Processos                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ APTR010                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ APTR010 - Generico - Release 4                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()
Local oReport 
Local oSection1 
Local cDesc1	:= OemToAnsi(STR0001) + OemToAnsi(STR0002) + OemToAnsi(STR0003)	
//"Resumo dos Processos" ### "Ser  impresso de acordo com os parametros solicitados pelo" ### "usu rio."

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao dos componentes de impressao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE REPORT oReport NAME "APTR010" TITLE cTitulo PARAMETER cPerg ACTION {|oReport| APT10Imp(oReport)} DESCRIPTION OemtoAnsi(STR0010) TOTAL IN COLUMN	 
//"Este programa emite Resumo dos Processos Trabalhistas."

	DEFINE SECTION oSection1 OF oReport TITLE OemToAnsi(STR0013) TABLES "RE0" TOTAL IN COLUMN ORDERS aOrd

		DEFINE CELL NAME "RE0_NUM" 		OF oSection1 ALIAS cString SIZE 38
		DEFINE CELL NAME "RE0_DTPROC"	OF oSection1 ALIAS cString SIZE 38
		DEFINE CELL NAME "RE0_FASECD" 	OF oSection1 ALIAS cString TITLE OemToAnsi(STR0012) SIZE 15	//"Fase"
		DEFINE CELL NAME "RE0_FASEDS"	OF oSection1 TITLE " " SIZE 35	;
				BLOCK {||RE0DescF((cAliasQry)->RE0_FASECD)}
		DEFINE CELL NAME "RE0_RECLAM"	OF oSection1 TITLE OemToAnsi(STR0011) SIZE 40	;	//"Reclamante"
				BLOCK {||Left(If((cAliasQry)->RE0_TPACAO#"1",space(06),(cAliasQry)->RE0_RECLAM)+space(06),6)}
		DEFINE CELL NAME "RE0_RECNOM"	OF oSection1 TITLE " " SIZE 30	;	
				BLOCK {||Left(If((cAliasQry)->RE0_TPACAO#"1",OemToAnsi(STR0007),fDesc("RD0",(cAliasQry)->RE0_RECLAM,"RD0_NOME"))+Space(31),31)}	//"DIVERSOS"
		DEFINE CELL NAME "RE0_VARA"		OF oSection1 ALIAS cString SIZE 15
		DEFINE CELL NAME "RE0_VADESC"	OF oSection1 TITLE " " SIZE 20	;	
				BLOCK {||Left(fDesc("RE1",(cAliasQry)->RE0_COMAR+(cAliasQry)->RE0_VARA,"RE1_DESCR")+space(30),30)}
		DEFINE CELL NAME "RE0_COMAR"	OF oSection1 ALIAS cString SIZE 30
		DEFINE CELL NAME "RE0_CONOME"	OF oSection1 TITLE " " SIZE 30	;	
				BLOCK {||Left(fDesc("REC",(cAliasQry)->RE0_COMAR,"REC_NOME")+space(40),40)}
		DEFINE CELL NAME "RE0_VLRCSA"	OF oSection1 ALIAS cString SIZE 40
		DEFINE CELL NAME "RE0_VLRCOR"	OF oSection1 ALIAS cString SIZE 40

Return(oReport)


Static Function APT10Imp(oReport)
Local oSection  := oReport:Section(1)
Local cFiltro 	:= "" 

Private nOrdem  	:= 1								//  Ordem do Relatorio

//--MUDAR ANO PARA 4 DIGITOS
SET CENTURY ON

If oReport:Cancel()
	If nTdata > 8
		SET CENTURY ON
	Else
		SET CENTURY OFF
	Endif
	Return
EndIf               

	cAliasQry := GetNextAlias()

	//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
	MakeSqlExpr(cPerg)
		
	BEGIN REPORT QUERY oSection
	                                           
	cOrdem := "%RE0.RE0_FILIAL,RE0.RE0_NUM%"
	
	BeginSql alias cAliasQry
	
		SELECT	
			RE0.RE0_FILIAL, 
			RE0.RE0_NUM,    
			RE0.RE0_DTPROC, 
			RE0.RE0_FASECD, 
			RE0.RE0_RECLAM, 
			RE0.RE0_VARA, 
			RE0.RE0_COMAR,  
			RE0.RE0_VLRCSA, 
			RE0.RE0_VLRCOR, 
			RE0.RE0_TPACAO
		FROM %table:RE0% RE0
		WHERE 
			RE0.%notDel%   
		ORDER BY %exp:cOrdem%  
	EndSql
		
	/*
	Prepara relatorio para executar a query gerada pelo Embedded SQL passando como 
	parametro a pergunta ou vetor com perguntas do tipo Range que foram alterados 
	pela funcao MakeSqlExpr para serem adicionados a query
	*/
	END REPORT QUERY oSection PARAM mv_par01, mv_par02, mv_par03, mv_par04
               
//-- Define o total da regua da tela de processamento do relatorio
oReport:SetMeter( 100 )   

oSection:Print()	 //Imprimir

Return
