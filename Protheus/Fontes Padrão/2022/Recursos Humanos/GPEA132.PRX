#Include "PROTHEUS.CH"
#Include "GPEA132.CH"
#INCLUDE "FWMVCDEF.CH"
 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Gpea132  ³ Autor ³ Flavio Correa                ³ Data        ³ 15/07/16  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Historico     de Beneficios:                                              ³±±
±±³          ³ -Vale Transporte, Vale Refeicao, Vale Alimentacao                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Uso       ³ Generico                                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programador ³   Data   ³ FNC/Chamado    ³  Motivo da Alteracao                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Flavio Corr.³17/07/2015³                ³ Criação do Fonte de historico               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gpea132()

Local oMBrowse
Local cFiltraRh
	
oMBrowse := FWMBrowse():New()

oMBrowse:SetAlias("SRA")
oMBrowse:SetDescription(OemToAnsi(STR0001)) //"Histórico de Vales"

GpLegMVC(@oMBrowse)	// Adicionar a Legenda no Browse

oMBrowse:ForceQuitButton()	//"Incluir botao de sair" 

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicializa o filtro utilizando a funcao FilBrowse                      ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cFiltraRh := CHKRH("GPEA132","SRA","1")
oMBrowse:SetFilterDefault( cFiltraRh )
                           
oMBrowse:DisableDetails()
oMBrowse:Activate()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Menudef     ³ Autor ³ Equipe RH Inovacao ³ Data ³ 10/01/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Isola opcoes de menu para que as opcoes da rotina possam   ³±±
±±³          ³ ser lidas pelas bibliotecas Framework da Versao 9.12.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA132                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MenuDef()
Local aRet
Local aRotina := {}

aAdd( aRotina, { OemToAnsi(STR0002)	, "PesqBrw"        , 0, 1, 0, .T. } ) // "Pesquisar"
aAdd( aRotina, { OemToAnsi(STR0003), "VIEWDEF.GPEA132", 0, 2, 0, NIL } ) // "Visualizar"

Return( aRotina )

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ ModelDef 		³Autor³  Flavio Correa	  ³ Data ³13/07/2016³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Regras de Modelagem da gravacao.                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA003                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function ModelDef()
Local oModel
Local oStructSRA
Local oRG20
Local oRG21
Local oRG22
Local oSP7

oModel     := MPFormModel():New("GPEA132", /*bPreValid*/ ,/*tudook*/,/*bCommit*/, /*bCancel*/)
oModel:SetDescription( OemToAnsi(STR0001) ) //"Histórico de Vales"

// Cabecalho de dados - SRA (Funcionario)//
oStructSRA := FWFormStruct(1, "SRA",  { |cCampo| Gpa132SRAStru(cCampo) })
A132PStr(1, @oStructSRA )
oStructSRA:SetProperty("RA_NOME"	, MODEL_FIELD_WHEN, {|| .F. } )
oStructSRA:SetProperty("RA_ADMISSA"	, MODEL_FIELD_WHEN, {|| .F. } )
oModel:AddFields("GPEA132_MSRA", NIL, oStructSRA )
oModel:GetModel( "GPEA132_MSRA" ):SetDescription( OemToAnsi(STR0001) ) //"Histórico de Vales"
oModel:GetModel('GPEA132_MSRA'):SetOnlyQuery(.T.)


//SP7 - Faltas
oSP7 := FWFormStruct(1, "SP7")
oModel:AddGrid("GPEA132_MSP7", "GPEA132_MSRA", oSP7)
oModel:GetModel("GPEA132_MSP7" ):SetDescription( OemToAnsi(STR0001) ) //"Histórico de Vales"
oModel:GetModel("GPEA132_MSP7" ):SetOptional( .T. )
oModel:GetModel('GPEA132_MSP7'):SetOnlyView(.T.)
oModel:GetModel('GPEA132_MSP7'):SetOnlyQuery(.T.)
oModel:SetRelation( "GPEA132_MSP7", { { "P7_FILIAL", 'xFilial( "SRA" )' }, { "P7_MAT", 'SRA->RA_MAT' }, { "P7_PERIOD", "TMP_PERIOD" }}, SP7->( IndexKey( 1 ) ) )


//RG2 - VALE TRANSPORTE
oRG20 := FWFormStruct(1, "RG2")
oModel:AddGrid("GPEA132_MRG20", "GPEA132_MSRA", oRG20,, { |oGrid| BenLinOk(oGrid) } ,/*bPreVal*/,/*bPosVal*/,{|oGrid| CarregaRG2(oGrid)})
oModel:GetModel( "GPEA132_MRG20" ):SetDescription( OemToAnsi(STR0001) ) //"Histórico de Vales"
oModel:GetModel( "GPEA132_MRG20" ):SetOptional( .T. )
oModel:GetModel('GPEA132_MRG20'):SetOnlyView(.T.)
oModel:GetModel('GPEA132_MRG20'):SetOnlyQuery(.T.)
oModel:SetRelation( "GPEA132_MRG20", { { "RG2_FILIAL", 'xFilial( "SRA" )' }, { "RG2_MAT", 'SRA->RA_MAT' }, { "RG2_TPVALE", "'0'" }, { "RG2_PERIOD", "TMP_PERIOD" }}, RG2->( IndexKey( 7 ) ) )

//RG2 - VALE REFEICAO
oRG21 := FWFormStruct(1, "RG2")
oModel:AddGrid("GPEA132_MRG21", "GPEA132_MSRA", oRG21,, { |oGrid| BenLinOk(oGrid) },/*bPreVal*/,/*bPosVal*/,{|oGrid| CarregaRG2(oGrid)})
oModel:GetModel( "GPEA132_MRG21" ):SetDescription( OemToAnsi(STR0001) ) //"Histórico de Vales"
oModel:GetModel( "GPEA132_MRG21" ):SetOptional( .T. )
oModel:GetModel('GPEA132_MRG21'):SetOnlyView(.T.)
oModel:GetModel('GPEA132_MRG21'):SetOnlyQuery(.T.)
oModel:SetRelation( "GPEA132_MRG21", { { "RG2_FILIAL", 'xFilial( "SRA" )' }, { "RG2_MAT", 'SRA->RA_MAT' }, { "RG2_TPVALE", "'1'" }, { "RG2_PERIOD", "TMP_PERIOD" }}, RG2->( IndexKey( 7 ) ) )

//RG2 - VALE ALIMENTACAO
oRG22 := FWFormStruct(1, "RG2")
oModel:AddGrid("GPEA132_MRG22", "GPEA132_MSRA", oRG22,, { |oGrid| BenLinOk(oGrid) },/*bPreVal*/,/*bPosVal*/,{|oGrid| CarregaRG2(oGrid)})
oModel:GetModel( "GPEA132_MRG22" ):SetDescription( OemToAnsi(STR0001) )//"Histórico de Vales" 
oModel:GetModel( "GPEA132_MRG22" ):SetOptional( .T. )
oModel:GetModel('GPEA132_MRG22'):SetOnlyView(.T.)
oModel:GetModel('GPEA132_MRG22'):SetOnlyQuery(.T.)
oModel:SetRelation( "GPEA132_MRG22", { { "RG2_FILIAL", 'xFilial( "SRA" )' }, { "RG2_MAT", 'SRA->RA_MAT' } ,{ "RG2_TPVALE", "'2'" }, { "RG2_PERIOD", "TMP_PERIOD" }}, RG2->( IndexKey( 7 ) ) )


Return( oModel )

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ ViewDef  		³Autor³  Flavio Correa	 ³ Data ³13/07/2016³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Regras de Interface com o Usuario                           ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA003                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function ViewDef()
Local oView 
Local oModel
Local oStructSRA
Local oRG20
Local oRG21
Local oRG22
Local oSP7
Local aGpa132FRG2 := {"RG2_FILIAL", "RG2_MAT", "RG2_ROTEIR", "RG2_SALFOL", "RG2_ORIGEM", "RG2_MEIO", "RG2_DESC", "RG2_PERIOD", "RG2_NROPGT", "RG2_DIAS" }//
Local aTmp			:= {}

If GetMv("MV_GSPUBL", Nil, "1") == "2"
	aGpa132FRG2 := { "RG2_FILIAL", "RG2_MAT", "RG2_ROTEIR", "RG2_SALFOL", "RG2_ORIGEM", "RG2_CODIGO", "RG2_TPVALE", "RG2_FALTAS", "RG2_FERIAS", "RG2_AFAST", "RG2_DIAS", "RG2_PEDIDO" }
EndIf

// Vincular o View ao Model //
oModel := FWLoadModel("GPEA132")

oView := FWFormView():New()
oView:SetModel(oModel)

// Criacao do Cabecalho - SRA (Funcionario)
oStructSRA := FWFormStruct(2, "SRA", { |cCampo| Gpa132SRAStru(cCampo) })
A132PStr(2, @oStructSRA )
oStructSRA:SetNoFolder()

oStructSRA:AddGroup( 'DADOS', OemToAnsi(STR0004) , '', 2 )   //"Dados Cadastrais"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                  	
oStructSRA:AddGroup( 'PERIODO', OemToAnsi(STR0005) , '', 2 )   	//"Pesquisa"
oStructSRA:SetProperty( "RA_MAT" , MVC_VIEW_GROUP_NUMBER , 'DADOS' )
oStructSRA:SetProperty( "RA_NOME" , MVC_VIEW_GROUP_NUMBER , 'DADOS' )
oStructSRA:SetProperty( "RA_ADMISSA" , MVC_VIEW_GROUP_NUMBER , 'DADOS' )
oStructSRA:SetProperty( "TMP_PERIOD" , MVC_VIEW_GROUP_NUMBER , 'PERIODO' )



oView:AddField("GPEA132_VSRA", oStructSRA, "GPEA132_MSRA" )

oSP7 	:= FWFormStruct(2, "SP7")
oView:AddGrid("GPEA132_VSP7", oSP7, "GPEA132_MSP7" )



oRG20 	:= FWFormStruct(2, "RG2")
aTmp := aClone(aGpa132FRG2)
A132Remove(@oRG20,aTmp)
oRG20:SetProperty("RG2_ANOMES" ,MVC_VIEW_PVAR,FwBuildFeature(STRUCT_FEATURE_PICTVAR,"'@R 9999/99'"))
oRG20:SetProperty("RG2_ANOMES" ,MVC_VIEW_TITULO,STR0006)//"Ano/Mês"
oView:AddGrid("GPEA132_VRG20", oRG20, "GPEA132_MRG20" )

oRG21 	:= FWFormStruct(2, "RG2")
aTmp := aClone(aGpa132FRG2)
aAdd(aTmp, "RG2_DIADIF")
aAdd(aTmp, "RG2_VALDIF")
aAdd(aTmp, "RG2_CUNIDF")
aAdd(aTmp, "RG2_CFUNDF")
aAdd(aTmp, "RG2_CEMPDF")
aAdd(aTmp, "RG2_VTDUTE")
aAdd(aTmp, "RG2_VTDNUT")
aAdd(aTmp, "RG2_DUTILM")
A132Remove(@oRG21,aTmp)
oRG21:SetProperty("RG2_ANOMES" ,MVC_VIEW_PVAR,FwBuildFeature(STRUCT_FEATURE_PICTVAR,"'@R 9999/99'"))
oRG21:SetProperty("RG2_ANOMES" ,MVC_VIEW_TITULO,STR0006)//"Ano/Mês"
oView:AddGrid("GPEA132_VRG21", oRG21, "GPEA132_MRG21" )

oRG22 	:= FWFormStruct(2, "RG2")
aTmp := aClone(aGpa132FRG2)
aAdd(aTmp, "RG2_DIADIF")
aAdd(aTmp, "RG2_VALDIF")
aAdd(aTmp, "RG2_CUNIDF")
aAdd(aTmp, "RG2_CFUNDF")
aAdd(aTmp, "RG2_CEMPDF")
aAdd(aTmp, "RG2_VTDUTE")
aAdd(aTmp, "RG2_VTDNUT")
aAdd(aTmp, "RG2_DUTILM")
A132Remove(@oRG22,aTmp)
oRG22:SetProperty("RG2_ANOMES" ,MVC_VIEW_PVAR,FwBuildFeature(STRUCT_FEATURE_PICTVAR,"'@R 9999/99'"))
oRG22:SetProperty("RG2_ANOMES" ,MVC_VIEW_TITULO,STR0006)//"Ano/Mês"

oView:AddGrid("GPEA132_VRG22", oRG22, "GPEA132_MRG22" )


// Desenho da Tela //
oView:CreateHorizontalBox("SRA_HEAD", 40)
oView:CreateHorizontalBox("SP7", 20)
oView:CreateHorizontalBox("RG2", 40)

oView:CreateFolder( 'FOLDER_CAD', 'RG2')

oView:AddSheet('FOLDER_CAD','VT',STR0007)//"Vale Transporte"
oView:CreateHorizontalBox( 'VT', 100, , , 'FOLDER_CAD', 'VT')

oView:AddSheet('FOLDER_CAD','VR',STR0008)//"Vale Refeição"
oView:CreateHorizontalBox( 'VR', 100, , , 'FOLDER_CAD', 'VR')

oView:AddSheet('FOLDER_CAD','VA',STR0009 )//"Vale Alimentação" 
oView:CreateHorizontalBox( 'VA', 100, , , 'FOLDER_CAD', 'VA')

oView:SetOwnerView( "GPEA132_VSRA", "SRA_HEAD" )
oView:SetOwnerView('GPEA132_VSP7','SP7')
oView:SetOwnerView('GPEA132_VRG20','VT')
oView:SetOwnerView('GPEA132_VRG21','VR')
oView:SetOwnerView('GPEA132_VRG22','VA')

oView:SetCloseOnOk({ || .T. })//retira bota salvar e criar novo
Return oView

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ A132Remove      ³Autor³  Flavio Correa	³ Data ³13/10/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Remove do View campos iguais dos grids		               ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
*/
Static Function A132Remove(oObj,aCampos)
Local nI 	:= 1
Local nTam 	:= Len(aCampos)
For nI := 1 To nTam
	oObj:RemoveField( aCampos[nI] )
Next nI

Return

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ Gpa132SRAStru	³Autor³  Flavio Correa	³ Data ³13/10/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Selecionar os campos para a estrutura do SRA                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
*/
Static Function Gpa132SRAStru( cCampo )
	Local lRet := .F.
	cCampo := AllTrim( cCampo )
	If cCampo $ 'RA_MAT*RA_NOME*RA_ADMISSA' 
		lRet := .T.
	EndIf
Return lRet

Static Function A132PStr(nTipo, oStruct )
Local aArea    := GetArea()
Local bValid   := {|| CargaRg2()}
Local bWhen    := Nil
Local bRelac   := Nil
	
	If nTipo = 1
		oStruct:AddField( ;
			STR0010           , ;              // [01] Titulo do campo "Periodo"
			STR0010            , ;              // [02] ToolTip do campo
			"TMP_PERIOD"           , ;              // [03] Id do Field
			"C"           , ;              // [04] Tipo do campo
			6           , ;              // [05] Tamanho do campo
			0           , ;              // [06] Decimal do campo
			bValid                , ;              // [07] Code-block de validação do campo
			bWhen                 , ;              // [08] Code-block de validação When do campo
			           , ;              // [09] Lista de valores permitido do campo
			.F.                   , ;              // [10] Indica se o campo tem preenchimento obrigatório
			 {|| Anomes(date())}       , ;              // [11] Code-block de inicializacao do campo
			NIL                   , ;              // [12] Indica se trata-se de um campo chave
			Nil                   , ;              // [13] Indica se o campo não pode receber valor em uma operação de update.
			.T.)          		           // [14] Indica se o campo é virtual
	Else
		oStruct:AddField( 				      ;
		   		"TMP_PERIOD"                 , ;              // [01] Campo
				"99"  	, ;              // [02] Ordem
				STR0010                , ;              // [03] Titulo
				STR0010                , ;              // [04] Descricao
				NIL                    		, ;              // [05] Help
				"G"                 		, ;              // [06] Tipo do campo   COMBO, Get ou CHECK
				"@R 9999/99"                 , ;              // [07] Picture
				                       		, ;              // [08] PictVar
				                  			, ;              // [09] F3
				.T.  	               , ;              // [10] Editavel
				                       , ;              // [11] Folder
				""                 , ;              // [12] Group
				                      , ;              // [13] Lista Combo
				                       , ;              // [14] Tam Max Combo
				                       , ;              // [15] Inic. Browse
				.T.)                                    // [16] Virtual
			
	EndIf


RestArea( aArea )

Return oStruct

Function CargaRG2()
Local lRet 		:= .T.
Local oMdl    	:= FWModelActive()
Local oMyMdl00 	:= oMdl:GetModel("GPEA132_MRG20")
Local oMyMdl01 	:= oMdl:GetModel("GPEA132_MRG21")
Local oMyMdl02 	:= oMdl:GetModel("GPEA132_MRG22")
Local oView 		:= FWViewActive()
oMyMdl00:deActivate()
oMyMdl00:Activate()

oMyMdl01:deActivate()
oMyMdl01:Activate()

oMyMdl02:deActivate()
oMyMdl02:Activate()

oView:Refresh()
Return lRet

Function CarregaRG2(oGrid)
Local aRet := {}

oGrid:aDataModel := {}
aRet:= FormLoadGrid(oGrid,.T.)

Return aClone(aret)
