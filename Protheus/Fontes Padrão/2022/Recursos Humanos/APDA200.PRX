#include "Protheus.ch"
#include "APDA200.CH"

/*эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ё APDA200  Ё Autor Ё Eduardo Ju              Ё Data Ё 26.09.05 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Cadastro de Mapa de Conhecimento                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё APDA200                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Avoid                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё APDA200                                                      Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL             Ё╠╠
╠╠цддддддддддддбддддддддддбдддддддддддбддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador ЁData      ЁBOPS       ЁMotivo da Alteracao                  Ё╠╠
╠╠цддддддддддддеддддддддддедддддддддддеддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁCecilia Car.Ё03/07/14  ЁTPZWBQ     ЁIncluido o fonte da 11 para a 12 e e-Ё╠╠ 
╠╠Ё            Ё          Ё           Ёfetuada a limpeza.                   Ё╠╠ 
╠╠юддддддддддддаддддддддддадддддддддддаддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/   
Function APDA200()

Local cFiltra	:= ""				//Variavel para filtro
Local aIndFil	:= {}				//Variavel Para Filtro

Private bFiltraBrw := {|| Nil}		//Variavel para Filtro

Private	aAC := {STR0001,STR0002}	//"Abandona"###"Confirma"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Array contendo as Rotinas a executar do programa      Ё
//Ё ----------- Elementos contidos por dimensao ------------     Ё
//Ё 1. Nome a aparecer no cabecalho                              Ё
//Ё 2. Nome da Rotina associada                                  Ё
//Ё 3. Usado pela rotina                                         Ё
//Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
//Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
//Ё    2 - Simplesmente Mostra os Campos                         Ё
//Ё    3 - Inclui registros no Bancos de Dados                   Ё
//Ё    4 - Altera o registro corrente                            Ё
//Ё    5 - Remove o registro corrente do Banco de Dados          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define o cabecalho da tela de atualizacoes                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Private cCadastro := OemtoAnsi(STR0008)	//"Cadastro "

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa o filtro utilizando a funcao FilBrowse                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("RBO")	//Cadastro de Mapa de Conhecimento
dbSetOrder(1)

cFiltra 	:= CHKRH(FunName(),"RBO","1")
//bFiltraBrw 	:= {|| FilBrowse("RBO",@aIndFil,@cFiltra) }
//Eval(bFiltraBrw)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Endereca a funcao de BROWSE                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("RBO")
dbGoTop()

mBrowse(6, 1, 22, 75, "RBO")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Deleta o filtro utilizando a funcao FilBrowse                     	   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
//EndFilBrw("RBO",aIndFil)

dbSelectArea("RBO")
dbSetOrder(1)

Return Nil

/*эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ё Ap200Del Ё Autor Ё Eduardo Ju            Ё Data Ё 27.09.05 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Trata a Exclusao do Mapa de Conhecimento                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Ap200Del(cExpC1,nExpN1,nExpN2)                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё APDA200                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function Ap200Del(cAlias,nReg,nOpc)

Local bCampo
Local oDlg
Local nOpca		:= 0
Local cIndCond	:= 0
Local cArqNtx	:= ""
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta a entrada de dados do arquivo                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Private aTELA[0][0],aGETS[0]
bCampo 	:= {|nCPO| Field(nCPO) }

nOpcA	:= 0
dbSelectArea(cAlias)

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Monta as Dimensoes dos Objetos         					   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
aAdvSize		:= MsAdvSize()
aAdvSize[5]	:=	(aAdvSize[5]/100) * 90	//horizontal
aAdvSize[6]	:=  (aAdvSize[6]/100) * 90	//Vertical
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }					 
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords )
aGdCoord	:= { (aObjSize[1,1]+3), (aObjSize[1,2]+5), (((aObjSize[1,3])/100)*90), (((aObjSize[1,4])/100)*95) }	//1,3 Vertical /1,4 Horizontal

DEFINE MSDIALOG oDlg TITLE cCadastro From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF GetWndDefault() PIXEL //"Pesquisar"
  	nOpcA:=EnChoice( cAlias, nReg, nOpc, aAC,"AC",OemToAnsi(STR0009),,,,,,,,,,,)	//"Quanto a exclus└o?"
ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg,{|| nOpca:=2,oDlg:End()},{|| nOpca:=1,oDlg:End()})

If nOpcA == 2 
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se o Mapa esta sendo utilizado pelo arquivo RB7 *Mapa de Conhecimento versus AvaliadosЁ
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	RBP->( dbSetOrder(1) )  
	             
	If RBP->( dbSeek(xFilial("RBP")+RBO->RBO_CODIGO) )
		Aviso(OemToAnsi(STR0010),OemToAnsi(STR0011), {"Ok"})	//"Atencao"###ExclusЦo nЦo permitida, Mapa de Conhecimento gerado para participantes
		Return .F.			
	EndIf

	Begin Transaction	
		dbSelectArea(cAlias)
		RecLock(cAlias,.F.)
			dbDelete()
		MsUnlock()		
	End Transaction
EndIf

dbSelectArea(cAlias)

Return .T. 

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRboVisaoVld 	 ЁAutorЁEmerson Grassi    Ё Data Ё15/02/2006Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValid do Campo RBO_VISAO									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁValid do Campo RBO_VISAO									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RboVisaoVld()

Local lRboVisaoVld	:= .T.

Begin Sequence
	IF !( lRboVisaoVld := ( Vazio() .Or. ExistCpo( "RDK", GetMemVar( "RBO_VISAO" ) ) ) )
		Break
	EndIF

	SetMemVar( "RBO_DESVIS" , RboDesVisInit() )
End Sequence

Return( lRboVisaoVld )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRboDesVisInitЁAutorЁEmerson Grassi Rocha  Ё Data Ё15/02/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao de Inicializador Padrao do campo RBO_DESVIS	        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RBO_DESVIS	                  	   	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RboDesVisInit()

Local cRboDesVis 	:= ""
Local cCodVis 		:= ""

IF Type("M->RBO_VISAO") != "U" .And. !Empty( M->RBO_VISAO )
	cCodVis := M->RBO_VISAO
ElseIf !Inclui
	cCodVis	:= RBO->RBO_VISAO
Else
	cCodVis := Space(GetSx3Cache( "RBO_VISAO" , "X3_TAMANHO" ))
EndIF
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Descricao da Visao			           				   	   	 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
cRboDesVis := FDesc("RDK", cCodVis, "RDK_DESC", GetSx3Cache( "RBO_DESVIS" , "X3_TAMANHO" ) , NIL , NIL , .F.)

Return( cRboDesVis )



/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRboGrupoVld	 	 ЁAutorЁEmerson Grassi    Ё Data Ё23/02/2006Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValid do Campo RBO_GRUPO									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁValid do Campo RBO_GRUPO									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RboGrupoVld()

Local lRboGrupoVld	:= .T.

Begin Sequence

	IF !( lRboGrupoVld := ( Vazio() .Or. ExistCpo( "SQ0", GetMemVar( "RBO_GRUPO" ) ) ) )
		Break
	EndIF

	SetMemVar( "RBO_DESGRU" , RboDesGruInit() )

End Sequence

Return( lRboGrupoVld )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRboDesGruInitЁAutorЁEmerson Grassi Rocha  Ё Data Ё23/02/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao de Inicializador Padrao do campo RBO_DESGRU	        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RBO_DESGRU	                  	   	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RboDesGruInit()

Local cRboDesGru 	:= ""
Local cCodGru 		:= ""

IF Type("M->RBO_GRUPO") != "U" .And. !Empty( M->RBO_GRUPO )
	cCodGru := M->RBO_GRUPO
ElseIf !Inclui
	cCodGru	:= RBO->RBO_GRUPO
Else
	cCodGru := Space(GetSx3Cache( "RBO_GRUPO" , "X3_TAMANHO" ))
EndIF

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Descricao do Grupo			           				   	   	 Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
cRboDesGru := FDesc("SQ0", cCodGru, "Q0_DESCRIC", GetSx3Cache( "RBO_DESGRU" , "X3_TAMANHO" ) , NIL , NIL , .F.)
Return( cRboDesGru )


/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRboPeriodVld 	 ЁAutorЁEmerson Grassi    Ё Data Ё14/06/2006Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValid do Campo RBO_PERIOD									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁValid do Campo RBO_PERIOD									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RboPeriodVld()

Local lRboPeriodVld	:= .T.

Begin Sequence

	IF !( lRboPeriodVld := ( NaoVazio() .And. ExistCpo("RDU", GetMemVar("RBO_PERIOD") ) .And.;
		AllTrim(Fdesc("RDU", GetMemVar("RBO_PERIOD"), "RDU_TIPO")) == "1" ) )
		Break
	EndIF

	SetMemVar( "RBO_DESCPE" , FDesc("RDU", M->RBO_PERIOD, "RDU_DESC")  )

End Sequence

Return( lRboPeriodVld )

/*                                	
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Gustavo     Ё Data Ё21/12/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas bibliotecas Framework da Versao 9.12 .      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁAPDA160                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/   

Static Function MenuDef()


 Local aRotina := { 	{ STR0003, 'PesqBrw'	, 0, 1,,.F.}, ;		//'Pesquisar'
						{ STR0004, 'AxVisual'	, 0, 2}, ;		//'Visualizar'
						{ STR0005, 'AxInclui'	, 0, 3}, ;		//'Incluir'
						{ STR0006, 'AxAltera'	, 0, 4}, ;		//'Alterar'
						{ STR0007, 'Ap200Del'	, 0, 5} }		//'Excluir'

Return aRotina
