#include "PROTHEUS.CH"
#include "PONR090.CH"
#Include "FIVEWIN.CH"
#Include "REPORT.CH"
#IFDEF TOP
	#INCLUDE "TOPCONN.CH"
#ENDIF
 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONR090  ³ Autor ³ Fernando Joly Siquini   ³ Data ³ 03.02.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio por Codigos                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONR090(void)                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data     ³ BOPS ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³MauricioMR  ³ 03/04/02 ³Acerto³ Correcao de Impressao de Acumulados       ³±±
±±³ Priscila R.³ 24/05/02 ³------³Ajuste no relatorio para que seja impresso ³±± 
±±³            ³ -------- ³------³corretamente o C.C no tamanho 20.          ³±± 
±±³Mauricio MR ³ 01/10/02 ³Melhor³ Inclusao de pergunta possibilitando a es- ³±± 
±±³            ³          ³060139³ colha de impressao de horas na base cen-  ³±± 
±±³            ³          ³      ³ tesimal e sexagesimal.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³                          *** Versao 8.11 ***                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Mauricio MR ³ 30/04/03 ³------³Retirada da chamda da ValidPerg.           ³±± 
±±³Mauricio MR ³ 08/05/03 ³Acerto³ Ajuste na impressao de codigos qdo opcao  ³±± 
±±³            ³          ³      ³ vertical e selecao avulsa de codigos.     ³±± 
±±³Ricardo D.  ³ 21/08/06 ³100319³Conversao para relatorios personalizaveis. ³±± 
±±³Tania       ³03/10/2006³100319³Complemenmto da conversao para o release 4.³±± 
±±³Luiz Gustavo³05/02/2007³      ³Retiradas funcoes de ajuste de dicionario. ³±± 
±±³==========================================================================³±± 
±±³                         *** Versao 9.12 ***                              ³±± 
±±³==========================================================================³±± 
±±³Leandro Dr  ³01/04/08³Melhor³Inclusão de chave processo/periodo/roteiro   ³±± 
±±³            |        |      |NumPagto. Impressao por data ou periodo      ³±±
±±³Leandro Dr  ³22/08/08³152559³Ajuste para considerar impressao horizontal  ³±± 
±±³            |        |      |apenas se nao for para imprimir todos eventos³±± 
±±³Francisco Jr³12/01/10³000430³Compatibilizar fonte para uso na Gestao Cor- ³±± 
±±³            |        |  2010|porativa.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ FNC            ³  Motivo da Alteracao                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Alceu P.    |01/08/11|00000019268/2011|Ajustado o Sx1 para o pais Angola.           ³±±
±±³            |        |  							                                   ³±±
±±³Cícero Alves³05/03/15³TRUFR3³ Retirada da função AjustaSx1						   ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PonR090()

Local aArea		:= GetArea()
Local nx		:= 0
Local oReport

Private cPerg     := ""
Private cProcesso := ""
Private cPeriodo  := ""
Private cRoteiro  := ""
Private cNumPagto := ""
Private cDepto	  := ""
Private cSitAux	  := ""
Private cCatAux   := ""
Private cSituacao := ""
Private cCateg    := ""
Private cCodigos  := ""
Private	cCondProc := ""
Private cRDepto   := ""
Private cRFilial  := ""
Private	cRCCusto  := ""
Private cRMat	  := ""
Private	cRNome    := ""
Private	cFil	  := ""
Private cCCusto   := ""
Private	cMat	  := ""
Private	cNome     := ""
Private dPerIni   := CtoD("//")
Private dPerFim   := CtoD("//")
Private lTodos    := .F.
Private lSexaCent := .F.
Private lSrvAs400 := ( TcSrvType() == "AS/400" )	
Private lPeriodo  := .F.
Private nSinAna   := 0
Private nVerHor   := 0
Private nColPro
Private oBreak, oBreakCc, oBreakMat
Private oPeriodo  := RHPERIODO():New()
Private oTmpTRBSRA 

	If FindFunction("TRepInUse") .And. TRepInUse()
		
		//-- Cria arquivo temporario para o ambiente CDX ou AS/400
		#IFNDEF TOP
			CriaTRBSRA()
		#ELSE
			If lSrvAs400
				CriaTRBSRA()
			Endif
		#ENDIF

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica se deve imprimir por periodo ou datas			   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If fOpcRadio(@nColPro, STR0001,;      //"Relatorio de Resultados"
							   STR0053,;      //"Selecione a opcao de impressao:"
					   		   STR0054,;      //"Por Periodo"
					   		   STR0055 ) == 0 //"Por Datas"
			Return
		EndIf
			
		cPerg := IF(nColPro == 1,"PNR090A","PNR090B")
		
		Pergunte(cPerg,.F.)
		
		If ( nColPro == 1 ) 
			cProcesso := mv_par01			//Processo
			cPeriodo  := mv_par02			//Periodo
			cRoteiro  := mv_par03			//Roteiro
			cNumPagto := mv_par04			//Numero de Pagamento
		EndIf
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:PrintDialog()
		
		//-- Apaga os arquivos temporarios
		If Select("TRBSRA") > 0
			TRBSRA->(DbClosearea())
		Endif
		
		If oTmpTRBSRA <> Nil
			oTmpTRBSRA:Delete()
			Freeobj(oTmpTRBSRA)
		EndIf
		
 	Else
 		PonR090R3()
	EndIF    

RestArea( aArea )

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ Ricardo Duarte Costa  ³ Data ³ 21.08.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definicao do relatorio                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

Local oReport 
Local oSection, oSection1, oSection2, oSection3
Local aOrd		:= {STR0004 , STR0005 , STR0006 , STR0028 , STR0056} // 'Matricula'###'Centro de Custo'###'Nome'###'C.Custo+Nome'###'Departamento '

Local bTipVHoras:= {|| If(lSexaCent, fConvHr((cAliasSPB)->PB_HORAS,'H') , (cAliasSPB)->PB_HORAS) }
Local bTipHHrs01:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD01,'H') , (cAliasSRA)->PD01) }
Local bTipHHrs02:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD02,'H') , (cAliasSRA)->PD02) }
Local bTipHHrs03:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD03,'H') , (cAliasSRA)->PD03) }
Local bTipHHrs04:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD04,'H') , (cAliasSRA)->PD04) }
Local bTipHHrs05:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD05,'H') , (cAliasSRA)->PD05) }
Local bTipHHrs06:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD06,'H') , (cAliasSRA)->PD06) }
Local bTipHHrs07:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD07,'H') , (cAliasSRA)->PD07) }
Local bTipHHrs08:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD08,'H') , (cAliasSRA)->PD08) }
Local bTipHHrs09:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD09,'H') , (cAliasSRA)->PD09) }
Local bTipHHrs10:= {|| If(lSexaCent, fConvHr((cAliasSRA)->PD10,'H') , (cAliasSRA)->PD10) }
Local bTipHHrsTT:= {|| If(lSexaCent, fSomaTot("H") , fSomaTot("C") ) }

	DEFINE REPORT oReport NAME "PONR090" TITLE OemToAnsi(STR0012) PARAMETER cPerg ACTION {|oReport| PrintReport(oReport)} DESCRIPTION OemtoAnsi(STR0046)
	//"Emite Relacao de resultados do Ponto Eletronico."

	//-- Indica que o tal sera em coluna. Os campos numericos serao totalizados abaixo de suas colunas onde foram impressos.
	oReport:SetTotalInLine(.F.)
   	
		DEFINE SECTION oSection1 OF oReport TABLES "SRA" ORDERS aOrd TITLE STR0047
		oSection1:SetTotalInLine(.F.)
		
			DEFINE CELL NAME "RA_FILIAL" 	OF oSection1 ALIAS "SRA"
			DEFINE CELL NAME "RA_CC"		OF oSection1 ALIAS "SRA"
			DEFINE CELL NAME "RA_MAT" 		OF oSection1 ALIAS "SRA"
			DEFINE CELL NAME "RA_NOME"		OF oSection1 ALIAS "SRA"
			DEFINE CELL NAME "RA_DEPTO"		OF oSection1 ALIAS "SRA"
//			DEFINE CELL NAME "QB_DESCRIC"	OF oSection1 ALIAS "SQB"

//			TRPosition():New(oSection1, "SQB", 1, {|| RhFilial("SQB",(cAliasSRA)->RA_FILIAL) + (cAliasSRA)->RA_DEPTO})

			DEFINE SECTION oSection2 OF oSection1 TABLES "SPB", "SRV" TITLE STR0048
		
				DEFINE CELL NAME "PB_PD" 		OF oSection2 ALIAS "SPB" 
				DEFINE CELL NAME "RV_DESC"		OF oSection2 ALIAS "SRV" 
				DEFINE CELL NAME "PB_HORAS"		OF oSection2 ALIAS "SPB"	BLOCK  bTipVHoras
				DEFINE CELL NAME "PB_PROCES"	OF oSection2 ALIAS "SPB"
				DEFINE CELL NAME "PB_PERIODO"	OF oSection2 ALIAS "SPB"
				DEFINE CELL NAME "PB_ROTEIR"	OF oSection2 ALIAS "SPB"
				DEFINE CELL NAME "PB_SEMANA"	OF oSection2 ALIAS "SPB"
			
				oSection2:SetLeftMargin(1)

				TRPosition():New(oSection2,"SRV",1,{|| RhFilial("SRV",(cAliasSPB)->PB_FILIAL)+(cAliasSPB)->PB_PD},.T.)

			  
		DEFINE SECTION oSection3 OF oReport TABLES "TRBSRA" ORDERS aOrd TITLE STR0049
		oSection3:SetTotalInLine(.F.)
		//oSection3:SetAutoSize()
		
			DEFINE CELL NAME "RA_FILIAL" 	OF oSection3 ALIAS "TRBSRA" AUTO SIZE
			DEFINE CELL NAME "RA_CC"		OF oSection3 ALIAS "TRBSRA" AUTO SIZE
			DEFINE CELL NAME "RA_MAT" 		OF oSection3 ALIAS "TRBSRA" AUTO SIZE
			DEFINE CELL NAME "RA_NOME"		OF oSection3 ALIAS "TRBSRA" AUTO SIZE
			DEFINE CELL NAME "RA_DEPTO"		OF oSection3 ALIAS "TRBSRA" AUTO SIZE
			DEFINE CELL NAME "PD01"	 		OF oSection3 				TITLE STR0034	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs01 ALIGN RIGHT	// #"COD 01"
			DEFINE CELL NAME "PD02"	 		OF oSection3 				TITLE STR0035	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs02 ALIGN RIGHT	// #"COD 02"
			DEFINE CELL NAME "PD03"	 		OF oSection3 				TITLE STR0036	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs03 ALIGN RIGHT	// #"COD 03"
			DEFINE CELL NAME "PD04"	 		OF oSection3 				TITLE STR0037	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs04 ALIGN RIGHT	// #"COD 04"
			DEFINE CELL NAME "PD05"	 		OF oSection3 				TITLE STR0038	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs05 ALIGN RIGHT	// #"COD 05"
			DEFINE CELL NAME "PD06"	 		OF oSection3 				TITLE STR0039	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs06 ALIGN RIGHT	// #"COD 06"
			DEFINE CELL NAME "PD07"	 		OF oSection3 				TITLE STR0040	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs07 ALIGN RIGHT	// #"COD 07"
			DEFINE CELL NAME "PD08"	 		OF oSection3 				TITLE STR0041	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs08 ALIGN RIGHT	// #"COD 08"
			DEFINE CELL NAME "PD09"	 		OF oSection3 				TITLE STR0042	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs09 ALIGN RIGHT	// #"COD 09"
			DEFINE CELL NAME "PD10"	 		OF oSection3 				TITLE STR0043	PICTURE PesqPict("SPB","PB_HORAS") BLOCK  bTipHHrs10 ALIGN RIGHT	// #"COD 10"
			DEFINE CELL NAME "PDTOTAL" 		OF oSection3 				TITLE STR0030+CRLF+STR0031 PICTURE "@E 999,999,999.99" SIZE 11 BLOCK  bTipHHrsTT ALIGN RIGHT	// ### TOTAL ### DE HORAS

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³PrintRepor³ Autor ³ Ricardo Duarte Costa  ³ Data ³ 21.08.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao do relatorio                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nColPro == 1 ) 
	lPeriodo  := .T.
	cProcesso  := mv_par01				//Processo
	cPeriodo   := mv_par02				//Periodo
	cRoteiro   := mv_par03				//Roteiro
	cNumPagto  := mv_par04				//Numero de Pagamento
	nSinAna    := mv_par10				//Relatorio Analitico ou Sintetica
	cSituacao  := mv_par11				//Situacoes a Imprimir
	cCateg     := mv_par12				//Categorias a Imprimir
	lTodos     := (mv_par13==1)			//Se lista todos os codigos encontrados
	cCodigos   := ALLTRIM(mv_par14)		//Codigos a Listar
	nVerHor    := IF(lTodos .Or. Len(cCodigos)/3 > 10,1,mv_par15) //Formato de Impressao. Vertical somente quando selecionar menos de 10 verbas
	lSexaCent  := (mv_par16==1) 			//Horas em  (Sexagesimal/Centesimal)
Else
	lPeriodo   := .F.
	cProcesso  := mv_par01				//Processo
	dPerIni    := mv_par02				//Periodo Inicial
	dPerFim    := mv_par03				//Periodo Final
	nSinAna    := mv_par09				//Relatorio Analitico ou Sintetica
	cSituacao  := mv_par10				//Situacoes a Imprimir
	cCateg     := mv_par11				//Categorias a Imprimir
	lTodos     := (mv_par12==1)			//Se lista todos os codigos encontrados
	cCodigos   := ALLTRIM(mv_par13)		//Codigos a Listar
	nVerHor    := IF(lTodos .Or. Len(cCodigos)/3 > 10,1,mv_par14) //Formato de Impressao. Vertical somente quando selecionar menos de 10 verbas
	lSexaCent  := (mv_par15==1)			//Horas em  (Sexagesimal/Centesimal)
EndIf

If lPeriodo
	oPeriodo:cProcesso := cProcesso
	oPeriodo:cPeriodo  := cPeriodo
	oPeriodo:cRoteiro  := cRoteiro
	oPeriodo:cNumPagto := cNumPagto

	oPeriodo:GetPer()
	
	If !(oPeriodo:lFound )
		MsgInfo( OemToAnsi( oPeriodo:cMsgNotFoundPer ) )    //"Período de Apontamento não encontrado."
		Return
	EndIf
	
	dPerIni := oPeriodo:dDataIni
	dPerFim := oPeriodo:dDataFim
Else
	oPeriodo:dDataIni := dPerIni
	oPeriodo:dDataFim := dPerFim	
EndIf

cSitAux := cSituacao
cCatAux := cCateg

If !Empty(cProcesso)
	cCondProc := "(RA_PROCES IN('" + cProcesso + "'))"
EndIf

If !lSrvAs400
	#IFDEF TOP
		//Transforma parametros do tipo Range em expressao SQL para ser utilizada na query
		MakeSqlExpr(cPerg)
		
		If lPeriodo
			cRDepto    := mv_par05				//Range Departamento
			cRFilial   := mv_par06				//Range Filial
			cRCCusto   := mv_par07				//Range Centro de Custo
			cRMat	   := mv_par08				//Range Matricula
			cRNome     := mv_par09				//RAnge Nome
		Else
			cRDepto    := mv_par04				//Range Departamento
			cRFilial   := mv_par05				//Range Filial
			cRCCusto   := mv_par06				//Range Centro de Custo
			cRMat	   := mv_par07				//Range Matricula
			cRNome     := mv_par08				//RAnge Nome
		EndIf
	#ELSE
EndIf
		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeAdvplExpr(cPerg)
		
		If lPeriodo
			cDepto    := mv_par05				//Range Departamento
			cFil      := mv_par06				//Range Filial
			cCCusto   := mv_par07				//Range Centro de Custo
			cMat	  := mv_par08				//Range Matricula
			cNome     := mv_par09				//Range Nome
		Else
			cDepto    := mv_par04				//Range Departamento
			cFil	  := mv_par05				//Range Filial
			cCCusto   := mv_par06				//Range Centro de Custo
			cMat	  := mv_par07				//Range Matricula
			cNome	  := mv_par08				//Range Nome
		EndIf
If !lSrvAs400
	#ENDIF
EndIf
    
If nVerHor == 1      
	RepVertical(oReport)
Else
	RepHorizontal(oReport)
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³RepVertical  ³ Autor ³ Ricardo Duarte Costa  ³ Data ³ 21.08.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao do relatorio                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function RepVertical(oReport)
                     
//-- Objeto
Local oSection1 	:= oReport:Section(1)
Local oSection2 	:= oReport:Section(1):Section(1)

//-- String
Local cFiltroSRA	:= ""  
Local cFiltroSPB	:= ""
Local cQVerba		:= ""  
Local cAcessaSRA	:= &( " { || " + ChkRH( "PONR090" , "SRA" , "2" ) + " } " )
Local cPerIni		:= ""
Local cPerFim		:= ""
Local cFiltraPd		:= ""
Local cOrdem		:= ""
Local cTitMat		:= ""       
Local cTitCc		:= ""  
Local cTitFil		:= ""
Local cFilSpb		:= ""
Local cFilSrv		:= ""
Local cFunction		:= If(lSexaCent,"TIMESUM","SUM")

//-- Numerica
Local nOrdem   		:= oReport:Section(1):GetOrder()
Local nVer			:= 0
Local nReg			:= 0

//-- Array
Local aInfo			:= {}


                        
If nSinAna == 2
	DEFINE BREAK oBreakMat	OF oReport WHEN oSection1:Cell("RA_MAT") 	TITLE STR0050		// ### T O T A L ### DO FUNCIONARIO
	TRFunction():New(oSection2:Cell("PB_HORAS"),,cFunction,oBreakMat,,"@E 9,999,999.99",,.F.,.F.)
	oBreakMat:OnBreak({|x,y|cTitMat:=OemToAnsi(STR0050)+x})	//"TOTAL DO FUNCIONARIO"
    oBreakMat:SetTotalText({||cTitMat})
Endif

//Centro de Custo ou C.Custo + Nome
If nOrdem == 2 .Or. nOrdem = 4
	DEFINE BREAK oBreakCc	OF oReport WHEN oSection1:Cell("RA_CC") 	
	TRFunction():New(oSection2:Cell("PB_HORAS"),,cFunction,oBreakCC,,"@E 9,999,999.99",,.F.,.F.)
	oBreakCc:OnBreak({|x,y|cTitCc:=OemToAnsi(STR0051)+x+" - "+fDesc("SI3",x,"SI3->I3_DESC",,oBreak:GetLastValue())})	//"TOTAL DO CENTRO DE CUSTO"
    oBreakCc:SetTotalText({||cTitCc})

	DEFINE COLLECTION OF oSection2 FUNCTION COUNT FORMULA {|| oSection2:Cell("PB_PD"):GetValue(.T.) + " - " + Left(oSection2:Cell("RV_DESC"):GetValue(.T.)+Space(20),20)+"."} CONTENT oSection2:Cell("PB_PD") 		BREAK oBreakCc TITLE OemToAnsi(STR0032)	PICTURE "999"			NO END REPORT NO END SECTION			//"TOT. LINHAS"
	If lSexaCent
		DEFINE COLLECTION OF oSection2 FUNCTION TIMESUM FORMULA oSection2:Cell("PB_PD") CONTENT oSection2:Cell("PB_HORAS")	BREAK oBreakCc TITLE OemToAnsi(STR0033)	PICTURE "@E 999,999,999.99"	NO END REPORT NO END SECTION			//"TOTAL UNIT."
	Else
		DEFINE COLLECTION OF oSection2 FUNCTION SUM     FORMULA oSection2:Cell("PB_PD") CONTENT oSection2:Cell("PB_HORAS")	BREAK oBreakCc TITLE OemToAnsi(STR0033)	PICTURE "@E 999,999,999.99"	NO END REPORT NO END SECTION			//"TOTAL UNIT."
	EndIf                                              
Endif

DEFINE BREAK oBreak 	OF oReport WHEN oSection1:Cell("RA_FILIAL")	
TRFunction():New(oSection2:Cell("PB_HORAS"),,cFunction,oBreak,,"@E 9,999,999.99",,.F.,.T.)
oBreak:OnBreak({|x,y|fInfo(@aInfo,x),cTitFil:=OemToAnsi(STR0052)+x+" - "+aInfo[1]})	//"TOTAL DA FILIAL"
oBreak:SetTotalText({||cTitFil})

DEFINE COLLECTION OF oSection2 FUNCTION COUNT FORMULA {|| oSection2:Cell("PB_PD"):GetValue(.T.) + " - " + Left(oSection2:Cell("RV_DESC"):GetValue(.T.)+Space(20),20)+"."} CONTENT oSection2:Cell("PB_PD") 		BREAK oBreak TITLE OemToAnsi(STR0032) PICTURE "999"			NO END SECTION			//"TOT. LINHAS"
If lSexaCent
	DEFINE COLLECTION OF oSection2 FUNCTION TIMESUM FORMULA oSection2:Cell("PB_PD") CONTENT oSection2:Cell("PB_HORAS")	BREAK oBreak TITLE OemToAnsi(STR0033) PICTURE "@E 999,999,999.99"	NO END SECTION 			//"TOTAL UNIT."
Else
	DEFINE COLLECTION OF oSection2 FUNCTION SUM 	FORMULA oSection2:Cell("PB_PD") CONTENT oSection2:Cell("PB_HORAS")	BREAK oBreak TITLE OemToAnsi(STR0033) PICTURE "@E 999,999,999.99"	NO END SECTION 			//"TOTAL UNIT."
EndIf



cPerIni		:= dtos(dPerIni)
cPerFim		:= dtos(dPerFim)

If !lSrvAs400
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz filtro no arquivo...                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP
	
		cAliasSRA := GetNextAlias()
		cAliasSPB := cAliasSRA
	
		//-- Filtro das verbas
		cQVerba 	:= ""
		cFiltraPd	:= "%%"
		
		If !lTodos
			For nReg:=1 to Len(cCodigos) Step 3
				cQVerba += "'"+Subs(cCodigos,nReg,3)+"'"
				If ( nReg+3 ) <= Len(cCodigos)
					cQVerba += "," 
				Endif
			Next nReg
			If !Empty(cQVerba)
				cFiltraPd	:= '% SPB.PB_PD IN('+Upper(cQVerba)+') AND %'
			EndIf	
		Endif   
		     
	

		
		//-- Eliminar o AND da string, sera incluido no select
		//-- Filtro de Situacoes a imprimir
		cSituacao	:= "% SRA.RA_SITFOLH IN("
	   	For nReg:= 1 to Len(cSitAux)  
				cSituacao  += "'"+substr(cSitAux,nReg,1)+"',"
		next nReg
		cSituacao	:= substr(cSituacao,1,len(cSituacao)-1)
		cSituacao	+= ") %"
		
		//-- Filtro de Categorias a imprimir
		cCateg		:= "% SRA.RA_CATFUNC IN("
		For nReg:= 1 to Len(cCatAux)
			cCateg	+= "'"+substr(cCatAux,nReg,1)+"',"
		next nReg
		cCateg		:= substr(cCateg,1,len(cCateg)-1)
		cCateg		+= ") %"
		
		//-- String da ordem e passar conforme a ordem selecionada no relatorio
		If nOrdem == 1						//Matricula
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(1) ) )+', PB_PD %'
		ElseIf nOrdem == 2					//Centro de Custo
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(2) ) )+', PB_PD %'
		ElseIf nOrdem == 3					//Nome
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(3) ) )+', PB_PD %'
		ElseIf nOrdem == 4					//Centro de Custo + Nome
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(8) ) )+', PB_PD %'
		ElseIf nOrdem == 5					//Departamento
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(22) ) )+', PB_PD %'		
		EndIf     
		
		//-- Trata Filial para query
		cFilSpb	:= If(SPB->(xFilial()) == Space(FWGETTAMFILIAL), "% SPB.PB_FILIAL = '" + Space(FWGETTAMFILIAL) + "' %","%  SPB.PB_FILIAL = SRA.RA_FILIAL %")
		cFilSrv := If(SRV->(xFilial()) == Space(FWGETTAMFILIAL), "% SRV.RV_FILIAL = '" + Space(FWGETTAMFILIAL) + "' %","%  SRV.RV_FILIAL = SRA.RA_FILIAL %")
	   
		//-- Query a ser executada para a section 1
		BEGIN REPORT QUERY oSection1
	
			BeginSql alias cAliasSRA                             
	
				SELECT 	RA_FILIAL, RA_MAT, RA_NOME, RA_DEPTO, PB_PD, RV_DESC, PB_HORAS, PB_FILIAL, PB_MAT, RA_SITFOLH, RA_CATFUNC, RA_CC , PB_PROCES , PB_PERIODO , PB_ROTEIR , PB_SEMANA
			
				FROM %table:SPB% SPB, %table:SRA% SRA, %table:SRV% SRV
			
				WHERE 	SRA.RA_MAT = SPB.PB_MAT AND 
						SPB.PB_PD = SRV.RV_COD AND 
						%exp:cFiltraPd% 
						SPB.PB_DATA >= %exp:cPerIni% AND SPB.PB_DATA <= %exp:cPerFim% AND
						%exp:cFilSrv% AND 
						%exp:cFilSpb% AND 
						%exp:cSituacao% AND %exp:cCateg% AND
					   	SPB.%notDel% AND SRA.%notDel% AND SRV.%notDel%
	
				ORDER BY %exp:cOrdem%
	
			EndSql
	
		END REPORT QUERY oSection1 PARAM cRFilial, cRCCusto, cRMat, cRNome ,cRDepto , cCondProc
	
		//Instrucao para utilizar o resultado (dados) do filtro da section 1 para a section 2
		oSection2:SetParentQuery()
	
		//-- Condicao para a impressao da linha
		//-- Pode ser usado uma linecondition para cada uma das sections.
		oSection1:SetLineCondition({|| (cAliasSRA)->( ( RA_FILIAL $ fValidFil() ) .And. Eval( cAcessaSRA ) )} )
	
	#ELSE
Endif
		cAliasSRA := "SRA"
		cAliasSPB := "SPB"
		
		//-- Filial
		If !Empty(cFil)
			cFiltroSRA += cFil
		EndIf
	
		//-- Centro de Custo
		If !Empty(cCCusto)
			cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
			cFiltroSRA += cCCusto
		EndIf
	
		//-- Matricula
		If !Empty(cMat)
			cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
			cFiltroSRA += cMat
		EndIf
	
		//-- Nome
		If !Empty(cNome)
			cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
			cFiltroSRA += cNome
		EndIf
		
		If !Empty(cDepto)
			cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
			cFiltroSRA += cDepto
		EndIf
		
		//-- Situacao
		cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
		cFiltroSRA += ' RA_SITFOLH $cSituacao'
	
		//-- Categoria
		cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
		cFiltroSRA += ' RA_CATFUNC $cCateg'
		
		//-- Monta o as Verbas
		cQVerba 	:= ""
		If !lTodos
			cCodigos 	:= Alltrim(cCodigos)
			For nVer := 1 To Len(cCodigos) Step 3
				cQVerba	+= Substr(cCodigos,nVer,3)
				cQVerba 	+= "*"
			Next               
			cFiltroSPB += 'PB_PD $ "' + cQVerba + '" .AND. '
		Endif
	
		cFiltroSPB	+= 'Dtos(PB_DATA) >= "'+ cPerIni +'".AND. Dtos(PB_DATA) <= "'+ cPerFim +'"'
		
		//-- Executar o setidxorder() para indicar a ordem da tela.
		If nOrdem == 1						//Matricula
			oSection2:SetIdxOrder ( 1 ) 
		ElseIf nOrdem == 2					//Centro de Custo
			oSection2:SetIdxOrder ( 2 ) 
		ElseIf nOrdem == 3					//Nome
			oSection2:SetIdxOrder ( 3 ) 
		ElseIf nOrdem == 4					//Centro de Custo + Nome
			oSection2:SetIdxOrder ( 8 ) 
		ElseIf nOrdem == 5					//Departamento
			oSection2:SetIdxOrder ( 22 )
		EndIf   		
		
		//-- Executa o filtro em CodeBase
		oSection1:SetFilter(cFiltroSRA) 
		oSection2:SetFilter(cFiltroSPB) 
		
		//-- Relacionamento entre as tabelas da section pai com a filha.
		oSection2:SetRelation({|| (cAliasSRA)->RA_FILIAL + (cAliasSRA)->RA_MAT },"SPB",1,.T.)
	
		//-- Condicao para a impressao da linha
		//-- Pode ser usado uma linecondition para cada uma das sections.
		oSection1:SetLineCondition({|| (cAliasSRA)->( ( RA_FILIAL $ fValidFil() ) .And. Eval( cAcessaSRA ) .And. ;
										(cAliasSPB)->(dbseek((cAliasSRA)->RA_FILIAL + (cAliasSRA)->RA_MAT)) )} )

//-- Nao entra em ambiente Banco de Dados quanto estiver utilizando AS/400
If !lSrvAs400
	#ENDIF	
Endif

//-- Relacionamento da section pai com a section filha para determinar ate onde deverao ser impressos os registros
oSection2:SetParentFilter({|cParam| (cAliasSPB)->PB_FILIAL + (cAliasSPB)->PB_MAT == cParam},{|| (cAliasSRA)->RA_FILIAL + (cAliasSRA)->RA_MAT })

//-- Inibe a exibicao das informacoes de funcionarios
If nSinAna == 1
	oSection1:Cell("RA_FILIAL"):Disable()
	oSection1:Cell("RA_CC"):Disable()
	oSection1:Cell("RA_MAT"):Disable()
	oSection1:Cell("RA_NOME"):Disable()
	oSection1:Cell("RA_DEPTO"):Disable()
//	oSection1:Cell("QB_DESCRIC"):Disable()	
Endif

//-- Guarda dados do primeiro registro
cUltFil	:= (cAliasSRA)->RA_FILIAL
cUltCc	:= (cAliasSRA)->RA_CC

//-- Impressao do relatorio
oSection1:Print()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³RepHorizontal³ Autor ³ Ricardo Duarte Costa  ³ Data ³ 21.08.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao do relatorio                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function RepHorizontal(oReport)

//-- String
Local cFiltroSRA	:= ""  
Local cFiltroSPB	:= ""
Local cQVerba		:= ""  
Local cAcessaSRA	:= &( " { || " + ChkRH( "PONR090" , "SRA" , "2" ) + " } " )
Local cPerIni		:= ""
Local cPerFim		:= ""
Local cPDS			:= ""
Local cOrdem		:= ""
Local cFunction		:= If(lSexaCent,"TIMESUM","SUM")
Local cAux			:= "" 
Local cTitFil		:= ""
Local cTitCc		:= ""

//-- Numerica
Local nOrdem   		:= oReport:Section(1):GetOrder()
Local nVer			:= 0
Local nReg			:= 0
Local nCont			:= 0
Local nx			:= 0

//-- Array
Local aInfo			:= {}
                        
//-- Objeto
Private oSection1 	:= oReport:Section(2)

//-- Variaveis montar a descricao dos totais
//Private	cUltFil	:= ""
//Private cUltCc	:= ""

	//-- Totais pela Ordem de Centro de Custo ou Centro de Custo + Nome
	If nOrdem == 2 .Or. nOrdem = 4
//		DEFINE BREAK oBreakCC	OF oReport WHEN oSection1:Cell("RA_CC") 	TITLE {|| STR0045 + STR0025 + fDescTot(1) }		// ### T O T A L ### DO CENTRO DE CUSTO
		oBreakCC 	:= TRBreak():New(oSection1, oSection1:Cell("RA_CC"))
		For nx := 1 to Len(cCodigos)/3
			TRFunction():New(oSection1:Cell("PD"+strzero(nx,2)),,cFunction,oBreakCC,,"@E 9,999,999.99",,.F.,.F.)
		next nx
		TRFunction():New(oSection1:Cell("PDTOTAL"),,cFunction,oBreakCC,,"@E 99,999,999.99",,.F.,.F.)
		oBreakCc:OnBreak({|x,y|cTitCc:=OemToAnsi(STR0051)+x+" - "+fDesc("SI3",x,"SI3->I3_DESC",,oBreak:GetLastValue())})	//"TOTAL DO CENTRO DE CUSTO"
	    oBreakCc:SetTotalText({||cTitCc})

	Endif

//	DEFINE BREAK oBreak	OF oReport WHEN oSection1:Cell("RA_FILIAL")	TITLE {|| STR0045 + STR0026 + fDescTot(2) }			// ### T O T A L ### DA FILIAL
	oBreak 	:= TRBreak():New(oSection1, oSection1:Cell("RA_FILIAL"))	//,{|| STR0052 + " " + fDescTot(2) })

	//-- Totalizacao da Filial
	For nx := 1 to Len(cCodigos)/3
		TRFunction():New(oSection1:Cell("PD"+strzero(nx,2)),,cFunction,oBreak,,"@E 9,999,999.99",,.F.,.T.)
	next nx
	TRFunction():New(oSection1:Cell("PDTOTAL"),,cFunction,oBreak,,"@E 99,999,999.99",,.F.,.T.)
	oBreak:OnBreak({|x,y|fInfo(@aInfo,x),cTitFil:=OemToAnsi(STR0052)+x+" - "+aInfo[1]})	//"TOTAL DA FILIAL"
    oBreak:SetTotalText({||cTitFil})

	cPerIni		:= dtos(dPerIni)
	cPerFim		:= dtos(dPerFim)

	//-- Nao entra em ambiente Banco de Dados quanto estiver utilizando AS/400
	If !lSrvAs400
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz filtro no arquivo...                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP

		cAliasSRA	:= GetNextAlias()
		cAliasSPB	:= cAliasSRA
		
		//-- Filtro das verbas
		cQVerba 	:= ""
		cFiltraPd	:= "%%"
		
		If lTodos
			
			cQrySpb 	:= GetNextAlias()
			cCodigos	:= ""
			
			BeginSql Alias cQrySpb
			
				SELECT DISTINCT (PB_PD)
				FROM %Table:SPB%
			
			EndSql
			
			While (cQrySpb)->( !Eof() )
				If ( Len(cCodigos) < 30 )
					cCodigos += (cQrySpb)->(PB_PD)
				Else
					Exit
				EndIf
				(cQrySpb)->(dbSkip())
			EndDo
			
		EndIf

		//-- Filtro das verbas
		cPDS		:= "%"
		nCont		:= 0
		For nReg:=1 to Len(cCodigos) Step 3
			If ( nReg+2 ) <= Len(cCodigos)
				nCont ++
				cPDS	+= ", (SELECT SUM(SPB.PB_HORAS) FROM "+RetSqlName("SPB")+" SPB where SPB.PB_FILIAL = SRA.RA_FILIAL "
				cPDS	+= "and SPB.PB_MAT = SRA.RA_MAT and SPB.PB_PD = '"+Subs(cCodigos,nReg,3)
				cPDS	+= "' and SPB.PB_DATA BETWEEN '"+cPerIni+"' AND '"+cPerFim+"' AND SPB.D_E_L_E_T_ = ' ' ) "
				cPDS	+= "PD"+strzero(nCont,2)+" "
			Endif
		Next nReg
		cPDS	+= "%"
		
		//-- Filtro de Situacoes a imprimir
		cSituacao	:= "% SRA.RA_SITFOLH IN("
		For nReg:= 1 to Len(cSitAux)
			cSituacao  += "'"+substr(cSitAux,nReg,1)+"',"
		next nReg
		cSituacao	:= substr(cSituacao,1,len(cSituacao)-1)
		cSituacao	+= ") %"
		
		//-- Filtro de Categorias a imprimir
		cCateg		:= "% SRA.RA_CATFUNC IN("
		For nReg:= 1 to Len(cCatAUx)
			cCateg	+= "'"+substr(cCatAux,nReg,1)+"',"
		next nReg
		cCateg		:= substr(cCateg,1,len(cCateg)-1)
		cCateg		+= ") %"
		
		//-- String da ordem e passar conforme a ordem selecionada no relatorio
		If nOrdem == 1						//Matricula
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(1) ) )+' %'
		ElseIf nOrdem == 2					//Centro de Custo
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(2) ) )+' %'
		ElseIf nOrdem == 3					//Nome
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(3) ) )+' %'
		ElseIf nOrdem == 4					//Centro de Custo + Nome
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(8) ) )+' %'
		ElseIf nOrdem == 5					//Departamento
			cOrdem	:= '%'+SqlOrder( SRA->( IndexKey(22) ) )+' %'		
		EndIf
		
		//-- Query a ser executada para a section 1
		BEGIN REPORT QUERY oSection1
	
			BeginSql alias cAliasSRA                             
	
				SELECT SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_DEPTO, SRA.RA_SITFOLH, SRA.RA_CATFUNC
						%exp:cPDS%
			
				FROM %table:SRA% SRA
			
				WHERE 	%exp:cSituacao% AND %exp:cCateg% AND 
					   	SRA.%notDel%
	
				ORDER BY %exp:cOrdem%
	
			EndSql
		END REPORT QUERY oSection1 PARAM cRFilial, cRCCusto, cRMat, cRNome , cRDepto , cCondProc

		//-- Condicao para a impressao da linha
		//-- Pode ser usado uma linecondition para cada uma das sections.
		oSection1:SetLineCondition({|| (cAliasSRA)->( ( RA_FILIAL $ fValidFil() ) .And. Eval( cAcessaSRA ) .And. ;
										fCondLinha() ) } )

	#ELSE                      
	Endif
		cAliasSRA := "TRBSRA"
		cAliasSPB := "SPB"
		
		//-- Filial
		If !Empty(cFil)
			cFiltroSRA += cFil
		EndIf

		//-- Centro de Custo
		If !Empty(cCCusto)
			cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
			cFiltroSRA += cCCusto
		EndIf

		//-- Matricula
		If !Empty(cMat)
			cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
			cFiltroSRA += cMat
		EndIf

		//-- Nome
		If !Empty(cNome)
			cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
			cFiltroSRA += cNome
		EndIf

		If !Empty(cDepto)
			cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
			cFiltroSRA += cDepto
		EndIf

		//-- Situacao
		cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
		cFiltroSRA += ' RA_SITFOLH $cSituacao'

		//-- Categoria
		cFiltroSRA += Iif(!Empty(cFiltroSRA)," .AND. ","")
		cFiltroSRA += ' RA_CATFUNC $cCateg'
		
		//-- Gera arquivo temporario com os dados do SPB
		GeraTRBSRA(cFiltroSRA,cAcessaSRA,nOrdem,cPerIni,cPerFim)
		
		//-- Executar o setidxorder() para indicar a ordem da tela.
		If nOrdem == 1						//Matricula
			oSection1:SetIdxOrder ( 1 ) 
		ElseIf nOrdem == 2					//Centro de Custo
			oSection1:SetIdxOrder ( 2 ) 
		ElseIf nOrdem == 3					//Nome
			oSection1:SetIdxOrder ( 3 ) 
		ElseIf nOrdem == 4					//Centro de Custo + Nome
			oSection1:SetIdxOrder ( 8 ) 
		ElseIf nOrdem == 5					//Departamento
			oSection1:SetIdxOrder ( 22 )
		EndIf

	//-- Nao entra em ambiente Banco de Dados quanto estiver utilizando AS/400
	If !lSrvAs400
	#ENDIF	
	Endif
	
	//-- Ajuste do titulo das colunas
	nCont := 0
	For nx := 1 to Len(cCodigos) step 3
		nCont ++
		If ( nx+2 ) <= Len(cCodigos)
			cAux	:= FVerba(Substr(cCodigos,nx,3),14,(cAliasSRA)->RA_FILIAL,.F.)
			cAux	:= Substr(cAux,1,09)+CRLF+Substr(cAux,10,09)
			oSection1:Cell("PD"+strzero(nCont,2)):SetTitle(cAux)
		Endif
	next nx
	
	//-- Desabilita as demais celulas pois as mesmas nao serao impressas
	For nReg := nCont + 1 to 10
		oSection1:Cell("PD"+strzero(nReg,2)):Disable()
		oSection1:Cell("PD"+strzero(nReg,2)):SetBlock({|| 0.00})
	Next nReg

	//-- Inibe a exibicao das informacoes de funcionarios
	If nSinAna == 1
		oSection1:Cell("RA_FILIAL"):Disable()
		oSection1:Cell("RA_CC"):Disable()
		oSection1:Cell("RA_MAT"):Disable()
		oSection1:Cell("RA_NOME"):Disable()
		oSection1:Cell("RA_DEPTO"):Disable()
	Endif

	//-- Guarda dados do primeiro registro
	cUltFil	:= (cAliasSRA)->RA_FILIAL
	cUltCc	:= (cAliasSRA)->RA_CC
	
	//-- Impressao do relatorio
	oSection1:Print()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fSomaTot       ³ Autor ³ Ricardo Duarte   ³ Data ³05/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao que retorna a coluna PDTOTAL                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ A partir do Release 4                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fSomaTot(cTipo)

Local nValor	:= 0
Local nx		:= 0

For nx := 1 to 10
	If cTipo == "H"
		nValor := __TimeSum(nValor,oSection1:Cell("PD"+strzero(nx,2)):GetValue(.T.))
	Else
		nValor += oSection1:Cell("PD"+strzero(nx,2)):GetValue(.T.)
	Endif
Next nx

Return(nValor)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fCondLinha     ³ Autor ³ Ricardo Duarte   ³ Data ³23/08/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Condicao para imprimir a linha horizontal                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ A partir do Release 4 - SO PARA TOP                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
#IFDEF TOP
	Static Function fCondLinha()
	
	Local nValor	:= 0
	Local nx		:= 0
	
	For nx := 1 to Len(cCodigos)/3
		nValor += &("PD"+strzero(nx,2))
	Next nx
	
	Return(nValor>0)
#ENDIF

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ CriaTRBSRA    ³ Autor ³ Ricardo Duarte   ³ Data ³11/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria o arquivo temporario para a impressao horizontal.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ A partir do Release 4                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function CriaTRBSRA()
	
	Local aArea		:= GetArea()
	Local aCampos	:= {}
	Local aTamFil	:= TamSx3("RA_FILIAL ")
	Local aTamCC	:= TamSx3("RA_CC     ")
	Local aTamMat	:= TamSx3("RA_MAT    ")
	Local aTamNome	:= TamSx3("RA_NOME   ")
	Local aTamDepto := TamSx3("RA_DEPTO  ")
	Local aTamHoras	:= TamSx3("PB_HORAS  ")
	Local nx		:= 0
	Local aLista    := {}
	Local aLstIndices := {}
	
	aAdd( aCampos, { "RA_FILIAL"	,"C",aTamFil[1]		,aTamFil[2] } )
	aAdd( aCampos, { "RA_CC"		,"C",aTamCC[1]		,aTamCC[2] } )
	aAdd( aCampos, { "RA_MAT"		,"C",aTamMat[1]		,aTamMat[2] } )
	aAdd( aCampos, { "RA_NOME"		,"C",aTamNome[1]	,aTamNome[2] } )
	aAdd( aCampos, { "RA_DEPTO"     ,"C",aTamDepto[1]   ,aTamDepto[2] } )
	
	For nx := 1 to 10
		aAdd( aCampos, { "PD"+STRZERO(NX,2),"N",aTamHoras[1],aTamHoras[2] } )
	next nx
	
	aLista :=StrToArray(SRA->( IndexKey(1) ), "+")
	AAdd( aLstIndices, aLista)
	aLista :=StrToArray(SRA->( IndexKey(2) ), "+")
	AAdd( aLstIndices, aLista)
	aLista :=StrToArray(SRA->( IndexKey(3) ), "+")
	AAdd( aLstIndices, aLista)
	aLista :=StrToArray(SRA->( IndexKey(8) ), "+")
	AAdd( aLstIndices, aLista)
	aLista :=StrToArray(SRA->( IndexKey(22) ), "+")
	AAdd( aLstIndices, aLista)
	
	oTmpTRBSRA := RhCriaTrab('TRBSRA', aCampos, aLstIndices)
	
	dbSelectArea( "TRBSRA" )
	dbSetOrder(1)
	
	RestArea(aArea)
	
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GeraTRBSRA    ³ Autor ³ Ricardo Duarte   ³ Data ³11/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera o arquivo temporario para a impressao horizontal.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ A partir do Release 4                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GeraTRBSRA(cFiltroSRA,cAcessaSRA,nOrdem,cPerIni,cPerFim)

Local nVer		:= 0

Dbselectarea("SPB")
Dbsetorder(1)

Dbselectarea("SRA")
If nOrdem == 1
	Dbsetorder(1)
ElseIf nOrdem == 2
	Dbsetorder(2)
ElseIf nOrdem == 3
	Dbsetorder(3)
ElseIf nOrdem == 4
	Dbsetorder(8)
ElseIf nOrdem == 5
	Dbsetorder(22)
Endif

SRA->(DbGotop())

While !SRA->(Eof())	

	//-- Testa matricula e grava o arquivo temporario caso encontre quantidade de horas a imprimir.
	lImpFunc := SRA->(&cFiltroSRA) .And. SRA->( ( RA_FILIAL $ fValidFil() ) .And. Eval( cAcessaSRA ) )
	If lImpFunc

		//-- Pesquisa as verbas no movimento
		cQVerba		:= ""
		nValPD01	:= 	nValPD02	:= 	nValPD03	:= 	nValPD04	:= 	nValPD05	:= 0
		nValPD06	:= 	nValPD07	:= 	nValPD08	:= 	nValPD09	:= 	nValPD10	:= 0
		nValTot		:= 	nCont		:= 	nAux		:= 0
		cCodigos 	:= Alltrim(cCodigos)
		cAux		:= ""
		
		For nVer := 1 To Len(cCodigos) Step 3
			nCont	++
			nAux	:= 0
			cQVerba	:= Substr(cCodigos,nVer,3)
			cAux	:= ""
			If SPB->(Dbseek(SRA->(RA_FILIAL+RA_MAT)+cQVerba))
				While !SPB->(Eof()) .And. SRA->(RA_FILIAL+RA_MAT)+cQVerba == SPB->(PB_FILIAL+PB_MAT+PB_PD)
					If SPB->( Dtos(PB_DATA) >= cPerIni .AND. Dtos(PB_DATA) <= cPerFim )
						nAux	+= SPB->PB_HORAS
					Endif
					SPB->(Dbskip())
				Enddo
				cAux	:= "NVALPD"+strzero(nCont,2)
				&(cAux)	:= nAux
			Endif
		Next nVer

		nValTot	:= nValPD01	+ nValPD02 + nValPD03 + nValPD04 + nValPD05
		nValTot	+= nValPD06	+ nValPD07 + nValPD08 + nValPD09 + nValPD10

		//-- Gero registro do TRBSRA quando tiver horas a listas
		If nValTot > 0
			RecLock("TRBSRA",.T.)
			TRBSRA->RA_FILIAL	:= SRA->RA_FILIAL
			TRBSRA->RA_CC		:= SRA->RA_CC
			TRBSRA->RA_MAT		:= SRA->RA_MAT
			TRBSRA->RA_NOME		:= SRA->RA_NOME
			TRBSRA->RA_DEPTO	:= SRA->RA_DEPTO
			TRBSRA->PD01		:= nValPD01
			TRBSRA->PD02		:= nValPD02
			TRBSRA->PD03		:= nValPD03
			TRBSRA->PD04		:= nValPD04
			TRBSRA->PD05		:= nValPD05
			TRBSRA->PD06		:= nValPD06
			TRBSRA->PD07		:= nValPD07
			TRBSRA->PD08		:= nValPD08
			TRBSRA->PD09		:= nValPD09
			TRBSRA->PD10		:= nValPD10
			TRBSRA->(MsUnlock())
		Endif

	Endif
	SRA->(Dbskip())

Enddo

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fDescTot  ºAutor  ³Ricardo Duarte Costaº Data ³  14/10/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a descricao do centro de custos e da filial para o  º±±
±±º          ³total impresso nas quebras.                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ PONR090                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/ 
/*
Static Function fDescTot(nTipo,cUltFil)

Local aInfo	:= {}
Local cRet	:= ""

If nTipo == 1	// Centro de Custos
	cRet	:= cUltCc + " " + Left(fDesc("SI3",cUltCc,"SI3->I3_DESC",,cUltFil)  + Space(15),15)
	cUltCc	:= (cAliasSRA)->RA_CC
ElseIf nTipo == 2	// Filial
	fInfo(@aInfo,cUltFil)
	cRet	:= cUltFil + " " + aInfo[1]
	cUltFil	:= (cAliasSRA)->RA_FILIAL
Endif

Return(cRet)
*/

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±±
±±³Funcao    ³PONR090R3³ Autor ³ Mauricio MR            ³ Data ³  20/08/06  ³±±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±±
±±³Descricao ³ Relatorio de resultados do ponto eletronico por codigo de    ³±±±
±±³          ³ verba.                                                       ³±±±
±±³          ³                                                              ³±±±
±±³          ³                                                              ³±±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±±
±±³Sintaxe   ³ PONR090R3()                                                  ³±±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±±
±±³ Uso      ³ Advanced Protheus                                            ³±±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/               
Function PonR090R3()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cDesc1     := STR0001  // 'Relatorio de Resultados'
Local cDesc2     := STR0002  // 'Ser  impresso de acordo com os parametros solicitados pelo'
Local cDesc3     := STR0003  // 'usuario.'
Local cString    := 'SRA' 	 //  Alias do arquivo principal (Base)
Local aOrd       := {STR0004 , STR0005 , STR0006 , STR0028 } // 'Matricula'###'Centro de Custo'###'Nome'###'C.Custo+Nome'
Local aRegs   	 := {}      //  Perguntas complementares do SX1
Local nFor		 := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private(Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aReturn    := {STR0007 , 1, STR0008 , 2, 2, 1, '',1 } // 'Zebrado'###'Administra‡„o'
Private nomeprog   := 'PONR090'
Private aLinha     := {}
Private nLastKey   := 0
Private cPerg      := 'PNR090'
Private aAC        := {STR0010 , STR0011 } // 'Abandona'###'Confirma'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private(Programa)                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private nOrdem     := 0
Private aInfo      := {}
Private aTotais    := {}
Private dPerMVIni  := CTOD(SPACE(8))
Private dPerMVFim  := CTOD(SPACE(8))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Vetor de Totalizacao Generico 1 Coluna = CODIGO                    ³
//³                              2 Coluna = Total Horas do Funcionario³
//³                              3 Coluna = Total Valor do Funcionario³
//³                              4 Coluna = Total Horas Centro Custo  ³
//³      aTotais                 5 Coluna = Total Valor Centro Custo  ³
//³                              6 Coluna = Total Horas Filial        ³
//³                              7 Coluna = Total Valor Filial        ³
//³                              8 Coluna = Total Geral Horas         ³
//³                              9 Coluna = Total Geral Valor         ³
//³                             10 Coluna = Descr.Codigo p/Total Empr ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis Utilizadas na funcao IMPR                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private Titulo     := ''
Private At_Prg     := 'PONR090'
Private wCabec0    := 0
Private wCabec1    := ''
Private wCabec2    := ''
Private ContFl     := 1
Private LI         := 0
Private Colunas    := 132
Private nTamanho   := 'M'
Private nChar      := 15
Private lRoda	   := .F.	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg, .F.)

cTit := STR0012  // 'RESULTADOS POR CODIGO'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnRel:= 'PONR090' //-- Nome Default do relatorio em Disco
wnRel:= SetPrint(cString, wnRel, cPerg, @cTit, cDesc1, cDesc2, cDesc3, .F., aOrd,,nTamanho)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define a Ordem de Impressao                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOrdem     := aReturn[8]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFilDe     := mv_par01				//Filial  De
cFilAte    := mv_par02				//Filial  Ate
cCCDe      := mv_par03				//Centro de Custo De
cCCAte     := mv_par04				//Centro de Custo Ate
cMatDe     := mv_par05				//Matricula De
cMatAte    := mv_par06				//Matricula Ate
cNomeDe    := mv_par07				//Nome De
cNomeAte   := mv_par08				//Nome Ate
nSinAna    := mv_par09				//Relatorio Analitico ou Sintetica
cSituacao  := mv_par10				//Situacoes a Imprimir
cCateg     := mv_par11				//Categorias a Imprimir
lTodos     := (mv_par12==1)			//Se lista todos os codigos encontrados
cCodigos   := mv_par13				//Codigos a Listar
nVerHor    := IF(lTodos,1,mv_par14) //Formato de Impressao
lConsidPer := (mv_par15==1)			//Considera Periodo de Apontamento
dPerMVIni  := mv_par16				//Periodo Inicial
dPerMVFim  := mv_par17				//Periodo Final
lSexaCent  := mv_par18				//Horas em  (Sexagenal/Centesimal)
//-- Nao Processa se Data inicial do Periodo superior a Data Final
If dPerMVIni > dPerMVFim
   Return Nil
Endif

Titulo := OemToAnsi(STR0013 ) // 'RESULTADOS DO PONTO ELETRONICO'

If	nLastKey == 27
	Return Nil
Endif

//- Cria as verbas solicitadas no vetor de totalizacao
If cCodigos # Space(10)
	For nFor := 1 To Len(ALLTRIM(cCodigos)) Step 3
		cVerba := Subs(cCodigos,nFor,3)
		Aadd(aTotais,{cVerba,0,0,0,0,0,0,0,0,''})
	Next
Endif

If nVerHor == 2
	If Len(aTotais) > 15
		Help(' ', 1, 'R100MAIO15')
		Return
	Endif
	If Len(aTotais) > 7
		nOpca := 0
		If MsgYesNo(OemToAnsi(STR0017 ),OemToAnsi(titulo)) // " preciso Compactar a Impressora para 18 CPI ?"
			nOpca := 2
		Endif

		If nOpcA == 2
			aReturn[4] := 1
			nTamanho   := "G"
			COLUNAS    := 220
		Else
			Return Nil
		Endif
	Endif
Endif

wCabec0 := 2
If nVerHor = 1
	wCabec1 := STR0018  // '                                                       |-      VERBA        -|'
	wCabec2 := STR0019  // 'FI C.CUSTO   MATR.  NOME                                 COD DESCRICAO           HORAS                  '
Else
	aTotais := aSort(aTotais,,, { |x,y| x[1] < y[1] })
	wCabec1 := STR0020  // 'FI C.CUSTO   MATR.  NOME'
	wCabec1 += Space(27)
	wCabec2 := Space(62)
	For nFor := 1 To Len(aTotais)
		WCabec1 += FVerba(aTotais[nFor,1], 5, cFilial) + ' '
		WCabec2 += STR0021  + ' ' // '    HORAS'
	Next
	WCabec2 += STR0022  // 'T O T A L'
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Passa parametros de controle da impressora ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetDefault(aReturn,cString)

If	nLastKey == 27
	Return Nil
Endif

RptStatus({|lEnd| PO090Imp(@lEnd, wnRel, cString)}, Titulo)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ PONR090  ³ Autor ³ R.H. - Ze Maria       ³ Data ³ 03.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio por Codigos                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ PNR090Imp(lEnd,wnRel,cString)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd        - A‡Æo do Codelock                             ³±±
±±³          ³ wnRel       - T¡tulo do relat¢rio                          ³±±
±±³Parametros³ cString     - Mensagem			                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PO090Imp(lEnd,wnRel,cString)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cbTxt //-- Ambiente
Local cbCont
Local dPerIni    := Ctod("  /  /  ")
Local dPerFim    := Ctod("  /  /  ")
Local cAcessaSRA := &("{ || " + ChkRH("PONR090","SRA","2") + "}")
Local lAcumulou  //-- Verificador se houve totalizacao de valores de funcionario


//-- Parƒmetro MV_PAPONTA
//-- Se Considerar Periodo de Apontamento
If lConsidPer
   If !PerAponta(@dPerIni,@dPerFim)
    	Return Nil
   Endif
Endif

dbSelectArea('SRA')
dbGoTop()

If nOrdem == 1
	dbSetOrder(1)
	dbSeek(cFilDe + cMatDe,.T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_MAT'
	cFim     := cFilAte + cMatAte
ElseIf nOrdem == 2
	dbSetOrder(2)
	dbSeek(cFilDe + cCCDe + cMatDe,.T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT'
	cFim     := cFilAte + cCCAte + cMatAte
ElseIf nOrdem == 3
	dbSetOrder(3)
	dbSeek(cFilDe + cNomeDe + cMatDe,.T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT'
	cFim     := cFilAte + cNomeAte + cMatAte
ElseIf nOrdem == 4
	dbSetOrder(8)
	dbSeek(cFilDe + cCcDe + cNomeDe,.T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_NOME'
	cFim     := cFilAte + cCcAte + cNomeAte
Endif

cFilAnterior := Replicate('!', FWGETTAMFILIAL)
cCcAnt  := '!!!!!!!!!'

dbSelectArea('SRA')
SetRegua(SRA->(RecCount()))

While	!Eof() .And. &cInicio <= cFim
	
	IncRegua()

	If lEnd
		IMPR(cCancela,'C')
		Exit
	Endif	 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a Mudanca de filial								 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If	Sra->ra_Filial # cFilAnterior
		If	cFilAnterior # Replicate('!', FWGETTAMFILIAL)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ So Imprime se Existirem Informacoes da Empresa				 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			IF !Empty( aInfo )
				fImpFil() //-- Totaliza Filial
			EndIF	
		Endif
		cFilAnterior := SRA->RA_FILIAL
		cCcAnt       := SRA->RA_CC
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Carrega Informacoes da Empresa								 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF !fInfo(@aInfo,cFilAnterior)
			fTestaTotal()
			Loop
		EndIF
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste controle de acessos e filiais validas               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SRA->( !(RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) )
		fTestaTotal()
		Loop
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste Parametrizacao do Intervalo de Impressao            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (SRA->RA_FILIAL < cFilDe) .Or. (SRA->RA_FILIAL > cFilAte) .Or. ;
		(SRA->RA_NOME < cNomeDe) .Or. (SRA->RA_NOME > cNomeAte) .Or. ;
		(SRA->RA_MAT < cMatDe) .Or. (SRA->RA_MAT > cMatAte) .Or. ;
		(SRA->RA_CC < cCCDe) .Or. (SRA->RA_CC > cCCAte)
		fTestaTotal()
		Loop
	EndIf
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Despreza Registros Conforme Situacao e Categoria Funcionarios³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If	!( SRA->RA_SITFOLH $ cSituacao ) .OR.  !( SRA->RA_CATFUNC $ cCateg )
		fTestaTotal()		
		Loop
	Endif
	
	dbSelectArea("SPB")
	If	dbSeek(fFilFunc('SPB') + Sra->ra_Mat)
	    
		lAcumulou:=.F.
		Do While	!Eof() .And. (SPB->PB_Filial+SPB->PB_Mat == fFilFunc('SPB')+Sra->ra_Mat)
			//-- Se Nao considerar o Periodo de Apontamento		
			If ! lConsidPer                         
			   //-- NAO Consiste Data para Periodo deixado em Branco  ou
			   //-- Consiste Data se Periodo Especificado 
			   If ( Empty(dPerMVIni) .And. Empty(dPerMVFim) ) .OR.;
			      (SPB->PB_DATA >= dPerMVIni .And. SPB->PB_DATA <= dPerMVFim) 
			   	  fAcumula(SPB->PB_PD,SPB->PB_HORAS,SPB->PB_VALOR, @lAcumulou)
			   Endif                                                                        
			//-- Se Considerar Periodo de Apontamento consiste data    
			ElseIf lConsidPer .And. (SPB->PB_DATA >= dPerIni .And. SPB->PB_DATA <= dPerFim)
			 	   fAcumula(SPB->PB_PD,SPB->PB_HORAS,SPB->PB_VALOR, @lAcumulou)
			Endif
			dbSkip()
		Enddo
		//-- Imprime Acumulados de Funcionario se houve acumulo
		If lAcumulou	
  			fImpFun()
  		Endif	
	Endif
   	dbSelectArea("SRA")
	fTestaTotal()
Enddo

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Imprime Rodape no Final da Ultima Pagina                     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF lRoda
	IF Li < 58
		Li := 58
	EndIF
	Impr("","F")
EndIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Termino do relatorio                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SRA")
dbSetOrder(1)

Set Device To Screen
If aReturn[5] = 1
	Set Printer To
	Commit
	ourspool(wnrel)
Endif
MS_FLUSH()

********************************
Static Function fTestaTotal()      // Executa Quebras
********************************
cCcAnt       := SRA->RA_CC
cFilAnterior := SRA->RA_FILIAL

dbSelectArea("SRA")
dbSkip()

IF !Empty( ainfo )
	If Eof() .Or. &cInicio > cFim
		fImpCc()
		fImpFil()
		fImpEmp()
	Elseif cFilAnterior # SRA->RA_FILIAL
		fImpCc()
		fImpFil()
	Elseif cCcAnt # SRA->RA_CC .And. !Eof()
		fImpCc()
	Endif
EndIF

Return( NIL )

***********************
Static Function fImpFun            // Imprime um Funcionario
***********************
If nSinAna = 2 // Se Relatorio e' Analitico
	fImprime(1,2)
Endif
FZera(2) // Zerar a Coluna de Funcionarios
Retu Nil

**********************
Static Function fImpCc             // Imprime Centro de Custo
**********************
If (nOrdem ==  2 .Or. nOrdem == 4) .AND. FTotaliza(4) + FTotaliza(5) > 0
	fImprime(2,4) // Imprime
Endif
FZera(4)
Retu Nil

***********************
Static Function fImpFil            // Imprime Filial
***********************
//--Imprime Total Filial se ocorrer
If (FTotaliza(6) + FTotaliza(7)) > 0
	fImprime(3,6)
Endif	
FZera(6)
Retu Nil

***********************
Static Function fImpEmp            // Imprime Geral
***********************
fImprime(4,8)
FZera(8)
Retu Nil

************************************
Static Function fImprime(nTipo,nCol)
************************************
// nTipo: 1- Funcionario
//        2- Centro de Custo
//        3- Filial
//        4- Geral

If nTipo == 1
	cDet:= Sra->ra_Filial + " " +SUBS(Sra->ra_CC+SPACE(20),1,20) + " " + Sra->ra_MAT + " " + Left(Sra->ra_NOME,30)
	FImpDet(2,3,1)
Elseif nTipo == 2
	If nVerHor = 2 // Se e' horizontal
		IMPR(REPL("-",COLUNAS),"C")
	Endif
	cDet:= cFilAnterior + " " + SUBS(cCcAnt+SPACE(20),1,20) + " - " + DescCC(cCcAnt,Sra->ra_Filial,20) + Space(15)
	FImpDet(4,5,2)
Elseif nTipo == 3
	cDet:= cFilAnterior + " " + Subs(aInfo[1],1,15) + Space(43)//33
	FImpDet(6,7,3)
	IMPR("","P")                // Salta Pagina apos Quebra Cc/Filial/Empresa
Elseif nTipo == 4
	cDet:= STR0023  + Subs(aInfo[3],1,33)+ SPACE (19) // "Empresa: "
	FImpDet(8,9,4)
	IMPR("","P")                // Salta Pagina apos Quebra Cc/Filial/Empresa
Endif

Return( NIL )

*******************************************
Static Function FAcumula(cPd,nHoras,nValor,lAcumulou,cFil)
*******************************************
Local n := 0 

n := Ascan(aTotais,{ |x| x[1] = cPd } )
If n = 0 .AND. lTodos
	//--Flag para verificar se houve acumulo de Valores para o Funcionario
	lAcumulou:=.T.
	Aadd(aTotais,{cPd,nHoras,nValor,nHoras,nValor,nHoras,nValor,nHoras,nValor,;
	              DescPdPon(cPd,cFil,15)})
ElseIf n > 0
	aTotais[n,2] += nHoras
	aTotais[n,3] += nValor
	aTotais[n,4] += nHoras
	aTotais[n,5] += nValor
	aTotais[n,6] += nHoras
	aTotais[n,7] += nValor
	aTotais[n,8] += nHoras
	aTotais[n,9] += nValor
	//--Flag para verificar se houve acumulo de Valores para o Funcionario
	lAcumulou:=.T.
Endif
Return Nil

***************************
Static Function FZera(nCol)
***************************
Local nFor	:= 0

For nFor := 1 To Len(aTotais)
	aTotais [nFor,nCol]   := 0   // Zera Totais de horas
	aTotais [nFor,nCol+1] := 0   // Zera Totais de Valores
Next
Return Nil

*************************************
Static Function FTotaliza(nCol,lSexaCent)
*************************************
Local nTot 			:= 0
DEFAULT lSexaCent 	:= 2 //-- Centesimal

//-- Quando Opta por imprimir horas em escala sexagesimal, transformamos os valores centisimais (padrao Resultados) para sexagesimal e 
//-- utilizamos a funcao __TimeSum(Somahoras) para totalizar as horas do evento
aEval(aTotais,{ |x| If(lSexaCent == 1, nTot :=__TimeSum(nTot,fConvHr(x[nCol], 'H')) , nTot += x[nCol]) })
Return nTot

********************************************
Static Function fImpDet(nCol1, nCol2, nMsgTot)
********************************************

Local lImprime := .F.
Local aMsg     := {}
Local nX       := 0
Local nHoras   := 0

IF nVerHor == 1 //-- Vertical
	aSort(aTotais,,, { |x,y| x[1] < y[1] })
	//-- Garante que os Detalhes serao impressos na proxima Linha
	IF !Empty( cDet )
		Impr( cDet , "C" , NIL , NIL , NIL , .T. )
		cDet := Space(71)
	EndIF
EndIF

aMsg := {}
aAdd(aMsg, STR0024  ) // 'D O     F U N C I O N A R I O      '
aAdd(aMsg, STR0025  ) // 'D O     C E N T R O  D E  C U S T O'
aAdd(aMsg, STR0026  ) // 'D A     F I L I A L                '                      
aAdd(aMsg, STR0027  ) // 'D A     E M P R E S A              '

//-- Imprime Eventos
For nX := 1 To Len(aTotais)
	If nVerHor == 1  //-- Vertical
		If aTotais[nX,nCol1] + aTotais[nX,nCol2] > 0
			cDet += Space(5) + FVerba(aTotais[nX,1],15,cFilAnterior,.F.)
			cDet += Space(1) + Transform( If( lSexaCent==1, fConvHr(aTotais[nX,nCol1],'H'), aTotais[nX,nCol1] ), '@E 999999.99')
			Impr(cDet,'C')
			cDet := Space(71)
		Endif
	Else //-- Horizontal
		cDet += Space(1) + Transform(If( lSexaCent==1, fConvHr(aTotais[nX,nCol1],'H'), aTotais[nX,nCol1] ), '@E 999999.99')
	Endif
Next nX

//-- Imprime Totais
nHoras:= fTotaliza(nCol1, lSexaCent)
If nVerHor == 1 //-- Vertical
	cDet := Space(6) + STR0022  +Space(5) + aMsg[nMsgTot] + Space(10) + '>>>>>' + Space(26) // 'T O T A L'
	cDet += Transform(nHoras, '@E 999999.99')
	If nHoras > 0
		Impr(Space(1), 'C')
		Impr(cDet, 'C')
		Impr(Replicate('-', Colunas),'C')
	Endif
Else //-- Horizontal
	If nHoras > 0
		cDet += Space(1) + Transform(nHoras, '@E 999999.99')
		Impr(cDet,'C')
		If nMsgTot == 2
			Impr(REPL('-',Colunas),'C')
		Endif
	Endif
Endif

lRoda := .T.

Return( NIL )

**************************************
Static Function FVerba(cCod,nTam,cFil,lAlinha)
**************************************
Local cRet

DEFAULT lAlinha := .T.

cRet := cCod + '-' + fDesc("SRV",cCod,"RV_DESC",nTam,cFil)

IF lAlinha
	cRet := AllTrim(cCod + '-' + fDesc("SRV",cCod,"RV_DESC",nTam,cFil) )
	cRet := Space(nTam+4-Len(cRet)) + cRet
EndIF	

Return( cRet )
/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr090Proc	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR01 para variavel private		³
|		   |cProcesso    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr090Proc(cTip)

Local lRet := .T.

cProcesso := &( ReadVar() )

If ( cTip == 'A' )
	lRet := PonX1Valid(cProcesso)
Else
	If !Empty(cProcesso)
		lRet := PonX1Valid(cProcesso)
	EndIf
EndIf

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr090Per	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR02 para variavel private		³
|		   |cPeriodo    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr090Per()

Local cChave := cProcesso
Local lRet := .T.

If !Empty(cRoteiro)
	cChave += cRoteiro
Else
	cChave += "PON"
EndIf

cPeriodo := &( ReadVar() )

cChave += cPeriodo

lRet := PonX1Valid(cChave)

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr090Rot	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR03 para variavel private		³
|		   |cRoteiro    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr090Rot()

Local lRet := .T.

cRoteiro := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro)

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr090NPag	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR04 para variavel private		³
|		   |cNumPag	    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr090NPag()

Local lRet := .T.

cNumPagto := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro+cPeriodo+cNumPagto)

Return( lRet )