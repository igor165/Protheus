#INCLUDE "PONR070.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "REPORT.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ PONR070  ³ Autor ³ R.H. - J. Ricardo     ³ Data ³ 23.08.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Controle de Refeicoes                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONR070(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Fernando J. ³20.02.98³------³Zerar total de Refei‡”es a cada func.     ³±±
±±³Fernando J. ³25.03.98³------³Perguntas para Limitar Intervalo de Horas,³±±
±±³            ³        ³      ³que quando zeradas faz com que o sistema  ³±±
±±³            ³        ³      ³considere os Limites do Turno de Trabalho.³±±
±±³Aldo	  	   ³08/10/98³------³ Acerto na Ordem No.4 - Turno (SRA).      ³±±
±±³Aldo	  	   ³06/01/99³------³ Alteracao da Data para 4 digitos.        ³±±
±±³Aldo	  	   ³30/03/99³------³ Passagem de nTamanho para SetPrint().    ³±±
±±³Aldo	  	   ³10/09/99³05622A³ Incluido ordem de C.Custo+Nome           ³±±
±±³Alexsandro  ³09/05/00³------³ Remocao do codigo DOS.                   ³±±
±±³Alexsandro  ³03/08/00³------³ Implementacao formato data universal.    ³±±
±±³Alexsandro  ³03/08/00³------³ Implementacao controle de acesso.        ³±± 
±±³Mauricio MR ³22/08/01³------³ Implementacao controle de Tipos de Rfeic.³±±
±±³ Priscila R.³05/06/02³------³Ajuste no relatorio para que seja impresso³±± 
±±³            ³--------³------³corretamente o C.C no tamanho 20.         ³±± 
±±³Mauricio MR ³04/12/02³------³Retirada a restricao de existencia de cra-³±± 
±±³            ³--------³------³cha para a emissao do relatorio.	      ³±± 
±±³Mauricio MR ³23/04/03³------³Correcao de Totalizacao de Filial/Empresa ³±± 
±±³            ³        ³------³e impressao de cabecalho.                 ³±± 
±±³=======================================================================³±± 
±±³                         *** Versao 9.12 ***                           ³±± 
±±³=======================================================================³±± 
±±³Leandro Dr  ³02/04/08³Melhor³Inclusão de chave processo/periodo/roteiro³±± 
±±³            |        |      |NumPagto. Impressao por data ou periodo   ³±±
±±³Francisco Jr³18/01/10³001029³Inclusão de chave processo/periodo/roteiro³±± 
±±³            |        |      |NumPagto. Impressao por data ou periodo   ³±±  
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programador  ³ Data     ³ FNC            ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³±±
±±³Aldo Marini  ³25/04/2011³00000007544/2011³Conversao para TReport                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PONR070()
Local	oReport   
Local	aArea 		:= GetArea()
Private nColPro		:= 1
Private cPerg		:= "PN070RA"
Private cProcesso 	:= Nil
Private cPeriodo 	:= Nil
Private cRoteiro  	:= Nil
Private cNumPagto 	:= Nil

If __cRDD == "TOPCONN" .And. ;	// Verifica se esta utilizando ambiente TOP
	GetRpoRelease("R5").And. ;	// Verifica se esta no Release 11.5
	TRepInUse()					// Verifica se Imprime em formato personalizavel

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica se deve imprimir por periodo ou datas			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If fOpcRadio(@nColPro, STR0001,;      //"Relatorio de Refeições"
						   STR0027,;      //"Selecione a opcao de impressao:"
				   		   STR0028,;      //"Por Periodo"
				   		   STR0029 ) == 0 //"Por Datas"
		Return
	EndIf
		
	cPerg := IF(nColPro == 1,"PN070RA","PN070RB")
	
	pergunte(cPerg,.F.) 

    oReport := ReportDef()
    oReport:PrintDialog()
Else
 	PONR070R3()
EndIF    

RestArea( aArea )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef  ³ Autor ³ Totvs IP RH           ³ Data ³25/04/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Relatorio de Refeicoes                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONR070                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONR070 - Generico                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()
Local oReport 
Local oSection1 
Local oSection2 
Local oSection3
Local bBlkFil 	:= { || If ( cCodFilial != (cAliasQry)->RA_FILIAL, Eval( { || fInfo(@aInfo,(cAliasQry)->RA_FILIAL), aInfo[3] } ), aInfo[1] ) }
Local aOrd      := {STR0004 , STR0005 , STR0006 , STR0007 , STR0021 , STR0034} // 'Matricula'###'Centro de Custo'###'Nome'###'Turno'###'C.Custo+Nome'###Departamento
Private cTitulo	:= OemToAnsi(STR0001)			// "Relatorio de Refeicoes"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao dos componentes de impressao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE REPORT oReport NAME "PONR070" TITLE cTitulo PARAMETER cPerg ACTION {|oReport| PN070Imp(oReport)} DESCRIPTION OemtoAnsi(STR0036) TOTAL IN COLUMN // "Este programa emite Relatorio para Controle de Refeicoes"

	oSection1 := TRSection():New( oReport, OemToAnsi(STR0037), {"SRA","CTT","SR6"}, aOrd,,,,,.F.,.F.,.F.) // "Funcionarios"


		TRCell():New(oSection1,"RA_FILIAL","SRA",,/*Picture*/,8,/*lPixel*/,{|| 	If(!Empty((cAliasQry)->RA_FILIAL),;
																								cCodFilial:= (cAliasQry)->RA_FILIAL,;
																								NIL),;
																								cCodFilial}	)
		TRCell():New(oSection1,"FILIAL","",OemToAnsi(STR0038),/*Picture*/,20,/*lPixel*/,{|| Eval( bBlkFil ) })	// "Desc.Filial"

		TRCell():New(oSection1,"RA_CHAPA"	,"SRA",OemToAnsi(STR0055),,5 ,,,,   ,,,,.F.)				// "Chapa"
		TRCell():New(oSection1,"RA_MAT"		,"SRA",OemToAnsi(STR0056),,6 ,,,,   ,,,,.F.)				// "Matric."
		TRCell():New(oSection1,"RA_NOME"	,"SRA",                  ,,30,,,,.T.,,,,.T.)
		TRCell():New(oSection1,"RA_CC"		,"SRA",OemToAnsi(STR0039),,  ,,,,   ,,,,.F.)				// "C.Custo"
		TRCell():New(oSection1,"CTT_DESC01"	,"CTT",OemToAnsi(STR0040),,25,,,,.T.,,,,.T.) 		// "Descricao"
		TRCell():New(oSection1,"RA_TNOTRAB"	,"SRA",OemToAnsi(STR0041),,3 ,,,,   ,,,,.F.)				// "Tno"
		TRCell():New(oSection1,"R6_DESC"	,"SR6",                  ,,20,,,,.T.,,,,.T.)
		TRCell():New(oSection1,"RA_DEPTO"	,"SRA",OemToAnsi(STR0057),,9 ,,,,   ,,,,.F.)				// Depto

		TRCell():New( oSection1, "TOTHORAS"			, "" ,OemToAnsi(STR0048),"@e 999.99",5,,,"RIGHT",,"RIGHT")	//	"Tot.Hr"
		TRCell():New( oSection1, "INTERVALO"		, "" ,OemToAnsi(STR0049),"@e 999.99",5,,,"RIGHT",,"RIGHT")	//	"Interv" 
		TRCell():New( oSection1, "HORASTRAB"		, "" ,OemToAnsi(STR0050),"@e 999.99",5,,,"RIGHT",,"RIGHT")	//	"H.Trab" 
		TRCell():New( oSection1, "HORASABONADAS"	, "" ,OemToAnsi(STR0051),"@e 999.99",5,,,"RIGHT",,"RIGHT")	//	"H.Abon"
		TRCell():New( oSection1, "HORASDIFERENCA"	, "" ,OemToAnsi(STR0052),"@e 9999.99",7,,,"RIGHT",,"RIGHT")	//	"Hr.Dif."
		TRCell():New( oSection1, "PERCDIFERENCA"	, "" ,OemToAnsi(STR0053),"@e 9999.99",7,,,"RIGHT",,"RIGHT")	//	"% Dif."

	oSection2 := TRSection():New( oSection1, OemToAnsi(STR0042),,,,,,,.F.,.F.,.F. )	 //"Refeicoes"
                                                                              
		TRCell():New( oSection2, "DATA"     	, "" ,OemToAnsi(STR0043),,10)			// "Data 
		TRCell():New( oSection2, "HORA"     	, "" ,OemToAnsi(STR0044),,5) 			// "Hora" 
		TRCell():New( oSection2, "TIPOREF"		, "" ,OemToAnsi(STR0045),,4) 			// "Tipo"
		TRCell():New( oSection2, "DESCRICAO"   	, "" ,OemToAnsi(STR0040),,45,,,,.T.) 			// "Descricao" 
		TRCell():New( oSection2, "VALORREF"		, "" ,OemToAnsi(STR0046),,14,,,,,"RIGHT") 	// "Valor Ref"
		TRCell():New( oSection2, "DESCFUNC"		, "" ,OemToAnsi(STR0047),,14,,,,,"RIGHT") 	// "Desc.Func."
		TRCell():New( oSection2, "CUSTOEMPRESA"	, "" ,OemToAnsi(STR0048),,14,,,,,"RIGHT") 	// "Custo Empresa"

		TRCell():New( oSection2, "PROCESSO"		, "" ,OemToAnsi(STR0058),,5)	// "Processo"
		TRCell():New( oSection2, "PERIODO"		, "" ,OemToAnsi(STR0059),,6)	// "Periodo"	
		TRCell():New( oSection2, "ROTEIRO"		, "" ,OemToAnsi(STR0060),,3)	// "Rot"
		TRCell():New( oSection2, "NUMPAGTO"		, "" ,OemToAnsi(STR0061),,2)	// "NP"

	oSection3 := TRSection():New( oReport, OemToAnsi(STR0049),,,,,,,.F.,.F.,.F. )	 //"Total Funcionario"
                                                                              
		TRCell():New( oSection3, "TIPO"     	, "" ,OemToAnsi(STR0045),,10)			// "Tipo" 
		TRCell():New( oSection3, "DESCRICAO"   	, "" ,OemToAnsi(STR0040),,45,,,,.T.) 			// "Descricao" 
		TRCell():New( oSection3, "QTDE"			, "" ,OemToAnsi(STR0050),,8,,,,,"RIGHT") 			// "Qtde"
		TRCell():New( oSection3, "VALORREF"		, "" ,OemToAnsi(STR0046),,14,,,,,"RIGHT") 			// "Valor Ref"
		TRCell():New( oSection3, "DESCFUNC"		, "" ,OemToAnsi(STR0047),,14,,,,,"RIGHT") 			// "Desc.Func."
		TRCell():New( oSection3, "CUSTOEMPRESA"	, "" ,OemToAnsi(STR0048),,14,,,,,"RIGHT") 			// "Custo Empresa"

Return(oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ PN070Imp   ³ Autor ³ Totvs IP RH           ³ Data ³24/04/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Impressao do relatorio em TReport                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PN070Imp(oReport)                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oReport                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONR070 - Generico - Release 4                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PN070Imp(oReport)

Local oSection  := oReport:Section(1)
Local oSection1 := oReport:Section(1):Section(1)
Local oSection2 := oReport:Section(2)
Local cSitQuery	:= ""    
Local cCatQuery	:= ""  
Local nReg		:= 0
Local nOrdem	:= 1

Local cFilSRACTT	:= ""
Local cFilSRASR6	:= ""
Local cFilSRASQB	:= ""

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis de Acesso do Usuario                               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local nFor       := 0
Local cAcessaSRA := &("{ || " + ChkRH("PONR070","SRA","2") + "}")

Local oBreakCc
Local oBreakFil
Local oBreakFun

Local lHeaderMar	:= .T.

Private aRefFunc   	:= 	{} 
Private aTipos	 	:=	{}  
Private aTipoCC	 	:=	{}  
Private aTipoFil	:=	{}  
Private aTipoEmp	:=	{}  

Private cCodFilial	:= "##"
Private aInfo     	:= {}
Private cFilAnte  	:= ""
Private cTnoAnte   	:= ""
Private cCccAnte   	:= ""
Private cSit       	:= ''
Private cCat       	:= ''
Private dInicio    	:= CtoD('  /  /  ')
Private dFim       	:= CtoD('  /  /  ')
Private lSintetico 	:= .F. 
Private lDiaDia    	:= .F.
Private nHoraDe    	:= 00.00
Private nHoraAte   	:= 23.59

Private cMV_PAR5 	:= ''   
Private cMV_PARN 	:= ''   

Private cAliasQry 	:= GetNextAlias()

Private dPerIni  	:= CTOD('  /  /  ')
Private dPerFim  	:= CTOD('  /  /  ')
Private lPeriodo  	:= .F.
Private oPeriodo  	:= RHPERIODO():New()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nColPro == 1 ) 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ MV_PAR01        //  Processo                                 ³
	//³ MV_PAR02        //  Periodo                                  ³
	//³ MV_PAR03        //  Roteiro                                  ³
	//³ MV_PAR04        //  Numero de Pagamento                      ³
	//³ MV_PAR05        //  Departamento                             ³				
	//³ MV_PAR06        //  Filial  De                               ³
	//³ MV_PAR07        //  Centro de Custo De                       ³
	//³ MV_PAR08        //  Turno De                                 ³
	//³ MV_PAR09        //  Matricula De                             ³
	//³ MV_PAR10        //  Nome De                                  ³
	//³ MV_PAR11        //  Situacao                                 ³
	//³ MV_PAR12        //  Categoria                                ³
	//³ MV_PAR13        //  Relatorio Sintetico/Analitico            ³
	//³ MV_PAR14        //  Dia a Dia                                ³
	//³ MV_PAR15        //  Hora Refeitorio De                       ³
	//³ MV_PAR16        //  Hora Refeitorio Ate                      ³
	//³ MV_PAR17        //  Refeicao Tipo                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lPeriodo   := .T.
	cProcesso  := mv_par01			//Processo
	cPeriodo   := mv_par02			//Periodo
	cRoteiro   := mv_par03			//Roteiro
	cNumPagto  := mv_par04			//Numero de Pagamento
	cSit       := mv_par11 
	cCat       := mv_par12
	lSintetico := If(mv_par13=1,.F.,.T.)
	lDiaDia    := If(mv_par14=1,.T.,.F.) 
	nHoraDe    := mv_par15
	nHoraAte   := mv_par16 
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ MV_PAR01        //  Processo                                 ³
	//³ MV_PAR02        //  Data Inicial                             ³
	//³ MV_PAR03        //  Data Final                               ³
	//³ MV_PAR04        //  Departamento                             ³				
	//³ MV_PAR05        //  Filial  De                               ³
	//³ MV_PAR06        //  Centro de Custo De                       ³
	//³ MV_PAR07        //  Turno De                                 ³
	//³ MV_PAR08        //  Matricula De                             ³
	//³ MV_PAR09        //  Nome De                                  ³
	//³ MV_PAR10        //  Situacao                                 ³
	//³ MV_PAR11        //  Categoria                                ³
	//³ MV_PAR12        //  Relatorio Sintetico/Analitico            ³
	//³ MV_PAR13        //  Dia a Dia                                ³
	//³ MV_PAR14        //  Hora Refeitorio De                       ³
	//³ MV_PAR15        //  Hora Refeitorio Ate                      ³
	//³ MV_PAR16        //  Refeicao Tipo                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lPeriodo   := .F.
	cProcesso  := mv_par01			//Processo
	dPerIni    := mv_par02
	dPerFim    := mv_par03
	cSit       := mv_par10
	cCat       := mv_par11
	lSintetico := If(mv_par12=1,.F.,.T.)
	lDiaDia    := If(mv_par13=1,.T.,.F.) 
	nHoraDe    := mv_par14
	nHoraAte   := mv_par15 
EndIf

If lPeriodo
	oPeriodo:cProcesso := cProcesso
	oPeriodo:cPeriodo  := cPeriodo
	oPeriodo:cRoteiro  := cRoteiro
	oPeriodo:cNumPagto := cNumPagto

	oPeriodo:GetPer()
	
	If !(oPeriodo:lFound )
		MsgInfo( OemToAnsi( oPeriodo:cMsgNotFoundPer ) )    //"Período de Apontamento não encontrado."
		Return
	EndIf
	
	dPerIni := oPeriodo:dDataIni
	dPerFim := oPeriodo:dDataFim
Else
	oPeriodo:dDataIni := dPerIni
	oPeriodo:dDataFim := dPerFim	
EndIf	

If dPerIni > dPerFim
   Help(" ",1,"PNR050DATA")
   Return Nil
Endif

SRA->(dbSetOrder(1))

//-- Modifica variaveis para a Query
For nReg:=1 to Len(cSit)
	cSitQuery += "'"+Subs(cSit,nReg,1)+"'"
	If ( nReg+1 ) <= Len(cSit)
		cSitQuery += "," 
	Endif
Next nReg        
cSitQuery := "%" + cSitQuery + "%"

cCatQuery := ""
For nReg:=1 to Len(cCat)
	cCatQuery += "'"+Subs(cCat,nReg,1)+"'"
	If ( nReg+1 ) <= Len(cCat)
		cCatQuery += "," 
	Endif
Next nReg        
cCatQuery := "%" + cCatQuery + "%"

//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
MakeAdvplExpr(cPerg)
If ( nColPro == 1 ) 
	If !Empty(MV_PAR17)
		cMV_PAR5   := MV_PAR17
		cMV_PARN   := STRTRAN(MV_PAR17,"P5_TIPOREF","PN_TIPOREF")
	Endif
Else
	If !Empty(MV_PAR16)
		cMV_PAR5   := MV_PAR16
		cMV_PARN   := STRTRAN(MV_PAR16,"P5_TIPOREF","PN_TIPOREF")
	Endif
Endif
	
//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
MakeSqlExpr(cPerg)
	
BEGIN REPORT QUERY oSection

cFilSRACTT	:= "%" + FWJoinFilial("SRA", "CTT") + "%"
cFilSRASR6	:= "%" + FWJoinFilial("SRA", "SR6") + "%"	
cFilSRASQB	:= "%" + FWJoinFilial("SRA", "SQB") + "%"	

nOrdem		:= oSection:GetOrder()
If nOrdem == 1
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_MAT%"
ElseIf nOrdem == 2
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_CC,SRA.RA_MAT%"
ElseIf nOrdem == 3
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_NOME,SRA.RA_MAT%"
ElseIf nOrdem == 4
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_TNOTRAB%"
ElseIf nOrdem == 5
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_CC,SRA.RA_NOME%"
ElseIf nOrdem == 6
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_DEPTO,SRA.RA_MAT%"
Endif

cQryProcesso := "%(SRA.RA_DEMISSA = '          ' OR (SRA.RA_DEMISSA >= '"+DTOS(dPerIni) + "' AND  SRA.RA_DEMISSA <> '          '))"
If !Empty(cProcesso)
	cQryProcesso += " AND SRA.RA_PROCES = '" + cProcesso + "' "	
Endif
cQryProcesso += "%"

BeginSql alias cAliasQry
	SELECT SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_TNOTRAB, SRA.RA_DEMISSA,SRA.RA_ADMISSA,SRA.RA_SITFOLH,
		SRA.RA_CHAPA,SRA.RA_DEPTO,SRA.RA_PROCES,
		CTT.CTT_FILIAL, CTT.CTT_CUSTO, CTT.CTT_DESC01,
		SR6.R6_TURNO, SR6.R6_DESC,
		SQB.QB_DEPTO, SQB.QB_DESCRIC
	FROM %table:SRA% SRA
	INNER JOIN %table:CTT% CTT ON RA_CC = CTT_CUSTO AND CTT.%NotDel% AND %exp:cFilSRACTT%
	INNER JOIN %table:SR6% SR6 ON RA_TNOTRAB = R6_TURNO AND SR6.%NotDel% AND %exp:cFilSRASR6%
	INNER JOIN %table:SQB% SQB ON RA_DEPTO = QB_DEPTO AND SQB.%NotDel% AND %exp:cFilSRASQB%
	WHERE SRA.RA_SITFOLH	IN	(%exp:Upper(cSitQuery)%) 	AND
		  SRA.RA_CATFUNC	IN	(%exp:Upper(cCatQuery)%)	AND
	      %exp:cQryProcesso% AND 
 	      SRA.%notDel%   
	ORDER BY %exp:cOrdem%
EndSql

If ( nColPro == 1 ) 
	END REPORT QUERY oSection PARAM mv_par05,mv_par06,mv_par07,mv_par08,mv_par09,mv_par10
Else
	END REPORT QUERY oSection PARAM mv_par04,mv_par05,mv_par06,mv_par07,mv_par08,mv_par09
Endif

DEFINE BREAK oBreakFil OF oSection WHEN {|| (cAliasQry)->RA_FILIAL }  TITLE OemToansi(STR0001) 
oBreakFil:SetPageBreak(.T.)
oBreakFil:SetHeaderBreak(.T.) 

DEFINE BREAK oBreakFun OF oSection WHEN {|| (cAliasQry)->RA_MAT }  TITLE OemToansi(STR0001) 
oBreakFun:SetHeaderBreak(.T.) 
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Quebra de pagina C.Custo                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (nOrdem = 2 .or. nOrdem = 5) 
	DEFINE BREAK oBreakCc OF oSection WHEN {|| (cAliasQry)->RA_CC } TITLE OemToansi(STR0001) 
	oBreakCc:SetPageBreak(.T.) 
	oBreakCc:SetHeaderBreak(.T.) 
ElseIf (nOrdem = 4) 
	DEFINE BREAK oBreakCc OF oSection WHEN {|| (cAliasQry)->RA_TNOTRAB } TITLE OemToansi(STR0001) 
	oBreakCc:SetPageBreak(.T.) 
	oBreakCc:SetHeaderBreak(.T.) 
Endif 

oSection:SetHeaderSection(.T.)
oSection:SetHeaderBreak(.T.) 

oSection1:SetHeaderSection(.F.)
oSection1:SetHeaderBreak(.F.) 

oSection2:SetHeaderBreak(.F.) 
oSection2:SetHeaderSection(.F.)

oSection:lHeaderPage:=.F. 
oSection1:lHeaderPage:=.F. 
oSection2:lHeaderPage:=.F. 

// Na quebra de pagina executa: Impressao da Linha do funcionario,pula linha,impressão do cabecalho das marcacoes, pula linha
oReport:OnPageBreak( {|| oReport:lOnPageBreak:= .F.,lHeaderMar := .T.,;
							If((oSection1:lPrinting ),;
								(oSection:PrintLine(),	;
									oReport:SkipLine(),;
									oSection1:SetHeaderSection(.T.),;
									oSection1:PrintHeader(),;
									oSection1:SetHeaderSection(.F.),;
									oReport:SkipLine();
								);
							,);
					 },.F. )

cFilAnte := (cAliasQry)->RA_FILIAL
cTnoAnte := (cAliasQry)->RA_TNOTRAB
cCccAnte := (cAliasQry)->RA_CC

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa a variavel aInfo com a filial Logada              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
fInfo(@aInfo,(cAliasQry)->RA_FILIAL)

//-- Define o total da regua da tela de processamento do relatorio
oReport:SetMeter( 100 )  

//-- Incializa impressao   
oSection:Init(.F.)                                                  

While !(cAliasQry)->( EOF() ) 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimenta Regua de Processamento                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:IncMeter( 1 )   

	//-- Verifica se o usuário cancelou a impressão do relatorio
	If oReport:Cancel()
		Exit
	EndIf               

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Posiciona o registro da query na tabela de Funcionarios                ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If !SRA->(dbSeek( (cAliasQry)->RA_FILIAL + (cAliasQry)->RA_MAT ))
		(cAliasQry)->(DbSkip())
	   	Loop
	Endif

	//-- Processa quebra de Filial.
	If (cAliasQry)->RA_FILIAL != cFilAnte 
	    //--Somente "Zera" as variaveis se jah foi impresso algo para nao pula 
	    //--de pagina na primeira vez

		If !fInfo(@aInfo,(cAliasQry)->RA_FILIAL)
			Exit
		Endif	

		// Imprime os totais por Centro de Custo ou Turno de Trabalho
		If nOrdem == 2 .Or. nOrdem == 4 .Or. nOrdem == 5
			F070RCabTipo(oReport,oSection2,@aTipoCC,nOrdem)
		Endif

		// Imprime os totais da filial
		oReport:EndPage()
		F070RCabTipo(oReport,oSection2,@aTipoFil,9)		

		cFilAnte := (cAliasQry)->RA_FILIAL
		cTnoAnte := (cAliasQry)->RA_TNOTRAB
		cCccAnte := (cAliasQry)->RA_CC

	Endif

	//-- Processa quebra de Centro de Custo ou Turno de Trabalho
	If (nOrdem == 4 .And. (cAliasQry)->RA_TNOTRAB != cTnoAnte) .Or. ;
		(( nOrdem == 2 .Or. nOrdem == 5) .And. (cAliasQry)->RA_CC != cCccAnte	)
		F070RCabTipo(oReport,oSection2,@aTipoCC,nOrdem)		
		cTnoAnte := (cAliasQry)->RA_TNOTRAB 
		cCccAnte := (cAliasQry)->RA_CC 
	Endif

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Consiste Filiais e Acessos                                             ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF !( (cAliasQry)->RA_FILIAL $ fValidFil() ) .or. !Eval( cAcessaSRA )
		(cAliasQry)->(DbSkip())
	   	Loop
	EndIF

	/*
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³*** Processa Marcacoes de Refeicoes ***³
	//³    do Funcionario Corrente            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	*/                                      

    //-- Zera Acumuladores do Funcionario
	aTipos		:={}
	aRefFunc	:={}                   
	
	//-- Obtem Marcacoes do Acumulado  (SPN)
	fObtemMarc("SPN","PN",@aRefFunc,@aTipos,.T.) 
	//-- Obtem Marcacoes da Manutencao (SP5)
	fObtemMarc("SP5","P5",@aRefFunc,@aTipos,.T.)   
   
    //--Se houve Marcacoes
    If Len(aTipos) > 0

	   	//-- Atualiza Matrizes Somatorias      
		fSomaMat(@aTipoCC ,@aTipos,1,{4,5,6,7}) //CC ou Turno  
		fSomaMat(@aTipoFil,@aTipos,1,{4,5,6,7}) //Filial
		fSomaMat(@aTipoEmp,@aTipos,1,{4,5,6,7}) //Empresa
  
	 	//-- Imprime Marcacoes do Funcionario  e Zera a Tipos
		
	    If Len(aRefFunc) > 0 .And. !lSintetico

			oSection:PrintLine()
			oReport:SkipLine()

			If lDiaDia
				oSection1:Init(.F.) 
			
				If lHeaderMar .Or. !lSintetico // Imprime cabecalho das marcacoes caso seja analitico
					oSection1:SetHeaderSection(.T.)
					oSection1:PrintHeader() 
					oSection1:SetHeaderSection(.F.)
					lHeaderMar := .F.
				Endif

				dDataFunc :=Ctod(Space(8))
				For nFor := 1 To Len(aRefFunc)

					//-- Verifica Quebra de Data
					If DTOS(dDataFunc)<>DTOS(aRefFunc[nFor,1]) 
					   //-- Iguala Variavel Verificadora de quebra
					   dDataFunc:=aRefFunc[nFor,1]  
						oSection1:Cell( "DATA"):SetValue(aRefFunc[nFor,1])
					Else    
						//-- Inicio das Linhas Seguintes contem data em branco
						oSection1:Cell( "DATA"):SetValue(Space(10))
					Endif
					   
				    //--Monta Linha Detalhe   
				    //-- Data + Hora + Tipo Ref + Valor Ref + Desc.Func. + Custo Empresa
		
					oSection1:Cell( "HORA"):SetValue(StrTran(aRefFunc[nFor,2],'.',':'))
					oSection1:Cell( "TIPOREF"):SetValue(aRefFunc[nFor,3])
					oSection1:Cell( "DESCRICAO"):SetValue(aRefFunc[nFor,4])
					oSection1:Cell( "VALORREF"):SetValue(Transform(aRefFunc[nFor,5],'@E 999,999,999.99'))
					oSection1:Cell( "DESCFUNC"):SetValue(Transform(aRefFunc[nFor,6],'@E 999,999,999.99'))			
					oSection1:Cell( "CUSTOEMPRESA"):SetValue(Transform(aRefFunc[nFor,7],'@E 999,999,999.99') )

					oSection1:Cell( "PROCESSO"):SetValue(aRefFunc[nFor,8])
					oSection1:Cell( "PERIODO"):SetValue(aRefFunc[nFor,9])
					oSection1:Cell( "ROTEIRO"):SetValue(aRefFunc[nFor,10])					
					oSection1:Cell( "NUMPAGTO"):SetValue(aRefFunc[nFor,11])

					//-- Imprime os conteudos da Section
				   	oSection1:PrintLine()
					oReport:SkipLine()

				Next nFor                         
			
				oSection1:Finish()
				oReport:ThinLine()
			Endif

			//Imprime totais dos Tipos do funcionario

			F070RCabTipo(oReport,oSection2,aTipos,0)
			
		Endif
    Endif
    
	(cAliasQry)->(DbSkip())

EndDo

// Imprime os totais por Centro de Custo ou Turno de Trabalho
If Len(aTipoCC) > 0 .And. (nOrdem == 2 .Or. nOrdem == 4 .Or. nOrdem == 5)
	F070RCabTipo(oReport,oSection2,@aTipoCC,nOrdem)
Endif

// Imprime os totais por Filial
If Len(aTipoFil) > 0
	oReport:EndPage()
	F070RCabTipo(oReport,oSection2,@aTipoFil,9)
Endif

// Imprime os totais por Empresa
If Len(aTipoEmp) > 0
	oReport:EndPage()
	F070RCabTipo(oReport,oSection2,@aTipoEmp,99)
Endif

oSection:Finish()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Termino do Relatorio                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If Select(cAliasQry) > 0
	(cAliasQry)->(dbCloseArea())
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³F070RCabTipo³Autor³ Totvs IP-RH 		    ³ Data ³ 24.04.11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao de totalizadores para TReport                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ F070RCabTipo(oReport,oSection ,aTipos)                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oReport     - objeto principal                             ³±±
±±³          ³ oSection    - objeto da secao                              ³±±
±±³          ³ aTipos      - Array contendo o conteudo a ser impresso     ³±±
±±³          ³ nOrdem      - Numero da Ordem de impressao                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONR070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/			
Static Function F070RCabTipo(oReport,oSectionTotal,aTipos,nOrdem)
Local cDescTot	:= ""
Local nFor 		:= 1
Local aTotTipo	:= {}
Local aTot		:= {}

If Len(aTipos) == 0
	Return
Endif

//-- Ordena Tipos em Ordem Crescente 
aSort(aTipos,,,{|x,y| x[1]<y[1]})

//-- Totaliza Colunas dos Tipos
aTotTipo:=fSoma(@aTipos ,{4,5,6,7})         

//-- Preenche 1.o Elemento da array Total dos Tipos com "Total: "
If nOrdem == 0
	cDescTot := STR0049	// "Total Funcionario" 
Elseif nOrdem == 9
	cDescTot := STR0051	// "Total Filial" 
	cDescTot += " " + Posicione("SM0",1,cEmpAnt+cFilAnte,"M0_NOME")
Elseif nOrdem == 2
	cDescTot := STR0052	// "Total C.Custo" 
	cDescTot += " " + Posicione("CTT",1,fFilFunc("CTT")+cCccAnte,"CTT_DESC01")
Elseif nOrdem == 4
	cDescTot := STR0053	// "Total Turno" 
	cDescTot += " " + Posicione("SR6",1,fFilFunc("SR6")+cTnoAnte,"R6_DESC")
Elseif nOrdem == 5
	cDescTot := STR0052	// "Total C.Custo" 
	cDescTot += " " + Posicione("CTT",1,fFilFunc("CTT")+cCccAnte,"CTT_DESC01")
Elseif nOrdem == 99
	cDescTot := STR0054	// "Total Empresa" 
Endif

aTot:= {Space(2),PADR(ALLTRIM(UPPER(cDescTot)),60),0}

//-- Preenche os demais elementos com a totalizacao das Colunas dos Tipos
Aeval(aTotTipo,{ |x| AADD(aTot, x) })
	   
//-- Adiciona o array totalizador ao array dos Tipos que o Originou
//-- Para ser impresso como ultimo elemento
AADD(aTipos,aTot)

oSectionTotal:SetHeaderSection(.T.)
oSectionTotal:Init(.F.)
oSectionTotal:PrintHeader() 
oSectionTotal:SetHeaderSection(.F.)

For nFor := 1 To Len(aTipos) 

	oSectionTotal:Cell( "TIPO"):SetValue(aTipos[nFor,1])
	oSectionTotal:Cell( "DESCRICAO"):SetValue(aTipos[nFor,2])
	oSectionTotal:Cell( "QTDE"):SetValue(Transform(aTipos[nFor,4],'@E 9999,999'))
	oSectionTotal:Cell( "VALORREF"):SetValue(Transform(aTipos[nFor,5],'@E 999,999,999.99'))
	oSectionTotal:Cell( "DESCFUNC"):SetValue(Transform(aTipos[nFor,6],'@E 999,999,999.99'))			
	oSectionTotal:Cell( "CUSTOEMPRESA"):SetValue(Transform(aTipos[nFor,7],'@E 999,999,999.99') )
	oSectionTotal:PrintLine()

Next nFor

oSectionTotal:Finish()
oReport:ThinLine()

aTipos := {}

Return


//======>>>>>>>>>> DAQUI PARA BAIXO, TRATA-SE DO RELEASE 3
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ PONR070  ³ Autor ³ R.H. - J. Ricardo     ³ Data ³ 23.08.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Controle de Refeicoes                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONR070(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Fernando J. ³20.02.98³------³Zerar total de Refei‡”es a cada func.     ³±±
±±³Fernando J. ³25.03.98³------³Perguntas para Limitar Intervalo de Horas,³±±
±±³            ³        ³      ³que quando zeradas faz com que o sistema  ³±±
±±³            ³        ³      ³considere os Limites do Turno de Trabalho.³±±
±±³Aldo	  	   ³08/10/98³------³ Acerto na Ordem No.4 - Turno (SRA).      ³±±
±±³Aldo	  	   ³06/01/99³------³ Alteracao da Data para 4 digitos.        ³±±
±±³Aldo	  	   ³30/03/99³------³ Passagem de nTamanho para SetPrint().    ³±±
±±³Aldo	  	   ³10/09/99³05622A³ Incluido ordem de C.Custo+Nome           ³±±
±±³Alexsandro  ³09/05/00³------³ Remocao do codigo DOS.                   ³±±
±±³Alexsandro  ³03/08/00³------³ Implementacao formato data universal.    ³±±
±±³Alexsandro  ³03/08/00³------³ Implementacao controle de acesso.        ³±± 
±±³Mauricio MR ³22/08/01³------³ Implementacao controle de Tipos de Rfeic.³±±
±±³ Priscila R.³05/06/02³------³Ajuste no relatorio para que seja impresso³±± 
±±³            ³--------³------³corretamente o C.C no tamanho 20.         ³±± 
±±³Mauricio MR ³04/12/02³------³Retirada a restricao de existencia de cra-³±± 
±±³            ³--------³------³cha para a emissao do relatorio.	      ³±± 
±±³Mauricio MR ³23/04/03³------³Correcao de Totalizacao de Filial/Empresa ³±± 
±±³            ³        ³------³e impressao de cabecalho.                 ³±± 
±±³=======================================================================³±± 
±±³                         *** Versao 9.12 ***                           ³±± 
±±³=======================================================================³±± 
±±³Leandro Dr  ³02/04/08³Melhor³Inclusão de chave processo/periodo/roteiro³±± 
±±³            |        |      |NumPagto. Impressao por data ou periodo   ³±±
±±³Francisco Jr³18/01/10³001029³Inclusão de chave processo/periodo/roteiro³±± 
±±³            |        |      |NumPagto. Impressao por data ou periodo   ³±±  
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PONR070R3()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cDesc1     := STR0001  // 'Relatorio de Refeicoes'
Local cDesc2     := STR0002  // 'Ser  impresso de acordo com os parametros solicitados pelo'
Local cDesc3     := STR0003  // 'usuario.'
Local cString    := 'SRA' //-- Alias do arquivo principal (Base)
Local aOrd       := {STR0004 , STR0005 , STR0006 , STR0007 , STR0021 , STR0034} // 'Matricula'###'Centro de Custo'###'Nome'###'Turno'###'C.Custo+Nome'###Departamento
Local WnRel

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis PRIVATE(Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aReturn    := { STR0008 , 1,STR0009 , 1, 2, 1, '',1 } // 'Zebrado'###'Administracao'
Private nomeprog   := 'PONR070'
Private aLinha     := {}
Private nLastKey   := 0
Private cPerg      := ''

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis Utilizadas na funcao IMPR                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private Titulo     := OemToAnsi(STR0001) // 'Relatorio de Refeicoes'
Private cCabec     := STR0011  // 'Relatorio de Controle de Refeicoes'
Private At_Prg     := 'PONR070'
Private wCabec0    := 2
Private wCabec1    := '' 
Private wCabec2    := ''
Private ContFl     := 1
Private Li         := 0
Private nTamanho   := 'P'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private (Programa)                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aInfo      := {}
Private nOrdem     := 0
Private cFilDe     := ''
Private cFilAte    := ''
Private cCCDe      := ''
Private cCCAte     := ''
Private cTnoDe     := ''
Private cTnoAte    := ''
Private cMatDe     := ''
Private cMatAte    := ''
Private cNomeDe    := ''
Private cNomeAte   := ''
Private cSit       := ''
Private cCat       := ''
Private lSintetico := .F. 
Private lDiaDia    := .F.
Private nHoraDe    := 00.00
Private nHoraAte   := 23.59
Private nTpRefDe   := ''
Private nTpRefAte  := ''   
Private lSalta	   :=	.F.

Private cProcesso := Nil
Private cPeriodo  := Nil
Private cRoteiro  := Nil
Private cNumPagto := Nil
Private cDeptoDe
Private cDeptoAte
Private dPerIni   := CtoD("//")
Private dPerFim   := CtoD("//")
Private lPeriodo  := .F.
Private nColPro
Private oPeriodo  := RHPERIODO():New()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se deve imprimir por periodo ou datas			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If fOpcRadio(@nColPro, STR0001,;      //"Relatorio de Refeicoes"
					   STR0027,;      //"Selecione a opcao de impressao:"
			   		   STR0028,;      //"Por Periodo"
			   		   STR0029 ) == 0 //"Por Datas"
	Return
EndIf
	
cPerg := IF(nColPro == 1,"PNR070A","PNR070B")


Pergunte(cPerg,.F.)
If ( nColPro == 1 ) 
	cProcesso := mv_par01			//Processo
	cPeriodo  := mv_par02			//Periodo
	cRoteiro  := mv_par03			//Roteiro
	cNumPagto := mv_par04			//Numero de Pagamento
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WnRel:='PONR070' //-- Nome Default do relatorio em Disco
WnRel:=SetPrint(cString,WnRel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)

If	nLastKey == 27
	Return Nil
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Return Nil
Endif

RptStatus({|lEnd| PO070Imp(@lEnd, WnRel, cString)}, Titulo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ T‚rmino do relatorio                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea('SRA')
dbSetOrder(1)

Set Device To Screen

If aReturn[5] == 1
	Set Printer To
	Commit
	OurSpool(WnRel)
Endif

MS_FLUSH()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ PO070Imp ³ Autor ³ Mauricio MR 		    ³ Data ³ 23.08.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de controle de Refeicoes                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ PO070Imp(lEnd,WnRel,cString)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd        - A‡Æo do Codelock                             ³±±
±±³          ³ WnRel       - T¡tulo do relat¢rio                          ³±±
±±³Parametros³ cString     - Mensagem                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PO070Imp(lEnd, WnRel, cString)

//-- Inicializa Variaveis Locais
Local nPos       := 0
Local nFor       := 0
Local dRef       := dPerIni
Local cTipoRef	 := '' 
Local nPosTipo   := 0 
Local dDataAux	 :=	CTOD(SPACE(8))

Local cFilSP5    := xFilial('SP5')
Local cAcessaSRA := &("{ || " + ChkRH("PONR070","SRA","2") + "}")
Local aTotal	 :=	{}


Private aRefFunc   	:= 	{} 
Private aTiposFunc 	:= 	{}
Private aTipos	 	:=	{}  
Private aTipoCC	 	:=	{}  
Private aTipoFil	:=	{}  
Private aTipoEmp	:=	{}  
Private cTnoAnt    	:= ''
Private cFAnt    	:= ''
Private cCCAnt     	:= ''
Private cChave      := ''

nOrdem     := aReturn[8]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nColPro == 1 ) 
	lPeriodo   := .T.
	cProcesso  := mv_par01			//Processo
	cPeriodo   := mv_par02			//Periodo
	cRoteiro   := mv_par03			//Roteiro
	cNumPagto  := mv_par04			//Numero de Pagamento
	cDeptoDe   := mv_par05			//Depto De
	cDeptoAte  := mv_par06			//Depto Ate
	cFilDe     := mv_par07
	cFilAte    := mv_par08
	cCCDe      := mv_par09
	cCCAte     := mv_par10
	cTnoDe     := mv_par11
	cTnoAte    := mv_par12
	cMatDe     := mv_par13
	cMatAte    := mv_par14
	cNomeDe    := mv_par15
	cNomeAte   := mv_par16
	cSit       := mv_par17
	cCat       := mv_par18
	lSintetico := If(mv_par19=1,.F.,.T.)
	lDiaDia    := If(mv_par20=1,.T.,.F.) 
	nHoraDe    := mv_par21
	nHoraAte   := mv_par22 
	nTpRefDe   := mv_par23
	nTpRefAte  := mv_par24
Else
	lPeriodo   := .F.
	cProcesso  := mv_par01			//Processo
	dPerIni    := mv_par02
	dPerFim    := mv_par03
	cDeptoDe   := mv_par04			//Depto De
	cDeptoAte  := mv_par05			//Depto Ate
	cFilDe     := mv_par06
	cFilAte    := mv_par07
	cCCDe      := mv_par08
	cCCAte     := mv_par09
	cTnoDe     := mv_par10
	cTnoAte    := mv_par11
	cMatDe     := mv_par12
	cMatAte    := mv_par13
	cNomeDe    := mv_par14
	cNomeAte   := mv_par15
	cSit       := mv_par16
	cCat       := mv_par17
	lSintetico := If(mv_par18=1,.F.,.T.)
	lDiaDia    := If(mv_par19=1,.T.,.F.) 
	nHoraDe    := mv_par20
	nHoraAte   := mv_par21 
	nTpRefDe   := mv_par22
	nTpRefAte  := mv_par23   
EndIf

If lPeriodo
	oPeriodo:cProcesso := cProcesso
	oPeriodo:cPeriodo  := cPeriodo
	oPeriodo:cRoteiro  := cRoteiro
	oPeriodo:cNumPagto := cNumPagto

	oPeriodo:GetPer()
	
	If !(oPeriodo:lFound )
		MsgInfo( OemToAnsi( oPeriodo:cMsgNotFoundPer ) )    //"Período de Apontamento não encontrado."
		Return
	EndIf
	
	dPerIni := oPeriodo:dDataIni
	dPerFim := oPeriodo:dDataFim
Else
	oPeriodo:dDataIni := dPerIni
	oPeriodo:dDataFim := dPerFim	
EndIf

If dPerIni > dPerFim
	Help(' ',1,'PNR050DATA')
	Return Nil
Endif
 
dbSelectArea('SRA')

dbSetOrder(nOrdem)  
//-- Filial + Matricula 
If nOrdem == 1
	dbSeek(cFilDe + cMatDe, .T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_MAT'
	cFim     := cFilAte + cMatAte
//-- Filial + Centro de Custo + Matricula	
ElseIf nOrdem == 2
	dbSeek(cFilDe + cCCDe + cMatDe, .T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT'
	cFim     := cFilAte + cCCAte + cMatAte
//-- Filial + Nome + Matricula
ElseIf nOrdem == 3
	dbSeek(cFilDe + cNomeDe + cMatDe, .T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT'
	cFim     := cFilAte + cNomeAte + cMatAte
//-- Filial + Turno de Trabalho	
ElseIf nOrdem == 4
	dbSeek(cFilDe + cTnoDe, .T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_TNOTRAB'
	cFim     := cFilAte + cTnoAte 
//-- Filial + Centro de Custo + Nome	
ElseIf nOrdem == 5
	dbSetOrder(8)
	dbSeek(cFilDe + cCcDe + cNomeDe,.T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_NOME'
	cFim     := cFilAte + cCcAte + cNomeAte
ElseIf nOrdem == 6
	dbSetOrder(22)
	dbSeek(cFilDe + cDeptoDe + cMatDe,.T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_DEPTO + SRA->RA_MAT'
	cFim     := cFilAte + cDeptoAte + cMatAte	
Endif

cFAnt 	:= Replicate('ú',FWGETTAMFILIAL)
cTnoAnt := 'úúú'
cCCAnt  := Replicate('ú', Len(SRA->RA_CC))

SetRegua(SRA->(RecCount())) //-- Inicializa R‚gua de Contagem

//-- Corre Todos os Funcionarios
While !SRA->(EOF()) .And. &cInicio <= cFim
   
	IncRegua() //-- Incrementa R‚gua de Contagem

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³**** Consiste Cadastro de Funcionario ****³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	//-- Cancela a Impress„o ao se pressionar ALT + A
	If lEnd
		IMPR(cCancela,'C')
		Return Nil
	EndIF
    
    If SRA->RA_FILIAL # cFAnt
		cFAnt 	:= SRA->RA_FILIAL
		cTnoAnt := SRA->RA_TNOTRAB
		cCcAnt  := SRA->RA_CC    
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Carrega Informacoes da Empresa								 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF !fInfo( @aInfo , cFAnt )
			fTestaTotal()
			Loop
		EndIF
		fComplcab()
	Endif
                   
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste controle de acessos e filiais validas               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !(SRA->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
		fTestaTotal()
		Loop
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste Parametrizacao do Intervalo de Impressao            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF SRA->(;
				(  RA_TNOTRAB	< cTnoDe ) .or. ( 	RA_TNOTRAB	> cTnoAte ) .or. ;
				(  RA_NOME 		< cNomeDe) .or. ( 	RA_NOME 	> cNomeAte) .or. ;
				(  RA_MAT 		< cMatDe ) .or. ( 	RA_MAT 		> cMatAte ) .or. ;
				(  RA_CC 		< cCCDe  ) .or. ( 	RA_CC 		> cCCAte  ) .or. ;
				(  RA_DEPTO		< cDeptoDe) .or. ( 	RA_DEPTO	> cDeptoAte	 ) .or. ;
				!( RA_SITFOLH	$ cSit   ) .or. !( 	RA_CATFUNC	$ cCat );
			)
		fTestaTotal()
		Loop
	EndIF	

	If !Empty(cProcesso)
		If SRA->RA_PROCES <> cProcesso
			fTestaTotal()
			Loop
		EndIf
	EndIf
        
	//- Verifica Demitidos antes do Periodo
	If (SRA->RA_DEMISSA < dPerIni .And. ! Empty(SRA->RA_DEMISSA))
		fTestaTotal()
		Loop
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³*** Processa Marcacoes de Refeicoes ***³
	//³    do Funcionario Corrente            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    
    //-- Zera Acumuladores do Funcionario
	aTipos		:={}
	aRefFunc	:={}                   
	
	//-- Obtem Marcacoes do Acumulado  (SPN)
	fObtemMarc("SPN","PN",@aRefFunc,@aTipos) 
	//-- Obtem Marcacoes da Manutencao (SP5)
	fObtemMarc("SP5","P5",@aRefFunc,@aTipos)   
   
    //--Se houve Marcacoes
    If !EMPTY(Len(aTipos))
	   	//-- Atualiza Matrizes Somatorias      
		fSomaMat(@aTipoCC ,@aTipos,1,{4,5,6,7}) //CC ou Turno  
		fSomaMat(@aTipoFil,@aTipos,1,{4,5,6,7}) //Filial
		fSomaMat(@aTipoEmp,@aTipos,1,{4,5,6,7}) //Empresa
  
	 	//-- Imprime Marcacoes do Funcionario  e Zera a Tipos
		
		fImpMarc(@aRefFunc,@aTipos)	
	Endif
	
	//-- Verifica Quebra
	fTestaTotal()
	
	cChave := ''
	
Enddo

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fObtemMarc³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Obtem Marcacoes de Refeicoes do Funcionario                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fObtemMarc(cAlias,cP,aRefFunc,aTipos,lTReport)
Local cFilSP	:=	''      
Local cTipoRef	:=	''
Local cDescRef  := 	''
DeFault lTReport := .F.

cFilSP  	:= fFilFunc(cAlias)
//-- Posiciona no Funcionario
&(cAlias)->(DbSeek(cFilSP + SRA->RA_MAT, .F.))

//-- Corre Refeicoes do Funcionario
Do While !&(cAlias)->(Eof()) .And. &(cAlias)->(&(cP+"_Filial + "+cP+"_Mat")) == cFilSP + SRA->RA_MAT

	//-- Limita o Intervalo de Datas
	If &(cAlias)->(&(cP+"_DATA") < dPerIni .Or. &(cP+"_DATA") > dPerFim)
		&(cAlias)->(dbSkip())
		Loop
	Endif
	
	//-- Limita o Intervalo de Horas
	If &(cAlias)->(&(cP+"_HORA") < nHoraDe .Or. &(cP+"_HORA") > nHoraAte )
		&(cAlias)->(dbSkip())
		Loop
	EndIf
	
	If lTReport
		//-- Limita o Tipo de Refeicao	
		If !Empty(&("cMV_PAR"+Right(cP,1)))
			If	!(&(cAlias)->(&(&("cMV_PAR"+Right(cP,1))))) 
				&(cAlias)->(dbSkip())
				Loop
			EndIf
		Endif
	Else
		//-- Limita o Tipo de Refeicao	
		If	&(cAlias)->(&(cP+"_TIPOREF") < nTpRefDe .OR. 	&(cP+"_TIPOREF") > nTpRefAte )        
			&(cAlias)->(dbSkip())
			Loop
		EndIf
	Endif	
	
	//-- Se Houve Quebra de Tipo de Refeicao
    If &(cAlias+"->"+cP+"_TipoRef")  <> cTipoRef 
        //-- Para Tipos Novos 	Obtem	: 	Descricao, Perc.Desc.Func.
        //	     				Reserva :	Qtde,Vlr da Refeicao ,Part Func., Part. Empresa
       	IF ( nPosTipo := aScan( aTipos,{ |xtipo| xtipo[1] == &(cAlias+"->"+cP+"_TipoRef") } ) ) == 0.00
		    cDescRef:= fDesc("SPM",&(cAlias+"->"+cP+"_TipoRef"),"PM_DESCREF")
		    
		    //-- Alerta para Tipo de Refeicao Nao Cadastrado
		    If Empty(cDescRef)
		    	cDescRef	:= STR0026   //** Nao Cadastrado **	
		    Endif    
		    
		    aAdd( aTipos , ;
		      	{ &(cAlias+"->"+cP+"_TipoRef"),   			 	; //Tipo 
		       		cDescRef				, 					; //Descricao
		        Posicione("SPM",1,fFilFunc("SPM")+;
		        &(cAlias+"->"+cP+"_TipoRef"),"PM_PERCFUN"), 	; //Perc. 
		        0	,										 	;  //Qtde
		        0.00,										 	;  //Vlr Refeicao
		        0.00,											;  //Part. Func.
		        0.00 										 	;  //Part. Empresa
		         } )	
			nPosTipo := Len( aTipos )
		EndIF
		//-- Iguala variavel verificadora de quebra de Tipo
		cTipoRef:=&(cAlias+"->"+cP+"_TipoRef")	
	Endif  
	                      
   
	
	//-- Soma Valores Referentes ao Tipo Refeicao Lido
	aTipos[nPosTipo][4] := aTipos[nPosTipo][4] + 1   		//Qtde
	aTipos[nPosTipo][5] := aTipos[nPosTipo][5] + ;
							&(cAlias+"->"+cP+"_ValRef") 	//Valor da Refeicao 
	aTipos[nPosTipo][6] := aTipos[nPosTipo][6] + ;
							&(cAlias+"->"+cP+"_DescFun") 	//Desc.Ref.Funcionario
	aTipos[nPosTipo][7] := aTipos[nPosTipo][7] + ;
							&(cAlias)->(&(cP+"_ValRef") - &(cP+"_DescFun"))	//Desc.Ref.Empresa					
	

	If lTReport
		Aadd(aRefFunc, {&(cAlias+"->"+cP+"_Data")						,;
		        Strzero(&(cAlias+"->"+cP+"_Hora"),5,2) 				,; 
		                &(cAlias+"->"+cP+"_TipoRef")					,;
				        Posicione("SPM",1,fFilFunc("SPM")+&(cAlias+"->"+cP+"_TipoRef"),"PM_DESCREF"),;
		                &(cAlias+"->"+cP+"_ValRef")						,;
					    &(cAlias+"->"+cP+"_DescFun")					,;
		                &(cAlias)->(&(cP+"_ValRef")- &(cP+"_DescFun")),;
				        &(cAlias+"->"+cP+"_PROCES")						,;  //Processo
				        &(cAlias+"->"+cP+"_PERIODO")					,;  //Periodo
				        &(cAlias+"->"+cP+"_ROTEIR")						,;  //Roteiro
				        &(cAlias+"->"+cP+"_NUMPAG")						 ;  //Num Pagto	                
		                })

	Else

		//-- Armazena Linhas Detalhes de Refeicao Por Funcionario
		Aadd(aRefFunc, {&(cAlias+"->"+cP+"_Data")						,;
		        Strzero(&(cAlias+"->"+cP+"_Hora"),5,2) 					,; 
		                &(cAlias+"->"+cP+"_TipoRef")					,;
		                &(cAlias+"->"+cP+"_ValRef")						,;
					    &(cAlias+"->"+cP+"_DescFun")					,;
		                &(cAlias)->(&(cP+"_ValRef")- &(cP+"_DescFun"))  ,;
				        &(cAlias+"->"+cP+"_PROCES")						,;  //Processo
				        &(cAlias+"->"+cP+"_PERIODO")					,;  //Periodo
				        &(cAlias+"->"+cP+"_ROTEIR")						,;  //Roteiro
				        &(cAlias+"->"+cP+"_NUMPAG")						 ;  //Num Pagto	                
		                })
	Endif
	   	                                         
	&(cAlias)->(DbSkip())
EndDo

Return (Nil)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fTestaTotal³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Obtem Marcacoes de Refeicoes do Funcionario                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fTestaTotal()      // Executa Quebras
Local 	nCab	:=1

cTnoAnt 		:= SRA->RA_TNOTRAB
cCcAnt       	:= SRA->RA_CC
cFAnt 			:= SRA->RA_FILIAL

dbSelectArea("SRA")
dbSkip()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Apenas se Carregou informacoes da Empresa					 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF fInfo(@aInfo,cFAnt)

	If Eof() .Or. &cInicio > cFim                       
	 
	    //-- Imprime em Total CC se ordem Turno
		If nordem==4  .AND. !EMPTY(Len(aTipoCC))
		    //Pula Pagina e Imprime Cabec
		    fComplcab(4)
			Impr("","P")
		    ImpCabTot(4)
		    ImpCabTipo()
			ImpTotTipo(@aTipoCC,4)  
		    nCab:=4
			 //-- Imprime em Total CC se ordem C.Custo
		ElseIf (nOrdem == 2 .Or. nOrdem == 5).AND. !EMPTY(Len(aTipoCC))
			//Pula Pagina e Imprime Cabec
		    fComplcab(5)
			Impr("","P")
			ImpCabTot(5)
		    ImpCabTipo()
			ImpTotTipo(@aTipoCC,5) 
			nCab:=5
		Endif	
		If !EMPTY(Len(aTipoFil))
			//Pula Pagina e Imprime Cabec
			fComplcab(2)
			Impr("","P")
		   	ImpCabTot(2)
			ImpCabTipo()
			ImpTotTipo(@aTipoFil,2)
			nCab:=2
	    Endif
	    If !EMPTY(Len(aTipoEmp))
		    fComplcab(3)
			Impr("","P")
			ImpCabTot(3)
			ImpCabTipo()
			ImpTotTipo(@aTipoEmp,3)
			Impr("","F")
			nCab:=3
		Endif
	Else
		if cFAnt # SRA->RA_FILIAL .AND. !EMPTY(Len(aTipoFil))
		    //-- Forca Salto de Pagina na Proxima Impressao de Funcionario
		    lSalta	:=	.T.
		     //-- Imprime em Total CC se ordem Turno
			If nordem==4 .AND. !EMPTY(Len(aTipoCC))
				//Pula Pagina e Imprime Cabec
				fComplcab(4)
		  		Impr("","P")
				ImpCabTot(4)
				ImpCabTipo()
				ImpTotTipo(@aTipoCC,4)
				nCab:=4     
				 //-- Imprime em Total CC se ordem C.Custo
			ElseIf (nOrdem == 2 .Or. nOrdem == 5).AND. !EMPTY(Len(aTipoCC))
				//Pula Pagina e Imprime Cabec
				fComplcab(5)
		  		Impr("","P")
				ImpCabTot(5)
				ImpCabTipo()
				ImpTotTipo(@aTipoCC,5) 
				nCab:=5
			Endif           
		   	If !EMPTY(Len(aTipoFil))
				fComplcab(2)
				Impr("","P")
				ImpCabTot(2)
				ImpCabTipo()	
				ImpTotTipo(@aTipoFil,2)  
				nCab:=2
			Endif	
		ElseIf cTnoAnt # SRA->RA_TNOTRAB .AND. 	nOrdem == 4 .And. !Eof().AND. !EMPTY(Len(aTipoCC))
		    //-- Forca Salto de Pagina na Proxima Impressao de Funcionario
		    lSalta	:=	.T.
		    fComplcab(4)
		    //Pula Pagina e Imprime Cabec
			Impr("","P")
			ImpCabTot(4)
			ImpCabTipo()
			ImpTotTipo(@aTipoCC,4)   
			nCab:=4
		ElseIf (nOrdem == 2 .Or. nOrdem == 5).And. SRA->RA_CC # cCCAnt .And. !Eof()    ;
			.AND. !EMPTY(Len(aTipoCC))
		    //-- Forca Salto de Pagina na Proxima Impressao de Funcionario
		    lSalta	:=	.T.
		    fComplcab(5)
		    //Pula Pagina e Imprime Cabec
			Impr("","P")
			ImpCabTot(5)
			ImpCabTipo()
			ImpTotTipo(@aTipoCC,5)         
			nCab:=5
		Endif
	
	Endif
EndIF
//Atualiza as variaveis e informacoes complementares para a proxima impressao de cabecalho
cTnoAnt 		:= SRA->RA_TNOTRAB
cCcAnt       	:= SRA->RA_CC 
fComplcab(nCab)

Return Nil    

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fConmplCab³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Complementa Cabecalho Padrao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³fComplCab(nTipo)  --> nTipo = Tipo totalizacao              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fComplCab(nTipo)            

Default nTipo	:= 1  //Funcionario

WCabec0:=1
wCabec1:= STR0020  + Left(SM0->M0_Nome+Space(15),15) // 'Empresa: ' 
WCabec2:=''  

If nTipo <> 3 //Empresa
	WCabec0:=2

	wCabec2:= STR0016  + cFAnt + '-' + Left(ainfo[2] + Space(15),15)  // 'Filial: '
    //Se ordem C.Custo  e o Total nao for de Filial
    If(nordem == 2 .or. nordem == 5) .AND. (nTipo == 5 .OR. nTipo == 1) //C.Custo   
       wCabec2+=STR0018  + SUBS(cCcAnt+SPACE(20),1,20) + '-' // C.C: '
	   wCabec2+= Left(DescCc(cCcAnt  ,cFAnt,27) + Space(27), 27)    
    //Se ordem Turno  e o Total nao for de Filial
    ElseIf nordem == 4  .AND. (nTipo ==4	 .OR. nTipo == 1)//Turno   
        wCabec2+= STR0017  + cTnoAnt //' Turno: '###'
    Endif
Endif

Return (Nil)
	
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fImpMarc  ³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime Marcacoes de Refeicoes do Funcionari               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
//--IMPRESSAO---------------------------------------	
Function fImpMarc(aRefFunc,aTipos)	
Local nFor		:= 0
Local cDetProc  := ''

If Len(aRefFunc) > 0 .And. !lSintetico 
    //-- Se houve quebra salta e atualiza e imprime novo cabecalho 
    If lSalta
       Impr("","P")
       lSalta	:=	.F.
    Endif   
    //-- Garante que o Nome do Funcionario Seja impresso na mesma pagina 
    //-- que as informacoes da refeicao 
    If LI>54
      //-- Salta Pagina
      Impr("","P")
    Endif    
    fImpCabFun()   
    
    //-- Imprime Detalhes das Marcacoes de Refeicoes
    If lDiaDia
       //-- Cabecalho Detalhes das Marcacoes de Refeicoes
       fImpCabDia()
       
       dDataFunc :=Ctod(Space(8))
		//-- Corre Todas as Marcacoes de Refeicoes
		//-- Quebra Data
		//-- Tamanho Maximo Linha: 60 caracteres
		For nFor := 1 To Len(aRefFunc)
			If LI>57         
		   	   fImpCabFun()
			   fImpCabDia() 
		   	Endif
		   	If cChave <> aRefFunc[nFor,7] + aRefFunc[nFor,8] + aRefFunc[nFor,9] + aRefFunc[nFor,10]
		   		cDetProc := STR0030 + aRefFunc[nFor,7] + " - " + ;
		   					STR0031 + aRefFunc[nFor,8] + " - " + ;
		   					STR0032 + aRefFunc[nFor,9] + " - " + ;
		   					STR0033 + aRefFunc[nFor,10]
		   		Impr(cDetProc,'C')
		   		cChave := aRefFunc[nFor,7] + aRefFunc[nFor,8] + aRefFunc[nFor,9] + aRefFunc[nFor,10]
		   	EndIf

			//-- Verifica Quebra de Data
			If DTOS(dDataFunc)<>DTOS(aRefFunc[nFor,1]) 
			   //-- Iguala Variavel Verificadora de quebra
			   dDataFunc:=aRefFunc[nFor,1]  
			   //-- Somente a  Primeira Linha da Data conterah a Data Corrente
			   cDet:=PADR(Dtoc(dDataFunc),10) + SPACE(01)            
			Else    
				//-- Inicio das Linhas Seguintes contem data em branco
				cDet:= SPACE(11)  
			Endif
			   
		    //--Monta Linha Detalhe   
		    //-- Data + Hora + Tipo Ref + Valor Ref + Desc.Func. + Custo Empresa
   	 
			cDet += StrTran(aRefFunc[nFor,2],'.',':') +Space(2)
			cDet += aRefFunc[nFor,3]+ Space(3)
			cDet += Transform(aRefFunc[nFor,4],'@E 999,999,999.99') +Space(1)
			cDet += Transform(aRefFunc[nFor,5],'@E 999,999,999.99') +Space(1)
			cDet += Transform(aRefFunc[nFor,6],'@E 999,999,999.99') 
		   
		   	Impr(cDet,'C')
			cDet:=''
		Next nFor                         
	
	Endif             
	
	//Imprime totais dos Tipos do funcionario
	
	ImpCabTipo()
	ImpTotTipo(@aTipos,1)	
    
	cDet:=''
Endif

Return (Nil)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fImpCabFun³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime Cabec Funcionario e Cabec Detalhes Refeicoes       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImpCabFun()   

//Imprime Cabec Matr e Nome
cDet := STR0013 + SRA->RA_MAT + ' ' + STR0014 + SUBS(SRA->RA_NOME+SPACE(30),1,30)

//-- Imprime C.C se ordem Turno
If nordem==4 
    cDet+=STR0018  + SUBS(SRA->RA_CC+SPACE(20),1,20) + '-' // ' C.C: '
	cDet+=Left(DescCc(SRA->RA_CC  ,SRA->RA_FILIAL,27) + Space(27), 27)
//-- Imprime Turno se Ordem CC 
ElseIf (nOrdem == 2 .Or. nOrdem == 5)
	cDet+=STR0017  + SRA->RA_TNOTRAB + ' ' + STR0035 + SRA->RA_DEPTO
Else
	cDet+=STR0035 + SRA->RA_DEPTO
Endif	

Impr(cDet,'C')
cDet := ''  

Return(Nil)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fImpCabDia³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime Cabec Detalhes Refeicoes                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImpCabDia()   
//-- Quando Opcao for Dia a Dia imprime separador 

cDet:=''
IMPR(Repl('-',80),'C')
	
//-- Imprime Cabec Detalhes de Marcacoes de Refeicoes
cDet := STR0022
Impr(cDet,'C')

Return(Nil)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ImpTotTipo³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime  Totais dos Tipos                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ImpTotTipo(aMatTipo,nIdent)  

Local aTotTipo	:=	{}
Local aTot		:=	{}
Local naTipos	:=	Len(aMatTipo) 
Local nFor		:= 0
Local cDet		:=	''

//-- L  E G E N D A
//-- Ordena Tipos em Ordem Crescente 
Asort(aMatTipo,,,{|x,y| x[1]<y[1]})

//-- Totaliza Colunas dos Tipos
aTotTipo:=fSoma(@aMatTipo ,{4,5,6,7})         

//-- Preenche 1.o Elemento da array Total dos Tipos com "Total: "
aTot:= {SPace(2),PADR(ALLTRIM(UPPER(STR0015)),20),0}                             
//-- Preenche os demais elementos com a totalizacao das Colunas dos Tipos
Aeval(aTotTipo,{ |x| AADD(aTot,x) })
	   
//-- Adiciona o array totalizador ao array dos Tipos que o Originou
//-- Para ser impresso como ultimo elemento
AADD(aMatTipo,aTot)

naTipos	:=	Len(aMatTipo) 

For nFor := 1 To naTipos
    //--Garante que pelo menos um Totalizador seja tranportado para a pagina seguinte 
    If LI >54        
        //-- Salta Pagina
        Impr("",'P')
        
        //-- Se a Totalizacao de Tipos se Referir a Funcionario Imprime 
        //---- Cabec de Identificacao do mesmo    
        If nIdent == 1
    		fImpCabFun()
    		ImpCabTipo()
        Endif
    Endif                                                            
//   'Tipo Descricao                Qtde           Valor    Desc. Func.   Desc.Empresa'
//    01   01234567890123456789 9999.999 999,999,999.99 999,999,999.99 999,999,999.99
        
	cDet := aMatTipo[nFor,1]+ Space(3)  
	cDet += aMatTipo[nFor,2]+ Space(1)                               
	cDet += Transform(aMatTipo[nFor,4],'@E 9999,999') +Space(1)
	cDet += Transform(aMatTipo[nFor,5],'@E 999,999,999.99') +Space(1)
	cDet += Transform(aMatTipo[nFor,6],'@E 999,999,999.99') +Space(1)
	cDet += Transform(aMatTipo[nFor,7],'@E 999,999,999.99') 
   
	Impr(cDet,'C')
	//-- Imprime Total da array Tipos Passada
	If nFor==(naTipos)-1
	   Impr(Repl('',80),'C')
	Endif
	cDet := ''
Next nFor

Impr(Repl('-',80),'C')

aMatTipo:={}

Return (Nil)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ImpCabTot ³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime  Cabec Totalizacao                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ImpCabTot(nIdent)  

Local cDet		:=	''

If nident == 3 		//-- Empresa
	cDet := STR0015 + STR0020  + Left(SM0->M0_Nome+Space(15),15) // 'Empresa: '
    
ElseIf nident == 2 //-- Filial
	cDet := STR0015 + STR0016  + cFAnt + '-' + Left(aInfo[2]+Space(15),15) // 'Filial: '
ElseIf nident ==4  //Turno           
		cDet := STR0015 + STR0017  + cTnoAnt // ' Turno: ' 
ElseIf nident == 5 //-- Centro de Custo 
	cDet := STR0015 + STR0018 + SUBS(cCCAnt+SPACE(20),1,20) + '-' + Left(DescCc(cCCAnt,cFAnt,27)+Space(27),27) // ' C.C: '		
	
Endif

If !Empty(cDet)           
	IMPR(Repl('-',80),'C')
	IMPR(cDet,'C')
Endif	

cDet:=''

Return (Nil)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ImpCabTipo³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime  Cabec dos Tipos                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ImpCabTipo()

//-- Imprime Legenda com Totais de Tipos
Impr(Repl('-',80),'C')
cDet:= ''                     

cDet := STR0023  //STR0023  'Tipo Descricao            Qtde           Valor         Desc. Func.        Desc.Empresa'
Impr(cDet,'C')
cDet := ''                   

Return Nil
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fSomaMat  ³ Autor ³ Mauricio MR           ³ Data ³ 15/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma duas Matrizes                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fSomarMat(aMatrizSoma,aMatriz,nColuna do Identificador)    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fSomaMat(aTot,aElem,nIdentificador,aColunas,nnada)

Local nJ		:=	0                                       
Local nI		:=	0        
Local naElem	:=	Len(aElem)
Local nPos		:=	0  
Local naColunas	:=Len(aColunas)

//Se esta vazio o aTot
If Empty(Len(aTot))           
   //Faz Copia de aElem
   aTot:=Aclone(aElem)
Else
 	//Corre Todas as Linhas de aElem
 	For nI:=1 to naElem          
 	    //Procura Identificador correspondente em aTot
		nPos:=Ascan(aTot,{|xIdent| xIdent[nIdentificador]==aElem[nI][nIdentificador]})
		//Se Encontrou
		If nPos>0
		   //Soma cada uma das Colunas do mesmo Identificador
		   For nJ:=1 To naColunas
		       aTot[nPos, aColunas[nJ] ]+=aElem[nI, aColunas[nJ] ]
		   Next nJ
		Else                       
		   //Se nao encontrou o Identificador adiciona-o a aTot
		   AADD(aTot,aClone(aElem[nI]))
		Endif
	Next nI
Endif	

Return (nil)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fSoma     ³ Autor ³ Mauricio MR           ³ Data ³ 25/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma Matriz                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Fsoma(Matriz,Colunas). Ex. aRefeicoes{ {a,b,c}...{x,y,z}}  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FSoma(aMatriz,aCol)

Local nCol	:=Len(aCol)
Local aSoma := Array(nCol)        
//Inicializa aSoma
Afill(aSoma,0)

If nCol > 1
	aEval(aMatriz,{ |x| fSomarCol(x,aCol,@aSoma) })
Endif

Return (aSoma)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fSomarCol ³ Autor ³ Mauricio MR           ³ Data ³ 15/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma Colunas das Matrizes                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fSomarCol(aMatrizFilha,aColunas,aMatrizSomatoria)          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ POR070IMP                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fSomarCol(aMatriz,aColunas,aSomatoria)

Local naCol	:=	Len(aColunas)                                       
Local nI	:=	0
//-- Corre Cada Coluna da Matriz 
For nI:=1 TO naCol                                
    //-- Soma Cada uma das Colunas Especificadas na matriz aSomatoria
	aSomatoria[nI]+=aMatriz[aColunas[nI]]
Next nI

Return (aSomatoria)
/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr070Proc	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR01 para variavel private		³
|		   |cProcesso    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr070Proc(cTip)

Local lRet := .T.

cProcesso := &( ReadVar() )

If ( cTip == 'A' )
	lRet := PonX1Valid(cProcesso)
Else
	If !Empty(cProcesso)
		lRet := PonX1Valid(cProcesso)
	EndIf
EndIf

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr070Per	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR02 para variavel private		³
|		   |cPeriodo    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr070Per()

Local cChave := cProcesso
Local lRet := .T.

If !Empty(cRoteiro)
	cChave += cRoteiro
Else
	cChave += "PON"
EndIf

cPeriodo := &( ReadVar() )

cChave += cPeriodo

lRet := PonX1Valid(cChave)

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr070Rot	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR03 para variavel private		³
|		   |cRoteiro    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr070Rot()

Local lRet := .T.

cRoteiro := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro)

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr070NPag	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR04 para variavel private		³
|		   |cNumPag	    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr070NPag()

Local lRet := .T.

cNumPagto := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro+cPeriodo+cNumPagto)

Return( lRet )