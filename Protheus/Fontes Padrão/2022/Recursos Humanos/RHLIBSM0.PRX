#INCLUDE "PROTHEUS.CH"
STATIC aSM0
/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInRhLibSm0ExecЁAutor ЁMarinaldo de Jesus   Ё Data Ё14/05/2003Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de RHLIBSM0                          Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInRhLibSm0Exec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InRhLibSm0Exec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁArrEmpFilGet    ЁAutorЁMarinaldo de Jesus Ё Data Ё14/01/2003Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁCarrega Informacos do SIGAMAT.EMP                           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais> 									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais> 									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerico      												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function ArrEmpFilGet()
         
Local aEmpFil	:= {}
Local aSM0      := fGetSM0()
Local i         := 0
Local cCodEmp   
Local cCodFil
Local nRecno

IF ( Select( "SM0" ) == 0 )
	Private cArqEmp := "sigamat.emp"
	OpenSM0()
EndIF

IF ( Select( "SM0" ) > 0 )   

	For i := 1 To Len(aSM0)
	    
		cCodEmp := aSM0[i][SM0_GRPEMP]
		cCodFil := aSM0[i][SM0_CODFIL]
		nRecno  := aSM0[i][SM0_RECNO] 
	
		aAdd( aEmpFil , { cCodEmp , cCodFil , nRecno } )
		
	Next i
	
EndIF

Return( aClone( aEmpFil ) )

/*/
зддддддддддбддддддддддддддддддбдддддбдддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁfIsCorpManage	  ЁAutorЁMauricio TakakuraЁ Data Ё16/01/2010Ё
цддддддддддеддддддддддддддддддадддддадддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna se o ambiente utiliza configuracao de Layout para a Ё
Ё          ЁGestao Corporativa                                          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais> 									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais> 									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerico      												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function fIsCorpManage(cGrpEmp)
Local cSvEmp		:= ""
Local lIsCorpManage := .F.  

DEFAULT cGrpEmp := ""
If Empty(aSM0)
	aSM0:= FWLoadSM0()	// Carrega o SM0 no array
EndIf
If Empty(cGrpEmp)
	lIsCorpManage := ascan(aSM0,{|x| !Empty(x[SM0_EMPRESA]).Or. !Empty(x[SM0_UNIDNEG]) }) // alterado para buscar diretamente o campo da SM0
Else
	lIsCorpManage := ascan(aSM0,{|x| x[SM0_GRPEMP] == cGrpEmp .And. (!Empty(x[SM0_EMPRESA]) .Or. !Empty(x[SM0_UNIDNEG])) }) > 0
EndIf

Return( lIsCorpManage )

//-------------------------------------------------------------------
/*/{Protheus.doc} EMPREPOBJ

@author		Igor Franzoi
@since		04/08/2010
@version	1.0
@return		NIL
@obs		Controle dos campos do SM0 - Empresas
/*/
//-------------------------------------------------------------------
class EMPREPOBJ
	
	data cCodEMP	//Codigo da Empresa
	data cCodFil	//Codigo da Filial
	data cNomEMP	//Nome da Empresa
	data nTypEMP	//Tipo de Identificacao do Empregado
	data cCeiEMP	//CEI
	data cEndEMP	//Nome da Empresa
	data cHasEMP	//HASH
	data cCNPJEMP	//CNPJ da Empresa
	data cCPFEMP	//CPF da Empresa	
	
	Method New()
	Method setVarMem()
	Method setVarFie()	
	Method posEmpSMO(cChave)

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} EMPREPOBJ

@author		Igor Franzoi
@since		04/08/2010
@version	1.0
@return		NIL
@obs		Construtor do objeto
/*/
//-------------------------------------------------------------------
Method New() class EMPREPOBJ

	::cCodEMP	:=	""	//Codigo da Empresa
	::cCodFil	:=  ""	//Codigo da Filial
	::cNomEMP	:=	""	//Nome da Empresa
	::nTypEMP	:=	0	//Tipo de Identificacao do Empregado
	::cCeiEMP	:=	""	//CEI
	::cEndEMP	:=	""	//Nome da Empresa
	::cHasEMP	:=	""	//HASH
	::cCNPJEMP	:=	""	//CNPJ da Empresa
	::cCPFEMP	:=  ""	//CPF da Empresa
	
Return (SELF)

//-------------------------------------------------------------------
/*/{Protheus.doc} EMPREPOBJ

@author		Igor Franzoi
@since		04/08/2010
@version	1.0
@return		NIL
@obs		Seta os valores das variaveis, com valores de memoria do SM0
/*/
//-------------------------------------------------------------------
Method setVarMem() class EMPREPOBJ

	//::cCodEMP	:=	M->	//Codigo da Empresa
	//::cNomEMP	:=	M->	//Nome da Empresa
	//::nTypEMP	:=	M->	//Tipo de Identificacao do Empregado
	//::nCeiEMP	:=	M->	//CEI
	//::cEndEMP	:=	M->	//Nome da Empresa
	//::cHasEMP	:=	M->	//HASH
	//::cCNPJEMP	:=	M->	//CNPJ da Empresa

Return (NIL)

//-------------------------------------------------------------------
/*/{Protheus.doc} New()

@author		Igor Franzoi
@since		04/08/2010
@version	1.0
@return		NIL
@obs		Seta os valores das variaveis, com valores da tabela SM0
/*/
//-------------------------------------------------------------------
Method setVarFie() class EMPREPOBJ

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁGarante que o alias SM0 esta selecionado					  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	If ( Select( "SM0" ) > 0 )

		If ( SM0->(!Eof()) )
		
			::cCodEMP	:=	SM0->M0_CODIGO		//Codigo da Empresa
			::cCodFil	:=	SM0->M0_CODFIL		//Codigo da Filial
			::cNomEMP	:=	SM0->M0_NOMECOM		//Nome da Empresa
			::cEndEMP	:=	SM0->M0_ENDENT		//Endereco da Empresa			
						
			/*/
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁTipo de Identificacao do Empregador						  Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			If ( SM0->M0_TPINSC == 1 ) //CEI
				::nTypEMP	:=	2
				::cCeiEMP	:= SM0->M0_CGC
				::cCNPJEMP	:= ""
				::cCPFEMP	:= ""
			ElseIf ( SM0->M0_TPINSC == 2 ) //CNPJ
				::nTypEMP	:=	1
				::cCeiEMP	:= ""
				::cCNPJEMP	:=	SM0->M0_CGC	//CNPJ da Empresa
				::cCPFEMP	:=	""
			ElseIf ( SM0->M0_TPINSC == 3 ) //CPF
				::cCeiEMP	:= ""
				::cCNPJEMP	:= ""
				::cCPFEMP	:= SM0->M0_CGC
			EndIf
			
			//::cHasEMP	:=	SM0->	//HASH				
	
		EndIf
	
	EndIf
	
Return (NIL)

//-------------------------------------------------------------------
/*/{Protheus.doc} New()

@author		Allyson Mesashi
@since		26/08/2010
@version	1.0
@return		NIL
@obs		Posiciona a empresa logada no SM0
/*/
//-------------------------------------------------------------------
Method posEmpSMO(cChave) class EMPREPOBJ

	Default cChave	:= ""
	
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁGarante que o alias SM0 esta selecionado					  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	If ( Select( "SM0" ) == 0 )
		Private cArqEmp := "sigamat.emp"
		OpenSM0()
	EndIf

	If ( Select( "SM0" ) > 0 )
		SM0->( dbSeek(cChave) )
	EndIf

Return (Nil)
