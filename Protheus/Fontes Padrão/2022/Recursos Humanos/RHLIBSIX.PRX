#INCLUDE "PROTHEUS.CH" 

Static __aRetOrder := {}

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRetOrder      Ё Autor ЁMarinaldo de Jesus     Ё Data Ё30/03/2001Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna a o Numero da Ordem Desejada                         	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias 			- Alias para Pesquisa no SINDEX					Ё
Ё          ЁcBusca			- Chave Para Pesquisa do Indice					Ё
Ё          ЁlForceOrdZero	- Se forca a Ordem Zero caso nao Encontre		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁnOrdem - Ordem que corresponde a Chave. Caso Contrrio a Ordem 1	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function RetOrder( cAlias , cBusca , lForceOrdZero )

Local aAreaSIX
Local bAscan
Local cPrefix
Local cChave
Local lChave
Local nOrdem
Local nPosOrdem
Local nLenBusca

DEFAULT cAlias			:= Alias()
DEFAULT cPrefix			:= PrefixoCpo( ( cAlias := Upper( AllTrim( cAlias ) ) ) )
DEFAULT cBusca			:= Upper( cPrefix + "_FILIAL" )
DEFAULT lForceOrdZero	:= .F.

DEFAULT __aRetOrder	:= {}

cAlias		:= Upper( AllTrim( cAlias ) )
cBusca		:= StrTran( Upper( cBusca )," ","")
nLenBusca	:= Len( cBusca := StrTran( Upper( cBusca )," ","") )
bAscan		:= { |x| ( ( x[1] == cAlias ) .and. ( x[2] == SubStr( cBusca , 1 , nLenBusca ) ) .and. ( x[3] == cEmpAnt ) ) }

IF ( ( nPosOrdem := aScan( __aRetOrder , bAscan ) ) > 0 )

	nOrdem := __aRetOrder[ nPosOrdem , 04 ]

Else

	lChave := .F.
	aAreaSIX := SIX->( GetArea() )
	SIX->( dbSetorder( 01 ) )
	IF SIX->( dbSeek( cAlias ) )
		nOrdem := 0
		While SIX->( !Eof() .and. ( cAlias == INDICE ) )
			cChave := StrTran( Upper( SIX->CHAVE ) , " " , "" )
			++nOrdem
			IF ( lChave := ( cBusca == SubStr( cChave , 1 , nLenBusca ) ) )
				Exit
			EndIF
			SIX->( dbSkip() )
		End While
	EndIF
	IF !( lChave )
		nOrdem := 0
	EndIF

	SIX->( dbSetOrder( aAreaSIX[ 02 ] ) )
	IF SIX->( Recno() != aAreaSIX[ 03 ] )
		SIX->( dbGoto( aAreaSIX[ 03 ] ) )
	EndIF

	IF !( lForceOrdZero )
		nOrdem := Max( 1 , nOrdem )
	EndIF	

	aAdd( __aRetOrder , { cAlias , cBusca , cEmpAnt , nOrdem } )

EndIF	

Return( nOrdem  )

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRetOrderRst	  Ё Autor ЁMarinaldo de Jesus     Ё Data Ё12/03/2003Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁReinicializa as Statics em RetOrder                          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ																Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL																Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function RetOrderRst()
Return( ( __aRetOrder := {} ) )

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRetOrdem      Ё Autor ЁMarinaldo de Jesus     Ё Data Ё30/03/2001Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna a o Numero da Ordem Desejada                         	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias 			- Alias para Pesquisa no SINDEX					Ё
Ё          ЁcBusca			- Chave Para Pesquisa do Indice					Ё
Ё          ЁlForceOrdZero	- Se forca a Ordem Zero caso nao Encontre		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁnOrdem - Ordem que corresponde a Chave. Caso Contrrio a Ordem 1	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function RetOrdem( cAlias , cBusca , lForceOrdZero )

Local cPrefix

Default cAlias	:= Alias()

IF Empty( cBusca )
	cPrefix	:= PrefixoCpo( ( cAlias := Upper( AllTrim( cAlias ) ) ) )
	cBusca	:= Upper( cPrefix + "_FILIAL+" + cPrefix + "_MAT"  )
EndIf

Default lForceOrdZero	:= .F.

Return( RetOrder( cAlias , cBusca , lForceOrdZero ) )

/*
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRstRetOrdem   Ё Autor ЁMarinaldo de Jesus     Ё Data Ё12/03/2003Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁReinicializa as Statics em RetOrdem                          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ																Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL																Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function RstRetOrdem()
Return( RetOrderRst() )
