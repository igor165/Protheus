#INCLUDE "PROTHEUS.CH"
#INCLUDE "APDA050.CH"

Static bRd2CnPdFilter
Static cRd2ItemInit

/*/
зддддддддддбддддддддддбдддддбдддддддддддддддддддддддддбддддддбддддддддддддд©
ЁFun┤└o    ЁAPDA050   ЁAutorЁMarinaldo de Jesus       Ё Data Ё  16/07/2003 Ё
цддддддддддеддддддддддадддддадддддддддддддддддддддддддаддддддаддддддддддддд╢
ЁDescri┤└o ЁCadastro de Compet┬ncias                                       Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                       Ё
цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё            ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL              Ё
цддддддддддддбддддддддддбддддддддддддддбддддддддддддддддддддддддддддддддддд╢
ЁProgramador ЁData      ЁBOPS          ЁMotivo da Alteracao                Ё
цддддддддддддеддддддддддеддддддддддддддеддддддддддддддддддддддддддддддддддд╢
ЁCecilia Car.Ё03/07/2014ЁTPZVUR        ЁIncluido o fonte da 11 para a 12 e Ё
Ё            Ё          Ё              Ёefetuada a limpeza.                Ё
цддддддддддддеддддддддддеддддддддддддддеддддддддддддддддддддддддддддддддддд╢
ЁWillian U.  Ё24/08/2017ЁDRHPONTP-1547 ЁAjuste para apresentar conteЗdo do Ё
Ё            Ё          Ё              Ёcampo memo na rotina de cadastro deЁ
Ё            Ё          Ё              ЁcompetЙncia.                       Ё
юддддддддддддаддддддддддадддддддддддаддддддддддддддддддддддддддддддддддды/*/
Function APDA050( cAlias , nReg , nOpc , lExecAuto , nOpcNewGd )

Local aArea 	:= GetArea()
Local aAreaRdm	:= RDM->( GetArea() )
Local aAreaRd2	:= RD2->( GetArea() )
Local lExistOpc	:= ( ValType( nOpc ) == "N" )

Local bBlock
Local nPos

Begin Sequence
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSo Executa se o Modo de Acesso dos Arquivos do Modulo APD estiЁ
	Ёverem OK													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( ApdRelationFile() )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁRedefine o Alias                                              Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cAlias	:= "RDM"

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define Array contendo as Rotinas a executar do programa      Ё
	Ё ----------- Elementos contidos por dimensao ------------     Ё
	Ё 1. Nome a aparecer no cabecalho                              Ё
	Ё 2. Nome da Rotina associada                                  Ё
	Ё 3. Usado pela rotina                                         Ё
	Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
	Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
	Ё    2 - Simplesmente Mostra os Campos                         Ё
	Ё    3 - Inclui registros no Bancos de Dados                   Ё
	Ё    4 - Altera o registro corrente                            Ё
	Ё    5 - Remove o registro corrente do Banco de Dados          Ё
	Ё    6 - Copiar                                                Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina

	Private cCadastro   := OemToAnsi( STR0007 )	//"Compet┬ncias"

	IF ( lExistOpc )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁGarante o Posicinamento do Recno                              Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		DEFAULT nReg	:= ( cAlias )->( Recno() )
		IF !Empty( nReg )
			( cAlias )->( MsGoto( nReg ) )
		EndIF

		DEFAULT lExecAuto := .F.
		IF ( lExecAuto )

			nPos := aScan( aRotina , { |x| x[4] == nOpc } )
			IF ( nPos == 0 )
				Break
			EndIF
			bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nPos , 2 ] + "(a,b,c,d,e) }" )
			Eval( bBlock , cAlias , nReg , nPos )
		
		Else

			Apda050Mnt( cAlias , nReg , nOpc , .T. , nOpcNewGd )
		
		EndIF	
	
	Else

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Chama a Funcao de Montagem do Browse                                   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		mBrowse( 6 , 1 , 22 , 75 , cAlias )
	
	EndIF
	
End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁColoca o Ponteiro do Mouse em Estado de Espera			   	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura os Dados de Entrada 											 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aAreaRd2 )
RestArea( aAreaRdm )
RestArea( aArea )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura o Cursor do Mouse                				   	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( NIL )

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInApda050Exec ЁAutor ЁMarinaldo de Jesus   Ё Data Ё24/08/2004Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de APDA050                           Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInApda050Exec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InApda050Exec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= &( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda050VisЁ Autor ЁMarinaldo de Jesus     Ё Data Ё27/02/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Compet┬ncias (visualizar)						Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda050Vis( cAlias , nReg , nOpcNewGd )
Return( APDA050( cAlias , nReg , 2 , NIL , nOpcNewGd ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda050IncЁ Autor ЁMarinaldo de Jesus     Ё Data Ё27/02/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Compet┬ncias (Incluir)							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda050Inc( cAlias , nReg , nOpcNewGd )
Return( APDA050( cAlias , nReg , 3 , NIL , nOpcNewGd ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda050AltЁ Autor ЁMarinaldo de Jesus     Ё Data Ё27/02/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Compet┬ncias (Alterar)							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda050Alt( cAlias , nReg , nOpcNewGd )
Return( APDA050( cAlias , nReg , 4 , NIL , nOpcNewGd ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda050DelЁ Autor ЁMarinaldo de Jesus     Ё Data Ё27/02/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Compet┬ncias (Alterar)							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>  		                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda050Del( cAlias , nReg )
Return( APDA050( cAlias , nReg , 5 , NIL , 0 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2Visual Ё Autor ЁMarinaldo de Jesus     Ё Data Ё12/04/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Itens de Visoes ( Visualizar )					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2Visual( cAlias , nReg  )

SetMemoFields( cAlias , GetMemoDb( cAlias ) )
aRotSetOpc( cAlias , @nReg , 2 )

Return( AxVisual( cAlias, nReg , 2 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda050MntЁ Autor ЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Compet┬ncias (Manutencao)						Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda050Mnt( cAlias , nReg , nOpc , lDlgPadSiga , nOpcNewGd )

Local aArea			:= GetArea(Alias())
Local aSvKeys		:= GetKeys()
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aRdmHeader	:= {}
Local aRdmCols		:= {}
Local aRdmFields	:= {}
Local aRdmAltera	:= {}
Local aRdmNaoAlt	:= {}
Local aRdmVirtEn	:= {}
Local aRdmNotFields	:= {}
Local aRdmRecnos	:= {}
Local aRdmKeys		:= {}
Local aRdmVisuEn	:= {}
Local aRd2GdAltera  := {}
Local aRd2GdNaoAlt	:= {}
Local aRd2Recnos	:= {}
Local aRd2Keys		:= {}
Local aRd2NotFields	:= {}
Local aRd2VirtGd	:= {}
Local aRd2VisuGd	:= {}
Local aRd2MemoGd	:= {}
Local aRd2Header	:= {}
Local aRd2Cols		:= {}
Local aRd2Query		:= {}
Local aRdyRecnos	:= {}
Local aRdyKeys		:= {}
Local aLog			:= {}
Local aLogTitle		:= {}
Local aLogGer		:= {}
Local aLogGerTitle	:= {}
Local aButtons		:= {}
Local aSvRdMCols	:= {}
Local aSvRd2Cols	:= {}
Local aFreeLocks	:= {}
Local bRd2GdDelOk	:= { |lDelOk| CursorWait() , lDelOk := Rd2GdDelOk( "RD2" , NIL , nOpc , cCodRDM , nRd2ItemOrd ) , CursorArrow() , lDelOk }
Local bRd2TreeDelOk	:= { || .T. }
Local bSet15		:= { || NIL }
Local bSet24		:= { || NIL }
Local bDialogInit	:= { || NIL }
Local bGdRd2Seek	:= { || NIL }
Local bSvblDblClick := { || NIL }
Local bRd2Sort		:= { || NIL }
Local cFilRDM		:= ""
Local cCodRDM		:= ""
Local cRDMKeySeek	:= ""
Local cMsgYesNo		:= ""
Local cTitLog		:= ""
Local lLocks		:= .F.
Local lExecLock		:= ( ( nOpc <> 2 ) .and. ( nOpc <> 3 ) )
Local lExcGeraLog	:= .F.
Local nOpcAlt		:= 0
Local nRDMUsado		:= 0
Local nRD2Usado		:= 0
Local nLoop			:= 0
Local nLoops		:= 0
Local nRd2ItemOrd	:= RetOrdem( "RD2" , "RD2_FILIAL+RD2_CODIGO+RD2_ITEM+RD2_TREE" )
Local nRd2PosItem	:= 0
Local nRd2MaxLocks	:= 10
Local nRd2GhostCol	:= 0
Local oDlg			:= NIL
Local oEnRdm		:= NIL
Local oGdRd2		:= NIL

Private aGets
Private aTela
Private bGetRdm
Private bGetRd2

Private nGetSX8Len	:= GetSX8Len()

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Coloca o ponteiro do Cursor do Mouse em Estado de Espera     Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorWait()

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁCheca a Opcao Selecionada									   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aRotSetOpc( cAlias , @nReg , nOpc )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta os Dados para a Enchoice							   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aRdmNotFields	:= { "RDM_FILIAL" }
		bGetRdm			:= { |lLock,lExclu|	IF( lExecLock , ( lLock := .T. , lExclu	:= .T. ) , aRdmKeys := NIL ),;
											aRdmCols := RDM->(;
																GdBuildCols(	@aRdmHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
																				@nRdmUsado		,;	//02 -> Numero de Campos em Uso
																				@aRdmVirtEn		,;	//03 -> [@]Array com os Campos Virtuais
																				@aRdmVisuEn		,;	//04 -> [@]Array com os Campos Visuais
																				"RDM"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
																				aRdmNotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
																				@aRdmRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
																				"RDM"		   	,;	//08 -> Alias do Arquivo Pai
																				NIL				,;	//09 -> Chave para o Posicionamento no Alias Filho
																				NIL				,;	//10 -> Bloco para condicao de Loop While
																				NIL				,;	//11 -> Bloco para Skip no Loop While
																				NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols 
																				NIL				,;	//13 -> Se Sera considerado o Inicializador Padrao
																				NIL				,;	//14 -> Opcional, Carregar Todos os Campos
																				NIL				,;	//15 -> Opcional, Nao Carregar os Campos Virtuais
																				NIL				,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
																				NIL				,;	//17 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
																				NIL				,;	//18 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
																				NIL				,;	//19 -> Carregar Coluna Fantasma
																				NIL				,;	//20 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
																				.F.				,;	//21 -> Verifica se Deve Checar se o campo eh usado
																				.F.				,;	//22 -> Verifica se Deve Checar o nivel do usuario
																				NIL				,;	//23 -> Verifica se Deve Carregar o Elemento Vazio no aCols
																				@aRdmKeys		,;	//24 -> [@]Array que contera as chaves conforme recnos
																				@lLock			,;	//25 -> [@]Se devera efetuar o Lock dos Registros
																				@lExclu			 ;	//26 -> [@]Se devera obter a Exclusividade nas chaves dos registros
																		    );
															  ),;
											IF( lExecLock , ( lLock .and. lExclu ) , .T. );
		  					} 
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁLock do Registro do RDM									   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF !( lLocks := WhileNoLock( "RDM" , NIL , NIL , 1 , 1 , .T. , 1 , 5 , bGetRdm ) )
			Break
		EndIF
		CursorWait()		
		aSvRdMCols		:= aClone( aRdmCols )
		cFilRDM			:= RDM->RDM_FILIAL
		cCodRDM			:= RDM->RDM_CODIGO
		cRDMKeySeek		:= ( cFilRDM + cCodRDM )
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Cria as Variaveis de Memoria e Carrega os Dados Conforme o arЁ
		Ё quivo														   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		For nLoop := 1 To nRDMUsado
			aAdd( aRdmFields , aRdmHeader[ nLoop , 02 ] )
			SetMemVar( aRdmHeader[ nLoop , 02 ] , aRdmCols[ 01 , nLoop ] , .T. )
		Next nLoop
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁDefine os Campos Editaveis na Enchoice Apenas na Inclusao( 3 )Ё
		Ёou Alteracao(4)											   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
	
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define os Campos Editaveis								   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			nLoops := Len( aRdmVisuEn )
			For nLoop := 1 To nLoops
				aAdd( aRdmNaoAlt , aRdmVisuEn[ nLoop ] )
			Next nLoop
			IF ( nOpc == 4 )
				aAdd( aRdmNaoAlt , "RDM_CODIGO" )
			EndIF
			nLoops := Len( aRdmFields )
			For nLoop := 1 To nLoops
				IF ( aScan( aRdmNaoAlt , { |cNaoA| cNaoA == aRdmFields[ nLoop ] } ) == 0 )
					aAdd( aRdmAltera , aRdmFields[ nLoop ] )
				EndIF
			Next nLoop
		
		EndIF
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define os Campos que nao serao Carregados					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aAdd( aRd2NotFields , "RD2_FILIAL"  )
		aAdd( aRd2NotFields , "RD2_CODIGO"	)
		aAdd( aRd2NotFields , "RD2_DATA"	)
		aAdd( aRd2NotFields , "RD2_STATUS"	)
		IF ( nOpc == 2 )	//Visualizacao
			aAdd( aRd2NotFields , "RD2_CODMEM" )
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta os Dados para a GetDados							   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aRd2Query		:= Array( 05 )
		aRd2Query[01]	:= "RD2_FILIAL='"+cFilRDM+"'"
		aRd2Query[02]	:= " AND "
		aRd2Query[03]	:= "RD2_CODIGO='"+cCodRDM+"'"
		aRd2Query[04]	:= " AND "
		aRd2Query[05]	:= "D_E_L_E_T_=' ' "
	   /*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Quando For Inclusao Posiciona o RD2 No Final do Arquivo	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( nOpc == 3  ) //Inclusao
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Garante que na Inclusao o Ponteiro do RD2 estara em Eof()    Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			PutFileInEof( "RD2" )
		EndIF
		RD2->( dbSetOrder( nRd2ItemOrd ) )
		bGetRd2	:= { |lLock,lExclu| IF( lExecLock , ( lLock := .T. , lExclu := .T. ) , aRd2Keys := NIL ),;
							 		aRd2Cols := RD2->(;
														GdBuildCols(	@aRd2Header		,;	//01 -> Array com os Campos do Cabecalho da GetDados
																		@nRd2Usado		,;	//02 -> Numero de Campos em Uso
																		@aRd2VirtGd		,;	//03 -> [@]Array com os Campos Virtuais
																		@aRd2VisuGd		,;	//04 -> [@]Array com os Campos Visuais
																		"RD2"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
																		aRd2NotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
																		@aRd2Recnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
																		"RDM"		   	,;	//08 -> Alias do Arquivo Pai
																		cRDMKeySeek		,;	//09 -> Chave para o Posicionamento no Alias Filho
																		NIL				,;	//10 -> Bloco para condicao de Loop While
																		NIL				,;	//11 -> Bloco para Skip no Loop While
																		NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols 
																		NIL				,;	//13 -> Se Sera considerado o Inicializador Padrao
																		NIL				,;	//14 -> Opcional, Carregar Todos os Campos
																		NIL				,;	//15 -> Opcional, Nao Carregar os Campos Virtuais
																		aRd2Query		,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
																		.F.				,;	//17 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
																		.F.				,;	//18 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
																		Altera			,;	//19 -> Carregar Coluna Fantasma
																		NIL				,;	//20 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
																		.F.				,;	//21 -> Verifica se Deve Checar se o campo eh usado
																		.F.				,;	//22 -> Verifica se Deve Checar o nivel do usuario
																		NIL				,;	//23 -> Verifica se Deve Carregar o Elemento Vazio no aCols
																		@aRd2Keys		,;	//24 -> [@]Array que contera as chaves conforme recnos
																		@lLock			,;	//25 -> [@]Se devera efetuar o Lock dos Registros
																		@lExclu			,;	//26 -> [@]Se devera obter a Exclusividade nas chaves dos registros
																		nRd2MaxLocks	,;	//27 -> Numero maximo de Locks a ser efetuado
																		Altera			 ;	//28 -> Utiliza Numeracao na GhostCol
																    );
														  ),;
									IF( lExecLock , ( lLock .and. lExclu ) , .T. );
		  		    }
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁLock do Registro do RD2									   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF !( lLocks := WhileNoLock( "RD2" , NIL , NIL , 1 , 1 , .T. , nRd2MaxLocks , 5 , bGetRd2 ) )
			Break
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁColoca o Ponteiro do Mouse em Estado de Espera			 	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		CursorWait()

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁInicializa o Ultimo Valor do RD2_ITEM   				   	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF (;
				Empty( Rd2ItemGet() );
				.or.;
				Empty( StrTran( Rd2ItemGet() , "0" , "" ) );
			)	
			Rd2ItemSet( GdFieldGet( "RD2_ITEM" , Len( aRd2Cols ) , .F. , aRd2Header , aRd2Cols ) )
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁMonta Bloco para Sort antes da Gravacao					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( ( nRd2GhostCol := GdFieldPos( "GHOSTCOL" , aRd2Header ) ) > 0 )
			bRd2Sort := { |x,y| ( x[ nRd2GhostCol ] < y[ nRd2GhostCol ] ) }
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁSalva aRd2Cols para Comparacao na Saida 					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aSvRd2Cols	:= aClone( aRd2Cols )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define os Campos Memos                     				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aAdd( aRd2MemoGd , { "RD2_DESMEM" , "RD2_CODMEM" } )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define os Campos Nao Alteraveis            				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aAdd( aRd2GdNaoAlt  , "RD2_TREE" )
		For nLoop := 1 To Len( aRd2MemoGd )
			aAdd( aRd2GdNaoAlt  , aRd2MemoGd[ nLoop , 02 ] )
		Next nLoop
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega os Campos Editaveis para a GetDados				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		For nLoop := 1	To nRD2Usado
			SetMemVar( aRd2Header[ nLoop , 02 ] , GetValType( aRd2Header[ nLoop , 08 ] , aRd2Header[ nLoop , 04 ] ) , .T. )
			IF (;
					(;
						( aScan( aRd2VirtGd		, aRd2Header[ nLoop , 02 ] ) == 0 ) .and.	;
			   			( aScan( aRd2VisuGd		, aRd2Header[ nLoop , 02 ] ) == 0 ) .and.	;
			   			( aScan( aRd2NotFields	, aRd2Header[ nLoop , 02 ] ) == 0 ) .and.	;
			   			( aScan( aRd2GdNaoAlt	, aRd2Header[ nLoop , 02 ] ) == 0 )			;
			   		) .or. ;
			   		( aScan( aRd2MemoGd	, { |x| aRd2Header[ nLoop , 02 ] == x[1] } ) > 0 )	;
			  	)
				aAdd( aRd2GdAltera , aRd2Header[ nLoop , 02 ] )
			EndIF			   
		Next nLoop
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁCarrega os Recnos e as Chaves correspondentes dos campos MemosЁ
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( ( nOpc <> 2 ) .and. ( nOpc <> 3 ) )
			IF !( lLocks := ApdMemRec( "RD2" , aRd2Recnos , aRd2MemoGd , @aRdyRecnos , @aRdyKeys , .T. ) )
				Break
			EndIF
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁConfigura o Tree  											   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( nOpc == 6  )
			bRd2TreeDelOk	:= { |cItem,aLog,aLogTitle| RD2->( dbSetOrder( nRd2ItemOrd ) ) , ApdChkDel( "RD2" , NIL , nOpc , ( cCodRDM + cItem ) , .F. , @aLog , @aLogTitle , { "RD2" } ) }
			Apd050Tree( cAlias , nReg , nOpc , bRd2TreeDelOk )
			Break
		EndIF	
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁVerifica se Pode Efetuar a Delecao dos Registros			   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( nOpc == 5 ) 
			cMsgYesNo	:= OemToAnsi(;
										STR0019 + ;	//"O sistema ir═ efetuar a verifica┤└o para ver se o registro selecionado"
										CRLF	+ ;	
										STR0020 + ;	//"para exclus└o est═ sendo utilizado. A verifica┤└o pode ser demorada." 
										CRLF	+ ;
										CRLF	+ ;
										STR0021	  ;	//"Confirma a exclus└o?"
									 )
			cTitLog		:= OemToAnsi( STR0022 )	//'Log de verifica┤└o de exclus└o'
			CursorArrow()
			IF ( MsgYesNo( OemToAnsi( cMsgYesNo ) , cCadastro + " - " + OemToAnsi( cTitLog ) ) )
				CursorWait()
				nRd2PosItem := GdFieldPos( "RD2_ITEM" , aRd2Header )
				RD2->( dbSetOrder( nRd2ItemOrd ) )
				nLoops := Len( aRd2Recnos )
				For nLoop := 1 To nLoops
					IF !( ApdChkDel( "RD2" , aRd2Recnos[ nLoop ] , nOpc , ( cCodRDM + aRd2Cols[ nLoop , nRd2PosItem ] ) , .F. , @aLog , @aLogTitle , { "RD2" } ) )
						aAdd( aLogGer , aClone( aLog ) )
						aAdd( aLogGerTitle , aLogTitle[1] )
					EndIF
				Next nLoop
				IF !( lExcGeraLog := !Empty( aLogGer ) )
					IF !( ApdChkDel( cAlias , nReg , nOpc , cCodRDM , .F. , @aLog , @aLogTitle , { "RD2" } ) )
						aAdd( aLogGer , aClone( aLog ) )
						aAdd( aLogGerTitle , aLogTitle[1] )
					EndIF
				EndIF
				IF ( lExcGeraLog := !Empty( aLogGer ) )
					CursorArrow()
					//"Deseja gerar Log?"
					IF ( lExcGeraLog := MsgNoYes( STR0025 , cCadastro + " - " + OemToAnsi( cTitLog ) ) )
						CursorWait()
						//"Log de Inconsistencia na Exclusao de Competencias"
						fMakeLog( aLogGer , aLogGerTitle , NIL , NIL , FunName() , STR0015 )
						CursorArrow()
					Else
						//"A chave a ser excluida est═ sendo utilizada."
						//"At┌ que as refer┬ncias a ela sejam eliminadas a mesma n└o pode ser excluida."
						MsgInfo( OemToAnsi( STR0023 + CRLF + STR0024 ) , cCadastro + " - " + OemToAnsi( cTitLog ) )
					EndIF
					Break
				EndIF
			Else
				Break
			EndIF
			CursorWait()
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta as Dimensoes dos Objetos         					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		DEFAULT lDlgPadSiga	:= .F.
		aAdvSize		:= MsAdvSize( NIL , lDlgPadSiga )
		aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
		aAdd( aObjCoords , { 000 , 050 , .T. , .F. } )
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
		aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Botao de Pesquisa na GetDados					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		bGdRd2Seek := { ||	GdRd2Seek( oGdRd2 )				,;
							SetKey( VK_F4 , bGdRd2Seek )	 ;
				   }
		aAdd(;
				aButtons	,;
								{;
									"pesquisa",;
		   							bGdRd2Seek,;
		       	   					OemToAnsi( STR0001 + "...<F4>" ),;	//"Pesquisar"
		       	   					OemToAnsi( STR0001			   );	//"Pesquisar"
		           				};
		     )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Bloco para a Tecla <CTRL-O> 						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		bSet15		:= { || IF(; 
									(;
										( nOpc == 3 );	//Inclusao
										.or.;
										( nOpc == 4 );	//Alteracao
									);					
									.and.;													
									Apda050TEncOk( nOpc , oEnRdm );							//Valida Todos os Campos da Enchoice
									.and.;													
									IF(;
										!ArrayCompare( aSvRd2Cols , oGdRd2:aCols ),;
										oGdRd2:TudoOk(),;									//Valida as Informacoes da GetDados
										.T.;
									  ),;		
									(;
										nOpcAlt 	:= 1 ,;
										aRd2Cols	:= oGdRd2:aCols,;						//Redireciona o Ponteiro do aRd2Cols
										RestKeys( aSvKeys , .T. ),;
										oDlg:End();
								 	),;
								 	IF(; 
								 		(;
								 			( nOpc == 3 );	//Inclusao
								 			.or.;
								 			( nOpc == 4 );	//Alteracao
								 		 ),;				
								 			(;
								 				nOpcAlt := 0,;
								 				.F.;
								 			 ),;
										(;
											nOpcAlt := IF( nOpc == 2 , 0 , 1 ) ,;			//Visualizacao ou Exclusao
											RestKeys( aSvKeys , .T. ),;
											oDlg:End();
								 		);
								 	  );
							   );
						 }
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Bloco para a Teclas <CTRL-X>     	   			   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		bSet24		:= { || ( nOpcAlt := 0 , RestKeys( aSvKeys , .T. ) , oDlg:End() ) }
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Bloco para o Init do Dialog						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		bDialogInit := { ||;
								EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons ),;
								SetKey( VK_F4 , bGdRd2Seek  ),;
						}
		
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Restaura o Ponteiro do Cursor do Mouse                  	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorArrow()
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta o Dialogo Principal para a Manutencao das Formulas	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0007 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF GetWndDefault() PIXEL

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto Enchoice para o RDM                      	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oEnRdm	:= MsmGet():New(	cAlias		,;
									nReg		,;
									nOpc		,;
									NIL			,;
									NIL			,;
									NIL			,;
									aRdmFields	,;
									aObjSize[1]	,;
									aRdmAltera	,;
									NIL			,;
									NIL			,;
									NIL			,;
									oDlg		,;
									NIL			,;
									.F.			,;
									NIL			,;
									.F.			 ;
								)

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Valor DEFAULT nOpcNewGd									   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		DEFAULT nOpcNewGd := IF( ( ( nOpc == 2 ) .or. ( nOpc == 5 ) ) , 0 , GD_INSERT + GD_UPDATE + GD_DELETE	)

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto GetDados para o RD2						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oGdRd2	:= MsNewGetDados():New(	aObjSize[2,1]														,;
											aObjSize[2,2]													,;
											aObjSize[2,3]													,;
											aObjSize[2,4]													,;
											nOpcNewGd														,;
											"Rd2GdLinOk"													,;
											"Rd2GdTudOk"													,;
											"+RD2_ITEM"														,;
											aRd2GdAltera													,;
											0																,;
											Val(Replicate("9",GetSx3Cache( "RD2_ITEM" , "X3_TAMANHO" )))	,;
											NIL																,;
											NIL																,;
											bRd2GdDelOk														,;
											oDlg															,;
											aRd2Header														,;
											aRd2Cols									 					 ;
										  )
		
		bSvblDblClick := oGdRd2:oBrowse:blDblClick
		oGdRd2:oBrowse:blDblClick := { || Apda050EdtMem( oGdRd2 , bSvblDblClick , ( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) ) }
	
	ACTIVATE MSDIALOG oDlg ON INIT Eval( bDialogInit ) CENTERED

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Coloca o Ponteiro do Mouse em Estado de Espera			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorWait()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁQuando Confirmada a Opcao e Nao for Visualizacao Grava ou   ExЁ
	Ёclui as Informacoes do RDM e RD2							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF( nOpcAlt == 1 )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Apenas se nao For Visualizacao              				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
 		IF ( nOpc != 2 )
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Gravando/Incluido ou Excluindo Informacoes do SRY/RD2        Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			MsAguarde(;
						{ ||;
								aSort( aRd2Cols , NIL , NIL , bRd2Sort ),;	//Sorteia as Informacoes do RD2 para Comparacao Antes da Gravacao
								Apda050Grava(;
												nOpc		,;	//Opcao de Acordo com aRotina
							 					nReg		,;	//Numero do Registro do Arquivo Pai ( RDM )
							 					aRdmHeader	,;	//Campos do Arquivo Pai ( RDM )
							 					aRdmCols	,;	//Conteudo Atual dos Campos do Arquivo Pai ( RDM )
							 					aSvRdmCols	,;	//Conteudo Anterior dos Campos do Arquivo Pai ( RDM )
							 					aRdmVirtEn	,;	//Campos Virtuais do Arquivo Pai ( RDM )
							 					aRd2Header	,;	//Campos do Arquivo Filho ( RD2 )
							 					aRd2Cols	,;	//Itens Atual do Arquivo Filho ( RD2 )
							 					aSvRd2Cols	,;	//Itens Anterior do Arquivo Filho ( RD2 )
							 					aRd2VirtGd	,;	//Campos Virtuais do Arquivo Filho ( RD2 )
							 					aRd2Recnos	,;	//Recnos do Arquivo Filho ( RD2 )
							 					aRd2MemoGd	 ;	//Campos Memo na GetDados ( RD2 )									
							 				 );
						 };
					 )
		EndIF
	Else
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё RollBack da Numeracao Automatica            				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		While ( GetSX8Len() > nGetSX8Len )
			RollBackSX8()
		End While
	EndIF

End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Coloca o Ponteiro do Mouse em Estado de Espera			   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁLibera os Locks             								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
aAdd( aFreeLocks , { "RDM" , aRdmRecnos , aRdmKeys } )
aAdd( aFreeLocks , { "RD2" , aRd2Recnos , aRd2Keys } )
aAdd( aFreeLocks , { "RDY" , aRdyRecnos , aRdyKeys } )
ApdFreeLocks( aFreeLocks )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura os Dados de Entrada								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aArea )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura as Teclas de Atalho								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Ponteiro do Cursor do Mouse                  	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( nOpcAlt )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda050TEncOkЁAutorЁMarinaldo de Jesus    Ё Data Ё18/06/2002Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁTudoOk para a Enchoice                                      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁApda050TEncOk( nOpc , oEnRdm )								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apda050TEncOk( nOpc , oEnRdm )

Local lTudoOk := .T.
                
IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
	lTudoOk := EnchoTudOk( oEnRdm )
EndIF

Return( lTudoOk )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2GdLinOk  ЁAutorЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRd2GdLinOk( oBrowse )									    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2GdLinOk( oBrowse )

Local aCposKey	:= {}
Local lLinOk	:= .T.

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	Begin Sequence
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Se a Linha da GetDados Nao Estiver Deletada				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF !( GdDeleted() )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica Itens Duplicados na GetDados						   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			aCposKey := GetArrUniqe( "RD2" )
			IF !( lLinOk := GdCheckKey( aCposKey , 4 ) )
				Break
			EndIF
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica Se o Campos Estao Devidamente Preenchidos		   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			aCposKey := GdObrigat( aHeader )
			IF !( lLinOk := GdNoEmpty( aCposKey ) )
		    	Break
			EndIF
	
		EndIF
		
	End Sequence
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe Houver Alguma Inconsistencia na GetDados, Seta-lhe o Foco  Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( lLinOk )
		oBrowse:SetFocus()
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Evitar que os Inicializadores padroes sejam carregados indeviЁ
	Ё damente													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	PutFileInEof( "RD2" )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()
	
Return( lLinOk )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2GdTudOk	ЁAutorЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRd2GdTudOk( oBrowse )									    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2GdTudOk( oBrowse )

Local lTudoOk := .T.

Local nLoop
Local nLoops

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	Begin Sequence
	
	    /*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Percorre Todas as Linhas para verificar se Esta Tudo OK      Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		nLoops := Len( aCols )
		For nLoop := 1 To Len( aCols )
			n := nLoop
			IF !( lTudoOk := Rd2GdLinOk( oBrowse ) )
				oBrowse:Refresh()
				Break
			EndIF
		Next n
	
	End Sequence
		
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( lTudoOk  )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2GdDelOk   ЁAutorЁMarinaldo de Jesus    Ё Data Ё18/07/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValidar a Delecao na GetDados                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRd2GdDelOk( cAlias , nRecno , nOpc , cCodigo )				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Rd2GdDelOk( cAlias , nRecno , nOpc , cCodigo , nRd2ItemOrd )
         
Local cDelKeySeek	:= GdFieldGet("RD2_ITEM")
Local cRstKeySeek 	:= GdFieldGet("RD2_TREE")
Local cMsg			:= ""
Local cItem			:= aHeader[ GdFieldPos("RD2_ITEM") , 01  ]
Local lDelOk 		:= .T.
Local lStatusDel	:= .F.
Local nPosKey		:= 0

Static lFirstDelOk
Static lLstDelOk

DEFAULT lFirstDelOk	:= .T.
DEFAULT lLstDelOk	:= .T.

Begin Sequence

	//Quando for Visualizacao ou Exclusao Abandona
	IF (;
			( nOpc == 2 ) .or. ;	//Visualizacao
			( nOpc == 5 );			//Exclusao
		)
		Break
	EndIF

	//Apenas se for a primeira vez
	IF !( lFirstDelOk )
		lFirstDelOk	:= .T.
		lDelOk 		:= lLstDelOk
		lLstDelOk	:= .T.
		Break
	EndIF

	lStatusDel	:= !( GdDeleted() ) //Inverte o Estado
	
	IF ( lStatusDel )	//Deletar
    	nPosKey := aScan( aCols , { |x| x[GdFieldPos("RD2_TREE")] == cDelKeySeek } )
    	IF !( lDelOk := ( nPosKey == 0 .or. ( nPosKey > 0 .and. GdFieldGet( "GDDELETED" , nPosKey ) ) ) )
    		cMsg := STR0008	//'Este ║tem n└o pode ser Deletado.'
    		cMsg += CRLF
    		cMsg += STR0009 //'Delete Primeiro a Linha'
    		cMsg += " "
    		cMsg += AllTrim( Str( nPosKey , 5 , 0 ) )
    		cMsg += " "
    		cMsg += cItem
    		cMsg += ": "
    		cMsg += GdFieldGet( "RD2_ITEM" , nPosKey )
    		MsgInfo( OemToAnsi( cMsg ) )
    		lLstDelOk := lDelOk
			//Ja Passou pela funcao
			lFirstDelOk := .F.
    		Break
    	EndIF
		IF !( nOpc == 3  )	//Quando nao for Inclusao
			RD2->( dbSetOrder( nRd2ItemOrd ) )
			IF !( lDelOk := ApdChkDel( cAlias , nRecno , nOpc , ( cCodigo + GdFieldGet( "RD2_ITEM" ) ) , .F. , NIL , NIL , NIL , NIL , .T. ) )
				CursorArrow()
				//"A chave a ser excluida est═ sendo utilizada."
				//"At┌ que as refer┬ncias a ela sejam eliminadas a mesma n└o pode ser excluida."
				MsgInfo( OemToAnsi( STR0023 + CRLF + STR0024 ) , cCadastro )
				lLstDelOk := lDelOk
    			//Ja Passou pela funcao
				lFirstDelOk := .F.
    			Break
    		EndIF
    	EndIF	
	Else				//Restaurar
    	nPosKey := aScan( aCols , { |x| x[GdFieldPos("RD2_ITEM")] == cRstKeySeek } )
    	IF !( lDelOk := ( nPosKey == 0 .or. ( nPosKey > 0 .and. !GdFieldGet( "GDDELETED" , nPosKey ) ) ) )
    		cMsg := STR0010	//'Este ║tem n└o pode ser Restaurado.'
    		cMsg += CRLF
    		cMsg += STR0011 //'Restaure Primeiro a Linha'
    		cMsg += " "
    		cMsg += AllTrim( Str( nPosKey , 5 , 0 ) )
    		cMsg += " "
    		cMsg += cItem
    		cMsg += ": "
    		cMsg += GdFieldGet( "RD2_ITEM" , nPosKey )
    		MsgInfo( OemToAnsi( cMsg ) )
    		lLstDelOk := lDelOk
			//Ja Passou pela funcao
			lFirstDelOk := .F.
    		Break
    	EndIF
	EndIF

	//Ja Passou pela funcao
	lFirstDelOk := .F.

End Sequence
	
Return( lDelOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda050Grava ЁAutorЁMarinaldo de Jesus    Ё Data Ё21/07/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apda050Grava(	nOpc		,;	//Opcao de Acordo com aRotina
							 	nReg		,;	//Numero do Registro do Arquivo Pai ( RDM )
							 	aRdmHeader	,;	//Campos do Arquivo Pai ( RDM )
							 	aRdmCols	,;	//Conteudo Atual dos Campos do Arquivo Pai ( RDM )
							 	aSvRdmCols	,;	//Conteudo Anterior dos Campos do Arquivo Pai ( RDM )
							 	aRdmVirtEn	,;	//Campos Virtuais do Arquivo Pai ( RDM )
							 	aRd2Header	,;	//Campos do Arquivo Filho ( RD2 )
							 	aRd2Cols	,;	//Itens Atual do Arquivo Filho ( RD2 )
							 	aSvRd2Cols	,;	//Itens Anterior do Arquivo Filho ( RD2 )
							 	aRd2VirtGd	,;	//Campos Virtuais do Arquivo Filho ( RD2 )
							 	aRd2Recnos	,;	//Recnos do Arquivo Filho ( RD2 )
							 	aRd2MemoGd	 ;	//Campos Memo na GetDados ( RD2 )
							  )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis de Inicializacao Obrigatoria					  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local aMestre		:= GdPutIStrMestre( 01 )
Local aItens		:= {}
Local cOpcao		:= IF( ( nOpc == 5 ) , "DELETE" , IF( ( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) , "PUT" , NIL ) )
Local lAllModif		:= .F.
Local lRdmModif		:= .F.
Local lRd2Modif		:= .F.
Local lRd2Delet		:= .F.

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis que serao inicializadas no Corpo da Funcao		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local aRd2ColDel
Local aRd2RecDel
Local nLoop
Local nLoops
Local nItens

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	IF ( cOpcao <> "DELETE" )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica se Houveram Modificacoes no RDV					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( lRd2Modif := !ArrayCompare( aRd2Cols , aSvRd2Cols ) )
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Deleto todos os Itens que nao Estao OK					   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			GdSuperDel( aRd2Header , @aRd2Cols , NIL , .T. , GdGetBlock( "RD2" , aRd2Header , .F. ) ) 
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Separa os Itens que foram Deletados     					   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			lRd2Delet := GdSplitDel( aRd2Header , @aRd2Cols , @aRd2Recnos , @aRd2ColDel , @aRd2RecDel  )
		EndIF
	Else
		lRd2Modif := .T.
		lRdmModif := .T.
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carrega os Itens Apenas se Houveram Alteracoes ou na ExclusaoЁ
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( lRd2Modif )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega as Informacoes deletadas RD2                   	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF ( lRd2Delet )

			aAdd( aItens , GdPutIStrItens() )
			nItens := Len( aItens )
		
			aItens[ nItens , 01 ] := "RD2"
			aItens[ nItens , 02 ] := NIL
			aItens[ nItens , 03 ] := aClone( aRd2Header )
			aItens[ nItens , 04 ] := aClone( aRd2ColDel )
			aItens[ nItens , 05 ] := aClone( aRd2VirtGd )
			aItens[ nItens , 06 ] := aClone( aRd2RecDel )
			aItens[ nItens , 07 ] := aClone( aRd2MemoGd )
			aItens[ nItens , 08 ] := NIL
			aItens[ nItens , 10 ] := "RDY"

		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega as Informacoes do RD2                   			   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aAdd( aItens , GdPutIStrItens() )
		nItens := Len( aItens )

		aItens[ nItens , 01 ] := "RD2"
		aItens[ nItens , 02 ] := {;
									{ "FILIAL" , xFilial( "RD2" , xFilial( "RDM" ) ) },;
									{ "CODIGO" , GetMemVar( "RDM_CODIGO" ) };
							 	 }
		aItens[ nItens , 03 ] := aClone( aRd2Header )
		aItens[ nItens , 04 ] := aClone( aRd2Cols   )
		aItens[ nItens , 05 ] := aClone( aRd2VirtGd )
		aItens[ nItens , 06 ] := aClone( aRd2Recnos )
		aItens[ nItens , 07 ] := aClone( aRd2MemoGd )
		aItens[ nItens , 08 ] := NIL
		aItens[ nItens , 10 ] := "RDY"

	EndIF

	IF !( lRdmModif )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Atualiza aRdmCols para Verificar se Houveram Alteracoes	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		nLoops := Len( aRdmHeader )
		For nLoop := 1 To nLoops
			aRdmCols[ 01 , nLoop ] := GetMemVar( aRdmHeader[ nLoop , 02 ] )
		Next nLoop
		lRdmModif := !( ArrayCompare( aRdmCols , aSvRdmCols ) )
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Seta a Gravacao ou Exclusao Apenas se Houveram Alteracoes  ouЁ
	Ё se foi Selecionada a Exclusao								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
 	lAllModif := ( ( lRd2Modif ) .or. ( lRdmModif ) )
 
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se Houveram Alteracoes, ou Exclusao efetua a Gravacao        Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( lAllModif )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Seta a Gravacao ou Exclusao Apenas se Houveram Alteracoes  ouЁ
		Ё se foi Selecionada a Exclusao								   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aMestre[ 01 , 01 ]	:= "RDM"
		aMestre[ 01 , 02 ]	:= nReg
		aMestre[ 01 , 03 ]	:= lRdmModif
		aMestre[ 01 , 04 ]	:= aClone( aRdmHeader )
		aMestre[ 01 , 05 ]	:= aClone( aRdmVirtEn )
		aMestre[ 01 , 06 ]	:= {}
		aMestre[ 01 , 07 ]	:= aClone( aItens )
		aMestre[ 01 , 08 ]	:= "RDY"
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Grava as Informacoes                        				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		GdPutInfoData( aMestre , cOpcao , .F. , .F. )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Confirmando a Numeracao Automatica          				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		While ( GetSX8Len() > nGetSX8Len )
			ConfirmSX8()
		End While

	EndIF
	
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApd050Tree   ЁAutorЁMarinaldo de Jesus    Ё Data Ё21/07/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apd050Tree( cAlias , nReg , nOpc , bRd2TreeDelOk )

Local aDbTreeInfo	:= {}
Local bRd2TreeAddItem 	:= { |cLastItemNum| Rd2TreeAddItem( cAlias , nReg , cLastItemNum ) }
Local cFilRDM		:= xFilial("RDM")
Local cKeyFilter	:= ""
Local nOrderItem	:= RetOrdem( "RD2" , "RD2_FILIAL+RD2_CODIGO+RD2_ITEM+RD2_TREE" )
Local nOrderTree	:= RetOrdem( "RD2" , "RD2_FILIAL+RD2_CODIGO+RD2_TREE+RD2_ITEM" )

RDM->( MsGoto( nReg ) )
cKeyFilter := RDM->RDM_CODIGO

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Define aDbTreeInfo                                      	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
aDbTreeInfo :=	{;
 			    	{;
 			    		cAlias,;				//01 , 01 -> Alias Mestre do Tree
 			    		nReg,;					//01 , 02 -> Recno para o Posicionamento do Tree
 			    		cFilRDM,;				//01 , 03 -> Filial para a Montagem do Tree
 			    		cKeyFilter,;			//01 , 04 -> Chave para a Montagem do Tree
 			    		"RDM_DESC";				//01 , 05 -> Descricao do Tree
 			    	},;
 			    	{;
 			    		"RD2",;					//02 , 01 -> Alias Filho do Tree
 			    		"RD2_ITEM",;			//02 , 02 -> Item
 			    		nOrderItem,;			//02 , 03 -> Ordem do Item
 			    		"RD2_TREE",;			//02 , 04 -> Tree (Grupo Superior)
 			    		nOrderTree,;			//02 , 05 -> Ordem do Tree
 			    		"RD2_DESC",;			//02 , 06 -> Descricao do Item
 			    		bRd2TreeDelOk,;   		//02 , 07 -> Bloco para o DelOk
 			    		"RD2SetPdFilter()",;	//02 , 08 -> Funcao Para Setar o Filtro
 			    		NIL,;					//02 , 09 -> Bloco para Validar o Cut
 			    		NIL,;					//02 , 10 -> Bloco para Get das Informacoes
 			    		NIL,;					//02 , 11 -> Campo para Get do Resource1
 			    		NIL,;					//02 , 12 -> Campo para Get do Resource2
 			    		"RD2_FILIAL",;			//02 , 13 -> Campo de Filial
 			    		"RD2_CODIGO",;			//02 , 14 -> Campo de Codigo
 			    		bRd2TreeAddItem,;		//02 , 15 -> Bloco para Adicao de Novo Item
 			    		"RD2",;					//02 , 16 -> Alias para a Consulta Padrao
 			    	};
 			    }

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁMonta o Tree             												 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
ApdBldTree( aDbTreeInfoc , STR0007 ) //"Compet┬ncias"

Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2TreeAddItemЁAutorЁMarinaldo de Jesus   Ё Data Ё31/08/2004Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Adicao de Novo Item no Tree                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050                    	                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Rd2TreeAddItem( cAlias , nReg , cLastItemNum )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁSeta o Ultimo Numero Utilizado								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Rd2ItemSet( cLastItemNum )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁExecuta a Rotina de Alteracao para Inclusao de Novos Itens	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Apda050Alt( cAlias , nReg , GD_INSERT + GD_UPDATE )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁLock do Registro do RDM									   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
WhileNoLock( "RDM" , NIL , NIL , 1 , 1 , .T. , 1 , 5 , bGetRdm )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁLock do Registro do RD2									   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
WhileNoLock( "RD2" , NIL , NIL , 1 , 1 , .T. , 10 , 5 , bGetRd2 )

Return( NIL )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda050EdtMem	 ЁAutorЁMarinaldo de JesusЁ Data Ё15/10/2003Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁEdit de Campo Memo                       					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁoGdRd2:oBrowse:blDblClick									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apda050EdtMem( oGdRd2 , blDblClick , lModify , cMemoEdit )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis de Inicializacao Obrigatoria					  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local nColDesMem	:= GdFieldPos( "RD2_DESMEM" , oGdRd2:aHeader )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis que serao inicializadas no Corpo da Funcao		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local aSvKeys
Local aAdvSize
Local aInfoAdvSize
Local aObjSize
Local aObjCoords
Local bSet15
Local bSet24
Local cTitCompl
Local cSvMemoEdit
Local lConfOk
Local oMemoEdit
Local oFont
Local oDlg

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se a Coluna Posicionada nao for a Coluna do Memo             Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( oGdRd2:oBrowse:nColPos == nColDesMem )
		GdRstDblClick( oGdRd2 , blDblClick )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Obtem o Memo                               				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF Empty( cMemoEdit )
		cMemoEdit := GdFieldGet( "RD2_DESMEM" , oGdRd2:oBrowse:nAt , .F. , oGdRd2:aHeader , oGdRd2:aCols )
	EndIF	
	cSvMemoEdit := cMemoEdit

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos             				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdvSize		:= MsAdvSize( .T. , .T. )
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
	aObjCoords		:= { { 0 , 0 , .T. , .T. } }
	aObjSize := MsObjSize( aInfoAdvSize , aObjCoords )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Salva as Teclas de Atalho                  				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aSvKeys := GetKeys()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Tecla <CTRL-O>						   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet15	:= { || RestKeys( aSvKeys , .T. ) , lConfOk := .T. , oDlg:End() }

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Tecla <CTRL-X>     	   				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet24	:= { || RestKeys( aSvKeys , .T. ) , lConfOk := .F. , oDlg:End() }

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carrega o Complemento do Titulo          	   				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cTitCompl := AllTrim( oGdRd2:aHeader[ GdFieldPos( "RD2_DESMEM" , oGdRd2:aHeader ) , 01 ] )
	cTitCompl += " -> "
	cTitCompl += oGdRd2:aHeader[ GdFieldPos( "RD2_ITEM"  , oGdRd2:aHeader ) , 01 ]
	cTitCompl += ": "
	cTitCompl += AllTrim( GdFieldGet( "RD2_ITEM" , oGdRd2:oBrowse:nAt , .F. , oGdRd2:aHeader , oGdRd2:aCols ) )
	cTitCompl += " / "
	cTitCompl += AllTrim( oGdRd2:aHeader[ GdFieldPos( "RD2_DESC" , oGdRd2:aHeader ) , 01 ] )
	cTitCompl += ": "
	cTitCompl += AllTrim( GdFieldGet( "RD2_DESC" , oGdRd2:oBrowse:nAt , .F. , oGdRd2:aHeader , oGdRd2:aCols ) )

	DEFINE FONT oFont NAME "Arial" SIZE 0,-15 //BOLD
	DEFINE MSDIALOG oDlg TITLE cCadastro + OemToAnsi( " - " + cTitCompl ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF GetWndDefault() STYLE DS_MODALFRAME STATUS PIXEL 
			
		@ aObjSize[1,1],aObjSize[1,2] GET oMemoEdit VAR cMemoEdit MEMO SIZE aObjSize[1,4],(aObjSize[1,3]-32) FONT oFont OF oDlg PIXEL WHEN ( .T. )
		oMemoEdit:lReadOnly := !( lModify )
		oDlg:lEscClose := .F. //Nao permite sair ao se pressionar a tecla ESC.
		
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 ) CENTERED
	RestKeys( aSvKeys , .T. )

	IF ( lModify )
		IF ( lConfOk )		//<CTRL-O>
			GdFieldPut( "RD2_DESMEM" , cMemoEdit , oGdRd2:oBrowse:nAt , oGdRd2:aHeader , oGdRd2:aCols , .F. )
			Private n := oGdRd2:oBrowse:nAt
			GdFieldPut( "RD2_DESMEM" , cMemoEdit , oGdRd2:oBrowse:nAt , oGdRd2:aHeader , oGdRd2:aCols , .T. )
		ElseIF !( lConfOk )	//<CTRL-X>
			IF !( cSvMemoEdit == cMemoEdit )
				//"Abandonar as altera┤■es?"
				IF !( MsgNoYes( OemToAnsi( STR0018 ) , cCadastro ) )
					Apda050EdtMem( oGdRd2 , blDblClick , lModify , @cMemoEdit )
				EndIF
			EndIF	
		EndIF
	EndIF
	
End Sequence

Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRD2SetPdFilterЁAutorЁMarinaldo de Jesus   Ё Data Ё14/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSeta Bloco que sera utilizado no Filtro do RD2              Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                   	                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RD2SetPdFilter( bSetFilter )

DEFAULT bRd2CnPdFilter := { || .T. }

IF ( ValType( bSetFilter ) == "B" )
	bRd2CnPdFilter := bSetFilter
Else
	bRd2CnPdFilter	:= { || .T. }
EndIF

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRD2CnPdFilterЁAutorЁMarinaldo de Jesus    Ё Data Ё21/07/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Filtro do RD2 conforme Item                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁConsulta SXB para o RD2                                  	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RD2CnPdFilter( cKeyFilter )

DEFAULT cKeyFilter		:= RD2->RD2_ITEM
DEFAULT bRd2CnPdFilter	:= { || .T. }

Return( Eval( bRd2CnPdFilter , cKeyFilter ) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdmCodigoVld ЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDM_CODIGO          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDM_CODIGO                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdmCodigoVld()

Local cRdmCodigo	:= GetMemVar( "RDM_CODIGO" )
Local lRdmCodigoOk	:= .T.

Begin Sequence

	IF !( lRdmCodigoOk := RdmGetCodigo( @cRdmCodigo , .F. , .T. ) )
    	Break
    EndIF

	SetMemVar( "RDM_CODIGO" , cRdmCodigo )

End Sequence

Return( lRdmCodigoOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdmGetCodigo ЁAutorЁMarinaldo de Jesus    Ё Data Ё27/02/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁObtem Numeracao Valida para o RDM_CODIGO                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁObter Numeracao valida para o RDM_CODIGO                 	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdmGetCodigo( cRdmCodigo , lExistChav , lShowHelp )
Return(;
			GetNrExclOk(	@cRdmCodigo 			,;
							"RDM"					,;
							"RDM_CODIGO"			,;
							"RDM_FILIAL+RDM_CODIGO" ,;
							NIL						,;
							lExistChav				,;
							lShowHelp	 			 ;
						);
		)

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdmCodigoInitЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializador Padrao para o Campo RDM_CODIGO                Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RDM_CODIGO                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdmCodigoInit()
Local cRdmCodigo
RdmGetCodigo( @cRdmCodigo , .F. , .F. )
Return( cRdmCodigo )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdmDescVld 	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё13/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDM_DESC          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDM_DESC	                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdmDescVld()

Local lRdmDescOk := .T.

Begin Sequence

	IF !( lRdmDescOk := NaoVazio() )
		Break
	EndIF

	IF !( lRdmDescOk := Texto() )
    	Break
    EndIF

End Sequence

Return( lRdmDescOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2CodigoVld ЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RD2_CODIGO          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RD2_CODIGO                         	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2CodigoVld()

Local lRd2CodigoOk := .T.

Begin Sequence

	IF !( lRd2CodigoOk := NaoVazio() )
		Break
	EndIF

	IF !( lRd2CodigoOk := ExistCpo("RDM") )
    	Break
    EndIF

End Sequence

Return( lRd2CodigoOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2ItemVld 	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RD2_ITEM          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RD2_ITEM                         		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2ItemVld()

Local lRd2ItemOk := .T.

Begin Sequence

	IF !( lRd2ItemOk := NaoVazio() )
		Break
	EndIF

	IF !( lRd2ItemOk := ExistChav( "RD2" , GetMemVar( "RD2_CODIGO" ) + GetMemVar( "RD2_ITEM" ) ) )
    	Break
    EndIF

End Sequence

Return( lRd2ItemOk )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2ItemInit		ЁAutorЁMarinaldo de Jesus Ё Data Ё31/08/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao de Inicializacao da Numeracao Automatica do RD2_ITEM Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO do campo RD2_ITEM		                        Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2ItemInit()

Local cLastRd2Item
Local cNumRd2Item
Local cKeyAux
Local cRdmCodigo

IF IsMemVar( "RDM_CODIGO" )
	cRdmCodigo := GetMemVar( "RDM_CODIGO" )
Else
	cRdmCodigo := RDM->RDM_CODIGO
EndIF
cKeyAux		:= ( xFilial( "RD2" , xFilial( "RDM" ) ) + cRdmCodigo )

IF !IsInCallStack( "APDA050ALT" , "__EXECUTE/RUNAPP/SIGAAPD/" )
	cLastRd2Item	:= Replicate( "0" , GetSx3Cache( "RD2_ITEM" , "X3_TAMANHO" ) )
Else
	cLastRd2Item	:= Rd2ItemGet()	
EndIF

IF ( StrTran( cLastRd2Item , "0" , "" ) == "" )
	cLastRd2Item := Soma1( cLastRd2Item )
EndIF

IF IsInGetDados( { "RD2_ITEM" } )
	cNumRd2Item := GdNumItem( "RD2_ITEM" , cLastRd2Item )
EndIF

DEFAULT cNumRd2Item	:= cLastRd2Item
cNumRd2Item			:= GetNewCodigo("RD2","RD2_ITEM",GetSx2Unico("RD2"),{ || cNumRd2Item := Soma1(cNumRd2Item) },.F.,.F.,cKeyAux,NIL,cNumRd2Item)

Rd2ItemSet( cNumRd2Item )

Return( cNumRd2Item )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2ItemSet 	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё31/08/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSetar a Variavel Static cRd2ItemInit						Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                            	   	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2ItemSet( cNewItem )

IF Empty( cNewItem )
	cNewItem := Replicate( "0" , GetSx3Cache( "RD2_ITEM" , "X3_TAMANHO" ) )
EndIF

cRd2ItemInit := cNewItem

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2ItemGet 	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё31/08/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁObter o Conteudo da Variavel Static cRd2ItemInit			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                            	   	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2ItemGet()

IF Empty( cRd2ItemInit )
	Rd2ItemSet( NIL )
EndIF

Return( cRd2ItemInit )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2ItemClear ЁAutorЁMarinaldo de Jesus    Ё Data Ё31/08/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁLimpar a Variavel Static cRd2ItemInit						Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                            	   	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2ItemClear()

cRd2ItemInit := NIL

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2DescVld 	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RD2_DESC          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RD2_DESC                         		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2DescVld()

Local lRd2DescOk := .T.

Begin Sequence

	IF !( lRd2DescOk := NaoVazio() )
		Break
	EndIF

	IF !( lRd2DescOk := Texto() )
    	Break
    EndIF

End Sequence

Return( lRd2DescOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2TreeVld 	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RD2_TREE          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RD2_TREE                         		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2TreeVld()

Local lRd2TreeOk := .T.

Begin Sequence

	IF ( Vazio() )
		Break
	EndIF

	IF !( lRd2TreeOk := ExistCpo( "RD2" , ( GetMemVar( "RDM_CODIGO" ) + GetMemVar( "RD2_TREE" ) ) ) )
    	Break
    EndIF

End Sequence

Return( lRd2TreeOk )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2DesMemInit	 ЁAutorЁMarinaldo de JesusЁ Data Ё22/11/2002Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializadora Padrao do Campo RD2_CODMEM					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO do campo RD2_CODMEM								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2DesMemInit()

Local cRd2DesMemInit := ""
Local aArea 	     := GetArea()

IF !( Inclui := IF( ( Type( "Inclui" ) == "L" ) , Inclui , .F. ) )

	dbSelectArea("RD2")
	cRd2DesMemInit := ApdMsMm( RD2->RD2_CODMEM )

EndIF

RestArea( aArea )
Return( cRd2DesMemInit )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGdRd2Seek	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё08/01/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁEfetuar Pesquisa na GetDados                               	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA050()                                              		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function GdRd2Seek( oGdRd2 )

Local aSvKeys		:= GetKeys()
Local cProcName3	:= Upper( AllTrim( ProcName( 3 ) ) )
Local cProcName5	:= Upper( AllTrim( ProcName( 5 ) ) )

Begin Sequence

	IF !( "APDA050MNT" $ ( cProcName3 + cProcName5 ) )
		Break
	EndIF

	GdSeek( oGdRd2 , OemToAnsi( STR0001 ) )	//"Pesquisar"

End Sequence	

RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2StatusVld ЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RD2_STATUS        	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RD2_STATUS                        	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2StatusVld()

Local lRd2StatusOk := .T.

Begin Sequence

	IF !( lRd2StatusOk := Pertence( OpBxAtvInat( .T. ) ) )
		Break
	EndIF

End Sequence

Return( lRd2StatusOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2StatusBox ЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Retornar as Opcoes do Campo RD2_STATUS        	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_CBOX para o campo RD2_STATUS                        	    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2StatusBox()
Return( OpBxAtvInat( .F. ) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2StatusInitЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Retornar as Opcoes do Campo RD2_STATUS        	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RD2_STATUS                        	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2StatusInit()
Return( SubStr( OpBxAtvInat( .T. ) , 2 , 1 ) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2DataVld   ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/01/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do campo Rd2_DATA          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_CBOX para o campo Rd2_DATA                          		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2DataVld()

Local lRd2DataVld := .T.

Begin Sequence

	lRd2DataVld := NaoVazio()

End Sequence

Return( lRd2DataVld )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRd2DataInit  ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/01/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Inicializar o conteudo do campo Rd2_DATA        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo Rd2_DATA                          	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Rd2DataInit()
Return( Ctod("//") )

/*                                	
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Gustavo     Ё Data Ё20/12/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas bibliotecas Framework da Versao 9.12 .      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁAPDA050                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/   

Static Function MenuDef()

 Local aRotina := {;
								{ STR0001 , "AxPesqui"	 , 0 , 01,,.F. } ,; //"Pesquisar"
								{ STR0002 , "Apda050Mnt" , 0 , 02 } ,; //"Visualizar"
								{ STR0003 , "Apda050Mnt" , 0 , 03 } ,; //"Incluir"
								{ STR0004 , "Apda050Mnt" , 0 , 04 } ,; //"Alterar"
								{ STR0005 , "Apda050Mnt" , 0 , 05 } ,; //"Excluir"
								{ STR0006 , "Apda050Mnt" , 0 , 04 }  ; //"Montar Estrutura"
							}
Return aRotina