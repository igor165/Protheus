#INCLUDE "PONA050.CH"
#INCLUDE "PROTHEUS.CH"

Static lPort1510 	:= Port1510() 	//Verifica se Portaria 1510/2009 esta em vigor.

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё PONA050  Ё Autor Ё Wiliam R. Bertoncini  Ё Data Ё 14/02/96 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Cadastro de Motivos de Abono/Justificativa                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё PONA050()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.            Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁCecilia Car.Ё29/05/14ЁTPQAN3ЁIncluido o fonte da 11 para a 12 e efetua-Ё╠╠
╠╠Ё            Ё        Ё      Ёda a limpeza.                             Ё╠╠ 
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function PONA050

Local aArea			:= GetArea()
Local aIndexSP6		:= {}
Local aAdvSize		:= MsAdvSize()
Local cFiltraSP6	:= ""	

Begin Sequence

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSo Executa se o Modo de Acesso dos Arquivos do Ponto EstiveremЁ
	ЁOK															   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF !ValidArqPon()
		Break
	EndIF
    
	Private aRotina := MenuDef() //chamada da funcao para modificacao de Menu funcional versao 9 
	
	Private bFiltraBrw	:= { || NIL }
	Private cCadastro	    := OemToAnsi(STR0009 ) // "Motivos Abono/Justif."
	Private lMvAbosEve	:= ( Upper( AllTrim( SuperGetMv( "MV_ABOSEVE" , NIL , "N" , cFilAnt ) ) ) == "S" )
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Inicializa o filtro utilizando a funcao FilBrowse                      Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	cFiltraRh	:= CHKRH("PONA050","SP6","1")
	bFiltraBrw 	:= {|| FilBrowse( "SP6" , @aIndexSP6 , @cFiltraRH ) }
	Eval(bFiltraBrw)
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Endereca a funcao de BROWSE                                  Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	dbSelectArea ( "SP6" )
	dbGoTop()

	mBrowse( aAdvSize[7] , 0 , aAdvSize[6] , aAdvSize[5] , "SP6" )
	
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Deleta o filtro utilizando a funcao FilBrowse                     	 Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	EndFilBrw( "SP6" , aIndexSP6 )

End Sequence
             
RestArea( aArea )

Return( NIL )

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona050ChkDel	ЁAutorЁMarinaldo de Jesus Ё Data Ё25/07/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerificar se uma Sequencia pode Ser Deletada                Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA050                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁNIL															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona050ChkDel( cAlias , nReg , nOpcx )

Local aArea		:= GetArea()
Local aAreas	:= {}
Local cFilSP6	:= xFilial( "SP6" )
Local cMsgYesNo	:= ""
Local cAbono	:= SP6->P6_CODIGO
Local cTitLog	:= STR0018	//"Log de Ocorrencias na Exclusao de Motivos de Abono"
Local lDelOk	:= .T.
Local nAreas	:= 0

//SPK
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPK->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PK_FILIAL"
				aAreas[nAreas,02,02] := "PK_CODABO"
aAreas[nAreas,03] := RetOrdem( "SPK" , "PK_FILIAL+PK_CODABO" , .T. )

//SPC
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPC->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PC_FILIAL"
				aAreas[nAreas,02,02] := "PC_ABONO"
aAreas[nAreas,03] := RetOrdem( "SPC" , "PC_FILIAL+PC_ABONO" , .T. )

//SPH
aAdd( aAreas , Array( 03 ) )
nAreas := Len( aAreas )
aAreas[nAreas,01] := SPH->( GetArea() )
aAreas[nAreas,02] := Array( 2 )
				aAreas[nAreas,02,01] := "PH_FILIAL"
				aAreas[nAreas,02,02] := "PH_ABONO"
aAreas[nAreas,03] := RetOrdem( "SPH" , "PH_FILIAL+PH_ABONO" , .T. )

cMsgYesNo 	:= STR0010	//"O Sistema Agora Ira efetuar a Verificacao para ver se o Motivo de Abono" 
cMsgYesNo 	+= CRLF
cMsgYesNo 	+= STR0011	//"selecionado para Exclusao esta sendo utilizado. A verifica┤└o pode ser demorada !!"
cMsgYesNo 	+= CRLF
cMsgYesNo 	+= CRLF
cMsgYesNo 	+= STR0012	//"Confirma a exclus└o do Motivo de Abono?"

( cAlias )->( MsGoto( nReg ) )

lDelOk := ChkDelRegs(	cAlias			,;	//01 -> Alias do Arquivo Principal
						nReg			,;	//02 -> Registro do Arquivo Principal
						nOpcx			,;	//03 -> Opcao para a AxDeleta
						cFilSP6			,;	//04 -> Filial do Arquivo principal para Delecao
						cAbono			,;	//05 -> Chave do Arquivo Principal para Delecao
						aAreas			,;	//06 -> Array contendo informacoes dos arquivos a serem pesquisados
						cMsgYesNo  		,;	//07 -> Mensagem para MsgYesNo
						cTitLog			,;	//08 -> Titulo do Log de Delecao
						NIL				,;	//09 -> Mensagem para o corpo do Log
						.T.				,;	//10 -> Se executa AxDeleta
						.T.				,;	//11 -> Se deve Mostrar o Log
						NIL				,;	//12 -> Array com o Log de Exclusao
						NIL				,;	//13 -> Array com o Titulo do Log
						NIL				,;	//14 -> Bloco para Posicionamento no Arquivo
						NIL				,;	//15 -> Bloco para a Condicao While
						NIL				 ;	//16 -> Bloco para Skip/Loop no While
					)

RestArea( aArea )

Return( lDelOk )

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona050Inc		ЁAutorЁMarinaldo de Jesus Ё Data Ё25/07/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁChamada a AxInclui com Validacao no TudoOk                  Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA050                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁNIL															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona050Inc( cAlias , nReg , nOpcx )
Return( AxInclui( cAlias , nReg , nOpcx , NIL , NIL , NIL , "Pona050TudOk()" ) )

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona050Alt		ЁAutorЁMarinaldo de Jesus Ё Data Ё25/07/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁChamada a AxAltera com Validacao no TudoOk                  Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA050                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁNIL															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona050Alt( cAlias , nReg , nOpcx )
Return( AxAltera( cAlias , nReg , nOpcx , NIL , NIL , NIL , NIL , "Pona050TudOk()" ) )

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona050TudOk	ЁAutorЁMarinaldo de Jesus Ё Data Ё25/07/2002Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁValida o Conteudo da Enchoice                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA050                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁNIL															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona050TudOk()

Local lTudoOk	:= .T.

/*
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Valida o preenchimento do campo P6_EVENTO apenvas quando   oЁ
Ё Parametro MV_ABOSEVE estiver com o conteudo igual a "N"     Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF ( M->P6_ABHORAS == "S" ) .and. !( lMvAbosEve )
	IF !( lTudoOk := ( !Empty( M->P6_EVENTO ) ) )
		Help( "" , 1 , "P6_EVENTO" )
	EndIF
EndIF

Return( lTudoOk )        


/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Almeida     Ё Data Ё14/11/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas blibliotecas Framework da Versao 9.12 .     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA050                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/



Static Function MenuDef()

Local aRotina := {;              
	   					{ STR0004 ,"PesqBrw" 		, 0 , 1, ,.F.},; // "Pesquisar"
           				{ STR0005 ,"AxVisual"		, 0 , 2},; // "Visualizar"
           				{ STR0006 ,"Pona050Inc"		, 0 , 3},; // "Incluir"
           				{ STR0007 ,"Pona050Alt"		, 0 , 4},; // "Alterar"
           				{ STR0008 ,"Pona050ChkDel"	, 0 , 5} ; // "Excluir"
         		 }

Return aRotina