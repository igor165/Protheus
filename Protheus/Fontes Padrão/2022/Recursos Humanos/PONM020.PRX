#include 'PONM020.CH'
#Include 'PROTHEUS.ch'

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONM020  ³ Autor ³ J.Ricardo             ³ Data ³ 03/05/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gr ficos                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³   SIGAPON                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cecilia C.  ³21/05/14³TPQAN3³Incluido o fonte da 11 para a 12 e efetua-³±± 
±±³            ³        ³      ³da a limpeza.                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PONM020()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Defini‡„o de Variaveis                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nOpca := 0
Local aArea := GetArea()

Local oDlg
Local aSays	:= {}
Local aButtons  := {}

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ So Executa se os Modos de Acesso dos Arquivos Relacionados es³
³ tiverm OK 												   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ValidArqPon()
	
	Private lAbortPrint := .F.
	Private cCadastro   := OemToAnsi(STR0001 ) // 'Gera‡„o do Gr ficos'
	
	Pergunte('PNM020',.F.)
	
	AADD(aSays,OemToAnsi( STR0003 ) )// 'Este programa tem como objetivo gerar um grafico de acordo com as horas'
	AADD(aSays,OemToAnsi( STR0004 ) )// 'apuradas no apontamento de marca‡”es e pelos parametros solicitados.   '
	
	AADD(aButtons, { 5,.T.,{|| Pergunte("PNM020",.T. ) } } )
	AADD(aButtons, { 1,.T.,{|o| nOpca := 1,IF(gpconfOK(),FechaBatch(),nOpca:=0 ) }} )
	AADD(aButtons, { 2,.T.,{|o| FechaBatch() }} )
		
	FormBatch( cCadastro, aSays, aButtons )
		
	IF nOpca == 1
		Processa({|lEnd| f020Processa()},,,.T.)
	EndIF

EndIF
	
RestArea( aArea )

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³f020Proces³ Autor ³ Equipe de RH MicroSiga³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta o Gr fico.                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONM020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function f020Processa()
Local nIndice   := 0
Local nMaior    := 0
Local nQuant    := 0
Local cCodExtra := ''
Local oQual
Local cAcessaSRA:= &("{ || " + ChkRH("PONR020","SRA","2") + "}")
Local nFor		:= 0
Local oSize		:= Nil

Private nSoma  := 0
Private aCores := {}
Private oCor1  := LoadBitmap( GetResources(), 'VERDE' )
Private oCor2  := LoadBitmap( GetResources(), 'AZUL' )
Private oCor3  := LoadBitmap( GetResources(), 'VERMELHO' )
Private oCor4  := LoadBitmap( GetResources(), 'ROSA' )
Private oCor5  := LoadBitmap( GetResources(), 'AMARELO'  )
Private oCor6  := LoadBitmap( GetResources(), 'CIANO' )
Private oCor7  := LoadBitmap( GetResources(), 'VERMESCURO' )

Aadd(aCores,oCor1)
Aadd(aCores,oCor2)
Aadd(aCores,oCor3)
Aadd(aCores,oCor4)
Aadd(aCores,oCor5)
Aadd(aCores,oCor6)
Aadd(aCores,oCor7)

Private aArray   := {}
Private aValores := {	{  0.00, 'B/BG'   , '', STR0020	}	,;	// 'HORAS NORMAIS'
						{  0.00, 'W+/BG'  , '', STR0022	}	,;	// 'DSR'
						{  0.00, 'GR+/BG' , '', STR0024	}	,;	// 'HORAS NOTURNAS'
						{  0.00, 'G+/BG'  , '', STR0026	}	,;	// 'HORAS EXTRAS'
						{  0.00, 'R/BG'   , '', STR0028	}	,;	// 'FALTAS'
						{  0.00, 'BR/BG'  , '', STR0030	}	,;	// 'ATRASOS'
						{  0.00, 'N+/BG'  , '', STR0032	} }		// 'SAIDAS ANTEC.'
Private nVezPizza	:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as Perguntas selecionadas                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte('PNM020',.F.)
nOrdem   := mv_par01
nCodigos := mv_par02
cFilDe   := mv_par03
cFilAte  := mv_par04
cCCDe    := mv_par05
cCCAte   := mv_par06
cMatDe   := mv_par07
cMatAte  := mv_par08
dDataIni := mv_par09
dDataFim := mv_par10

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Procura primeiro funcionario                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea('SRA')
If nOrdem == 1 //-- Filial.
	dbSetOrder( 1 )
	dbSeek( cFilDe , .T. )
Elseif nOrdem == 2 //-- Centro Custo.
	dbSetOrder( 2 )
	dbSeek( cFilDe + cCcDe + cMatDe , .T. )
ElseIf nOrdem = 3 //-- Matricula.
	dbSetOrder( 1 )
	dbSeek( cFilDe + cMatDe , .T. )
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicia regua de processamento.                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ProcRegua(SRA->(RecCount()))

//-- Carrega os identificadores dos codigos.
CarCodFol(aValores,@cCodExtra)

Begin Sequence
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Looping no Arquivo de Funcion rios (SRA)                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While SRA->( !Eof() )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aborta o Calculo, caso seja pressionado Alt + B   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lAbortPrint
			Break
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Incrementa Regua de Processamento.                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		IncProc(OemToAnsi(STR0033 ))		 // 'Gera‡„o do Gr fico...'

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste o De / At‚ (SRA)                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (Sra->Ra_Filial < cFilDe) .Or. (Sra->Ra_Filial > cFilAte) .Or. ;
			(Sra->Ra_Mat < cMatDe) .Or. (Sra->Ra_Mat > cMatAte)  .Or. ;
			(Sra->Ra_CC < cCcDe) .Or. (Sra->Ra_CC > cCCAte)
			SRA->(dbSkip())
			Loop
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste controle de acessos e filiais validas               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SRA->( !(RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) )
			SRA->(dbSkip())
			Loop
		EndIf

		If SPC->(dbSeek (SRA->RA_Filial + SRA->RA_Mat, .F.))
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Looping no Arquivo de Apontamentos (SPC)                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			While SPC->( !Eof() .and. PC_FILIAL+PC_MAT == SRA->( RA_FILIAL+RA_MAT ) )

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Consiste Datas De / At‚ (SPC)                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SPC->PC_DATA < dDataIni .Or. ;
					SPC->PC_DATA > dDataFim .Or. ;
					Empty(SPC->PC_DATA)
					SPC->(dbSkip())
					Loop
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Armazena Horas nos Arrays                                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( nPos := aScan(aValores,{ |x| (If(!Empty(SPC->PC_PDI),SPC->PC_PDI,SPC->PC_PD)) $ x[3]}) ) > 0
					aValores[nPos,1] := __TimeSum(aValores[nPos,1],(If(!Empty(SPC->PC_QUANTI),SPC->PC_QUANTI,SPC->PC_QUANTC)) )
				ElseIf (If(!Empty(SPC->PC_PDI),SPC->PC_PDI,SPC->PC_PD)) $ cCodExtra
					aValores[4,1] := __TimeSum(aValores[4,1],(If(!Empty(SPC->PC_QUANTI),SPC->PC_QUANTI,SPC->PC_QUANTC)))
				Endif

				SPC->(dbSkip())
			EndDo
		EndIf

		SRA->(dbSkip())
	Enddo
End Sequence

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula Percentuais e Totais                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nIndice := 0
nSoma   := 0
Aeval( aValores , {|X| nIndice := MAX(nIndice,X[1]) } )
Aeval( aValores , {|X| nSoma += X[1] } )

For nFor := 1 To Len(aValores)
	Aadd(aArray,{nFor,aValores[nFor,3],aValores[nFor,4],aValores[nFor,1],Round(aValores[nFor,1]*100/nSoma,2)})
Next nFor

oSize := FwDefSize():New(.F.)             

oSize:AddObject( "ENCHOICE",oSize:aWindSize[4],oSize:aWindSize[3]*0.4 , .F., .F. )// Não dimensionavel
oSize:AddObject( "GRAFICO",oSize:aWindSize[4],oSize:aWindSize[3]*0.6 , .F., .F. )// Não dimensionavel
oSize:aMargins 	:= { 0, 0, 0, 0 } // Espaco ao lado dos objetos 0, entre eles 3		
oSize:lProp 	:= .F. // Proporcional             
oSize:Process() 	   // Dispara os calculos

//MONTA AS DIMENSOES DOS OBJETOS
DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0008) FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4] PIXEL OF oMainWnd // 'Gr ficos

	oVbx:= GRF_CreateNew( oDlg, 7 )
	GRF_ChartType(oVbx,1)

	//-- ListBox da Analise.  
	@ oSize:GetDimension("ENCHOICE","LININI")+1,oSize:GetDimension("ENCHOICE","COLINI") LISTBOX oQual FIELDS HEADER OemToAnsi(STR0035),; // 'Leg.'
	OemToAnsi(STR0036),; // 'Cod.'
	OemToAnsi(STR0037),; // 'Descricao'
	OemToAnsi(STR0038),; // 'Total'
	OemToAnsi(STR0039); // 'Percentagem (%)'
	SIZE (oSize:GetDimension("ENCHOICE","COLEND")*0.5)-1,(oSize:GetDimension("ENCHOICE","LINEND")*0.4) OF oDlg//Altura, 

	oQual:SetArray(aArray)
	
	fMontLeg(1,oQual) 
	
	
ACTIVATE MSDIALOG oDlg  CENTERED  ON INIT (InitGraph(oVbx,oDlg, oSize),oQual:refresh(),SetGraph(oVbx,1,oQual),VC020TOOLS(oDlg,oVbx,oQual))

deleteobjects(oCor1)
deleteobjects(oCor2)
deleteobjects(oCor3)
deleteobjects(oCor4)
deleteobjects(oCor5)
deleteobjects(oCor6)
deleteobjects(oCor7)

GRF_Destroy(oVbx)

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ CarCodFol³ Autor ³ Fernando              ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Codigos para Array                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONM020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function CarCodFol(aValores,cCodExtra)

Local cFil        := xFilial("SP9",SRA->RA_FILIAL)
Local cFilCompara := ''

cCodExtra  := ''

DbSelectArea('SP9')
If !SP9->(DbSeek(cFil))
	Return( NIL )
Endif

cFilCompara := SP9->P9_FILIAL

While SP9->( !Eof() .and. P9_Filial == cFilCompara )
	If Left(SP9->P9_IDPON,3) == '001' //-- H. Normais.
		aValores[1,3] += SP9->P9_CODIGO
	ElseIf Left(SP9->P9_IDPON,3) == '002' //-- DSR.
		aValores[2,3] += SP9->P9_CODIGO
	Endif
	
	//-- Codigos NAO Autorizados.
	If nCodigos == 2 .OR. nCodigos == 3
		If Left(SP9->P9_IDPON,3) == '003' //-- Noturma nao Autorizada.
			aValores[3,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) $ '007ú009' //-- Falta nao Autorizada.
			aValores[5,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) == '011' //-- Atraso nao Autorizado.
			aValores[6,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) == '013' //-- Saida Antecipada nao Autorizado.
			aValores[7,3] += SP9->P9_CODIGO + 'ú'
		Endif
	Endif
	
	//-- Codigos Autorizados.
	If nCodigos == 1 .OR. nCodigos == 3
		If Left(SP9->P9_IDPON,3) == '004' //-- Noturna Autorizado.
			aValores[3,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) $ '008ú010' //-- Falta Autorizada.
			aValores[5,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) == '012' //-- Atraso Autorizada.
			aValores[6,3] += SP9->P9_CODIGO + 'ú'
		ElseIf Left(SP9->P9_IDPON,3) == '014' //-- Saida antecipada Autorizado.
			aValores[7,3] += SP9->P9_CODIGO + 'ú'
		Endif
	Endif
	
	//-- Horas Extras
	fCodHExt(@cCodExtra,cFilCompara)
	aValores[4,3] := cCodExtra
	
	SP9->(DbSkip())
EndDo

Return( NIL )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ FCodHExt ³ Autor ³ Fernando              ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Codigos de Horas Extras para Array                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONM020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fCodHExt(cCodExtra,cFil)

cFil := If(Empty(xFilial('SP4')), xFilial('SP4'), cFil)

If SP4->(dbSeek(cFil,.F.))
	While SP4->( !Eof() .And. cFil == P4_FILIAL )
		//-- Nao Autorizados
		If nCodigos == 2 .OR. nCodigos == 3
			cCodExtra += SP4->P4_CodNAut + 'ú'
		EndIf
		//-- Autorizados
		If nCodigos == 1 .OR. nCodigos == 3
			cCodExtra += SP4->P4_CodAut + 'ú'
		EndIf
		SP4->(dbSkip(1))
	EndDo
EndIf

Return( NIL )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³VC020Tools³ Autor ³ Ary Medeiros          ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta o ToolBar na Tela                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINC020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function VC020TOOLS(oDlg,oVbx,oQual)
Local oBar, lPizza:=.F.
Local olinha	
Local oBarra 
Local oArea 
Local oPizza
Local oBid
Local oTrid

DEFINE BUTTONBAR oBar SIZE 25,25 3D OF oDlg

DEFINE BUTTON RESOURCE 'FINAL'       OF oBar GROUP ACTION oDlg:End() TOOLTIP OemToAnsi(STR0041)  // 'Sair'

DEFINE BUTTON oLinha RESOURCE 'LINE'        OF oBar GROUP ACTION (SETGRAPH(oVbx,1,oQual), GRF_ChartType(oVbx,1), GRF_FixedGap(oVbx,36), lPizza:=.F.)  TOOLTIP OemToAnsi(STR0043) // 'Gr fico de Linha'
oLinha:cTitle:= ""
DEFINE BUTTON oBarra RESOURCE 'BAR'         OF oBar       ACTION (GRF_ChartType(oVbx,7), GRF_FixedGap(oVbx,26), SETGRAPH(oVbx,7,oQual), lPizza:=.F.)  TOOLTIP OemToAnsi(STR0045) // 'Gr fico de Barras'
oBarra:cTitle:= ""
DEFINE BUTTON oArea RESOURCE 'AREA'         OF oBar       ACTION (SETGRAPH(oVbx,1,oQual), GRF_ChartType(oVbx,6), GRF_FixedGap(oVbx,36), lPizza:=.F.)  TOOLTIP OemToAnsi(STR0055) // 'Gr fico de µrea'
oArea:cTitle:= ""
DEFINE BUTTON oPizza RESOURCE 'S4WB013N'    OF oBar       ACTION (SETGRAPH(oVbx,7,oQual,.T.), SETGRAPH(oVbx,5,oQual), GRF_ChartType(oVbx,5), lPizza:=.T.) TOOLTIP OemToAnsi(STR0048) // 'Gr fico de Pizza'
oPizza:cTitle:= ""
DEFINE BUTTON oBid RESOURCE 'GRAF2D'        OF oBar GROUP ACTION GRF_Chart3D(oVbx,If(lPizza,.T.,.F.))         TOOLTIP OemToAnsi(STR0050) // 'Gr ficos Bidimensionais'
oBid:cTitle:= ""
DEFINE BUTTON oTrid RESOURCE 'GRAF3D'       OF oBar       ACTION GRF_Chart3D(oVbx,.T.)         TOOLTIP OemToAnsi(STR0052) // 'Gr ficos Tridimensionais'
oTrid:cTitle:= "" 

Return(NIL)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ SetGraph ³ Autor ³ Ary Medeiros          ³ Data ³            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta o Grafico da participacao                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ VEIC020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SETGRAPH(oVbx,nTipoGraf,oQual, lPizza)
Local i 
Local aCorProt:={10,12,9,13,11,14,1}
Default lPizza := .F.

If lPizza
	nVezPizza++
	If nVezPizza >= 2   // SE FOR PIZZA PERMITE APENAS DUAS EXECUCOES. ISSO IMPEDE A REEXECUCAO QUANDO O GRAFICO JA FOR PIZZA.
		If nTipoGraf == 5
			nVezPizza := 0
		EndIf
		Return
	EndIf
EndIf

If nSoma > 0
		GRF_InitData(oVbx,1,7,1,nDbl2Flt(2013262335),2,0)

		fMontLeg(nTipoGraf,oQual)            // Monta legenda do grafico

		For i:= 1 to 7

			GRF_SetData(oVbx						,;	// Objeto
						If (nTipoGraf=7,i,1)    	,;	// Serie
						i							,;	// Ponto (Coluna)
						nDbl2Flt(aArray[i,5])		,;	// Altura da Coluna
						Substr(aArray[i,2],1,3)   	,;	// Caption da Coluna
						aCorProt[i]					,;  // Cores
						If (nTipoGraf=7,i,1))			// nSerieToAdd		
		Next i

		GRF_EndData(oVbx,nDbl2Flt(1),0,0,0)
Else
	If !nTipoGraf == 5 // SE FOR PIZZA EXIBE MENSAGEM SOMENTE UMA VEZ
		Alert(OemToAnsi( STR0056 )) //"Todos os eventos estão zerados! Não é possível exibir este tipo de gráfico""
	EndIf
EndIf

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AjustRIGHT³ Autor ³ Vinicius Barreira     ³ Data ³ 23/01/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Alinha valores numericos em um list-box.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³BancDisplay                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß	*/
Static Function AjustRIGHT(nValor)                                
Local nAjuste	:= 0
Local cAjuste	:= ''
Local cReturn	:= ''
Local nTamanho	:= 0.00	

If Int(nValor)> 0
   nAjuste	:= Len( Alltrim( Str( Int(nValor),8,0) ) ) 
   nAjuste  := nAjuste-1 
   cAjuste	:= Space(nAjuste)
Endif	

cReturn  := Transform(nValor,'@E 99,999,999.99')
nTamanho := GETTextWidth(0  ,'99,999,999.99')

While GETTextWidth(0,cReturn) < nTamanho
	cReturn := ' ' + cReturn
EndDo
Return cAjuste+cReturn
	

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ InitGraph ³ Autor ³ Ary Medeiros		    ³ Data ³ 		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta o ToolBar na Tela 									  ³±±
±±³          ³                                                            ³±±
±±³Revis„o   ³ Revis„o em 27.05.99 por J£lio Wittwer para chamar fun‡”es  ³±±
±±³          ³ gr ficas gerericas criadas para compatibilizar Advanced    ³±±
±±³          ³ com Protheus.                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	     ³ FINC020													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function INITGRAPH(oVbx,oDlg, oSize)

GRF_ChartType(oVbx,1)
GRF_Chart3D(oVbx,.t.)
GRF_FixedGap(oVbx,36)


**********************************************************************
* AQUI! Funcao Winset no Protheus seta coordenadas da Janela do gr fico,
* mas no advanced seta propriedades internas do gr fico.
*

GRF_WinSet(	oVbx	,;
			Int(oSize:GetDimension("GRAFICO","LININI"))	,;	// nTop
			Int(oSize:GetDimension("GRAFICO","COLINI"))		,;  // nLeft
			Int(oSize:GetDimension("GRAFICO","LINEND"))	,; 	// nBottom
			Int(oSize:GetDimension("GRAFICO","COLEND"))	,;  // nRight
			0		,;  
			0		,;  
			0		,;	// nRight
			7)			// Tamanho

Return( NIL )


Static Function nDbl2Flt(nNumero)
	* ----------------------------------------------------------------------
	*	N„o implementada no protheus, e desnecess ria para parametriza‡„o 
	*	Gr fica no PRotheus.
	* ----------------------------------------------------------------------
Return nNumero

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fMontLeg  ³ Autor ³ Gisele V.		    ³ Data ³ 		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta Legenda do Gráfico									  ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	     ³          												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fMontLeg(nTipoGraf,oQual)

//-- Legenda do Gr fico
oQual:bLine := { || { If(nTipoGraf = 7 .OR. nTipoGraf = 5, aCores[aArray[oQual:nAT,1]], aCores[1]),;    //-- Cor (Se for gráfico do tipo barra ou pizza cada Evento da legenda terá o seu valor conforme gráfico apresentado)
Subs(aArray[oQual:nAT,2],1,3)  , ;                														//-- C¢digo
aArray[oQual:nAT,3]      	   , ;                														//-- Descri‡„o 
AjustRIGHT(aArray[oQual:nAT,4]) , ;               														//-- Total
AjustRIGHT(aArray[oQual:nAT,5])}}                														//-- Percentual
			
Return( NIL )