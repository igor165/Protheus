#INCLUDE "PROTHEUS.CH"

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInRhLibRpcExecЁAutor ЁMarinaldo de Jesus   Ё Data Ё14/05/2003Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de RHLIBRPC                          Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInRhLibRpcExec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InRhLibRpcExec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRpcChkSxs	  ЁAutor ЁMarinaldo de Jesus   Ё Data Ё20/09/2004Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica se existe os SXs para a PREPARE ENVIRONMENT		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlChkSxsOk                                              	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RpcChkSxs( cEmp , aMsgErr , lSXM )

Local aFiles

Local cMsgErr
Local cFileChk
Local cDbExtension

Local lChkSxsOk

Local nFile
Local nFiles

Begin Sequence

	DEFAULT aMsgErr := {}

	IF ( aScan( aMsgErr , { |x| ( x[1] == cEmp ) } ) > 0 )
		lChkSxsOk := .F.
		Break
	EndIF
    
	aFiles	:= {}
	aAdd( aFiles , "SIX" )
	aAdd( aFiles , "SX1" )
	aAdd( aFiles , "SX2" )
	aAdd( aFiles , "SX3" )
	aAdd( aFiles , "SX4" )
	aAdd( aFiles , "SX6" )
	aAdd( aFiles , "SX7" )
	aAdd( aFiles , "SX9" )
	aAdd( aFiles , "SXA" )
	aAdd( aFiles , "SXB" )
	aAdd( aFiles , "SXO" )
	DEFAULT lSXM := .T.
	IF ( lSXM )
		aAdd( aFiles , "SXM" )
	EndIF

	cMsgErr			:= ""
	cDbExtension	:= GetDbExtension()

	nFiles := Len( aFiles )
	For nFile := 1 To nFiles
		cFileChk := ( aFiles[ nFile ] + cEmp + "0" + cDbExtension )
		IF !( File( cFileChk ) )
		    If aFiles[ nFile ] = "SIX"
		    	// Se for o SIX .. e nЦo achou ... procura pelo SINDEX
				cFileChk := ( "SINDEX" + cDbExtension )
				If !( File( cFileChk ) )
					cMsgErr += "No File: " + cFileChk + CRLF
				Endif		    
			Else
				cMsgErr += "No File: " + cFileChk + CRLF
		    Endif
		EndIF
	Next nFile

	IF !( lChkSxsOk := Empty( cMsgErr ) )
		aAdd( aMsgErr , { cEmp , cMsgErr } )
	EndIF

End Sequence

Return( lChkSxsOk )
