#define Confirma 1
#define Redigita 2
#define Abandona 3
#Include "PROTHEUS.CH"
#Include "GPEA131.CH"
#INCLUDE "SHELL.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Gpea131  ³ Autor ³ Equipe RH Inovacao           ³ Data        ³ 15/01/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cadastramento de Beneficios:                                              ³±±
±±³          ³ -Vale Transporte, Vale Refeicao, Vale Alimentacao                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programador ³   Data   ³ FNC/Chamado    ³  Motivo da Alteracao                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Raquel Hager³17/01/2014³M12RH01  RQ2008 ³ Unificacao Folha de Pagamento.              ³±±
±±³M. Silveira ³29/08/2014³TQISWE          ³Incluida a funcao Gp131AltVT() p/ atualizar o³±±
±±³            ³          ³                ³valor na inclusao ou alteracao do tipo de VT.³±±
±±³M. Silveira ³24/10/2014³TQU440          ³Ajuste na Gp131AltVT() para validar correta- ³±±
±±³            ³          ³                ³mente os registros e nao gerar erro.         ³±±
±±³Wag. Mobile ³11/11/2014³TQXHSI          ³Ajuste na Gp131AltVT() p/ tratar tambem a tab³±±
±±³            ³          ³                ³RFO do Vale Alimentacao/Refeicao.            ³±±
±±³Wag. Mobile ³23/12/2014³TRHFV0          ³Ajuste  na  BenLinOk2  p/  validar  o  campo ³±±
±±³            ³          ³                ³RG2_VTDUTE somente na atualizacao do Vale    ³±±
±±³            ³          ³                ³Transporte.                                  ³±±
±±³Henrique V. ³07/05/2015³TSBYWG          ³Ajuste  na  rotina para tratar corretamente  ³±±
±±³            ³          ³                ³a filial do registro a ser processado.       ³±±
±±³Allyson M.  ³04/12/2015³TTRRN6      	   ³Ajuste para que apareça o valor unitario na  ³±±
±±³            ³          ³            	   ³inclusao de registro de VT quando for por    ³±±
±±³            ³          ³            	   ³ediçao em linha								 ³±±
±±³Esther V.   ³14/12/2015³TUANJT          ³ Permitido a visualização do histórico de VT ³±±
±±³            ³          ³                ³mesmo se func. não possuir registro na SR0.  ³±±
±±³Gustavo M.  ³24/05/2015³TVDPKT          ³Ajuste na navegação entre as setas.			 ³±±
±±³Allyson M.  ³03/11/2016³TVFJZ8      	   ³Ajuste p/ não permitir edição na getDados da ³±±
±±³            ³          ³            	   ³tabela RG2    								 ³±±
±±³Gabriel A.  ³21/12/2016³MRH-3119        ³Ajuste para que caso a string retornada      ³±±
±±³            ³          ³                ³pela fValidFil() for maior que 2000 bytes    ³±±
±±³            ³          ³                ³seja filtrada as filiais que o usuário       ³±±
±±³            ³          ³                ³não tem acesso.                              ³±±
±±³Raquel Hager³05/01/2017³MRH-4205        ³Ajuste na função Gp131AltVT para atualização ³±±
±±³            ³          ³                ³do Vlr Unitário no momento de alteração.     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gpea131(nOpcAuto, lRobo, nVale, aSR0Auto,aRG2Auto)

Local aIndexSRA		:= {}		// Variavel Para Filtro
Local oDlg
Local oRadio
Local nRadio
Local nOpca 		:= 1
local nAviso

Private lNovoCalc 	:= NovoCalcBEN()

DEFAULT lRobo		:= .F.
DEFAULT nVale		:= 0
DEFAULT aSR0Auto	:= {}
DEFAULT aRG2Auto	:= {}

Private lAutomato	:= lRobo
Private aAutSR0	:= aSR0Auto
Private aAutRG2	:= aRG2Auto

If !lNovoCalc
	
	Private bFiltraBrw 	:= {|| Nil}		// Variavel para Filtro
	Private nTpVale
	Private aRotina 	:= MenuDef()
	Private cSubCabec 	:= ""
	
	If !lAutomato
		
		nAviso := Aviso( STR0049, ; // "Temos uma nova solução para cálculos de benefícios! "
		CRLF + STR0050 + CRLF + CRLF + CRLF + CRLF + ; // "Uma nova solução, com uma nova tela para atualização dos benefícios e inúmeros novos recursos."
		STR0051 + CRLF + CRLF + CRLF + CRLF + CRLF + ; // "Saiba mais acessando a documentação no TDN, clicando no botão abaixo."
		STR0052,; // "*A partir da release 12.1.27 esta versão do cálculo de benefícios será descontinuada."
		{STR0054, STR0053}, 3 ) //"Saiba Mais" # "OK"
		If nAviso == 1
			OpenTDN()
		EndIf
		
	EndIf
		
		While nOpca == 1
			If !lAutomato
				DEFINE MSDIALOG oDlg FROM  94,1 TO 273,293 TITLE OemToAnsi(STR0026) PIXEL // "Tipo do Beneficio"

				@ 10,17 Say OemToAnsi(STR0027) SIZE 150,7 OF oDlg PIXEL // "Escolha o Tipo do Beneficio:"

				@ 27,07 TO 72, 140 OF oDlg  PIXEL


					@ 35,10 Radio 	oRadio VAR nRadio;
							ITEMS 	OemToAnsi(STR0017),;	// " Vale Transporte "
									OemToAnsi(STR0018),;	// " Vale Refeicao "
									OemToAnsi(STR0019);		// " Vale Alimentacao"
							3D SIZE 100,10 OF oDlg PIXEL


				DEFINE SBUTTON FROM 75,085 TYPE 1 ENABLE OF oDlg ACTION (nOpca := 1, oDlg:End())
				DEFINE SBUTTON FROM 75,115 TYPE 2 ENABLE OF oDlg ACTION (nOpca := 0, oDlg:End())

				ACTIVATE MSDIALOG oDlg CENTERED ON INIT (nOpca := 0, .T.)	// Zero nOpca caso
																			// para saida com ESC
			Else
				nRadio := nVale
			EndIf
			If nOpca == 1

				If nRadio == 1
					nTpVale := 0 // Vale Transporte
					cSubCabec := OemToAnsi(STR0010)  	// "V A L E   T R A N S P O R T E"
					cCadastro := OemToAnsi(STR0017)  			// "Vale Transporte"
				ElseIf nRadio == 2
		            nTpVale := 1 // Vale Refeicao
		            cSubCabec := OemToAnsi(STR0011)  	// "V A L E   R E F E I C A O"
					cCadastro := OemToAnsi(STR0018)  			// "Vale Refeicao"
				ElseIf nRadio == 3
					nTpVale := 2 // Vale Alimentacao
					cSubCabec := OemToAnsi(STR0012)  	// "V A L E  A L I M E N T A C A O"
					cCadastro := OemToAnsi(STR0019)  			// "Vale Alimentacao"
				EndIf

				If nOpcAuto <> Nil

					Do Case
						Case nOpcAuto == 3
							INCLUI := .T.
							ALTERA := .F.
						Case nOpcAuto == 4
							INCLUI := .F.
							ALTERA := .T.
						OtherWise
							INCLUI := .F.
							ALTERA := .F.
					EndCase

					dbSelectArea('SRA')
					nPos := Ascan(aRotina,{|x| x[4]== nOpcAuto})
					If ( nPos <> 0 )
						bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nPos,2 ] + "(a,b,c,d,e) }" )
						Eval( bBlock, Alias(), (Alias())->(Recno()),nPos)
					EndIf
				Else

					// Verifica se o Arquivo Esta Vazio
					If !ChkVazio("SRA")
						Return
					EndIf

					// Inicializa o filtro utilizando a funcao FilBrowse
					cFiltraRh := CHKRH("GPEA131","SRA","1")
					bFiltraBrw 	:= {|| FilBrowse("SRA",@aIndexSRA,@cFiltraRH) }
					Eval(bFiltraBrw)

					// Endereca a funcao de BROWSE
					dbSelectArea("SRA")
					mBrowse( 6, 1,22,75,"SRA",,,,,,fCriaCor())

					// Deleta o filtro utilizando a funcao FilBrowse
					EndFilBrw("SRA",aIndexSra)
				EndIf

				nOpcA := 0

			EndIf

		EndDo
Else
	GPEA133()
EndIf

Return
/*/{Protheus.doc} OpenTDN
Abre o navegador padrão com a página sobre o Meu RH
@author jose.silveira
@since 09/02/2018
@version 12.1.17
/*/
Static Function OpenTDN()

	Local cURL := "http://tdn.totvs.com/x/ikyZDg"

	shellExecute("Open", cURL, "", "", SW_NORMAL)

Return Nil
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ gp131Atu ³ Autor ³ Mauro Sergio          ³ Data ³ 23/04/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Programa de (Vis.,Inc.,Alt. e Del.)                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ gp131Atu(ExpC1,ExpN1,ExpN2)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp131Atu(cAlias,nReg,nOpcx,nTp)
Local aArea			:= GetArea()
Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), {.T. , .F.}) //[1] Acesso; [2]Ofusca
Local aFldRot 		:= {'RA_NOME', 'RA_ADMISSA'}
Local nX			:= 0.00
Local nOpcNewGd		:= IF(nOpcx==2.Or.nOpcx==5,0, GD_INSERT+GD_UPDATE+GD_DELETE	)
Local aFldOfusca 	:= {}
Local lOfuscaNom	:= .F.
Local lOfuscaAdm	:= .F.

// Objetos visuais
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local bSet15
Local bSet24
Local oDlg
Local oFont
Local oGroup

// Objetos da pesquisa
Local aPesqIdx 		:= {}
Local aPesqOrd		:= {}
Local lSeeAll		:= .T.
Local cPesqOrd
Local cPesqCampo	:= space(40)
Local cPesqFiltro	:= space(7)
Local lAxPesqui 	:=.F.

// Blocos de Codigo usados para os objetos primeiro, ultimo, próximo e anterior
Local bRetrocOne := {|| SRA->( dbskip(-1)),gp131MntGetDados(cAlias, nOpcX, SRA->RA_FILIAL, @cMat, @aVirtualSR0, @aColsRecSR0, @aVirtualRG2, @aColsRecRG2, .T., .F.), oGetSR0:oBrowse:Refresh(),oDLG:Refresh()}
Local bRetrocAll := {|| SRA->( dbGoTop()),gp131MntGetDados(cAlias, nOpcX, SRA->RA_FILIAL, @cMat, @aVirtualSR0, @aColsRecSR0, @aVirtualRG2, @aColsRecRG2, .T., .F.), oGetSR0:oBrowse:Refresh(),oDLG:Refresh()}
Local bAvancaOne := {|| SRA->( dbskip()), Ver_SRA_EOF(),gp131MntGetDados(cAlias, nOpcX, SRA->RA_FILIAL, @cMat, @aVirtualSR0, @aColsRecSR0, @aVirtualRG2, @aColsRecRG2, .T., .F.), oGetSR0:oBrowse:Refresh(), oDLG:Refresh()}
Local bAvancaAll := {|| SRA->( dbGoBottom()),gp131MntGetDados(cAlias, nOpcX, SRA->RA_FILIAL, @cMat, @aVirtualSR0, @aColsRecSR0, @aVirtualRG2, @aColsRecRG2, .T., .F.),oGetSR0:oBrowse:Refresh(),oDLG:Refresh()}
Local bFica      := {|| gp131MntGetDados(cAlias, nOpcX, SRA->RA_FILIAL, @cMat, @aVirtualSR0, @aColsRecSR0, @aVirtualRG2, @aColsRecRG2, .T., .F.),If(lDaRefresh,(oGetSR0:oBrowse:Refresh(),oDLG:Refresh()),.T.)}

// Objetos genericos para os alias SR0, RG2 e opções do menu
Local nOpca   := 0.00
Local cFilterAux := ""
Local oBrwAux
Local oFilAux

// ALIAS UTILIZADO
Private cAliasSR0 := "SR0"
Private cAliasRG2 := "RG2"

// Arrays usados no GDMONTACOLS para o alias SR0 e RG2

// CAMPOS NÃO MOSTRADOS
Private aGpa131FSR0 := { "R0_FILIAL", "R0_MAT", "R0_MEIO", "R0_DESC","R0_ANOMES","R0_PERIOD","R0_PEDIDO" }
Private aGpa131FRG2 := { "RG2_FILIAL", "RG2_MAT", "RG2_ROTEIR", "RG2_SALFOL", "RG2_ORIGEM", "RG2_MEIO", "RG2_DESC", "RG2_PERIOD", "RG2_NROPGT", "RG2_DIAS" }

// CAMPOS QUE PODERÃO SER EDITAVEIS
Private aGdAlteraRG2   := {}
Private aGdAlteraSR0   := {}

// CAMPOS VIRTUAIS
Private aVirtualRG2    := { "GHOSTCOL" }
Private aVirtuaLSR0    := { "GHOSTCOL" }

// CAMPOS VISUAIS
Private aVisualRG2     := {}
Private aVisualSR0     := { "R0_TPVALE" }

// GUARDA O RECNO() DOS REGISTROS
Private aColsRecRG2    := {}
Private aColsRecSR0    := {}

//Campos que Nao Serao Alterados
Private aGdNAltSR0		:= { "R0_DIASPRO", "R0_SALBASE", "R0_TPVALE","R0_TPCALC" }
Private aGdNAltRG2 		:= { }

// CABEÇALHO DOS CAMPOS
Private aHeaderSR0     := {}
Private aHeaderRG2     := {}

// COLUNAS DO OBJETO
Private aColsSR0       := {}
Private aColsRG2       := {}

// GUARDA OS DADOS ORIGINAIS PARA COMPARAÇÃO NA GRAVAÇAO
Private aColsAntSR0    := {}
Private aColsAntRG2    := {}

// GUARDA A QUANTIDADE DE CAMPOS EDITAVEIS
Private nUsadoSR0  	   := nUsadoRG2      := 0.00

// VARIAVEIS PARA MONTAGEM DAS QUERYS
Private aQuerySR0      := {}
Private aQueryRG2      := {}

// GUARDA A POSICAO E CONTEUDO DOS CAMPOS LOOKUP
Private nPosMeio
Private nPosDesc

// Variaveis usadas em todo o programa
Private cFil		:= SRA->RA_FILIAL
Private cMat    	:= SRA->RA_MAT
//Private cNome   	:= SRA->RA_NOME

// Objetos SR0 e RG2
Private oGetSR0
Private oGetRG2

// MONTA BLOCO PARA SKIP EM GDMONTACOLS
Private bSkip   := { || .F. }
Private cKeySeek:= cKeySeek2 := ""
Private lDaRefresh := .F.

Private lR0CodAtu	:= fVerAtuR0Cod()
If Type("lAutomato") == "U"
	Private lAutomato	:= .F.
EndIf

If !lAutomato
	oBrwAux    := GetMBrowse()
	If Type("oBrwAux") <> "U"
		oFilAux    := oBrwAux:FwFilter()
		cFilterAux := oFilAux:GetExprADVPL()
	EndIf
EndIf

If Len(fValidFil()) <= 2000
	cFilterAux += If( !( Empty( cFilterAux ) ), ' .And. SRA->RA_FILIAL $ fValidFil() ', ' SRA->RA_FILIAL $ fValidFil() ' )
Else
	cFilterAux += If( !( Empty( cFilterAux ) ), ' .And. !( SRA->RA_FILIAL $ fValidFil(,.T.) ) ', ' !( SRA->RA_FILIAL $ fValidFil(,.T.) ) ' )
EndIf

DbSetFilter({||&cFilterAux},cFilterAux)

If ( Type('nTpVale') == "U" )
	Private nTpVale := nTp
EndIf

	If nTpVale == 0
		cSubCabec := OemToAnsi(STR0010)  	// "V A L E   T R A N S P O R T E"
		cCadastro := OemToAnsi(STR0017)  			// "Vale Transporte"
	ElseIf nTpVale == 1
        cSubCabec := OemToAnsi(STR0011)  	// "V A L E   R E F E I C A O"
		cCadastro := OemToAnsi(STR0018)  			// "Vale Refeicao"
	ElseIf nTpVale == 2
		cSubCabec := OemToAnsi(STR0012)  	// "V A L E  A L I M E N T A C A O"
		cCadastro := OemToAnsi(STR0019)  			// "Vale Alimentacao"
	EndIf

	//-Verifica se eh Gestao Publica
	If GetMv("MV_GSPUBL", Nil, "1") == "2"
		aGpa131FSR0 := { "R0_FILIAL", "R0_MAT", "R0_CODIGO", "R0_TPVALE", "R0_VLRVALE", "R0_VLRFUNC", "R0_VLREMP", "R0_FALTAS", "R0_FERIAS", "R0_AFAST" }
		aGpa131FRG2 := { "RG2_FILIAL", "RG2_MAT", "RG2_ROTEIR", "RG2_SALFOL", "RG2_ORIGEM", "RG2_CODIGO", "RG2_TPVALE", "RG2_FALTAS", "RG2_FERIAS", "RG2_AFAST", "RG2_DIAS", "RG2_PEDIDO" }
	EndIf

	If nTpVale >= 1
		aAdd(aGpa131FSR0, "R0_QDIADIF")
		aAdd(aGpa131FSR0, "R0_VALDIF")

		aAdd(aGpa131FRG2, "RG2_DIADIF")
		aAdd(aGpa131FRG2, "RG2_VALDIF")
		aAdd(aGpa131FRG2, "RG2_CUNIDF")
		aAdd(aGpa131FRG2, "RG2_CFUNDF")
		aAdd(aGpa131FRG2, "RG2_CEMPDF")

		aAdd(aGpa131FRG2, "RG2_VTDUTE")
		aAdd(aGpa131FRG2, "RG2_VTDNUT")
		aAdd(aGpa131FRG2, "RG2_DUTILM")
	Else
		aAdd(aGpa131FSR0, "R0_TPCALC")
	EndIf

	If !lNovoCalc
		aadd(aGpa131FSR0,"R0_QVALDOM")
		aadd(aGpa131FSR0,"R0_QVALSEG")
		aadd(aGpa131FSR0,"R0_QVALTER")
		aadd(aGpa131FSR0,"R0_QVALQUA")
		aadd(aGpa131FSR0,"R0_QVALQUI")
		aadd(aGpa131FSR0,"R0_QVALSEX")
		aadd(aGpa131FSR0,"R0_QVALSAB")
		aadd(aGpa131FSR0,"R0_QVALFIX")
		aadd(aGpa131FSR0,"R0_PERIOD")
		aadd(aGpa131FSR0,"R0_NROPED")
		aadd(aGpa131FSR0,"R0_DTREFI")
		aadd(aGpa131FSR0,"R0_DTREFF")
		aadd(aGpa131FSR0,"R0_NROPGT")
		aadd(aGpa131FSR0,"R0_ROTEIR")
		aadd(aGpa131FSR0,"R0_DFALT")
		aadd(aGpa131FSR0,"R0_TPBEN")


		aAdd(aGpa131FRG2, "RG2_NROPED")
		aAdd(aGpa131FRG2, "RG2_VALDOM")
		aAdd(aGpa131FRG2, "RG2_VALSEG")
		aAdd(aGpa131FRG2, "RG2_VALTER")
		aAdd(aGpa131FRG2, "RG2_VALQUA")
		aAdd(aGpa131FRG2, "RG2_VALQUI")
		aAdd(aGpa131FRG2, "RG2_VALSEX")
		aAdd(aGpa131FRG2, "RG2_VALSAB")
		aAdd(aGpa131FRG2, "RG2_VALFIX")
	EndIf

	Begin Sequence

		dbSelectArea(cAliasSR0)

		cKeySeek := ( cFil + cMat )

	   // Monta querys para topConnection
	   QrysTop()

	   // Monta array AcolsSR0 e aColsRG2 dos objetos
	   MontaCols()

		If ( nOpcX == 3 ) .and. ( Len( aColsRecSR0 ) > 0 ) // Inclusao com lancamentos
			Help(" ",1,"A040CLANC") // "Ja existem lancamento para esse funcionario."
			Break
		ElseIf ( nOpcX # 3 ) .and. ( Len( aColsRecSR0 ) == 0 ) .and. ( Len( aColsRecRG2 ) == 0 )// Alteracao sem lancamentos
			Help(" ",1,"A040SLANC")  // "Nao existem lancamentos para esse funcionario."
			Break
		EndIf

	    // Carrega campos Editaveis
	    MontaEditaveis()

		// Monta as Dimensoes dos Objetos
		aAdvSize		:= MsAdvSize()
		aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4], 0, 0 }
		aAdd( aObjCoords , { 015 , 020 , .T. , .F. } )	// Matricula + Nome + Admissao
		aAdd( aObjCoords , { 015 , 015 , .T. , .F. } )	// Meios de Transporte - Titulo
		aAdd( aObjCoords , { 000 , 070 , .T. , .F. } )	// Meios de Transporte - Dados
		aAdd( aObjCoords , { 015 , 015 , .T. , .F. } )	// Historico - Titulo
		aAdd( aObjCoords , { 000 , 110 , .T. , .F. } )	// Historico - Dados
		aAdd( aObjCoords , { 000 , 030 , .T. , .F. } )	// Navegacao
		aObjSize := MsObjSize( aInfoAdvSize , aObjCoords )

		nOpcA 		:= 0
		aColsAntSR0 := Aclone(aColsSR0)
		aColsAntRG2 := Aclone(aColsRG2)

		If !lAutomato

		    DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD

			DEFINE MSDIALOG oDlg TITLE cCadastro From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL

			//Protecao de Dados Sensiveis
			If aOfusca[2]
				aFldOfusca 	:= FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRot ) // CAMPOS SEM ACESSO

				IF aScan( aFldOfusca , { |x| x:CFIELD == "RA_NOME" } ) > 0 
					lOfuscaNom	:= FwProtectedDataUtil():IsFieldInList( "RA_NOME" )
				ENDIF
				IF aScan( aFldOfusca , { |x| x:CFIELD == "RA_ADMISSA" } ) > 0 
					lOfuscaAdm := FwProtectedDataUtil():IsFieldInList( "RA_ADMISSA" )
				ENDIF
			EndIf

			// Matricula + Nome + Admissao
			@ aObjSize[1,1] , aObjSize[1,2] GROUP oGroup TO ( aObjSize[1,3] ),( ( aObjSize[1,4]/100*10 - 2 ) )				LABEL OemToAnsi(STR0007) OF oDlg PIXEL		// "Matricula:"
			oGroup:oFont:= oFont
			@ aObjSize[1,1] , ( ( aObjSize[1,4]/100*10 ) ) GROUP oGroup TO ( aObjSize[1,3] ),( aObjSize[1,4]/100*80 - 2 )	LABEL OemToAnsi(STR0008) OF oDlg PIXEL		// "Nome:"
			oGroup:oFont:= oFont
			@ aObjSize[1,1] , ( aObjSize[1,4]/100*80 ) GROUP oGroup TO ( aObjSize[1,3] ),aObjSize[1,4]						LABEL OemToAnsi(STR0013) OF oDlg PIXEL		// "Admiss„o:"
			oGroup:oFont:= oFont
			@ ( ( aObjSize[1,1]+10 ) ) , ( aObjSize[1,2] + 5)		SAY StrZero(Val(SRA->RA_MAT),Len(SRA->RA_MAT))	SIZE 050,10 OF oDlg PIXEL FONT oFont	// Matricula
			@ ( ( aObjSize[1,1]+10 ) ) , ( ( aObjSize[1,4]/100*10 ) + 5)	SAY If(lOfuscaNom,Replicate('*',15),OemToAnsi(SRA->RA_NOME))		SIZE 146,10 OF oDlg PIXEL FONT oFont	// Nome
			@ ( ( aObjSize[1,1]+10 ) ) , ( ( aObjSize[1,4]/100*80 ) + 5 )	SAY If(lOfuscaAdm,Replicate('*',10),Dtoc(SRA->RA_ADMISSA))			SIZE 050,10 OF oDlg PIXEL FONT oFont	// Admissao

			// Meios de Transporte
			@ aObjSize[2,1] , aObjSize[2,2] GROUP oGroup TO ( aObjSize[2,3] ), ( aObjSize[2,4] )	LABEL ""	OF oDlg PIXEL
			oGroup:oFont:= oFont
			@ ( ( ( ( aObjSize[2,3] ) - ( aObjSize[2,1] ) ) / 2 ) + ( aObjSize[2,1] - 3 ) ) , ( ( aObjSize[2,2] ) + 180 )	SAY OemToAnsi(cSubCabec)	SIZE 150,10 OF oDlg PIXEL FONT oFont	// Vale ...

			oGetSR0        := MsNewGetDados():New(;
			                                       aObjSize[3,1],;
					                               aObjSize[3,2],;
			    		                           aObjSize[3,3],;
					                               aObjSize[3,4],;
					                               IF(nOpcx==2.Or.nOpcx==5,0, GD_INSERT+GD_UPDATE+GD_DELETE	),; // controle do que podera ser realizado na GetDado - nstyle
					                               "BenLinOk",;
					                               "BenTudOk",;
					                               "R0_TPVALE="+StrZero(nTpVale,1),;
					                               aGdAlteraSR0,;					// aAlter
					                               0,;
					                               99999,;
				                               	   If(lR0CodAtu, Nil, 'Gp131AltVT()'),;
					                               Nil,;
					                               Nil,;
					                               oDlg,;
					                               aHeaderSR0,;
					                               aColsSR0,;
					                              )

			// Historico
			@ aObjSize[4,1] , aObjSize[4,2] GROUP oGroup TO ( aObjSize[4,3] ), ( aObjSize[4,4] )				LABEL ""	OF oDlg PIXEL
			oGroup:oFont:= oFont
			@ ( ( ( ( aObjSize[4,3] ) - ( aObjSize[4,1] ) ) / 2 ) + ( aObjSize[4,1] - 3 ) ) , ( ( aObjSize[4,2] ) + 180 )	SAY OemToAnsi(STR0009)+cSubCabec	SIZE 150,10 OF oDlg PIXEL FONT oFont	// Historico de ...

		    oGetRG2 	  := MsNewGetDados():New(;
					                                aObjSize[5,1],;  		// nTop
													aObjSize[5,2],;			// nLelft
													aObjSize[5,3],;			// nBottom
													aObjSize[5,4],;			// nRright
													0,; // controle do que podera ser realizado na GetDado - nstyle
													"BenLinOk2",;			// funcao para validar a edicao da linha - ulinhaOK
													"BenTudOk2",;   		// funcao para validar todos os registros da GetDados - uTudoOK
													NIL,;					// cIniCPOS
													aGdAlteraRG2,;					// aAlter
													0,;						// nfreeze
													99999,;					// nMax
													NIL,;					// cFieldOK
													NIL,;					// usuperdel
													Nil,;					// bloco com funcao para validar registros deletados
													oDlg,;                 	// objeto de dialogo - oWnd
												    aHeaderRG2,;			// Vetor com Header - AparHeader
												    aColsRG2,;				// Vetor com Colunas - AparCols
												    )


			// Define grupo do rodape
			@ (aObjSize[6,1] + 6) , ( ( aObjSize[6,2]+2 ) ) 		GROUP oGroup TO ( aObjSize[6,3] +05 ),( (aObjSize[6,4]/100*78)	  ) LABEL OemToAnsi(STR0015) OF oDlg PIXEL	 //"Pesquisar Funcionarios"
			oGroup:oFont:= oFont
			@ (aObjSize[6,1] + 6) , ( ( aObjSize[6,4]/100*78 ) ) 	GROUP oGroup TO ( aObjSize[6,3] +05 ),( (aObjSize[6,4])	  ) 		LABEL OemToAnsi(STR0016) OF oDlg PIXEL	 //"Filtrar Competencia"
			oGroup:oFont:= oFont

			// Lista Botoes de posicionamento do funcionario
			@ ( ( aObjSize[6,1] ) +17 ) , ( ( aObjSize[6,2] )+08.96) 	BUTTON oBtnPgPrev 	PROMPT OemToAnsi("<<")	 SIZE 021.5,009 DESIGN ACTION Gp131Mov(bRetrocAll) OF oDlg PIXEL
			@ ( ( aObjSize[6,1] ) +17 ) , ( ( aObjSize[6,2] )+29.96)	BUTTON oBtnPrev		PROMPT OemToAnsi("<")	 SIZE 021.5,009 DESIGN ACTION Gp131Mov(bRetrocOne) OF oDlg PIXEL
			@ ( ( aObjSize[6,1] ) +17 ) , ( ( aObjSize[6,2] )+50.96)	BUTTON oBntNext 	PROMPT OemToAnsi(">")	 SIZE 021.5,009 DESIGN ACTION Gp131Mov(bAvancaOne) OF oDlg PIXEL
			@ ( ( aObjSize[6,1] ) +17 ) , ( ( aObjSize[6,2] )+71.96)	BUTTON oBtnPgNext 	PROMPT OemToAnsi(">>")	 SIZE 021.5,009 DESIGN ACTION Gp131Mov(bAvancaALL) OF oDlg PIXEL

		    AxPesqOrd("SRA",@aPesqIdx,,lSeeAll,@aPesqOrd)
		    cPesqOrd := aPesqOrd[1]

			@ ((aObjSize[6,1])+17),((aObjSize[6,4])*0.22) 			COMBOBOX oPesqCbx 	VAR cPesqOrd ITEMS aPesqOrd SIZE 100,36 	PIXEL OF oDlg ON CHANGE If(lAxPesqui,(DbSelectArea(cAliasSR0),DbSetOrder(aPesqIdx[oPesqCbx:nAt][1]),__oObjBrow:Refresh()),)
		  	@ ((aObjSize[6,1])+17),((aObjSize[6,4])*0.22 + 110) 	MSGET    oPesqGet 	VAR cPesqCampo 	SIZE 100,09 	PIXEL OF oDlg
			@ ((aObjSize[6,1])+17),((aObjSize[6,4])*0.22 + 220) 	BUTTON   "Ok" 		SIZE 021.5,009 	PIXEL OF oDlg ACTION (AxPesqSeek("SRA",.F.,cPesqCampo,,aPesqIdx,oPesqCbx:nAt,lSeeAll,),Gp640Movimenta(bFica))

			// Pesquisar Competencia
		  	@ ((aObjSize[6,1])+17),((aObjSize[6,4])/100*82)  		MSGET    oFiltraGet	VAR cPesqFiltro	SIZE 040,09 	PIXEL OF oDlg PICTURE "@R 99/9999"   	VALID ( VAL(SubStr(cPesqFiltro,1,2)) > 0 .And. VAL(SubStr(cPesqFiltro,1,2)) < 13 )
			@ ((aObjSize[6,1])+17),((aObjSize[6,4])/100*92)  		BUTTON   "Ok" 		SIZE 021.5,009 	PIXEL OF oDlg ACTION ( fPosRG2(cPesqFiltro) )

		    bSet15 := {|| nOpca:=If(nOpcx=5,2,1), If(oGetSR0:TudoOk(), If(oGetRG2:TudoOk(), oDlg:End(), nOpca:= 0), nOpca:= 0)}
		    bSet24 := {|| nOpca:= 0, oDlg:End() }

			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, bSet15, bSet24 )
		Else
			If nOpcNewGd != 5
				If Len(aAutSR0) > 0
					aHeader := aHeaderSR0
					aCols 	:= aColsSR0
					If MsGetDAuto( aAutSR0, "BenLinOk", "BenTudOk", , nOpcNewGd)
						nOpcA := Confirma
					EndIf
				ElseIf Len(aAutRG2) > 0
					aHeader := aHeaderRG2
					aCols 	:= aColsRG2
					If MsGetDAuto( aAutRG2, "BenLinOk2", "BenTudOk2", , 0)
						nOpcA := Confirma
					EndIf
				EndIf
			Else
				nOpcA := Confirma
			EndIf

		EndIf

		If ( ( nOpca == 1 ) .and. ( nOpcX <> 2 ) )
		     If !lAutomato
				Gpea131Grava( cAliasSR0 , cFil , cMat , aVirtualSR0 , aColsRecSR0 , nOpcx , .T. )
				Gpea131Grava( cAliasRG2 , cFil , cMat , aVirtualRG2 , aColsRecRG2 , nOpcx , .F. )
			Else
				If Len(aAutSR0) > 0
					Gpea131Grava( cAliasSR0 , cFil , cMat , aVirtualSR0 , aColsRecSR0 , nOpcx , .T. )
				Else
					Gpea131Grava( cAliasRG2 , cFil , cMat , aVirtualRG2 , aColsRecRG2 , nOpcx , .F. )
				EndIf
			EndIf
		EndIf

	End Sequence

	RestArea( aArea )
	If ( nOpcX == 3 )
		MBrChgLoop(.F.)
	EndIf

Return( Nil )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ Gpea131GrInc º Autor ³ Equipe RH          º Data ³24/05/95 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Grava os registros Incluidos na GetDados.			      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gpea131GrInc( cAlias , cPrefixo , cFil , cMat , aVirtual , nIniciar, lSR0 )
Local aHeaderAux    := aClone( If( lAutomato, aHeader , If( lSR0, oGetSR0:aHeader, oGetRG2:aHeader ) ) )
Local aColsAux      := aClone( If( lAutomato, aCols , If( lSR0, oGetSR0:aCols, oGetRG2:aCols ) ) )
Local cCampo		:= ""
Local nLenHeader	:= Len( aHeaderAux )
Local nLenCols		:= Len( aColsAux )
Local nPosDel		:= GdFieldPos( "GDDELETED", aHeaderAux )
Local nX			:= 0.00
Local nY 			:= 0.00

	For nX := nIniciar To nLenCols
		If !( aColsAux[nX,nPosDel] )
			(cAlias)->( RecLock(cAlias,.T.) )
			(cAlias)->(&(cPrefixo+"FILIAL"))	:= cFil
			(cAlias)->(&(cPrefixo+"MAT"))		:= cMat

			For nY := 1 To nLenHeader

				// Obtem o Campo para Gravacao
				cCampo := aHeaderAux[nY,2]

				// Nao Grava Campo Virtual
				If ( aScan(aVirtual,cCampo) # 0 )
					Loop
				EndIf

				If cCampo == "RG2_ANOMES"
					(cAlias)->( &cCampo ) := Right(aColsAux[nX,nY],4) + Left(aColsAux[nX,nY],2)
				Else
					(cAlias)->( &cCampo ) := aColsAux[nX,nY]
				EndIf

			Next nY

			If !lSR0
				(cAlias)->RG2_ORIGEM := "I"
			EndIf

			// Destrava o Registro
			( cAlias )->( MsUnLock() )
		EndIf
	Next nX

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ BenLinOk     º Autor ³ Marcos A. Stiefano º Data ³19/07/95 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Critica linha digitada.      						      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function BenLinOk()
Local lRet  := .T.
Local nX	:= 0
Local nY	:= 0
Local nLenCols   := 0
Local nPosTpVale := 0
Local nPosCodigo := 0
Local nLenHeader := 0

	nLenCols   := If( lAutomato, Len(aCols) , Len( oGetSR0:aCols ) )
	nLenHeader := If( lAutomato, Len(aHeader) , Len( oGetSR0:aHeader ) )
	nPosTpVale := Ascan(If( lAutomato, aHeader , oGetSR0:aHeader ), {|x| Alltrim(x[2]) = "R0_TPVALE" })
	nPosCodigo := Ascan(If( lAutomato, aHeader , oGetSR0:aHeader ), {|x| Alltrim(x[2]) = "R0_CODIGO" })

	If ( ( !lAutomato .and. !oGetSR0:aCols[n,nUsadoSR0+1] ) .or. ( lAutomato .and. !aCols[n,nUsadoSR0+1] ) )
		For nx = 1 To nLenHeader
			If ( ( !lAutomato .and. Empty(oGetSR0:aCols[n,nx]) ) .and. ( lAutomato .and. Empty(aCols[n,nx]) ) )
				If Trim(If( lAutomato, aHeader , oGetSR0:aHeader )) == "R0_CODIGO" .and. lRet
					Help(" ",1,"GP130SMEI") // "Codigo de Meio de Transporte em branco"
					lRet:= .F.
					Exit
				EndIf
			Endif
		Next nx

		For nY = 1 To nLenCols
			// Valida Chave Unica -> R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO
			If n > 1
				nChvUnic := aScan(If( lAutomato, aCols , oGetSR0:aCols ), {|y| y[nPosTpVale]+y[nPosCodigo] = If( lAutomato, aCols[n,nPosTpValen]+aCols[n,nPosCodigo] , oGetSR0:aCols[n,nPosTpValen]+oGetSR0:aCols[n,nPosCodigo] )})
				If nChvUnic > 0 .And. nChvUnic <> n
					Help(" ",1,"CHAVDUPL") // "Informacao duplicada"
					lRet := .F.
					Exit
				EndIf
			EndIf

		Next nY
	EndIf

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ BenLinOk2    º Autor ³ Marcos A. Stiefano º Data ³19/07/95 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Critica linha digitada.      						      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function BenLinOk2()
Local lRet			:= .T.
Local nX,nY
Local nPosDias   	:= 0
Local nPosDNUteis	:= 0
Local nLenColsRG2	:= 0
Local aRG2Cols


	nLenColsRG2	:= If( lAutomato, Len(aCols), Len( oGetRG2:aCols ) )
	aRG2Cols	:= If( lAutomato, aCols, oGetRG2:aCols )

	If ArrayCompare( aRG2Cols , aColsAntRG2 )
		Return (lRet)
	EndIf

	If nTpVale == 0 //Vale Transporte
		If aRG2Cols[n,nUsadoRG2+1] = .F.
			nPosDias    := Ascan(aHeaderRG2,{|x| Alltrim(x[2]) = "RG2_VTDUTE" })
			nPosDNUteis := Ascan(aHeaderRG2,{|x| Alltrim(x[2]) = "RG2_VTDNUT" })
			For nX := 1 To nLenColsRG2
		 		If Empty(aRG2Cols[n, nPosDias]) .And. Empty(aRG2Cols[n, nPosDNUteis]) .And. lRet
		  			Help(" ",1,"GP130SDIA") // "Quantidade de Dia por Meio de Transporte em branco."
			   		lRet := .F.
				   	Exit
		  		EndIf
			Next nx
		EndIf
	EndIf

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ BenTudOk     º Autor ³ Marcos A. Stiefano º Data ³19/07/95 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Movimentacao dos funcionarios.						      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function BenTudOk()
Local lRetorna  := .T.
Return lRetorna


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ BenTudOk2    º Autor ³ Marcos A. Stiefano º Data ³19/07/95 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Movimentacao dos funcionarios.						      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function BenTudOk2()
Local lRetorna:= .T.
Return lRetorna


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ Gp131Mov     º Autor ³ Equipe RH      º Data ³  19/07/95   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Movimentacao dos funcionarios.						      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gp131Mov( bFunc )
Local oObjBrow := GetObjBrow()
Local cFiltro  := oObjBrow:oFWFilter:GetExprADVPL()

cFiltro := StrTran(cFiltro, 'RA_SITFOLH== ""', 'RA_SITFOLH== " "')

bFiltro  := &( ' { || ' + cFiltro + ' } ' )

if !Empty(cFiltro)
	SRA->( dbSetFilter( bFiltro, cFiltro ) )
EndIf

Eval(bFunc)

Return(.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ gp131MntGetDados º Autor ³ Equipe RH      º Data ³  20/12/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Montagem da GetDados.		  						          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cAlias													      º±±
±±º          ³ nOpcX  														  º±±
±±º          ³ cFil - Filial.												  º±±
±±º          ³ cMat - Matricula.											  º±±
±±º          ³ aVirtual - array dos campos virtuais.						  º±±
±±º          ³ aColsRec - array que contem o Recno() dos registros da aCols.  º±±
±±º          ³ aColsSR0 													  º±±
±±º          ³ lGravarAntes - indica se deve chamar a funcao de gravacao      º±±
±±º          ³ antes de montar a tela.  									  º±±
±±º          ³ lEntrada - indica se a funcao gp131MntGetDados esta sendo      º±±
±±º          ³            chamada na entrada da rotina de lancamentos         º±±
±±º          ³            por periodo.   									  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131							                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function gp131MntGetDados(cAlias, nOpcX, cFil, cMat, aVirtual1, aColsRec1, aVirtual2, aColsRec2, lGravarAntes, lEntrada)
Default lEntrada 	:= .F.

	// Antes de montar a tela, verifica se deseja gravar ... caso tenha tido alguma alteracao.
	If lGravarAntes
		MsAguarde( { || Gpea131Grava( "SR0" , cFil , cMat , aVirtual1 , aColsRec1 , nOpcx , .T. ),;
			      		Gpea131Grava( "RG2" , cFil , cMat , aVirtual2 , aColsRec2 , nOpcx , .F. ) }, OemToAnsi(STR0014))	// Aguarde...
	EndIf

	cMat	:= SRA->RA_MAT

	// Monta Query para a Selecao das Informacoes em GdMontaCols
	QrysTop()

	// Se esta entrando na tela, verifica se existe algum lancamento, independente do periodo,
	// para o funcionario selecionado. Se for inclusao, nao deve ter nenhum registro. Se for
	// alteracao, deve ter pelo menos um registro.
	If lEntrada
		cKeySeek := ( cFil + cMat )


      // Monta array AcolsSR0 e aColsRG2 dos objetos
      MontaCols()

		IF ( ( nOpcX == 3 ) .And. ( Len( aColsRec ) > 0 ) ) //Quando for Inclusao
			Help(" ",1,"A040CLANC") // "Ja existem lancamentos para este funcionario."
			Break
		ElseIf ( nOpcX <> 3 ) .And. ( Len( aColsRec ) == 0 ) //Quando Nao for Inclusao
			Help(" ",1,"A040SLANC") // "Nao existem lancamentos para este funcionario."
			Break
		EndIF
	EndIf

	cKeySeek := ( cFil + cMat )

	// Monta os Detalhes
	SR0->( dbSetOrder( RetOrder( "SR0" , "R0_FILIAL+R0_MAT" ) ) )

	// Monta array AcolsSR0 e aColsRG2 dos objetos
	MontaCols()

	// Carrega campos Editaveis
	MontaEditaveis()


	// Salva o acols antes da alteracao
	aColsAntSR0	:= aClone( aColsSR0 )
  	aColsAntRG2 := aClone( aColsRG2 )

	// Atualiza a getdados
	If !(oGetSR0 == nil)
		oGetSR0:aCols := aClone(aColsSR0)
		oGetSR0:aHeader := aClone(aHeaderSR0)
		oGetSR0:Refresh()
	EndIf

	If !(oGetRG2 == nil)
		oGetRG2:aCols := aClone(aColsRG2)
		oGetRG2:aHeader := aClone(aHeaderRG2)
		oGetRG2:Refresh()
	EndIf

Return( .T. )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ Gpea131Grava º Autor ³ Ronan          º Data ³  20/12/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Grava no arquivo de Historico.						      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gpea131Grava( cAlias , cFil , cMat , aVirtual , aRegAltera , nOpc , lSR0 )
Local aHeaderAux    := aClone( If( lAutomato, aHeader , If( lSR0, oGetSR0:aHeader, oGetRG2:aHeader ) ) )
Local aColsAux      := aClone( If( lAutomato, aCols , If( lSR0, oGetSR0:aCols, oGetRG2:aCols ) ) )
Local aColsAnt		:= aClone( If( lSR0, aColsAntSR0,aColsAntRG2 ) )
Local cPrefixo		:= ( PrefixoCpo( cAlias ) + "_" )
Local cCampo		:= ""
Local lTudoIgual	:= .F.
Local nX 			:= 0.00
Local nY 			:= 0.00
Local nLenHeader	:= Len( aHeaderAux 	)
Local nLenCols		:= Len( aColsAux   	)
Local nLenAlt		:= Len( aRegAltera	)
Local nPosDel		:= GdFieldPos( "GDDELETED" , aHeaderAux)

	Begin Transaction

		If ( nOpc == 5 ) // Exclusao

			DelRecnos( cAlias , @aRegAltera )
			( cAlias )->( EvalTrigger() )

		Else // Inclusao ou Alteracao

			If !fCompArray( aColsAux , aColsAnt )

				If ( nLenAlt > 0.00 )
					For nX := 1 To nLenAlt
						lTudoIgual := fCompArray( aColsAux[ nX ] , aColsAnt[nX] )
						If ( lTudoIgual ) .and. !( aColsAux[nX,nPosDel] )
							Loop
						EndIf
						( cAlias )->( dbGoto( aRegAltera[nX] ) )
						( cAlias )->( RecLock( cAlias , .F. , .T. ) )
						If ( aColsAux[nX,nPosDel] )
							(cAlias)->( dbDelete() )
						Else
							(cAlias)->(&(cPrefixo+"FILIAL"))	:= cFil
							(cAlias)->(&(cPrefixo+"MAT"))		:= cMat
							For nY := 1 To nLenHeader
								// Obtem o Campo para Gravacao
								cCampo := aHeaderAux[nY,2]

								// Nao Grava Campo Virtual
								If ( aScan(aVirtual,cCampo) # 0 )
									Loop
								EndIf

								If cCampo == "RG2_ANOMES"
									(cAlias)->( &cCampo ) := Right(aColsAux[nX,nY],4) + Left(aColsAux[nX,nY],2)
								Else
									(cAlias)->( &cCampo ) := aColsAux[nX,nY]
								EndIf

							Next nY
							If !lSR0
								(cAlias)->RG2_ORIGEM := "I"
							EndIf
		            EndIf
						// Destrava o Registro
						( cAlias )->( MsUnLock() )
					Next nX
					If ( nLenCols > nLenAlt ) // Se Trata de Inclusao de Itens
						Gpea131GrInc( cAlias , cPrefixo , cFil , cMat , aVirtual , ( nLenAlt + 1 ), lSR0 )
					EndIf
				Else
					Gpea131GrInc( cAlias , cPrefixo , cFil , cMat , aVirtual , 1, lSR0 )
				EndIf

				( cAlias )->( EvalTrigger() )

			EndIf

		EndIf

	End Transaction

Return( .T. )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ QrysTop  ºAutor  ³Ronan               º Data ³  20/12/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Montar querys necessarias para uso com topconnection       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function QrysTop()

   aQuerySR0    := ARRAY(1)
   aQuerySR0[1] := "R0_FILIAL='"+xFilial("SR0",SRA->RA_FILIAL)+"' AND R0_MAT='"+cMat+"' AND D_E_L_E_T_=' ' AND R0_TPVALE = '"+StrZero(nTpVale,1)+"' "
   aQueryRG2    := ARRAY(1)
   aQueryRG2[1] := "RG2_FILIAL='"+xFilial("RG2",SRA->RA_FILIAL)+"' AND RG2_MAT='"+cMat+"' AND D_E_L_E_T_=' ' AND RG2_TPVALE = '"+StrZero(nTpVale,1)+"' "

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ MontaEditaveis º Autor |Ronan             º Data ³  12/20/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Carrega, apenas, os Campos Editaveis usados em GDMONTACOLS.    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Somente neste programa (gpea131)                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MontaEditaveis()
Local nX:= 0.00

	aGdAlteraSR0:= {}
	aGdAlteraRG2:= {}
	If nTpVale > 0
		aeval(aHeaderSR0,{|x| x[13] := "GdFieldGet( 'R0_TPCALC') <> '2'"})
	EndIf
	For nX := 1	To nUsadoSR0
		IF (;
			( aScan( aVirtualSR0 , aHeaderSR0[ nX , 02 ] ) == 0.00 ) .and. ;
		   	( aScan( aVisualSR0  , aHeaderSR0[ nX , 02 ] ) == 0.00 ) .and. ;
		   	( aScan( aGpa131FSR0 , aHeaderSR0[ nX , 02 ] ) == 0.00 ) .and. ;
		   	( aScan( aGdNAltSR0  , aHeaderSR0[ nX , 02 ] ) == 0.00 ) 	   ;
		  	)
			aAdd( aGdAlteraSR0 , aHeaderSR0[ nX , 02 ] )
		EndIF
	Next nX

	For nX := 1	To nUsadoRG2
		IF (;
			( aScan( aVirtualRG2 , aHeaderRG2[ nX , 02 ] ) == 0.00 ) .and. ;
		   	( aScan( aVisualRG2  , aHeaderRG2[ nX , 02 ] ) == 0.00 ) .and. ;
		   	( aScan( aGpa131FRG2 , aHeaderRG2[ nX , 02 ] ) == 0.00 ) .and. ;
		   	( aScan( aGdNAltRG2  , aHeaderRG2[ nX , 02 ] ) == 0.00 ) 	   ;
		  	)
			aAdd( aGdAlteraRG2, aHeaderRG2[ nX , 02 ] )
		EndIF
	Next nX

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ MontaCols    º Autor |Ronan               º Data ³  12/20/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Montagem das colunas para o gdmontadols (aColsSR0 e aColsRG2). º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131 							                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MontaCols()
Local nX		:= 0
Local nI		:= 0
Local nPosPeriod
Local nPosAnoMes := 0
Local nPosTpVale := 0
Local nPosAuto	 := 0
Local aAuxSR0	:= {}
Local aAuxRG2	:= {}
Local lPrimeira	:= .F.

	aColsSR0:= GdMontaCols(		  @aHeaderSR0,;
								  @nUsadoSR0,;
								  @aVirtualSR0,;
								  @aVisualSR0,;
								  "SR0",;
								  aGpa131FSR0,;
								  @aColsRecSR0,;
								  "SR0",;
								  cKeySeek,;
								  NIL,; // 10
								  bSkip,;
								  NIL,;
								  NIL,;
								  NIL,;
								  NIL,;
								  NIL,;
								  NIL,;
								  aQuerySR0,;
								  .F.,;
								  .F.,;  // 20
								  .T.,;
								  NIL,;
								  NIL,;
								  NIL,;
								  NIL,;
								  NIL,;
								  NIL,;
								  NIL,;
								  NIL,;
								 .F.;     // 30
								 )

	RG2->( DBSETORDER(5) )
	aColsRG2:= GdMontaCols(@aHeaderRG2,;
	 								@nUsadoRG2,;
	  								@aVirtualRG2,;
	  								@aVisualRG2,;
	  								cAliasRG2,;
	  								aGpa131FRG2,;
	  								@aColsRecRG2,;
	  								cAliasRG2,;
	  								cKeySeek,;
	  								NIL,;
	  								bSkip,;
	  								NIL,;
	  							    NIL,;
	  								NIL,;
	  								NIL,;
	  								NIL,;
	  								NIL,;
	  								aQueryRG2,;
	  								.F.,;
	     							.F.,;
	  								.T.;
	  							 )
	RG2->( DBSETORDER(1) )

	If ( nPosPeriod := Ascan( aHeaderRG2, { |x| Alltrim(x[2]) = "RG2_PERIOD" } ) ) > 0
		If len(AllTrim(aHeaderRG2[nPosPeriod][6])) > 0
			aHeaderRG2[nPosPeriod][6] += " .and. fRG2AnoMes(M->RG2_PERIOD)"
		Else
			aHeaderRG2[nPosPeriod][6] := "fRG2AnoMes(M->RG2_PERIOD)"
		Endif
	Endif

	// Apresenta o campo ANOMES no formato MM/YYYY
	If ( nPosAnoMes	:= Ascan(aHeaderRG2,{ |x| Alltrim(x[2]) = "RG2_ANOMES" }) ) > 0
		For nX := 1 to Len(aColsRG2)
			aColsRG2[nX][nPosAnoMes] := Right(aColsRG2[nX][nPosAnoMes],2) + "/" + Left(aColsRG2[nX][nPosAnoMes],4)
		Next nX
	EndIf

	// Carrega o campo R0_TPVALE com a opcao selecionada no Menu
	If ( nPosTpVale	:= Ascan(aHeaderSR0,{ |x| Alltrim(x[2]) = "R0_TPVALE" }) ) > 0
		If nTpVale = 0
			aColsSR0[1][nPosTpVale] := "0"
		ElseIf nTpVale = 1
			aColsSR0[1][nPosTpVale] := "1"
		ElseIf nTpVale = 2
			aColsSR0[1][nPosTpVale] := "2"
		EndIf
	EndIf

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ fPosRG2      ³ Autor ³ Kelly Soares      ³ Data ³ 23/12/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Posiciona o historico de acordo com o mes/ano solicitado.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fPosRG2(cMesAno)
Local nPosCampo := Ascan( aHeaderRG2   , { |x| Alltrim(x[2]) = "RG2_ANOMES" } )
Local nPos		:= Ascan( If( lAutomato, aCols, oGetRG2:aCols ), { |x| x[nPosCampo]  == cMesAno } )

	If !lAutomato
		If ( nPos > 0 , oGetRG2:GoTo(nPos) , oGetRG2:GoTo(1) )

		oGetRG2:oBrowse:Refresh(.T.)
	EndIf

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fRG2AnoMes   ³ Autor ³ Kelly Soares      ³ Data ³ 23/12/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Carrega o campo AnoMes do historico de acordo com o        ³±±
±±³          ³ periodo indicado.                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fRG2AnoMes(cPeriodo)
Local aArea		:= GetArea()
Local nPos		:= Ascan(If( lAutomato, aHeader, oGetRG2:aHeader ),{|x| Alltrim(x[2]) = "RG2_ANOMES" })
Local lRet

	DbSelectArea("RCH")
	DbSetOrder(1)
	lRet := DbSeek(xFilial("RCH")+SRA->RA_PROCES+cPeriodo)
	If lRet
		If !lAutomato
			oGetRG2:aCols[n][nPos] := RCH->RCH_MES + "/" + RCH->RCH_ANO
		Else
			aColsRG2[n][nPos] := RCH->RCH_MES + "/" + RCH->RCH_ANO
		EndIf
	Endif

	Restarea(aArea)

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ GPEA131  º Autor ³ Equipe RH          º Data ³  01/11/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA131                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Ver_SRA_EOF()
Local lSkip:= .T.

If SRA->(EOF())
   lSkip:= .F.
   aColsSR0:= {}
   aColsRG2:= {}
   SRA->(dbskip(-1))
EndIf

Return( lSkip )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ CodigoBenef ³ Autor ³ Equipe RH Inovacao ³ Data ³ 16/01/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Monta Combo com os codigos de beneficios                   ³±±
±±³          ³ periodo indicado.                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CodigoBenef()
Local aArea		:= GetArea()
Local aAreaSR0	:= SR0->( GetArea() )
Local aAreaRFO	:= RFO->( GetArea() )
Local cOpcBox   := ""

	If ( Type('nTpVale') == "U" )
		Return
	EndIf

   dbSelectArea('SR0')

If nTpVale = 0
   DbSelectArea("SRN")
   DbSetOrder(1) //-RN_FILIAL+RN_COD
   DbGoTop()
   dbSeek(xFilial('SRN',SRA->RA_FILIAL))
   While !Eof() .And. SRN->RN_FILIAL == xFilial('SRN',SRA->RA_FILIAL)
	   cOpcBox += ( RN_COD + "=" + RN_DESC + ";" )
       dbSkip()
   EndDo

Else //-nTpVale $ "1*2"

   DbSelectArea("RFO")
   DbSetOrder(1) //-RFO_FILIAL+RFO_TPVALE+RFO_DESCR+RFO_TPBEN+RFO_CODIGO
   DbGoTop()
   dbSeek(xFilial('RFO',SRA->RA_FILIAL))
   While !Eof() .And. RFO->RFO_FILIAL == xFilial('RFO',SRA->RA_FILIAL)
   	 If RFO_TPVALE == StrZero(nTpVale,1)
        cOpcBox += ( RFO_CODIGO + "=" + RFO_DESCR + ";" )
     EndIf
     dbSkip()
   EndDo
EndIf

RestArea(aAreaSR0)
RestArea(aAreaRFO)
RestArea(aArea)

Return( cOpcBox )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Gp131Calc   ³ Autor ³ Leandro Drumond    ³ Data ³ 06/08/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Chama rotina de calculo.					                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gp131Calc()
	RstExecCalc() //Reseta statics antes de cada calculo
	Gpem020() //Executa rotina de calculo
Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Gp131CalcA  ³ Autor ³ Leandro Drumond    ³ Data ³ 30/03/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Chama rotina de calculo (Outros Beneficios).               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gp131CalcA()
	RstExecCalc() //Reseta statics antes de cada calculo
	Gpem020() //Executa rotina de calculo
Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Gp131Fech   ³ Autor ³ Raquel Hager       ³ Data ³ 16/01/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Chama rotina de fechamento.  			                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gp131Fech()

GPEM120()

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Gp131AltVT  ³ Autor ³Marcelo Silveira    ³ Data ³ 29/08/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Preenche o valor na inclusao ou alteracao do codigo de VT. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gp131AltVT()

Local cTipoVale	:= cValToChar( nTpVale )
Local nValUni  	:= 0.00
Local aAreaAtu 	:= {}

nPosCodVale := GdFieldPos("R0_CODIGO", aHeaderSR0) //Codigo do VT
nPosValUnit := GdFieldPos("R0_VLRVALE", aHeaderSR0) //Valor Unitario
nPosDiaCalc := GdFieldPos("R0_QDIACAL", aHeaderSR0) //Quantidade Total
nPosValCalc := GdFieldPos("R0_VALCAL", aHeaderSR0) //Valor Calculado

If Type('lR0CodAtu') == "U"
	Private lR0CodAtu := fVerAtuR0Cod()
EndIf

If ( !lR0CodAtu .And. "R0_CODIGO" $ ReadVar() ) .Or. lR0CodAtu

	If Len( aColsAntSR0 ) > 0 .And. nPosCodVale > 0

		If Len(aCols) > Len(aColsAntSR0) .Or. M->R0_CODIGO # aColsAntSR0[n,nPosCodVale] .Or. !fCompArray(aCols,aColsAntSR0)//Verifica se houve alteracao do Codigo do VT

			aAreaAtu := GetArea()

			If nTpVale == 0 //Vale Transporte
				dbSelectArea( "SRN" )
				If dbSeek( xFilial("SRN", SRA->RA_FILIAL ) + M->R0_CODIGO )
					nValUni := SRN->RN_VUNIATU
				Endif
			Else  //Vale Alimentacao/Refeicao
				DbSelectArea( "RFO" )
				If dbSeek( xFilial("RFO", SRA->RA_FILIAL ) + cTipoVale + M->R0_CODIGO )
					nValUni := RFO->RFO_VALOR
				EndIf
			EndIf

			If nPosValUnit > 0 .And. nValUni > 0
				aCols[n,nPosValUnit] := nValUni		//Atualiza o Valor do Vale
			EndIf
			If nPosDiaCalc > 0
				aCols[n,nPosDiaCalc] := 0.00		//Atualiza a quantidade Total
			EndIf
			If nPosValCalc > 0
				aCols[n,nPosValCalc] := 0.00		//Atualiza o Valor Calculado
			EndIf

			RestArea(aAreaAtu)
		EndIf

	EndIf

EndIf

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Menudef     ³ Autor ³ Equipe RH Inovacao ³ Data ³ 10/01/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Isola opcoes de menu para que as opcoes da rotina possam   ³±±
±±³          ³ ser lidas pelas bibliotecas Framework da Versao 9.12.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MenuDef()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aRet
Local aRotina := { 	{ STR0001 	,"PesqBrw"	, 0 , 1,,.F.},;    //"Pesquisar"
					{ STR0002	,"gp131Atu"	, 0 , 2},;  		//"Visualizar"
					{ STR0003 	,"gp131Inc"	, 0 , If(IsBlind(),3,4)},;  		//"Incluir"
					{ STR0004 	,"gp131Atu"	, 0 , 4},;  		//"Alterar"
					{ STR0006	,"GpLegend"	, 0 , 5 , ,.F.}}	//"Legenda"


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para adicionar opcoes de menu.			      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If IsInCallStack("GPEA131")
	If ExistBlock( "GPA131MN")
		IF ( ValType( aRet := ExecBlock( "GPA131MN" ,.F.,.F.,{aClone(aRotina)}, .F. ) ) == "A" )
	   		aRotina := aClone(aRet)
		Endif
	EndIf
EndIf
Return( aRotina )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ gp131Inc  ³ Autor ³ Equipe RH Inovacao ³ Data ³ 11/11/2014 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Função para chamda da inclusão para que a caixa de escolha ³±±
±±³          ³ das filiais não seja aberta                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA131                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp131Inc(cAlias)

gp131Atu(cAlias, 0, 3)

Return

/*{Protheus.doc} fVerAtuR0Cod

Funcao que verifica se o valid do campo R0_CODIGO esta atualizado

Obs.: esse processo e' necessario devido ao congelamento da versao 12.1.7,
pois pode ser que o cliente nao esteja com o dicionario atualizado. Dessa
forma, a funcao verifica se o campo esta atualizado e a rotina faz o
devido tratamento quando o dicionario esta ou nao atualizado

@author Allyson Mesashi
@since 16/03/2016
@version P12.1.7
@Return lAtu Retorna se o campo R0_CODIGO esta atualizado
*/

Static Function fVerAtuR0Cod()

Local aArea		:= GetArea()
Local aAreaSX3	:= SX3->( GetArea() )
Local lAtu		:= .F.

dbSelectArea("SX3")
dbSetOrder(2)

If SX3->( dbSeek( "R0_CODIGO" ) )
	If AllTrim( Upper( SX3->X3_VALID ) ) == Upper( "NaoVazio() .And. Gp131AltVT()" )
		lAtu := .T.
	EndIf
EndIf

RestArea( aAreaSX3 )
RestArea( aArea )

Return( lAtu )
