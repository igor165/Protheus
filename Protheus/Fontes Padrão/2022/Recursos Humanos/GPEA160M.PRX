#INCLUDE "TOTVS.CH"
#INCLUDE "GPEA160M.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPEA160M ³ Autor ³ Leandro Drumond       ³ Data ³ 13/11/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cadastro e Manutencao de Roteiros Interno                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gpea160M()

Local aArea			:= GetArea()
Local aAreaSRY		:= SRY->( GetArea() )

Private lImpForm		:= .F.

Begin Sequence

	// Gerar as formulas padroes atraves do programa GpexINI.PRX //
	If ( lImpForm := MsgNoYes(	OemToAnsi(	STR0002 + ;	//"O Sistema ira Atualizar todas os Roteiros Padroes de sua Base de Dados."
											CRLF	+ ;
											If (MV_MODFOL == "2", IIf(cPaisLoc <> "BRA",;
																	 STR0007,; //"Realice un backup (SRY, RGA, SRM, RG5, RG6 y RG9) antes de iniciar el proceso de actualización."
																	 STR0006),; //"Realice un backup (SRY, RGA, SRM) antes de Iniciar el Proceso de Actualizacion."
																	 STR0003) + ; //"Haga un Backup (SRY y SRM) antes de Iniciar el proceso de actualizac."
											CRLF	+ ;
											CRLF	+ ;
											STR0004   ;	//"Confirma a Atualizacao dos Roteiros Padroes?"
					   					 ),;
				 				OemToAnsi( STR0001 ) ;	//"Aten‡„o"
							);
		)
		If MsgNoYes (	OemToAnsi( STR0005 ),;	//"Tem certeza ?"
						OemToAnsi( STR0001 ) ;	//"Aten‡„o"
					)
			MsAguarde( { || Gpea160MDel() } )
		EndIf
	EndIf

	//-- Chamada ao programa de Cadastro de Roteiros
	If MV_MODFOL = '2'
		aRotina := FWLoadMenuDef( 'GPEA161' )
		GPEA161()
	Else
		Gpea160()
	EndIf

End Sequence

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura os Dados de Entrada 											 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
RestArea( aAreaSRY )
RestArea( aArea )

Return( NIL )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gpea160MDel    ³ Autor ³ Leandro Drumond    ³ Data ³ 13/11/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ 									              				³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Gpea160MDel()		                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA160M          		                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Gpea160MDel()

Local cModFol	:= SuperGetMV("MV_MODFOL", .F., "2") //Parámetro para determinar el modelo de la rutina.

SRY->(DbGoTop())
SRY->(DbSetOrder(1)) //RY_FILIAL+RY_CALCULO

While SRY->(!Eof())
	If ( SRY->RY_ORIGEM == "1" )
		SRM->(DbSetOrder(1)) //RM_FILIAL+RM_CALCULO+RM_SEQ+RM_SUBSEQ
		SRM->(DbSeek(SRY->(RY_FILIAL + RY_CALCULO), .F.))
		While SRM->( !Eof() ) .And. (SRM->(RM_FILIAL + RM_CALCULO) == SRY->(RY_FILIAL + RY_CALCULO))
			If (cModFol == "2") // Se Modelo 2 exclui dados da Tabela RGA
				RGA->( DbSeek(SRM->( RM_FILIAL + RM_PROCES + RM_CALCULO ) , .F. ) )
				While RGA->( !Eof() ) .And. (RGA->(RGA_FILIAL + RGA_PROCES + RGA_CALCULO) == SRM->(RM_FILIAL + RM_PROCES + RM_CALCULO))
					If RGA->( RecLock( "RGA" , .F. , .F. ) )
						RGA->( dbDelete() )
						RGA->( MsUnLock() )
					EndIf
					RGA->( DbSkip() )
				EndDo
			EndIf
			
			If SRM->( RecLock( "SRM" , .F. , .F. ) )
				SRM->( dbDelete() )
				SRM->( MsUnLock() )
			EndIf
			SRM->( DbSkip() )
		EndDo

		If SRY->( RecLock( "SRY" , .F. , .F. ) )
			SRY->( dbDelete() )
			SRY->( MsUnLock() )
		EndIf
	EndIf
	SRY->( DbSkip() )
EndDo

If cPaisLoc <> "BRA"
	IIf(FindFunction("GPEXDELRGX"), GpexDelRGX(), Help("", 1, "OBJECTRPO", , STR0008 + "GPEXDELRGX" + "().", 1, 0)) //"Rutina desactualizada, solicite paquete con actualizaciones: "
EndIf

Return( NIL )
