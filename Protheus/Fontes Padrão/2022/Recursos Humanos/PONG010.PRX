#Include 'PONG010.CH'
#Include 'PROTHEUS.CH'
#include "MSGRAPHI.CH"

#DEFINE OPCOES_GRAFICO			15

#DEFINE LINHA 					01
#DEFINE AREA  					02
#DEFINE PONTOS 				03
#DEFINE BARRAS 				04
#DEFINE PIRAMIDE 				05
#DEFINE CILINDRO 				06
#DEFINE BARRAS_HORIZONTAIS 	07
#DEFINE PIRAMIDE_VERTICAL 		08
#DEFINE CILINDRO_HORIZONTAL 	09
#DEFINE PIZZA 					10
#DEFINE FORMA 					11
#DEFINE LINHA_RAPIDA 			12
#DEFINE FLEXAS 				13
#DEFINE GANTT   				14
#DEFINE BOLHAS 				15  
 
#DEFINE POSIDENTIFICACAO		01
#DEFINE POSDADO				02

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Pong01   ³ Autor ³ Mauricio MR           ³ Data ³ 06/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao generica para montagem de EnchoiceBar dos graficos  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cecilia C.  ³21/05/14³TPQAN3³Incluido o fonte da 11 para a 12 e efetua-³±±
±±³            ³        ³      ³da a limpeza.                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function   PonGrfBar(oDlgGraph, nTipo, aDados, aEstrutura, aLegenda, aSerie, cLegenda, cTitulo, cTituloX, cTituloY, aTabela, aPosCol, aPosGrh, aHeaderTab, aColsTab, aNaoDispoe)		

Local aSvKeys		:= GetKeys()    
Local aSize     	:= {}
Local aObjSize  	 := {} 
Local aInfo			:= {}  
Local aGfFullCoords	:= {}
Local aGfMinCoords	:= {}
Local aGdCoords		:= {}

Local aRet
Local aLeg		:= {} 
Local aColors 	:= {}
Local aBmp		:= {}
Local oBar         
  
Local oBtnCfg
Local oBtnDim
Local oBtnSav
Local oBtnPrt
Local oBtnEnv
Local oBtnPrev
Local oBtnNext
Local oBtnLeft
Local oBtnUp
Local oBtnDown
Local oBtnRight
Local oBtnMais
Local oBtnMenos
Local oBtnLeg
Local oBtntabela

Local oGrafFull
Local oGrafMin
Local nSerie	:= 0   
Local nGrafFull	:= 0
Local nGrafMin	:= 0

Local nI		:= 0        
Local bSetF12	:= {||aOpcGraf:=fMontaOpcGraf(aNaoDispoe),	ParamBox( {	{3,STR0026,1,aOpcGraf[3],100,,.F.}},"",@aRet),; //"Tipos de Graficos"
			 			If(Len(aRet) > 0, nTipo := aOpcGraf[1,aRet[1],1],nTipo),;
				 			(oGrafFull:HIDE(),oGrafMin:HIDE(),;
				 			 PonGraf(@oDlgGraph, @oGrafMin , aDados,aEstrutura, nTipo,cTitulo,cTituloX,cTituloY,aLegenda,aSerie,aTabela, aGfMinCoords, aColors, aBmp, @nSerie, 'MIN'),;
				 			 oGrafMin:HIDE(),;
				 			 oGet:oBrowse:HIDE(),;
				 			 PonGraf(@oDlgGraph, @oGrafFull, aDados,aEstrutura, nTipo,cTitulo,cTituloX,cTituloY,aLegenda,aSerie,aTabela, aGfFullCoords, aColors, aBmp, @nSerie, 'FULL'),;
							 lShowGD:= .F. ,;
						     oGet:oBrowse:Setfocus();    
						    );              
		 			}
Local bSetF4	:= {||oDlgGraph:End()}
Local bSetF5	:= {||oGrafFull:l3D := !oGrafFull:l3D,oGrafMin:l3D := !oGrafMin:l3D}
Local bSetF6	:= {||GrafSavBmp( If(lShowGD,@oGrafMin,@oGrafFull) )}
Local bSetF7	:= {||	(Iif(oGrafFull:l3D	,	oGrafFull:ChgRotat( nSerie, 1, .F. ),Nil)),;
						(Iif(oGrafMin:l3D	,	oGrafMin:ChgRotat( nSerie, 1, .F. ),Nil))}
Local bSetF8	:= {||	(Iif(oGrafFull:l3D	,	oGrafFull:ChgRotat( nSerie, 1, .T. ),Nil)),;
						(Iif(oGrafMin:l3D	,	oGrafMin:ChgRotat( nSerie, 1, .T. ),Nil))}
Local bSetF9	:= {||BrwLegenda(cCadastro,cLegenda,aLeg)}
Local bSetF10	:= {||CtbGrafPrint(If(lShowGD,@oGrafMin,@oGrafFull),cTitulo,{ cTitulo },aTabela, .F.,aPosCol,aPosGrh)}
Local bSetF11	:= {||PmsGrafMail(If(lShowGD,@oGrafMin,@oGrafFull)	,cTitulo,{cTitulo },aTabela)}
Local lShowGD	:= .F.

Private aHeader:=aClone(aHeaderTab)
Private aCols  :=aClone(aColsTab)

SetKey(VK_F12,bSetF12)
SetKey(VK_F4 ,bSetF4)
SetKey(VK_F5 ,bSetF5)
SetKey(VK_F6 ,bSetF6)
SetKey(VK_F7 ,bSetF7)
SetKey(VK_F8 ,bSetF8)
SetKey(VK_F9 ,bSetF9)
SetKey(VK_F10,bSetF10)
SetKey(VK_F11,bSetF11)   

// Adiciona as cores padrao
AAdd( aColors,  CLR_HBLUE     )
AAdd( aColors,  CLR_HGREEN    )
AAdd( aColors,  CLR_HRED      )
AAdd( aColors,  CLR_HMAGENTA  )
AAdd( aColors,  CLR_RED       )
AAdd( aColors,  CLR_GRAY      )
AAdd( aColors,  CLR_HCYAN     )
AAdd( aColors,  CLR_YELLOW    )
AAdd( aColors,  CLR_WHITE     )
AAdd( aColors,  CLR_BLACK     )

Aadd( aBmp, "BR_AZUL"		)
Aadd( aBmp, "BR_VERDE"		)
Aadd( aBmp, "BR_VERMELHO"	)
Aadd( aBmp, "BR_PINK"		)
Aadd( aBmp, "BR_MARRON"		)
Aadd( aBmp, "BR_CINZA"		)
Aadd( aBmp, "LIGHTBLU"		)
Aadd( aBmp, "BR_AMARELO"	)
Aadd( aBmp, "BRANCO"		)
Aadd( aBmp, "BR_PRETO"		)

For nI := 1 to Len(aLegenda)
	If nI <= Len(aBmp)
		Aadd(aLeg,{aBmp[nI],aLegenda[nI,2]})
	Else 
		Aadd(aLeg,{aBmp[nI-Len(aBmp)],aLegenda[nI,2]})	
	EndIf
Next nI

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Efetua os calculos de Auto Size                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize    		:= MsAdvSize( .T. ) 
aInfo 			:= { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 0, 0 }

aObjects := {}
aAdd( aObjects , {  95, 095 , .T. , .F. } )
aAdd( aObjects , {  000, 000 , .T. , .T. } )
aObjSize		:= MsObjSize( aInfo, aObjects )

aGfMinCoords	:= { aObjSize[1,1],aObjSize[1,2],aObjSize[1,3]-10,aObjSize[1,4]}

aGdCoords		:= { aObjSize[2,1],aObjSize[2,2],aObjSize[2,3],aObjSize[2,4]}

aObjects 		:= {}      
aObjSize		:= {}
aAdd( aObjects , {  000, 000 , .T. , .T. } )
aObjSize		:= MsObjSize( aInfo, aObjects )
aGfFullCoords	:= { aObjSize[1,1],aObjSize[1,2],aObjSize[1,3]-10,aObjSize[1,4]}

DEFINE MSDIALOG oDlgGraph TITLE cTitulo FROM aSize[7],0 TO aSize[6],aSize[5] PIXEL OF oMainWnd
 

DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD

DEFINE BUTTONBAR oBar SIZE 25,25 3D OF oDlgGraph

DEFINE BUTTON oBtnCfg RESOURCE "AREA"  OF oBar GROUP ;
	ACTION Eval(bSetF12) ;
	TOOLTIP OemToAnsi(STR0025) //"Configura Grafico <F12>"  		

oBtnCfg:cTitle:= OemToAnsi(STR0125)	//"Config"	


DEFINE BUTTON oBtnDim RESOURCE "GRAF3D" OF oBar GROUP ;
	ACTION 	Eval(bSetF5);
	TOOLTIP OemToAnsi(STR0018) //"Dimensao <F5>"
oBtnDim:cTitle:= OemToAnsi(STR0118)	//"Dimensao"	
	
DEFINE BUTTON oBtnSav RESOURCE "SALVAR"  OF oBar GROUP ; 
	ACTION 	Eval(bSetF6);
	TOOLTIP OemToAnsi(STR0019) //"Salva BMP <F6>"    
oBtnSav:cTitle:= OemToAnsi(STR0119)	//"Salvar"	

DEFINE BUTTON oBtnPrt RESOURCE "impressao"  OF oBar GROUP ;
	ACTION Eval(bSetF10);
	TOOLTIP OemToAnsi(STR0023) //"Imprimir grafico <F10>"
oBtnPrt:cTitle:= OemToAnsi(STR0123)	//"Imprimir"	
	
DEFINE BUTTON oBtnEnv RESOURCE "BMPPOST"  OF oBar GROUP ;
	ACTION Eval(bSetF11);
	TOOLTIP OemToAnsi(STR0024) //"Enviar por E-Mail <F11>"   
oBtnEnv:cTitle:= OemToAnsi(STR0124)	//"Enviar"	
	
DEFINE BUTTON oBtnEnv RESOURCE "FINAL" OF oBar GROUP ;
	ACTION Eval(bSetF4);
	TOOLTIP OemToAnsi(STR0017) //"Sair <F4>" 	
oBtnEnv:cTitle:= OemToAnsi(STR0117)	//"Sair"	
	
/* 
//Opcao de rotacao deixou de estar disponivel pelo novo objeto
	DEFINE BUTTON oBtnPrev RESOURCE "PREV"  OF oBar GROUP ;
		ACTION Eval(bSetF7);
		TOOLTIP OemToAnsi(STR0020) //"Rotacao - <F7>" 
	oBtnPrev:cTitle:= OemToAnsi(STR0120)	//"Rot.-"	
	
	DEFINE BUTTON oBtnNext RESOURCE "NEXT"  OF oBar GROUP ;
		ACTION Eval(bSetF8);
		TOOLTIP OemToAnsi(STR0021) //"Rotacao + <F8>"
	oBtnNext:cTitle:= OemToAnsi(STR0121)	//"Rot.+"	
		
//Opcao de movimentacao deixou de estar disponivel pelo novo objeto	                              
	DEFINE BUTTON oBtnLeft RESOURCE "PMSSETAESQ" 	OF oBar GROUP ACTION (oGrafFull:Scroll( GRP_SCRLEFT   , 10 ), 	oGrafMin:Scroll( GRP_SCRLEFT   , 10 )); // Left 10 %
		TOOLTIP OemToAnsi(STR0028) //"Move para Esquerda"
	oBtnLeft:cTitle:= OemToAnsi(STR0128)	//"Esquerda"	
	
	DEFINE BUTTON oBtnUp RESOURCE "PMSSETAUP"  	OF oBar GROUP ACTION (oGrafFull:Scroll( GRP_SCRTOP    , 10 ),	oGrafMin:Scroll( GRP_SCRTOP    , 10 ) ); // Top 10 %
		TOOLTIP OemToAnsi(STR0030) //"Move para Cima"
	oBtnUp:cTitle:= OemToAnsi(STR0130)	//"Cima"	
	
	DEFINE BUTTON oBtnDown RESOURCE "PMSSETADOWN"  	OF oBar GROUP ACTION (oGrafFull:Scroll( GRP_SCRBOTTOM , 10 ), 	oGrafMin:Scroll( GRP_SCRBOTTOM , 10 )); // Bottom 10 %
		TOOLTIP OemToAnsi(STR0031) //"Move para Baixo"
	oBtnDown:cTitle:= OemToAnsi(STR0131)	//"Baixo"	
	
	DEFINE BUTTON oBtnRight RESOURCE "PMSSETADIR"	OF oBar GROUP ACTION (oGrafFull:Scroll( GRP_SCRRIGHT  , 10 ), 	oGrafMin:Scroll( GRP_SCRRIGHT  , 10 )); // Right 10 %
		TOOLTIP OemToAnsi(STR0029) //"Move para Direita"     
	oBtnRight:cTitle:= OemToAnsi(STR0129)	//"Direita"	
*/
	
DEFINE BUTTON oBtnMais RESOURCE "PMSZOOMIN" 	OF oBar GROUP ACTION (oGrafFull:ZoomIn(), oGrafMin:ZoomIn() );
	TOOLTIP OemToAnsi(STR0032) //"Mais Zoom"
oBtnMais:cTitle:= OemToAnsi(STR0132)	//"+Zoom"	

DEFINE BUTTON oBtnMenos RESOURCE "PMSZOOMOUT"	OF oBar GROUP PIXEL ACTION (oGrafFull:ZoomOut(), oGrafMin:ZoomOut() );	 
	TOOLTIP OemToAnsi(STR0033) //"Menos Zoom"
oBtnMenos:cTitle:= OemToAnsi(STR0133)	//"-Zoom"	


DEFINE BUTTON oBtnLeg RESOURCE "BMPVISUAL"  OF oBar GROUP ;
	ACTION Eval(bSetF9);
	TOOLTIP OemToAnsi(STR0022) //"Legenda <F9>"  
oBtnLeg:cTitle:= OemToAnsi(STR0122)	//"Legenda"	

DEFINE BUTTON oBtntabela RESOURCE "BMPTABLE"	OF oBar GROUP PIXEL ACTION (If(!lShowGD,;
 																		(lShowGD	:= .T. , oGrafFull:HIDE(), oGrafMin:SHOW() , oGet:oBrowse:SHOW()   ) ,;
 																		(lShowGD	:= .F. , oGrafMin:HIDE() , oGrafFull:SHOW(), oGet:oBrowse:HIDE()  )	;
 																   ), ;
 																   oGet:oBrowse:Setfocus();  
 															   )	;
TOOLTIP OemToAnsi(STR0027) //"Tabela de Dados"      	
oBtntabela:cTitle:= OemToAnsi(STR0127)	//"Tabela"	

ACTIVATE MSDIALOG oDlgGraph CENTER  ON INIT (	PonGraf(@oDlgGraph, @oGrafMin , aDados,aEstrutura, nTipo,cTitulo,cTituloX,cTituloY,aLegenda,aSerie,aTabela, aGfMinCoords, aColors, aBmp, @nSerie, 'MIN' ),; 	  	  
												oGrafMin:HIDE(),;
												oGet := MsGetDados():New(aGdCoords[1],aGdCoords[2],aGdCoords[3],aGdCoords[4],2,,,"",NIL,NIL,0),;
												oGet:oBrowse:HIDE(),; 
												oGet:oBrowse:SetFocus(),;
												PonGraf(@oDlgGraph, @oGrafFull, aDados,aEstrutura, nTipo,cTitulo,cTituloX,cTituloY,aLegenda,aSerie,aTabela, aGfFullCoords, aColors, aBmp, @nSerie, 'FULL' ),;
												lShowGD:=.F.;
											)
											 	
RestKeys( aSvKeys , .T. ) 
RETURN NIL
   
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PonGraf  ³ Autor ³ Mauricio MR           ³ Data ³ 04/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta Grafico.									          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ RsGraf()						                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA = Dados do Grafico                                    ³±±
±±³          ³ 	   [n][1] Eixo X				                          ³±±
±±³          ³ 	   [n][2] Eixo Y				                          ³±±
±±³          ³ 	   [n][3] Grupo do Eixo X		                          ³±±
±±³          ³ ExpN = Tipo do grafico selecionado                         ³±±
±±³          ³ ExpC1= Titulo do grafico 			                      ³±±
±±³          ³ ExpC2= Titulo do Eixo X			                          ³±±
±±³          ³ ExpC3= Titulo do Eixo Y			                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PONGraf(oDlg, oGraphic, aDados, aEstrutura, nCbx , cTitulo, cTituloX, cTituloY, aLegenda, aSerieDados, aTabela,  aGrafCoords  , aColors, aBmp, nSerie, cPosicao)

Local aSerie	:= {} 
Local aCor		:= {}
Local lRet		:= .T.
Local nI		:= 0
Local nCor		:= 1  

 
@ aGrafCoords[1]+15, aGrafCoords[2] 	MSGRAPHIC oGraphic SIZE aGrafCoords[4], aGrafCoords[3]-20 OF oDlg PIXEL

//-- Guarda a Posicao do Grafico
oDlg:aControls[Len(oDlg:aControls)]:Cargo:= cPosicao
	
oGraphic:SetMargins( 10 ,10, 10, 10 )

//Cria Series
For nI := 1 to Len(aSerieDados)
	nSerie:= oGraphic:CreateSerie(nCbx)
	If nSerie = GRP_CREATE_ERR 
	   MsgStop(STR0016)//"Tipo nao disponivel para este grafico."
	   lRet:= .F.   
	   Exit
	Endif   
	nCor := Iif(nI <= Len(aColors), aColors[nI],aColors[nI-Len(aColors)])
	Aadd(aSerie, {nSerie,nCor})   
Next nI

//Cria a Cor dos itens correspondentes a legenda
For nI := 1 to Len(aLegenda)
	nCor := Iif(nI <= Len(aColors), aColors[nI],aColors[nI-Len(aColors)])
	Aadd(aCor, {aLegenda[nI,1], nCor} )   
Next nI
    
If lRet
	For nI := 1 to Len(aDados)
		nSerie:= Ascan(aSerieDados,{|x| x == aDados[nI,1] } ) 
		nCor  := Ascan(aCor,{|x| x[1] == aDados[nI,1] } )
		// Adiciona dados ao grafico
		oGraphic:Add(aSerie[nSerie,1],aDados[ nI, aEstrutura[POSDADO] ],aDados[ nI, aEstrutura[POSIDENTIFICACAO] ], aSerie[nSerie,2] ) //Cor de cada barra
	Next nI

	oGraphic:SetGradient( GDBOTTOMTOP	, CLR_HGRAY	, CLR_WHITE )
	oGraphic:SetTitle( cTituloY			, ""		, CLR_HRED 	, A_LEFTJUST , GRP_TITLE )
	oGraphic:SetTitle( cTituloX			, ""		, CLR_GREEN	, A_RIGHTJUS , GRP_FOOT  )
	oGraphic:SetLegenProp( GRP_SCRTOP, CLR_YELLOW, GRP_SERIES, .F. ) 
Endif	

Return 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fOpcoesdeGrafico()³ Autor ³ Mauricio MR   		   ³ Data ³ 04/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cria array com as opcoes dos tipos de graficos		    			 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fOpcoesdeGrafico()			                       				     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 								                             			 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                          			         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/    
Static Function fOpcoesdeGrafico()
Local aGraf := {}		

AADD(aGraf,{LINHA				, STR0001} )	//1- "Linha"
AADD(aGraf,{AREA				, STR0002} )	//2- "Area"
AADD(aGraf,{PONTOS				, STR0003} )	//3- "Pontos"
AADD(aGraf,{BARRAS				, STR0004} )	//4- "Barras"
AADD(aGraf,{PIRAMIDE			, STR0005} )	//5- "Piramide"
AADD(aGraf,{CILINDRO			, STR0006} )	//6- "Cilindro"
AADD(aGraf,{BARRAS_HORIZONTAIS	, STR0007} )	//7- "Barras Horiz"
AADD(aGraf,{PIRAMIDE_VERTICAL	, STR0008} )	//8- "Piramide Vertical"
AADD(aGraf,{CILINDRO_HORIZONTAL	, STR0009} )	//9- "Cilindro Horiz"
AADD(aGraf,{PIZZA				, STR0010} )	//10- "Pizza"
AADD(aGraf,{FORMA				, STR0011} )	//11- "Forma"
AADD(aGraf,{LINHA_RAPIDA		, STR0012} )	//12- "Linha Rapida"
AADD(aGraf,{FLEXAS				, STR0013} )	//13- "Flexas"       
AADD(aGraf,{GANTT				, STR0014} )	//14- "GANTT"       
AADD(aGraf,{BOLHAS				, STR0015} ) 	//15- "Bolhas"       

Return aGraf

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fMontaOpcGraf		³ Autor ³ Mauricio MR   		   ³ Data ³ 04/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cria array com as opcoes dos tipos de graficos filtrados  			 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fMontaOpcGraf(aNaoDispoe)	                       				     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 								                             			 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                          			         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/    

Function  fMontaOpcGraf(aNaoDispoe) 

Local aOpcGraf	:= {}
Local aRet		:= {}
Local aDescGraf	:= {}
Local cOpcBox	:= ''
Local nNaoDispoe:= 0
Local nOpc		:= 0
Local nX		:= 0	
Local aNOGRAPH	:= {PIRAMIDE,CILINDRO,BARRAS_HORIZONTAIS,PIRAMIDE_VERTICAL,CILINDRO_HORIZONTAL,;
					FORMA ,LINHA_RAPIDA,FLEXAS ,GANTT, BOLHAS}
Static aOpcPadrao

DEFAULT aNaoDispoe:={}

nNaoDispoe:=Len(aNaoDispoe)        
//Acrescenta as opcoes de grafico nao disponiveis pelo objeto as opcoes que a rotina nao deseja que sejam disponibilizadas
For nX:= 1 to Len(aNaoDispoe)
    If (Ascan(aNOGRAPH,{|nGraf| nGraf == aNaoDispoe[nX] }) == 0)
        AADD(aNOGRAPH,aNaoDispoe[nX])
    Endif
Next nX        

//-- Carrega array com as opcoes Default de graficos
If aOpcPadrao == Nil
   aOpcPadrao:=fOpcoesdeGrafico()
Endif
    
//-- Somente Disponibiliza as Opcoes Desejadas
For nX:=1 TO OPCOES_GRAFICO  

    If !Empty(Ascan(aNOGRAPH,{|x|aOpcPadrao[nX,1] == X}))
       Loop
    Endif
    
    AADD(aOpcGraf,aOpcPadrao[nX])
Next nX

aEval(aOpcGraf,{|x| AADD(aDescGraf,x[2] ), nOpc++, cOpcBox+= STRZERO(x[1],2)+ '=' + X[2] +IIF(nOpc<Len(aOpcGraf),";","")})

AADD(aRet, aOpcGraf)
AADD(aRet, cOpcBox)
AADD(aRet, aClone(aDescGraf))
Return aRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fMontaDiagTab  ³ Autor ³ Mauricio MR           ³ Data ³ 04/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta Grafico.									        		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fMontaDiagTab(o, x, y, cTitulo, aHeader, aCols)          		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ o		-> Objeto		                               		    ³±±
±±³          ³ x		-> Posicao eixo X		                    		    ³±±
±±³          ³ y		-> Posicao eixo Y		              		            ³±±
±±³          ³ cTitulo	-> Titulo da Janela Chamadora                      	  	³±±
±±³          ³ aHeader	-> Cabecalho do Get		                     		    ³±±
±±³          ³ aCols	-> Detalhe do Get	      			          		    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                  		                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fMontaDiagTab(o, x, y, cTitulo, aHeaderTab, aColsTab)
Local aArea				:= GetArea()
Local aKeys			 	:= GetKeys()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para tratamento de dimensao/formatacao do dialogo  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aAdvSize			:= {}
Local aInfoAdvSize		:= {}
Local aObjCoords		:= {} 
Local aGpGdCoords   	:= {} 
Local aGdCoords			:= {} 
Local nOpca				:= 1
Local oDlg
Local oGet   


Private aHeader			:= aClone(aHeaderTab)
Private aCols           := aClone(aColsTab)
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta o Dialogo para Edicao                			   	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
SetaPilha()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdvSize		:= MsAdvSize()
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }   
aAdd( aObjCoords , { 050 , 050 , .T. , .F. } )
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords,.T.,.T. )
aGdCoords		:= { aObjSize[1,1],aObjSize[1,2],aObjSize[1,3],aObjSize[1,4]}


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define os Objetos       				  					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

SetaPilha()
DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD   
//-- Define o Dialogo              Top               Bottom      Direita
DEFINE MSDIALOG oDlg TITLE OemToAnsi(cTitulo) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL 

//-- GetDados 
//oGet 		:= MsGetDados():New(aGdCoords[1],aGdCoords[2],aGdCoords[3],aGdCoords[4],2   	,"LinhaOK"    ,"TudoOK"      ," ",.F.) //,,Nil,,Len(aCols))
oGet    := MSGetDados():New(25, 05, 165, 315                                    ,2    ,"AllWaysTrue","AllWaysTrue"  ,"", .F.,,Nil,Len(acols))
//oGet:oBrowse:lReadOnly:=.T. 

//-- Define o bloco de execucao da confirmacao da rotina
oDlg:bSet15 	:= { || nOpca := 1.00,  oDlg:End()  }
                    
//-- programa a tecla de confirmacao
bSvSet15 		:= SetKey( 15 , oDlg:bSet15 )
//-- Define o bloco de abandono da rotina
oDlg:bSet24 	:= { || nOpca := 0.00 , oDlg:End() }
//-- programa a tecla de saida da rotina
bSvSet24 		:= SetKey( 24 , oDlg:bSet24 )


//-- Ativa o dialogo
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , oDlg:bSet15 , oDlg:bSet24) CENTERED
SetaPilha()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura <Ctrl-O> e <Ctrl-X>               			   	   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
SetKey( 15 , bSvSet15 )
SetKey( 24 , bSvSet24 )
	

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura Teclas de Atalho e Ponteiros de Entrada			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
RestKeys( aKeys )

RestArea( aArea )	
Return Nil

