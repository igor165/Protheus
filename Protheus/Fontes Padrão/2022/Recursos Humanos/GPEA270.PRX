#INCLUDE "GPEA270.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GpeA270  ³ Autor ³ Wagner Xavier         ³ Data ³ 13/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Controle de Acessos                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GpeA270()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA     ³ Programador  ³BOPS       ³Manutencao Efetuada              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³15/05/2014³Cecilia Carv. ³TPPCSB     ³Incluido o fonte da 11 para a 12 ³±± 
±±³          ³              ³           ³e efetuada a limpeza.            ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GpeA270()

Local cMsgAlert	:= ""

PRIVATE aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabe‡alho da tela de atualiza‡”es                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemToAnsi(STR0006) //"Atualiza‡„o de Controle de Processos"
PRIVATE cNomeAlias
Private lGp270Alt := .F.
Private aSaveCpo  := {"", ""}
Private aTabFilter:= LoadTbSpFil(.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse( 6, 1,22,75,"SRW")

Return


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³G270Dele  ³ Autor ³ Wagner Xavier         ³ Data ³ 13/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de exclus„o de Processos                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function G270Dele(cAlias,nReg,nOpc )

Local nOpcA:=0,bCampo,i
Local aAC := { "Abandona","Confirma" }
Local oDlg
Local aPosEnch:={}
Local nCont	:= 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaração de arrays para dimensionar tela		                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aGDCoord		:= {}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aTELA[0][0],aGETS[0]
Private Deleta := .T.
lGp270Alt := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia para processamento dos Gets          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOpcA:=0
dbSelectArea(cAlias)

bCampo := {|nCPO| Field(nCPO) }
FOR i := 1 TO FCount()
	M->&(EVAL(bCampo,i)) := FieldGet(i)
NEXT i

If !SoftLock( cAlias )
	Return
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdvSize		:= MsAdvSize()
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }					 
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords )
aGdCoord	:= { (aObjSize[1,1]+5), (aObjSize[1,2]+5), (((aObjSize[1,3])/100)*75), (((aObjSize[1,4])/100)*55) }

DEFINE MSDIALOG oDlg TITLE cCadastro FROM aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL

nOpcA:=EnChoice(cAlias, nReg, nOpc, ,"AC",OemToAnsi(STR0007),,aObjSize[1])  //"Quanto … exclus„o?"
nOpca := 1

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})

DbSelectArea(cAlias)
If nOpcA == 2
	If (cPaisLoc != "BRA")
		//Grava o log da alteracao
		fLogAccess( M->RW_GRUPO, M->RW_IDUSER, M->RW_ALIAS, M->RW_FILBROW, Date()) 
	EndIf
	Begin Transaction
		dbSelectArea(cAlias)
		RecLock(cAlias,.F.,.T.)
		dbDelete()
	End Transaction
Else
	MsUnlock()
End
dbSelectArea(cAlias)

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³G270Browse³ Autor ³ Cristina Ogura        ³ Data ³ 21/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina que preenche o nome do usuario                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function gp270Nome()

M->RW_USUARIO := UsrRetName( M->RW_IDUSER )
lRefresh:=.T.

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp270Alias³ Autor ³ Cristina Ogura        ³ Data ³ 21/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de montagem da tela com os alias                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gp270Alias()

Local cVar := Alltrim(ReadVar())

// Somente para o campo de Alias do arquivo
If 	cVar == "M->RW_ALIAS"

	// Indica que estou na alteracao das restricoes
	If 	lGp270Alt        
		cNomeAlias := M->RW_ALIAS
	EndIf	

	// Funcao que abre os arquivos do modulo em um listbox
	If  SpFilSelArq(@cNomeAlias,.f.)
		M->RW_ALIAS		:= cNomeAlias
		M->RW_FILBROW	:= Space(Len(SRW->RW_FILBROW))
		M->RW_FILREL	:= Space(Len(SRW->RW_FILREL))
		lRefresh	:= .T.
	EndIf	
EndIf		

Return .T.     

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³G270Browse³ Autor ³ Cristina Ogura        ³ Data ³ 21/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de montagem do filtro do browse e relatorios         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gp270Browse()

Local cVar 			:= Alltrim(ReadVar())
Local cFilBrowse
Local lFilterNew
Local lExpBtn	:= .T.
Local uRet			:= .T.



IF !Empty( cNomeAlias )
	If 	( cVar $ "M->RW_FILBROW*M->RW_FILREL" )
		If ( If(cPaisLoc != "BRA", .T., .F.) .And. ( ( cVar $ "M->RW_FILREL" ) .AND. (M->RW_SPFIL == "1" ) ) )  
			uRet := .F.	
		Else
		
			//Quando nao for Brasil, inibe o botao expressao utilizado no construtor de filtros
			If !( cPaisLoc == "BRA" )
				lExpBtn := .F.
			EndIf
			
			cFilBrowse := AllTrim( &( cVar ) )
			lFilterNew := GpFltBldExp( cNomeAlias , NIL , @cFilBrowse, ,lExpBtn ,Iif(cVar == "M->RW_FILBROW",GetSx3Cache( "RW_FILBROW" , "X3_TAMANHO"  ),GetSx3Cache( "RW_FILREL" , "X3_TAMANHO"  )))
			cFilBrowse := AllTrim( cFilBrowse )
			IF ( ( lFilterNew ) )
				If cVar == "M->RW_FILBROW"
					cFilBrowse		:= Padr( cFilBrowse , GetSx3Cache( "RW_FILBROW" , "X3_TAMANHO"  ) )
					M->RW_FILBROW	:= cFilBrowse
				Else
					cFilBrowse		:= Padr( cFilBrowse , GetSx3Cache( "RW_FILREL" , "X3_TAMANHO"  ) )
					M->RW_FILREL	:= cFilBrowse
				EndIf
				lRefresh:= .T.
			EndIf
		EndIf
	EndIf        
	     
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Grava a ultima restricao criada pelo assistente para ³
//³poder validar e negar possivel digitacao manual.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSaveCpo[1]:=  M->RW_FILBROW  
	If( If( cPaisLoc != "BRA", .T., .F. ) .And. !(M->RW_SPFIL == "1" )  )
		aSaveCpo[2]:=  M->RW_FILREL
	Else             
		aSaveCpo[2]:= ""
	EndIf
EndIF            

Return uRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp270Rot  ³ Autor ³ Cristina Ogura        ³ Data ³ 21/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Trata a rotina de inclusao e alteracao do arotina           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gp270Rot(cAlias,nReg,nOpc)                           

Local aAlter[SRW->(fCount())]
Local aAcho[SRW->(fCount())]    
Local nPos	:=	0                                
Local nCont	:= 	1    

// Indica que estou na alteracao
lGp270Alt := If(nOpc==4,.T.,.F.)
	
// Preencho os arrays de campos do SRW para mostrar na enchoice
SRW->(aFields(aAlter))

// Campo nao alteraveis na AxAltera
aArmCampo	:= {}
Aadd(aArmCampo,"RW_FILIAL")
Aadd(aArmCampo,"RW_PROCESS")
Aadd(aArmCampo,"RW_ALIAS")
Aadd(aArmCampo,"RW_IDUSER")
Aadd(aArmCampo,"RW_GRUPO")

//--Monta o array com os campos do SRW que aparecem na ficha  aAcho := {}
aAcho 	:= aClone(aAlter)
IF ( ( nPos := aScan(aAcho, {|x| aArmCampo[nCont] $ Upper(x)}) ) > 0 )   // campo Filial
	aDel(aAcho, nPos)		
	aSize(aAcho, Len(aAlter)-1)
EndIf

If Inclui
	cNomeAlias := ""	
Else
	cNomeAlias := SRW->RW_ALIAS
EndIf 

If ( cPaisLoc != "BRA" )

	If !Gp270AnulDigit(.T.)
		Return .T.	
	EndIf

EndIf

If nOpc == 4
	//--Adiciona no array aAlter, os campos que poderao ser editados 
	For nCont:= 1 to len(aArmCampo)
		IF ( nPos := aScan(aAlter, {|x| aArmCampo[nCont] $ Upper(x)}) ) > 0
			aDel(aAlter, nPos)		
			aSize(aAlter, Len(aAlter)-1)
		EndIf
	Next             
	AxAltera(SRW->(Alias()),SRW->(Recno()),4,aAcho,aAlter,,,"Gp270TudOk(nOpc)")
ElseIf nOpc ==3
	AxInclui(SRW->(Alias()),SRW->(Recno()),3,aAcho,,aAlter,"Gp270TudOk(nOpc)")
EndIf          

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp270TudOk³ Autor ³ Cristina Ogura        ³ Data ³ 21/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao que verifica os campos quando Ok da enchoice         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gp270TudOk(nOpc)

Local lRet := .T.

If 	(M->RW_IDUSER=Space(Len(SRW->RW_IDUSER))) .And.;
	(M->RW_GRUPO=Space(Len(SRW->RW_GRUPO)))
	Help("",1,"Gp270Ver")		// Verifique os campos de usuario ou grupo deve estar preeenchido
	lRet := .F.
EndIf		        

// Somente quando for inclusao
If nOpc == 3 .AND. lRet
	SRW->(dbSetOrder( RetOrder("RW_FILIAL+RW_PROCESS+RW_ALIAS+RW_GRUPO+RW_IDUSER") ))
	If SRW->( dbSeek(xFilial("SRW")+M->RW_PROCESS+M->RW_ALIAS+M->RW_GRUPO+M->RW_IDUSER) )
		Help("",1,"JAGRAVADO")
		lRet := .F.
	EndIF	
EndIf

If lRet 
	If ( cPaisLoc != "BRA" .and. (lRet := fValSPFil() .AND. gp270AnulDigit(.F.)) )
		//Grava o log da alteracao
		fLogAccess( M->RW_GRUPO, M->RW_IDUSER, M->RW_ALIAS, M->RW_FILBROW, Date())
	EndIf
EndIf	
Return lRet
    
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEA270   ºAutor  ³Valdeci Lira        º Data ³  10/03/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Anula o que o usuario digitar manualmente no campo FILBROW  º±±
±±º          ³e campo FILREL, permitindo somente o que foi digitado pelo  º±±
±±º          ³assistente e informa sobre inconsistencias de cadastro      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Gp270AnulDigit(lOpenScreen)      

Local lIsSpFil		:= .F.
Local cProcess		:= ""
Local cFilRel		:= ""
Local cMsg			:= ""
Local cDescFilBrow	:= ""  
Local lEraseF		:= .F.
Local lErr			:= .F.
Local lRet			:= .T.
	
Default lOpenScreen := .F.	

If cPaisLoc == "BRA" 
	Return .T.
EndIf
IF !Empty( cNomeAlias )
	//lIsSpFil := IsSpFil( cNomeAlias )
	lIsSpFil := Type("M->RW_SPFIL") != "U" .AND. (M->RW_SPFIL == "1")
			
	If( lIsSpFil )          
		//Se for uma tabela de SuperFiltro entao 
		//o processo não deve ser preenchido.
		If Type("M->RW_PROCESS") != "U"
			cProcess := M->RW_PROCESS
		ElseIf(Type("RW_PROCESS") != "U")
			If!(Inclui)
				cProcess := RW_PROCESS
			endif
		EndIf 
		If Type("M->RW_FILREL") != "U"
			cFilRel := M->RW_FILREL
		ElseIf(Type("RW_FILREL") != "U")
			If!(Inclui)
				cFilRel := RW_FILREL
			EndIf
		EndIf
		
		If (!cFilRel = Space(FwGetTamFilial) .OR. !Empty(cProcess))                           				
			lEraseF := .T.
			 			   
			If!Empty(cMsg)	                    					
				cMsg += CRLF
			EndIf 
			
			aArea := getArea()
			SX3->(dbSetOrder(2))
			If SX3->(dbSeek("RW_FILBROW"))
				cDescFilBrow := X3Titulo()					
			EndIf
			restArea( aArea )
			
			cMsg+= OemToAnsi(STR0012) + CRLF //"->Para esta tabela somente a restrição do campo" 
			cMsg+= cDescFilBrow + OemToAnsi(STR0013)//" é considerado."
			lErr:= .T.
		EndIf                    
		
		If (!Empty(cMsg))
			cMsg := OemToAnsi(STR0014) + CRLF + CRLF + cMsg //"Informações inconsistentes encontradas:"
			If lEraseF 
				cMsg += CRLF + OemToAnsi(STR0015) //"->Os erros serão corrigidos em tela."				
			End if	  
			
			//Informa o erro ao usuario. Superfiltro + (Processo ou Filtro de relat) nao pode
			MsgAlert(cMsg, OemToAnsi(STR0009))
			
			If (lEraseF)
				If Type("M->RW_PROCESS") != "U"
					M->RW_PROCESS := ""
				ElseIf(Type("RW_PROCESS") != "U")
					If!(Inclui)
						RW_PROCESS	:= ""
					endif
				EndIf 
				If Type("M->RW_FILREL") != "U"
					M->RW_FILREL := ""
				ElseIf(Type("RW_FILREL") != "U")
					If!(Inclui)
						RW_FILREL	:= ""
					endif
				EndIf 					
			end if    
			lErr:= .T.
		EndIf
	EndIf 
	          
	//Verifica se o usuario digitou alguma validacao manualmente                 
	If( Type("M->RW_FILBROW")!= "U" .AND. Type("aSaveCpo[1]")!= "U")
		If !(AllTrim(M->RW_FILBROW) == AllTrim(aSaveCpo[1]))
			lErr := .T.
			M->RW_FILBROW := aSaveCpo[1]
			msgInfo(OemToAnsi(STR0008), OemToAnsi(STR0009) )
		EndIf
	End If
	If( Type("M->RW_FILREL")!= "U" .AND. Type("aSaveCpo[2]")!= "U")
		If !(AllTrim(M->RW_FILREL) == AllTrim(aSaveCpo[2]) )
			lErr := .T.
			M->RW_FILREL := aSaveCpo[2]                         
			MsgInfo(OemToAnsi(STR0008), OemToAnsi(STR0009) )
		End If
	End If 
EndIf

If !lOpenScreen .AND. !lErr
	lRet := .T.	
Else 
	If lIsSpFil
		lRet := !lErr
	Else
		lRet := .T.	
	EndIf
EndIf
	
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEA270   ºAutor  ³Microsiga           º Data ³  03/16/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se e uma tabela de Super filtro com base no       º±±
±±º          ³ array de tabelas configurado                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function IsSpFil(cNomeAlias, lShowMsg)                             
	
Local aTabs := aClone(aTabFilter)
Local lRet	:= .T.

Default cNomeAlias := Alias()
Default lShowMsg   := .F.

If ( !Empty(cNomeAlias) )
	lRet := ( aScan( aTabs, {|x| x[1] == cNomeAlias}) > 0 )
EndIf
	
If ( FieldPos("RW_SPFIL") > 0 )
	If (Inclui)
		If ( M->RW_SPFIL == "1" .and. !lRet)
			lRet := .F.
		Else		
			lRet := .T.
		EndIf
	EndIf
EndIf

If( !lRet .and. lShowMsg )	
	MsgAlert( OemToAnsi(STR0020) , OemToAnsi(STR0009))//"Tabela não válida para restricao"##"Atencao"
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEA270   ºAutor  ³Microsiga           º Data ³  03/17/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function SpFilSelArq( cNomeAlias , lBranco , aLbxA , lShowDialog )

Local aArea  	:= getArea()
Local cPesq   	:= IF( !Empty( cNomeAlias ) , Padr( cNomeAlias , 20 ) , Space( 20 ) )
Local lOK     	:= .F.
Local lSPFil	:= .F.
Local nPosLbxA	:= 0.00
Local oPesq		:= NIL
Local oDlg		:= NIL
Local oLbxA		:= NIL
Local bSet15
Local bSet24

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaração de arrays para dimensionar tela		                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aGDCoord		:= {}

DEFAULT lBranco		:= .F.
DEFAULT aLbxA		:= {}
DEFAULT lShowDialog	:= .T.

If ( cPaisLoc != "BRA" )
	//If ( (M->RW_SPFIL == "1") .and. (Empty(M->RW_PROCESS)) )
		lSPFil := .T.
	//EndIf
EndIf

If ( lSPFil )

	aLbxA := aClone(aTabFilter)

	IF ( lBranco )
		aAdd( aLbxA , { Space( 3 ) , Space( 30 ) } )
	EndIF	
	
	IF ( lShowDialog )
	    
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Monta as Dimensoes dos Objetos         					   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		aAdvSize		:= MsAdvSize()
		aAdvSize[5]	:=	(aAdvSize[5]/100) * 60
		aAdvSize[6]	:=  (aAdvSize[6]/100) * 65
		aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }					 
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
		aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords )
		aGdCoord	:= { (aObjSize[1,1]+5), (aObjSize[1,2]+5), (((aObjSize[1,3])/100)*40), (((aObjSize[1,4])/100)*55) }
		
		DEFINE MSDIALOG oDlg FROM aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5]  TITLE OemToAnsi(STR0017) PIXEL		// "Selecione o Arquivo" 150,5,358,361
		    bSet15 :=  {|| lOk := .T., nPosLbxA:=oLbxA:nAt,oDlg:End()}
		    bSet24 :=  {|| lOk := .F., oDlg:End()}
		    
			@ aGdCoord[1],aGdCoord[2]+7 LISTBOX oLbxA FIELDS HEADER "" , OemToAnsi(STR0018) SIZE aGdCoord[4], aGdCoord[3] ;	// "Bases de Dados"  165,063
			      OF oDlg PIXEL ON DBLCLICK ( lOk := .T., nPosLbxA:=oLbxA:nAt,oDlg:End() )
			
			oLbxA:SetArray(aLbxA)
			oLbxA:bLine := { || {aLbxA[oLbxA:nAt,1],aLbxA[oLbxA:nAt,2]}}
			
			@ aGdCoord[3]+20,aGdCoord[2]+10 SAY OemToAnsi(STR0016) SIZE 25, 7 OF oDlg PIXEL //"Pesquisar:"
			@ aGdCoord[3]+20,aGdCoord[2]+40 MSGET oPesq VAR cPesq  SIZE 80,10 OF oDlg PIXEL PICTURE "@!" VALID ExistInList( oLbxA , aLbxA , cPesq )

		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 , Nil , Nil ) CENTERED
		
		If ( lOk )
			cNomeAlias := aLbxA[nPosLbxA,1]
		EndIf
	
	EndIF	

Else
	If FSeleArq(@cNomeAlias,.F.)
		lOk := .T.
	EndIf	
EndIf

RestArea( aArea )

Return( lOk )  
    
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEA270   ºAutor  ³Microsiga           º Data ³  03/20/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega as tabelas do SPFiltro em um array                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function LoadTbSpFil(lName)      

Local aTabs  	:= {} 

Default lName 	:= .F.

If lName
	If AliasInDic("SRA")
		aAdd( aTabs, {"SRA", getSx2Name("SRA")} )
	EndIf               
	If AliasInDic("SRC")
		aAdd( aTabs, {"SRC", getSx2Name("SRC")} )
	EndIf               
	If AliasInDic("SRD")
		aAdd( aTabs, {"SRD", getSx2Name("SRD")} )
	EndIf               
	If AliasInDic("SRY")
		aAdd( aTabs, {"SRY", getSx2Name("SRY")} )
	EndIf               
	If AliasInDic("SRV")
		aAdd( aTabs, {"SRV", getSx2Name("SRV")} )
	EndIf               
	If AliasInDic("SQB")
		aAdd( aTabs, {"SQB", getSx2Name("SQB")} )
	EndIf               
	If AliasInDic("CTT")
		aAdd( aTabs, {"CTT", getSx2Name("CTT")} )
	EndIf               	
	If AliasInDic("RCH")		
		aAdd( aTabs, {"RCH", getSx2Name("RCH")} )
	EndIf               
	If AliasInDic("RCJ")
		aAdd( aTabs, {"RCJ", getSx2Name("RCJ")} )
	EndIf               
	If AliasInDic("SI3")
		aAdd( aTabs, {"SI3", getSx2Name("SI3")} )    		
	EndIf               
	If AliasInDic("RCO")
		aAdd( aTabs, {"RCO", getSx2Name("RCO")} )    		
	EndIf               
	If AliasInDic("RGC")
		aAdd( aTabs, {"RGC", getSx2Name("RGC")} )    						
	EndIf               
	If AliasInDic("RGB")
		aAdd( aTabs, {"RGB", getSx2Name("RGB")} )
	EndIf               
	If AliasInDic("RCP")
		aAdd( aTabs, {"RCP", getSx2Name("RCP")} )	
	EndIf               
	If AliasInDic("RCQ")
		aAdd( aTabs, {"RCQ", getSx2Name("RCQ")} )	
	EndIf               
	If AliasInDic("SR3")
		aAdd( aTabs, {"SR3", getSx2Name("SR3")} )
	EndIf               
	If AliasInDic("SR7")
		aAdd( aTabs, {"SR7", getSx2Name("SR7")} )	
	EndIf               
	If AliasInDic("RG7")
		aAdd( aTabs, {"RG7", getSx2Name("RG7")} )
	EndIf               
Else			
	If AliasInDic("SRA")
		aAdd( aTabs, "SRA" )
	EndIf
	If AliasInDic("SRC")
		aAdd( aTabs, "SRC" )
	EndIf
	If AliasInDic("SRD")
		aAdd( aTabs, "SRD" )
	EndIf
	If AliasInDic("SRY")
		aAdd( aTabs, "SRY" )
	EndIf
	If AliasInDic("SRV")
		aAdd( aTabs, "SRV" )
	EndIf
	If AliasInDic("SQB")
		aAdd( aTabs, "SQB" )
	EndIf
	If AliasInDic("CTT")
		aAdd( aTabs, "CTT" )
	EndIf
	If AliasInDic("RCH")
		aAdd( aTabs, "RCH" )
	EndIf
	If AliasInDic("RCJ")
		aAdd( aTabs, "RCJ" )
	EndIf
	If AliasInDic("SI3")
		aAdd( aTabs, "SI3" ) 				
	EndIf
	If AliasInDic("RCO")
		aAdd( aTabs, "RCO" )
	EndIf
	If AliasInDic("RGC")
		aAdd( aTabs, "RGC" )
	EndIf
	If AliasInDic("RGB")
		aAdd( aTabs, "RGB" )
	EndIf
	If AliasInDic("RCP")
		aAdd( aTabs, "RCP" )
	EndIf
	If AliasInDic("RCQ")
		aAdd( aTabs, "RCQ" )
	EndIf
	If AliasInDic("SR3")
		aAdd( aTabs, "SR3" )
	EndIf
	If AliasInDic("SR7")
		aAdd( aTabs, "SR7" )
	EndIf
	If AliasInDic("RG7")
		aAdd( aTabs, "RG7" )
	EndIf
EndIf			
	
Return aTabs
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEA270   ºAutor  ³Valdeci Lira        º Data ³  10/03/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna o nome da tabela no SX2 de acordo com a traducao    º±±
±±º          ³para a linguagem configurada                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function GetSx2Name(cAlias)

Local aArea := getArea()
Local cRet	:= ""

SX2->(dbSetOrder(1))
If SX2->(dbSeek(cAlias))
	cRet := AllTrim(X2Nome())
EndIf
RestArea( aArea )

Return cRet

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³ExistInList	  ³Autor³ Valdeci Lira		  ³ Data ³17/03/2009³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Efetua a Pesquisa do Alias                                  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais> 									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais> 									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³fSeleArq()    												³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function ExistInList( oListBox , aListBox , cAliasPesq  )

Local bAscan	:= { || NIL }
Local lValidArq := .T.
Local nPosArq	:= 0.00

DEFAULT aListBox	:= {}	
DEFAULT cAliasPesq	:= ""

IF ( ValType( oListBox ) == "O" )
	IF !Empty( cAliasPesq )
		bAscan := { |z| AllTrim( cAliasPesq ) $ z[1] .or. AllTrim( cAliasPesq ) $ z[2] }
		IF ( lValidArq := ( nPosArq := aScan( aListBox , bAscan ) ) > 0.00 )
			oListBox:nAt := nPosArq
		Else
			MsgStop( OemtoAnsi( STR0019 ) ) //"Arquivo nao encontrado"
		EndIF
		oListBox:Refresh()
	EndIF	
EndIF
	
Return( lValidArq )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³fValSPFil ³Autor³Igor Franzoi	            ³Data³09/04/2009³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Pesquisa no SX3												³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³GPEA270														³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function fValSPFil()

Local lRet := .T.

If ( !Empty(M->RW_PROCESS) .and. (M->RW_SPFIL == "1" ) )
	MsgAlert( OemToAnsi(STR0025), OemToAnsi(STR0009) ) //"SuperFiltro não pode ser configurado por Rotina/Processo ou Alias Informado!" - Atenção!
	lRet := .F.
ElseIf ( Empty(M->RW_PROCESS) .and. (M->RW_SPFIL == "1" ) .and. !( aScan( aTabFilter, { |x| x[1] == cNomeAlias } ) > 0 ) )
	If ( !Empty(M->RW_ALIAS) )
		MsgAlert( OemToAnsi(STR0025), OemToAnsi(STR0009) ) //"SuperFiltro não pode ser configurado por Rotina/Processo ou Alias Informado!" - Atenção!
		lRet := .F.
	EndIf
EndIf

If (lRet)
	lRet := IsSpFil(cNomeAlias, .T.)
EndIf

Return (lRet)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³fValPrcSPFil³Autor³Igor Franzoi	            ³Data³09/04/2009³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Pesquisa no SX3												³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³GPEA270														³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function fValPrcSPFil()

Local lRet := .T.

If ( !Empty(M->RW_PROCESS) .and. (M->RW_SPFIL == "1" ) )
	MsgAlert( OemToAnsi(STR0025), OemToAnsi(STR0009) ) //"SuperFiltro não pode ser configurado por Rotina/Processo ou Alias Informado!" - Atenção!
	lRet := .F.
ElseIf ( Empty(M->RW_PROCESS) .and. (M->RW_SPFIL == "2" ) .and. !( aScan( aTabFilter, { |x| x[1] == M->RW_ALIAS } ) > 0 ) )
	If ( !Empty(M->RW_ALIAS) )
		MsgAlert( OemToAnsi(STR0025), OemToAnsi(STR0009) ) //"SuperFiltro não pode ser configurado por Rotina/Processo ou Alias Informado!" - Atenção!
		lRet := .F.
	EndIf
EndIf

Return (lRet)

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³fLogAccess³ Autor ³Igor Franzoi           ³ Data ³23/01/2009³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Grava log de restricoes                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fLogAccess													³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            |                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³ fLogAccess                                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fLogAccess( cGrupo, cUsuario, cAlias, cCond, dData )      

Local aArea		:= getArea()
Local lRet      := .T.
Local cRdd      := "DBFCDX"
Local cTxtAlias := "LogCAcsGpe" 
Local cOperacao	:= ""
Local aFields   := {}

Begin Sequence               

	//Se nao existir o arquivo, monta a estrutura e cria o arquivo
	If !( MsFile( cTxtAlias + GetDbExtension() , NIL , cRdd ) )
		AADD(aFields, { "GRUPO"		,"C", Len(cGrupo)		, 0 } )
		AADD(aFields, { "USUARIO"	,"C", Len(__cUserID) 	, 0 } )        
        AADD(aFields, { "ALIAS"		,"C", 3              	, 0 } )    
        AADD(aFields, { "CONDI"		,"C", 250				, 0 } )    
        AADD(aFields, { "DATAUPD"	,"D", 8 				, 0 } )
        AADD(aFields, { "HORA"		,"C", 8 				, 0 } )
        AADD(aFields, { "OPERACAO"	,"C", 7 				, 0 } )
		If !( lRet := MsCreate( cTxtAlias , aFields , cRdd ) )
			Break                                       
		EndIf 
	EndIf

	If !( lRet := MsOpenDbf( .T. , cRdd , cTxtAlias , cTxtAlias , .T. , .F. , .F. , .F. ) )
		Break
	EndIf

	If !( lRet := ( Select( cTxtAlias ) > 0 ) )
		Break
    EndIf
    
	If (Inclui)
		cOperacao := "INCLUIR"
	ElseIf(Altera)	
		cOperacao := "ALTERAR"
	ElseIf(Deleta)
		cOperacao := "DELETAR"
	EndIf
	
	RecLock(cTxtAlias,.T.)
		(cTxtAlias)->(GRUPO)   := cGrupo
		(cTxtAlias)->(USUARIO)	:= cUsuario
		(cTxtAlias)->(ALIAS)  	:= cAlias
		(cTxtAlias)->(CONDI)  	:= cCond
		(cTxtAlias)->(DATAUPD)	:= dData
		(cTxtAlias)->(HORA)		:= Time()
		(cTxtAlias)->(OPERACAO)	:= cOperacao
	(cTxtAlias)->(MsUnLock())

	(cTxtAlias)->(dbCloseArea())

End Sequence

RestArea(aArea)

Return (lRet)


/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ MenuDef		³Autor³  Luiz Gustavo     ³ Data ³07/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Isola opcoes de menu para que as opcoes da rotina possam    ³
³          ³ser lidas pelas bibliotecas Framework da Versao 9.12 .      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA270                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/   

Static Function MenuDef()

Local aRotina :={{OemToAnsi(STR0001),"AxPesqui", 0 , 1,,.F.}	,;	// "Pesquisar"
					{OemToAnsi(STR0002),"AxVisual", 0 , 2} 		,; 	// "Visualizar"
					{OemToAnsi(STR0003),"Gp270Rot", 0 , 3} 		,; 	// "Incluir"
					{OemToAnsi(STR0004),"Gp270Rot", 0 , 4} 		,; 	// "Alterar"
					{OemToAnsi(STR0005),"G270Dele" , 0, 5,3}	}  	// "Excluir"
					
Return aRotina
