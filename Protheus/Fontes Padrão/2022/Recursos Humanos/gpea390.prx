#INCLUDE "GPEA390.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH" 

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠здддддддддддддбддддддддддддбдддддддбддддддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    	Ё GPEA390    Ё Autor Ё Mauricio Takakura     	        Ё Data Ё 10/11/10 Ё╠╠
╠╠цдддддддддддддеддддддддддддадддддддаддддддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o 	Ё Cadastro de Registro Patronal em MVC                         			 Ё╠╠
╠╠цдддддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   	Ё GPEA390()                                                    			Ё╠╠
╠╠цдддддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      	Ё Generico ( DOS e Windows )                                   			Ё╠╠
╠╠цдддддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.               			    Ё╠╠
╠╠цдддддддддддддбддддддддддбддддддддддддддддбддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador  Ё Data     Ё FNC			    Ё  Motivo da Alteracao                      Ё╠╠
╠╠цдддддддддддддеддддддддддеддддддддддддддддеддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠|Mohanad Odeh Ё13/03/2013ЁM12RH01 RQ3004  ЁUnificacao dos fontes V12                  Ё╠╠
╠╠|             Ё          Ё                ЁRemovida validacoes de dicionario da funcaoЁ╠╠ 
╠╠Ё             Ё          Ё                Ё ValidDic()                                Ё╠╠ 
╠╠|CМcero Alves Ё03/03/2015Ё                ЁRemoГЦo da funГЦo AjustaSx1                Ё╠╠
╠╠|             Ё          Ё                Ёpara a versЦo 12   						Ё╠╠ 
╠╠ЁJonathan GlezЁ10/01/2016ЁPCREQ-7944      ЁLocalizacion GPE de Chile p/v12.           Ё╠╠
╠╠Ё             Ё          Ё                Ё-Se agrega la funcion EXISTRCO y se realizaЁ╠╠
╠╠Ё             Ё          Ё                Ёajustes en el diccionario.                 Ё╠╠
╠╠юдддддддддддддаддддддддддаддддддддддддддддаддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function GPEA390()
	Local oMBrowse
	Local cFiltraRh

	// Realiza Validacao de Dicionario para atualizacao via UPDATE
	If !ValidDic()
		Return
	EndIf  

	oMBrowse := FWMBrowse():New()

	oMBrowse:SetAlias("RCO")
	oMBrowse:SetDescription(OemToAnsi(STR0006)) //'Cadastro de Registro Patronal'
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa o filtro utilizando a funcao FilBrowse                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	cFiltraRh := CHKRH("GPEA390","RCO","1")
	oMBrowse:SetFilterDefault( cFiltraRh )
	
	oMBrowse:Activate()
	
Return

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Mauricio TakakuraЁ Data Ё10/11/2010Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCriacao do Menu do Browse.                                  Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGPEA390                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function MenuDef()

	Local aRotina := {}

	ADD OPTION aRotina TITLE STR0001  ACTION "PESQBRW"         		OPERATION 1 ACCESS 0 DISABLE MENU 	//'Pesquisar'
	ADD OPTION aRotina TITLE STR0002  ACTION "VIEWDEF.GPEA390" 		OPERATION 2 ACCESS 0 				//'Visualizar'
	ADD OPTION aRotina TITLE STR0003  ACTION "VIEWDEF.GPEA390" 		OPERATION 3 ACCESS 0				//'Incluir'
	ADD OPTION aRotina TITLE STR0004  ACTION "VIEWDEF.GPEA390" 		OPERATION 4 ACCESS 0				//'Alterar'
	ADD OPTION aRotina TITLE STR0005  ACTION "VIEWDEF.GPEA390" 		OPERATION 5 ACCESS 0				//'Excluir'

Return aRotina

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё ModelDef 		ЁAutorЁ  Mauricio TakakuraЁ Data Ё10/11/2010Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRegras de Modelagem da gravacao.                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGPEA390                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function ModelDef()

	Local oModel
	Local oStructRCO

	// Criacao do Objeto de Modelagem de dados da tabela SRJ //	
	oModel     := MPFormModel():New("GPEA390", NIL, NIL)
	oModel:SetDescription( OemToAnsi(STR0006) ) //'Cadastro de Registro Patronal"
	
	// Estrutura de campos do Model //
	oStructRCO := FWFormStruct(1, "RCO")
	oStructRCO:RemoveField("RCO_FILIAL")

	oModel:AddFields("GPEA390_RCO", NIL, oStructRCO)
	oModel:SetPrimaryKey({"RCO_CODIGO"})

	oModel:GetModel( "GPEA390_RCO" ):SetDescription( OemToAnsi(STR0006) ) //'Cadastro de Registro Patronal'

Return( oModel )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё ViewDef  		ЁAutorЁ  Mauricio TakakuraЁ Data Ё10/11/2010Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRegras de Interface com o Usuario                           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGPEA390                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function ViewDef()
	Local oView 
	Local oModel
	Local oStructRCO
	                           
	// Criacao da Interface //
	oView := FWFormView():New()

	// Vincular o View ao Model //
	oModel := FWLoadModel("GPEA390")
	oStructRCO := FWFormStruct(2, "RCO")

	oStructRCO:RemoveField("RCO_FILIAL")

	oView:SetModel(oModel)
	oView:AddField("GPEA390_RCO", oStructRCO)

	oView:CreateHorizontalBox("FIELDS", 100)

	oView:SetOwnerView("GPEA390_RCO", "FIELDS")

Return oView

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё ValidDic  		ЁAutorЁ  Mauricio TakakuraЁ Data Ё24/05/2011Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁValidacao de Dicionarios Atualizados por UPDATE             Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGPEA390                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function ValidDic()
Local lRet := .T.

If cPaisLoc = "PTG"
	DbSelectArea("RCO")
	IF !( RCO->( FieldPos( "RCO_CDPAIS" ) ) > 0 )  //Verificar se o campo existe, caso nЦo exista nЦo foi executado o update
		Aviso(OemToAnsi(STR0007), OemToAnsi(STR0010), {"OK"})	//"ATENгцO!"##"Antes de prosseguir, И necessАrio executar a atualizaГЦo 'CАlculo de Ajuda de Custo - Portugal', disponМvel para o mСdulo SIGAGPE no compatibilizador RHUPDMOD."
		lRet := .F.
	Endif
Endif

Return(lRet)  

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё ExistRCO		ЁAutorЁ  Mayra Camargo    Ё Data Ё27/05/2015Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁValidaciСn en RCCO                                          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁExistRCO(cExp01,cExp02)						            	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGPEA390                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁlRet											    		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcExp01= Concepto											Ё
ЁParametrosЁcExp02= Codigo de Entidad   								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/*/
Function ExistRCO(cConcepto,cEntidad)

	Local lRet 	:= .F.
	Local aArea	:= getArea()
	Local cTipo	:= "6"

	dbSelectArea("SRV")
	SRV->(dbSetOrder(1))

	If SRV->(dbSeek(xfilial("SRV")+cConcepto))
		IF !Empty(SRV->RV_TPOINS)
			cTipo := SRV->RV_TPOINS
		Else
			cTipo := "6"
		EndIf
		dbSelectArea("RCO")
		RCO->(dbSetOrder(4))

		lRet := RCO->(dbSeek(XFILIAL("RCO")+cTipo+cEntidad))//ExistCpo("RCO",cTipo+cEntidad,4)
	EndIf

	If !lRet
		Help( ,, OemToAnsi(STR0011),, OemToAnsi(STR0012),1, 0 )
	EndIF

	RestArea(aArea)

Return lRet
