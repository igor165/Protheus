#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEA1240.CH"

#DEFINE STR0999 "Enter the name of the employee (maximum - 96 characters, including spaces)"

/*
{Protheus.doc} GPEA240RUS
    Absence Register File (Russia)

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Function GPEA240RUS()
Return GPEA240()

/*
{Protheus.doc} BrowseDef
    Browse definition

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Static Function BrowseDef()
Return FWLoadBrw("GPEA240")

/*
{Protheus.doc} MenuDef
    Menu definition

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Static Function MenuDef()
Return FWLoadMenuDef("GPEA240")

/*
{Protheus.doc} ModelDef
    Model definition

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Static Function ModelDef()
    Local oModel As Object
    Local oStructSRA As Object
    Local cAField As Character

    cAField := 'RA_NOMECMP'
    oModel := FwLoadModel("GPEA240")
    oStructSRA := oModel:GetModelStruct("GPEA240_SRA")[3]:oFormModelStruct

    oStructSRA:Deactivate()
    oStructSRA:RemoveField("RA_NOME")

    oStructSRA:AddField(             ; 
        AllTrim(OemToAnsi(STR0012)), ;
        AllTrim(OemToAnsi(STR0128)), ;
        'RA_NOMECMP', ; 
        GetSx3Cache(cAField, "X3_TIPO"), ; 
        GetSx3Cache(cAField, "X3_TAMANHO"), ;
        GetSx3Cache(cAField, "X3_DECIMAL"), ; 
        NIL, ; 
        NIL, ;
        NIL, ; 
        .T., ;
        NIL, ;
        NIL, ;
        NIL, ;
        .F.  ;
    )

    oModel:GetModel("GPEA240_SRA"):SetOnlyView( .T. )
    oModel:GetModel("GPEA240_SRA"):SetOnlyQuery( .T. )
    oStructSRA:Activate()
Return oModel

/*
{Protheus.doc} ViewDef
    View definition

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Static Function ViewDef()
    Local oView As Object
    Local cAField As Character

    cAField := 'RA_NOMECMP'
    oView := FwLoadView("GPEA240")

    oView:GetViewStruct("GPEA240_SRA"):RemoveField("RA_NOME")

    oView:GetViewStruct("GPEA240_SRA"):AddField(;
        'RA_NOMECMP', ;
        GetSx3Cache(cAField, "X3_ORDEM"), ;
        OemToAnsi(STR0012), ;
        OemToAnsi(STR0128), ;
        {OemToAnsi(STR0999)}, ;
        GetSx3Cache(cAField, "X3_TIPO"), ;
        GetSx3Cache(cAField, "X3_PICTURE"), ;
        NIL, ;
        NIL, ;
        .F., ;
        NIL, ; 
        NIL, ; 
        NIL, ; 
        NIL, ; 
        NIL, ; 
        .F., ; 
        NIL  ; 
    )

Return oView
