#INCLUDE "PROTHEUS.CH"
#INCLUDE "DBTREE.CH"
#INCLUDE "CNTA240.CH"

#DEFINE FLG_RORI "O" //Flag de controle dos registros originais
#DEFINE FLG_RDEL "D" //Flag de controle dos registros deletados
#DEFINE FLG_RADD "A" //Flag de controle dos registros adicionados

#DEFINE FLG_ARVU "U" //Chave identificacao da arvore de usuarios
#DEFINE FLG_ARVG "G" //Chave identificacao da arvore de grupos
#DEFINE FLG_ARVC "C" //Chave identificacao da arvore do contrato
#DEFINE FLG_ARVF "F" //Chave identificacao da arvore do contrato

#DEFINE DEF_TRAALT "039" //Atualizacao do contrato
#DEFINE DEF_CTRACS "041" //Controle de Acessos

#DEFINE DEF_TRANS "001" //Controle Total

#DEFINE TOTALTRAN 37 //Numero total de transacoes

Static lValida := .F. //Valida o acesso do usuАrio quando ExecAuto

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠                           ;
╠╠ЁFun┤ao    ЁCN240AcessoЁ Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Configura o acesso dos usuario as transacoes de contrato    Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё CNTA100                                                     Ё╠╠
╠╠цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.              Ё╠╠
╠╠цддддддддддддбддддддддбддддддбддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                      Ё╠╠
╠╠цддддддддддддеддддддддеддддддеддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                           Ё╠╠
╠╠юддддддддддддаддддддддаддддддаддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240Acesso()
Local cCadastro:= STR0001//"Controle de Acesso"
Local lContinua:= .T.
Local cContra  := CN9->CN9_NUMERO//Contrato Atual
Local aUsrs    := {}
Local aUsers   := {} //Usuarios
Local aGrps    := {} //Grupos
Local aTrans   := {} //Listagem

Local aInc		:= {} // Filiais inclusas no Tree
Local aExc		:= {} // Filiais retidas do Tree

Local lRet   :=.F.
Local cQuery := ""
Local cTpCto := Posicione("CN1",1,xFilial("CN1")+CN9->CN9_TPCTO,"CN1_DESCRI")
//зддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Controles visuais                           Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
Local oDlg
Local oTree//Objeto dbtree
Local oTreeFil//Objeto dbtree Filiais autorizadas
Local aCoors		:= FWGetDialogSize( oMainWnd )
Local oPnl1, oPnl2
Local nOpca     :=2
Local cTra			:= ""
//зддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Carrega relacionamentos UsrXTransacao       Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
Local cAlias    := GetNextAlias()
Local lCN240Exc := ExistBlock("CN240Exc")

//Atualiza o array com os usuarios
aGrps    := FWSFAllGrps()//Grupos
aUsers   := c240UsBusca(cContra, aUsrs, aGrps) //Usuarios

If lContinua
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Validacao para a CNO.  			      		        Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("CNO")

	cQuery := "SELECT COUNT(*) AS TOTAL FROM "+ RetSQLName("CNO") +" CNO WHERE "
	cQuery += "CNO.CNO_FILIAL = '"+ xFilial("CNO") +"' AND "
	cQuery += "CNO.D_E_L_E_T_ = ' '"

	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.F.,.T.)

	If (cAlias)->TOTAL < TOTALTRAN
		CtaGerTra()
	EndIf

	(cAlias)->(dbCloseArea())

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁValidacao para a CN9. Campo para controle de acesso. Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддды

	lContinua := (Empty(CN9->CN9_VLDCTR) .Or. CN9->CN9_VLDCTR == "1")

	If lContinua
		If FunName() == "CNTA300"
			cTra := DEF_CTRACS
		Else
			cTra := DEF_TRAALT
		EndIf
		If CN240VldUsr(cContra,cTra,.T.)
			aTrans := CN240ListAc(.F.)

			//- Coordenadas da area total da Dialog
			oSize := FWDefSize():New(.F.)
			oSize:AddObject('PNL1',100,45,.T.,.T.)
			oSize:AddObject('PNL2',100,45,.T.,.T.)
			oSize:AddObject('BUT',100,10,.T.,.F.)
			oSize:SetWindowSize(aCoors)
			oSize:lProp 	:= .T.
			oSize:aMargins := {3,3,3,3}
			oSize:Process()

			DEFINE MSDIALOG oDlg TITLE cCadastro From oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4] PIXEL

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Cria painel para o Tree de acesso de usuario       Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			@ oSize:aPosObj[1,1], oSize:aPosObj[1,2] MSPANEL oPnl1 PROMPT "" SIZE oSize:aPosObj[1,4],oSize:aPosObj[1,3] OF oDlg

			oSize1 := FWDefSize():New(.F.)
			oSize1:AddObject('CAB',100,10,.T.,.T.)
			oSize1:AddObject('TREE',100,90,.T.,.T.)
			oSize1:SetWindowSize({0,0,oPnl1:NHEIGHT,oPnl1:NWIDTH})
			oSize1:lProp 	:= .T.
			oSize1:aMargins := {3,3,3,3}
			oSize1:Process()

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Contrato                                           Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			@ oSize1:aPosObj[1,1],oSize1:aPosObj[1,2] Say RetTitle("CN9_NUMERO") Of oPnl1 PIXEL
			@ oSize1:aPosObj[1,1],oSize1:aPosObj[1,2]+030 MsGet oGetCtr Var cContra When .F. PIXEL  Size 50,5 Of oPnl1

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Tipo do contrato                                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			@ oSize1:aPosObj[1,1],oSize:aPosObj[1,2]+082 Say RetTitle("CN9_TPCTO") Of oPnl1 PIXEL
			@ oSize:aPosObj[1,1],oSize:aPosObj[1,2]+122 MsGet oGetTCto Var cTpCto When .F. PIXEL  Size 100,5 Of oPnl1

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Carrega DBTree                                     Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			DEFINE DBTREE oTree FROM oSize1:aPosObj[2,1], oSize1:aPosObj[2,2] TO oSize1:aPosObj[2,3],oSize1:aPosObj[2,4] OF oPnl1 CARGO

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Monta arvore principal                             Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			DBADDTREE oTree PROMPT OemToAnsi(STR0015)+Space(30) OPEN OPENED CARGO FLG_ARVC+space(9)

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Carrega SubArvores                                 Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			CN240ItTree(oTree,aTrans,aUsrs)

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Monta menu de edicao/exclusao                      Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			MENU oMenuTree POPUP
			MENUITEM OemToAnsi(STR0018) Resource "BMPUSER"  	Action CN240Inc(oTree,aTrans,FLG_ARVU,aUsrs[1],aUsers,aGrps,,1)//Incluir Usuario
			MENUITEM OemToAnsi(STR0024) Resource "BMPGROUP" 	Action CN240Inc(oTree,aTrans,FLG_ARVG,aUsrs[2],aUsers,aGrps,,2)//Incluir Grupo
			MENUITEM OemToAnsi(STR0025) Resource "note" 		Action CN240Edit(oTree,aTrans,aUsrs,aUsers,aGrps)//Edita
			MENUITEM OemToAnsi(STR0026) Resource "excluir" 		Action If(!lCN240Exc,CN240Del(oTree,aTrans,aUsrs),(lRet:=ExecBlock("CN240Exc",.F.,.F.),If((valtype(lRet)=="L" .And. lRet),CN240Del(oTree,aTrans,aUsrs),nOpca:=2))) //Exclui
			MENUITEM OemToAnsi(STR0048) Resource "bmpincluir" 	Action CN240Cop(oTree,aTrans,aUsrs,aUsers,aGrps,cContra)//Copiar Estrutura
			ENDMENU

			oTree:bRClicked   := { |oObject,nx,ny| Cn240HPOPUP( oObject, nx-80, ny-150, oMenuTree , "oMenuTree") }

			DBENDTREE oTree

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Cria painel para o Tree de Filiais autorizadas	  Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			@ oSize:aPosObj[2,1], oSize:aPosObj[2,2] MSPANEL oPnl2 PROMPT "" SIZE oSize:aPosObj[2,4],oSize:aPosObj[2,3] OF oDlg

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Carrega DBTree Filiais autorizadas                Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			DEFINE DBTREE oTreeFil FROM 003, 003 TO oSize1:aPosObj[2,3],oSize1:aPosObj[2,4] OF oPnl2 CARGO

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Monta arvore principal                             Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			DBADDTREE oTreeFil PROMPT STR0061 OPEN OPENED CARGO FLG_ARVF+space(40)

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Carrega SubArvores                                 Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			CN240ItTree(oTreeFil,,,.T.)

			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Monta menu de InclusЦo/exclusao                      Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
									
			MENU oMenuTreeFil POPUP
			MENUITEM OemToAnsi(STR0065) Resource "bmpincluir"  	Action CN240IncFil(oTreeFil, @aInc, @aExc, oPnl2 )	//Incluir Filial
			MENUITEM OemToAnsi(STR0066) Resource "Excluir" 		Action CN240DelFil(oTreeFil, @aInc, @aExc) 			//Excluir Filial
			ENDMENU

			oTreeFil:bRClicked   :=  { |oObject,nx,ny| Cn240HPOPUP( oObject, nx-60, ny-(oSize1:aPosObj[2,4]-270), oMenuTreeFil , "oMenuTreeFil") }

			DBENDTREE oTreeFil

			DEFINE SBUTTON FROM oSize:aPosObj[3,1], oSize:aPosObj[3,4]-55 TYPE 1 ACTION If(CNTA240Ok(aUsrs),(nOpca:=1,oDlg:End()),) ENABLE OF oDlg
			DEFINE SBUTTON FROM oSize:aPosObj[3,1], oSize:aPosObj[3,4]-25 TYPE 2 ACTION (nOpca:=2,oDlg:End()) ENABLE OF oDlg

			ACTIVATE MSDIALOG oDlg CENTERED

			If nOpca==1					
				CN240Grv(aUsrs,cContra)//Executa gravacao da estrutura de acesso					
				If !Empty(aInc) .Or. !Empty(aExc)
					CN240GrvCPD(aInc, aExc)
				EndIf
			EndIf
		EndIf
	Else
		Help("",1,"CNTA240_01")
	EndIf
Else
	Aviso("CNTA240",OemToAnsi(STR0055),{"Ok"})//"O dicionario de dados nЦo se encontra atualizado, execute o compatibilizador GCTUPD02 atraves do remote Protheus"
EndIf
Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240Grv   Ё Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Executa gravaГЦo da estrutura de acesso do contrato         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240Grv(aExp01,cExp02)                                     Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ aExp01 = Array contendo os relacionamentos dos usuariosXtranЁ╠╠
╠╠Ё          Ё          sacoes                                             Ё╠╠
╠╠Ё          Ё cExp02 = Codigo do contrato                                 Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240Grv(aUsrs,cContra)
Local nx
Local cFilCod := xFilial("CNN")

dbSelectArea("CNN")
dbSetOrder(1)

//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Varre estrutura de usuarios                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
For nx:=1 to len(aUsrs[1])
	If aUsrs[1,nx,4] == FLG_RADD//Item adicionado
		//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Executa gravacao do item                           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
		RecLock("CNN",.T.)
		CNN->CNN_FILIAL := cFilCod
		CNN->CNN_CONTRA := cContra
		CNN->CNN_USRCOD := aUsrs[1,nx,1]
		CNN->CNN_TRACOD := aUsrs[1,nx,2]
		MsUnlock()
	ElseIf aUsrs[1,nx,4] == FLG_RDEL//Item excluido
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica item no banco e executa exclusao se necessarioЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If dbSeek(xFilial("CNN")+aUsrs[1,nx,1]+cContra+aUsrs[1,nx,2])
			RecLock("CNN",.F.)
			dbDelete()
			MsUnlock()
		EndIf
	EndIf
Next

dbSetOrder(2)

//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Varre estrutura de grupos                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
For nx:=1 to len(aUsrs[2])
	If aUsrs[2,nx,4] == FLG_RADD//Item adicionado
		//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Executa gravacao do item                           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
		RecLock("CNN",.T.)
		CNN->CNN_FILIAL := cFilCod
		CNN->CNN_CONTRA := cContra
		CNN->CNN_GRPCOD := aUsrs[2,nx,1]
		CNN->CNN_TRACOD := aUsrs[2,nx,2]
		MsUnlock()
	ElseIf aUsrs[2,nx,4] == FLG_RDEL//Item excluido
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica item no banco e executa exclusao se necessarioЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If dbSeek(xFilial("CNN")+aUsrs[2,nx,1]+cContra+aUsrs[2,nx,2])
			RecLock("CNN",.F.)
			dbDelete()
			MsUnlock()
		EndIf
	EndIf
Next

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada executado apСs a gravaГЦo da estrutura de acesso do contrato   Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If (ExistBlock("CN240CGRV"))
	ExecBlock("CN240CGRV",.F.,.F.,{aUsrs,cContra})
EndIf


Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240ItTreeЁ Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Gera as arvores de usuario e grupo do dbTree                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240ItTree(oExp01,aExp02,aExp03)                           Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - Objeto DbTree                                      Ё╠╠
╠╠Ё          Ё aExp02 - Array contendo as transacoes do sistema            Ё╠╠
╠╠Ё          Ё aExp03 - Array contendo os relacionamentos UrsXTransacoes   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240ItTree(oTree,aTrans,aUsrs,lTreeFil)

Default lTreeFil := .F.

If lTreeFil
	//зддддддддддддддддддддддддддддддддддддд©
	//Ё Gera arvore de Filiais autorizadas  Ё
	//юддддддддддддддддддддддддддддддддддддды
	CN240GerArv(oTree,{},"",STR0062,FLG_ARVF,"BMPUSER")
Else
//здддддддддддддддддддддддддд©
//Ё Gera arvore de usuarios  Ё
//юдддддддддддддддддддддддддды
CN240GerArv(oTree,aTrans,aUsrs[1],STR0017,FLG_ARVU,"BMPUSER")
//здддддддддддддддддддддддддд©
//Ё Gera arvore de grupos    Ё
//юдддддддддддддддддддддддддды
CN240GerArv(oTree,aTrans,aUsrs[2],STR0016,FLG_ARVG,"BMPGROUP")
EndIf

Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240GerArvЁ Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Gera arvore do dbtree                                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240GerArv(oExp01,aExp02,aExp03,cExp04,cExp05)             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - Objeto DbTree                                      Ё╠╠
╠╠Ё          Ё aExp02 - Array contendo as transacoes do sistema            Ё╠╠
╠╠Ё          Ё aExp03 - Array contendo os relacionamentos UrsXTransacoes   Ё╠╠
╠╠Ё          Ё cExp04 - Titulo da arvore                                   Ё╠╠
╠╠Ё          Ё cExp05 - Chave de pesquisa da arvore                        Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240GerArv(oTree,aTrans,aUsrs,cTit,cCargo,cRsr)
Local nx
Local lFirst := .T.
Local nPosTrans

//здддддддддддддддддддддддддд©
//Ё Cria arvore              Ё
//юдддддддддддддддддддддддддды
If cCargo == FLG_ARVF
	DBADDTREE oTree PROMPT STR0064 RESOURCE "" CARGO FLG_ARVF
	ADDTREEPLAN(oTree, CN9->(CN9_FILIAL+CN9_NUMERO+CN9_REVISA))
Else
	DBADDTREE oTree PROMPT cTit RESOURCE cRsr CARGO cCargo

	For nx:=1 to len(aUsrs)
		If lFirst
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Adiciona arvore quando primeira execucao do usuario/grupo  Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			DBADDTREE oTree PROMPT aUsrs[nx,3] CARGO cCargo+aUsrs[nx,1]
		EndIf

		nPosTrans := aScan(aTrans,{|x| x[2] == aUsrs[nx,2]})

		if nPosTrans > 0
			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Adiciona transacao na arvore do usuario/grupo  Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды
			DBADDITEM oTree PROMPT aTrans[nPosTrans,3] CARGO cCargo+aUsrs[nx,1]+aUsrs[nx,2]
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica proximo usuario/grupo                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		lFirst := ((len(aUsrs)>nx) .And. aUsrs[nx+1,1] != aUsrs[nx,1])
		If lFirst .OR. nx == len(aUsrs)
			DBENDTREE oTree
		EndIf
	Next
EndIf

DBENDTREE oTree

Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240Del   Ё Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Exclui usuario/grupo da estrutura                           Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240Del(oExp01,aExp02,aExp03)                              Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - Objeto DbTree                                      Ё╠╠
╠╠Ё          Ё aExp02 - Array contendo as transacoes do sistema            Ё╠╠
╠╠Ё          Ё aExp03 - Array contendo os relacionamentos UrsXTransacoes   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240Del(oTree,aTrans,aUsrs)
Local cCargo
Local nx
Local cCod
Local nTDelArr := 0
Local nInd
Local lUser    := .T.

//здддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Carrega chave do registro selecionado no dbTreeЁ
//юдддддддддддддддддддддддддддддддддддддддддддддддды
cCargo:=oTree:GetCargo()

nInd := If(SubStr(cCargo,1,1)==FLG_ARVU,1,2)

//здддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se foi selecionado grupo ou usuario   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддды
If len(AllTrim(cCargo))>1
	//здддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Seleciona codigo                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддды
	cCod := SubStr(cCargo,2,6)

	If Aviso("CNTA240",OemToAnsi(If(nInd==1,STR0044,STR0045))+If(nInd==1,UsrRetName(cCod),GrpRetName(cCod)),{OemToAnsi(STR0046),OemToAnsi(STR0047)})==1
		nx    :=len(aUsrs[nInd])


		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Exclui transacoes do usuario/grupo             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		While nx > 0
			If aUsrs[nInd,nx,1] == cCod .And. lUser
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Altera estado do registro para ser usado na gravacao Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If aUsrs[nInd,nx,4] == FLG_RORI
					aUsrs[nInd,nx,4] := FLG_RDEL
				ElseIf aUsrs[nInd,nx,4] == FLG_RADD
					aDel(aUsrs[nInd],nx)
					nTDelArr++
				EndIf
			EndIf
			nx--
		EndDo

		//здддддддддддддддддддддддддддддд©
		//Ё Atualiza dbTree              Ё
		//юдддддддддддддддддддддддддддддды
		If lUser
			oTree:BeginUpdate()
			If oTree:TreeSeek(If(nInd==1,FLG_ARVU,FLG_ARVG)+cCod)
				oTree:DelItem()
			EndIf
			oTree:EndUpdate()
			oTree:Refresh()
			aSize(aUsrs[nInd],len(aUsrs[nInd])-nTDelArr)
		Else
			Aviso("CNTA240",OemToAnsi(STR0059),{"OK"})
		EndIf
	EndIf
EndIf

Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240Del   Ё Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Exclui usuario/grupo da estrutura                           Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240Del(oExp01,aExp02,aExp03,aExp04,aExp05)                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - Objeto DbTree                                      Ё╠╠
╠╠Ё          Ё aExp02 - Array contendo as transacoes do sistema            Ё╠╠
╠╠Ё          Ё aExp03 - Array contendo os relacionamentos UrsXTransacoes   Ё╠╠
╠╠Ё          Ё aExp04 - Array contendo os usuarios do sistema - AllUsers() Ё╠╠
╠╠Ё          Ё aExp05 - Array contendo os grupos de usuario do sistema -   Ё╠╠
╠╠Ё          Ё          AllGroups()                                        Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240Edit(oTree,aTrans,aUsrs,aUsers,aGrps)
Local cCargo

//здддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Carrega chave do registro selecionado no dbTreeЁ
//юдддддддддддддддддддддддддддддддддддддддддддддддды
cCargo:=oTree:GetCargo()

//здддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se foi selecionado grupo ou usuario   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддды
If len(AllTrim(cCargo))>1
	If SubStr(cCargo,1,1) == FLG_ARVU//Usuario
		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Edita configuracao do usuario                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		CN240Inc(oTree,aTrans,FLG_ARVU,aUsrs[1],aUsers,aGrps,SubStr(cCargo,2,6),3)
	Else//Grupo
		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Edita configuracao do grupo                    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		CN240Inc(oTree,aTrans,FLG_ARVG,aUsrs[2],aUsers,aGrps,SubStr(cCargo,2,6),4)
	EndIf
EndIf

Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240Inc   Ё Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Inclui/Edita grupo/usuario na estrutura de acesso           Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240Inc(oExp01,aExp02,cExp03,aExp04,aExp05,aExp06,cExp07)  Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - Objeto DbTree                                      Ё╠╠
╠╠Ё          Ё aExp02 - Array contendo as transacoes do sistema            Ё╠╠
╠╠Ё          Ё cExp03 - Chave da arvore onde sera incluso o item           Ё╠╠
╠╠Ё          Ё aExp04 - Array contendo os relacionamentos UrsXTransacoes   Ё╠╠
╠╠Ё          Ё aExp05 - Array contendo os usuarios do sistema - AllUsers() Ё╠╠
╠╠Ё          Ё aExp06 - Array contendo os grupos de usuario do sistema -   Ё╠╠
╠╠Ё          Ё          AllGroups()                                        Ё╠╠
╠╠Ё          Ё cExp07 - Codigo do usuario/grupo a ser editado - opcional - Ё╠╠
╠╠Ё          Ё          usado apenas na edicao                             Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240Inc(oTree,aTrans,cCargo,aUsrs,aUsers,aGrps,cCod,nOpc)
Local lEdit   :=(cCod!=Nil)//Edicao quando recebe codigo
Local oGetUsr
Local cCodGen := Space(6)
Local oGetGrp
Local oDlg
Local nPos
Local nPosPai
Local nOpca:=2
Local cNome
Local nX
Local oTreeT
Local lInc

If lEdit
	cCodGen := cCod
EndIf

If !lEdit
	//здддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Configura todas as transacoes como falso       Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддды
	aEval(aTrans,{|x| x[1] := .F.})
Else
	//здддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Varre todas as transacoes do sistema           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддды
	For nx:=1 to len(aTrans)
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se a transacao esta configurada para o usuario/grupoЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aTrans[nx,1] := (aScan(aUsrs,{|x| x[1]==cCodGen .And. x[2]==aTrans[nx,2] .And. x[4] != FLG_RDEL}) > 0)

		If !aTrans[nx,1] .And. !Empty(aTrans[nx,4])
			aTrans[nx,1] := aTrans[aTrans[nx,6],1]
		EndIf
	Next
EndIf

If cCargo == FLG_ARVU//Usuario
	If nOpc==1
   		DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0018) From 165,115 TO 450,450 PIXEL
	Else
		DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0025) From 165,115 TO 450,450 PIXEL
	EndIf
	@ 07,10 Say STR0056 Of oDlg PIXEL//"UsuАrio"
	@ 05,50 MsGet oGetUsr Var cCodGen F3 "USR" WHEN !lEdit ON CHANGE (oDesc:Refresh()) Size 60,5 Of oDlg PIXEL VALID Cn240VldGr('USR',cCodGen)
	@ 18,10 Say STR0027 Of oDlg PIXEL//"UsuАrio"
	@ 16,50 MsGet oDesc Var UsrRetName(cCodGen) WHEN .F. Size 60,5 Of oDlg PIXEL
Else//Grupo
	If nOpc==2
   		DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0024) From 165,115 TO 450,450 PIXEL
	Else
		DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0025) From 165,115 TO 450,450 PIXEL
	EndIf
	@ 07,10 Say STR0057 Of oDlg PIXEL//"Grupo"
	@ 05,50 MsGet oGetGrp Var cCodGen F3 "GRP" WHEN !lEdit ON CHANGE (oDesc:Refresh()) Size 60,5 Of oDlg PIXEL VALID Cn240VldGr('GRP',cCodGen)
	@ 18,10 Say STR0027 Of oDlg PIXEL//"Grupo"
	@ 16,50 MsGet oDesc Var Iif(Empty(cCodGen),"",GrpRetName(cCodGen)) WHEN .F. Size 60,5 Of oDlg PIXEL
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Carrega DBTree com as transacoes                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
DEFINE DBTREE oTreeT FROM 30,10 TO __DlgHeight(oDlg)-27,__DlgWidth(oDlg)-3 OF oDlg CARGO

//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Carrega Arvore de Transacao                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
CN240LTreeT(oTreeT,aTrans)

DBENDTREE oTreeT

//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa rotina de alteracao da situacao            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
oTreeT:bLDblClick := {|| CN240AltSit(oTreeT,aTrans)}

DEFINE SBUTTON FROM __DlgHeight(oDlg)-25, __DlgWidth(oDlg)-60 TYPE 1 ACTION (If(Empty(cCodGen),Aviso("CNTA240",OemToAnsi(STR0028),{"Ok"}),(nOpca:=1,oDlg:End()))) ENABLE OF oDlg PIXEL
DEFINE SBUTTON FROM __DlgHeight(oDlg)-25, __DlgWidth(oDlg)-30 TYPE 2 ACTION (nOpca:=2,oDlg:End()) ENABLE OF oDlg PIXEL

ACTIVATE MSDIALOG oDlg CENTERED

If nOpca == 1

	oTree:BeginUpdate()
	For nx:=1 to len(aTrans)
		lInc:=aTrans[nX,1]

		If lInc .And. !Empty(aTrans[nX,4])
			nPosPai := aTrans[nx,6]
			If nPosPai > 0
				lInc := !aTrans[nPosPai,1]
			EndIf
		EndIf

		If lInc
			//здддддддддддддддддддддддддддд©
			//Ё Executa inclusao no dbTree Ё
			//юдддддддддддддддддддддддддддды
			If CN240IncIt(oTree,cCargo,cCodGen,aTrans[nx,3],aTrans[nx,2],aUsers,aGrps)
				If (nPos := aScan(aUsrs,{|x| x[1] == cCodGen .And. x[2] == aTrans[nx,2]})) > 0
					aUsrs[nPos,4] := FLG_RORI
				Else
					If cCargo==FLG_ARVU
						If (nPos:=ascan(aUsers,{|x| x[2] = cCodGen}))>0
							cNome:=aUsers[nPos,3]
						EndIf
					Else
						If (nPos:=ascan(aGrps,{|x| x[1,1] = cCodGen}))>0
							cNome:=aGrps[nPos,1,2]
						EndIf
					EndIf
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Executa inclusao no array de relacionamento UsrsXTransacoes Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					aAdd(aUsrs,{cCodGen,aTrans[nx,2],cNome,FLG_RADD})
				EndIf
			EndIf
		Else
			//здддддддддддддддддддддддддддддддддддд©
			//Ё Verifica se item foi excluido      Ё
			//юдддддддддддддддддддддддддддддддддддды
			If oTree:TreeSeek(cCargo+cCodGen+aTrans[nx,2])
				//здддддддддддддддддддддддддддддддддддд©
				//Ё Exclui item do dbTree              Ё
				//юдддддддддддддддддддддддддддддддддддды
				oTree:DelItem()
				nPos := aScan(aUsrs,{|x| x[1] == cCodGen .And. x[2] == aTrans[nx,2]})
				//здддддддддддддддддддддддддддддддддддд©
				//Ё Exclui item no array de controle   Ё
				//юдддддддддддддддддддддддддддддддддддды
				If nPos > 0
					If aUsrs[nPos,4] == FLG_RADD
						aDel(aUsrs,nPos)
						aSize(aUsrs,len(aUsrs)-1)
					Else
						aUsrs[nPos,4] := FLG_RDEL
					EndIf
				EndIf
			EndIf
		EndIf
	Next

	oTree:EndUpdate()
	oTree:Refresh()
EndIf
Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240AltSitЁ Autor Ё Marcelo Custodio      Ё Data Ё20.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Altera selecao da transacao                                 Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240AltSit(oExp01,aExp02)                                  Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - Objeto DbTree - Transacoes                         Ё╠╠
╠╠Ё          Ё aExp02 - Array contendo as transacoes do sistema            Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240AltSit(oTreeT,aTrans)
Local cCargo := oTreeT:GetCargo()
Local cOkRs := "LBTIK"
Local cNoRs := "LBNO"
Local nPos := aScan(aTrans,{|x| x[2]==cCargo})
Local nx

If nPos > 0
	oTreeT:BeginUpdate()
	//здддддддддддддддддддддддддддддддддддд©
	//Ё Altera situacao no array           Ё
	//юдддддддддддддддддддддддддддддддддддды
	aTrans[nPos,1] := !aTrans[nPos,1]

	//здддддддддддддддддддддддддддддддддддд©
	//Ё Altera imagem                      Ё
	//юдддддддддддддддддддддддддддддддддддды
	If aTrans[nPos,1]
		oTreeT:ChangeBmp(cOkRs,cOkRs)
	Else
		oTreeT:ChangeBmp(cNoRs,cNoRs)
	EndIf

	If aTrans[nPos,5]
		SitAll(oTreeT,aTrans[nPos,2],aTrans,If(aTrans[nPos,1],cOkRs,cNoRs),nPos,aTrans[nPos,1])
	EndIf

	If !Empty(aTrans[nPos,4])
		SitUp(oTreeT,aTrans,nPos)
	EndIf

	oTreeT:TreeSeek(aTrans[nPos,2])
	oTreeT:EndUpdate()
	oTreeT:Refresh()
EndIf

Return

Function SitUp(oTreeT,aTrans,nPos)
Local nx
Local lSelec:=.T.
Local cOkRs := "LBTIK"
Local cNoRs := "LBNO"
Local nPosPai

If !Empty(aTrans[nPos,4])
	For nx:=1 to len(aTrans)
		If aTrans[nx,2] == aTrans[nPos,4]
			nPosPai := nx
		ElseIf aTrans[nx,4] == aTrans[nPos,4] .And. !aTrans[nx,1]
			lSelec:=.F.
			Exit
		EndIf
	Next

	If lSelec
		If oTreeT:TreeSeek(aTrans[nPos,4])
			oTreeT:ChangeBmp(cOkRs,cOkRs)
			aTrans[nPosPai,1]:=.T.
		EndIf
	Else
		If oTreeT:TreeSeek(aTrans[nPos,4])
			oTreeT:ChangeBmp(cNoRs,cNoRs)
			aTrans[nPosPai,1]:=.F.
		EndIf
	EndIf
	SitUp(oTreeT,aTrans,nPosPai)
EndIf
Return

Function SitAll(oTreeT,cTrans,aTrans,cRsc,nPos,lAdd)
Local nx

For nx:=nPos+1 to len(aTrans)
	If aTrans[nx,4] == cTrans
		If oTreeT:TreeSeek(aTrans[nx,2])
			oTreeT:ChangeBmp(cRsc,cRsc)
			aTrans[nx,1] := lAdd
			If aTrans[nx,5]
				SitAll(oTreeT,aTrans[nx,2],aTrans,cRsc,nx,lAdd)
			EndIf
		EndIf
	EndIf
Next
Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240LTreeTЁ Autor Ё Marcelo Custodio      Ё Data Ё20.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Constroi arvore das transacoes do sistema                   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240LTreeT(oExp01,aExp02)                                  Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - Objeto DbTree - Transacoes                         Ё╠╠
╠╠Ё          Ё aExp02 - Array contendo as transacoes do sistema            Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240LTreeT(oTreeT,aTrans)
Local nx
Local cOkRs := "LBTIK"
Local cNoRs := "LBNO"

//здддддддддддддддддддддддддддддддддддд©
//Ё Gera arvore das transacoes         Ё
//юдддддддддддддддддддддддддддддддддддды
oTreeT:BeginUpdate()
For nx:=1 to len(aTrans)
	//здддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a transacao possui pai Ё
	//юдддддддддддддддддддддддддддддддддддды
	If Empty(atrans[nx,4])
		oTreeT:AddItem(aTrans[nx,3]+Space(30),aTrans[nx,2],If(aTrans[nx,1],cOkRs,cNoRs),,,,2)
	Else
		//здддддддддддддддддддддддддддддддддддд©
		//Ё Verifica transacao pai             Ё
		//юдддддддддддддддддддддддддддддддддддды
		If oTreeT:TreeSeek(aTrans[nx,4])
			oTreeT:PTCollapse()
			oTreeT:AddItem(aTrans[nx,3],aTrans[nx,2],If(aTrans[nx,1],cOkRs,cNoRs),,,,2)
		EndIf
	EndIf
Next

oTreeT:EndUpdate()
oTreeT:Refresh()
Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240IncIt Ё Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Inclui item na arvore do dbTree                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240IncIt(oExp01,cExp02,cExp03,cExp04,cExp05,aExp06,aExp07)Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - Objeto DbTree                                      Ё╠╠
╠╠Ё          Ё cExp02 - Chave da arvore onde sera incluso o item           Ё╠╠
╠╠Ё          Ё cExp03 - Codigo do usuario/grupo                            Ё╠╠
╠╠Ё          Ё cExp04 - Descricao da transacao                             Ё╠╠
╠╠Ё          Ё cExp05 - Codigo da transacao                                Ё╠╠
╠╠Ё          Ё aExp06 - Array contendo os usuarios do sistema - AllUsers() Ё╠╠
╠╠Ё          Ё aExp07 - Array contendo os grupos de usuario do sistema -   Ё╠╠
╠╠Ё          Ё          AllGroups()                                        Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240IncIt(oTree,cCargo,cCod,cTrans,cCodtra,aUsers,aGrps)
Local lRet      := .F.
Local lContinua := .T.
Local cNome     := ""
Local nPos

//здддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se o item ja se encontra no dbTree  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддды
if oTree:TreeSeek(cCargo+cCod)
	If !oTree:TreeSeek(cCargo+cCod+cCodtra)//Verifica se transacao ja existe
		//здддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Inclui item no dbTree                        Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддды
		oTree:AddItem(cTrans,cCargo+cCod+cCodtra,,,,,2)
		lRet := .T.
	EndIf
ElseIf oTree:TreeSeek(cCargo)//Posiciona na arvore
	If cCargo==FLG_ARVU
	    PswOrder(1)
     	If (  PswSeek(cCod, .T.) )
			cNome:=Pswret(1)[1][2]
		EndIf
	Else
		If (nPos:=ascan(aGrps,{|x| x[1,1] = cCod}))>0
			cNome:=aGrps[nPos,1,2]
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Valida usuАrio/grupo                         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддды
	If Empty(cNome)
		Aviso("CNTA240",OemToAnsi(STR0058),{"Ok"})//"UsuАrio/Grupo inexistente, selecione um usuАrio vАlido."
	    lContinua := .F.
	EndIf

	If lContinua
		//здддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Inclui item no dbTree                        Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддды
		oTree:AddItem(cNome,cCargo+cCod,,,,,2)
		If oTree:TreeSeek(cCargo+cCod)
			oTree:AddItem(cTrans,cCargo+cCod+cCodtra,,,,,2)
			lRet := .T.
		EndIf
	EndIf
EndIf

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240ListAcЁ Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006 Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Gera lista sequencial das transacoes                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё aExp01 := CN240ListAc(lExp02)                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ aExp01 - Array preenchido pela rotina com a seguinte         Ё╠╠
╠╠Ё          Ё          estrutura:                                          Ё╠╠
╠╠Ё          Ё          -aExp02[x,1] = .F.                                  Ё╠╠
╠╠Ё          Ё          -aExp02[x,2] = Codigo da Transacao                  Ё╠╠
╠╠Ё          Ё          -aExp02[x,3] = Descricao da Transacao               Ё╠╠
╠╠Ё          Ё          -aExp02[x,4] = Listagem das transacoes pai, usado   Ё╠╠
╠╠Ё          Ё                         na query de pesquisa Ex: 'XXX','XXX' Ё╠╠
╠╠Ё          Ё                         quando lQuery==.T. OU apenas a       Ё╠╠
╠╠Ё          Ё                         identificacao da trancasao pai quandoЁ╠╠
╠╠Ё          Ё                         lQuery==.F.                          Ё╠╠
╠╠Ё          Ё          -aExp02[x,5] = Informa quando transacao possui      Ё╠╠
╠╠Ё          Ё                         filhas                               Ё╠╠
╠╠Ё          Ё          -aExp02[x,6] = Informa posicao da transacao pai     Ё╠╠
╠╠Ё          Ё                         quando possuir                       Ё╠╠
╠╠Ё          Ё lExp02 - Monta estrutura pai para ser usada na query de      Ё╠╠
╠╠Ё          Ё          pesquisa, ou apenas carrega transacao superior      Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240ListAc(lQuery)
Local nx        := 0
Local nPosPai   := 0
Local cInQry    := ""
Local aTrans    :={}
Local cDescT    :=""
Local cQuery    := {}
Local cAliasQry := GetNextAlias()

DEFAULT lQuery := .F.

//здддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Abre tabela padrao de transacoes do SIGAGCT  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("CNO")
cQuery := "SELECT CNO.* FROM " + RetSQLName("CNO") + " CNO WHERE "
cQuery += "CNO.CNO_FILIAL = '"+ xFilial("CNO") +"' AND "
cQuery += "CNO.D_E_L_E_T_ = '' "

cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.F.,.T.)

While !(cAliasQry)->(Eof())

	cDescT := (cAliasQry)->CNO_DESCRI

	//здддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a transacao possui pai           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддды
	If !Empty((cAliasQry)->CNO_CODPAI)
		//здддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Encontra posicao da transacao pai            Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддды
		If (nPosPai := aScan(aTrans,{|x| x[2]==(cAliasQry)->CNO_CODPAI})) > 0
			aTrans[nPosPai,5] := .T.//Altera  marcacao informando que transacao possui filha
			If lQuery
				//здддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Monta query com a hierarquia das transacoes  Ё
				//Ё quando solicitado                            Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддды
				cInQry := aTrans[nPosPai,4]
				If Empty(cInQry)
					cInQry := "'"+(cAliasQry)->CNO_CODPAI+"'"
				Else
					cInQry += ",'"+(cAliasQry)->CNO_CODPAI+"'"
				EndIf

				aAdd(aTrans,{.F.,(cAliasQry)->CNO_CODTRA,cDescT,cInQry,.F.,nPosPai})
			Else
				aAdd(aTrans,{.F.,(cAliasQry)->CNO_CODTRA,cDescT,(cAliasQry)->CNO_CODPAI,.F.,nPosPai})
			EndIf
		EndIf
	Else
		aAdd(aTrans,{.F.,(cAliasQry)->CNO_CODTRA,cDescT,"",.F.,0})
	EndIf

	(cAliasQry)->(dbSkip())
EndDo

(cAliasQry)->(dbCloseArea())

Return aTrans

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240VldUsrЁ Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida o acesso do usuario a uma transacao especifica do    Ё╠╠
╠╠Ё          Ё contrato                                                    Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240VldUsr(cExp01,cExp02,lExp03,lExp04)                    Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё SIGAGCT                                                     Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ cExp01 - Contrato                                           Ё╠╠
╠╠Ё          Ё cExp02 - Codigo da Transacao                                Ё╠╠
╠╠Ё          Ё lExp03 - Informa se exibe mensagem de erro                  Ё╠╠
╠╠Ё          Ё lExp04 - Indica se o usuario podera VISUALIZAR o contrato   Ё╠╠
╠╠Ё          Ё 			mesmo sem ter acesso ao mesmo. Esse parametro e	   Ё╠╠
╠╠Ё          Ё 			enviado como .T. exclusivamente pela rotina de 	   Ё╠╠
╠╠Ё          Ё 			liberacao (MATA097), e somente quando o documento  Ё╠╠
╠╠Ё          Ё 			e do tipo CT (Contrato) e o Aprovador nao possui   Ё╠╠
╠╠Ё          Ё 			acesso ao contrato.		   						   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240VldUsr(cContra,cTrans,lMens,lVisAprCt,cFilCTR)
	Local aAreas    := {CN9->(GetArea()), GetArea()}
	Local cCod      := ""
	Local cGrps     := ""
	Local lRet      := .T.
	Local cAliasQry := GetNextAlias()
	Local aTrans    := {}
	Local lPai
	Local aGrp
	Local nPos
	Local nX
	Local cQuery
	Local lContinua := !IsBlind() .Or. CN240GVld() //-- Se for ExecAuto ou a variАvel estАtica lValida for .F. nЦo precisa validar
	Local aHelp		:= {}
	DEFAULT lMens 	  := .T.
	DEFAULT lVisAprCt := .F.
	DEFAULT cFilCTR 	:= cFilAnt
	
	If lContinua
		CN9->(dbSetOrder(1))
		lContinua := CN9->(dbSeek(xFilial("CN9", cFilCTR)+cContra) .And. CN9_VLDCTR != "2" )
	EndIf

	If lContinua
		cCod	:= RetCodUsr()
		aTrans	:= CN240ListAc(.T.)//Carrega transacoes e listagem com a estrutura
		If (nPos := aScan(aTrans,{|x| x[2] == AllTrim(cTrans)})) > 0 //Encontra posicao da transacao			
			
			lPai := !Empty(aTrans[nPos,4])//Ё Verifica se a transacao possui pai
			
			aGrp := FWSFUsrGrps(cCod)//Carrega Grupos do usuario

			For nX:=1 to len(aGrp)
				cGrps += "'"+aGrp[nX]+"',"
			Next
			cGrps := SubStr(cGrps,1,len(cGrps)-1)
			
			//Filtra transacoes do usuario considerando pai			
			cQuery := "SELECT COUNT(CNN_CONTRA) AS QTD_TRA FROM " + RetSQLName("CNN") + " CNN WHERE "
			cQuery += "CNN.CNN_FILIAL = '"+ xFilial("CNN",cFilCTR) +"' AND "
			cQuery += "CNN.CNN_CONTRA = '"+ cContra +"' AND "
			If lPai
				cQuery += "CNN.CNN_TRACOD IN ('"+ cTrans +"',"+aTrans[nPos,4]+") AND "
			Else
				cQuery += "CNN.CNN_TRACOD = '"+ cTrans +"' AND "
			EndIf
			cQuery += "(CNN.CNN_USRCOD = '"+ cCod +"'"

			If len(aGrp) > 0
				cQuery += " OR CNN.CNN_GRPCOD IN ("+ cGrps +")"
			EndIf
			cQuery += ") AND CNN.D_E_L_E_T_ = ''"
			cQuery := ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.F.,.T.)			

			lRet := ((cAliasQry)->QTD_TRA > 0)//Verifica se usuario possui acesso			
			(cAliasQry)->(dbCloseArea())
			
			
			If lVisAprCt .AND. !lRet //Acesso de VISUALIZACAO liberado quando o usuario e aprovador do Contrato (MATA097) mas nao possui acesso
				lRet := .T.
			EndIf

			If !lRet .And. (cTrans == DEF_CTRACS .And. FWIsAdmin()) //Se nЦo tem permissЦo, porИm Admim tentando conceder acesso(DEF_CTRACS)
				lRet := !(HasUsrPerm(cContra)) //SС permite se nЦo existir nenhum usuАrio com permissЦo de acesso
			EndIf

			If !lRet .And. lMens
				aAdd(aHelp, STR0070 + cTrans + " - " + AllTrim(aTrans[nPos,3])+"."+ CRLF) //Sem permissЦo para realizar a operaГЦo:
				aAdd(aHelp, STR0071 + cContra + ".")//Verifique as permissУes do contrato				
				Help(" ",1,"CNNOTRANS",/*cNome*/, /*cMensagem*/,,,,,,,, aHelp) //Exibe mensagem de acesso negado				
			EndIf
		EndIf
	EndIf
	aEval(aAreas, {|x|RestArea(x), aSize(x, 0) })
Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN240Cop   Ё Autor Ё Marcelo Custodio      Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Copia estrutura de acesso de outro contrato                 Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN240Cop(oExp01,aExp02,aExp03,aExp04,aExp05,cExp06)         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - DbTree                                             Ё╠╠
╠╠Ё          Ё aExp02 - Array contendo as transacoes do sistema            Ё╠╠
╠╠Ё          Ё aExp03 - Array contendo os relacionamentos UrsXTransacoes   Ё╠╠
╠╠Ё          Ё aExp04 - Array contendo os usuarios do sistema - AllUsers() Ё╠╠
╠╠Ё          Ё aExp05 - Array contendo os grupos de usuario do sistema -   Ё╠╠
╠╠Ё          Ё          AllGroups()                                        Ё╠╠
╠╠Ё          Ё cExp06 - Codigo do contrato                                 Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN240Cop(oTree,aTrans,aUsrs,aUsers,aGrps,cContra)
Local cContraN := Space(TamSX3("CN9_NUMERO")[1])
Local oGetContra
Local nOpca := 2
Local aUsrs2:={}
Local nx
Local nPos

DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0048) From 165,115 TO 245,430 PIXEL

@ 07,10 Say RetTitle("CN9_NUMERO") Of oDlg PIXEL
@ 05,40 MsGet oGetContra Var cContraN Valid (cContraN != cContra) F3 "CN9" Size 60,5 Of oDlg PIXEL

DEFINE SBUTTON FROM __DlgHeight(oDlg)-23, __DlgWidth(oDlg)-60 TYPE 1 ACTION (If(Empty(cContraN),Aviso("CNTA240",OemToAnsi(STR0030),{"Ok"}),(nOpca:=1,oDlg:End()))) ENABLE OF oDlg PIXEL
DEFINE SBUTTON FROM __DlgHeight(oDlg)-23, __DlgWidth(oDlg)-30 TYPE 2 ACTION (nOpca:=2,oDlg:End()) ENABLE OF oDlg PIXEL

ACTIVATE MSDIALOG oDlg CENTERED

If nOpca == 1	
	 
	 c240UsBusca(cContraN, @aUsrs2, aGrps)//Carrega relacionamentos do contrato original
	
	For nx:=1 to len(aUsrs2[1])//Ё Varre transacoes de usuarios		
		
		If (nPos:=aScan(aUsrs[1],{|x| x[1]==aUsrs2[1,nx,1] .And. x[2]==aUsrs2[1,nx,2]}))>0//Verifica se item jА existe
			If aUsrs[1,nPos,4] == FLG_RDEL//Ё Altera situacao quando estiver sido excluido Ё
				aUsrs[1,nPos,4] := FLG_RORI
			EndIf
		Else
			aAdd(aUsrs[1],aUsrs2[1,nx])//Ё Inclui item                   Ё
			aUsrs[1,len(aUsrs[1]),4] := FLG_RADD
		EndIf
	Next
	
	For nx:=1 to len(aUsrs2[2])	//Ё Varre transacoes de grupos

		If (nPos:=aScan(aUsrs[2],{|x| x[1]==aUsrs2[2,nx,1] .And. x[2]==aUsrs2[2,nx,2]}))>0		//Ё Verifica se item jА existe                   Ё

			If aUsrs[2,nPos,4] == FLG_RDEL//Ё Altera situacao quando estiver sido excluido Ё
				aUsrs[2,nPos,4] := FLG_RORI
			EndIf
		Else			
			aAdd(aUsrs[2],aUsrs2[2,nx])	//Ё Inclui item                   Ё
			aUsrs[2,len(aUsrs[2]),4] := FLG_RADD
		EndIf
	Next

	//Ё Atualiza dbTree
	oTree:BeginUpdate()
	If oTree:TreeSeek(FLG_ARVC)
		oTree:DelItem()
	EndIf
	oTree:Reset()
	oTree:Refresh()

	DBADDTREE oTree PROMPT OemToAnsi(STR0015)+Space(30) OPENED CARGO FLG_ARVC+space(9)
	CN240ItTree(oTree,aTrans,aUsrs)
	DBENDTREE oTree

	oTree:EndUpdate()
	oTree:Refresh()
EndIf

Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁA240TudoOk Ё Autor Ё Bruno Schmidt	     Ё Data Ё12.06.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Verifica se todos os acessos estЦo corretos                 Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
static Function CNTA240Ok(aUsrs)
Local lRet:=.T.
Local ni  := 0
Local ny  := 0
Local x   := 0
Local y   := 0
Local nO  := 0
Local lAcess   := .T.


For ni:=1 to len(aUsrs[1])
	If aUsrs[1,ni,4] <> FLG_RDEL
		x+=1
	EndIF
Next
For ny:=1 to len(aUsrs[2])
	If aUsrs[2,ny,4] <> FLG_RDEL
		y+=1
	EndIF
Next

If Empty(x).and. Empty(y)
	Aviso("CNTA240",OemToAnsi(STR0060),{"Ok"})
	lret:=.F.
EndIF

For nO:=1 to len(aUsrs[1])
	If aUsrs[1,nO,4] <> FLG_RDEL
		If aUsrs[1,nO,2] == '041' .or. aUsrs[1,nO,2] == '001'
		lAcess := .F.
		EndIF
	EndIF
Next
For nO:=1 to len(aUsrs[2])
	If aUsrs[2,nO,4] <> FLG_RDEL
		If aUsrs[2,nO,2] == '041' .or. aUsrs[2,nO,2] == '001'
		lAcess := .F.
		EndIF
	EndIF
Next

If lAcess	
 Help( "" ,1,"CNTA240_CA")
	lret:=.F.
EndIF

Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    Ёc240UsBuscaЁ Autor Ё Aline S Damasceno     Ё Data Ё07.03.2013Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Gera array com os relacionamentos usuarios X transacoes do  Ё╠╠
╠╠Ё          Ё contrato                                                    Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ aExp01 - Array com os relacionamentos usuariosXtransacao    Ё╠╠
╠╠Ё          Ё Estrutura do aExp01:                                        Ё╠╠
╠╠Ё          Ё -aExp01[1]-Usuarios                                         Ё╠╠
╠╠Ё          Ё  -aExp01[1,1]-Codigo do Usuario                             Ё╠╠
╠╠Ё          Ё  -aExp01[1,2]-Codigo da Transacao                           Ё╠╠
╠╠Ё          Ё  -aExp01[1,3]-Nome do Usuario                               Ё╠╠
╠╠Ё          Ё  -aExp01[1,4]-Situacao do registro: FLG_RORI - Original     Ё╠╠
╠╠Ё          Ё                                     FLG_RDEL - Excluido     Ё╠╠
╠╠Ё          Ё                                     FLG_RADD - Adicionado   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/

Function c240UsBusca(cContra, aUsrs, aGroups)
	Local aUsr      := {}
	Local aArea		:= GetArea()
	Local cAliasQry := GetNextAlias()
	Local aTemp		:= {}
	Local nX 		:= 0
	Local nPos		:= 0
	Default aGroups := FWSFAllGrps()
	
	aUsrs 	:= Array(2)
	aUsrs[1]:= {}
	aUsrs[2]:= {}	
	
	BeginSQL Alias cAliasQry
		SELECT CNN.CNN_USRCOD, CNN.CNN_TRACOD,CNN.CNN_GRPCOD FROM %Table:CNN% CNN
		WHERE
		CNN.CNN_FILIAL = %xFilial:CNN% 
		AND CNN.CNN_CONTRA = %Exp:cContra%
		AND	CNN.CNN_GRPCOD = %Exp:' '% 
		AND CNN.%NotDel%
		
		UNION
		
		SELECT CNN.CNN_USRCOD, CNN.CNN_TRACOD,CNN.CNN_GRPCOD FROM %Table:CNN% CNN
		WHERE
		CNN.CNN_FILIAL = %xFilial:CNN%
		AND CNN.CNN_CONTRA = %Exp:cContra%
		AND CNN.CNN_USRCOD = %Exp:' '%
		AND CNN.%NotDel%
		ORDER BY CNN_USRCOD, CNN_GRPCOD, CNN_TRACOD
	EndSQL
	
	While !(cAliasQry)->(Eof())	
		If(!Empty((cAliasQry)->CNN_USRCOD))			
			aAdd(aTemp, (cAliasQry)->CNN_USRCOD)			
			aAdd(aUsrs[1], { (cAliasQry)->CNN_USRCOD, (cAliasQry)->CNN_TRACOD, "", FLG_RORI})
		ElseIf(!Empty((cAliasQry)->CNN_GRPCOD))
			aAdd(aUsrs[2], { (cAliasQry)->CNN_GRPCOD, (cAliasQry)->CNN_TRACOD, "", FLG_RORI})				
		EndIf
		(cAliasQry)->(dbSkip())
	EndDo
	(cAliasQry)->(dbCloseArea())
	
	If(Len(aTemp) > 0)		
		aTemp := FWSFAllUsers(aTemp)
		for nX:= 1 to Len(aTemp)
			//Armazena a estrutura dos usuarios e grupos
			//aUsr[1]     - Usuario/Grupo
			//aUsr[1,1]   - email do usuario
			//aUsr[1,2]   - Id da Tabela
			//aUsr[1,3]   - login
			//aUsr[1,4]   - Nome Completo		
			aAdd(aUsr, {aTemp[nX,5],aTemp[nX,2], aTemp[nX,3], aTemp[nX,4]})
			
			If((nPos := aScan(aUsrs[1], {|x| AllTrim(x[1]) == AllTrim(aTemp[nX,2]) })) > 0)
				aUsrs[1, nPos, 3] := aTemp[nX,4]
			EndIf
		next nX
		FwFreeArray(aTemp)
	EndIf
	
	For nX:= 1 to Len(aGroups)
		If(Len(aGroups[nX]) == 4)
			aDel(aGroups[nX]	, 1)
			aSize(aGroups[nX]	, 3)
			aGroups[nX] := { aGroups[nX] } //Adequa a estrutura do array pra compatibilidade entre as funcoes <AllGroups> e <FWSFAllGrps>
		Else
			Exit //Array ja compativel
		EndIf
	Next nX
	
	If(Len(aUsrs[2]) > 0)		
		For nX:= 1 to Len(aUsrs[2])			
			If((nPos := aScan(aGroups, {|x| AllTrim(x[1,1]) == AllTrim(aUsrs[2,nX, 1]) })) > 0)
				aUsrs[2, nX, 3] := aGroups[nPos, 1, 3]
			EndIf			
		Next nX		
	EndIf	
				
	RestArea(aArea)
Return aUsr

//-------------------------------------------------------------------
/*{Protheus.doc} ADDTREEPLAN
Adiciona as planilhas no Tree

@param oTree  - Objeto Tree
		cChave - Chave de pesquisa do registro da planilha

@author AИcio Ferreira Gomes
@since 11/10/2013
@version P11.90
*/
//-------------------------------------------------------------------
Static Function ADDTREEPLAN(oTree, cChave)
Local aArea 	:= GetArea()

dbSelectArea("CNA")
dbSetOrder(1)
If CNA->(dbSeek(cChave))
	While (!Eof() .And. cChave == CNA_FILIAL+CNA_CONTRA+CNA_REVISA)
		DBADDTREE oTree PROMPT CNA_NUMERO RESOURCE "" CARGO CNA_CONTRA+CNA_NUMERO
		ADDTREEFAUT(oTree, CNA_CONTRA+CNA_NUMERO)
		DBENDTREE oTree
		dbSkip()
	End
EndIf

CNA->(dbCloseArea())
RestArea(aArea)
Return

//-------------------------------------------------------------------
/*{Protheus.doc} ADDTREEFAUT
Faz a carga das filiais autorizadas e adiciona no item do tree ou
adiciona um item especifico no tree das filiais autorizadas

@param oTree  - Objeto Tree
		cCargo - Chave de pesquisa do registro
		lLoad 	- Indica que serА feito a carga das filiais para o tree
		cPlan  - Informe a planilha ao qual a filial pertence
		cFilAut- Informe a filiais que serА adicionado no Tree
		aInc - Array com as filiais inclusas
		aExc - Array com as filiais excluidas


@author AИcio Ferreira Gomes
@since 11/10/2013
@version P11.90
*/
//-------------------------------------------------------------------
Static Function ADDTREEFAUT(oTree, cCargo, lLoad, cPlan, cFilAut, aInc, aExc)
Local aArea := GetArea()

Default  lLoad := .T.

dbSelectArea("CPD")
dbSetOrder(1)

If lLoad
	dbSeek(xFilial("CPD")+cCargo)
	While (!Eof() .And. cCargo == CPD_CONTRA+CPD_NUMPLA)
		DBADDITEM oTree PROMPT CPD_FILAUT+" - " +FWFilialName(cEmpAnt,CPD_FILAUT) CARGO cCargo+CPD_FILAUT
		dbSkip()
	End
Else
	If !oTree:TreeSeek(cCargo+cFilAut)
		oTree:BeginUpdate()
		oTree:AddItem(cFilAut+" - " +FWFilialName(cEmpAnt,cFilAut),cCargo+cFilAut,,,,,2)
		oTree:EndUpdate()
		oTree:Refresh()
		If oTree:TreeSeek(cCargo+cFilAut)
			AADD(aInc, cCargo+cFilAut)

			If (nPos := aScan(aExc,{|x| Alltrim(x) == Alltrim(cCargo+cFilAut)})) > 0
				aDel(aExc, nPos)
				aSize(aExc, Len(aExc)-1)
			EndIf
		EndIf
	Endif
EndIf

CPD->(dbCloseArea())
RestArea(aArea)
Return

//-------------------------------------------------------------------
/*{Protheus.doc} CN240DelFil
Exclui o item da arvore de filiais autorizadas

@param oTree - Objeto Tree
		aInc - Array com as filiais inclusas
		aExc - Array com as filiais excluidas

@author AИcio Ferreira Gomes
@since 11/10/2013
@version P11.90
*/
//-------------------------------------------------------------------
Static Function CN240DelFil(oTree, aInc, aExc)
Local aArea		:= GetArea()
Local cCargo		:= ""
Local cNumPlan 	:= ""
Local cNumCTR 		:= ""
Local cNumFil		:= Space(FWGETTAMFILIAL)
Local lRet			:= .T.
Local nTamCTR		:= TamSX3("CPD_CONTRA")[1]
Local nTamPlan		:= TamSX3("CPD_NUMPLA")[1]

cNumPlan:= SubStr(cCargo,nTamCTR+1,nTamPlan)

//здддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Carrega chave do registro selecionado no dbTreeЁ
//юдддддддддддддддддддддддддддддддддддддддддддддддды
cCargo		:= oTree:GetCargo()
cNumCTR	:= SubStr(cCargo,1,nTamCTR)
cNumPlan	:= SubStr(cCargo,nTamCTR+1,nTamPlan)
cNumFil 	:= SubStr(cCargo,nTamCTR+nTamPlan+1,FWGETTAMFILIAL)

//здддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se foi selecionado grupo ou usuario   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддды
If !oTree:IsEmpty() .And. !Empty(cNumFil) .And. MsgYesNo(STR0063) //-- Deseja realmente excluir este item das filiais autorizadas para mediГЦo do contrato?
	oTree:BeginUpdate()
	If oTree:TreeSeek(cCargo)
		aAdd(aExc, cCargo)

		// Remove item do array de itens inclusos no tree
		If (nPos := aScan(aInc,{|x| Alltrim(x) == Alltrim(cCargo)})) > 0
			aDel(aInc, nPos)
			aSize(aInc, Len(aInc)-1)
		EndIf

		oTree:DelItem()
	EndIf
	oTree:EndUpdate()
	oTree:Refresh()
EndIf

RestArea(aArea)

Return

//-------------------------------------------------------------------
/*{Protheus.doc} CN240IncFil
Inclui um item na arvore de filiais autorizadas

@param oTree - Objeto Tree
		aInc - Array com as filiais inclusas
		aExc - Array com as filiais excluidas
		aPanel - Container onde o Dialog serА criada

@author AИcio Ferreira Gomes
@since 11/10/2013
@version P11.90
*/
//-------------------------------------------------------------------
Static Function CN240IncFil(oTree, aInc, aExc, oPanel)
Local oDlg			:= Nil
Local oFil			:= Nil
Local oDesc		:= Nil
Local aArea 		:= GetArea()
Local cCargo		:= oTree:GetCargo()
Local cCTRFIX		:= "0"
Local cNumPlan 	:= Space(TamSX3("CPD_NUMPLA")[1])
Local cNumFil		:= Space(FWGETTAMFILIAL)
Local cNomeFil		:= Space(30)
Local nTamCTR		:= TamSX3("CPD_CONTRA")[1]
Local nTamPlan		:= TamSX3("CPD_NUMPLA")[1]
Local nOpcX		:= 0
Local oSize

cNumPlan:= SubStr(cCargo,nTamCTR+1,nTamPlan)

DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0067) From 000,000 TO 150,450 PIXEL OF oPanel //"Fliais Autorizadas - Incluir"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Calcula dimensУes                                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oSize := FwDefSize():New(.T.,,,oDlg)
oSize:AddObject( "CABECALHO",  100, 100, .T., .T. ) // Totalmente dimensionavel

oSize:lProp 	:= .T. // Proporcional
oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3

oSize:Process() 	   // Dispara os calculos

@ oSize:GetDimension("CABECALHO","LININI")+2 	,oSize:GetDimension("CABECALHO","COLINI") SAY STR0068 OF oDlg PIXEL//"Filial"
@ oSize:GetDimension("CABECALHO","LININI") 	,oSize:GetDimension("CABECALHO","COLINI")+30 MSGET cNumFil F3 "SM0EMP" Valid ExistCpo("SM0",cEmpAnt+cNumFil) Size 60,5 OF  oDlg PIXEL PICTURE "@!"
@ oSize:GetDimension("CABECALHO","LININI")+22	,oSize:GetDimension("CABECALHO","COLINI") SAY STR0069 OF oDlg PIXEL//"DescriГЦo"
@ oSize:GetDimension("CABECALHO","LININI")+20	,oSize:GetDimension("CABECALHO","COLINI")+30 MSGET IIF( !Empty( AllTrim(cNumFil) ), FWFilialName(cEmpAnt,cNumFil), cNomeFil ) WHEN .F. Size 150,5 OF oDlg PIXEL

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar (oDlg, {|| nOpcX := 1, oDlg:End()},{|| nOpcX := 2, oDlg:End()}) CENTERED

If nOpcX == 1
	If cCTRFIX == '1'
		CNA->(dbSetOrder(1))
		If CNA->(dbSeek(xFilial("CNA")+CN9->CN9_NUMERO+CN9->CN9_REVISA+cNumPlan))
			If oTree:TreeSeek(CN9->CN9_NUMERO+cNumPlan)
				ADDTREEFAUT(oTree, CN9->CN9_NUMERO+cNumPlan, .F., CNA->CNA_NUMERO, cNumFil, @aInc, @aExc)
			EndIf
		EndIf
	Else
		If oTree:TreeSeek(FLG_ARVF)
			ADDTREEFAUT(oTree, CN9->CN9_NUMERO+cNumPlan, .F., CNA->CNA_NUMERO, cNumFil, @aInc, @aExc)
		Endif
	EndIf
EndIf

RestArea(aArea)

Return

//-------------------------------------------------------------------
/*{Protheus.doc} CN240DelFil
Grava alteraГЦo nas filiais autorizadas x contrato

@param aInc - Array com as filiais inclusas
		aExc - Array com as filiais excluidas

@author AИcio Ferreira Gomes
@since 14/10/2013
@version P11.90
*/
//-------------------------------------------------------------------
Static Function CN240GrvCPD(aInc, aExc)
Local aArea	:= GetArea()
Local nX 		:= 0
Local nTamCTR	:= TamSX3("CPD_CONTRA")[1]
Local nTamPla	:= TamSX3("CPD_NUMPLA")[1]

dbSelectArea("CPD")
dbSetOrder(1)

Begin Transaction

// Inclui os novos registros na CPD( Filiais autorizadas x contrato)
For nX := 1 To Len(aInc)
	If !dbSeek(xFilial("CPD")+aInc[nX])
		Reclock("CPD", .T.)
		CPD_FILIAL := xFilial("CPD")
		CPD_CONTRA := SubStr(aInc[nX],1,nTamCTR)
		CPD_NUMPLA := SubStr(aInc[nX],nTamCTR+1,nTamPla)
		CPD_FILAUT := SubStr(aInc[nX],nTamCTR+nTamPla+1,FWGETTAMFILIAL)
		MsUnlock()
	EndIf
Next nX

// Exclui os registros da CPD( Filiais autorizadas x contrato)
For nX := 1 To Len(aExc)
	If dbSeek(xFilial("CPD")+aExc[nX])
		Reclock("CPD", .F.)
		dbDelete()
		MsUnlock()
	EndIf
Next nX

End Transaction

RestArea(aArea)
Return

//==============================================================================================================================
/*/{Protheus.doc} Cn240VldGr()
FunГЦo responsАvel por validar existЙncia do grupo selecionado no controle de acesso
@author Israel.Escorizza 
@since 08/02/2017 / 
@return
/*/
//==============================================================================================================================
Function Cn240VldGr(cTipo, cCodGen)
	Local lRet 		:= .F.
	Default cTipo	:= ""
	Default cCodGen := ""

	If !Empty(cTipo) .And. !Empty(cCodGen)
		If cTipo == 'USR'
			lRet := !Empty(UsrRetName(cCodGen))
		Else
			lRet := !Empty(GrpRetName(cCodGen))
		EndIf
	EndIf

	If !lRet
		Aviso("CNTA240",OemToAnsi(STR0058),{"Ok"})//"UsuАrio/Grupo inexistente, selecione um usuАrio vАlido."
	Else
		lRet := UsrGrpBloq(cCodGen, (cTipo == 'USR'))
	EndIf
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} Cn240HPOPUP
Habilita/desabilita opГУes dos MENUS POPUP

@param oTree, Objeto,  Objeto TREE
@param nX, NЗmerico,  Linha inicial
@param nY, NЗmerico,  Coluna inicial
@param oMenu, Objeto,  Objeto OMENU
@param cNomeMenu, Caracter, Nome do MENU posicionado
@return Nenhum
@author Eduardo Gomes JЗnior
@since 14/03/2018
/*/
//-------------------------------------------------------------------
Function Cn240HPOPUP( oTree, nX, nY, oMenu, cNomeMenu )

Local cCargo
Local nTamCTR		:= TamSX3("CPD_CONTRA")[1]
Local nTamPlan		:= TamSX3("CPD_NUMPLA")[1]
Local cNumFil		:= Space(FWGETTAMFILIAL)

cCargo		:= oTree:GetCargo()
cNumCTR		:= SubStr(cCargo,1,nTamCTR)
cNumPlan	:= SubStr(cCargo,nTamCTR+1,nTamPlan)
cNumFil 	:= SubStr(cCargo,nTamCTR+nTamPlan+1,FWGETTAMFILIAL)

//Tratamento para MENU de Acessos
If	cNomeMenu == "oMenuTree"

	AEval( oMenu:aItems, { |x| x:disable() } )
	
	If	SubStr(cCargo,1,1) == FLG_ARVC 
		oMenu:aItems[5]:enable()	//Copiar Estrutura
	Endif

	//UsuАrio
	If	SubStr(cCargo,1,1) == FLG_ARVU	.AND. Len(Alltrim(cCargo)) <= 1  
		oMenu:aItems[1]:enable()	//Incluir Usuario
	Endif
	
	If	SubStr(cCargo,1,1) == FLG_ARVU	.AND. Len(Alltrim(cCargo)) > 2  
		oMenu:aItems[3]:enable()	//Edita	
		oMenu:aItems[4]:enable()	//Exclui
	Endif

	//Grupos
	If	SubStr(cCargo,1,1) == FLG_ARVG	.AND. Len(Alltrim(cCargo)) <= 1
		oMenu:aItems[2]:enable()	//Incluir Grupo
	Endif
	
	If	SubStr(cCargo,1,1) == FLG_ARVG	.AND. Len(Alltrim(cCargo)) > 2  
		oMenu:aItems[3]:enable()	//Edita	
		oMenu:aItems[4]:enable()	//Exclui	
	Endif

Endif 

//Tratamento para MENU de Filiais Autorizadas para MediГЦo
If	cNomeMenu == "oMenuTreeFil"

	If	SubStr(cCargo,1,1) == FLG_ARVF .And. oTree:Nivel() < 3 //-- Desabilita menu para os 2 primeiros nМveis da tree
		oMenu:aItems[1]:disable()	//Incluir Filial
		oMenu:aItems[2]:disable()	//Excluir Filial
	Else
	
		If	Empty(cNumFil) 
			oMenu:aItems[1]:enable()	//Incluir Filial
			oMenu:aItems[2]:disable()	//Excluir Filial
		Else
			oMenu:aItems[1]:disable()	//Incluir Filial
			oMenu:aItems[2]:enable()	//Excluir Filial
		Endif 		
		 		
	Endif

Endif 	
	 	 
//Ativa o Menu PopUp
oMenu:Activate( nX, nY, oTree )
                                   
Return 

/*/{Protheus.doc} CN240SVld
FunГЦo para atribuir valor lСgico Ю variАvel estАtica lValida.

@param lVld, logical, Indica se deve validar acesso do usuАrio quando ExecAuto.
@Return Nil

@author juan.felipe
@since 19/08/2020
/*/
Function CN240SVld(lVld)
    lValida := lVld
Return Nil

/*/{Protheus.doc} CN240GVld
FunГЦo para retornar valor lСgico da variАvel estАtica lValida.

@Return lValida, logical, Indica se deve validar acesso do usuАrio quando ExecAuto.

@author juan.felipe
@since 19/08/2020
/*/
Function CN240GVld()
Return lValida

/*/{Protheus.doc} UsrGrpBloq
	Valida se o usuАrio ou grupo informado em <cCodigo> estА bloqueado.
@author philipe.pompeu
@since 23/03/2022
@param cCodigo, caractere, cСdigo do grupo ou usuАrio
@param lIsUser, lСgico, se И um usuАrio
@return lValido, lСgico, se И vАlido
*/
Static Function UsrGrpBloq(cCodigo as Character, lIsUser as Logical) as Logical
	Local lValido	:= .T.
	Local lBloqueado:= .F.
	Default lIsUser	:= .T.

	lBloqueado	:= IIF(lIsUser, CNIsUsrBlq(cCodigo), CNIsGrpBlq(cCodigo))
	lValido 	:= !lBloqueado

	If !lValido
		Help("", 1, "A240REGBLQ",,STR0072, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0073}) //UsuАrio/Grupo bloqueado, realize o desbloqueio.
	EndIf
Return lValido

/*/{Protheus.doc} CNIsUsrBlq
	Retorna se <cCodUser> И um usuАrio bloqueado.
@author philipe.pompeu
@since 23/03/2022
@param cCodUser, caractere, cСdigo usuАrio
@return lBloqueado, lСgico, se estА bloqueado
*/
Function CNIsUsrBlq(cCodUser as Character) as Logical
	Local lBloqueado:= .F.
	Local aUsrInfo	:= {}
	Default cCodUser := RetCodUsr()

	aUsrInfo:= aTail(FWSFAllUsers({cCodUser}, {"USR_MSBLQL"}))
	
	If (Len(aUsrInfo) >= 3)		
		lBloqueado	:= (aUsrInfo[3] == "1")//aUsrInfo[1] = RecNo do UsuАrio / aUsrInfo[2] = Id do UsuАrio / aUsrInfo[3] = campo solicitado(USR_MSBLQL)
	EndIf
	
	FwFreeArray(aUsrInfo)
Return lBloqueado

/*/{Protheus.doc} CNIsGrpBlq
	Retorna se <cCodGrp> И um grupo bloqueado.
@author philipe.pompeu
@since 23/03/2022
@param cCodGrp, caractere, cСdigo do grupo
@return lBloqueado, lСgico, se estА bloqueado
*/
Function CNIsGrpBlq(cCodGrp as Character) as Logical
	Local lBloqueado:= .F.
	Local aGrpInfo	:= ""

	aGrpInfo := FWGrpParam(cCodGrp)[1]
	
	lBloqueado	:= (aGrpInfo[3] == "1")
	
	FwFreeArray(aGrpInfo)
Return lBloqueado

/*/{Protheus.doc} HasUsrPerm
	Confere se hА usuАrios nЦo bloqueados com permissЦo total ou de concessЦo de acessos para <cContra>
@author philipe.pompeu
@since 25/05/2022
@param cContra, caractere, cСdigo do contrato
@return lTemUsr, lСgico, se hА usuАrios com permissЦo
*/
Static Function HasUsrPerm(cContra)
	Local aArea		:= GetArea()
	Local lTemUsr 	:= .F.
	Local cMyAlias	:= GetNextAlias()
	Local cUsrVazio	:= Space(CNN->(Len(CNN_USRCOD)))
	Local cPermTotal:= DEF_TRANS
	Local cPermAcess:= DEF_CTRACS

	BeginSql alias cMyAlias
		SELECT DISTINCT(CNN_USRCOD) CNN_USRCOD
		FROM %table:CNN% CNN
		WHERE
		CNN_FILIAL = %xFilial:CNN%
		AND CNN_CONTRA = %Exp:cContra%
		AND CNN_USRCOD <> %Exp:cUsrVazio%
		AND CNN_TRACOD IN (%Exp:cPermTotal%, %Exp:cPermAcess%)
		AND CNN.%notDel%
	EndSql
	
	If (cMyAlias)->(!EOF())
		While (cMyAlias)->(!EOF())
			If !(CNIsUsrBlq((cMyAlias)->CNN_USRCOD))
				lTemUsr := .T.
				Exit
			EndIf			
			(cMyAlias)->(DbSkip())
		EndDo
	EndIf
	(cMyAlias)->(dbCloseArea())

	RestArea(aArea)
	FwFreeArray(aArea)
Return lTemUsr
