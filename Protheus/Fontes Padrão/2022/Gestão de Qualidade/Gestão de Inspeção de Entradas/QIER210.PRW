#INCLUDE "QIER210.CH"
#INCLUDE "PROTHEUS.CH"


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё Qier210  Ё Autor Ё Marcos Cesar          Ё Data Ё 15/01/99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Relatorio de Permissao de Uso.                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Qier210()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё			ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.			  Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data	Ё BOPS Ё  Motivo da Alteracao 					  Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁVera        Ё14/04/99Ё------Ё Inclusao da Loja do Fornecedor           Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁAntonio     Ё24/08/99ЁддддддЁ Inclusao funcao QA_AjusSX1() (QAXFUN.PRX)Ё╠╠
╠╠Ё            Ё        Ё      Ё Esta funcao corresponde a versao genericaЁ╠╠ 
╠╠Ё            Ё        Ё      Ё da AjustaSX1                             Ё╠╠ 
╠╠ЁPaulo EmidioЁ18/05/00Ё------ЁRetirada da Funcao de ajuste do SX1       Ё╠╠
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function Qier210()

Local wnrel   := "QIER210"
Local cString := "QEK"
Local cDesc1  := OemToAnsi(STR0001)  // "Impressao da Permissao de Uso."
Local cDesc2  := ""
Local cDesc3  := ""

Private cTitulo  := OemToAnsi(STR0002)  // "Relatorio de Permissao de Uso"
Private cTamanho := "M"
Private aReturn  := { STR0003, 1, STR0004, 1, 2, 1, "", 1 }  // "Zebrado"###"Administracao"
Private nLastKey := 0
Private cPerg    := "QER210"
Private NomeProg := "QIER210"
Private Cabec1
Private Cabec2
Private nTipo
Private li       := 80
Private m_pag    := 1

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas p/ Parametros :                         Ё
//Ё                                                              Ё
//Ё mv_par01  // Fornecedor de                                   Ё
//Ё mv_par02  // Fornecedor ate                                  Ё
//Ё mv_par03  // Loja Fornecedor de                              Ё
//Ё mv_par04  // Loja Fornecedor ate                             Ё
//Ё mv_par05  // Produto de                                      Ё
//Ё mv_par06  // Produto ate                                     Ё
//Ё mv_par07  // Data de Entrada de                              Ё
//Ё mv_par08  // Data de Entrada ate                             Ё
//Ё mv_par09  // Lote de                                         Ё
//Ё mv_par10  // Lote ate                                        Ё
//Ё mv_par11  // Permissao Uso de                                Ё
//Ё mv_par12  // Permissao Uso ate                               Ё
//Ё mv_par13  // Status da Permissao (Aprovado   / Reprovado /   Ё
//Ё                                   Em Analise / Todos         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Pergunte(cPerg, .F.)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Envia Controle p/ a Funcao SetPrint.                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
wnrel := SetPrint(cString,wnrel,cPerg,@ctitulo,cDesc1,cDesc2,cDesc3,.F.,"",.T.,cTamanho)

If nLastKey == 27
	Set Filter To
	Return Nil
EndIf

SetDefault(aReturn,cString)

If nLastKey == 27
	Set Filter To
	Return Nil
EndIf

nTipo := Iif(aReturn[8] == 1, 15, 18)

RptStatus({|lEnd| R210Imp(@lEnd,wnRel,cString)},cTitulo)

Return Nil

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё R210Imp  Ё Autor Ё Marcos Cesar          Ё Data Ё 15/01/99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Impressao da Permissao de Uso.                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё R210Imp(ExpL1,ExpC1,ExpC2)                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpL1 : A┤фo do CodeBlock                                  Ё╠╠
╠╠Ё          Ё ExpC1 : T║tulo do relat╒rio                                Ё╠╠
╠╠Ё          Ё ExpC2 : Mensagem                                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function R210Imp(lEnd,wnRel,cString)

Local cbtxt  := Space(10)
Local cbcont := 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Parametros.                                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local cFornecDe  := mv_par01
Local cFornecAte := mv_par02
Local cLojForDe  := mv_par03
Local cLojForAte := mv_par04
Local cProdutDe  := mv_par05
Local cProdutAte := mv_par06
Local dDatEntDe  := mv_par07
Local dDatEntAte := mv_par08
Local cLoteDe    := mv_par09
Local cLoteAte   := mv_par10
Local cPermisDe  := mv_par11
Local cPermisAte := mv_par12
Local cStatus    

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Dados da Entrada.                                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local cFornecAnt
Local cLojForAnt
Local cProdutAnt
Local dDatEntAnt
Local cLoteAnt

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Identifica o Status Selecionado pelo Usuario.                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If mv_par13 == 1
	cStatus := "A"  // Aprovado

ElseIf mv_par13 == 2
	cStatus := "R"  // Reprovado

ElseIf mv_par13 == 3
	cStatus := "E"  // Em Analise

Else
	cStatus := "T"  // Todos os Status
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Pesquisa o Arquivo QF2 (Entradas).                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("QF2")
dbSetOrder(1)
SetRegua(LastRec())
If !Empty(cFornecDe) .And. !Empty(cProdutDe) .And. !Empty(dDatEntDe) .And.;
		!Empty(cLoteDe) .And. !Empty(cLojForde)
	dbSeek(xFilial("QF2")+cFornecDe+cLojForDe+cProdutDe+Dtos(dDatEntDe)+cLoteDe,.T.)
Else
	dbSeek(xFilial("QF2"))
EndIf

While QF2->QF2_FILIAL == xFilial("QF2") .And. QF2->QF2_FORNEC <= cFornecAte .And.;
	QF2->QF2_LOJFOR <= cLojForAte .And. QF2->QF2_PRODUT <= cProdutAte .And.;
	QF2->QF2_DTENTR <= dDatEntAte .And. QF2->QF2_LOTE   <= cLoteAte .And. QEF->(!Eof())

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Dados da Entrada.                                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	cFornecAnt := QF2->QF2_FORNEC
	cLojForAnt := QF2->QF2_LOJFOR
	cProdutAnt := QF2->QF2_PRODUT
	dDatEntAnt := QF2->QF2_DTENTR
	cLoteAnt   := QF2->QF2_LOTE

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Quebra por Entrada.                                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	While QF2->QF2_FILIAL == xFilial("QF2") .And. QF2->QF2_FORNEC == cFornecAnt .And.;
		QF2->QF2_LOJFOR == cLojForAnt .And. QF2->QF2_PRODUT == cProdutAnt .And.;
		QF2->QF2_DTENTR == dDatEntAnt .And. QF2->QF2_LOTE   == cLoteAnt .And. QF2->(!Eof())

		IncRegua()

		If QF2->QF2_NUMERO >= cPermisDe .And. QF2->QF2_NUMERO <= cPermisAte .And.;
			 (QF2->QF2_STATUS == cStatus .Or. cStatus == "T")
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Monta as Variaveis do Cabecalho Padrao Microsiga.            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			Cabec1 :=          "Fornecedor : "   + AllTrim(QF2->QF2_FORNEC)+"-"+QF2->QF2_LOJFOR+Space(2)
			Cabec1 := Cabec1 + "Produto : "      + AllTrim(QF2->QF2_PRODUT) + Space(5)
			Cabec1 := Cabec1 + "Data Entrada : " + Dtoc(QF2->QF2_DTENTR)    + Space(5)
			Cabec1 := Cabec1 + "Lote : "         + AllTrim(QF2->QF2_LOTE)
			Cabec2 := "Permissao de Uso : " + QF2->QF2_NUMERO
			If QF2->QF2_STATUS == "A"
				Cabec2+= " - Aprovada"
			Elseif QF2->QF2_STATUS == "R"
				Cabec2+= " - Reprovada"
			Elseif QF2->QF2_STATUS == "E"
				Cabec2+= " - Em aberto"
			EndIf

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Impressao do Cabecalho.                                      Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			Cabec(cTitulo,Cabec1,Cabec2,NomeProg,cTamanho,nTipo)

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Impressao do Solicitante da Permissao de Uso.                Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			R210Solici()

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Impressao das Recomendacoes.                                 Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			R210Recome()

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Impressao dos Responsaveis pela Aprovacao.                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			R210Respon()
		EndIf

		dbSelectArea("QF2")
		dbSkip()
	End  // Quebra por Entrada

	dbSelectArea("QF2")
End

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impressao do Rodape.                                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If li <> 80
	Roda(cbcont, cbtxt)
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Spool de Impressao.                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If aReturn[5] == 1
	Set Printer To
	Commit
	OurSpool(wnrel)
EndIf

MS_FLUSH()

Return Nil

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё R210Solici Ё Autor Ё Marcos Cesar        Ё Data Ё 15/01/99 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Impressao do Solicitante da Permissao de Uso.              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё R210Solici()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function R210Solici()

Local nA := 0

@ li,000 PSAY "A - Solicitante :"

li := li + 2
@ li,000 PSAY "|" + Replicate("-",128) + "|"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impressao do Nome do Solicitante.                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li := li + 1
@ li,000 PSAY "| Solicitada por : " + R210Nome(QF2->QF2_FILMAT,QF2->QF2_MAT)
@ li,129 PSAY "|"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impressao do Nome da Area do Solicitante.                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li := li + 1
@ li,000 PSAY "| Processo       : " + R210Area(QF2->QF2_MAT)
@ li,129 PSAY "|"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impressao do Motivo da Inclusao da Permissao de Uso.         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li := li + 1
@ li,000 PSAY "|"
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "| Motivo : "
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "|"
@ li,129 PSAY "|"

dbSelectArea("QA2")
dbSetOrder(1)
dbSeek(xFilial() + "Inclusao" + QF2->QF2_CHAVE)

If Found()
	While (QA2->QA2_FILIAL == xFilial("QA2")) .And. (QA2->QA2_ESPEC  == "Inclusao")    .And.;
		(QA2->QA2_CHAVE  == QF2->QF2_CHAVE) .And. !Eof()
		li := li + 1
		@ li,000 PSAY "| " + QA2->QA2_TEXTO
		@ li,129 PSAY "|"
		dbSelectArea("QA2")
		dbSkip()
	End
Else
	For nA := 1 To 3
		li := li + 1
		@ li,000 PSAY "| " + Replicate(".", 125)
		@ li,129 PSAY "|"
	Next nA
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Assinatura e Data.                                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li := li + 1
@ li,000 PSAY "|"
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "| " + "Ass.: .........................................................."
@ li,108 PSAY "Data : " + Dtoc(QF2->QF2_DATENT)
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "|" + Replicate("-",128) + "|"

Return Nil

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё R210Recome Ё Autor Ё Marcos Cesar        Ё Data Ё 15/01/99 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Impressao das Recomendacoes.                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё R210Recome()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function R210Recome()

Local lResponsavel := .F.
Local nA           := 0

li := li + 2
@ li,000 PSAY "B - Recomendacoes :"

li := li + 2
@ li,000 PSAY "|" + Replicate("-",128) + "|"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Pesquisa Arquivo QF3 (Destinatarios) p/ Obter o Responsavel. Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("QF3")
dbSetOrder(1)
dbSeek(xFilial()+QF2->QF2_FORNEC+QF2->QF2_LOJFOR+QF2->QF2_PRODUT+;
		Dtos(QF2->QF2_DTENTR)+QF2->QF2_LOTE+QF2->QF2_NUMERO)

While QF3->QF3_FILIAL == xFilial() .And.;
		QF3->QF3_FORNEC == QF2->QF2_FORNEC .And.;
		QF3->QF3_LOJFOR == QF2->QF2_LOJFOR .And.;
		QF3->QF3_PRODUT == QF2->QF2_PRODUT .And.;
		QF3->QF3_DTENTR == QF2->QF2_DTENTR .And.;
		QF3->QF3_LOTE   == QF2->QF2_LOTE   .And.;
		QF3->QF3_NUMERO == QF2->QF2_NUMERO

	If QF3->QF3_MAT <> QF2->QF2_MATQLD
		lResponsavel := .T.

		Exit
	EndIf

	dbSelectArea("QF3")
	dbSkip()
End

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impressao do Nome do Responsavel.                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li := li + 1
@ li,000 PSAY "| Responsavel : " + Iif(lResponsavel, R210Nome(QF3->QF3_FILMAT,QF3->QF3_MAT), Space(1))
@ li,129 PSAY "|"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impressao do Nome da Area do Responsavel.                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li := li + 1
@ li,000 PSAY "| Processo    : " + Iif(lResponsavel, R210Area(QF3->QF3_MAT), Space(1))
@ li,129 PSAY "|"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impressao da Acao Recomendada p/ o Lote.                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li := li + 1
@ li,000 PSAY "|"
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "| Acao Recomendada p/ o Lote : "
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "|"
@ li,129 PSAY "|"

dbSelectArea("QA2")
dbSetOrder(1)
dbSeek(xFilial() + "Acao Rec" + QF2->QF2_CHAVE1)

If Found()
	While QA2->QA2_FILIAL == xFilial()       .And.;
			QA2->QA2_ESPEC  == "Acao Rec"      .And.;
			QA2->QA2_CHAVE  == QF2->QF2_CHAVE1 .And. !Eof()
		li := li + 1
		@ li,000 PSAY "| " + QA2->QA2_TEXTO
		@ li,129 PSAY "|"

		dbSelectArea("QA2")
		dbSkip()
	End
Else
	For nA := 1 To 2
		li := li + 1
		@ li,000 PSAY "| " + Replicate(".", 125)
		@ li,129 PSAY "|"
	Next nA
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Impressao da Acao Corretiva p/ o Problema.                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li := li + 1
@ li,000 PSAY "|"
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "| Acao Corretiva p/ o Problema : "
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "|"
@ li,129 PSAY "|"

dbSelectArea("QA2")
dbSetOrder(1)
dbSeek(xFilial() + "Acao Cor" + QF2->QF2_CHAVE1)

If Found()
	While QA2->QA2_FILIAL == xFilial()       .And.;
			QA2->QA2_ESPEC  == "Acao Cor"      .And.;
			QA2->QA2_CHAVE  == QF2->QF2_CHAVE1 .And. !Eof()
		li := li + 1
		@ li,000 PSAY "| " + QA2->QA2_TEXTO
		@ li,129 PSAY "|"

		dbSelectArea("QA2")
		dbSkip()
	End
Else
	For nA := 1 To 2
		li := li + 1
		@ li,000 PSAY "| " + Replicate(".", 125)
		@ li,129 PSAY "|"
	Next nA
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Assinatura e Data.                                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
li := li + 1
@ li,000 PSAY "|"
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "| " + "Ass.: .........................................................."
@ li,108 PSAY "Data : " + Dtoc(QF3->QF3_DTGERA)
@ li,129 PSAY "|"

li := li + 1
@ li,000 PSAY "|" + Replicate("-",128) + "|"

Return Nil

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё R210Respon Ё Autor Ё Marcos Cesar        Ё Data Ё 15/01/99 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Impressao dos Responsaveis pela Aprovacao.                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё R210Respon()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function R210Respon()

Local cCab1 := ""
Local cCab2 := ""
Local cCab3 := ""
Local cCab4 := ""
Local cCab5 := ""

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Definicao do Cabecalho dos Responsaveis pela Aprovacao.      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
//                  1         2         3         4         5         6         7         8         9        10        11        12        13        14
//        012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

cCab1 := "|--------------------------------------------------------------------------------------------------------------------------------|"
cCab2 := "| Nome                               | Unidade Negocio - Area        |Recons.  |    Data      | Assinatura                       |"
cCab3 := "|                                    |                               |---------|              |                                  |"
cCab4 := "|                                    |                               |Sim |Nao |              |                                  |"
cCab5 := "|================================================================================================================================|"                   

//        | xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   | xxxxxxxxxxxxxxxxxxxxxxxxx     | x  | x  |  99/99/9999  |									       |

li := li + 2
@ li,000 PSAY "C - Responsavel pela Aprovacao :"

li := li + 2
@ li,000 PSAY cCab1

li := li + 1
@ li,000 PSAY cCab2

li := li + 1
@ li,000 PSAY cCab3

li := li + 1
@ li,000 PSAY cCab4

li := li + 1
@ li,000 PSAY cCab5

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Pesquisa Arquivo QF3 (Destinatarios) p/ Obter o Responsavel. Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("QF3")
dbSetOrder(1)
dbSeek(xFilial()+QF2->QF2_FORNEC+QF2->QF2_LOJFOR+QF2->QF2_PRODUT+;
		Dtos(QF2->QF2_DTENTR)+QF2->QF2_LOTE+QF2->QF2_NUMERO)

While QF3->QF3_FILIAL == xFilial() .And.;
		QF3->QF3_FORNEC == QF2->QF2_FORNEC .And.;
		QF3->QF3_LOJFOR == QF2->QF2_LOJFOR .And.;
		QF3->QF3_PRODUT == QF2->QF2_PRODUT .And.;
		QF3->QF3_DTENTR == QF2->QF2_DTENTR .And.;
		QF3->QF3_LOTE   == QF2->QF2_LOTE   .And.;
		QF3->QF3_NUMERO == QF2->QF2_NUMERO

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Impressao do Cabecalho.                                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If li > 80
		Cabec(cTitulo,Cabec1,Cabec2,NomeProg,cTamanho,nTipo)

		li := li + 2
		@ li,000 PSAY "C - Responsavel pela Aprovacao :"

		li := li + 2
		@ li,000 PSAY cCab1

		li := li + 1
		@ li,000 PSAY cCab2

		li := li + 1
		@ li,000 PSAY cCab3

		li := li + 1
		@ li,000 PSAY cCab4

		li := li + 1
		@ li,000 PSAY cCab5
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Impressao do Responsavel.                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	li := li + 1
	@ li,000 PSAY "| " + R210Nome(QF3->QF3_FILMAT,QF3->QF3_MAT)
	@ li,037 PSAY "| " + R210Area(QF3->QF3_FILMAT,QF3->QF3_MAT)
	@ li,069 PSAY "| " + Iif(QF3->QF3_STATUS == "A", "x", " ")
	@ li,074 PSAY "| " + Iif(QF3->QF3_STATUS == "R", "x", " ")
	@ li,079 PSAY "| " + Dtoc(QF3->QF3_DTBAIX)
	@ li,094 PSAY "|"
	@ li,129 PSAY "|"

	li := li + 1
	@ li,000 PSAY "|" + Replicate("-",128) + "|"

	dbSelectArea("QF3")
	dbSkip()
End

Return Nil

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё R210Nome   Ё Autor Ё Marcos Cesar        Ё Data Ё 15/01/99 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Obtem o Nome do Funcionario.                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё R210Nome(cFilUsu,cMatricula)								  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 := Filial do usuario								  Ё╠╠
╠╠Ё			 Ё ExpC2 := Matricula do Funcionario                          Ё╠╠				  
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC2 : Nome do Funcionario                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function R210Nome(cFilUsu,cMatricula)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Pesquisa o Arquivo QAA (Cadastro de Funcionarios).           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("QAA")
dbSetOrder(1)
dbSeek(cFilUsu+cMatricula)

Return(QAA->QAA_NOME)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё R210Area   Ё Autor Ё Marcos Cesar        Ё Data Ё 15/01/99 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Obtem o Nome da Area (Departamento) do Funcionario.        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC2 := R210Area(ExpC1)                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 : Matricula do Funcionario                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC2 : Nome da Area (Departamento) do Funcionario         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function R210Area(cMatricula)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Pesquisa o Arquivo QAA (Cadastro de Funcionarios).           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("QAA")
dbSetOrder(1)
dbSeek(xFilial('QAA')+cMatricula)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Pesquisa o Arquivo QAD (Centro de Custo).                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("QAD")
dbSetOrder(1)
dbSeek(xFilial('QAD')+QAA->QAA_CC)

Return(QAD->QAD_DESC)