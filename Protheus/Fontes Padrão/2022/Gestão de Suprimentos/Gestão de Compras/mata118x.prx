#INCLUDE "PROTHEUS.CH"

/*


Ŀ
FUNCAO     MATA118X  Autor                         Data             
Ĵ
DESCRICAO  Funcoes do programa MATA118 - Nfiscal de Desp.Importacao.    
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                     
Ĵ
Marcos Simidu 29/06/98XXXXXXAcertos para NF com 12 bytes na exclusao- 
                            limpeza do campo F1_NFORI.                
Ana Claudia   22/12/98XXXXXXInclusao de IFDEF SHELL.                  
Fernando Joly 13/01/99XXXXXXPermitir a gravao de Movimentaes "DE6"
                            em Almoxarifados  diferentes do B1_LOCPAD,
                            na funo a118GravaD3.                    
Fernando Joly 03.02.98META  Correo na gravao do SD7 (Retirado o   
                            D7_UM e Incluido o D7_TIPOCQ).            
Rodrigo Sartor09/02/99xxxxxxAcerto na IndRegua p/ TOPCONNECT com AS400
Rodrigo Sart  25/02/99META  Revisao Rastreabilidade                   
Cesar Valadao 08/11/9919336ACorrecao na gravacao do SD7 (Incluido o   
                            D7_NUMLOTE e D7_LOTECTL).                 
Cesar Valadao 22/11/9925057ACorrecao de Duplicidade de Regs no SD3.   
                            Revisao de Entradas e Saidas no CQ.       
Patricia Sal. 01/03/00XXXXXXA118GravaCQ():Gravar os campos D7_PRODUTO,
                            D7_DOC,D7_SERIE,D7_FORNECE,D7_LOJA ao in- 
                            ves do D7_CHAVE.                           
ٱ




Ŀ
Funo     A118VALOR Autor  Cristina Ogura          Data  20/03/95 
Ĵ
Descrio  Checa o valor do frete se esta zerado       	               
Ĵ
Uso        MATA118                                                     
ٱ


*/
FUNCTION A118VALOR()
LOCAL x

x:= &(ReadVar())

If x <= 0
	Help(" ",1,"A118VL")
	lRet := .f.
Else
	lRet := .t.
Endif

Return (lRet)

/*


Ŀ
Funo     A118TES   Autor Rodrigo de A. Sartorio   Data  28/05/96 
Ĵ
Descrio  Checa se a TES digitada existe ou no.      	               
Ĵ
Uso        MATA118                                                     
ٱ


*/
FUNCTION A118TES()
LOCAL lRet:=.T.
If !(SF4->(dbSeek(xFilial("SF4")+&(ReadVar()))))
	Help(" ",1,".MTA11801.")
	lRet:=.F.
EndIf
Return lRet

/*


Ŀ
Funo     A118For   Autor  Ary Medeiros            Data  01/06/95 
Ĵ
Descrio  Retorna condicao para indexacao condicional                 
Ĵ
Uso        MATA118                                                     
ٱ


*/
Function A118For()

Local cCondicao

cCondicao:='F1_FILIAL=="'+xFilial('SF1')+'".AND.'
#IFDEF SHELL
	cCondicao+='F1_CANCEL!="N" .AND.'
#ENDIF
cCondicao+='DTOS(F1_DTDIGIT)>="'+DTOS(aPar[01])+'".AND.DTOS(F1_DTDIGIT)<="'+DTOS(aPar[2])+'".AND.'

If aPar[3]==1 	// Estorno
	cCondicao+='F1_TIPO=="C".AND.F1_ORIGLAN==" D"'
Else			// Geracao
	cCondicao+='F1_TIPO$"'+If(aPar[6]==1,"N","BD")+'"'
Endif
If !Empty(aPar[4]).And.!Empty(aPar[5])
	cCondicao+='.AND.F1_FORNECE=="'+aPar[4]+'".AND.F1_LOJA=="'+aPar[5]+'"'
Endif

Return cCondicao
/*


Ŀ
Funo    A118GrSF8  Autor Rodrigo de A. Sartorio   Data  10/05/96 
Ĵ
Descrio  Funcao que grava a NF de Conhecimento de Frete no SF8       
Ĵ
Uso        MATA118                                                     
ٱ


*/
Function A118GrSF8(aREFs,aPosSF1,lMaxItem)
//Ŀ
// Define Variaveis                                             
//
LOCAL cAlias:=Alias(),nAux01:=0,nAux02:=0,nAux03:=0

// 1) Codigo do Produto
// 2) Codigo da OP
// 3) Array de NFs Originais no formato { Numero,Serie,Fornecdor,Loja }
// 4) Numero da Nota Fiscal Gerada
// 5) Serie  da Nota Fiscal Gerada
dbSelectArea("SF8")
dbSetOrder(1)

If lMaxItem	// Mais itens que MV_NUMITEN
	For nAux01:=1 To Len(aREFs)
		
		For nAux02:=1 To Len(aREFs[nAux01,3])
			dbSelectArea("SF8")
			If !dbSeek(xFilial()+aREFs[nAux01,4]+aREFs[nAux01,5]+aREFs[nAux01,3,nAux02,3]+aREFs[nAux01,3,nAux02,4],.F.)
				RecLock("SF8",.T.)
				Replace F8_FILIAL 	With xFilial()
				Replace F8_NFDIFRE 	With aREFs[nAux01,4]
				Replace F8_SEDIFRE 	With aREFs[nAux01,5]
				Replace F8_TRANSP		With ca100For
				Replace F8_LOJTRAN 	With cLoja
				Replace F8_DTDIGIT 	With dDEmissao
				Replace F8_NFORIG		With aREFs[nAux01,3,nAux02,1]
				Replace F8_SERORIG 	With aREFs[nAux01,3,nAux02,2]
				Replace F8_FORNECE 	With aREFs[nAux01,3,nAux02,3]
				Replace F8_LOJA		With aREFs[nAux01,3,nAux02,4]
				Replace F8_TIPO		With "D"
				MsUnlock()
			ElseIf !(F8_NFORIG+F8_SERORIG)==(aREFs[nAux01,3,nAux02,1]+aREFs[nAux01,3,nAux02,2])
				RecLock("SF8",.T.)
				Replace F8_FILIAL 	With xFilial()
				Replace F8_NFDIFRE 	With aREFs[nAux01,4]
				Replace F8_SEDIFRE 	With aREFs[nAux01,5]
				Replace F8_TRANSP		With ca100For
				Replace F8_LOJTRAN 	With cLoja
				Replace F8_DTDIGIT 	With dDEmissao
				Replace F8_NFORIG		With aREFs[nAux01,3,nAux02,1]
				Replace F8_SERORIG 	With aREFs[nAux01,3,nAux02,2]
				Replace F8_FORNECE 	With aREFs[nAux01,3,nAux02,3]
				Replace F8_LOJA		With aREFs[nAux01,3,nAux02,4]
				Replace F8_TIPO		With "D"
				MsUnlock()
			Endif
		Next
	Next
Else
	For nAux03:=1 To Len(aPosSF1)
		dbSelectArea("SF1")
		dbGoto(aPosSF1[nAux03])
		
		dbSelectArea("SF8")
		If !dbSeek(xFilial()+SF1->F1_DOC+SF1->F1_SERIE+SF1->F1_FORNECE+SF1->F1_LOJA) .AND. SF1->F1_DOC+SF1->F1_SERIE != MV_PAR05+MV_PAR06
			RecLock("SF8",.T.)
			Replace F8_FILIAL 	With xFilial()
			Replace F8_NFDIFRE 	With cNFiscal
			Replace F8_SEDIFRE 	With cSerie
			Replace F8_TRANSP		With ca100For
			Replace F8_LOJTRAN 	With cLoja
			Replace F8_DTDIGIT 	With dDEmissao
			Replace F8_NFORIG		With SF1->F1_DOC
			Replace F8_SERORIG 	With SF1->F1_SERIE
			Replace F8_FORNECE 	With SF1->F1_FORNECE
			Replace F8_LOJA		With SF1->F1_LOJA
			Replace F8_TIPO		With "D"
			MsUnlock()
		Endif
	Next
Endif

dbSelectArea(cAlias)

Return Nil

/*

Ŀ
Funo     A118Esto  Autor  Rodrigo de A. Sartorio Data  15.05.96 
Ĵ
Descrio  Faz a exclusao da Nota de Despesa de Importacao            
Ĵ
Uso        Mata118                                                    
ٱ


*/
Function A118Esto(cAlias,nReg,nOpcx)
LOCAL cOldAlias:=Alias(),nOldOrder:=IndexOrd()
LOCAL cSeek:=SF1->F1_DOC+SF1->F1_SERIE+SF1->F1_FORNECE+SF1->F1_LOJA
LOCAL cSeek2:=SF1->F1_DOC+SF1->F1_SERIE
LOCAL nDelSF8:=0
LOCAL aRecCQ:={},cLocCQ:=GetMV("MV_CQ")
Local nX         := 0

PRIVATE cArquivo := ""
PRIVATE lLancPad40:=lLancPad50:=lLancPad60:=.F.
PRIVATE lLancPad55:=lLancPad65:=lLancPad95:=.F.
PRIVATE nHdlPrv:=1,nTotal:=0,cLoteCom,nLinha:=2,nMoedaCor:=1
cAlias:="SD1"

//-- Array aRecCQ:
//-- [n, 1] = D1_NUMCQ
//-- [n, 2] = D1_COD
//-- [n, 3] = D1_LOCAL
//-- [n, 4] = {D1_DOC, D1_SERIE, D1_FORNECE, D1_LOJA} - (Para ser utilizado Idem ao D7_CHAVE)

dbSelectArea(cAlias)
dbSetOrder(1)
If dbSeek(xFilial()+cSeek)
	nReg:=Recno()
	While !Eof() .And. xFilial()+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA == xFilial()+cSeek
		If D1_LOCAL==cLocCQ
			aAdd(aRecCQ, {D1_NUMCQ, D1_COD, D1_LOCAL, {D1_DOC, D1_SERIE, D1_FORNECE, D1_LOJA}})
		Endif
		dbSkip()
	EndDo
	dbGoto(nReg)
	
	dbSelectArea(cAlias)
	A100Deleta(cAlias,nReg,nOpcx,.T.)
	
	If lmat118
		//Ŀ
		// Estorna movimento de custeio do CQ          
		//
		For nX := 1 to Len(aRecCQ)
			fEstoCQ8(aRecCQ[nX, 1], aRecCQ[nX, 2], aRecCQ[nX, 3], aRecCQ[nX, 4])
		Next nX	

		dbSelectArea("SF8")
		dbSetOrder(1)
		If dbSeek(xFilial()+cSeek2)
			Begin Transaction
			//Ŀ
			// Limpa o flag na NF original.                                 
			//
			dbSelectArea("SF8")
			Do While !Eof() .And. xFilial()+cSeek2 == F8_FILIAL+F8_NFDIFRE+F8_SEDIFRE
				If xFilial()+cSeek != F8_FILIAL+F8_NFDIFRE+F8_SEDIFRE+F8_TRANSP+F8_LOJTRAN
					dbSkip()
					Loop
				EndIf
				dbSelectArea("SF1")
				dbSetOrder(1)
				If dbSeek(xFilial()+SF8->F8_NFORIG+SF8->F8_SERORIG+SF8->F8_FORNECE+SF8->F8_LOJA,.F.)
					RecLock("SF1",.F.)
					Replace SF1->F1_ORIGLAN With IIF(Substr(SF1->F1_ORIGLAN,1,1) == "F","F ","  ")
					Replace SF1->F1_NFORIG  With Space(Len(SF1->F1_NFORIG))
					Replace SF1->F1_SERORIG With Space(03)
					MsUnlock()
				EndIf
				dbSelectArea("SF8")
				RecLock("SF8",.F.,.T.)
				dbDelete()
				nDelSF8++
				dbSkip()
			EndDo
			End Transaction
		Endif
	EndIf
EndIf
cAlias:=cOldAlias
dbSelectArea(cOldAlias)
dbSetOrder(nOldOrder)
Return

/*


Ŀ
Funo    A118EstD3  Autor  Cesar Eduardo Valadao   Data 16/11/1999
Ĵ
Descrio Estorna o lancamento na NF do SD3                            
ٱ


*/
Function A118EstD3(cNumCQ,cNumSeq,cLocal)
Local nRecOri:=0,i,nRecDest:=0
Local bCampo:={|nCPO| Field(nCPO)}
Local aOriDest:={}, j

dbSelectArea("SD3")
dbSetOrder(4)
If dbSeek(xFilial()+cNumSeq,.F.)
	While !Eof() .And. xFilial()+cNumSeq == D3_FILIAL+D3_NUMSEQ
		If D3_QUANT<=0
			AAdd(aOriDest, {Recno(), 0})
			dbSkip()
			If xFilial()==D3_FILIAL .And. cNumSeq==D3_NUMSEQ
				aOriDest[Len(aOriDest)][2] := Recno()
			Endif
		EndIf
		dbSkip()
		
		
	EndDo
Endif

For j := 1 To Len(aOriDest)
	nRecOri  := aOriDest[j][1]
	nRecDest := aOriDest[j][2]
	If nRecOri > 0
		dbSelectArea("SD3")
		dbGoto(nRecOri)
		RecLock("SD3",.F.)
		Replace D3_ESTORNO With "S"
		//Ŀ
		// Salva a integridade dos campos de Bancos de Dados       
		//
		For i := 1 to FCount()
			M->&(EVAL(bCampo,i)):=FieldGet(i)
		Next
		//Ŀ
		// Cria o registro de estorno com mesmos dados do original 
		//
		RecLock("SD3",.T.)
		For i := 1 TO FCount()
			FieldPut(i,M->&(EVAL(bCampo,i)))
		Next
		If D3_TM=="499"
			Replace  D3_TM    With "999",;
			D3_CF    With "RE6",;
			D3_CHAVE With "E9"
		Else
			Replace  D3_TM    With "499",;
			D3_CF    With "DE6",;
			D3_CHAVE With "E0"
		EndIf
		Replace D3_USUARIO With CUSERNAME
		//Ŀ
		// Pega o custo da movimentacao               
		//
		aCusto := PegaCusD3()
		//Ŀ
		// Atualiza o saldo atual (VATU) com os dados do SD3     
		//
		B2AtuComD3(aCusto)
		//Ŀ
		// Acerta custo da OP relacionada na NF de Entrada       
		//
		If !Empty(SD3->D3_OP)
			C2AtuComD3(aCusto)
		EndIf
	EndIf
	//Ŀ
	// Gera movimento inverso do destino          
	//
	If nRecDest > 0
		dbSelectArea("SD3")
		dbGoTo(nRecDest)
		RecLock("SD3",.F.)
		//Ŀ
		// Grava o Flag de estorno                    
		//
		Replace D3_ESTORNO With "S"
		//Ŀ
		// Salva a integridade dos campos de Bancos de Dados       
		//
		For i:=1 TO FCount()
			M->&(EVAL(bCampo,i)):=FieldGet(i)
		Next
		//Ŀ
		// Cria o registro de estorno com mesmos dados do original 
		//
		RecLock("SD3",.T.)
		For i:=1 TO FCount()
			FieldPut(i,M->&(EVAL(bCampo,i)))
		Next
		If D3_TM=="499"
			Replace  D3_TM    With "999",;
			D3_CF    With "RE6",;
			D3_CHAVE With "E9"
		Else
			Replace  D3_TM    With "499",;
			D3_CF    With "DE6",;
			D3_CHAVE With "E0"
		EndIf
		Replace D3_USUARIO With CUSERNAME
		//Ŀ
		// Pega o custo da movimentacao               
		//
		aCusto := PegaCusD3()
		//Ŀ
		// Atualiza o saldo atual (VATU) com os dados do SD3     
		//
		B2AtuComD3(aCusto)
	EndIf
Next
Return(NIL)

/*


Ŀ
Funo    A118EstCQ  Autor     Marcos Simidu        Data  09/12/97 
Ĵ
Descrio Estorna o lancamento na NF do SD3                            
ٱ


*/
Function A118EstCQ(cNumCQ,cCod,cLocal)
Local bCampo:={|nCPO| Field(nCPO) },i
Local nRecDest:=0,nRecOri:=0

dbSelectArea("SD7")
If D7_TIPO==8
	//-- Excluso de Movimentaes Internas Ref. CQ
	dbSelectArea('SD3')
	nOrdSD3 := IndexOrd()
	nRecSD3 := RecNo()
	dbSetOrder(4)
	If dbSeek(xFilial()+SD7->D7_NUMSEQ)
		A118EstD3(SD7->D7_NUMERO, SD7->D7_NUMSEQ, SD7->D7_LOCAL)
	EndIf
	
	//Ŀ
	// Estorna custeio do SD7.                
	//
	dbSelectArea("SD7")
	RecLock("SD7",.F.)
	Replace D7_ESTORNO With "S"
	//Ŀ
	// Salva a integridade dos campos de Bancos de Dados            
	//
	For i:=1 To FCount()
		M->&(EVAL(bCampo,i)):=FieldGet(i)
	Next
	
	//Ŀ
	// Cria o registro de estorno com mesmos dados do original      
	//
	RecLock("SD7",.T.)
	For i:=1 TO FCount()
		FieldPut(i,M->&(EVAL(bCampo,i)))
	Next i
	Replace D7_TIPO With 9
	MsUnlock()
	
Endif

Return

/*


Ŀ
Funo    A118GravaCQ   Autor    Marcos Simidu     Data  05/12/97 
Ĵ
Descrio  Grava item no SD7 - Despesa de Importacao.                 
ٱ


*/
Function A118GravaCQ(cLocCQ,cNumSeq,cDocCQ,nSaldo)
Local nSavRec:=0,cSeq:=""
Local cTipoCQ := ''
Local cLibera := ''

dbSelectArea("SB1")
dbSeek(xFilial()+SD1->D1_COD,.F.)
cTipoCQ := If(Empty(RetFldProd(SB1->B1_COD,"B1_TIPOCQ")),'M',RetFldProd(SB1->B1_COD,"B1_TIPOCQ"))

dbSelectArea("SD7")
dbSeek(xFilial()+cDocCQ+SD1->D1_COD+cLocCq)
//Ŀ
//  Pega numero sequencial - campo D7_SEQ                               
//
nSavRec:=Recno()
While !Eof().And.xFilial()==D7_FILIAL ;
	.And.(cDocCQ+SD1->D1_COD+cLocCQ)==(D7_NUMERO+D7_PRODUTO+D7_LOCAL)
	If SD7->D7_TIPO == 0
		cTipoCQ := SD7->D7_TIPOCQ
		cLibera := SD7->D7_LIBERA
	EndIf
	cSeq:=D7_SEQ
	dbSkip()
EndDo
dbGoto(nSavRec)

RecLock("SD7",.T.)

Replace 	D7_FILIAL	With xFilial()
Replace 	D7_NUMERO	With cDocCQ
Replace 	D7_PRODUTO	With SD1->D1_COD
Replace 	D7_ORIGLAN	With "CP"
Replace 	D7_USUARIO	With CUSERNAME
Replace 	D7_LOCAL	With cLocCQ
Replace 	D7_LOCDEST	With RetFldProd(SB1->B1_COD,"B1_LOCPAD")
Replace     D7_TIPO		With 8
Replace     D7_DOC      With  SD1->D1_DOC
Replace     D7_SERIE    With  SD1->D1_SERIE
Replace     D7_FORNECE  With  SD1->D1_FORNECE
Replace     D7_LOJA     With  SD1->D1_LOJA
Replace	    D7_SALDO	With 0
Replace	    D7_QTDE		With 0
Replace	    D7_DATA		With dDataBase
Replace	    D7_NUMSEQ	With cNumSeq
Replace	    D7_SEQ		With StrZero((Val(cSeq)+1),3)
Replace	    D7_SALDO	With nSaldo //(aSaldoCQ[1]-aSaldoCQ[2]-aSaldoCQ[3]+aSaldoCQ[4]-aSaldoCQ[5])
Replace     D7_TIPOCQ   With cTipoCQ
Replace     D7_LIBERA   With cLibera
Replace     D7_NUMLOTE  With SD1->D1_NUMLOTE
Replace     D7_LOTECTL  With SD1->D1_LOTECTL

MsUnlock()

Return
/*


Ŀ
Funo    A118GravaD3	 Autor    Marcos Simidu     Data  05/12/97 
Ĵ
Descrio  Grava item no SD3 - RE6/DE6.                               
ٱ


*/
Function A118GravaD3(cTipo,cLocDev,aCusto,nLiber,nSaldoOri,cNumSeq,cLocReq,cDocCQ)

dbSelectArea("SB1")
dbSeek(xFilial()+SD1->D1_COD,.F.)

cLocReq := If(cLocReq==NIL,RetFldProd(SB1->B1_COD,"B1_LOCPAD"),cLocReq)

RecLock("SD3",.T.)

Replace D3_FILIAL 	With xFilial()
Replace D3_DOC			With cDocCQ
Replace D3_COD			With SD1->D1_COD
Replace D3_UM			With SB1->B1_UM
Replace D3_EMISSAO	With ddatabase
Replace D3_GRUPO 		With SB1->B1_GRUPO
Replace D3_TIPO  		With SB1->B1_TIPO
//Ŀ
// Grava a classe do movimento RE6/DE6            
//
If cTipo=="R" //-- Requisio
	Replace D3_CF 		With "RE6"
	Replace D3_LOCAL 	With cLocReq
	Replace D3_TM	 	With "999"
	Replace D3_CHAVE  With "E9"
Else //-- Devoluo
	Replace D3_CF 		With "DE6"
	Replace D3_LOCAL 	With cLocDev
	Replace D3_TM	 	With "499"
	Replace D3_CHAVE  With "E0"
EndIf
Replace D3_USUARIO With CUSERNAME

//Ŀ
// Grava o numero sequencial do movimento         
//
Replace D3_NUMSEQ With cNumSeq

//Ŀ
// Grava Qtde. zero - Movimento apenas para agregar custo  
//
Replace D3_QUANT With 0

Replace D3_CUSTO1 With (aCusto[1]/nSaldoOri)*nLiber
Replace D3_CUSTO2 With (aCusto[2]/nSaldoOri)*nLiber
Replace D3_CUSTO3 With (aCusto[3]/nSaldoOri)*nLiber
Replace D3_CUSTO4 With (aCusto[4]/nSaldoOri)*nLiber
Replace D3_CUSTO5 With (aCusto[5]/nSaldoOri)*nLiber

//Ŀ
// Atualiza o saldo atual (VATU) com os dados do SD3     
//
B2AtuComD3({D3_CUSTO1,D3_CUSTO2,D3_CUSTO3,D3_CUSTO4,D3_CUSTO5})

//Ŀ
// Atualiza os saldos atuais (VATU) das OP's 
//
If !Empty(SD3->D3_OP)
	C2AtuComD3({D3_CUSTO1,D3_CUSTO2,D3_CUSTO3,D3_CUSTO4,D3_CUSTO5})
EndIf

MsUnlock()

Return
