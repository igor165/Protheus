#INCLUDE "protheus.ch"
#INCLUDE "quicksearch.ch"
#INCLUDE "MATQ125.ch" 

QSSTRUCT MATQ12501 DESCRIPTION STR0001 MODULE 2 //-- Contratos de parceria em aberto por fornecedores

QSMETHOD INIT QSSTRUCT MATQ12501

//-- Tabelas envolvidas na consulta e seus relacionamentos
QSTABLE "SC3" JOIN "SA2" ON "A2_COD = C3_FORNECE AND A2_LOJA = C3_LOJA"

//-- Campos/índices utilizados para a pesquisa
QSPARENTFIELD "C3_FORNECE" INDEX ORDER 2 LABEL STR0002	//-- Código do Fornecedor
QSPARENTFIELD "A2_NOME" INDEX ORDER 2 SET RELATION TO "C3_FORNECE","C3_LOJA" WITH "A2_COD","A2_LOJA" LABEL STR0003	//-- Nome do Fornecedor
QSPARENTFIELD "A2_CGC" INDEX ORDER 3 SET RELATION TO "C3_FORNECE","C3_LOJA" WITH "A2_COD","A2_LOJA" LABEL STR0004	//-- CNPJ/CPF do Fornecedor

//-- Opcoes de filtro
QSFILTER STR0005 WHERE "C3_QUJE < C3_QUANT AND C3_RESIDUO <> 'S' AND C3_EMISSAO >= '" +DToS(Date() - 30) +"'"	//-- Últimos 30 dias
QSFILTER STR0006 WHERE "C3_QUJE < C3_QUANT AND C3_RESIDUO <> 'S' AND C3_EMISSAO >= '" +DToS(Date() - 60) +"'"	//-- Últimos 60 dias 
QSFILTER STR0007 WHERE "C3_QUJE < C3_QUANT AND C3_RESIDUO <> 'S' AND C3_EMISSAO >= '" +DToS(Date() - 90) +"'"	//-- Últimos 90 dias
QSFILTER STR0008 WHERE "C3_QUJE < C3_QUANT AND C3_RESIDUO <> 'S' AND C3_EMISSAO >= '" +DToS(Date() - 120) +"'"	//-- Últimos 120 dias
QSFILTER STR0009 WHERE "C3_QUJE < C3_QUANT AND C3_RESIDUO <> 'S' AND C3_EMISSAO >= '" +DToS(Date() - 360) +"'"	//-- Últimos 360 dias
QSFILTER STR0010 WHERE "C3_QUJE < C3_QUANT AND C3_RESIDUO <> 'S'"	//-- Todos

//-- Campos da consulta rapida
QSFIELD "C3_FILIAL" LABEL STR0011	//-- Empresa
QSFIELD "C3_NUM" LABEL STR0012		//-- Número do Contrato
QSFIELD "C3_FORNECE" LABEL STR0002	//-- Código do Fornecedor
QSFIELD "C3_LOJA" LABEL STR0013		//-- Loja
QSFIELD "A2_NOME" LABEL STR0003		//-- Nome do Fornecedor
QSFIELD "A2_CGC" LABEL STR0004		//-- CNPJ/CPF do Fornecedor
QSFIELD "MOEDA" BLOCK {|| SuperGetMV("MV_MOEDA"+AllTrim(Str(C8_MOEDA)),.F.,"")} FIELDS "C3_MOEDA" LABEL "Moeda" TYPE "C" SIZE 15	//-- Moeda
QSFIELD "VLCART" EXPRESSION "SUM((C3_QUANT - C3_QUJE) * C3_PRECO)" LABEL STR0015 FIELDS "C3_QUANT","C3_QUJE","C3_PRECO" GROUP BY TYPE "N" SIZE TamSX3("C3_TOTAL")[1] DECIMAL TamSX3("C3_TOTAL")[2] PICTURE PesqPict("SC3","C3_TOTAL")	//-- Valor em Carteira
QSFIELD "VLCARTMC" BLOCK {|| xMoeda(VLCART,C8_MOEDA,1)} FIELDS "C8_MOEDA" LABEL STR0015+STR0016 TYPE "N" SIZE TamSX3("C3_TOTAL")[1] DECIMAL TamSX3("C3_TOTAL")[2] PICTURE PesqPict("SC3","C3_TOTAL")	//-- Valor em Carteira (Moeda Corrente)
QSFIELD "C3_EMISSAO" LABEL STR0017	//-- Emissão

//-- Acoes relacionadas
QSACTION MENUDEF "MATA125" OPERATION 2 LABEL STR0018	//-- Detalhes do Contrato
QSACTION MENUDEF "MATA020" OPERATION 2 LABEL STR0019	//-- Detalhes do Fornecedor

Return

QSSTRUCT MATQ12502 DESCRIPTION STR0020 MODULE 2	//-- Contratos de parceria em aberto por produtos

QSMETHOD INIT QSSTRUCT MATQ12502

//-- Tabelas envolvidas na consulta e seus relacionamentos
QSTABLE "SC3" JOIN "SA2" ON "A2_COD = C3_FORNECE AND A2_LOJA = C3_LOJA"
QSTABLE "SC3" JOIN "SB1" ON "B1_COD = C3_PRODUTO"
QSTABLE "SC3" LEFT JOIN "SB5" ON "B5_COD = C3_PRODUTO"

//-- Campos/índices utilizados para a pesquisa
QSPARENTFIELD "C3_PRODUTO" INDEX ORDER 3 LABEL STR0021	//-- Código do Produto
QSPARENTFIELD "C3_NUM" INDEX ORDER 1 LABEL STR0022		//-- Número do Contrato
QSPARENTFIELD "B1_DESC" INDEX ORDER 3 SET RELATION TO "C3_PRODUTO" WITH "B1_COD" LABEL STR0023	//-- Descrição do Produto
QSPARENTFIELD "B5_CEME" INDEX ORDER 7 SET RELATION TO "C3_PRODUTO" WITH "B5_COD" LABEL STR0024	//-- Descrição Complementar do Produto
QSPARENTFIELD "A2_NOME" INDEX ORDER 2 SET RELATION TO "C3_FORNECE","C3_LOJA" WITH "A2_COD","A2_LOJA" LABEL STR0003	//-- Nome do Fornecedor

//-- Opcoes de filtro
QSFILTER STR0025 WHERE "C3_QUJE < C3_QUANT AND C3_RESIDUO <> 'S' AND '" +DToS(Date()) +"' BETWEEN C3_DATPRI AND C3_DATPRF"	//-- Dentro da validade
QSFILTER STR0026 WHERE "C3_QUJE < C3_QUANT AND C3_RESIDUO <> 'S' AND ('" +DToS(Date()) +"' < C3_DATPRI OR '" +DToS(Date()) +"' > C3_DATPRF)"	//-- Fora da validade
QSFILTER STR0027 WHERE "C3_QUJE < C3_QUANT AND C3_RESIDUO <> 'S'"	//-- Todos

//-- Campos da consulta rapida
QSFIELD "C3_FILIAL" LABEL STR0011	//-- Empresa
QSFIELD "C3_NUM" LABEL STR0022		//-- Número do Contrato
QSFIELD "A2_NOME" LABEL STR0003		//-- Fornecedor
QSFIELD "A2_CGC" LABEL STR0004		//-- CNPJ/CPF do Fornecedor
QSFIELD "C3_PRODUTO" LABEL STR0021	//-- Código do Produto
QSFIELD "B1_DESC" LABEL STR0023		//-- Descrição do Produto
QSFIELD "SALDO" EXPRESSION "C3_QUANT - C3_QUJE" LABEL STR0028 FIELDS "C3_QUANT","C3_QUJE" TYPE "N" SIZE TamSX3("C3_QUANT")[1] DECIMAL TamSX3("C3_QUANT")[2] PICTURE PesqPict("SC3","C3_QUANT")	//-- Quantidade a Receber
QSFIELD "C3_DATPRI" LABEL STR0029	//-- Início de Validade
QSFIELD "C3_DATPRF" LABEL STR0030	//-- "Fim de Validade"

//-- Acoes relacionadas
QSACTION MENUDEF "MATA125" OPERATION 2 LABEL STR0018	//-- Detalhes do Contrato
QSACTION MENUDEF "MATA010" OPERATION 2 LABEL STR0031	//-- Detalhes do Produto
QSACTION MENUDEF "MATA020" OPERATION 2 LABEL STR0019	//-- Detalhes do Fornecedor

Return
