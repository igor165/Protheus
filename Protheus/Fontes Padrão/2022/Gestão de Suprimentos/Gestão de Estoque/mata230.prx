#INCLUDE "TBICONN.CH"
#INCLUDE "MATA230.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE 'FWMVCDEF.CH'

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA230  ³ Autor ³ Eveli Morasco         ³ Data ³ 13/01/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de atualizacao do cadastro de Tipos de movimenta- ³±±
±±³          ³ cao.                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Rodrigo Sart³09/06/99³PROTHE³Verificar permissao programa              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso        ³ Generico                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATA230(aAuto,nOpcAuto)
Local cNameTable := ""
Private aRotina	:= MenuDef()  

cNameTable := RetSQLName("D3C")
If ! Empty(cNameTable)
	TCSqlExec("DELETE FROM "+cNameTable+" WHERE D3C_DESCRI = 'Carga Fresca processada em unidades de destila‡ao prim ria                      '")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a permissao do programa em relacao aos modulos      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If AMIIn(4,10,11,12,14,25,39,41,42,44,67,72)
	PRIVATE cCadastro := OemToAnsi(STR0006) //"Tipos de Movimenta‡„o"
	AxCadastro("SF5",cCadastro,"A230Deleta()","A230TudoOk()",aRotina,NIL,NIL,NIL,NIL,aAuto,nOpcAuto)
EndIf
RETURN

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A230Deleta³ Autor ³ Eveli Morasco         ³ Data ³ 13/01/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para validacao da exclusao de TM's                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA230                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A230Deleta()
LOCAL aArea:= GetArea()
LOCAL lRet := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Valida exclusao de TP                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
lRet := A230TudoOk()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se nao tem movimentacao                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
If lRet
	dbSelectArea("SD3")
	dbSetOrder(5)
	lRet:=!(dbSeek(xFilial("SD3")+SF5->F5_CODIGO))
	If !lRet
		Help(" ",1,"MA230TEMOV")
	EndIf  
EndIf

If lRet
	While SGP->(dbSeek(xFilial("SGP")+SF5->F5_CODIGO))
		RecLock("SGP",.F.)
		SGP->(dbDelete())
		MsUnlock()
		SGP->(dbskip())
	EndDo
EndIf

RestArea(aArea)    
Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A230Tipo  ³ Autor ³ Eveli Morasco         ³ Data ³ 13/01/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o tipo esta' condizente com o codigo :         ³±±
±±³          ³ <= 500 deve ser entrada    > 500 deve ser saida            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA230                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A230Tipo()
LOCAL cVar := &(ReadVar())
LOCAL lRet:=.T.
If Alltrim(ReadVar()) == "M->F5_TIPO"
	If (M->F5_CODIGO > "999") .Or. (M->F5_CODIGO <= "500" .And. cVar=="R") .Or. (M->F5_CODIGO > "500" .And. cVar$"DP")
		Help(" ",1,"F5_TIPO")
		lRet:=.F.
	EndIf
ElseIf Alltrim(ReadVar()) == "M->F5_CODIGO"
	If cVar$"499/999"
		Help(" ",1,"MA230RESER")
		lRet:=.F.
	EndIf
	If lRet
		If (Type(cVar) <> "N") .Or. (cVar <= "500" .And. M->F5_TIPO=="R") .Or. (cVar > "500" .And. M->F5_TIPO$"DP")
			Help(" ",1,"F5_TIPO")
			lRet:=.F.
		EndIf
	EndIf
EndIf
Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MyMata230 ³ Autor ³Rodrigo de A Sartorio  ³ Data ³12.07.2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de teste da rotina automatica do programa MATA230     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo efetuar testes na rotina de    ³±±
±±³          ³cadastro de tipo de movimentacao interna                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function MyMata230()
Local aCabec    := {}
Local cMovimento:= ""
Local cTipoMovim:= "D"
PRIVATE lMsErroAuto := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Abertura do ambiente                                         |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PREPARE ENVIRONMENT EMPRESA "99" FILIAL "01" MODULO "EST" TABLES "SF5"
ConOut(Repl("-",80))
ConOut(PadC("Teste de Inclusao de Tipo Movimento Interno",80))
ConOut("Inicio: "+Time())
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Verifica o ultimo MOVIMENTO                                  |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF5")
dbSetOrder(1)
MsSeek(xFilial("SF5")+Replicate("z",Len(SF5->F5_CODIGO)),.T.)
dbSkip(-1)

If xFilial("SF5") == SF5->F5_FILIAL
	cMovimento := Soma1(SF5->F5_CODIGO)
Else
	cMovimento := "001"
EndIf                              

If cMovimento <= "500"
	cTipoMovim:= "D"
Else
	cTipoMovim:= "R"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Teste de Inclusao                                            |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCabec := {}
aadd(aCabec,{"F5_CODIGO" ,cMovimento,})
aadd(aCabec,{"F5_TIPO"   ,cTipoMovim,})
aadd(aCabec,{"F5_TEXTO"  ,"TESTE ROT AUTOM INC",})
aadd(aCabec,{"F5_APROPR" ,"N",})
aadd(aCabec,{"F5_ATUEMP" ,"N",})
aadd(aCabec,{"F5_TRANMOD","N",})
aadd(aCabec,{"F5_VAL"    ,"N",})
aadd(aCabec,{"F5_ENVCQPR","N",})
aadd(aCabec,{"F5_LIBPVPR","N",})
aadd(aCabec,{"F5_QTDZERO","2",})
MATA230(aCabec,3)
If !lMsErroAuto
	ConOut("Incluido com sucesso! "+cMovimento)	
Else
	ConOut("Erro na inclusao!")
EndIf
ConOut("Fim  : "+Time())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Teste de Alteracao                                           |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCabec := {}
aadd(aCabec,{"F5_CODIGO",cMovimento,})
aadd(aCabec,{"F5_TEXTO","TESTE ROT AUTOM ALT",})
MATA230(aCabec,4)
If !lMsErroAuto
	ConOut("Alteracao com sucesso! "+cMovimento)	
Else
	ConOut("Erro na alteracao!")
EndIf
ConOut("Fim  : "+Time())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Teste de exclusao                                            |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCabec := {}
aadd(aCabec,{"F5_CODIGO",cMovimento,})
MATA230(aCabec,5)
If !lMsErroAuto
	ConOut("Exclusao com sucesso! "+cMovimento)	
Else
	ConOut("Erro na exclusao!")
EndIf
ConOut("Fim  : "+Time())
RESET ENVIRONMENT
Return(.T.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A230TudoOk³ Autor ³ Aline Sebrian		     | Data ³ 29/01/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a rotina Tipos de Movimentacao                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA230                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A230TudoOk()
Local lRet := .T.
Local lMA230TOK := ExistBlock("MA230TOK")

If cPaisLoc == "PTG" .And. IIF(Type("M->F5_TIPO")<>"U",M->F5_TIPO<>"R",.T.)
	If Type("M->F5_TIPO")<>"U"
		If M->F5_PERDA == 'OB'.Or. M->F5_PERDA =='EX' 
			Aviso("SIGAEST",STR0010,{"Ok"})
			lRet := .F.
		EndIf          
	EndIf
EndIf

If lMA230TOK
	lRet := ExecBlock("MA230TOK",.F.,.F.)
	If ValType(lRet) # "L"
		lRet := .T.
	EndIf
EndIf     

Return lRet         
                                                                  
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A230PERM   ºAutor  ³Leonardo Quintania    º Data ³16/01/2012º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡ao ³ Montagem de Interface de permissões de Tipos de Movimentos º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA230                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
 
Function A230PERM()
Local oSize
Local oSize2    
Local cWhile    := ""
Local cSeek		:= "" 
Local nDel      
Local aNoFields	:= {} 
Local aHeadSGP	:= {}
Local aColsSGP	:= {}
Local nPosUsr  
Local nPosGrp   
Local nX, nY        
Local nOpcA		:= 0 
Local cVar		:= ""
Local cIniCpos 	:= "GP_NOME" 
Local nPosRecno
Local lContinua  := .T.
Local cMode 	:= FWModeAccess("SF5",1)+FWModeAccess("SF5",2)+FWModeAccess("SF5",3)
Private oGetDad            

If cMode # "CCC" .And. xFilial("SF5") # SF5->F5_FILIAL
	Help(" ",1,"A000FI")
	lContinua:= .F.			
EndIf

If lContinua
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcula dimensões                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSize := FwDefSize():New()             
	oSize:AddObject( "CABECALHO",  100, 10, .T., .T. ) // Totalmente dimensionavel
	oSize:AddObject( "GETDADOS" ,  100, 85, .T., .T. ) // Totalmente dimensionavel 
	
	oSize:lProp 	:= .T. // Proporcional             
	oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3 
	
	oSize:Process() // Dispara os calculos 
	 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Divide cabeçalho                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSize2 := FwDefSize():New()
	
	oSize2:aWorkArea := oSize:GetNextCallArea( "CABECALHO" ) 
	
	oSize2:AddObject( "TM"   , 20, 100, .T., .T.) // Dimensionavel
	oSize2:AddObject( "DESC" , 80, 100, .T., .F.) // Dimensionavel  
	  
	oSize2:lLateral := .T.            //Calculo em Lateral
	oSize2:lProp := .T.               // Proporcional             
	oSize2:aMargins := { 3, 3, 0, 0 } // Espaco ao lado dos objetos 0, entre eles 3 
	
	oSize2:Process() // Dispara os calculos  
	
	M->GP_TM  := SF5->F5_CODIGO    
	M->GP_DESC	:= SF5->F5_TEXTO   
	
	cWhile		:= "SGP->(GP_FILIAL+GP_TM)"
	cSeek		:= "xFilial('SGP')+SF5->F5_CODIGO"
	aNoFields	:= { "GP_TM", "GP_DESC" }
	
	FillGetDados(	4,; 							// numero correspondente à operação a ser executada, exemplo: 3 - inclusão, 4 alteração e etc;
	               	"SGP",;       					// area a ser utilizada;
	               	1,;      						// nOrdem - ordem correspondente a chave de índice para preencher o  acols;
	               	&cSeek,;  						// chave utilizada no posicionamento da área para preencher o acols; 
	               	{|| &cWhile},; 					// bloco contendo a expressão a ser comparada com cSeekKey na condição  do While. 
	               	{|| .T.},;  					// uSeekFor
	               	aNoFields,;  					// aNoFields - array contendo os campos que não estarao no aHeader;
	               	,;  							// aYesFields - array contendo somente os campos que estarao no aHeader;
	               	.F.,;      						// se verdadeiro, exibe apenas os campos de usuário;
	                '',;      						// cQuery - query a ser executada para preencher o acols;
	               	,;    				   			// bloco contendo funcao especifica para preencher o aCols; 
	               	.F.,;  	// lEmpty 
	               	aHeadSGP,;  					// aHeaderAux
	               	aColsSGP,; 						// aColsAux
	               	,; 								// bAfterCols
	               	,; 								// bBeforeCols
	               	,; 								// bAfterHeader
	               	'') 							// cAliasQry
	
	
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0011) OF oMainWnd PIXEL; //"Cadastro de Permissões"
												FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4] OF oMainWnd PIXEL
	
	@ oSize2:GetDimension("TM","LININI")+15  ,oSize2:GetDimension("TM","COLINI")    SAY OemToAnsi(STR0016) Of oDlg PIXEL 		//"Tipo Mov."
	@ oSize2:GetDimension("TM","LININI")+13  ,oSize2:GetDimension("TM","COLINI")+25 MSGET M->GP_TM 	 Picture PesqPict("SGP","GP_TM") SIZE 50,010 When .F. Of oDlg PIXEL           
	
	@ oSize2:GetDimension("DESC","LININI")+15  ,oSize2:GetDimension("DESC","COLINI")    SAY OemToAnsi(STR0017) Of oDlg PIXEL	//"Descrição"
	@ oSize2:GetDimension("DESC","LININI")+13  ,oSize2:GetDimension("DESC","COLINI")+30 MSGET M->GP_DESC  Picture PesqPict("SGP","GP_DESC") SIZE 100,010 When .F.  Of oDlg PIXEL
	          
	
	oGetDad := MsNewGetDados():New(oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),;
		     							   oSize:GetDimension("GETDADOS","LINEND"),oSize:GetDimension("GETDADOS","COLEND"),;
	    									GD_INSERT+GD_UPDATE+GD_DELETE,"M230LinOk","M230TudOk",cIniCpos,,,999,/*fieldok*/,/*superdel*/,/*delok*/,;
	    									oDlg,aHeadSGP,aColsSGP)
	    									
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,If(oGetDad:TudoOk(),oDlg:End(),)},{|| nOpcA:=0,oDlg:End()})
	
	If nOpcA == 1                                                
	
		nDel      := Len(oGetDad:aHeader)+1
		nPosUsr   := Ascan(oGetDad:aHeader,{|x| x[2] = "GP_USER"})
		nPosGrp   := Ascan(oGetDad:aHeader,{|x| x[2] = "GP_GRUPO"})
		nPosRecno := Ascan(oGetDad:aHeader,{|x| x[2] = "GP_REC_WT"})
	
		For nX := 1 to Len(oGetDad:aCols)
		  	If !Empty(oGetDad:aCols[nX,nPosUsr]) .OR. !Empty(oGetDad:aCols[nX,nPosGrp]) // Verifica se nao esta vazio
				If !oGetDad:aCols[nX,nDel] // Nao esta deletado 
					If oGetDad:aCols[nX,nPosRecno]!= 0//Procura no Recno
						SGP->(DBGoTo(oGetDad:aCols[nX,nPosRecno]))
						RecLock("SGP",.F.)   // Lock
					Else
						RecLock("SGP",.T.)   // Append 
						SGP->GP_FILIAL	:= xFilial("SGP")
						SGP->GP_TM	   	:= M->GP_TM			
					EndIf	
					For nY := 1 to Len(oGetDad:aHeader)
						If oGetDad:aHeader[ny,10] # "V"
							cVar := Trim(oGetDad:aHeader[ny,2])
							Replace &cVar With oGetDad:aCols[nx,ny]
						Endif
					Next nY
				ElseIf !Empty(oGetDad:aCols[nX,nPosRecno])
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Deleta linhas         				  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					SGP->(DBGoTo(oGetDad:aCols[nX,nPosRecno]))
					RecLock("SGP",.F.)
					dbDelete()
				EndIf
				MsUnlock()
			EndIf
		Next nX    
	EndIf
EndIf


Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³M230VLDALLºAutor  ³Leonardo Quintania  º Data ³ 18/01/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Validacao e preenchimento dos campos de usuarios e Grupo    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MATA230                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M230VLDALL()
Local lRet:= .F.     
 

If ReadVar()=="M->GP_USER"
	If Vazio() .Or. ((AllTrim(&(ReadVar())) == "*" .Or. UsrExist(&(ReadVar()))) .And. ExistChav("SGP",M->GP_TM+&(ReadVar())))
		lRet:= .T.	   
		If AllTrim(&(ReadVar())) == "*"
			oGetDad:aCols[oGetDad:nAT,2]:= PadR("*",TamSX3("GP_GRUPO")[1])
			oGetDad:aCols[oGetDad:nAT,3]:= STR0012
		Else	
			oGetDad:aCols[oGetDad:nAT,2]:= CriaVar("GP_GRUPO",.F.) 
			oGetDad:aCols[oGetDad:nAT,3]:= UsrRetName(&(ReadVar()))
		EndIf
    EndIf              
    
ElseIf ReadVar()=="M->GP_GRUPO"
	If Vazio() .Or. ((AllTrim(&(ReadVar())) == "*" .Or. UsrExist(&(ReadVar()),.F.)) .And. ExistChav("SGP",M->GP_TM+&(ReadVar()),2))
		lRet:= .T.	
		If AllTrim(&(ReadVar())) == "*"
			oGetDad:aCols[oGetDad:nAT,1]:= PadR("*",TamSX3("GP_USER")[1])
			oGetDad:aCols[oGetDad:nAT,3]:= STR0012
		Else 
			If !Vazio()
				oGetDad:aCols[oGetDad:nAT,1]:= CriaVar("GP_USER",.F.) 
				oGetDad:aCols[oGetDad:nAT,3]:= GrpRetName(&(ReadVar()))
			EndIf
		EndIf
    EndIf      
EndIf
Return lRet        

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³M230LinOk ºAutor  ³Leonardo Quintania  º Data ³ 18/01/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida a linha da Get não permitindo registros duplicados   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MATA230                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M230LinOk()    
Local lRet := .T.
Local nX
Local nDel := Len(oGetDad:aHeader)+1    
Local nLin := oGetDad:nAT

For nX := 1 to Len(oGetDad:aCols)
	If oGetDad:nAT <> nX .AND. !oGetDad:aCols[nX,nDel] .AND. ;
				oGetDad:aCols[nLin,1]+oGetDad:aCols[nLin,2]==;
				oGetDad:aCols[nX,1]+oGetDad:aCols[nX,2]
		MsgInfo(STR0013,STR0011)
		lRet:= .F.                                  
		EXIT 					
	ElseIf Empty(oGetDad:aCols[nLin,1]+oGetDad:aCols[nLin,2]) 
		MsgInfo(STR0015,STR0014)
		lRet:= .F.                                     
		EXIT
	EndIf                                              
	
Next nX

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³M230TudOk ºAutor  ³Allyson Freitas     º Data ³ 24/05/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida a Get não permitindo registros duplicados            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MATA230                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M230TudOk()
Local lRet := .T.

	lRet := M230LinOk()

Return lRet  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³M230IncNo ºAutor  ³Leonardo Quintania  º Data ³ 18/01/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Inicializador padrao do campo GP_NOME que carrega           º±±  
±±º			 ³a descricao todos  										  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MATA230                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/  
Function M230IncNo()
Local cRet := ""

If oGetDad <> NIL .And. oGetDad:aCols[oGetDad:nat,5]<> NIL 
	cRet := CriaVar("GP_NOME",.F.)
ElseIf AllTrim(SGP->GP_USER) == "*"
	cRet := STR0012
ElseIf !Empty(SGP->GP_USER)
	cRet := UsrRetName(SGP->GP_USER)
ElseIf !Empty(SGP->GP_GRUPO)
	cRet := GrpRetName(SGP->GP_GRUPO)
Else
	cRet := CriaVar("GP_NOME",.F.)                      
EndIf

Return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A230VldTES()
Validação do tipo de TES utilizada
@author Leonardo Quintania
@since 28/01/2013
@version 1.0
@return NIL
/*/
//-------------------------------------------------------------------
Function A230VldTES()
Local cTes	:= &(ReadVar())
Local lRet	:= .T.

SF4->(dbSetOrder(1))
SF4->(MsSeek(xFilial("SF4")+cTes))

If SF4->F4_ATUATF <> "S"
	Help(" ",1,"F5_TEATF")
	lRet:=.F.
EndIf

Return lRet

//-------------------------------------------------------------------
// MenuDef()
//-------------------------------------------------------------------
Static Function MenuDef()
Local aRotina := {}
aRotina:= StaticCall(MATXATU,MENUDEF)
aAdd(aRotina,{ STR0011 , "A230PERM()"  , 0, 4})  //"Permissoes"
Return aRotina
