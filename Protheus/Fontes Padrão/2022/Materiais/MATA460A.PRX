#INCLUDE "MATA460.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWADAPTEREAI.CH"

/*


Ŀ
Funo	  MATA460A	 Autor  Claudinei M. Benzi	  Data  15.01.92 
Ĵ
Descrio  Programa de geracao de Notas Fiscais		                   
Ĵ
Uso		  Generico 												   
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                    
Ĵ
Jose Lucas    15/07/99      Adaptaes realizadas na Argentina...    
Jose Lucas    18/07/9919827ATratamento da varivael cPaisLoc e dire-  
                            tiva de comipilacao #IFNDEF SPANISH      
Aline C. Vale 27/07/9922483 Continuar considerando paramentros       
Aline C. Vale 16/08/9923320 Nao desposicionar reg. na pesq. filtrada 
Andreia       19/10/9924594AAlteracao da funcal A460Consum, para sele
                            cionar corretamente material de consumo. 
ٱ


*/

/*


Ŀ
Funo	 A460pesqui Autor  Marcelo B. Abe		 Data 19/03/1994
Ĵ
Descrio  Pesquisa usando indice condicional						  
Ĵ
 Uso		  Mata460													  
ٱ


*/
Function A460Pesqui()

LOCAL cCondicao
LOCAL nIndex		:= 0
LOCAL cIndex		:= ""
Local nRegAtu		:= 0

dbSetOrder(n460OrdAtiva)

AxPesqui()
nRegAtu      := Recno()
n460OrdAtiva := IndexOrd()

If lFiltra
	//Ŀ
	// Cria Indice Temporario para a ordem escolhida. 		   
	//
	cIndex := CriaTrab(nil,.f.)
	AADD(aInd,cIndex)
	cCondicao:= 'C9_BLEST <>"10" .And. C9_BLCRED <>"10" .And. C9_FILIAL=="' + xFilial("SC9") + '"'
	IF !Empty(cFilSC9)
		cCondicao += ' .and. '+cFilSC9
	Endif

	If mv_par03 == 1 //Considera parametros abaixo? Sim/Nao
		cCondicao += '.And.C9_PEDIDO>="'+mv_par05+'".And.C9_PEDIDO<="'+mv_par06+'".And.'
		cCondicao += 'C9_CLIENTE>="'+mv_par07+'".And.C9_CLIENTE<="'+mv_par08+'"'
		cCondicao += '.And.C9_LOJA>="'+mv_par09+'".And.C9_LOJA<="'+mv_par10+'"'
		cCondicao += '.And.DTOS(C9_DATALIB)>="'+DTOS(mv_par11)+'".And.DTOS(C9_DATALIB)<="'+DTOS(mv_par12)+'"'
		cCondicao += ' .And. C9_DATENT >= "' + DToS(MV_PAR13) + '" .And. C9_DATENT <= "' + DToS(MV_PAR14) + '"'
	EndIf

	IndRegua("SC9",cIndex,IndexKey(),,cCondicao,OemToAnsi(STR0006))		//"Selecionando Registros..."
	nIndex  := RetIndex("SC9")
	dbSelectArea("SC9")

	dbSetOrder(nIndex+1)
	dbGoTo(nRegAtu)
EndIf

Return .T.

/*

Ŀ
Funo	  A460CDev  Autor  Claudinei M. Benzi	 Data  11.02.92 
Ĵ
Descrio  Rotina de atualizacao de comissoes						  
Ĵ
 Uso		  MATA460													  
ٱ


*/
Function A460CDev(lSD1, nItemValTot)
LOCAL aCM[5], i, bMoeda:={ |x| "SM2->M2_MOEDA"+Str(x,1) }
LOCAL nCusto

//Ŀ
// Posiciona Arq. de moedas pela data de digitacao 
//
dbSelectArea("SM2")
Set SoftSeek On
dbSeek( IIF(lSD1,SD1->D1_DTDIGIT,dDataBase) )
Set SoftSeek Off
If !Found()
	dbSkip(-1)
Endif

nCusto := IIF(lSD1,SD1->D1_CUSTO,nItemValTot)
aCM[1] := nCusto
For i  := 2 To 5
	aCM[i] := ( nCusto / &(Eval(bMoeda,i) ) )
Next i
Return aCM

/*

Ŀ
Funo	 A460CALCTX Autor  Claudinei M. Benzi	 Data  11.02.92 
Ĵ
Descrio  Rotina de atualizacao de comissoes						  
Ĵ
 Uso		  MATA460													  
ٱ


*/
Function A460CALCTX(dDataCnd)
LOCAL nRet:=0, aVencs:=Condicao(100,SC5->C5_CONDPAG,,dDataCnd) ,ni ,nSoma:=0 , nAux, nDec,nVal,lFlag,nx

If nDuplif > 1 .or. SC5->C5_TIPO $"PI" .or. nTxDICMS = 0
	Return (0)
Endif

nDec:=Set(3,8)
lFlag:=.t.

Set (3,8)

nx:=0
ny:=0
For ni:=1 to Len(aVencs)
	nx += aVencs[ni][2]
Next

For ni:=1 to (Len(aVencs)-1)
	aVencs[ni][2]:= (aVencs[ni][2]/nx)*100
	ny+=(aVencs[ni][2])
Next
If Len(aVencs)>0
	aVencs[Len(aVencs)][2] := 100-ny
Endif

If Len(aVencs) > 1
	For ni :=1 to Len(aVencs)
		nSoma+=(aVencs[ni][1]-dDatabase)
	Next ni
	nPMP :=Round((nSoma/Len(aVencs)),8)

	lFlag := IIF(nPMP < 30,.f.,.t.)	 // Novo conceito de Parcela Mensal

	nSoma:=Round((nSoma/Len(aVencs)) / 30,8)
	If lFlag
		nAux :=Int(nSoma)
		nSoma-=Int(nSoma)
		If nSoma > .74999
			nAux++
		Elseif nSoma > .24999
			nAux+=.5
		Endif
		Set (3,3)
		nRet := 100 / ((1+nTxDICMS/100) ** nAux)
	Else
		nVal:=0
		For ni:=1 to Len(aVencs)
			Set (3,8)
			nAux:=(1+nTxDICMS/100) ** (1/30)
			nRet:=100/(nAux ** (aVencs[ni][1]-dDatabase))
			nRet :=Round(nRet,3)
			nVal += aVencs[ni][2] * (nRet / 100)
		Next
		Set(3,8)
		nRet := nVal
	Endif
Elseif Len(aVencs) == 1
	Set(3,8)
	nPMP=(aVencs[1][1]-dDatabase)
	nAux:=(1+nTxDICMS/100) ** (1/30)
	nRet:= 100 / (nAux ** (aVencs[1][1]-dDataBase))
	nRet:=Round(nRet,3)
Endif
Set(3,nDec)
Return (nRet)
/*

Ŀ
Funo	 A460CalDef Autor  Claudinei M. Benzi	   Data  11.02.92 
Ĵ
Descrio  Rotina de atualizacao de comissoes								  
Ĵ
 Uso		  MATA460																	  
ٱ


*/
Function A460CalDef(nPrcVen)
LOCAL nRet, bPrv := { |cTab| "B5_PRV"+cTab }, nAux,nDec
LOCAL lArredonda :=.f.,nCasas:=9

If cArreVis == "S" .or. (SC5->C5_TIPOCLI == "F" .and. cArreVis == "C")
	lArredonda :=.t.
	nCasas := 8
Endif

nDec:=Set(3,8)
If cCalcAcr == "U"
	nRet		:= nTxIdeal
	nD2Acres := ( ( 1 / ( nTxIdeal / 100) ) - 1 ) * 100
	nD2AcVal := Round(nPrcVen - (nPrcVen / (1+ nD2Acres / 100)), nCasas)
	nD2Acres := ((nPrcVen / ( nPrcVen - nD2AcVal )) - 1 ) * 100
	nAux		:= ((nPrcVen - nD2AcVal) / nPrcVen) * 100
	nRet		:= IIF( nAux < nTxIdeal, nTxIdeal, nAux )
Elseif cCalcAcr == "P"
	nD2Acres := SC5->C5_ACRSFIN
	nD2AcVal := Round(nPrcVen - (nPrcVen / (1+ nD2Acres / 100)), nCasas)
	nD2Acres := ((nPrcVen / ( nPrcVen - nD2AcVal )) - 1 ) * 100
	nAux		:= ((nPrcVen - nD2AcVal) / nPrcVen) * 100
	nRet		:= IIF( nAux < nTxIdeal, nTxIdeal, nAux )
Elseif cCalcAcr == "V"
	If cTabVista == '1'
		nValPres := SB1->B1_PRV1
	Elseif ! cTabVista $ " 1"
		dbSelectArea("SB5")
		dbSeek(cFilial+SB1->B1_COD)
		nValPres := &( EVAL( bPrv,cTabVista ) )
	Endif

	If Empty(cTabVista) .or. nValPres == 0
		nValPres := Round(nPrcVen / ((1+( GetMV("mv_juros")/100)) **;
		(nPMP/30)),nCasas)
	Endif
	nD2Acres := ((nPrcVen / nValpres) - 1 ) * 100
	nD2AcVal := nPrcVen - nValPres
	nAux		:= (nValPres / nPrcVen) * 100
	nRet		:= IIF( nAux < nTxIdeal, nTxIdeal, nAux )
Endif
Set(3,nDec)
Return nRet
/*

Ŀ
Funo	 A460Consum Autor  Marcos Bregantim 	   Data  30.06.95 
Ĵ
Descrio Verifica se o CFO e de material de consumo 					  
Ĵ
Uso		  MATA460																	  
ٱ


*/
Function A460Consum(cCfo)
LOCAL lRet := .F.
IF SF4->F4_CONSUMO == " "
	If SF4->F4_INCIDE == "S" .And. Substr(cCfo,2,2) $ "91/92/95"
		lRet := .T.
	Endif
Else
	lRet := (SF4->F4_CONSUMO == "S")
Endif
Return lRet

/*

Ŀ
Funo	 A460GETRAB Autor  Wilson Godoy			   Data  30.03.95 
Ĵ
Descrio  Gera arq. trabalho SC5 + SC6 (p/indice quando lJunta==.T.  
Ĵ
Uso		  MATA460																	  
ٱ


*/
Function A460Geratrab( aPedParc )
LOCAL nScan

dbSelectArea("SC9")

While !Eof() .And. cFilial == C9_FILIAL

	//Ŀ
	// Consiste se o pedido encontra-se entre os nao validos (liberacao total)
	//

	nScan := AScan( aPedParc, C9_PEDIDO )

	If nScan <> 0
		dbSkip()
		Loop
	EndIf

	dbSelectArea("SC5")
	dbSetOrder(1)
	dbSeek(cFilial+SC9->C9_PEDIDO)

	If !Found()
		dbSelectArea("SC9")
		dbSkip()
		Loop
	Endif

	RecLock("TRB",.T.)
	Replace	C5TIPO	 With SC5->C5_TIPO
	Replace	C5CLIENTE With SC5->C5_CLIENTE
	Replace	C5LOJACLI With SC5->C5_LOJACLI
	Replace	C5CONDPAG With SC5->C5_CONDPAG
	Replace	C5REAJUST With SC5->C5_REAJUST
	Replace	C5VEND1	 With SC5->C5_VEND1
	Replace	C5VEND2	 With SC5->C5_VEND2
	Replace	C5VEND3	 With SC5->C5_VEND3
	Replace	C5VEND4	 With SC5->C5_VEND4
	Replace	C5VEND5	 With SC5->C5_VEND5
	Replace	C5INCISS  With SC5->C5_INCISS
	Replace	C9CLIENTE With SC9->C9_CLIENTE
	Replace	C9LOJA	 With SC9->C9_LOJA
	Replace	C9AGREG	 With SC9->C9_AGREG
	Replace	C9FILIAL  With SC9->C9_FILIAL
	Replace	C9PEDIDO  With SC9->C9_PEDIDO
	Replace	C9NUMREG  With SC9->(RecNo())
	Replace	C9DATALIB With SC9->C9_DATALIB

	If lLibGrupo
		Replace C9GRUPO	With SC9->C9_GRUPO
		Replace C9PRODUTO With SC9->C9_PRODUTO
	Else
		Replace C9ITEM   with SC9->C9_ITEM
		Replace C9SEQUEN with SC9->C9_SEQUEN
	EndIf

	MsUnlock()
	dbSelectArea("SC9")
	dbSkip()
End
Return


/*


Ŀ
Funo	  A460Ordem Autor  Claudinei M. Benzi	   Data  10.01.92 
Ĵ
Descrio  Retorna preco unitario da tabela com desconto cascata 	  
Ĵ
Uso		  MatA460																	  
ٱ


*/
Function A460Ordem()

LOCAL cCondicao
LOCAL nOrdem		:= 1
LOCAL nIndex		:= 0
LOCAL cIndex		:= ""
Local oDlg
Local oQual

Private aOrd := { }
//Ŀ
// Pesquisa as ordens	existentes para o alias corrente 		  
//
RetIndex(Alias())
PesqOrd()


DEFINE MSDIALOG oDlg TITLE cCadastro From 09,0 To 20,50 OF oMainWnd
@ 0.5,  0 TO 6, 20.0 OF oDlg
@ 1.5,.7 LISTBOX oQual	 VAR cVar Fields HEADER "" SIZE 150,42
oQual:SetArray(aOrd)
oQual:bLine := { || {aOrd[oQual:nAT]}}
DEFINE SBUTTON FROM 10	,166	TYPE 1 ACTION (nOrdem:= oQual:nAT,oDlg:End()) ENABLE OF oDlg
DEFINE SBUTTON FROM 22.5,166	TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
ACTIVATE MSDIALOG oDlg

dbSetOrder(nOrdem)

If lFiltra
	//Ŀ
	// Cria Indice Temporario para a ordem escolhida. 		  
	//
	cIndex := CriaTrab(nil,.f.)
	AADD(aInd,cIndex)
	cCondicao:= 'C9_BLEST<>"10".And.C9_BLCRED<>"10".And.C9_FILIAL=="' + xFilial("SC9") + '"'

	If mv_par03==1   // Cons. Param. Abaixo	 - Sim/Nao
		cCondicao += '.And.C9_PEDIDO>="'+mv_par05+'".And.C9_PEDIDO<="'+mv_par06+'".And.'
		cCondicao += 'C9_CLIENTE>="'+mv_par07+'".And.C9_CLIENTE<="'+mv_par08+'"'
		cCondicao += '.And.C9_LOJA>="'+mv_par09+'".And.C9_LOJA<="'+mv_par10+'"'
		cCondicao += '.And.DTOS(C9_DATALIB)>="'+DTOS(mv_par11)+'".And.DTOS(C9_DATALIB)<="'+DTOS(mv_par12)+'"'
	EndIf

	IF !Empty(cFilSC9)
		cCondicao += ' .and. '+cFilSC9
	Endif
	IndRegua("SC9",cIndex,IndexKey(),,cCondicao,OemToAnsi(STR0006))		//"Selecionando Registros..."
	nIndex  := RetIndex("SC9")
	dbSelectArea("SC9")

	dbSetOrder(nIndex+1)
	dbGoTop()
EndIf

Return .T.

/*

Ŀ
Funo	  A460Frete Autor  Waldemiro L. Lustosa   Data  19.12.94 
Ĵ
Descrio  Programa para Rateio de Frete.									  
Ĵ
Sintaxe	  ExpA1 := A460Frete(ExpN1,ExpN2,ExpC1)							  
Ĵ
Uso		  MATA460																	  
ٱ


*/
Function A460Frete(cNumPed)
LOCAL cAlias := Alias(), nRecnoSC9 := SC9->(Recno())
LOCAL aFretes := {}, nItensFrete := 0, aTotsFrete := {}, k
LOCAL nTotalNf := 0,nParNf := 0, nItemNf := 1, cGrupoFrete := ""
LOCAL nAcumFrete := 0,nAcumSeguro := 0, nAcumDespesa:=0, lReajuste:= IIF(mv_par05==1,.T.,.F.)
LOCAL lLibGrupo:= IIF(GetMV("MV_LIBGRUP")=="S",.T.,.F.), lUltimo := .F.
Local nRegSC5:= SC5->(Recno()), nRegTRB,lJuntaEste
Local cSoma := "xxxxx"
Local nParFrete := 0, nParSeguro := 0, nParDespesa:= 0
Local cPedant	:= SC5->C5_NUM
Local cTipant	:= SC5->C5_TIPO
Local cTransAnt:= SC5->C5_TRANSP
Local cCliant	:= SC5->C5_CLIENTE+SC5->C5_LOJACLI
Local cReajant := SC5->C5_REAJUST
Local cVendant := SC5->C5_VEND1+SC5->C5_VEND2+SC5->C5_VEND3+SC5->C5_VEND4+SC5->C5_VEND5
Local cCondAnt := SC5->C5_CONDPAG
Local cIssAnt	:= SC5->C5_INCISS
Local lFreteMoe:= IIF(GetMv("MV_FRETMOE") == "S",.T.,.F.)
Local cAgregAnt, uRet, nFret,nSegu, nDesp,nPGrupo,nPAgreg

IF lJunta
	nRegTRB := TRB->(Recno())
	dbSelectArea("TRB")
Else
	dbSelectArea("SC9")
Endif
lFim := .f.

nItensFrete:=a460NumIt(cSerie)

While !Eof() .And. &cCondSC9 .And. If( lJunta,((TRB->C5CLIENTE+TRB->C5LOJACLI) == cCliAnt),.T. )

	If lJunta
		dbSelectArea("SC9")
		dbGoto(TRB->C9NUMREG)
		dbSelectArea("SC5")
		dbSeek(cFilial+SC9->C9_PEDIDO)
	Endif

	If !lJunta
		dbSelectArea("SC9")
		nReg := Recno()
		dbSkip()
		lFim := IIf(Eof(),.T.,.F.)
		dbSkip(-1)
	Else
		dbSelectArea("TRB")
		nReg := Recno()
		dbSkip()
		lFim := IIf(Eof(),.T.,.F.)
		dbSkip(-1)
		dbSelectArea("SC9")
	Endif

	lJuntaEste := lJunta
	If (SC9->C9_PEDIDO != cPedant ) .and. !lJunta
		dbSelectarea("SC9")
		lFim:=.F.
		lMudouPed := .T.
		Exit
	Endif

	If (SC5->C5_TIPO != cTipant) .Or. (SC5->C5_CLIENTE+SC5->C5_LOJACLI) != cCliant;
		.Or. (SC5->C5_REAJUST != cReajant) .Or. (SC5->C5_CONDPAG != cCondAnt) ;
		.Or. (SC5->C5_VEND1+SC5->C5_VEND2+SC5->C5_VEND3+SC5->C5_VEND4+SC5->C5_VEND5;
		!= cVendant) .Or. SC5->C5_INCISS != cIssAnt .Or. SC5->C5_TIPO != "N";
		.Or. (SC5->C5_TRANSP != cTransAnt)
		lJuntaEste := .F.
	Endif

	If SC9->C9_PEDIDO != cPedAnt
		dbSelectArea("SE4")
		dbSeek(cFilial+SC5->C5_CONDPAG)
		If SE4->E4_TIPO == "9"
			lJuntaEste :=.F.
		Endif
	Endif

	If SC9->C9_PEDIDO != cPedant .and. !lJuntaEste
		dbSelectarea("SC9")
		lMudouPed := .T.
		lFim:=.F.
		Exit
	Endif

	If cSoma != SC9->C9_PEDIDO
		nFret := Nil
		nSegu := Nil
		nDesp := Nil
		If l460Frete
			uRet := ExecBlock("M460FRET",.f.,.f.)
			IF ValType(uRet) = "N"
				nFret   := uRet
			ElseIF ValType(uRet) = "A"
				nFret := uRet[1]
				If Len(uRet) > 2
					nSegu := uRet[2]
					nDesp := uRet[3]
				ElseIf Len(uRet) > 1
					nSegu := uRet[2]
				Endif
			Endif
		EnDif
		IF nFret != Nil
			nParFrete	+= nFret
		Else
			nParfrete	+= IIF(lFreteMoe .And. SC5->C5_MOEDA > 1,xMoeda(SC5->C5_FRETE,SC5->C5_MOEDA,1,dDataBase),sc5->c5_frete)
		Endif
		If nSegu != Nil
			nParSeguro	+= nSegu
		Else
			nParSeguro	+= IIF(lFreteMoe .And. SC5->C5_MOEDA > 1,xMoeda(SC5->C5_SEGURO,SC5->C5_MOEDA,1,dDataBase),SC5->C5_SEGURO)
		Endif
		If nDesp != Nil
			nParDespesa += nDesp
		Else
			nParDespesa += IIF(lFreteMoe .And. SC5->C5_MOEDA > 1,xMoeda(SC5->C5_DESPESA,SC5->C5_MOEDA,1,dDataBase),SC5->C5_DESPESA)
		Endif
		cSoma 		:= SC9->C9_PEDIDO
	Endif
	cPedAnt			:= SC9->C9_PEDIDO
	A460AcumIt(@aTotsFrete,SC9->C9_QTDLIB,lReajuste,0)
	nTotalNf 		+= aTotsFrete[Len(aTotsFrete)][4]
	Aadd(aTotsFrete[Len(aTotsFrete)],SC9->C9_GRUPO)
	Aadd(aTotsFrete[Len(aTotsFrete)],SC9->C9_AGREG)
	nPGrupo := Len(aTotsFrete[1])-1
	nPAgreg := nPGrupo+1
	If !lJunta
		dbSelectArea("SC9")
		dbSkip()
	Else
		dbSelectArea("TRB")
		dbSkip()
	EndIf
End
//Ŀ
// Verifica se o valor total a ser faturado (nTotalNF), e' maior      
// ou igual ao valor minimo estipulado nas perguntas, a nota fiscal   
// so e gerada se o valor total a ser faturado for maior ou igual	  
// ao valor minimo estipulado													  
//
If nTotalNf >= nValorMin
	cGrupoFrete := aTotsFrete[1][nPGrupo]
	For k := 1 To Len(aTotsFrete)
		cAgregAnt := aTotsFrete[k][nPAgreg]
		nParNf  += aTotsFrete[k][4]
		lUltimo := .F.
		//Ŀ
		// Verifica, quando lLibGrupo .F. (MV_LIBGRUPO=N), se esta no ulti-   
		// mo produto da NF. ou, quando lLibGrupo .T. (MV_LIBGRUPO=S), se	  
		// esta no ultimo produto do ultimo grupo da NF.. 						  
		//
		If ( nItemNf == nItensFrete .And. !lLibGrupo ) .Or. ;
			(nItemNf  == nItensFrete .And. IIf( k == Len(aTotsFrete), .T. ,;
			aTotsFrete[k][nPGrupo] != aTotsFrete[k+1][nPGrupo] ) .And. lLibGrupo) .Or.;
				(If(k==Len(aTotsFrete),.T.,cAgregAnt != aTotsFrete[k+1][nPAgreg]))

			If k == Len(aTotsFrete)
				Aadd(aFretes,{ nParFrete	- nAcumFrete ,;
				nParSeguro	- nAcumSeguro,;
				nParDespesa - nAcumDespesa })
				lUltimo := .T.
			Else
				Aadd(aFretes,{ Round(nParFrete  *(nParNf/nTotalNf),2),;
				Round(nParSeguro *(nParNf/nTotalNf),2),;
				Round(nParDespesa*(nParNf/nTotalNf),2) })

				nAcumFrete	+= Round(nParFrete*(nParNf/nTotalNf),2)
				nAcumSeguro += Round(nParSeguro*(nParNf/nTotalNf),2)
				nAcumDespesa+= Round(nParDespesa*(nParNf/nTotalNf),2)
			EndIf
			nItemNf := 0
			nParNf  := 0
		EndIf

		If lLibGrupo
			If IIf( k == Len(aTotsFrete) , .F. , cGrupoFrete != aTotsFrete[k+1][nPGrupo] )
				nItemNf++
				cGrupoFrete := aTotsFrete[k+1][nPGrupo]
			EndIf
		Else
			nItemNf++
		EndIf
	Next k

	If !lUltimo
		Aadd(aFretes,{ nParFrete-nAcumFrete  ,;
		nParSeguro-nAcumSeguro,;
		nParDespesa-nAcumDespesa })
	EndIf
	lFim		 := .f.
	//Ŀ
	// Valor minimo atendido															  
	//
	lValorMin := .T.
Else
	//Ŀ
	// Retorna que o valor minimo nao foi atendido 							  
	//
	lValorMin := .F.
Endif

dbSelectArea("SC5")
dbGoTo(nRegSC5)
If lJunta
	dbSelectArea("TRB")
	dbGoto(nRegTRB)
Endif
dbSelectArea("SC9")
dbGoTo(nRecnoSC9)
dbSelectArea(cAlias)
Return(aFretes)

/*


Ŀ
Funo	  A460Estor Autor  Claudinei M. Benzi	   Data  10.02.93 
Ĵ
Descrio  Rotina para estorno de itens liberados 						  
Ĵ
Uso		  MatA410																	  
ٱ


*/
Function A460Estor(cAlias,nReg,nOpc,cMarca,lInverte)
LOCAL nQtdLib ,cSaveMenuh ,nOpcA:=nOpcB:=0 ,aAC:={ "Abandona","Confirma" }, cNumPed
LOCAL nQtdVen ,aSave:={Alias(),IndexOrd(),Recno()}
LOCAL lYesNo:=.F.
//Ŀ
// Verifica Item ja Liberado / somente mv_par02 = 1       
//
IF mv_par02 == 1
	If C9_FILIAL != cFilial
		HELP(" ",1,"A000FI")
		Return (.T.)
	Endif
	If C9_BLEST == "10" .AND. C9_BLCRED == "10"
		HELP(" ",1,"NFEMITIDA")
		Return (.T.)
	Endif
Endif

IF mv_par02 == 1
	lYesNo := MsgYesNo(OemToAnsi(STR0008),OemToAnsi(STR0009))		//"Confirma o Estorno da Liberao?"###"Ateno"
Else
	lYesNo := MsgYesNo(OemToAnsi(STR0019),OemToAnsi(STR0009))		//"Confirma o Estorno da Liberao para todos os itens selecionados ?"###"Ateno"
Endif
If lYesNo
	IF mv_par02 == 1
		If Empty(c460Cond) .Or. &(c460Cond)
			a460Estorna()
		EndIf
	Else
		dbGoTop()
		While !Eof()
			If  Empty(C9_BLEST) .and. Empty(C9_BLCRED)
				If ((C9_FILIAL != cFilial) .And.;
					(C9_BLEST == "10" .AND. C9_BLCRED == "10")) .Or.;
					!((C9_OK != cMarca .And. lInverte) .Or.;
					(C9_OK == cMarca .And. !lInverte))
					dbSkip()
					Loop
				Endif
				If Empty(c460Cond) .Or. &(c460Cond)
					a460Estorna()
				EndIf
			Endif
			dbSkip()
		EndDo
	Endif
Endif
dbSelectArea(aSave[1])
dbSetOrder(aSave[2])
dbGoto(aSave[3])
Return

/*

Ŀ
Funo    A460MarkC9 Autor Jos Lucas              Data  01/06/99 
Ĵ
Descrio  Gravar marca no SC9 dos registros a Faturar baseados nos   
           Remitos Selecionadas para Faturamento (Argentina)...       
Ĵ
Sintaxe    A460MarkC9()                                               
ٱ


/*/
Function A460MarkC9(_cMarca,lInverte)
Local cCurAlias := ""

cCurAlias := Alias()

If cCurAlias == "SCN"
	If IsMark("CN_OK", _cMarca, lInverte)
		RecLock("SCN",.F.)
		Replace CN_OK With _cMarca
		MsUnLock()
		dbSelectArea("SC9")
		dbSeek(xFilial("SC9")+SCN->CN_REMITO+SCN->CN_ITEM)
		RecLock("SC9",.F.)
		Replace C9_OK With _cMarca
		MsUnLock()
	EndIf
Else
	If C9_PEDIDO >= mv_par03 .and. C9_PEDIDO <= mv_par04
		If C9_CLIENTE >= mv_par05 .and. C9_CLIENTE <= mv_par06
			If C9_LOJA >= mv_par07 .and. C9_LOJA <= mv_par08
				If lFiltra
					If C9_BLEST <>"10" .And.C9_BLCRED <>"10"
						RecLock("SC9")
						Replace C9_OK With _cMarca
						MsUnLock()
					EndIf
				Else
					RecLock("SC9")
					Replace C9_OK With _cMarca
					MsUnLock()
				EndIf
			End
		EndIf
	EndIf
EndIf
Return

/*/


Ŀ
Programa  MenuDef    Autor  Marco Bianchi          Data 03/12/2008
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()

Local aRotAdic	:= {}				// Array utilizado no ponto de entrada

Private aRotina	:= {{STR0001,            "Ma460Pesq",  0, 1, 0, .F.},;	//"Pesquisar"
               	    {STR0002,            "Ma460Ordem", 0, 1, 0, .F.},;	//"Ordem"
               	    {STR0003,            "Ma460Nota",  0, 4, 0, .F.},;	//"Prep. Doc's"
               	    {STR0004,            "Ma461Estor", 0, 4, 0, .F.},;	//"Estor.Doc's"
               	    {STR0038,            "Ma461View",  0, 2, 0, .F.},;	//"Visualiza Doc."
               	    {OemToAnsi(STR0035), "CTBC662",    0, 8, 0, NIL},;	//"Tracker Contbil"
               	    {STR0037,            "Ma461Conh",  0, 4, 0, .F.},;	//"Conhecimento"
               	    {STR0036,            "A461Legend", 0, 6, 0, .F.}}	//"Legenda"

//Ŀ
// Ponto de entrada - Adiciona rotinas ao aRotina       
//
If ExistBlock("MA461ROT")
	aRotAdic := ExecBlock("MA461ROT", .F., .F.)
	If ValType(aRotAdic) == "A"
		aEval(aRotAdic,{|x| aAdd(aRotina,x)})
	EndIf
EndIf
Return(aRotina)


//---------------------------------------------------------------------------------
/*/{Protheus.doc} IntegDef
Funo para integrao via Mensagem nica Totvs.

@author  Alison Leite
@version P12.1.22
@since   07/02/2019
/*/
//---------------------------------------------------------------------------------
Static Function IntegDef(cXML, cTypeTrans, cTypeMsg, cVersion, cTransac)
Return MATI461(cXML, cTypeTrans, cTypeMsg, cVersion, cTransac)