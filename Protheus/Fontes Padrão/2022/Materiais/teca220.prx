#INCLUDE "TECA220.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TECA220  ³ Autor ³ Eduardo Riera         ³ Data ³ 19/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Contrato Manutencao Preventiva                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA220                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TECA220()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private aRotina := MenuDef()

Private cCadastro := STR0006	//"Contrato de Manutencao Preventiva"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse( 6, 1,22,75,"AAJ")
dbSelectArea("AAJ")
dbSetOrder(1)

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³MenuDef   ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 08.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definição do aRotina (Menu funcional)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA220                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Array contendo as Rotinas a executar do programa      ³
	//³ ----------- Elementos contidos por dimensao ------------     ³
	//³ 1. Nome a aparecer no cabecalho                              ³
	//³ 2. Nome da Rotina associada                                  ³
	//³ 3. Usado pela rotina                                         ³
	//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
	//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
	//³    2 - Simplesmente Mostra os Campos                         ³
	//³    3 - Inclui registros no Bancos de Dados                   ³
	//³    4 - Altera o registro corrente                            ³
	//³    5 - Remove o registro corrente do Banco de Dados          ³
	//³    6 - Alteracao sem inclusao de registro                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aRotina := {	{ STR0001	,"AxPesqui"	  	,0	,1	,0	,.F.	}	,;	//"Pesquisar"
						{ STR0002	,"At220Visua"	,0	,2	,0	,.T.	}	,;	//"Visualizar"
						{ STR0003	,"At220Inclu"	,0	,3	,0	,.T.	}	,;	//"Incluir"
						{ STR0004	,"At220alter"	,0	,4	,0	,.T.	}	,;	//"Alterar"
						{ STR0005	,"At220Dele"	,0	,5	,0	,.T.	} 	}	//"Excluir"
Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT220INCLU³ Autor ³ Eduardo Riera         ³ Data ³ 19/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Inclusao de Contrato Manutencao Preventiva     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT220INCLU(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA220                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³27/12/2006³ Conrado Q.    ³Bops 115741: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDados.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At220Inclu(cAlias,nReg,nOpc)

Local cIndAA3		:= CriaTrab(,.F.)
Local cFilAA3		:= ""
Local aSavaRot		:= aClone(aRotina)
Local cMarca		:= GetMark()
Local lInverte		:= .F.
Local oMark			:= Nil
Local nOpcA			:= 0
Local oDlg			:= Nil
Local oGetD			:= Nil
Local oCombo		:= Nil
Local aGet			:= {}
Local aCbo			:= {}
Local aCbx			:= {}
Local nCbx			:= 0
Local aIndexAA3		:= {}
Local aButtons		:= {}
Local nLinIni		:= 0
Local nColIni		:= 0
Local aSize			:= {}
Local aInfo			:= {}
Local aPosObj		:= {}
Local aPosGet		:= {}
Local aObjects	:= {}
Local nSaveSX8	:= GetSX8Len()
Local aNoFields	:= {}					// Campos que não serão exibidos na GetDados
Local nRow			:= 040 				// Coordenada vertical em pixels ou caracteres

Private aCols		:= {}
Private aHeader		:= {}
Private CAT220NUM	:= CriaVar("AAJ_CTAPRE")
Private CAT220CLI	:= CriaVar("AAJ_CODCLI")
Private CAT220LOJA	:= CriaVar("AAJ_LOJA")
Private CAT220ABRANG:= CriaVar("AAJ_ABRANG")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem do Array do Cabecalho                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_CTAPRE")
aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_CODCLI")
aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,SX3->X3_F3})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_LOJA")
aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_ABRANG")
aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
aCbx	:=	RetSx3Box(X3CBox(),,,SX3->X3_TAMANHO)
For nCbx := 1 To Len(aCbx)
	Aadd(aCbo,aCbx[nCbx][1])
Next nCbx

dbSelectArea( cAlias )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem aHeader, aCols³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aNoFields := {	"AAJ_CTAPRE",	"AAJ_CODCLI",	"AAJ_LOJA"	,"AAJ_ABRANG"	}

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,"AAJ"		,1				,/*cSeek*/		,;
					/*{||&cWhile}*/	,{|| .T. }	,aNoFields		,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,.T.			)
Endif

aCols[1][GdFieldPos("AAJ_ITEM")] := "01"

aSize := MsAdvSize()

aObjects := {}

AAdd( aObjects, { 100,  25, .T., .F. } )
AAdd( aObjects, { 100, 100, .T., .T. } )

aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 3, 3 }

aPosObj := MsObjSize( aInfo, aObjects )

DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME

aPosGet := MsObjGetPos(aSize[3]-aSize[1],310,;
			{{005,045,110,135,170,185,210,250}} )

@ nRow,aPosGet[1,1] SAY aGet[1][1] 	SIZE 050,009 	OF oDlg PIXEL
@ nRow,aPosGet[1,2] MSGET CAT220NUM 	SIZE 060,009 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAJ_CTAPRE') ;
					WHEN VisualSX3('AAJ_CTAPRE')

@ nRow,aPosGet[1,3] SAY aGet[2][1] 	SIZE 036,009 	OF oDlg PIXEL
@ nRow,aPosGet[1,4] MSGET CAT220CLI  SIZE 030,009 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAJ_CODCLI') ;
					WHEN VisualSX3('AAJ_CODCLI') ;
					F3 aGet[2][3]
@ nRow,aPosGet[1,5] SAY aGet[3][1] 	SIZE 036,009 	OF oDlg PIXEL
@ nRow,aPosGet[1,6] MSGET CAT220LOJA SIZE 010,009 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAJ_LOJA') ;
					WHEN VisualSX3('AAJ_LOJA') ;
					F3 aGet[3][3]
@ nRow,aPosGet[1,7] SAY aGet[4][1] 	SIZE 036,009 	OF oDlg PIXEL
@ nRow,aPosGet[1,8] MSCOMBOBOX oCombo VAR CAT220ABRANG ITEMS aCbo SIZE 060,009 OF oDlg PIXEL ;
					VALID CheckSX3('AAJ_ABRANG') ;
					WHEN VisualSX3('AAJ_ABRANG')
oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"AllwaysTrue","AT220TudOk","+AAJ_ITEM",.T.)
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,If(oGetd:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()})
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a MarkBrowse                                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nOpcA == 1 )
	cFilAA3 := "AA3_FILIAL=='"+xFilial("AA3")+"'.And."
	cFilAA3 += "AA3_CODCLI=='"+M->CAT220CLI+"'.And."
	cFilAA3 += If( M->CAT220ABRANG == "1","AA3_LOJA=='"+M->CAT220LOJA+"'.And.", " " )
	cFilAA3 += "AA3_CTAPRE=='"+Space(Len(CAT220NUM))+"'"
	
	bFiltraBrw 	:= {|| FilBrowse("AA3",@aIndexAA3,@cFilAA3) }
	Eval(bFiltraBrw)
	
	dbGotop()
	
	If ( !Eof() )
		dbSelectArea("SA1")
		dbSetOrder(1)
		dbSeek(xFilial("SA1")+M->CAT220CLI+If(M->CAT220ABRANG=="1",M->CAT220LOJA, ""))
		aRotina  := {}
		aButtons := { { "PESQUISA", { || PesqBrw() }, STR0001, STR0009 } }
		
		aObjects := {}
		AAdd( aObjects, { 100,  25, .T., .F. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta tela de entrada                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DEFINE MSDIALOG oDlg TITLE STR0007 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME //"Equipamentos com Contrato de Manutencao Preventiva"
		
		nLinIni := aPosObj[1,1]
		nColIni := aPosObj[1,2]
		
		@ nLinIni    , nColIni SAY STR0008 + M->CAT220CLI+If(M->CAT220ABRANG=="1","-"+M->CAT220LOJA,"") PIXEL //"Cliente: "
		@ nLinIni+12 , nColIni SAY SA1->A1_NOME PIXEL
		oMark:=MsSelect():New("AA3","AA3_OK",,,@lInverte,@cMarca,aPosObj[2])
		oMark:oBrowse:lhasMark = .T.
		oMark:oBrowse:lCanAllmark := .T.
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpcA:=1,oDlg:End()},{||nOpcA:=0,oDlg:End()},,aButtons)
		aRotina := aClone(aSavaRot)
	Else
		Help(" ",1,"REGNOIS")
		nOpcA := 0
	EndIf
EndIf
If ( nOpcA == 1 )
	Begin Transaction
	If ( aT220Grava(1) )
		EvalTrigger()
		While ( GetSX8Len() > nSaveSx8 )
			ConfirmSx8()
		EndDo
	EndIf
	End Transaction
Endif
While ( GetSX8Len() > nSaveSx8 )
	RollBackSx8()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza o uso da funcao FilBrowse e retorna os indices padroes.       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
EndFilBrw("AA3",aIndexAA3)

dbSelectArea(cAlias)
Return( nOpcA )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT220Visua³ Autor ³ Eduardo Riera         ³ Data ³ 19/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Visualizacao de Contrato Manutencao Preventiva ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT220VISUA(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA220                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³27/12/2006³ Conrado Q.    ³Bops 115741: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDados.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At220Visua(cAlias,nReg,nOpc)

Local cIndAA3		:= CriaTrab(,.F.)
Local cFilAA3		:= ""
Local aSavaRot		:= aClone(aRotina)
Local cMarca		:= GetMark()
Local lInverte		:= .F.
Local oMark			:= Nil
Local oDlg			:= Nil
Local oGetD			:= Nil
Local oCombo		:= Nil
Local aGet			:= {}
Local aCbo			:= {}
Local aCbx			:= {}
Local nCbx			:= 0
Local aIndexAA3		:= {}
Local aButtons		:= {}
Local nLinIni		:= 0
Local nColIni		:= 0
Local aSize			:= {}
Local aInfo			:= {}
Local aPosObj		:= {}
Local aPosGet		:= {}
Local aObjects		:= {}
Local cSeek			:= ""			// Seek para montagem da aCols
Local cWhile		:= ""			// While para montagem da aHeader
Local aNoFields	:= ""			// Campos que não serão adicionados ao aHeader
Local nRow			:= 040			// Coordenada vertical em pixels ou caracteres



Private aCols		:= {}
Private aHeader		:= {}
Private CAT220NUM	:= AAJ->AAJ_CTAPRE
Private CAT220CLI	:= AAJ->AAJ_CODCLI
Private CAT220LOJA	:= AAJ->AAJ_LOJA
Private CAT220ABRANG:= AAJ->AAJ_ABRANG

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem do Array do Cabecalho                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_CTAPRE")
aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_CODCLI")
aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,SX3->X3_F3})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_LOJA")
aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
dbSeek("AAJ_ABRANG")
aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
aCbx	:=	RetSx3Box(X3CBox(),,,SX3->X3_TAMANHO)
For nCbx := 1 To Len(aCbx)
	Aadd(aCbo,aCbx[nCbx][1])
Next nCbx

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem aHeader, aCols³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSeek	:= xFilial("AAJ")+M->CAT220NUM
cWhile	:= "AAJ->AAJ_FILIAL+AAJ->AAJ_CTAPRE"
aNoFields := {	"AAJ_CTAPRE"	,"AAJ_CODCLI"	,"AAJ_LOJA"	,"AAJ_ABRANG"	}

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,"AAJ"		,1				,cSeek			,;
					{|| &cWhile }	,{|| .T. }	,aNoFields		,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/		)
Endif

aSize := MsAdvSize()

aObjects := {}

AAdd( aObjects, { 100,  25, .T., .F. } )
AAdd( aObjects, { 100, 100, .T., .T. } )

aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 3, 3 }

aPosObj := MsObjSize( aInfo, aObjects )

DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME

aPosGet := MsObjGetPos(aSize[3]-aSize[1],310,;
			{{005,045,110,135,170,185,210,250}} )

@ nRow,aPosGet[1,1] SAY aGet[1][1] 	SIZE 050,009 	OF oDlg PIXEL
@ nRow,aPosGet[1,2] MSGET CAT220NUM 	SIZE 060,009 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAJ_CTAPRE') ;
					WHEN .F.
@ nRow,aPosGet[1,3] SAY aGet[2][1] 	SIZE 036,009 	OF oDlg PIXEL
@ nRow,aPosGet[1,4] MSGET CAT220CLI  SIZE 030,009 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAJ_CODCLI') ;
					WHEN .F.
@ nRow,aPosGet[1,5] SAY aGet[3][1] 	SIZE 036,009 	OF oDlg PIXEL
@ nRow,aPosGet[1,6] MSGET CAT220LOJA SIZE 010,009 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAJ_LOJA') ;
					WHEN .F.
@ nRow,aPosGet[1,7] SAY aGet[4][1] 	SIZE 036,009 	OF oDlg PIXEL
@ nRow,aPosGet[1,8] MSCOMBOBOX oCombo VAR CAT220ABRANG ITEMS aCbo SIZE 060,009 OF oDlg PIXEL ;
					VALID CheckSX3('AAJ_ABRANG') ;
					WHEN .F.
oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"AllwaysTrue","AllwayTrue","+AAJ_ITEM",.T.)
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End()},{||oDlg:End()})
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a MarkBrowse                                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFilAA3 := "AA3_FILIAL=='"+xFilial("AA3")+"'.And."
cFilAA3 += "AA3_CODCLI=='"+M->CAT220CLI+"'.And."
cFilAA3 += If( M->CAT220ABRANG == "1","AA3_LOJA=='"+M->CAT220LOJA+"'.And.", " " )
cFilAA3 += "AA3_CTAPRE=='"+M->CAT220NUM+"'"

bFiltraBrw 	:= {|| FilBrowse("AA3",@aIndexAA3,@cFilAA3) }
Eval(bFiltraBrw)

dbGotop()

If ( !Eof() )
	dbSelectArea("SA1")
	dbSetOrder(1)
	dbSeek(xFilial("SA1")+M->CAT220CLI+If(M->CAT220ABRANG=="1",M->CAT220LOJA,""))
	aRotina := {}
	aButtons := { { "PESQUISA", { || PesqBrw() }, STR0001, STR0009 } }
	
	aObjects := {}
	AAdd( aObjects, { 100,  20, .T., .F. } )
	AAdd( aObjects, { 100, 100, .t., .t. } )
	
	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
	aPosObj := MsObjSize( aInfo, aObjects )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta tela de entrada                                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE MSDIALOG oDlg TITLE STR0007 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME   //"Equipamentos com Contrato de Manutencao Preventiva"
	
	nLinIni := aPosObj[1,1]
	nColIni := aPosObj[1,2]
	
	@ nLinIni    , nColIni SAY STR0008 + M->CAT220CLI+If(M->CAT220ABRANG=="1","-"+M->CAT220LOJA,"") PIXEL //"Cliente: "
	@ nLinIni+12 , nColIni SAY SA1->A1_NOME PIXEL
	oMark:=MsSelect():New("AA3","",,,@lInverte,@cMarca,aPosObj[2])
	oMark:oBrowse:lhasMark = .F.
	oMark:oBrowse:lCanAllmark := .F.
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpcA:=1,oDlg:End()},{||nOpcA:=0,oDlg:End()},,aButtons)
	aRotina := aClone(aSavaRot)
Else
	Help(" ",1,"REGNOIS")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza o uso da funcao FilBrowse e retorna os indices padroes.       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
EndFilBrw("AA3",aIndexAA3)

dbSelectArea(cAlias)
Return( .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT220Alter³ Autor ³ Eduardo Riera         ³ Data ³ 19/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Alteracao de Contrato Manutencao Preventiva    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT220Alter(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA220                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³27/12/2006³ Conrado Q.    ³Bops 115741: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDados.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At220Alter(cAlias,nReg,nOpc)

Local cIndAA3		:= CriaTrab(,.F.)
Local cFilAA3		:= ""
Local aSavaRot		:= aClone(aRotina)
Local cMarca		:= GetMark()
Local lInverte		:= .F.
Local oMark			:= Nil
Local nOpcA			:= 0
Local oDlg			:= Nil
Local oGetD			:= Nil
Local oCombo		:= Nil
Local aGet			:= {}
Local aCbo			:= {}
Local aCbx			:= {}
Local nCbx			:= 0
Local aIndexAA3		:= {}
Local aButtons		:= {}
Local nLinIni		:= 0
Local nColIni		:= 0
Local aSize			:= {}
Local aInfo			:= {}
Local aPosObj		:= {}
Local aPosGet		:= {}
Local aObjects		:= {}
Local nSaveSX8		:= GetSX8Len()
Local cSeek			:= ""		   						// Seek para montagem da aCols
Local cWhile		:= ""								// While para montagem da aHeader
Local aNoFields		:= ""								// Campos que não serão adicionados ao aHeader
Local aTravas		:= {}								// Campos que foram travados
Local lTravas		:= .F.								// Se todos os campos foram travados com sucesso
Local nRow			:= 040 							// Coordenada vertical em pixels ou caracteres


Private aCols		:= {}
Private aHeader		:= {}
Private CAT220NUM	:= AAJ->AAJ_CTAPRE
Private CAT220CLI	:= AAJ->AAJ_CODCLI
Private CAT220LOJA	:= AAJ->AAJ_LOJA
Private CAT220ABRANG:= AAJ->AAJ_ABRANG

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem do Array do Cabecalho                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_CTAPRE")
Aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_CODCLI")
Aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,SX3->X3_F3})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_LOJA")
Aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_ABRANG")
Aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
aCbx	:=	RetSx3Box(X3CBox(),,,SX3->X3_TAMANHO)
For nCbx := 1 To Len(aCbx)
	Aadd(aCbo,aCbx[nCbx][1])
Next nCbx

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem aHeader, aCols³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSeek	:= xFilial("AAJ")+M->CAT220NUM
cWhile	:= "AAJ->AAJ_FILIAL+AAJ->AAJ_CTAPRE"
aNoFields := {	"AAJ_CTAPRE",	"AAJ_CODCLI",	"AAJ_LOJA",	"AAJ_ABRANG"	}

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	lTravas := FillGetDados(	nOpc			,"AAJ"		,1				,cSeek									,;
		   		  				{|| &cWhile }	,{|| .T. }	,aNoFields		,/*aYesFields*/							,; 
								/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/								,;
								/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| AtTravaReg("AAJ", aTravas) }	)
Endif

If Empty(aCols[1][GdFieldPos("AAJ_ITEM")])
	aCols[1][GdFieldPos("AAJ_ITEM")] := "01"
Endif

If ( lTravas )
	
	aSize := MsAdvSize()
	
	aObjects := {}
	
	AAdd( aObjects, { 100,  25, .T., .F. } )
	AAdd( aObjects, { 100, 100, .T., .T. } )
	
	aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 3, 3 }
	
	aPosObj := MsObjSize( aInfo, aObjects )
	
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME
	
	aPosGet := MsObjGetPos(aSize[3]-aSize[1],310,;
				{{005,045,110,135,170,185,210,250}} )
	
	
	@ nRow,aPosGet[1,1] SAY aGet[1][1] 	SIZE 050,009 	OF oDlg PIXEL
	@ nRow,aPosGet[1,2] MSGET CAT220NUM 	SIZE 060,009 	OF oDlg PIXEL ;
						PICTURE "@!" ;
						VALID CheckSX3('AAJ_CTAPRE') ;
						WHEN .F.
	@ nRow,aPosGet[1,3] SAY aGet[2][1] 	SIZE 036,009 	OF oDlg PIXEL
	@ nRow,aPosGet[1,4] MSGET CAT220CLI  SIZE 030,009 	OF oDlg PIXEL ;
						PICTURE "@!" ;
						VALID CheckSX3('AAJ_CODCLI') ;
						WHEN .F.
	@ nRow,aPosGet[1,5] SAY aGet[3][1] 	SIZE 036,009 	OF oDlg PIXEL
	@ nRow,aPosGet[1,6] MSGET CAT220LOJA SIZE 010,009 	OF oDlg PIXEL ;
						PICTURE "@!" ;
						VALID CheckSX3('AAJ_LOJA') ;
						WHEN .F.
	@ nRow,aPosGet[1,7] SAY aGet[4][1] 	SIZE 036,009 	OF oDlg PIXEL
	@ nRow,aPosGet[1,8] MSCOMBOBOX oCombo VAR CAT220ABRANG ITEMS aCbo SIZE 060,009 OF oDlg PIXEL ;
						VALID CheckSX3('AAJ_ABRANG') ;
						WHEN VisualSX3('AAJ_ABRANG')
	oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"AllwaysTrue","AT220TudOk","+AAJ_ITEM",.T.)
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,If(oGetd:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()})
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta a MarkBrowse                                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( nOpcA == 1 )
		cFilAA3 := "AA3_FILIAL=='"+xFilial("AA3")+"'.And."
		cFilAA3 += "AA3_CODCLI=='"+M->CAT220CLI+"'.And."
		cFilAA3 += If( M->CAT220ABRANG == "1","AA3_LOJA=='"+M->CAT220LOJA+"'.And.", " " )
		cFilAA3 += "(AA3_CTAPRE=='"+M->CAT220NUM+"'.OR."
		cFilAA3 += "AA3_CTAPRE=='"+Space(Len(CAT220NUM))+"')"
		
		bFiltraBrw 	:= {|| FilBrowse("AA3",@aIndexAA3,@cFilAA3) }
		Eval(bFiltraBrw)
		
		dbGotop()
		
		If ( !Eof() )
			dbSelectArea("AA3")
			dbGotop()
			While ( !Eof() )
				dbSelectArea("AA3")
				If ( !Empty(AA3->AA3_CTAPRE) )
					If ( RecLock("AA3") )
						AA3->AA3_OK := cMarca
						MsUnLock()
					EndIf
				EndIf
				If !AtTravaReg("AA3", aTravas)
					lTravas := .F.
				EndIf
				dbSkip()
			EndDo
			dbSelectArea("SA1")
			dbSetOrder(1)
			dbSeek(xFilial("SA1")+M->CAT220CLI+If(M->CAT220ABRANG=="1",M->CAT220LOJA,""))
			aRotina  := {}
			aButtons := { { "PESQUISA", { || PesqBrw() }, STR0001, STR0009 } }
			
			aObjects := {}
			AAdd( aObjects, { 100,  25, .T., .F. } )
			AAdd( aObjects, { 100, 100, .T., .T. } )
			
			aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
			aPosObj := MsObjSize( aInfo, aObjects )
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Monta tela de entrada                                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DEFINE MSDIALOG oDlg TITLE STR0007 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME	  //"Equipamentos com Contrato de Manutencao Preventiva"
			
			nLinIni := aPosObj[1,1]
			nColIni := aPosObj[1,2]
			
			@ nLinIni    , nColIni SAY STR0008 + M->CAT220CLI+If(M->CAT220ABRANG=="1","-"+M->CAT220LOJA,"") PIXEL //"Cliente: "
			@ nLinIni+12 , nColIni SAY SA1->A1_NOME PIXEL
			oMark:=MsSelect():New("AA3","AA3_OK",,,@lInverte,@cMarca,aPosObj[2])
			oMark:oBrowse:lhasMark = .T.
			oMark:oBrowse:lCanAllmark := .T.
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpcA:=1,oDlg:End()},{||nOpcA:=0,oDlg:End()},,aButtons)
			aRotina := aClone(aSavaRot)
		Else
			Help(" ",1,"REGNOIS")
			nOpcA := 0
		EndIf
	EndIf
	If ( nOpcA == 1 )
		Begin Transaction
		If ( aT220Grava(2) )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSx8()
			EndDo
		EndIf
		End Transaction
	Endif
	While ( GetSX8Len() > nSaveSx8 )
		RollBackSx8()
	EndDo
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Efetua o destravamento dos registros travados.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AtDestravaReg( aTravas )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza o uso da funcao FilBrowse e retorna os indices padroes.       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
EndFilBrw("AA3",aIndexAA3)

dbSelectArea(cAlias)
Return( nOpcA )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT220DELE ³ Autor ³ Eduardo Riera         ³ Data ³ 19/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Exclusao de Contrato Manutencao Preventiva     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT220Dele (ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA220                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³27/12/2006³ Conrado Q.    ³Bops 115741: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDados.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At220Dele(cAlias,nReg,nOpc)

Local cIndAA3		:= CriaTrab(,.F.)
Local cFilAA3		:= ""
Local aSavaRot		:= aClone(aRotina)
Local cMarca		:= GetMark()
Local lInverte		:= .F.
Local oMark
Local nOpcA			:= 0
Local oDlg,oGetD,oCombo
Local aGet			:= {}
Local aCbo			:= {}
Local aCbx			:= {}
Local nCbx			:= 0
Local aIndexAA3		:= {}
Local aButtons		:= {}
Local nLinIni		:= 0
Local nColIni		:= 0
Local aSize			:= {}
Local aInfo			:= {}
Local aPosObj		:= {}
Local aPosGet		:= {}
Local aObjects		:= {}
Local nSaveSX8		:= GetSX8Len()
Local cSeek			:= ""			// Seek para montagem da aCols
Local cWhile		:= ""			// While para montagem da aHeader
Local aNoFields		:= ""			// Campos que não serão adicionados ao aHeader
Local aTravas		:= {}			// Campos que foram travados
Local lTravas		:= .F.			// Se todos os campos foram travados com sucesso
Local nRow			:= 040			// Coordenada vertical em pixels ou caracteres

Private aCols		:= {}
Private aHeader		:= {}
Private CAT220NUM	:= AAJ->AAJ_CTAPRE
Private CAT220CLI	:= AAJ->AAJ_CODCLI
Private CAT220LOJA	:= AAJ->AAJ_LOJA
Private CAT220ABRANG:= AAJ->AAJ_ABRANG

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem do Array do Cabecalho                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_CTAPRE")
Aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_CODCLI")
Aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,SX3->X3_F3})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_LOJA")
Aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AAJ_ABRANG")
Aadd(aGet,{X3Titulo(),SX3->X3_PICTURE,})
aCbx	:=	RetSx3Box(X3CBox(),,,SX3->X3_TAMANHO)
For nCbx := 1 To Len(aCbx)
	Aadd(aCbo,aCbx[nCbx][1])
Next nCbx

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem aHeader, aCols³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSeek	:= xFilial("AAJ")+M->CAT220NUM
cWhile	:= "AAJ->AAJ_FILIAL+AAJ->AAJ_CTAPRE"
aNoFields := {	"AAJ_CTAPRE",	"AAJ_CODCLI",	"AAJ_LOJA",	"AAJ_ABRANG"	}

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	lTravas := FillGetDados(	nOpc			,"AAJ"		,1				,cSeek									,;
		   		  				{|| &cWhile }	,{|| .T. }	,aNoFields		,/*aYesFields*/							,; 
								/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/								,;
								/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| AtTravaReg("AAJ", aTravas) }	)
Endif

If Empty(aCols[1][GdFieldPos("AAJ_ITEM")])
	aCols[1][GdFieldPos("AAJ_ITEM")] := "01"
Endif

If ( lTravas )
	
	aSize := MsAdvSize()
	
	aObjects := {}
	
	AAdd( aObjects, { 100,  25, .T., .F. } )
	AAdd( aObjects, { 100, 100, .T., .T. } )
	
	aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 3, 3 }
	
	aPosObj := MsObjSize( aInfo, aObjects )
	
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME
	
	aPosGet := MsObjGetPos(aSize[3]-aSize[1],310,;
				{{005,045,110,135,170,185,210,250}} )
	
	@ nRow,aPosGet[1,1] SAY aGet[1][1] 	SIZE 050,009 	OF oDlg PIXEL
	@ nRow,aPosGet[1,2] MSGET CAT220NUM 	SIZE 060,009 	OF oDlg PIXEL ;
						PICTURE "@!" ;
						VALID CheckSX3('AAJ_CTAPRE') ;
						WHEN .F.
	@ nRow,aPosGet[1,3] SAY aGet[2][1] 	SIZE 036,009 	OF oDlg PIXEL
	@ nRow,aPosGet[1,4] MSGET CAT220CLI  SIZE 030,009 	OF oDlg PIXEL ;
						PICTURE "@!" ;
						VALID CheckSX3('AAJ_CODCLI') ;
						WHEN .F.
	@ nRow,aPosGet[1,5] SAY aGet[3][1] 	SIZE 036,009 	OF oDlg PIXEL
	@ nRow,aPosGet[1,6] MSGET CAT220LOJA SIZE 010,009 	OF oDlg PIXEL ;
						PICTURE "@!" ;
						VALID CheckSX3('AAJ_LOJA') ;
						WHEN .F.
	@ nRow,aPosGet[1,7] SAY aGet[4][1] 	SIZE 036,009 	OF oDlg PIXEL
	@ nRow,aPosGet[1,8] MSCOMBOBOX oCombo VAR CAT220ABRANG ITEMS aCbo SIZE 060,009 OF oDlg PIXEL ;
						VALID CheckSX3('AAJ_ABRANG') ;
						WHEN .F.
	oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"AllwaysTrue","AT220TudOk","+AAJ_ITEM",.T.)
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,oDlg:End()},{||oDlg:End()})
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta a MarkBrowse                                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( nOpcA == 1 )
		cFilAA3 := "AA3_FILIAL=='"+xFilial("AA3")+"'.And."
		cFilAA3 += "AA3_CODCLI=='"+M->CAT220CLI+"'.And."
		cFilAA3 += If( M->CAT220ABRANG == "1","AA3_LOJA=='"+M->CAT220LOJA+"'.And.", " " )
		cFilAA3 += "AA3_CTAPRE=='"+M->CAT220NUM+"'"
		
		bFiltraBrw 	:= {|| FilBrowse("AA3",@aIndexAA3,@cFilAA3) }
		Eval(bFiltraBrw)
		
		If ( !Eof() )
			dbSelectArea("AA3")
			dbGotop()
			While ( !Eof() )
				dbSelectArea("AA3")
				If ( !Empty(AA3->AA3_CTAPRE) )
					If ( RecLock("AA3") )
						AA3->AA3_OK := cMarca
						MsUnLock()
					EndIf
				EndIf
				If !AtTravaReg("AA3", aTravas)
					lTravas := .F.
				EndIf
				dbSkip()
			EndDo
			dbSelectArea("SA1")
			dbSetOrder(1)
			dbSeek(xFilial("SA1")+M->CAT220CLI+If(M->CAT220ABRANG=="1",M->CAT220LOJA,""))
			
			aRotina  := {}
			aButtons := { { "PESQUISA", { || PesqBrw() }, STR0001, STR0009 } }
			
			aObjects := {}
			AAdd( aObjects, { 100,  20, .T., .F. } )
			AAdd( aObjects, { 100, 100, .t., .t. } )
			
			aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
			aPosObj := MsObjSize( aInfo, aObjects )
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Monta tela de entrada                                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DEFINE MSDIALOG oDlg TITLE STR0007 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL  STYLE WS_DLGFRAME//"Equipamentos com Contrato de Manutencao Preventiva"
			
			nLinIni := aPosObj[1,1]
			nColIni := aPosObj[1,2]
			
			@ nLinIni    , nColIni SAY STR0008 + M->CAT220CLI+If(M->CAT220ABRANG=="1","-"+M->CAT220LOJA,"") PIXEL //"Cliente: "
			@ nLinIni+12 , nColIni SAY SA1->A1_NOME PIXEL
			oMark:=MsSelect():New("AA3",,,,@lInverte,@cMarca,aPosObj[2])
			oMark:oBrowse:lhasMark = .T.
			oMark:oBrowse:lCanAllmark := .T.
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpcA:=1,oDlg:End()},{||nOpcA:=0,oDlg:End()},,aButtons)
			aRotina := aClone(aSavaRot)
		Else
			Help(" ",1,"REGNOIS")
			nOpcA := 0
		EndIf
	EndIf
	If ( nOpcA == 1 )
		Begin Transaction
		If ( aT220Grava(3) )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSx8()
			EndDo
		EndIf
		End Transaction
	Endif
	While ( GetSX8Len() > nSaveSx8 )
		RollBackSx8()
	EndDo
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Destrava os Registros                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AtDestravaReg( aTravas )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza o uso da funcao FilBrowse e retorna os indices padroes.       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
EndFilBrw("AA3",aIndexAA3)

dbSelectArea(cAlias)
Return( nOpcA )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT220Grava³ Autor ³ Eduardo Riera         ³ Data ³ 17.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualizar o Arquivo de Contratos e as amarracoes           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 : Indica se foi possivel gravar                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : 1 - Incluir , 2 - Alterar , 3 - Excluir            ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AT220Grava(nOpcao)

Local nCntFor,nCntFor2
Local bCampo	:= {|x| FieldName(x) }
Local lGravou	:= .F.
Local nUsado	:= 0
Local nPosIte	:= aScan(aHeader,{|x| AllTrim(x[2])=="AAJ_ITEM"})

Do Case
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³A Inclusao e alteracao e efetuada com MarkBrowse                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case nOpcao <> 3
		dbSelectArea("AA3")
		dbGotop() // IndRegua()
		While ( !Eof() )
			RecLock("AA3")
			If ( IsMark("AA3_OK",ThisMark(),ThisInv()) )
				AA3->AA3_CTAPRE := M->CAT220NUM
				lGravou := .T.
			Else
				AA3->AA3_CTAPRE := ""
			EndIf
			dbSelectArea("AA3")
			dbSkip()
		EndDo
		If ( lGravou )
			For nCntFor := 1 To Len(aCols)
				dbSelectArea("AAJ")
				dbSetOrder(1)
				dbSeek(xFilial("AAJ")+M->CAT220NUM+aCols[nCntFor][nPosIte])
				If aCols[ nCntFor, Len( aCols[ nCntFor ] ) ]
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Item do acols excluido                                                 ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If AAJ->( Found() )
						RecLock( "AAJ", .F. )
						AAJ->( dbDelete() )
						AAJ->( MsUnLock() )
					EndIf
				Else
					RecLock("AAJ",!Found())
					For nCntFor2 := 1 To Len(aHeader)
						FieldPut(FieldPos(aHeader[nCntFor2][2]),aCols[nCntFor][nCntFor2])
					Next nCntFor2
					AAJ->AAJ_FILIAL := xFilial("AAJ")
					AAJ->AAJ_CTAPRE := M->CAT220NUM
					AAJ->AAJ_CODCLI := M->CAT220CLI
					AAJ->AAJ_LOJA   := M->CAT220LOJA
					If(!Empty(AAJ->(FieldPos("AAJ_ABRANG"))),AAJ->AAJ_ABRANG := M->CAT220ABRANG,)
						AAJ->( MsUnLock() )
					EndIf
			Next nCntFor
		Else
			dbSelectArea("AAJ")
			dbSetOrder(1)
			dbSeek(xFilial("AAJ")+M->CAT220NUM)
			While ( !Eof() .And. xFilial("AAJ") == AAJ->AAJ_FILIAL .And.;
				M->CAT220NUM   == AAJ->AAJ_CTAPRE )
				RecLock("AAJ")
				dbDelete()
				dbSelectArea("AAJ")
				dbSkip()
			EndDo
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³A exclusao  e' efetuada com getdados                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case nOpcao == 3
		dbSelectArea("AA3")
		dbGotop() // IndRegua()
		While ( !Eof() )
			RecLock("AA3")
			If ( IsMark("AA3_OK",ThisMark(),ThisInv()) )
				AA3->AA3_CTAPRE := ""
			EndIf
			dbSelectArea("AA3")
			dbSkip()
		EndDo
		dbSelectArea("AAJ")
		dbSetOrder(1)
		dbSeek(xFilial("AAJ")+M->CAT220NUM)
		While ( !Eof() .And. xFilial("AAJ") == AAJ->AAJ_FILIAL .And.;
			M->CAT220NUM   == AAJ->AAJ_CTAPRE )
			RecLock("AAJ")
			dbDelete()
			dbSelectArea("AAJ")
			dbSkip()
		EndDo
EndCase

Return(lGravou)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT220TudOk³ Autor ³ Eduardo Riera         ³ Data ³ 19.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao TudoOk da GetDados                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 : .T. se Ok , .F. se nao Ok                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At220TudOk()

Local nPosData	:= aScan(aHeader,{|x| AllTrim(x[2])=="AAJ_DTPREV" })
Local nCntFor	:= 0
Local nUsado	:= Len(aHeader)
Local lRetorno	:= .T.
Local dUltData	:= Ctod("")
Local nCount	:= 0

For nCntFor := 1 To Len(aCols)
	If ( !aCols[nCntFor][nUsado+1] )
		If ( aCols[nCntFor][nPosData]<=dUltData )
			Help(" ",1,"AT220TUDOK")
			lRetorno := .F.
		Else
			dUltData := aCols[nCntFor][nPosData]
		EndIf
	EndIf
	
	If lRetorno
		If ( !aCols[nCntFor][nUsado+1] )
			nCount++
		EndIf
	EndIf
Next

If Empty( nCount )
	Help( " ", 1, "AT220NOIT" ) // Para Excluir todos os itens utilize a rotina de exclusao
	lRetorno := .F.
EndIf

Return(lRetorno)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³At220ValAb³ Autor ³ Kleber Dias Gomes     ³ Data ³29/05/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validacao da digitacao da abrangencia                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At220ValAb()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA220                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function At220ValAb()

LOCAL cConteudo := &( ReadVar() )
LOCAL lRet      := .T.

dbSelectArea("AAJ")
dbSetOrder(1)
DbSeek(xFilial("AAJ")+M->CAT220NUM)

If Altera
	If cConteudo == "1"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Nao permite alterar para Cliente/Loja se estava em Cliente   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If AAJ->AAJ_ABRANG == "2"
			Help( " ", 1, "AT220ABRAN" ) // Nao e possivel alterar a abrangencia de cliente para cliente / loja
			lRet := .F.
		EndIf
	EndIf
EndIf

Return( lRet )