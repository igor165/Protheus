#Include "Mata024.ch"
#Include "Protheus.ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Mata024   ºAutor  ³Juliana Taveira     º Data ³  30/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Apuracao Cat 83											  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Mata024()

Local cCadastro := STR0001 //"Apuracao CAT83"
Local aSays		:= {}
Local aButtons	:= {}
LOCAL nOpca 	:= 0
LOCAL aCAP		:= {STR0002,STR0003,STR0004}  //"Confirma"###"Abandona"###"Parƒmetros"
Local cPerg		:=	"MTA024"
LocaL cTitulo	:=	STR0001 //"Apuracao CAT83"
Local cText1	:=	STR0010 //"Este programa faz a Apura‡„o de ICMS, conforme parƒmetros "
Local cText2	:=	STR0011 //"informados pelo usu rio."
Local cMV_Estado:=	SuperGetMv("MV_ESTADO")	// Def. estado empresa
Local lAborta   := .F.   

PRIVATE aRotina := {	{ OemToAnsi(STR0005)		,"AxPesqui"  	,0,1},;	 //"Pesquisar"
						{ OemToAnsi(STR0006)		,"AxAlter"		,0,2},;	 //"Visual"
						{ OemToAnsi(STR0007)		,"AxInclui"		,0,3},;	 //"Incluir"
						{ OemToAnsi(STR0008)		,"AxAlter"		,0,4},;  //"Alterar"
						{ OemToAnsi(STR0009)		,"AxAlter"		,0,5} }  //"Exclusao"

If !SuperGetMV("MV_CAT8309",,.F.) 
	MsgAlert("Verifique o conteúdo do parâmetro MV_CAT8309.")
	lAborta := .T.
Else
    If cMV_Estado <> "SP"
    	MsgAlert("Esta rotina só deve ser executada para o estado de São Paulo.")
        lAborta := .T.
    Else
        DbSelectArea ("CDZ")	//Cod.lcto cat83
        CDZ->(DbSetOrder (1))	    
        DbGoTop()     
        If Eof()
        	MsgAlert("É necessário fazer o cadastramento dos cód.lcto Cat83 primeiro, através da rotina Atualizações/Cadastros/Cod.Lcto Cat83.")
            lAborta := .T.
        Else
            DbSelectArea ("CCU")	//Amarracao Ficha x Cod.lcto cat83
            CCU->(DbSetOrder (1))	    
            DbGoTop()     
            If Eof()
        	    MsgAlert("É necessário fazer o relacionamento das fichas com os cód.lcto Cat83 primeiro, através da rotina Atualizações/Cadastros/Amarração Ficha x Cod.Lcto Cat83.")
    	        lAborta := .T.
    	    EndIf
    	EndIf
    EndIf
EndIf    

If !lAborta	
	Pergunte(cPerg,.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Janela Principal ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While .T.
		AADD(aSays,OemToAnsi( ctext1 ) )
		AADD(aSays,OemToAnsi( cText2 ) )
		AADD(aButtons, { 1,.T.,{|o| nOpca:= 1,o:oWnd:End()}} )
		AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )
		AADD(aButtons, { 5,.T.,{|| Pergunte(cPerg,.T. ) } } )
		FormBatch( cCadastro, aSays, aButtons )

		Do Case
			Case nOpca ==1
				Processa({||A024Processa()})
			Case nOpca==3
				Pergunte(cPerg,.t.)
				Loop
		EndCase
		Exit
	EndDo
EndIf	
Return  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±ºFuncao    ³A024ProcessaºAutor  ³Andreia dos Santos     º Data ³ 09/08/2001º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ºDesc.     ³Apuracao CAT83                                                 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function A024Processa()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                             ³
//³ mv_par01  // Mes de Apuracao			   	                	 ³
//³ mv_par02  // Ano da Apuracao                                     ³
//³ mv_par03  // Considera Filiais( Sim/Nao )                        ³
//³ mv_par04  // Da Filial                                           ³
//³ mv_par05  // Ate a Filial                                        ³
//³ mv_par06  // Seleiona Filiais ( Sim/Nao )                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nMes		:= mv_par01
Local nAno		:= mv_par02
Local nConsFil  := mv_par03                               
Local cFilDe	:= mv_par04 
Local cFilAte	:= mv_par05
Local nApuracao := 0
Local nPeriodo  := 1
Local lFiliais  := If(mv_par06==1,.T.,.F.) 
Local aDatas    := {}
Local dDtIni    := ""
Local dDtFim    := ""
Local cTitulo	:= ""
Local cErro		:= ""
Local cSolucao	:= ""
Local aLisFil	:= {}
//Local aEmpCorr  := {cEmpAnt, cFilAnt}

Private cCadastro := STR0001 //"Apuracao de CAT83"                         
    
nApuracao := 3 //mensal
nPeriodo  := 1 //primeiro
aDatas	  := DetDatas(nMes,nAno,nApuracao,nPeriodo)     //se encontra em Fisxpur
dDtIni    := aDatas[1]
dDtFim	  := aDatas[2]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Somente sera permitido processar filial de/ate se o processamento for consolidado.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (nConsFil == 1 .And. !(cFilDe <= cFilAnt .Or. cFilAnt >= cFilAte)) .Or.;
   (nConsFil == 1 .And.  (cFilDe == "" .Or. cFilAte == ""))
	cTitulo 	:= STR0030  				//"Processamento Consolidado"	    
	cErro		:= STR0031 					//"O processamento consolidado (filial de/até) somente poderá ser executado " 
	cErro		+= STR0032 					//"se a filial inicial (filial de) for a filial consolidadora, ou seja, ser "
	cErro		+= STR0033 					//"a filial da empresa selecionada no momento do processamento da rotina. No momento, a "
	cErro		+= STR0034 + cFilAnt 		//"filial selecionada é a " 
	cErro		+= STR0035 + cFilDe + "." 	//", e a filial inicial informada a ser processada foi a "
	cSolucao	:= STR0036 					//"Efetue o processamento consolidado da apuração CAT83 apenas na empresa "
	cSolucao	+= STR0037 + cFilDe 		//"consolidadora. Exemplo: caso queira efetuar a apuração da filial "
	cSolucao	+= STR0038 + cFilDe 		//"selecione a filial "
	cSolucao	+= STR0039 					//" antes de efetuar o processamento da rotina." 		
	xMagHelpFis(cTitulo,cErro,cSolucao)
	Return
Endif

If lFiliais
	
	aLisFil  :=MatFilCalc(lFiliais, , , ( lFiliais .and. MV_PAR07==1 ), , 2 )
	nConsFil :=1
	
	If Empty(aLisFil)
		cFilDe := cFilAnt
		cFilAte := cFilAnt
	Else
		cFilAte	:= aLisFil[Len(aLisFil)][2]	
	EndIf
		  
Else

	If ((nConsFil == 2) .Or. (nConsFil == 1 .And. Empty(cFilDe) .And. Empty(cFilAte))) 
		cFilDe := cFilAnt
		cFilAte := cFilAnt		
	EndIf
	
EndIf     

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao da ordem das tabelas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea ("SX5")	//Tabelas Genericas
SX5->(DbSetOrder (1))
DbSelectArea ("SF4")	//Cadastro de TES
SF4->(DbSetOrder (1))
DbSelectArea ("SF5")	//Tipos de Movimentacoes
SF5->(DbSetOrder (1))
DbSelectArea ("SD1")	//Itens das Nfs de Entrada
SD1->(DbSetOrder (1))
DbSelectArea ("SD2")	//Itens das Nfs de Saida
SD2->(DbSetOrder (3))
DbSelectArea ("SD3")	//Movimentacao Interna
SD3->(DbSetOrder (3))
DbSelectArea ("SB1")	//Cadastro de Produtos
SB1->(DbSetOrder (1))	

A024Apura(dDtIni,dDtFim,nConsFil,cFilDe,cFilAte,nAno,nMes,@aLisFil)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A024Apura  ³Autor  ³Andreia dos Santos    ³ Data ³ 20/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Desc.     ³ Apura os valores do periodo                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function A024Apura(dDtIni,dDtFim,nConsFil,cFilDe,cFilAte,nAno,nMes,aLisFil)
//Local cFiltro	:=	""
Local cPeriodo  := ""
Local cCodPro   := ""
Local cFilial   := ""   

Default aLisFil := {}

Private cFicha1A := "" 
Private cFicha1B := ""
Private cFicha1C := ""
Private cFicha1D := ""
Private cFicha1E := ""
Private cFicha2A := ""
Private cFicha2B := ""
Private cFicha2C := ""
Private cFicha2D := ""
Private cFicha2E := ""
Private cFicha2F := ""
Private cFicha2G := ""    
Private cFicha3A := ""    
Private cFicha3B := ""    
Private cFicha3C := ""  
Private aMovim   := {}      
Private cFicha   := ""
Private cCodLan  := ""
Private cEspecie := ""       

cPeriodo := StrZero(nAno,4)+StrZero(nMes,2)		
MontFicha()

DbSelectArea ("SM0")
SM0->(DbGoTop())
SM0->(DbSeek(cEmpAnt+cFilDe, .T.))	//Pego a filial mais proxima

Do While !SM0->(Eof ()) .And. (cEmpAnt==SM0->M0_CODIGO) .And. (SM0->M0_CODFIL<=cFilAte)
		If Len(aLisFil) > 0
			If ( nPosFil := aScan( aLisFil, {|x| x[2] == FWCodFil()} ) ) == 0 .or. !aLisFil[nPosFil,1]
				SM0->( dbSkip() ) 
				Loop
			EndIf
		EndIf
	
	cFilAnt := SM0->M0_CODFIL
	cMvEstado := GetMv("MV_ESTADO") 	
    ZeraMov(cPeriodo,cFilAnt) 	
 	aMovim := ProcMovim(dDtIni, dDtFim, cPeriodo)
	
    If Len(aMovim) > 0
        GravMov(cPeriodo, dDtIni, dDtFim) 
    Endif
 	
	DbSelectArea ("SB1")
	DbSetOrder(1)
	SB1->( DbSeek( xFilial("SB1") ))                     	
 	ProcRegua (SB1->(RecCount ()))
 	
	While !SB1->(Eof ()) .And. SB1->B1_FILIAL = xFilial("SB1")      
		cProd   := SB1->B1_COD+xFilial("SB1")
		cCodPro := SB1->B1_COD           
		cFicha  := ""
   	    cCodLan := Iif(!Empty(SB1->(FieldPos("B1_CODLAN"))),SB1->B1_CODLAN, "")					                                                       
	    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	    //³Busca notas fiscais do produto e periodo  ³
	    //³informados no parametro                   ³
	    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aMovim := ProcEntr(cCodPro, dDtIni, dDtFim, cCodLan, cPeriodo) 
		If Len(aMovim) > 0
		    GravMov(cPeriodo, dDtIni, dDtFim) 
		EndIf
	    aMovim := ProcDevl(cCodPro, dDtIni, dDtFim, cCodLan, cPeriodo)
		If Len(aMovim) > 0
		    GravMov(cPeriodo, dDtIni, dDtFim) 
		EndIf
	    aMovim := ProcSaid(cCodPro, dDtIni, dDtFim, cCodLan, cPeriodo)
	    If Len(aMovim) > 0
	        GravMov(cPeriodo, dDtIni, dDtFim) 
		EndIf
		SB1->(DbSkip())        
	EndDo   
           
    DbSelectArea ("SM0")       
	SM0->(DbSkip())
EndDo
Return(.t.)



/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MontFicha   ºAutor  ³Cecilia Carvalho    º Data ³  29/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Monta relacionamento Ficha x Cod.Lacnto                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function MontFicha()

Local cAliasCCU :=	"CCU"
                         
DbSelectArea (cAliasCCU)
(cAliasCCU)->(DbSetOrder(1))
(cAliasCCU)->(DbGoTop()) 
While !(cAliasCCU)->(Eof())      
   Do Case
      Case (cAliasCCU)->CCU_FICHA == "11"		
	   	   cFicha1A := cFicha1A + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "12" 	
	       cFicha1B := cFicha1B + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "13" 	
		   cFicha1C := cFicha1C + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "14" 	
		   cFicha1D := cFicha1D + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "15" 
		   cFicha1E := cFicha1E + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "21" 	
		   cFicha2A := cFicha2A + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "22" 	
		   cFicha2B := cFicha2B + (cAliasCCU)->CCU_CODLAN  
	  Case (cAliasCCU)->CCU_FICHA == "23" 	
		   cFicha2C := cFicha2C + (cAliasCCU)->CCU_CODLAN 
	  Case (cAliasCCU)->CCU_FICHA == "24" 	
		   cFicha2D := cFicha2D + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "25" 	
		   cFicha2E := cFicha2E + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "26" 	
		   cFicha2F := cFicha2F + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "27" 	
		   cFicha2G := cFicha2G + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "31" 	
		   cFicha3A := cFicha3A + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "32" 	
		   cFicha3B := cFicha3B + (cAliasCCU)->CCU_CODLAN
	  Case (cAliasCCU)->CCU_FICHA == "33" 	
		   cFicha3C := cFicha3C + (cAliasCCU)->CCU_CODLAN		
   EndCase
   (cAliasCCU)->(DbSkip())
EndDo                            
(cAliasCCU)->(DbCloseArea())
Return()
                                                                                                       '
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ProcEntr  ºAutor  ³Cecilia Carvalho    º Data ³  26/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Processa notas fiscais de entrada com tributacao de ICMS    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cCodPro - Produto a ser considerado na busca                º±±
±±º          ³dDtIni  - Periodo inicial a ser considerado na busca        º±±
±±º          ³dDtFim  - Periodo final a ser considerado na busca          º±±
±±º          ³dDtFim  - Periodo final a ser considerado na busca          º±±
±±º          ³cCodLan - Codigo do lancamento cat83                        º±±
±±º          ³cPeriodo- Periodo a ser considerado na busca                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function ProcEntr(cCodPro, dDtini, dDtFim, cCodLan, cPeriodo) 
Local cAlias 	:= GetNextAlias()
Local cDtDig    := ""
Local cAuxFic   := ""
Local cAuxLan   := ""
Local nPos      := 0
Local aDados    := {}

cAlias := GetNextAlias()
BeginSql Alias cAlias
	COLUMN D1_DTDIGIT AS DATE
	COLUMN D1_EMISSAO AS DATE
	
	SELECT SD1.D1_FILIAL, SD1.D1_DTDIGIT, SD1.D1_DOC, SD1.D1_SERIE, SD1.D1_FORNECE, SD1.D1_LOJA,
		SD1.D1_COD, SD1.D1_ITEM, SD1.D1_QUANT, SD1.D1_EMISSAO, SD1.D1_VALIPI, SD1.D1_VALICM,
		SD1.D1_BASEICM, SD1.D1_TOTAL, SD1.D1_VALFRE, SD1.D1_SEGURO, SD1.D1_DESPESA, SD1.D1_VALDESC,
		SD1.D1_TIPO, SD1.D1_BRICMS, SD1.D1_CF, SD1.D1_TES, SD1.D1_CUSTO, SF1.F1_ESPECIE, SD1.D1_CODLAN,
		SD1.D1_NUMSEQ
	FROM %table:SD1% SD1
	JOIN %table:SF4% SF4 ON SF4.F4_FILIAL = %xFilial:SF4%
		AND SF4.F4_CODIGO = SD1.D1_TES
		AND SF4.F4_ICM = 'S' AND SF4.%NotDel%
	JOIN %table:SF1% SF1 ON SF1.F1_FILIAL = %xFilial:SF1%
		AND SF1.F1_DOC = SD1.D1_DOC
		AND SF1.F1_SERIE = SD1.D1_SERIE
		AND SF1.F1_FORNECE = SD1.D1_FORNECE
		AND SF1.F1_LOJA = SD1.D1_LOJA
		AND SF1.F1_TIPO = SD1.D1_TIPO
		AND SF1.%NotDel%
	WHERE SD1.D1_FILIAL = %xFilial:SD1% AND
		SD1.D1_DTDIGIT >=%Exp:dDtIni% AND
		SD1.D1_DTDIGIT <=%Exp:dDtFim% AND
	    	      SD1.D1_TIPO IN('N','C','I', 'D') AND      
		SD1.D1_COD = %Exp:cCodPro% AND
		SD1.%NotDel%
	ORDER BY 1,2
EndSql
dbSelectArea(cAlias)
	
DbSelectArea(cAlias)
(cAlias)->(DbGoTop())
ProcRegua((cAlias)->(RecCount()))
While !(cAlias)->(Eof())
    cAuxLan := Iif(!Empty((cAlias)->(FieldPos("D1_CODLAN"))),(cAlias)->D1_CODLAN, "") 
    If Alltrim(cAuxLan) == ""                                                                          
        cAuxLan := Iif(!Empty((cAlias)->(FieldPos("F4_CODLAN"))),(cAlias)->F4_CODLAN, "") 
    EndIf
	If Alltrim(cAuxLan) <> ""            
        cCodLan  := cAuxLan 	
	EndIf  
	    
	cEspecie := (cAlias)->F1_ESPECIE
    cAuxFic  := VerFicha(cPeriodo, cCodPro, cProd, cCodLan, cEspecie)
    If AllTrim(cAuxFic) <> "" .And. AllTrim(cCodLan) <> ""
	    cDtDig  := dtos((cAlias)->D1_DTDIGIT)
	    aAdd(aDados,{})
	    nPos :=	Len(aDados)
	    aAdd (aDados[nPos], cCodPro)                                                   //01 - PRODUTO
        aAdd (aDados[nPos], cCodLan)                                                   //02 - COD LANCTO
	    aAdd (aDados[nPos], (cAlias)->D1_QUANT)	                                    //03 - QUANTIDADE
	    aAdd (aDados[nPos], (cAlias)->D1_CUSTO)					                    //04 - CUSTO 
	    aAdd (aDados[nPos], (cAlias)->D1_VALICM)                      		            //05 - VL ICMS
	    //aAdd (aDados[nPos], substr(cDtDig,7,2)+substr(cDtDig,5,2)+substr(cDtDig,1,4)) //06 - DT DIGITACAO
	    aAdd (aDados[nPos], (cAlias)->D1_DTDIGIT)
	    aAdd (aDados[nPos], (cAlias)->D1_NUMSEQ)                      		            //07 - NRO.SEQUENCIAL
	    aAdd (aDados[nPos], cAuxFic)                             		                //08 - FICHA
	    aAdd (aDados[nPos], Iif((cAlias)->D1_TIPO == "D", "E", "I"))	                //09 - OPERACAO	
	    aAdd (aDados[nPos], "")                                   		                //10 - CHAVE SD3	
    EndIf
   	(cAlias)->(DbSkip())
EndDo
DbSelectArea(cAlias)
(cAlias)->(DbCloseArea())   
Return(aDados)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ProcDevl  ºAutor  ³Cecilia Carvalho    º Data ³  26/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Processa notas fiscais de devolucao  	                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cCodPro  - Produto a ser considerado na busca               º±±
±±º          ³dDtIni  - Periodo inicial a ser considerado na busca        º±±
±±º          ³dDtFim - Periodo final a ser considerado na busca           º±±
±±º          ³cCodLan - Codigo do lancamento cat83                        º±±
±±º          ³cPeriodo- Periodo a ser considerado na busca                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/                    
Static Function ProcDevl(cCodPro, dDtIni, dDtFim, cCodLan, cPeriodo)
Local cAlias 	:= GetNextAlias()
Local cDtEmiss  := ""
Local cAuxFic   := ""
Local cAuxLan   := ""
Local nPos      := 0
Local aDados    := {}

        cAlias := GetNextAlias()       	           
       	BeginSql Alias cAlias
			COLUMN D2_EMISSAO AS DATE   
			SELECT SD2.D2_FILIAL, SD2.D2_EMISSAO, SD2.D2_DOC, SD2.D2_SERIE, SD2.D2_CLIENTE, SD2.D2_LOJA, SD2.D2_COD, SD2.D2_ITEM,
				   SD2.D2_QUANT, SD2.D2_CF, SD2.D2_PICM, SD2.D2_VALICM, SD2.D2_BASEICM, SD2.D2_TIPO, SD2.D2_CUSTO1,      
				   SD2.D2_NFORI, SD2.D2_SERIORI, SD2.D2_ITEMORI, SD2.D2_TES, SF2.F2_ESPECIE, SD2.D2_CODLAN,
				   SD2.D2_NUMSEQ 
			FROM %table:SD2% SD2
			JOIN %table:SF4% SF4 ON SF4.F4_FILIAL = %xFilial:SF4% AND
									SF4.F4_CODIGO = SD2.D2_TES AND
									SF4.F4_ICM = 'S' AND SF4.%NotDel%       		  
			JOIN %table:SF2% SF2 ON SF2.F2_FILIAL = %xFilial:SF2%
									AND SF2.F2_DOC = SD2.D2_DOC
									AND SF2.F2_SERIE = SD2.D2_SERIE
									AND SF2.F2_CLIENTE = SD2.D2_CLIENTE 
									AND SF2.F2_LOJA = SD2.D2_LOJA							
									AND SF2.%NotDel%       		  																				
        	WHERE SD2.D2_FILIAL = %xFilial:SD2% AND		                     
			      SD2.D2_EMISSAO >=%Exp:dDtIni% AND
       			  SD2.D2_EMISSAO <=%Exp:dDtFim% AND				      
 				  SD2.D2_TIPO = 'D' AND
 				  SD2.D2_COD = %Exp:cCodPro% AND
				  SD2.%NotDel%        
			ORDER BY 1,2			 				  				  		            
    	EndSql               
		dbSelectArea(cAlias)         

DbSelectArea(cAlias)
(cAlias)->(DbGoTop())
ProcRegua((cAlias)->(RecCount()))
While !(cAlias)->(Eof())
    cAuxLan := Iif(!Empty((cAlias)->(FieldPos("D2_CODLAN"))),(cAlias)->D2_CODLAN, "") 
    If Alltrim(cAuxLan) == ""                                                                          
        cAuxLan := Iif(!Empty((cAlias)->(FieldPos("F4_CODLAN"))),(cAlias)->F4_CODLAN, "") 
    EndIf
	If Alltrim(cAuxLan) <> ""            
        cCodLan  := cAuxLan 	
	EndIf	 
    cEspecie := (cAlias)->F2_ESPECIE
    cAuxFic  := VerFicha(cPeriodo, cCodPro, cProd, cCodLan, cEspecie)
    If AllTrim(cAuxFic) <> "" .And. AllTrim(cCodLan) <> ""
	    cDtEmiss := dtos((cAlias)->D2_EMISSAO)
     	aAdd(aDados,{})
	    nPos :=	Len(aDados)
	    aAdd (aDados[nPos], cCodPro)	                                                      //01 - PRODUTO
	    aAdd (aDados[nPos], cCodLan)                                                         //02 - COD LANCTO 
	    aAdd (aDados[nPos], (cAlias)->D2_QUANT)	                                          //03 - QUANTIDADE
	    aAdd (aDados[nPos], (cAlias)->D2_CUSTO1)					                          //04 - CUSTO
	    aAdd (aDados[nPos], (cAlias)->D2_VALICM)                      			              //05 - VL ICMS
	    //aAdd (aDados[nPos], substr(cDtEmiss,7,2)+substr(cDtEmiss,5,2)+substr(cDtEmiss,1,4)) //06 - DT DIGITACAO
	    aAdd (aDados[nPos],(cAlias)->D2_EMISSAO)
	    aAdd (aDados[nPos], (cAlias)->D2_NUMSEQ)                      		                  //07 - NRO.SEQUENCIAL
	    aAdd (aDados[nPos], cAuxFic)                             		                      //08 - FICHA
	    aAdd (aDados[nPos], "E")                                 		                      //09 - OPERACAO	
	    aAdd (aDados[nPos], "")                                        		                  //10 - CHAVE SD3	
    EndIf
    (cAlias)->(DbSkip())
EndDo
DbSelectArea(cAlias)
(cAlias)->(DbCloseArea())
Return(aDados)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ProcSaid  ºAutor  ³Cecilia Carvalho    º Data ³  26/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Processa notas fiscais de saida	 		                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cCodPro  - Produto a ser considerado na busca               º±±
±±º          ³dDtIni  - Periodo inicial a ser considerado na busca        º±±
±±º          ³dDtFim - Periodo final a ser considerado na busca           º±±
±±º          ³cCodLan - Codigo do lancamento cat83                        º±±
±±º          ³cPeriodo- Periodo a ser considerado na busca                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/                    
Static Function ProcSaid(cCodPro, dDtIni, dDtFim, cCodLan, cPeriodo)
Local cAlias 	:= GetNextAlias()
Local cDtEmiss  := ""
Local cAuxFic   := ""
Local cAuxLan   := ""
Local nPos      := 0
Local aDados    := {}
	 
        cAlias := GetNextAlias()       	           
       	BeginSql Alias cAlias
			COLUMN D2_EMISSAO AS DATE   
			SELECT SD2.D2_FILIAL, SD2.D2_EMISSAO, SD2.D2_DOC, SD2.D2_SERIE, SD2.D2_CLIENTE, SD2.D2_LOJA, SD2.D2_COD, SD2.D2_ITEM,
				   SD2.D2_QUANT, SD2.D2_CF, SD2.D2_PICM, SD2.D2_VALICM, SD2.D2_BASEICM, SD2.D2_TIPO, SD2.D2_CUSTO1,      
				   SD2.D2_TES, SF2.F2_ESPECIE, SD2.D2_CODLAN, SD2.D2_DESCZFR, SF4.F4_CRDACUM, SD2.D2_NUMSEQ, SF4.F4_CODLAN
			FROM %table:SD2% SD2
			JOIN %table:SF4% SF4 ON SF4.F4_FILIAL = %xFilial:SF4% AND
									SF4.F4_CODIGO = SD2.D2_TES AND
									SF4.F4_ICM = 'S' AND SF4.%NotDel%       		  
			JOIN %table:SF2% SF2 ON SF2.F2_FILIAL = %xFilial:SF2%
									AND SF2.F2_DOC = SD2.D2_DOC
									AND SF2.F2_SERIE = SD2.D2_SERIE
									AND SF2.F2_CLIENTE = SD2.D2_CLIENTE 
									AND SF2.F2_LOJA = SD2.D2_LOJA							
									AND SF2.%NotDel%       		  																				
        	WHERE SD2.D2_FILIAL = %xFilial:SD2% AND		                     
			      SD2.D2_EMISSAO >=%Exp:dDtIni% AND
       			  SD2.D2_EMISSAO <=%Exp:dDtFim% AND				      
 				  SD2.D2_TIPO IN ( 'N', 'I', 'C' ) AND
 				  SD2.D2_COD = %Exp:cCodPro% AND
				  SD2.%NotDel%        
			ORDER BY 1,2			 				  				  		            
    	EndSql               
		dbSelectArea(cAlias)         

DbSelectArea(cAlias)
(cAlias)->(DbGoTop())
ProcRegua((cAlias)->(RecCount()))
While !(cAlias)->(Eof())
    cAuxLan := Iif(!Empty((cAlias)->(FieldPos("D2_CODLAN"))),(cAlias)->D2_CODLAN, "") 
    If Alltrim(cAuxLan) == ""                                                                          
        cAuxLan := Iif(!Empty((cAlias)->(FieldPos("F4_CODLAN"))),(cAlias)->F4_CODLAN, "") 
    EndIf
	If Alltrim(cAuxLan) <> ""            
        cCodLan  := cAuxLan	
	EndIf	 
    cEspecie := (cAlias)->F2_ESPECIE
    cAuxFic  := VerFicha(cPeriodo, cCodPro, cProd, cCodLan, cEspecie)
    If AllTrim(cAuxFic) <> "" .And. AllTrim(cCodLan) <> ""
	   cDtEmiss := dtos((cAlias)->D2_EMISSAO)
	   aAdd(aDados,{})
	   nPos := Len(aDados)
	   aAdd (aDados[nPos], cCodPro)                                                            //01 - PRODUTO
	   aAdd (aDados[nPos], cCodLan)                                                            //02 - COD.LANCTO 
	   aAdd (aDados[nPos], (cAlias)->D2_QUANT)	                                                //03 - QUANTIDADE
	   aAdd (aDados[nPos], (cAlias)->D2_CUSTO1)						                        //04 - CUSTO
	   aAdd (aDados[nPos], (cAlias)->D2_VALICM)                      				            //05 - VL ICMS
	   //aAdd (aDados[nPos], substr(cDtEmiss,7,2)+substr(cDtEmiss,5,2)+substr(cDtEmiss,1,4))	//06 - DT DIGITACAO
	   aAdd (aDados[nPos],(cAlias)->D2_EMISSAO)
	   aAdd (aDados[nPos], (cAlias)->D2_NUMSEQ)                      		                    //07 - NRO.SEQUENCIAL
	   aAdd (aDados[nPos], cAuxFic)                             		                        //08 - FICHA
	   aAdd (aDados[nPos], "E")                                 		                        //09 - OPERACAO	
	   aAdd (aDados[nPos], "")                                   		                        //10 - CHAVE SD3	
    EndIf
    (cAlias)->(DbSkip())
EndDo
DbSelectArea(cAlias)
(cAlias)->(DbCloseArea())                             
Return(aDados)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ProcMovim ºAutor  ³Cecilia Carvalho    º Data ³  26/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Processa SD3 (Movimentacoes Internas)                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³dDtIni   - Periodo inicial a ser considerado na busca       º±±
±±º          ³dDtFim   - Periodo final a ser considerado na busca         º±±
±±º          ³cPeriodo - Periodo de referencia                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function ProcMovim(dDtIni, dDtFim, cPeriodo)
Local cAlias 	:= GetNextAlias()
Local cDtDig    := ""      
Local cAuxFic   := "  "
Local cAuxLan   := ""
Local nPos      := 0
Local aDados    := {}

        cAlias := GetNextAlias()   
        BeginSql Alias cAlias
			COLUMN D3_EMISSAO AS DATE 
			SELECT SD3.D3_FILIAL, SD3.D3_COD, SD3.D3_DOC, SD3.D3_LOCAL, SD3.D3_QUANT, SD3.D3_EMISSAO, 
			       SD3.D3_TM, SD3.D3_CF, SD3.D3_OP, SD3.D3_CUSTO1, SD3.D3_ESTORNO, SD3.D3_CHAVE,
                   SD3.D3_CODLAN, SD3.D3_NUMSEQ, SD3.D3_ESTORNO, SF5.F5_CODIGO, SF5.F5_CODLAN 
   		    FROM %table:SD3% SD3 
   		    JOIN %table:SF5% SF5 ON SF5.F5_FILIAL = %xFilial:SF5%
									AND SF5.F5_CODIGO = SD3.D3_TM
									AND SF5.%NotDel%       		  
	        WHERE SD3.D3_FILIAL = %xFilial:SD3% AND
		          SD3.D3_EMISSAO >=%Exp:dDtIni% AND
       			  SD3.D3_EMISSAO <=%Exp:dDtFim% AND
	        	  SD3.%NotDel%
		    ORDER BY 1,2
    	EndSql        		
		dbSelectArea(cAlias)         

DbSelectArea(cAlias)
(cAlias)->(DbGoTop())
ProcRegua((cAlias)->(RecCount()))
While !(cAlias)->(Eof())
    cAuxLan := Iif(!Empty((cAlias)->(FieldPos("D3_CODLAN"))),(cAlias)->D3_CODLAN, "") 
    If Alltrim(cAuxLan) = ""                                                                          
        cAuxLan := Iif(!Empty((cAlias)->(FieldPos("F5_CODLAN"))),(cAlias)->F5_CODLAN, "") 
    EndIf
	If Alltrim(cAuxLan) <> ""
		cDtDig := dtos((cAlias)->D3_EMISSAO)
	 	If cAuxLan $ cFicha1A 
			cAuxFic := "11"
	    ElseIf cAuxLan $ cFicha1B 
			cAuxFic := "12"
	    ElseIf cAuxLan $ cFicha2A 
	        cAuxFic := "21"	     
	    ElseIf cAuxLan $ cFicha2B 
	        cAuxFic := "22"	    
	    ElseIf cAuxLan $ cFicha2C 
	        cAuxFic := "23"	    
	    ElseIf cAuxLan $ cFicha2D 
	        cAuxFic := "24"
	    ElseIf cAuxLan $ cFicha2E 
	        cAuxFic := "25"
	    ElseIf cAuxLan $ cFicha2F 
	        cAuxFic := "26" 
	    ElseIf cAuxLan $ cFicha2G 
	        cAuxFic := "27"	                                                                                       
	    ElseIf cAuxLan $ cFicha3A 
	        cAuxFic := "31"
	    ElseIf cAuxLan $ cFicha3B 
	        cAuxFic := "32"
	    ElseIf cAuxLan $ cFicha3C 
	        cAuxFic := "33"
	    EndIf                            
	
		aAdd(aDados,{})
		nPos :=	Len(aDados)               
		aAdd (aDados[nPos], (cAlias)->D3_COD)      		                             //01 - PRODUTO
	    aAdd (aDados[nPos], cAuxLan)                                                    //02 - COD LANCTO 
		aAdd (aDados[nPos], (cAlias)->D3_QUANT)	                                     //03 - QUANTIDADE
		aAdd (aDados[nPos], (cAlias)->D3_CUSTO1)						                 //04 - CUSTO 
		aAdd (aDados[nPos], 0)						                                     //05 - VL_ICMS 	
		//aAdd (aDados[nPos], substr(cDtDig,7,2)+substr(cDtDig,5,2)+substr(cDtDig,1,4))  //06 - DT DIGITACAO
	    aAdd (aDados[nPos],(cAlias)->D3_EMISSAO)
		aAdd (aDados[nPos], Alltrim((cAlias)->D3_NUMSEQ))					             //07 - NRO SEQUENCIAL
		aAdd (aDados[nPos], cAuxFic)			 		                                 //08 - FICHA
		aAdd (aDados[nPos], Iif((cAlias)->D3_ESTORNO == "S", "E", "I"))	             //09 - OPERACAO	
   	    aAdd (aDados[nPos], (cAlias)->D3_CHAVE)                                 		 //10 - CHAVE SD3	
	Endif
	(cAlias)->(DbSkip())                    
EndDo
DbSelectArea(cAlias)	
(cAlias)->(DbCloseArea())
Return(aDados)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GravMov   ºAutor  ³Cecilia Carvalho    º Data ³  16/09/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Apuracao de movimentos Cat 83/09                           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cPeriodo : Periodo de referencia                            º±± 
±±º          ³dDtIni   - Periodo inicial a ser considerado na busca       º±±
±±º          ³dDtFim   - Periodo final a ser considerado na busca         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function GravMov(cPeriodo, dDtIni, dDtFim)  
Local aArea 	:= GetArea()
Local nX        := 0
Local lRet   	:= .T.
Local nSaldoIni := 0
Local nCustoIni := 0
Local nMes      := 0
Local nAno      := 0
Local cFic      := ""
Local cPer      := ""
Local cOpe      := ""
Local cAliasCDU := "CDU"

For nX:=1 to Len(aMovim)
	Begin Transaction
		DbSelectArea("CDW")
		CDW->(DbSetOrder(1))
		cFic := aMovim[nX][08]
		cOpe := aMovim[nX][09]
		If CDW->(DbSeek(xFilial("CDW")+cPeriodo+cFic+aMovim[nx][01]+aMovim[nx][02]))
			RecLock("CDW",.F.)
			If Alltrim(cOpe) == "I"
				CDW->CDW_VALICM +=	aMovim[nX][05]
				CDW->CDW_VALCUS +=	aMovim[nX][04]
				CDW->CDW_QUANT  +=	aMovim[nX][03]
			Else
				CDW->CDW_VALICM +=	aMovim[nX][05]//Iif((CDW->CDW_VALICM - aMovim[nX][05])>0,(CDW->CDW_VALICM - aMovim[nX][05]),0)
				CDW->CDW_VALCUS +=	aMovim[nX][04]//Iif((CDW->CDW_VALCUS - aMovim[nX][04])>0,(CDW->CDW_VALCUS - aMovim[nX][04]),0)
				CDW->CDW_QUANT  +=	aMovim[nX][03]//Iif((CDW->CDW_QUANT  - aMovim[nX][03])>0,(CDW->CDW_QUANT - aMovim[nX][03]),0)
			EndIf
			CDW->(MsUnLock())
		Else
			RecLock("CDW", .T. )
			CDW->CDW_FILIAL := xFilial("CDW")
			CDW->CDW_PERIOD := cPeriodo
			CDW->CDW_FICHA  := cFic
			CDW->CDW_PRODUT := aMovim[nX][01]
			CDW->CDW_CODLAN := aMovim[nX][02]
			CDW->CDW_OPERAC := Iif(Alltrim(cOpe) == "I", "1", "2")
			CDW->CDW_DTMOV  := aMovim[nX][06]
			CDW->CDW_QUANT  := aMovim[nX][03]
			CDW->CDW_VALCUS := aMovim[nX][04]
			CDW->CDW_VALICM := aMovim[nX][05]
			CDW->CDW_NUMSEQ := aMovim[nX][07]
			CDW->CDW_CHAVE  := aMovim[nX][10]
			CDW->(MsUnLock())
		Endif
         //buscar saldo anterior
		nMes := Month(dDtIni) - 1
		nAno := Year(dDtFim)
         //Verifica o mes e o ano correto da regressao
		If nMes == 0
			nMes := 12
			nAno := nAno - 1
		EndIf
		cPer := StrZero(nAno,4)+StrZero(nMes,2)
		DbSelectArea(cAliasCDU)
		(cAliasCDU)->(DbSetOrder(1))
		If (cAliasCDU)->(DbSeek(xFilial(cAliasCDU)+cPer+cFic+aMovim[nX][01]))
			nSaldoIni:=(cAliasCDU)->CDU_VALICM
			nCustoIni:=(cAliasCDU)->CDU_VALCUS
		Else
			nSaldoIni:= 0
			nCustoIni:= 0
		EndIf
		DbSelectArea(cAliasCDU)
		(cAliasCDU)->(DbCloseArea())

		DbSelectArea("CDU")
		CDU->(DbSetOrder(1))
		If CDU->(DbSeek(xFilial("CDU")+cPeriodo+cFic+aMovim[nX][01]))
			RecLock("CDU",.F.)
			If Alltrim(cOpe) == "I"
				CDU->CDU_VALICM +=	aMovim[nX][05]
				CDU->CDU_VALCUS +=	aMovim[nX][04]
			Else
				CDU->CDU_VALICM :=	Iif((CDU->CDU_VALICM - aMovim[nX][05])>0,(CDU->CDU_VALICM - aMovim[nX][05]),0)
				CDU->CDU_VALCUS :=	Iif((CDU->CDU_VALCUS - aMovim[nX][04])>0,(CDU->CDU_VALCUS - aMovim[nX][04]),0)
			EndIf
			CDU->(MsUnLock())
		Else
			RecLock("CDU",.T.)
			CDU->CDU_FILIAL := xFilial("CDU")
			CDU->CDU_PERIOD := cPeriodo
			CDU->CDU_FICHA  := cFic
			CDU->CDU_PRODUT :=	aMovim[nX][01]
			CDU->CDU_VALICM :=	aMovim[nX][05] + nSaldoIni
			CDU->CDU_VALCUS :=	aMovim[nX][04] + nCustoIni
			CDU->(MsUnLock())
		EndIf
	End Transaction
Next nX
RestArea(aArea)
Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ZeraMov   ºAutor  ³Cecilia Carvalho    º Data ³  16/09/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Limpa as tabelas CDW e CDU para o periodo em processamento º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³.T.                                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cPeriodo: Periodo                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function ZeraMov(cPeriodo, cFilialp)
Local cAliasCDW :=	"CDW"
Local cAliasCDU :=	"CDU"
Local lRet   	:= .T.
                         
DbSelectArea (cAliasCDW)
(cAliasCDW)->(DbSetOrder(1))
(cAliasCDW)->(DbGoTop()) 
If (cAliasCDW)->(DbSeek(xFilial(cAliasCDW)+cPeriodo),.T.)
While !(cAliasCDW)->(Eof())      
   		If (cAliasCDW)->CDW_PERIOD == cPeriodo .And. (cAliasCDW)->CDW_FILIAL==cFilialp
       (cAliasCDW)->(RecLock("CDW"))
       (cAliasCDW)->(dbDelete())
       (cAliasCDW)->(MsUnLock())    
   EndIf
   (cAliasCDW)->(DbSkip())
EndDo                            
EndIf
(cAliasCDW)->(DbCloseArea())

DbSelectArea (cAliasCDU)
(cAliasCDU)->(DbSetOrder(1))
If (cAliasCDU)->(DbSeek(xFilial(cAliasCDU)+cPeriodo),.T.)
    While (cAliasCDU)->CDU_PERIOD == cPeriodo .And. (cAliasCDU)->CDU_FILIAL==cFilialp
      (cAliasCDU)->(RecLock("CDU"))
      (cAliasCDU)->(dbDelete())
      (cAliasCDU)->(MsUnLock())    
      (cAliasCDU)->(DbSkip())
    EndDo                            
EndIf
(cAliasCDU)->(DbCloseArea())
Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VerFicha  ºAutor  ³Cecilia Carvalho    º Data ³  16/09/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica qual ficha para o produto e codigo lcto cat83     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³Codigo da ficha                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cPeriodo: Periodo                                           º±±  
±±º          ³cCodPro : Codigo do produto                                 º±±  
±±º          ³cProd   : Codigo do produto + filial                        º±±  
±±º          ³cCodLan : Codigo do lcto cat83                              º±±  
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function VerFicha(cPeriodo, cCodPro, cProd, cCodLan, cEspecie)
Local cAuxFicha := ""
Local cAliasCDW :=	"CDW"

DbSelectArea(cAliasCDW)
(cAliasCDW)->(DbSetOrder (1))

      cAliasCDW	:=	GetNextAlias()
   		   	
   	  BeginSql Alias cAliasCDW    	
		SELECT 
		      CDW.CDW_FILIAL, CDW.CDW_PERIOD, CDW.CDW_FICHA, CDW.CDW_PRODUT, CDW.CDW_CODLAN
		FROM 
		    %Table:CDW% CDW 
		WHERE 
		     CDW.CDW_FILIAL=%xFilial:CDW% AND 
			 CDW.CDW_PRODUT = (%Exp:cCodPro%) AND
			 CDW.CDW_PERIOD = (%Exp:cPeriodo%) AND
			 CDW.CDW_CODLAN = (%Exp:cCodLan%) AND	
			 CDW.%NotDel%					               
		ORDER BY 
		        1,2,3,4				
	  EndSql
		              
  DbSelectArea (cAliasCDW)
  (cAliasCDW)->(DbGoTop ())
  While !(cAliasCDW)->(Eof ())      
    If (cAliasCDW)->CDW_PRODUT == cCodPro .And. (cAliasCDW)->CDW_CODLAN == cCodLan .And. (cAliasCDW)->CDW_PERIOD == cPeriodo
        cAuxFicha := (cAliasCDW)->CDW_FICHA
        Exit
    Endif
	DbSelectArea(cAliasCDW)
	(cAliasCDW)->(DbSkip())        
 EndDo  
 If AllTrim(cAuxFicha) == ""
	 If cCodLan $ cFicha1A 
	     cAuxFicha := "11"
	 ElseIf cCodLan $ cFicha1B 
         cAuxFicha := "12"    
	 ElseIf cCodLan $ cFicha1C .Or. cEspecie $ "NFCEE"
         cAuxFicha := "13"    
	 ElseIf cCodLan $ cFicha1D .Or. cEspecie $ "NFSC','NTSC"
         cAuxFicha := "14"    
     ElseIf cCodLan $ cFicha1E .Or. cEspecie $ "CTR"
         cAuxFicha := "15"
     ElseIf cCodLan $ cFicha2A 
         cAuxFicha := "21"	     
     ElseIf cCodLan $ cFicha2B 
          cAuxFicha := "22"	    
     ElseIf cCodLan $ cFicha2C 
          cAuxFicha := "23"	    
     ElseIf cCodLan $ cFicha2D 
          cAuxFicha := "24"
     ElseIf cCodLan $ cFicha2E 
          cAuxFicha := "25"
     ElseIf cCodLan $ cFicha2F 
          cAuxFicha := "26" 
     ElseIf cCodLan $ cFicha2G 
          cAuxFicha := "27"	                                                                                       
	 ElseIf cCodLan $ cFicha3A      
 		  cAuxFicha := "31"
	 ElseIf cCodLan $ cFicha3B      
   	      cAuxFicha := "32"
     ElseIf cCodLan $ cFicha3C 
          cAuxFicha := "33"
	 EndIf                            
 EndIf
 (cAliasCDW)->(DbCloseArea()) 
Return(cAuxFicha)
