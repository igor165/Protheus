#INCLUDE "Mata464.CH"
#INCLUDE "fivewin.ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Mata464  ³ Autor ³ Leonardo Ruben        ³ Data ³ 03/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Notas de Correcao                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAFAT  ( Localizacao Chile )                             ³±±
±±ÃÄÄÄÄÄÄÄÄÂÄÁÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA   ³ BOPS ³Prograd.³ALTERACAO                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³15.07.03³65.566³Denardi ³Inclusao da opcao de Cancelar uma Nt. Correcao³±±
±±³15.09.06³107726³Bianchi ³Implementado Menu Funcional                   ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Mata464

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Notas de Correcao de Clientes...                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a existencia do parametro dos impostos variaveis.   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cNFOrig := Space(6), cSerieOri := Space(3)
Private cCondicao
Private aIndexADC  := {}
Private bFiltraBrw 	:= {|| FilBrowse("SF3",@aIndexADC,@cCondicao) }       

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa    ³
//³ ----------- Elementos contidos por dimensao ------------   ³
//³ 1. Nome a aparecer no cabecalho                            ³
//³ 2. Nome da Rotina associada                                ³
//³ 3. Usado pela rotina                                       ³
//³ 4. Tipo de Transa‡„o a ser efetuada                        ³
//³  1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³  2 - Simplesmente Mostra os Campos                         ³
//³  3 - Inclui registros no Bancos de Dados                   ³
//³  4 - Altera o registro corrente
//³  5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cCondicao :="F3_FILIAL = '"+xFilial("SF3")+"' .AND. "
cCondicao += "F3_ESPECIE == 'COR  ' .and. Empty(F3_DTCANC)"
dbSelectArea("SF3")
Eval(bFiltraBrw)
PRIVATE aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemToAnsi(STR0011)  //"Nota de Correccion"
PRIVATE lInclui := .F.

dbSelectArea("SF3")
dbSetOrder(1)
Eval(bFiltraBrw)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse(6,1,22,75,"SF3")

dbSelectArea("SF3")
dbSetOrder(1)
EndFilBrw("SF3",aIndexADC)

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A464Visual ³ Autor ³ Leonardo Ruben      ³ Data ³ 03.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de visualizacao das Notas de Correcao...          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A464Visual(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo									  ³±±
±±³		 	 ³ ExpN1 = Numero do registro 								  ³±±
±±³		 	 ³ ExpN2 = Numero da opcao selecionada 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA464                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION A464Visual(cAlias,nReg,nOpc)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis.																 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

LOCAL oCliFor, oCliente, oLoja
LOCAL oDlg, oTipo
Local oBrowse, oFolder

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis para tratamento dos impostos.					  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

LOCAL cTipo := "COR-"+OemToAnsi(STR0011)  //"Nota de Correcci¢n"

LOCAL cNFClie :=TamSx3("D1_DOC")[1]

LOCAL cPicNF := X3Picture("F3_NFISCAL")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo								³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cNFClie := F3_NFISCAL
PRIVATE cNFiscal   := F3_NFISCAL
PRIVATE cSerie     := F3_SERIE
PRIVATE dDEmissao  := F3_EMISSAO
PRIVATE cEspecie   := F3_ESPECIE
PRIVATE ca464Cli   := F3_CLIEFOR
PRIVATE cLoja      := F3_LOJA
PRIVATE cObserv    := F3_OBSERV
PRIVATE cObserv2   := F3_OBSERV2
PRIVATE cObserv3   := F3_OBSERV3
PRIVATE nUsado     := 0, nOpcA :=0

If FieldPos("F3_DOCOR")>0 
	cNFOrig:= F3_DOCOR
EndIf
If FieldPos("F3_SERIEOR")>0
	cSerOrig:= F3_SERIEOR
EndIf
cF3      := "SA1"
cCliente := OemtoAnsi(STR0013)  //'Cliente'
cTipAnt  := cTipo
lInclui  := .F.

DEFINE MSDIALOG oDlg TITLE cCadastro From 9,0 To 31.5,80 OF oBrowse //oMainWnd

@ 40, 005 TO 160, 309 LABEL "" OF oDlg  PIXEL 

@ 50, 010 SAY OemtoAnsi(STR0006) SIZE 15, 8 OF oDlg PIXEL
@ 50, 030 MSGET oTipo VAR cTipo  PICTURE "@!" WHEN .F. SIZE 90, 10 OF oDlg PIXEL

@ 50, 135 SAY oCliente  VAR cCliente SIZE 20, 7 OF oDlg PIXEL
@ 50, 165 MSGET oCliFor VAR cA464CLI PICTURE "@K!" WHEN .F. ;
	SIZE 47, 10 OF oDlg PIXEL
@ 50, 215 MSGET oLoja   VAR cLoja PICTURE "@!" WHEN .F. ;
	SIZE 14, 10 OF oDlg PIXEL

@ 50, 240 SAY OemtoAnsi(STR0007) SIZE 20, 7 OF oDlg PIXEL//Emision
@ 50, 273 MSGET odEmissao VAR dDEmissao WHEN .F. ;
	SIZE 30, 10 OF oDlg PIXEL

@ 75, 010 SAY OemtoAnsi(STR0008)SIZE 30, 7 OF oDlg PIXEL //Nota Cred

@ 75, 056 MSGET oNFClie VAR cNFCLIE PICTURE cPicNF WHEN .F. ;
	SIZE 50, 10 OF oDlg PIXEL
@ 75, 120 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie"
@ 75, 140 MSGET cSerie PICTURE "!!!" WHEN .F. ;
	SIZE 15, 10 OF oDlg PIXEL

If FieldPos("F3_DOCOR")>0 
	@ 90, 010 SAY OemtoAnsi(STR0021) SIZE 30, 7 OF oDlg PIXEL //"Fact Original"
	@ 90, 056 MSGET oNFClie VAR cNFOrig PICTURE cPicNF WHEN .F. ;
	SIZE 50, 10 OF oDlg PIXEL
EndIf    

If  FieldPos("F3_SERIEOR")>0
	@ 90, 120 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie De Factura Original"
	@ 90, 140 MSGET cSerOrig PICTURE "!!!" WHEN .F. ;
	SIZE 15, 10 OF oDlg PIXEL
EndIf


@ 110, 010 SAY OemtoAnsi(STR0010) SIZE 40, 7 OF oDlg PIXEL
@ 110, 056 MSGET oObserv  VAR cObserv  WHEN .F. SIZE 200, 10 OF oDlg PIXEL
@ 125, 056 MSGET oObserv2 VAR cObserv2 WHEN .F. SIZE 200, 10 OF oDlg PIXEL
@ 140, 056 MSGET oObserv3 VAR cObserv3 WHEN .F. SIZE 200, 10 OF oDlg PIXEL

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca:= 1 , oDlg:End()},{|| nOpca:= 0 , oDlg:End()}) CENTERED
Setapilha()
RELEASE OBJECTS oFolder

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da janela									 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")
Eval(bFiltraBrw)
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A464Inclui³ Autor ³ Leonardo Ruben        ³ Data ³ 03.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de inclusao de Notas de Correcao                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A464Inclui(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo									  ³±±
±±³			 ³ ExpN1 = Numero do registro 								  ³±±
±±³			 ³ ExpN2 = Numero da opcao selecionada 			   		      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA464                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION A464Inclui(cAlias,nReg,nOpc)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis.																 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

LOCAL oCliFor, oCliente, oLoja
LOCAL oDlg, oTipo
LOCAL cSucClie:=Space(4)
LOCAL cTipo := "COR-"+OemToAnsi(STR0011)  //"Nota de Correcci¢n"
LOCAL M->F3_EMISSAO	:= dDataBase 
LOCAL M->F3_NFISCAL	:= Space(TamSX3("F3_NFISCAL")[1]) 
Local M->F3_SERIE	:= Space(TamSX3("F3_SERIE")[1]) 
LOCAL M->F3_OBSERV	:= Space(TamSX3("F3_OBSERV")[1]) 
LOCAL cObserv2		:= Space(TamSX3("F3_OBSERV2")[1]) 
LOCAL cObserv3		:= Space(TamSX3("F3_OBSERV3")[1])
LOCAL cPicNF := X3Picture("F3_NFISCAL")


PRIVATE oBrowse, oFolder
PRIVATE nUsado:= 0, nOpcA:=0
PRIVATE cEspecie		:= Space(5)
PRIVATE cNFiscal		:= Space(TamSX3("F3_NFISCAL")[1]) 
PRIVATE M->F3_CLIEFOR	:= CriaVar("F3_CLIEFOR")
PRIVATE M->F3_LOJA		:= CriaVar("F3_LOJA")
PRIVATE M->F3_DOCOR		:= ""
PRIVATE M->F3_SERIEOR	:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo						   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
M->F3_CLIEFOR:=CriaVar("F3_CLIEFOR")
M->F3_LOJA:=CriaVar("F3_LOJA")

M->F3_DOCOR  :=Space(TamSx3("F3_NFISCAL")[1])
If SF3->(ColumnPos("F3_SERIEOR")) > 0
	M->F3_SERIEOR:=Space(TamSx3("F3_SERIEOR")[1])
Endif

cNFiscal := CriaVar("F1_DOC")
cEspecie := Padr("COR",Len(Criavar("F1_ESPECIE",.F.)))

cF3      := "SA1"
cCliente := OemtoAnsi(STR0013)  //'Cliente'
cTipAnt  := cTipo
lInclui  := .T.

Set Key VK_F4 To a464F4()

DEFINE MSDIALOG oDlg TITLE cCadastro From 9,0 To 31,80 OF oBrowse //oMainWnd

@ 40, 005 TO 160, 309 LABEL "" OF oDlg  PIXEL

@ 50, 010 SAY OemtoAnsi(STR0006) SIZE 15, 8 OF oDlg PIXEL //Tipo
@ 50, 025 MSGET oTipo VAR cTipo  PICTURE "@!" WHEN .F. SIZE 90, 10 OF oDlg PIXEL

@ 50, 120 SAY oCliente  VAR cCliente SIZE 20, 7 OF oDlg PIXEL
@ 50, 140 MSGET oCliFor VAR M->F3_CLIEFOR PICTURE "@K!";
	F3 cF3 ;
	SIZE 47, 10 OF oDlg PIXEL
@ 50, 190 MSGET oLoja VAR M->F3_LOJA PICTURE "@!";
	VALID (A464Loja("I",@oTipo,@cSucClie,@M->F3_NFISCAL,@M->F3_SERIE,oDlg,@oCliFor,M->F3_CLIEFOR,M->F3_LOJA) .And. A464cNFis(cSucClie,M->F3_NFISCAL,M->F3_SERIE));
	SIZE 14, 10 OF oDlg PIXEL

@ 50, 240 SAY OemtoAnsi(STR0007) SIZE 20, 7 OF oDlg PIXEL //Emision
@ 50, 263 MSGET odEmissao VAR M->F3_EMISSAO SIZE 40, 10 OF oDlg PIXEL

@ 70, 010 SAY OemtoAnsi(STR0008) SIZE 30, 7 OF oDlg PIXEL //Nota Cred

If cPaisLoc == "ARG"
   @ 70, 056 MSGET oNFClie VAR M->F3_NFISCAL PICTURE "99999999";
	  VALID (A464ZERTELA(@cNFClie).and.A464ActVar(@cNFiscal,cSucClie,M->F3_NFISCAL).and.A464cNFis(cSucClie,cNFISCAL));
	  SIZE 27, 10 OF oDlg PIXEL
	  
   @ 70, 092 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie"
   @ 70, 112 MSGET M->F3_SERIE PICTURE "!!!";
	  VALID A464ActVar(@cNFiscal,cSucClie,M->F3_NFISCAL) .and. A464cNFis(cSucClie,cNFiscal,M->F3_SERIE);
	  SIZE 15, 10 OF oDlg PIXEL	  
Else
   @ 70, 056 MSGET oNFClie VAR M->F3_NFISCAL PICTURE cPicNF;
	  VALID (A464ZERTELA(@M->F3_NFISCAL) .And. A464cNFis(cSucClie,cNFISCAL));
	 SIZE 50, 10 OF oDlg PIXEL
	  
   @ 70, 120 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie"
   @ 70, 140 MSGET M->F3_SERIE PICTURE "!!!";
	  VALID A464cNFis(cSucClie,cNFiscal,M->F3_SERIE);
	  SIZE 15, 10 OF oDlg PIXEL	  
EndIf	  
 If ColumnPos("F3_DOCOR")>0 
	@ 85, 010 SAY OemtoAnsi(STR0021) SIZE 50, 7 OF oDlg PIXEL //"Fact Original"
	If cPaisLoc == "ARG"
		@ 85, 056 MSGET oNFClie VAR M->F3_DOCOR PICTURE "99999999";
			VALID A464ActVar(@cNFiscal,cSucClie,cNFOrig);
			SIZE 27, 10 OF oDlg PIXEL
		If ColumnPos("F3_SERIEOR")>0  
  			@ 85, 092 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie"
   			@ 85, 112 MSGET M->F3_SERIEOR PICTURE "!!!";
		     SIZE 15, 10 OF oDlg PIXEL	  
		 EndIf
	Else
   		@ 85, 056 MSGET oNFClie VAR M->F3_DOCOR PICTURE cPicNF;
	   		SIZE 50, 10 OF oDlg PIXEL
		If ColumnPos("F3_SERIEOR")>0 
	 	  	@ 85 ,120 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie De Factura Original"
   			@ 85, 140 MSGET M->F3_SERIEOR PICTURE "!!!";
	   		SIZE 15, 10 OF oDlg PIXEL	  
		EndIf	  
	EndIf
EndIf	  

@ 100, 010 SAY OemtoAnsi(STR0010) SIZE 40, 7 OF oDlg PIXEL //"Observaciones"
@ 100, 056 MSGET oObserv  VAR M->F3_OBSERV  SIZE 200, 10 OF oDlg PIXEL
@ 115, 056 MSGET oObserv2 VAR cObserv2 SIZE 200, 10 OF oDlg PIXEL
@ 130, 056 MSGET oObserv3 VAR cObserv3 SIZE 200, 10 OF oDlg PIXEL

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| A464BOK("SF3",@oDlg,@nOpca,M->F3_NFISCAL,M->F3_SERIE,M->F3_CLIEFOR,M->F3_LOJA,M->F3_EMISSAO,M->F3_DOCOR,M->F3_SERIEOR)},{|| nOpca:= 0 , oDlg:End()}) CENTERED
Setapilha()
RELEASE OBJECTS oFolder

If nOpca == 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava arquivo de Livros Fiscais (SF3)                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cModulo != "LOJ"
		RecLock( "SF3",.T. )
		Replace  F3_FILIAL  With xFilial("SF3"),;
			F3_ESPECIE With cEspecie,;
			F3_NFISCAL With cNFiscal,;
			F3_SERIE   With M->F3_SERIE,;
			F3_CLIEFOR With Subs(M->F3_CLIEFOR,1,Len(Criavar("F3_CLIEFOR"))),;
			F3_LOJA    With M->F3_LOJA,;
			F3_TIPO    With "D",; //cTipo
			F3_TIPOMOV With "V",;
			F3_ENTRADA With dDataBase,;
			F3_EMISSAO With M->F3_EMISSAO,;
			F3_REPROC  With "N",;
			F3_OBSERV  With M->F3_OBSERV,;
			F3_OBSERV2 With cObserv2,;
			F3_OBSERV3 With cObserv3,;
			F3_TPDOC   With "3"
		    If ColumnPos("F3_DOCOR")>0 
				Replace	F3_DOCOR   With M->F3_DOCOR
			EndIf
			If ColumnPos("F3_SERIEOR")>0
				Replace F3_SERIEOR With M->F3_SERIEOR
			EndIf	 
		
		MsUnLock()
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualizar a numeracao de Notas Fiscais no arquivo Tabelas... ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cNFiscal := StrZero(Val(cNFiscal)+1,Len(cNFiscal))
		
		dbSelectArea("SX5")
		dbSetOrder(1)
		IF (dbSeek(xFilial("SX5")+"99NOTACR"))
			RecLock("SX5",.F.)
			Replace X5_DESCRI  With cNFiscal
			Replace X5_DESCSPA With cNFiscal
			Replace X5_DESCENG With cNFiscal
			MsUnLock()
		ENDIF
	EndIf
EndIf

Set Key VK_F4 To 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da janela							 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")

Eval(bFiltraBrw)
Return


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A464Altera³ Autor ³ Leonardo Ruben        ³ Data ³03/03/00  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Alteracao das notas de correcao...             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA464                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A464Altera(cAlias,nReg,nOpc)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis.																 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

LOCAL oCliFor, oCliente, oLoja
LOCAL oDlg, oTipo
Local oBrowse, oFolder

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis para tratamento dos impostos.					  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cTipo := "COR-"+OemToAnsi(STR0011)  //"Nota de Correcci¢n"

LOCAL cNFClie :=TamSx3("D1_DOC")[1]	
LOCAL cNFOrig := ""
LOCAL cSerOrig:= ""
LOCAL cPicNF := X3Picture("F3_NFISCAL")

Local dData:=cTod(space(8))

dData:=SF3->F3_EMISSAO

If !FisChkDt(dData)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura a integridade da janela                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SF3")
	Eval(bFiltraBrw)
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo								³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cNFClie := F3_NFISCAL
PRIVATE cNFiscal   := F3_NFISCAL
PRIVATE cSerie     := F3_SERIE
PRIVATE dDEmissao  := F3_EMISSAO
PRIVATE cEspecie   := F3_ESPECIE
PRIVATE ca464Cli   := F3_CLIEFOR
PRIVATE cLoja      := F3_LOJA
PRIVATE M->F3_OBSERV  := F3_OBSERV
PRIVATE cObserv2   := F3_OBSERV2
PRIVATE cObserv3   := F3_OBSERV3
PRIVATE nUsado     := 0  , nOpcA     :=0

cF3      := "SA1"
cCliente := OemtoAnsi(STR0013)  //'Cliente'
cTipAnt  := cTipo
lInclui    := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a existencia do parametro dos impostos variaveis.   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ColumnPos("F3_DOCOR")>0 
	cNFOrig:= F3_DOCOR
EndIf
	
If ColumnPos("F3_SERIEOR")>0
	cSerOrig:= F3_SERIEOR
EndIf

DEFINE MSDIALOG oDlg TITLE cCadastro From 9,0 To 31.5,80 OF oBrowse //oMainWnd

@ 40, 005 TO 160, 309 LABEL "" OF oDlg  PIXEL
@ 50, 010 SAY OemtoAnsi(STR0006) SIZE 15, 8 OF oDlg PIXEL //Tipo
@ 50, 025 MSGET oTipo VAR cTipo  PICTURE "@!" WHEN .F. SIZE 90, 10 OF oDlg PIXEL

@ 50, 135 SAY oCliente  VAR cCliente SIZE 20, 7 OF oDlg PIXEL
@ 50, 165 MSGET oCliFor VAR cA464CLI PICTURE "@K!" WHEN .F. ;
	SIZE 47, 10 OF oDlg PIXEL
@ 50, 215 MSGET oLoja VAR cLoja PICTURE "@!" WHEN .F. ;
	SIZE 14, 10 OF oDlg PIXEL

@ 50, 250 SAY OemtoAnsi(STR0007)SIZE 20, 7 OF oDlg PIXEL  //Emision
@ 50, 273 MSGET odEmissao VAR dDEmissao WHEN .F.  SIZE 30, 10 OF oDlg PIXEL

@ 75, 010 SAY OemtoAnsi(STR0008) SIZE 30, 7 OF oDlg PIXEL //Nota Credi

@ 75, 056 MSGET oNFClie VAR cNFCLIE PICTURE cPicNF WHEN .F. ;
	SIZE 50, 10 OF oDlg PIXEL
@ 75, 120 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie"
@ 75, 140 MSGET cSerie           PICTURE "!!!" WHEN .F. ;
	SIZE 15, 10 OF oDlg PIXEL

If ColumnPos("F3_DOCOR")>0 
	@ 90, 010 SAY OemtoAnsi(STR0021) SIZE 50, 7 OF oDlg PIXEL //"Fact Original"
	@ 90, 056 MSGET oNFClie VAR cNFOrig  PICTURE cPicNF WHEN .F. ;
		SIZE 50, 10 OF oDlg PIXEL
EndIf	                            
If ColumnPos("F3_SERIEOR")>0  	
	@ 90, 120 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie De Factura Original"
	@ 90, 140 MSGET cSerOrig  PICTURE "!!!" WHEN .F. ;
		SIZE 15, 10 OF oDlg PIXEL
EndIf

@ 110, 010 SAY OemtoAnsi(STR0010) SIZE 40, 7 OF oDlg PIXEL //"Observaciones"
@ 110, 056 MSGET oObserv  VAR M->F3_OBSERV  SIZE 200, 10 OF oDlg PIXEL
@ 125, 056 MSGET oObserv2 VAR cObserv2 SIZE 200, 10 OF oDlg PIXEL
@ 140, 056 MSGET oObserv3 VAR cObserv3 SIZE 200, 10 OF oDlg PIXEL

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| A464BOK("SF3",@oDlg,@nOpca,cNFClie,cSerie,cA464Cli,cLoja,dDemissao,cNFOrig,cSerOrig)},{|| nOpca:= 0 , oDlg:End()}) CENTERED
Setapilha()
RELEASE OBJECTS oFolder

If nOpca == 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava arquivo de Livros Fiscais (SF3)                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cModulo != "LOJ"
		RecLock( "SF3",.F. )
		Replace	F3_OBSERV	With M->F3_OBSERV
		Replace	F3_OBSERV2	With cObserv2
		Replace	F3_OBSERV3	With cObserv3
		MsUnLock()
	EndIf
Else
	Return
EndIf


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da janela									  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")
Eval(bFiltraBrw)
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A464Deleta³ Autor ³ Leonardo Ruben       ³ Data ³ 03.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de exclusao de Notas de Correcao                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A464Deleta(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo											  ³±±
±±³			 ³ ExpN1 = Numero do registro 										  ³±±
±±³			 ³ ExpN2 = Numero da opcao selecionada 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA464                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A464Deleta(cAlias,nReg,nOpc)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis.																 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

LOCAL oCliFor, oCliente, oLoja
LOCAL oDlg, oTipo
Local oBrowse, oFolder

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis para tratamento dos impostos.					  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cTipo := "COR-"+OemToAnsi(STR0011)  //"Nota de Correcci¢n"
LOCAL cNFClie :=TamSx3("D1_DOC")[1]
Local dData:=cTod(space(8))
LOCAL cPicNF := X3Picture("F3_NFISCAL")

dData:=SF3->F3_EMISSAO

If !FisChkDt(dData)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura a integridade da janela                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SF3")
	Eval(bFiltraBrw)
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo								³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cNFClie := F3_NFISCAL
PRIVATE cNFiscal   := F3_NFISCAL
PRIVATE cSerie     := F3_SERIE
PRIVATE dDEmissao  := F3_EMISSAO
PRIVATE cEspecie   := F3_ESPECIE
PRIVATE ca464Cli   := F3_CLIEFOR
PRIVATE cLoja      := F3_LOJA
PRIVATE cObserv    := F3_OBSERV
PRIVATE cObserv2   := F3_OBSERV2
PRIVATE cObserv3   := F3_OBSERV3
PRIVATE nUsado     := 0  , nOpcA     :=0

cF3      := "SA1"
cCliente := OemtoAnsi(STR0013) //'Cliente'
cTipAnt  := cTipo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a existencia do parametro dos impostos variaveis.   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ColumnPos("F3_DOCOR")>0 
	cNFOrig := F3_DOCOR
EndIf	           
                 
If ColumnPos("F3_SERIEOR")>0  	
	cSerOrig := F3_SERIEOR
EndIf

DEFINE MSDIALOG oDlg TITLE cCadastro From 9,0 To 31.5,80 OF oBrowse //oMainWnd

@ 40, 005 TO 165, 309 LABEL "" OF oDlg  PIXEL

@ 50, 010 SAY OemtoAnsi(STR0006) SIZE 15, 8 OF oDlg PIXEL //Tipo
@ 50, 025 MSGET oTipo VAR cTipo  PICTURE "@!" WHEN .F. SIZE 90, 10 OF oDlg PIXEL

@ 50, 135 SAY oCliente  VAR cCliente SIZE 20, 7 OF oDlg PIXEL
@ 50, 165 MSGET oCliFor VAR cA464CLI PICTURE "@K!" WHEN .F. ;
	SIZE 47, 10 OF oDlg PIXEL
@ 50, 215 MSGET oLoja   VAR cLoja PICTURE "@!" WHEN .F. ;
	SIZE 14, 10 OF oDlg PIXEL

@ 50, 250 SAY OemtoAnsi(STR0007) SIZE 20, 7 OF oDlg PIXEL //Emision
@ 50, 273 MSGET odEmissao VAR dDEmissao WHEN .F. ;
	SIZE 30, 10 OF oDlg PIXEL

@ 75, 010 SAY OemtoAnsi(STR0008)SIZE 30, 7 OF oDlg PIXEL //Nota Credi

@ 75, 056 MSGET oNFClie VAR cNFCLIE PICTURE cPicNF WHEN .F. ;
	SIZE 50, 10 OF oDlg PIXEL
@ 75, 120 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie"
@ 75, 140 MSGET cSerie PICTURE "!!!" WHEN .F. ;
	SIZE 15, 10 OF oDlg PIXEL

If ColumnPos("F3_DOCOR")>0 
	@ 90, 010 SAY OemtoAnsi(STR0021) SIZE 50, 7 OF oDlg PIXEL //"Fact Original"
	@ 90, 056 MSGET oNFClie VAR cNFOrig PICTURE cPicNF WHEN .F. ;
		SIZE 50, 10 OF oDlg PIXEL
EndIf	                            
If ColumnPos("F3_SERIEOR")>0  	
	@ 90, 120 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie De Factura Original"
	@ 90, 140 MSGET cSerOrig  PICTURE "!!!" WHEN .F. ;
		SIZE 15, 10 OF oDlg PIXEL
EndIf
@ 110, 010 SAY OemtoAnsi(STR0010) SIZE 40, 7 OF oDlg PIXEL
@ 110, 056 MSGET oObserv  VAR cObserv  WHEN .F. SIZE 200, 10 OF oDlg PIXEL
@ 125, 056 MSGET oObserv2 VAR cObserv2 WHEN .F. SIZE 200, 10 OF oDlg PIXEL
@ 140, 056 MSGET oObserv3 VAR cObserv3 WHEN .F. SIZE 200, 10 OF oDlg PIXEL

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca:= 1 , oDlg:End()},{|| nOpca:= 0 , oDlg:End()}) CENTERED
Setapilha()
RELEASE OBJECTS oFolder

If nOpcA == 1
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Excluir Registros do Livro Fiscal...                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cModulo != "LOJ"
		RecLock("SF3",.F.,.T.)
		dbDelete()
		MsUnLock()
	EndIf
	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da janela									  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")
Eval(bFiltraBrw)
Return



/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A464Loja ³ Autor ³ Leonardo Ruben        ³ Data ³ 03.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validar a Loja do Cliente  										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametro ³ A464Loja(cExpr1)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³			 ³ cExpr1 := "I" Inclusao / "N" Nota Fiscal em Lote           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MatA464                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A464Loja(cVarTipo,oTipo,cSucClie,cNFClie,cSerie,oDlg,oCliFor,cClieFor,cLoja) //Bruno

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis 													³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL lRet := .F.
LOCAL aNumNF := {}
LOCAL aSerNF := {}
Local nTamNota:=TamSX3("F3_NFISCAL")[1]


cNFClie := If( cNFClie==NIL,"", cNFClie )
cSucClie := If( cSucClie==NIL,"", cSucClie )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a existencia do registro									 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty(cLoja)
	Return .F.
EndIf

dbSelectArea("SA1")
dbSetOrder(1)
dbSeek(cFilial+Subs(cClieFor,1,Len(Criavar("F3_CLIEFOR")))+cLoja)

IF Found()
	lRet := .T.
Else
	HELP(" ",1,"A464LOJA")
	oCliFor:SetFocus()
	Return .T.
Endif

If Getmv("MV_FACTAUT") == "N"
	aSerNF := A464Sx5FA(oDlg)  //Fernando Dourado 29/12/99 ,pois no Chile
	If Len(aSerNF) == 0
		oCliFor :SetFocus()
		Return( .T. )
	EndIf
	cSerie  := aSerNF[2]
	cNFiscal:= Subs(aSerNF[1],1,nTamNota)
	cNFClie := cNFiscal
Else
	aNumNF := ExecBlock("M464SQNF",.F.,.F.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se nao for encontrado em nenhuma circunstancia, edita a Consulta     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lRet
	HELP(" ",1,"A464LOJA")
	oCliFor :SetFocus()
	lRet	:=	.T.
EndIf

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A464cNFis ³ Autor ³ Leonardo Ruben        ³ Data ³ 03.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Validar o n£mero da Nota de Correcao...                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA464                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC FUNCTION A464cNFis(cSucClie,cNFClie,cSerie)
//Validar por A464Tudok.... Bruno 14/10/99
Local lRet := .T.
Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ A464Bok    ³ Autor ³ Leonardo Ruben      ³ Data ³ 03.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Controla Telas de Notas de Saida de Materias...            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MatA464                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A464Bok(cAlias,oDlg,nOpca,cNFClie,cSerie,cClieFor,cLoja,dDEmissao,cDocOr,cSerOr)
Local lRet1:= .F.
Local aArea:= GetArea()
Local lVldSer := ExistBlock("F3VLDSER")
Local lRet:= .T.

nOpca:=1
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao das variaveis do cabecalho.                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If EMPTY(cClieFor) .Or. EMPTY(cLoja) .Or. EMPTY(dDEmissao) .Or. EMPTY(cNFiscal)
	Help(" ",1,"OBRIGAT")
	Return(.F.)
Endif
SA1->(DbSetOrder(1))
If !SA1->(DbSeek(xFilial("SA1")+cClieFor+cLoja))
	Help(" ",1,"NoCLIENTE")
	Return(.F.)
Endif      
If lInclui
   cNFiscal := StrZero(Val(cNFClie),Len(cNFClie))
   lRet1:= ChkF3Loc(cSerie,cNFiscal,"COR")  //Dourado  para consistencia da Fatura no SF3   
   If !lRet1
	  lRet1:= ChkF3Loc(cSerie,cNFiscal,"NCC")  //Dourado  para consistencia da Fatura no SF3 
   EndIf
   If lRet1
      HELP(" ",1,"A464EXIST")
      lRet := .F.
   Endif   
	If lRet
		DbSelectArea("SF3")
		DbSetOrder(4)
		DbClearFilter()
		If !empty(cDocOr).and. !DbSeek( xFilial("SF3")+cClieFor+cLoja+cDocOr+cSerOr)   
		   	MsgAlert(OemToAnsi(STR0022),OemToAnsi(STR0008)) // Documento Original no Existe    Nota Credit
			RestArea(aArea)
			lRet := .F.  
		EndIf
	EndIf
	If lRet .And. lVldSer
		lRet:= ExecBlock("F3VLDSER",.F.,.F.,{cSerie,cNFiscal})
		If Valtype(lRet) <> "L"
			lRet := .T.
		EndIf	
	EndIf			 
EndIf    

RestArea(aArea)
If lRet
	oDlg:End()
EndIf	
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a464Sx5FA ³ Autor ³ Jose Lucas/Fernando   ³ Data ³ 29.12.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Achoice para numero da nota de Credito/Debito    do SX5    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA464                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a464Sx5FA(oDlgPae)
Local aSerNF := {}
Local nTamNota:=TamSX3("F1_DOC")[1]  // Argentina
Local nTimeout:=	GetMV("MV_NOTAOUT")
Local cAliAnt := Alias()
Local oDlg,cCadastro := OemToAnsi(STR0001),cVarQ:="  ",oQual   //"Notas"
Local nOpcA := 0
Local lAbandona := .F.
Local lTmk := IIF(cModulo == "TMK",.T.,.F.)  // Se o m¢dulo for SIGATMK

PRIVATE lSX5Troca := .F.

vNumero :=""
cCadastro := OemToAnsi(STR0014)  //"Nota Credito"
cBUSCANOT := "NOTACR"

If cModulo != "LOJ"
	
	cFiltro := ""
	//If ExistBlock("M465FILT")
	//   cFiltro := ExecBlock("M465FILT")
	//EndIf
	
	aSerNF := {}
	DbSelectArea("SX5")
	DbSetOrder(1)
	If !DbSeek( xFilial("SX5")+"99"+CBUSCANOT,.F. )
		RecLock("SX5",.T.)
		SX5->X5_TABELA := "99"
		SX5->X5_FILIAL := xFilial("SX5")
		SX5->X5_CHAVE  := CBUSCANOT
		SX5->X5_DESCRI := "00000001"
		SX5->X5_DESCSPA:= "00000001"
		SX5->X5_DESCENG:= "00000001"
		MSUNLOCK()
	Endif
	If ! Empty(cFiltro)
		If Subs(SX5->X5_CHAVE,1,3) $ cFiltro
			AADD( aSerNF,{ "   ", StrZero( Val( X5Descri() ),nTamNota ) } )
		EndIf
	Else
		AADD( aSerNF,{"   " , StrZero( Val( X5Descri() ),nTamNota ) } )
	EndIf
	
	If (Len(aSerNF) == 0) .Or. lAbandona
		MsRUnLock()
		Help(" ",1,"A460FLOCK")
		DbSelectArea(cAliant)
		Return( {} )
	Endif
	
Else
	
	cFiltro := ""
	If ExistBlock("M465LOJA")
		cFiltro := ExecBlock("M465LOJA")
	EndIf
	
	aSerNF := {}
	DbSelectArea("SX5")
	DbSetOrder(1)
	If !DbSeek( xFilial("SX5")+"99"+cBUSCANOT,.F. )
		RecLock("SX5",.T.)
		SX5->X5_TABELA := "99"
		SX5->X5_FILIAL := xFilial("SX5")
		SX5->X5_CHAVE  := CBUSCANOT
		SX5->X5_DESCRI := "00000001"
		SX5->X5_DESCSPA:= "00000001"
		SX5->X5_DESCENG:= "00000001"
		MSUNLOCK()
	Endif
	If ! Empty(cFiltro)
		If Subs(SX5->X5_CHAVE,1,3) $ cFiltro
			AADD( aSerNF,{ "   ", StrZero( Val( X5Descri() ),nTamNota ) } )
		EndIf
	Else
		// Fernando 26/10/99 inclusao pois no Chile nao existe serie
		AADD( aSerNF,{"   " , StrZero( Val( X5Descri() ),nTamNota ) } )
	EndIf
	If (Len(aSerNF) == 0) .Or. lAbandona
		MsRUnLock()
		Help(" ",1,"A460FLOCK")
		DbSelectArea(cAliant)
		Return( {} )
	Endif
	
Endif

Do while lastkey()<>27
	DEFINE MSDIALOG oDlg TITLE cCadastro From 10,30 To 19,68 OF oDlgPae //oMainWnd
	@ .5,.80 LISTBOX oQual VAR cVarQ Fields HEADER "",OemToAnsi(cCadastro) SIZE 130,42 ON DBLCLICK (aSerNF:=aSX5Troca(oQual:nAt,aSerNF,nTimeOut),oQual:Refresh()) NOSCROLL //"Serie"
	oQual:SetArray(aSerNF)
	oQual:bLine := { || {aSerNf[oQual:nAT,1],aSerNf[oQual:nAT,2]}}
	DEFINE SBUTTON FROM 51,80     TYPE 1 ACTION (nOpca := 1,nQualnAt:=oQual:nAt,oDlg:End()) ENABLE OF oDlg
	// Se o modulo nao for SIGATMK exibe o botao, caso contrario
	// nao exibe pois essa rotina nao podera ser cancelada pelo usuario
	If !lTmk
		DEFINE SBUTTON FROM 51,107    TYPE 2 ACTION (nOpcA := 3,nQualnAt:=oQual:nAt,oDlg:End()) ENABLE OF oDlg
	Endif
	ACTIVATE MSDIALOG oDlg
	
	if nOpcA<> 1
		exit
	Endif
	
	cNumero := aSerNf[nQualnAt,2]
	cSerie  := aSerNf[nQualnAt,1]
	
	If SF1->(DbSeek(xfilial("SF1")+cNumero+cSerie))
		If Alltrim(SF1->F1_ESPECIE)$"NCC|NCI"
			MsgStop(OemToAnsi(STR0015)+STRZERO(VAL(cnumero)+1,8))  //"Nota Credito ya cadastrada. La proxima nota "
			Loop
		Endif
	Endif
	Exit
Enddo

IF (nOpcA != 1)
	MsRUnLock()
	DbSelectArea(cAliant)
	Return ( {} )
Endif

If !lSX5Troca
	vNumero := cNumero
Endif

DbSelectArea(cAliant)
Return( { cNumero, cSerie } )

Function  A464ZerTela(cCaptura)
If !Empty(cCaptura)
	cCaptura:=StrZero(val(cCaptura),LEN(cCaptura))
Endif
Return(.T.)

Function A464ACTVar (cActual,cPrim,cSec)
If cPaisLoc == "ARG"
	cActual:=cPrim+cSec
Else
	cActual:=cSec
Endif
Return(.T.)
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A464Cancel³ Autor ³ Cristiano D. Alarcon ³ Data ³ 15.07.03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de cancelamento de Notas de Correcao              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A464Cancel(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo											  ³±±
±±³			 ³ ExpN1 = Numero do registro 										  ³±±
±±³			 ³ ExpN2 = Numero da opcao selecionada 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA464                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A464Cancel(cAlias,nReg,nOpc)

/////////////
// Var: geral
Local oCliFor, oCliente, oLoja
Local oDlg, oTipo
Local oBrowse, oFolder
///////////////////////////////
// Var: tratamento dos impostos
Local cTipo 	:= "COR-"+OemToAnsi(STR0011)  // Nota de Correcao
Local cNFClie 	:= TamSx3("D1_DOC")[1]
Local dData		:= cTod(space(8))
// Var: geral
/////////////
LOCAL cPicNF := X3Picture("F3_NFISCAL")

dData := SF3->F3_EMISSAO


If !FisChkDt(dData)
	///////////////////////////////////
	// Restaura a integridade da janela
	dbSelectArea("SF3")
	Eval(bFiltraBrw)
	Return
Endif
                                      

//////////////////////////////////////
// Monta a entrada de dados do arquivo
		  cNFClie	:= F3_NFISCAL
Private cNFiscal  := F3_NFISCAL
Private cSerie    := F3_SERIE
Private dDEmissao := F3_EMISSAO
Private cEspecie  := F3_ESPECIE
Private ca464Cli  := F3_CLIEFOR
Private cLoja     := F3_LOJA
Private cObserv   := F3_OBSERV
Private cObserv2  := F3_OBSERV2
Private cObserv3  := F3_OBSERV3
Private nUsado    := 0
Private nOpcA		:= 0


cF3      := "SA1"
cCliente := OemtoAnsi(STR0013) //'Cliente'
cTipAnt  := cTipo


/////////////////////////////////////////////////////////////
// Verifica a existencia do parametro dos impostos variaveis.

If ColumnPos("F3_DOCOR")>0 
	cNFOrig:= F3_DOCOR
EndIf
If ColumnPos("F3_SERIEOR")>0
	cSerOrig:= F3_SERIEOR
EndIf

// Anulacao da Nota de Correcao
DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0017) From 9,0 To 31.5,80 OF oBrowse //oMainWnd

@ 40, 005 TO 165, 309 LABEL "" OF oDlg  PIXEL

@ 50,010 	SAY OemtoAnsi(STR0006)SIZE 015,008 OF oDlg PIXEL // Tipo
@ 50,025 	MSGET oTipo VAR cTipo  PICTURE "@!" WHEN .F. SIZE 090,010 OF oDlg PIXEL

@ 50,135 SAY oCliente  VAR cCliente 								SIZE 020,007 OF oDlg PIXEL
@ 50,165 MSGET oCliFor VAR cA464CLI PICTURE "@K!" WHEN .F. SIZE 047,010 OF oDlg PIXEL
@ 50,215 MSGET oLoja   VAR cLoja 	PICTURE "@!" 	WHEN .F. SIZE 014,010 OF oDlg PIXEL

@ 50,250 SAY OemtoAnsi(STR0007) SIZE 020,007 OF oDlg PIXEL //Emision
@ 50,273 MSGET odEmissao VAR dDEmissao WHEN .F.	SIZE 030,010 OF oDlg PIXEL

@ 75,010 SAY OemtoAnsi(STR0008) SIZE 050,007 OF oDlg PIXEL //Nota Credito

@ 75,056 MSGET oNFClie VAR cNFCLIE PICTURE cPicNF	WHEN .F. SIZE 050,010 OF oDlg PIXEL
@ 75,120 SAY OemtoAnsi(STR0009) SIZE 015,007 OF oDlg PIXEL // Serie
@ 75,140 MSGET cSerie PICTURE "!!!" WHEN .F. SIZE 015,010 OF oDlg PIXEL

If ColumnPos("F3_DOCOR")>0 
	@ 90, 010 SAY OemtoAnsi(STR0021) SIZE 30, 7 OF oDlg PIXEL //"Fact Original"
	@ 90, 056 MSGET oNFClie VAR cNFOrig PICTURE cPicNF WHEN .F. ;
	SIZE 50, 10 OF oDlg PIXEL
EndIf    

If  ColumnPos("F3_SERIEOR")>0
	@ 90, 120 SAY OemtoAnsi(STR0009) SIZE 15, 7 OF oDlg PIXEL //"Serie De Factura Original"
	@ 90, 140 MSGET cSerOrig PICTURE "!!!" WHEN .F. ;
	SIZE 15, 10 OF oDlg PIXEL
EndIf

@ 110,010 SAY OemtoAnsi(STR0010) SIZE 040,007 OF oDlg PIXEL // Observacao
@ 110,056 MSGET oObserv  VAR cObserv  WHEN .F. 	SIZE 200,010 OF oDlg PIXEL
@ 125,056 MSGET oObserv2 VAR cObserv2 WHEN .F. 	SIZE 200,010 OF oDlg PIXEL
@ 140,056 MSGET oObserv3 VAR cObserv3 WHEN .F. 	SIZE 200,010 OF oDlg PIXEL

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca:= 1 , oDlg:End()},{|| nOpca:= 0 , oDlg:End()}) CENTERED
Setapilha()
RELEASE OBJECTS oFolder

If nOpcA == 1
	
	///////////////////////////////////////
	// Cancela Registros do Livro Fiscal...
	If cModulo != "LOJ"
		If MsgYesNo( OemToAnsi(STR0018) ) // Deseja cancelar esta Nota de Correcao
			RecLock("SF3",.F.,.T.)
				SF3->F3_DTCANC := dDataBase
			MsUnLock()
		Endif
	EndIf
	
EndIf

///////////////////////////////////
// Restaura a integridade da janela
dbSelectArea("SF3")
Eval(bFiltraBrw)
RETURN

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A464F4    ºAutor  ³Microsiga           ºFecha ³             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Exibe a relacao de documentos originais                     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a464F4()
Local aArea	:= GetArea()
Local aSerNF := {}
Local nOpcA := 0 
Local cVarEsp := ""
Local nSpc :=""
Local cQuery:=""
lExiste := .f.
aSerNF := {}

cVarEsp:=GetSESNew("NCC","1")
nSpc:=Tamsx3("F3_ESPECIE")[1] - Len(Alltrim(cVarEsp))         
cTipo:= Alltrim(cvarEsp)+space(nSpc)

DbSelectArea("SF3")
DbSetOrder(4) 

#IFDEF TOP

	If (TcSrvType()!="AS/400")
		lQuery := .T.
		cQuery := "SELECT SF3.R_E_C_N_O_ RECNO, SF3.F3_FILIAL,SF3.F3_VALCONT,SF3.F3_EMISSAO,SF3.F3_SERIE,SF3.F3_LOJA,SF3.F3_CLIEFOR,SF3.F3_ESPECIE,SF3.F3_NFISCAL,SF3.R_E_C_N_O_ SF3RECNO "
		cQuery += "FROM "+RetSqlName("SF3")+" SF3 "
		cQuery += "WHERE SF3.F3_FILIAL = '"+xFilial("SF3")+"' AND "
		cQuery += "F3_ESPECIE in (" + "'"+ctipo+ "'"+ ",'NF   ') AND " 
		cQuery += "F3_CLIEFOR= '"+M->F3_CLIEFOR+"' AND "
		cQuery += "F3_LOJA ='"+M->F3_LOJA+"' AND "
		cQuery += "SF3.D_E_L_E_T_ <> '*' "
		cQuery += "ORDER BY "+SqlOrder(SF3->(IndexKey()))
		cQuery := ChangeQuery(cQuery)
	
		dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),"SF3FILT", .T., .T.)
		dbSelectArea("SF3FILT")
		TCSetField("SF3FILT","F3_EMISSAO","D",8,0)
	Else	
		#ENDIF
		cFiltro:= "F3_ESPECIE $ " + "'"+ctipo +"|NF   '"
		cFiltro:= cFiltro + '.and.  F3_FILIAL==xFilial("SF3") .and. F3_CLIEFOR== "' + M->F3_CLIEFOR + '" .and. F3_LOJA == "' + M->F3_LOJA + '"'
		Set Filter To &(cfiltro)
	#IFDEF TOP
		EndIf
	#ENDIF

dBgOTOP()

While !Eof() 
	AADD( aSerNF,{  F3_NFISCAL,F3_SERIE,F3_EMISSAO,F3_VALCONT } )
	dbSkip()
EndDo

If Len(aSerNF) > 0

	DEFINE MSDIALOG oDlg TITLE STR0008 From 10,30 To 28,90 OF oBrowse//nota Credito //oMainWnd
	@ .5,.80 LISTBOX oQual VAR cVarQ Fields HEADER STR0008,STR0009,STR0019,STR0020 SIZE 225,110 NOSCROLL //"Nota de credito" "Serie Factura"  "Nº de Factura"
	oQual:SetArray(aSerNF)
	oQual:bLine := { || {aSerNf[oQual:nAT,1],aSerNf[oQual:nAT,2],aSerNf[oQual:nAT,3],aSerNf[oQual:nAT,4]}}
	DEFINE SBUTTON FROM 120,175 TYPE 1 ACTION (nOpca := 1,M->F3_DOCOR:=aSerNf[oQual:nAt,1],M->F3_SERIEOR:=aSerNf[oQual:nAt,2],oDlg:End()) ENABLE OF oDlg
	DEFINE SBUTTON FROM 120,205    TYPE 2 ACTION (nOpcA := 3,oDlg:End()) ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg
Else
	MsgAlert(OemToAnsi(STR0022),OemToAnsi(STR0008))// Nota de credito
EndIf          

#IFNDEF TOP
	DbClearFilter()
#ELSE 
	If (TcSrvType()!="AS/400")
		dbSelectArea("SF3FILT")
		DbCloseArea()
	Else         
		DbClearFilter()
	EndIf
#ENDIF

RestArea(aArea)
	
Return( )       

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Marco Bianchi         ³ Data ³01/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
     
Private aRotina := {{ STR0001 ,"AxPesqui"  , 0 , 1 , 0 , .F.},;   // Pesquisar
						{ STR0002 ,"A464Visual", 0 , 2 , 0 , NIL},;   // Visualizar
						{ STR0003 ,"A464Inclui", 0 , 3 , 0 , NIL},;   // Incluir
						{ STR0004 ,"A464Altera", 0 , 4 , 0 , NIL},;   // Altera
						{ STR0005 ,"A464Deleta", 0 , 5 , 0 , NIL},;   // Excluir
						{ STR0016 ,"A464Cancel", 0 , 5 , 0 , NIL} }   // Cancelar

If ExistBlock("M464MNU")
	ExecBlock("M464MNU",.F.,.F.)
EndIf

Return(aRotina)
