#INCLUDE "MATA360.CH" 
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "APWIZARD.CH"
#INCLUDE "FWLIBVERSION.CH"

#DEFINE MAXGETDAD 4096

Static aIntSE4	 := {} //Definida como Static pois no mvc Será necessário gerar na pré-validação e finalizar no commit
Static __lMetric := Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA360  ³ Autor ³ Claudinei Matasso Benzi³ Data ³ 10/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de atualizacao do cadastro de Cond. de Pagamentos  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void MATA360(void)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATA360(xAutoCab,xAutoItens,nOpcAuto) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aRotina	  := MenuDef()
PRIVATE cCadastro := OemToAnsi(STR0006)	//"Condi‡„o de Pagamento"


If !Empty(xAutoCab)
	If ValType(xAutoItens) <> "A"
		xAutoItens := {}	
	EndIf
	FWMVCRotAuto(ModelDef(),"SE4",nOpcAuto,{{"SE4MASTER",xAutoCab},{"SECDETAIL",xAutoItens}})
Else
	DEFINE FWMBROWSE oMBrowse ALIAS "SE4" DESCRIPTION STR0006 //"Condição de Pagamento"
		oMBrowse:SetAttach( .T. )			// Habilita visoes
		oMBrowse:SetTotalDefault('E4_FILIAL','COUNT',STR0031) //'Total de Registros'
		//Se não for SIGACRM inibe a exibição do gráfico
		If nModulo <> 73
			oMBrowse:SetOpenChart( .F. )
		EndIf
	ACTIVATE FWMBROWSE oMBrowse
EndIf
	
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Vendas & CRM          ³ Data ³18/01/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/           
Static Function MenuDef()     

Private aRotina	:= {} 


ADD OPTION aRotina Title STR0001 Action 'PesqBrw'         OPERATION 1 ACCESS 0 //'Pesquisar'
ADD OPTION aRotina Title STR0002 Action 'VIEWDEF.MATA360' OPERATION 2 ACCESS 0 //"Visualizar"
ADD OPTION aRotina Title STR0003 Action 'VIEWDEF.MATA360' OPERATION 3 ACCESS 0 //"Incluir"
ADD OPTION aRotina Title STR0004 Action 'VIEWDEF.MATA360' OPERATION 4 ACCESS 0 //"Alterar"
ADD OPTION aRotina Title STR0005 Action 'VIEWDEF.MATA360' OPERATION 5 ACCESS 3 //"Excluir"
If ( cPaisLoc == "BRA" )		
	ADD OPTION aRotina Title STR0025 Action 'A360SimCnd' OPERATION 7 ACCESS 0	 //"Simulação"
Endif
ADD OPTION aRotina Title STR0008 Action 'MATR035' OPERATION 8 ACCESS 0	 //"Imprimir"

 
aRotina:= CRMXINCROT("SE4",aRotina)
		
If ExistBlock ("MTA360MNU")
	ExecBlock ("MTA360MNU",.F.,.F.)
Endif	

return (aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ViewDef     ³ Autor ³Vendas & CRM           ³ Data ³ 18/01/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Definicao da View                                          	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oView                                                       	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ViewDef()
Local oModel   := FWLoadModel( 'MATA360' )	// Cria um objeto de Modelo de dados baseado no ModelDef do fonte informado
Local oStruSE4 := FWFormStruct( 2, 'SE4' )	// Cria as estruturas a serem usadas na View
Local oStruSEC := FWFormStruct( 2, 'SEC' ) // Cria as estruturas a serem usadas na View
Local oView									// Interface de visualização construída

oView := FWFormView():New()								// Cria o objeto de View
oView:SetContinuousForm()
oView:SetModel( oModel )									// Define qual Modelo de dados será utilizado				
oView:AddField( 'VIEW_SE4', oStruSE4, 'SE4MASTER' )	// Adiciona no nosso View um controle do tipo formulário (antiga Enchoice)
oView:AddGrid( 'VIEW_SEC' , oStruSEC, 'SECDETAIL' )	// Adiciona no nosso View um controle do tipo Grid (antiga Getdados)
oView:AddIncrementField( 'VIEW_SEC', 'EC_ITEM' )		// Item Incremental do Grid

// Cria um "box" horizontal para receber cada elemento da view
oView:CreateHorizontalBox( 'SUPERIOR', 35 )			
oView:CreateHorizontalBox( 'INFERIOR', 65 )

// Relaciona o identificador (ID) da View com o "box" para exibição
oView:SetOwnerView( 'VIEW_SE4', 'SUPERIOR' )			
oView:SetOwnerView( 'VIEW_SEC', 'INFERIOR' )

Return oView

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ModelDef    ³ Autor ³Vendas & CRM           ³ Data ³ 18/01/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Definicao do Model                                         	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ModelDef()
// Cria as estruturas a serem usadas no Modelo de Dados
Local oStruSE4	:= FWFormStruct( 1, 'SE4' )	// Estrutura SE4
Local oStruSEC	:= FWFormStruct( 1, 'SEC' ) // Estrutura SEC
Local oModel 								// Modelo de dados construído

// Cria o objeto do Modelo de Dados
oModel := MPFormModel():New( 'MATA360', /*pre*/,{|oModel| A360Pos(oModel) },{|oModel| A360Commit(oModel) })
oModel:SetVldActivate( { |oModel| A360VldAct( oModel ) } )
oModel:SetActivate({|oModel| A360Activ(oModel) })

// Adiciona ao modelo um componente de formulário
oModel:AddFields('SE4MASTER' /*cId*/,;
                 /*cOwner*/,;
                 oStruSE4 /*oModelStruct*/,;
                 {|oMdlSE4, cAction, cField, xValue| Mt360PrVdF(oMdlSE4, cAction, cField, xValue)} /*bPre*/,;
                 /*bPost*/,;
                 /*bLoad*/)
// Adiciona ao modelo uma componente de grid
oModel:AddGrid( 'SECDETAIL', 'SE4MASTER', oStruSEC , /*pre*/,{|oModel| A360LPos(oModel) })
oModel:GetModel( 'SECDETAIL'):SetOptional( .T. )
// Faz relacionamento entre os componentes do model
oModel:SetRelation( 'SECDETAIL', { { 'EC_FILIAL', 'xFilial("SEC")' }, { 'EC_CODIGO', 'E4_CODIGO' } }, SEC->( IndexKey( 1 ) ) )
// Adiciona a descrição do Modelo de Dados
oModel:SetDescription( STR0006 ) //"Condição de Pagamento"
// Retorna o Modelo de dados

Return oModel

//------------------------------------------------------------------------------
/*/{Protheus.doc} Mt360PrVdF
@Description  Bloco de Código de pré-validação do submodelo.
                Esse bloco é invocado quando há uma tentativa de atribuição de valores.
@sample        MT360PVldF(oMdlSE4, cAction, cField, xValue)
@param         oMdlSE4: Objeto do Modelo
                cAction: Ação a ser executada
                cField: Campo posicionado
                xValue: Conteúdo para o campo
@return		ExpL - Verdadeiro (Validação OK) ou Falso (Inconsistência na validação)
@author        CRM/Faturamento
@since         02/02/2018
@version       12.1.20
/*/
//------------------------------------------------------------------------------
Static Function Mt360PrVdF(oMdlSE4, cAction, cField, xValue)

Local lRet		:= .T.
Local cAliasQry	:= ""
Local cQuery	:= ""

If cAction == "SETVALUE"
	If cPaisLoc == "BRA"
		If cField == "E4_IPI"
			If	oMdlSE4:GetValue("E4_TIPO") == "9"
				If ( !("%" $ oMdlSE4:GetValue("E4_COND")) .AND. xValue $ "JS" ) .OR.;
				   (   "%" $ oMdlSE4:GetValue("E4_COND")  .AND. xValue == "S" )
					Help( " ",1,"A360TIPO9" )
					lRet	:= .F.
				EndIf
			EndIf
		ElseIf cField == "E4_SOLID"
			If	oMdlSE4:GetValue("E4_TIPO") == "9"
				If ( !("%" $ oMdlSE4:GetValue("E4_COND")) .AND. xValue $ "JS" ) .OR.;
				   (   "%" $ oMdlSE4:GetValue("E4_COND")  .AND. xValue == "S" )
					Help( " ",1,"A360TIPO9" )
					lRet	:= .F.
				EndIf
			ElseIf	oMdlSE4:GetValue("E4_TIPO") == "8" .AND. xValue $ "JS"
				Help( " ",1,"A360TIPO8" )
				lRet	:= .F.
			EndIf
		ElseIf cField == "E4_CTRADT"
			If	oMdlSE4:GetOperation() = MODEL_OPERATION_UPDATE .And. xValue <> oMdlSE4:GetValue("E4_CTRADT") .And. xValue <> "1"
				cAliasQry := GetNextAlias()

				cQuery := "SELECT DISTINCT SE4.E4_CODIGO "
				cQuery += "FROM " + RetSqlName("SE4") + " SE4 "
				cQuery += "INNER JOIN " + RetSqlName("SC5") + " SC5 "
				cQuery += "ON SC5.C5_FILIAL = SE4.E4_FILIAL AND SC5.C5_CONDPAG = SE4.E4_CODIGO AND SC5.D_E_L_E_T_ = ' ' "
				cQuery += "INNER JOIN " + RetSqlName("FIE") + " FIE "
				cQuery += "ON FIE.FIE_FILIAL = SC5.C5_FILIAL AND FIE.FIE_PEDIDO = SC5.C5_NUM AND (CASE  "
				cQuery += "WHEN  FIE.FIE_CLIENT <> ' ' "
				cQuery += "THEN FIE.FIE_CLIENT "
				cQuery += "ELSE FIE.FIE_FORNEC "
				cQuery += "END) = SC5.C5_CLIENTE AND FIE.D_E_L_E_T_ = ' ' "
				cQuery += "WHERE SE4.E4_FILIAL = '" + xFilial("SE4") + "' "
				cQuery += "AND SE4.E4_CODIGO = '" + oMdlSE4:GetValue("E4_CODIGO") + "' "
				cQuery += "AND SE4.D_E_L_E_T_ = ' ' "
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasQry, .T., .T. )

				If (cAliasQry)->( !Eof() )
					lRet	:= .F.
					(cAliasQry)->( dbCloseArea() )
					Help( "", 1, "NOALTERA", , Chr(13) + Chr(10) +  "SE4 - " + AllTrim( RetTitle( "E4_CTRADT" ) ), 3, 0 )
				EndIf
			EndIf
		EndIf
	EndIf
EndIf
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A360LPos    ³ Autor ³Vendas & CRM           ³ Data ³ 18/01/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Validação de Linha - LinOK - Grid SEC						³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A360LPos(oModelSEC)
Local lRet 		:= .T.
Local oModel 	:= FwModelActive() 
Local oModelSE4 := oModel:GetModel( 'SE4MASTER' )

If oModelSE4:GetValue("E4_TIPO") == "B"
	If !oModelSEC:IsDeleted()
		If Empty(oModelSEC:GetValue("EC_COND")) .OR. oModelSEC:GetValue("EC_RATEIO") == 0 .OR. Empty(oModelSEC:GetValue("EC_TIPO"))		
			Help(" ",1,"OBRIGAT")
			lRet := .F.
		Endif
	
	Endif	
Else
	If !oModelSEC:IsDeleted()
		If !Empty(oModelSEC:GetValue("EC_COND")) .OR. oModelSEC:GetValue("EC_RATEIO") != 0 .OR. !Empty(oModelSEC:GetValue("EC_TIPO"))		
			Help(" ",1,"M360DESMEM")
			lRet := .F.
		Endif
	Endif	
Endif	

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A360VldAct  ³ Autor ³Vendas & CRM           ³ Data ³ 18/01/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Validação de Ativação do Model								³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A360VldAct(oModel)
Local lRet := .T.

If oModel:getOperation() == MODEL_OPERATION_DELETE
	lRet := A360CanDel(SE4->E4_CODIGO)
EndIf      	
		
Return lRet 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A360Activ   ³ Autor ³Vendas & CRM           ³ Data ³ 18/01/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ P´re-Validação do Model                                    	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A360Activ(oModel)
Local lRet 	:= .T.
Local nOper := oModel:GetOperation()

If nOper == MODEL_OPERATION_INSERT .OR.;
	nOper == MODEL_OPERATION_UPDATE .OR.;
	nOper == MODEL_OPERATION_DELETE
	
	//Inicializa na Pré Validação
	aIntSE4 := {}	
	A035Int( 1, nOper, aIntSE4 )		
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A360Pos     ³ Autor ³Vendas & CRM           ³ Data ³ 18/01/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Pos-Validação do Model                                    	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A360Pos(oModel)

Local lRet 			:= .T.
Local nI				:= 0
Local nPercent		:= 0
Local oModelSE4	 	:= oModel:GetModel("SE4MASTER")
Local oModelSEC	 	:= oModel:GetModel("SECDETAIL")

If cPaisLoc == "BRA"

	If	oModelSE4:GetValue("E4_TIPO") == "9" .AND. oModelSE4:GetIdField("E4_IPI") > 0 .AND. oModelSE4:GetIdField("E4_SOLID") > 0
		If ( !("%" $ oModelSE4:GetValue("E4_COND")) .AND. ( oModelSE4:GetValue("E4_IPI") $ "JS" .OR. oModelSE4:GetValue("E4_SOLID") $ "JS" )) .OR.;
		   (   "%" $ oModelSE4:GetValue("E4_COND")  .AND. ( oModelSE4:GetValue("E4_IPI") == "S" .OR. oModelSE4:GetValue("E4_SOLID") == "S" ))
			Help( " ", 1, "A360TIPO9" )
			lRet	:= .F.
		EndIf
	ElseIf	oModelSE4:GetValue("E4_TIPO") == "8" .AND. oModelSE4:GetIdField("E4_SOLID") > 0 .AND. oModelSE4:GetValue("E4_SOLID") $ "JS"
		Help( " ", 1, "A360TIPO8" )
		lRet	:= .F.
	EndIf

EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se a soma dos rateios informados                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .AND. oModel:GetValue("SE4MASTER","E4_TIPO") == "B"

	For nI := 1 To oModelSEC:Length()
		oModelSEC:GoLine( nI )
		If !oModelSEC:IsDeleted()
			nPercent += oModelSEC:GetValue("EC_RATEIO")
			If Empty(oModelSEC:GetValue("EC_COND")) .Or. Empty(oModelSEC:GetValue("EC_TIPO")) .Or. oModelSEC:GetValue("EC_RATEIO") == 0
				Help(" ",1,"OBRIGAT")
				lRet := .F.
			Endif
		Endif
	Next nX
	
	If lRet .AND. nPercent <> 100
		Help(" ",1,"M360RATEIO")
		lRet := .F.
	EndIf

Else

	If lRet .AND. oModel:GetValue("SE4MASTER","E4_TIPO") == "2" .And. Val(Subs(oModel:GetValue("SE4MASTER","E4_CODIGO"),2,1)) == 0
		Help( " ",1,"A360TIPO2" )
		lRet := .F.
	Endif
 
	If lRet
		For nI := 1 To oModelSEC:Length()
			oModelSEC:GoLine( nI )
			If !oModelSEC:IsDeleted() .And. (!Empty(oModelSEC:GetValue("EC_COND")) .Or. oModelSEC:GetValue("EC_RATEIO") <> 0 .Or. ;
											!Empty(oModelSEC:GetValue("EC_TIPO")))
				Help(" ",1,"M360DESMEM")
				lRet := .F.
			Endif
		Next nX
	Endif 
Endif 
 
If lRet
	If ExistBlock("MT360VLD")
		lRet := ExecBlock("MT360VLD",.F.,.F.) 
	Endif

	/* Integração RISK - TOTVS Mais Negócios
	Avalia se existem registros com a condição de pagamento do TOTVS Mais Negócios */
	If lRet .And. FindFunction( "RskIsActive" ) .And. RskIsActive()
		lRet := RskVdPayCond( oModel )
	EndIf
Endif 

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A360Commit  ³ Autor ³Vendas & CRM           ³ Data ³ 18/01/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Gravação            	                                    	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a360Commit(oModel)
Local nOper 		:= oModel:GetOperation()
Local lOk 			:= .T.

If nOper == MODEL_OPERATION_DELETE
	If ExistBlock("MT360DEL")
		lOk := ExecBlock("MT360DEL",.F.,.F.)
		lOk := IIf(ValType(lOk)<>"L",.T.,lOk)
	EndIf	
EndIf

If lOk
	lOk := FwFormCommit(oModel)
	
	// Integra Protheus x LEGAL DESK - SIGAPFS             
	// Grava na fila de sincronização se o parâmetro MV_JFSINC = '1' - SIGAPFS
	If lOk .AND. FindFunction("J170GRAVA")
		J170GRAVA("SE4", xFilial("SE4") + FwFldGet("E4_CODIGO"), AllTrim(Str(nOper)))
	EndIf
	
	// Função usada para validar se existe integração do Protheus com o RM Solum e
	// Para chamar a validação específica do RM Solum na deleção do Produto. (PMSXFUNB)
	If lOk .AND. FindFunction("PmsSlmPrd") .AND. !PmsSlmPrd("CP")
		lOk := .F.
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de entrada apos a atualizacao das tabelas da Cond.Pagto³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lOk .AND. ExistBlock("MT360GRV")
		ExecBlock("MT360GRV",.F.,.F.)
	EndIf

	//Valida a data da LIB para utilização na Telemetria
	If (nOper <> MODEL_OPERATION_DELETE) .And. FwFldGet("E4_CTRADT") == "1" .And. FatLibMetric()
		//Telemetria - Pagamentos com Recebimento Antecipado
		FwCustomMetrics():setUniqueMetric("MATA360", "faturamento-protheus_patamentos-recebimentos-antecipados_total", FwFldGet("E4_CTRADT"), /*dDateSend*/, /*nLapTime*/,"MATA360")
	EndIf
EndIf

If lOk 	
	A035Int( 2, nOper , aIntSE4 )
	MT360AltOk()
		
	//Limpa após gravação
	aSize(aIntSE4,0)			
EndIf

Return lOk

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A360Man   ³ Autor ³ Henry Fila            ³ Data ³ 15/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Inclusao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Alias do cabecalho do pedido de venda                ³±±
±±³          ³ExpN2: Recno do cabecalho do pedido de venda                ³±±
±±³          ³ExpN3: Opcao do arotina                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A360Inclui()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A360Man(cAlias,nReg,nOpc)

Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := MsAdvSize() 

Local cSeek     := ""
Local cWhile    := ""

Local oDlg

Local lContinua := .T.
Local lInclui   := (nOpc == 3)
Local lAltera   := (nOpc == 4)

Local nOpca     := 0
Local nSaveSx8  := GetSx8Len()
Local aIntSE4	:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aCols     := {}
Private aHeader   := {}
Private aTELA[0][0]
Private aGETS[0]                    
Private aRotina   := MenuDef()


A035Int( 1, nOpc, aIntSE4 )

If nOpc > Len(aRotina)
	nOpc := 1
Endif

SaveInter()

If nOpc == 5
	lContinua := A360CanDel(SE4->E4_CODIGO)
Endif	

If lContinua

	If lInclui
		RegToMemory( "SE4", .T., .F. )
	Else
		If lAltera
			SoftLock("SE4")
			RegToMemory( "SE4", .F., .F. )
		Else
			RegToMemory( "SE4", .F., .F. )
			lContinua := .T.
		EndIf
	EndIf

	dbSelectArea("SEC")
	dbSetOrder(1)
	
	If lContinua
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta aHeader e aCols utilizando a funcao FillGetDados.      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lInclui
			cSeek  := xFilial("SEC")+M->E4_CODIGO
			cWhile := "SEC->EC_FILIAL+SEC->EC_CODIGO"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Sintaxe da FillGetDados(nOpcX,Alias,nOrdem,cSeek,bSeekWhile,uSeekFor,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			FillGetDados(nOpc,"SEC",1,cSeek,{|| &cWhile },,,,,,,,aHeader,aCols,,,,)
		Endif
		
		If Empty(aCols)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Sintaxe da FillGetDados(nOpcX,Alias,nOrdem,cSeek,bSeekWhile,uSeekFor,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			FillGetDados(nOpc,"SEC",1,,,,,,,,,.T.,aHeader,aCols,,,,)
			aCols[1][aScan(aHeader,{|x| Trim(x[2])=="EC_ITEM"})] := StrZero(1,Len(SEC->EC_ITEM))
		Endif	

		If ( Type("l360Auto") == "U" .OR. !l360Auto )
			aObjects := {} 
			AAdd( aObjects, { 100, 100, .t., .t. } )
			AAdd( aObjects, { 100, 100, .t., .t. } )
			
			aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
			aPosObj := MsObjSize( aInfo, aObjects ) 
			
		 	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 To aSize[6],aSize[5] of oMainWnd PIXEL
				EnChoice( "SE4", nReg, nOpc,,,,,aPosObj[1], , 3, , , , , ,.F. )
				oGetD := MsNewGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],IIF(!lInclui.And.!lAltera,0,GD_INSERT+GD_UPDATE+GD_DELETE),"A360LinOk()","A360TudOk()","+EC_ITEM",/*alteraveis*/,/*freeze*/,MAXGETDAD,/*fieldok*/,/*superdel*/,/*delok*/,,aHeader,aCols)	
				
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 1,aCols:=oGetD:aCols,If(Obrigatorio(aGets,aTela) .And. oGetD:TudoOk(),oDlg:End(), nOpca :=  0 )},{|| nOpca := 2,oDlg:End()})
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ validando dados pela rotina automatica                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)},aRotina[nOpc][4]) .And. MsGetDAuto(aAutoItens,"A360LinOk()",{|| A360TudOk()},aAutoCab,aRotina[nOpc][4])
				nOpcA := 1
			EndIf		
		EndIf
		If nOpca == 1
			Begin Transaction
				lGravou := A360Grava(nOpc-2,aCols,aHeader)
				
				If ( lGravou )
					EvalTrigger()
					While (GetSx8Len() > nSaveSx8)
						ConfirmSx8()
					EndDo
				EndIf
				
			End Transaction
		
			If (lGravou)
				A035Int( 2, nOpc, aIntSE4 )
			EndIf
		
			While (GetSx8Len() > nSaveSx8)
				RollBackSx8()
			EndDo
		
			MsUnlockAll()    	
		Else
			While (GetSx8Len() > nSaveSx8)
				RollBackSx8()
			EndDo			
		Endif	
	Endif	

Endif
RestInter()
	
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A360Limite³ Autor ³ Raul C. Capeleti      ³ Data ³ 28/03/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica o valor dos limites digitados.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A360Limite()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION A360Limite()
If Empty(M->E4_INFER) .and. Empty(M->E4_SUPER)
	Return .T.
ElseIf !Empty(M->E4_INFER) .and. Empty(M->E4_SUPER)
	Return .T.
ElseIf !Empty(M->E4_INFER) .and. !Empty(M->E4_SUPER)
	If M->E4_INFER > M->E4_SUPER
		Help(" ",1,"A360LIMINF")
		Return .F.
	End
ElseIf Empty(M->E4_SUPER) .AND. !Empty(M->E4_INFER)
	Help(" ",1,"A360LIMINF")
	Return .F.
End
Return .T.


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A360Grava ³ Autor ³ Henry Fila            ³ Data ³ 15/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Gravacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Alias do cabecalho do pedido de venda                ³±±
±±³          ³ExpN2: Recno do cabecalho do pedido de venda                ³±±
±±³          ³ExpN3: Opcao do arotina                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A360Grava()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A360Grava(nOpcao,aCols,aHeader)

Local aArea     := GetArea()
Local bCampo 	:= {|nCPO| Field(nCPO) }
Local lGravou   := .F.
Local lOk		  := .T.
Local nX        := 0
Local nY        := 0        
Local nPosRecno := Len(aHeader)
Local lIntegDef  := FWHasEAI("MATA360",.T.,,.T.)

Private INCLUI  := .F.
PRIVATE ALTERA  :=  .F.
Do Case
Case nOpcao <> 3

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Grava o Cabecalho                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SE4")
	dbSetOrder(1)
	If MsSeek(xFilial("SE4")+M->E4_CODIGO)
		ALTERA := .T.
		RecLock("SE4",.F.)
	Else
		INCLUI := .T.
		RecLock("SE4",.T.)
	EndIf
	For nX := 1 TO FCount()
		FieldPut(nX,M->&(EVAL(bCampo,nX)))
	Next nX
	SE4->E4_FILIAL := xFilial("SE4")
	
	SE4->(FkCommit())

	If lIntegDef
		FwIntegDef("MATA360")
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o tipo e de desmembramento de condicoes          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If M->E4_TIPO == "B"

		dbSelectArea("SEC")	

		For nX := 1 To Len(aCols)
	
			If !aCols[nX,Len(aCols[nX])]
	
				If aCols[nX][nPosRecno] > 0    
					dbGoto(aCols[nX][nPosRecno])
					RecLock("SEC",.F.)											
				Else 
					RecLock("SEC",.T.)					
				Endif						

				For nY := 1 To Len(aHeader)
					If ( aHeader[nY][10] != "V" )
						SEC->(FieldPut(FieldPos(aHeader[nY][2]),aCols[nX][nY]))
					EndIf
				Next nY

				SEC->EC_FILIAL := xFilial("SEC")
				SEC->EC_CODIGO := M->E4_CODIGO
				MsUnLock()

			Else
				dbSelectArea("SEC")
				If aCols[nX][nPosRecno] > 0    
					dbGoto(aCols[nX][nPosRecno])
					RecLock("SEC")
						dbDelete() 
					MsUnlock()
				Endif						

			Endif
    		
		Next nX
	Else

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se for diferente do tipo B verifica se existe alguma linha   ³
		//³no acols e deleta pois nao deve existir SEC com tipo <> de B ³		
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SEC")
		For nX := 1 To Len(aCols)	
			If aCols[nX][nPosRecno] > 0    
				dbGoto(aCols[nX][nPosRecno])
				RecLock("SEC")
					dbDelete() 
				MsUnlock()
			Endif						
		Next nX
		
	Endif	
                
	lGravou := .T.                                  

OtherWise

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³De acordo com a normalizacao exclui itens depois o cabecalho ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o tipo e de desmembramento de condicoes          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("MT360DEL")
		lOk := ExecBlock("MT360DEL",.F.,.F.)
		lOk := IIf(ValType(lOk)<>"L",.T.,lOk)
	EndIf	
   
	If lOk
		If M->E4_TIPO == "B"
		                                  
			dbSelectArea("SEC")
			For nX := 1 To Len(aCols)
				If aCols[nX][nPosRecno] > 0    
					dbGoto(aCols[nX][nPosRecno])
					RecLock("SEC")
						dbDelete() 
					MsUnlock()
				Endif						
			Next nX
			
	    Endif
		
		RecLock("SE4",.F.)
			dbDelete()
		MsUnlock()
					
		If lIntegDef
			FwIntegDef("MATA360")
		EndIf
		
		// Função usada para validar se existe integração do Protheus com o RM Solum e
		// Para chamar a validação específica do RM Solum na deleção do Produto. (PMSXFUNB)
		If FindFunction("PmsSlmPrd") .AND. !PmsSlmPrd("CP")
			lGravou := .F.
			Return lGravou
		Endif
		
		lGravou := .T.                                  
	EndIf

EndCase

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ponto de entrada apos a atualizacao das tabelas da Cond.Pagto³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MT360GRV")
	ExecBlock("MT360GRV",.F.,.F.)
EndIf	

If lGravou
	MT360AltOk()
EndIf
RestArea(aArea)
Return(lGravou)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A360CanDel³ Autor ³ Henry Fila            ³ Data ³ 15/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Gravacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo da condicao de pagamento                      ³±±
±±³          ³ExpL2: Exibe help                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A360CanDel()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A360CanDel(cCodigo,lHelp)

Local aArea     := GetArea()
Local aAreaSE4  := SE4->(GetArea())
Local aAreaSEC  := SEC->(GetArea())
Local lCanDel   := .T.          
Local cQuery    := ""

Default lHelp   := .T.
Default cCodigo := " "
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea( "SE4" )
If lCanDel

   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³Verifica nos pedidos de venda                            ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   	cQuery := "SELECT COUNT(*) RECSC5 FROM "
	cQuery += RetSqlName("SC5") + " SC5 "
	cQuery += " WHERE "                                    
	cQuery += "C5_FILIAL = '"+xFilial("SC5")+"' AND "
	cQuery += "C5_CONDPAG = '" + cCodigo + "' AND "
	cQuery += "SC5.D_E_L_E_T_ = ' ' "
	
	cQuery := ChangeQuery(cQuery)
	
	dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBSC5",.F.,.T.)
	
	If TRBSC5->RECSC5 > 0
		If lHelp
			a360DelMsg("SC5")
		Endif	
		lCanDel := .F.
	Endif							
	
	dbCloseArea()
	dbSelectArea("SE4")
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica nos orcamentos                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCanDel
		cQuery := "SELECT COUNT(*) RECSCJ FROM "
		cQuery += RetSqlName("SCJ") + " SCJ "
		cQuery += " WHERE "                                    
		cQuery += "CJ_FILIAL = '"+xFilial("SCJ")+"' AND "
		cQuery += "CJ_CONDPAG = '" + cCodigo + "' AND "
		cQuery += "SCJ.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBSCJ",.F.,.T.)
	
		If TRBSCJ->RECSCJ > 0
			If lHelp
				a360DelMsg("SCJ")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica nos pedidos de compra                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCanDel
		cQuery := "SELECT COUNT(*) RECSC7 FROM "
		cQuery += RetSqlName("SC7") + " SC7 "
		cQuery += " WHERE "                                    
		cQuery += "C7_FILIAL = '"+xFilial("SC7")+"' AND "
		cQuery += "C7_COND = '" + cCodigo + "' AND "
		cQuery += "SC7.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBSC7",.F.,.T.)
	
		If TRBSC7->RECSC7 > 0
			If lHelp
				a360DelMsg("SC7")
			Endif	
			lCanDel := .F.
		Endif							
	
		dbCloseArea()
		dbSelectArea("SE4")
	                     
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica nos clientes                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCanDel
		cQuery := "SELECT COUNT(*) RECSA1 FROM "
		cQuery += RetSqlName("SA1") + " SA1 "
		cQuery += " WHERE "
		cQuery += "A1_FILIAL = '"+xFilial("SA1")+"' AND "
		cQuery += "A1_COND = '" + cCodigo + "' AND "
		cQuery += "SA1.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBSA1",.F.,.T.)
	
		If TRBSA1->RECSA1 > 0
			If lHelp
				a360DelMsg("SA1")
			Endif	
			lCanDel := .F.
		Endif							
	
		dbCloseArea()
		dbSelectArea("SE4")
	                     
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica nas cotacoes                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	If lCanDel
		cQuery := "SELECT COUNT(*) RECSC8 FROM "
		cQuery += RetSqlName("SC8") + " SC8 "
		cQuery += " WHERE "                                    
		cQuery += "C8_FILIAL = '"+xFilial("SC8")+"' AND "
		cQuery += "C8_COND = '" + cCodigo + "' AND "
		cQuery += "SC8.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBSC8",.F.,.T.)
	
		If TRBSC8->RECSC8 > 0
			If lHelp
				a360DelMsg("SC8")
			Endif	
			lCanDel := .F.
		Endif							
	
		dbCloseArea()
		dbSelectArea("SE4")
	
	                     
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica nas notas de entrada                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCanDel
		cQuery := "SELECT COUNT(*) RECSF1 FROM "
		cQuery += RetSqlName("SF1") + " SF1 "
		cQuery += " WHERE "                                    
		cQuery += "F1_FILIAL = '"+xFilial("SF1")+"' AND "
		cQuery += "F1_COND = '" + cCodigo + "' AND "
		cQuery += "SF1.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBSF1",.F.,.T.)
	
		If TRBSF1->RECSF1 > 0
			If lHelp
				a360DelMsg("SF1")
			Endif	
			lCanDel := .F.
		Endif							
	
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica nas notas de saida                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCanDel
		cQuery := "SELECT COUNT(*) RECSF2 FROM "
		cQuery += RetSqlName("SF2") + " SF2 "
		cQuery += " WHERE "                                    
		cQuery += "F2_FILIAL = '"+xFilial("SF1")+"' AND "
		cQuery += "F2_COND = '" + cCodigo + "' AND "
		cQuery += "SF2.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBSF2",.F.,.T.)
	
		If TRBSF2->RECSF2 > 0
			If lHelp
				a360DelMsg("SF2")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica os contratos de parceria - compras              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCanDel
		cQuery := "SELECT COUNT(*) RECSC3 FROM "
		cQuery += RetSqlName("SC3") + " SC3 "
		cQuery += " WHERE "                                    
		cQuery += "C3_FILIAL = '"+xFilial("SC3")+"' AND "
		cQuery += "C3_COND = '" + cCodigo + "' AND "
		cQuery += "SC3.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBSC3",.F.,.T.)
	
		If TRBSC3->RECSC3 > 0
			If lHelp
				a360DelMsg("SC3")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica os contratos de parceria - vendas               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCanDel
		cQuery := "SELECT COUNT(*) RECADA FROM "
		cQuery += RetSqlName("ADA") + " ADA "
		cQuery += " WHERE "                                    
		cQuery += "ADA_FILIAL = '"+xFilial("ADA")+"' AND "
		cQuery += "ADA_CONDPG = '" + cCodigo + "' AND "
		cQuery += "ADA.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBADA",.F.,.T.)
	
		If TRBADA->RECADA > 0
			If lHelp
				a360DelMsg("ADA")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica os Projetos - Field Service                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCanDel 
		cQuery := "SELECT COUNT(*) RECABH FROM "
		cQuery += RetSqlName("ABH") + " ABH "
		cQuery += " WHERE "                                    
		cQuery += "ABH_FILIAL = '"+xFilial("ABH")+"' AND "
		cQuery += "ABH_CPAGPV = '" + cCodigo + "' AND "
		cQuery += "ABH.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBABH",.F.,.T.)
	
		If TRBABH->RECABH > 0
			If lHelp
				a360DelMsg("ABH")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica os Orçamentos - Field Service                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	If lCanDel
		cQuery := "SELECT COUNT(*) RECAB3 FROM "
		cQuery += RetSqlName("AB3") + " AB3 "
		cQuery += " WHERE "                                    
		cQuery += "AB3_FILIAL = '"+xFilial("AB3")+"' AND "
		cQuery += "AB3_CONPAG = '" + cCodigo + "' AND "
		cQuery += "AB3.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBAB3",.F.,.T.)
	
		If TRBAB3->RECAB3 > 0
			If lHelp
				a360DelMsg("AB3")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica os Tabela de preço                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	If lCanDel 
		cQuery := "SELECT COUNT(*) RECDA0 FROM "
		cQuery += RetSqlName("DA0") + " DA0 "
		cQuery += " WHERE "                                    
		cQuery += "DA0_FILIAL = '"+xFilial("DA0")+"' AND "
		cQuery += "DA0_CONDPG = '" + cCodigo + "' AND "
		cQuery += "DA0.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBDA0",.F.,.T.)
	
		If TRBDA0->RECDA0 > 0
			If lHelp
				a360DelMsg("DA0")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
				
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica as Regras de Desconto                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	If lCanDel 
		cQuery := "SELECT COUNT(*) RECACO FROM "
		cQuery += RetSqlName("ACO") + " ACO "
		cQuery += " WHERE "                                    
		cQuery += "ACO_FILIAL = '"+xFilial("ACO")+"' AND "
		cQuery += "ACO_CONDPG = '" + cCodigo + "' AND "
		cQuery += "ACO.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBACO",.F.,.T.)
	
		If TRBACO->RECACO > 0
			If lHelp
				a360DelMsg("ACO")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica as Regras de Bonificação                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	If lCanDel 
		cQuery := "SELECT COUNT(*) RECACQ FROM "
		cQuery += RetSqlName("ACQ") + " ACQ "
		cQuery += " WHERE "                                    
		cQuery += "ACQ_FILIAL = '"+xFilial("ACQ")+"' AND "
		cQuery += "ACQ_CONDPG = '" + cCodigo + "' AND "
		cQuery += "ACQ.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBACQ",.F.,.T.)
	
		If TRBACQ->RECACQ > 0
			If lHelp
				a360DelMsg("ACQ")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica as Regras de Negocio                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	If lCanDel 
		cQuery := "SELECT COUNT(*) RECACT FROM "
		cQuery += RetSqlName("ACT") + " ACT "
		cQuery += " WHERE "                                    
		cQuery += "ACT_FILIAL = '"+xFilial("ACT")+"' AND "
		cQuery += "ACT_CONDPG = '" + cCodigo + "' AND "
		cQuery += "ACT.D_E_L_E_T_ = ' ' "
	
		cQuery := ChangeQuery(cQuery)
	
		dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"TRBACT",.F.,.T.)
	
		If TRBACT->RECACT > 0
			If lHelp
				a360DelMsg("ACT")
			Endif	
			lCanDel := .F.
		Endif							
		
		dbCloseArea()
		dbSelectArea("SE4")
		
	Endif
			
EndIf             

RestArea(aAreaSEC)
RestArea(aAreaSE4)
RestArea(aArea)

Return(lCanDel)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A360DelMsgºAutor  ³Microsiga           ºFecha ³  01/06/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Exibe uma mensagem de aviso para os items que nao podem ser º±±
±±º          ³excluidos porque estao em outros arquivos.                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cAlias - arquivo onde o item foi encontrado.               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a360DelMsg(cAlias)
Local cMsg := ""

SX2->(MsSeek(cAlias))
cMsg:=STR0027+' "'+Lower(Alltrim(X2Nome()))+'"'+CRLF+Lower(STR0009) //"Ha itens em"
Help(" ",1,"NODELETA",,cMsg,3)
Return .F.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A360LinOk ³ Autor ³Henry Fila             ³ Data ³16.10.2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao da Linha Ok da Getdados do desmembramento das     ³±±
±±³          ³condicoes de pagamento                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica se a linha e valida                           ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo efetuar a validacao da linhaOk³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a360LinOk()

Local lRet     := .T.
Local nPosCond := Ascan(aHeader,{|x| Alltrim(x[2]) == "EC_COND"})
Local nPosTipo := Ascan(aHeader,{|x| Alltrim(x[2]) == "EC_TIPO"})
Local nPosRat  := Ascan(aHeader,{|x| Alltrim(x[2]) == "EC_RATEIO"})

If M->E4_TIPO == "B"

	If !aCols[n][Len(aHeader)+1]

		If Empty(aCols[n][nPosCond]) .Or. aCols[n][nPosRat] == 0 .Or. Empty(aCols[n][nPosTipo])
			Help(" ",1,"OBRIGAT")
			lRet := .F.
		Endif
	
	Endif	
Else
	If !aCols[n][Len(aHeader)+1]

		If !Empty(aCols[n][nPosCond]) .Or. aCols[n][nPosRat] != 0 .Or. !Empty(aCols[n][nPosTipo])
			Help(" ",1,"M360DESMEM")
			lRet := .F.
		Endif
	Endif	
Endif	

Return(lRet)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A360TudOk ³ Autor ³Henry Fila             ³ Data ³16.10.2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao da TudoOk da Getdados do desmembramento da condi  ³±±
±±³          ³cao de pagamento                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica se todos os itens sao validos                 ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo efetuar a validacao da TudoOk ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais/Distribuicao/Logistica                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a360TudOk()

Local lRet     := .T.
Local nX       := 0
Local nPercent := 0                                                 
Local nPosCond := Ascan(aHeader,{|x| Alltrim(x[2]) == "EC_COND"})
Local nPosTipo := Ascan(aHeader,{|x| Alltrim(x[2]) == "EC_TIPO"})
Local nPosRat  := Ascan(aHeader,{|x| Alltrim(x[2]) == "EC_RATEIO"})

lRet := A360Cond() //verifica o tipo e a condição de pagamento

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se a soma dos rateios informados                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If M->E4_TIPO == "B"

	For nX := 1 to Len(aCols)
		If !aCols[nX][Len(aHeader)+1]
			nPercent += aCols[nX][nPosRat]
			
			If Empty(aCols[nX][nPosCond]) .Or. Empty(aCols[nX][nPosTipo]) .Or. aCols[nX][nPosRat] == 0
				Help(" ",1,"OBRIGAT")
				lRet := .F.
			Endif	
			
		Endif		
	Next nX
	
	If nPercent <> 100
		Help(" ",1,"M360RATEIO")
		lRet := .F.
	EndIf

Else

	If M->E4_TIPO == "2" .And. Val(Subs(M->E4_CODIGO,2,1)) == 0
		Help( " ",1,"A360TIPO2" )
		lRet := .F.
	Endif

	If lRet
		For nX := 1 to Len(aCols)
			If !aCols[nX][Len(aHeader)+1] .And. (!Empty(aCols[nX][nPosCond]) .Or. aCols[nX][nPosRat] <> 0 .Or. ;
											!Empty(aCols[nX][nPosTipo]))
				Help(" ",1,"M360DESMEM")
				lRet := .F.
			Endif		
		Next nX
    Endif
    
Endif

If lRet
	If ExistBlock("MT360VLD")
		lRet := ExecBlock("MT360VLD",.F.,.F.)
	Endif
Endif	

	
Return(lRet)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A360VldTP ³ Autor ³Henry Fila             ³ Data ³16.10.2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao da condicao de pagamento informada no desmembra-  ³±±
±±³          ³mento                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica se a condicao de pagamento informada e valida ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo efetuar a validacao da condi- ³±±
±±³          ³cao de pagamento no desmembramento                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais/Distribuicao/Logistica                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a360VldTP()

Local lRet     := .T.        
Local aArea    := GetArea()
Local aAreaSE4 := SE4->(GetArea())
Local oMdl


oMdl := FwModelActive()	 
If oMdl:GetValue("SECDETAIL","EC_TIPO") == "9" 
	Help(" ",1,"M360TIPO9")
	lRet := .F.            
Endif


RestArea(aAreaSE4)
RestArea(aArea)

Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…  o  ³A360ConDes³ Autor ³ Henry Fila            ³ Data ³ 08/05/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Executa a Validacao da Condicao de Pagamento               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A360ConDes()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA360                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function A360ConDes()

Local lRet   := .T.
Local nNumEl := 0
Local aTp8   := {}
Local oMdl	 := FwModelActive()        
Local nPosTipo
Local oMdlSEC
Local cTipoCond

If ValType(oMdl) == "O"

	
	oMdlSEC := oMdl:GetModel("SECDETAIL")
	
	Begin Sequence
			
		cTipoCond := oMdlSEC:GetValue("EC_TIPO")
		
		Do Case
		Case A360CndErr( cTipoCond,oMdlSEC:GetValue("EC_COND"),@nNumEl )
			Help( " ",1,"A360TIPO" ) 
			lRet := .F.
		Case ((cTipoCond == "1") .And. (nNumEl < 01)) 
			Break( nil )
		Case ((cTipoCond == "2") .And. (nNumEl # 01)) 
			Break( nil )
		Case ((cTipoCond == "3") .And. (nNumEl < 03)) 
			Break( nil )
		Case ((cTipoCond == "4") .And. (nNumEl # 03)) 
			Break( nil )
		Case ((cTipoCond == "5") .And. (nNumEl # 03)) 
			Break( nil )
		Case ((cTipoCond == "6") .And. (nNumEl # 04)) 
			Break( nil )
		Case ((cTipoCond == "7") .And. (nNumEl # 13)) 
			Break( nil )
		Case ((cTipoCond == "8"))
			If nNumEl < 01
				Break( nil )
			Else
				aTp8:=ArrayTP8(oMdlSEC:GetValue("EC_COND"))
				If Len(aTP8) = 0
					Break( Nil )
				EndIf
			EndIf
		Case ((cTipoCond == "9") .And. (nNumEl > 01)) ; Break( nil )
		Case (cTipoCond == "A")
		EndCase
		Recover
		
		Help( " ",1,"A360TIP"+cTipoCond ) 
		lRet := .F.
		
	End Sequence
Else 
	nPosTipo := Ascan(aHeader,{|x| Alltrim(x[2]) == "EC_TIPO"})

	Begin Sequence
		
		Do Case
		Case A360CndErr( aCols[n][nPosTipo],M->EC_COND,@nNumEl )
			Help( " ",1,"A360TIPO" ) 
			lRet := .F.
		Case ((aCols[n][nPosTipo] == "1") .And. (nNumEl < 01)) 
			Break( nil )
		Case ((aCols[n][nPosTipo] == "2") .And. (nNumEl # 01)) 
			Break( nil )
		Case ((aCols[n][nPosTipo] == "3") .And. (nNumEl < 03)) 
			Break( nil )
		Case ((aCols[n][nPosTipo] == "4") .And. (nNumEl # 03)) 
			Break( nil )
		Case ((aCols[n][nPosTipo] == "5") .And. (nNumEl # 03)) 
			Break( nil )
		Case ((aCols[n][nPosTipo] == "6") .And. (nNumEl # 04)) 
			Break( nil )
		Case ((aCols[n][nPosTipo] == "7") .And. (nNumEl # 13)) 
			Break( nil )
		Case ((aCols[n][nPosTipo] == "8"))
			If nNumEl < 01
				Break( nil )
			Else
				aTp8:=ArrayTP8(M->EC_COND)
				If Len(aTP8) = 0
					Break( Nil )
				EndIf
			EndIf
		Case ((aCols[n][nPosTipo] == "9") .And. (nNumEl > 01)) ; Break( nil )
		Case (aCols[n][nPosTipo] == "A")
		EndCase
		Recover
		
		Help( " ",1,"A360TIP"+aCols[n][nPosTipo] ) 
		lRet := .F.
		
	End Sequence
EndIf
Return( lRet )



/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³a360SimCnd³ Autor ³ Henry Fila            ³ Data ³ 23.10.03  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina para simulacao de condicao de pagamento              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ a360SimCnd()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico 										           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL. 					   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³	MOTIVO DA ALTERACAO					   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function a360SimCnd()

Local aVencto  := {}
Local aArea    := GetArea()
Local aAreaSE4 := SE4->(GetArea())

Local cCodigo  := SE4->E4_CODIGO
Local cTipo    := SE4->E4_TIPO

Local lRet     := .F.

Local nValTit  := 0      
Local nValIpi  := 0      
Local nValSol  := 0      
Local nY       := 0
Local dDataRef := Ctod("//")        

Local oWizard   
Local oValTit                         
Local oValIPI
Local oValSol
Local oDataRef     
Local oVencto
    

DEFINE WIZARD oWizard TITLE OemtoAnsi(STR0010) HEADER OemtoAnsi(STR0011) ; //"Simulacao de aplicacao de condicao de pagamento"###"Dados Genericos"
		MESSAGE OemtoAnsi(STR0012)  TEXT " "; //"Informe os dados necessarios para a simulacao da condicao de pagamento."
		NEXT {|| .t.} ;
		FINISH {||.F.} PANEL NOFIRSTPANEL 

	oWizard:GetPanel(1)
	
	@ 15,20 SAY OemtoAnsi(STR0013) OF oWizard:GetPanel(1) SIZE 60,8 PIXEL  //"Data de referencia"
	@ 15,80 MSGET oDataRef VAR dDataRef OF oWizard:GetPanel(1) SIZE 60,8 PIXEL 
	
	@ 30,20 SAY OemtoAnsi(STR0014) OF oWizard:GetPanel(1) SIZE 60,8 PIXEL  //"Valor de referencia"
	@ 30,80 MSGET oValTit VAR nValTit Picture "@E 99,999,999.99" OF oWizard:GetPanel(1) SIZE 60,8 PIXEL 

	CREATE PANEL oWizard  HEADER OemtoAnsi(STR0015) MESSAGE OemtoAnsi(STR0016) ; //"Informe os valores dos impostos a serem calculados"
	 BACK {||.T.} PANEL


	oWizard:GetPanel(2)
	
	@ 15,20 SAY OemtoAnsi(STR0017) OF oWizard:GetPanel(2) SIZE 60,8 PIXEL  //"Valor do IPI"
	@ 15,80 MSGET oValIPI VAR nValIpi Picture "@E 99,999,999.99" OF oWizard:GetPanel(2) SIZE 60,8 PIXEL 
	
	@ 30,20 SAY OemtoAnsi(STR0018) OF oWizard:GetPanel(2) SIZE 70,8 PIXEL  //"Valor do Icms Solidario"
	@ 30,80 MSGET oValSol VAR nValSol Picture "@E 99,999,999.99" OF oWizard:GetPanel(2) SIZE 60,8 PIXEL 


	CREATE PANEL oWizard  HEADER OemtoAnsi(STR0019) MESSAGE " " ; //"Resumo das informacoes"
	 BACK {||.T.} FINISH {|| lRet := .T. } PANEL

	oWizard:GetPanel(3)

	@ 05,20 SAY OemtoAnsi(STR0020) OF oWizard:GetPanel(3) SIZE 60,8 PIXEL  //"Codigo da condicao "
	@ 05,80 MSGET cCodigo OF oWizard:GetPanel(3) SIZE 30,8 F3 "SE4" READONLY PIXEL 

	@ 18,20 SAY OemtoAnsi(STR0021) OF oWizard:GetPanel(3) SIZE 60,8 PIXEL  //"Tipo "
	@ 18,80 MSGET cTipo OF oWizard:GetPanel(3) SIZE 15,8 PIXEL When .F.

	@ 31,20 SAY OemtoAnsi(STR0014) OF oWizard:GetPanel(3) SIZE 60,8 PIXEL  //"Valor de referencia"
	@ 31,80 MSGET nValTit OF oWizard:GetPanel(3) Picture "@E 99,999,999.99" SIZE 60,8 PIXEL 

	@ 44,20 SAY OemtoAnsi(STR0013) OF oWizard:GetPanel(3) SIZE 60,8 PIXEL  //"Data de referencia"
	@ 44,80 MSGET dDataRef OF oWizard:GetPanel(3) SIZE 60,8 When .F. PIXEL 

	@ 57,20 SAY OemtoAnsi(STR0017) OF oWizard:GetPanel(3) SIZE 60,8 PIXEL  //"Valor do IPI"
	@ 57,80 MSGET nValIpi OF oWizard:GetPanel(3) Picture "@E 99,999,999.99" SIZE 60,8 When .F. PIXEL 

	@ 70,20 SAY OemtoAnsi(STR0018) OF oWizard:GetPanel(3) SIZE 60,8 PIXEL  
	@ 70,80 MSGET nValSol OF oWizard:GetPanel(3) Picture "@E 99,999,999.99" SIZE 60,8 When .F. PIXEL 


ACTIVATE WIZARD oWizard CENTERED  WHEN {||.T.}

If lRet
	aVencto := Condicao(nValTit,cCodigo,nValIpi,dDataRef,nValSol)                          

	For nY := 1 To Len(aVencto)
		aVencto[nY][2] := TransForm(aVencto[nY][2],PesqPict("SE1","E1_VALOR"))
	Next nY
    If Len(aVencto)>0
		DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0022) FROM 09,00 TO 28,80 //"Titulos a serem gerados de acordo com a simulacao"
	                                                                                                        
			@ 005,004 LISTBOX oVencto FIELDS TITLE OemtoAnsi(STR0023),OemtoAnsi(STR0024) SIZE 310,120 PIXEL
			oVencto:SetArray(aVencto)
			oVencto:bLine := {|| aVencto[oVencto:nAt] }
	
			DEFINE SBUTTON FROM 130 ,280 TYPE 1 PIXEL ACTION (oDlg:End()) ENABLE OF oDlg Pixel
	
		ACTIVATE MSDIALOG oDlg CENTERED
	EndIf
Endif

RestArea(aAreaSE4)
RestArea(aArea)
Return


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³a360F3    ³ Autor ³ Henry Fila            ³ Data ³ 23.10.03  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina para inclusao de condicaoo de pagamento via F3       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ a360F3()                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 3=Inclusao;2=Visualizacao                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico 										           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL. 					   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄ ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³	MOTIVO DA ALTERACAO					   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
	
Function a360F3(nTipo)	

If !IsInCallStack("CNTA100") .AND. !IsInCallStack("FINA290")
	SaveInter()
	FWExecView(,'MATA360', nTipo)
	RestInter()
Else 
	A360Man("SE4",SE4->(Recno()),nTipo)
EndIf
Return  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MT360AltOk³ Autor ³ Nunzio Autorino Junior³ Data ³ 31/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Responsavel em enviar os dados da condicao de pagamento    ³±±
±±³			 ³ para integracao    										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Cadastro de condicao de pagamento                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MT360AltOk()

Local cTipo 		:= ""				   							//Como os dados serao integrados no processo offline
Local oProcessOff 	:= Nil				   							//Objeto do tipo LJCProcessoOffLine
Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)			//Identifica se o ambiente esta operando em offline
	
//Verifica se o ambiente esta em off-line
If lAmbOffLn
	//Instancia o objeto LJCProcessoOffLine
	oProcessOff := LJCProcessoOffLine():New("013")
		
	//Determina o tipo de operacao 
	If INCLUI
		cTipo := "INSERT"
	ElseIf ALTERA
		cTipo := "UPDATE"
	Else
		cTipo := "DELETE"
			
		//Considera os registros deletados
		SET DELETED OFF
	EndIf
			    
	If !Empty(cTipo)
		//Insere os dados do processo (registro da tabela)
		oProcessOff:Inserir("SE4", xFilial("SE4") + SE4->E4_CODIGO, 1, cTipo)
		
		//Processa os dados 
		oProcessOff:Processar()
	EndIf
		
	//Desconsidera os registros deletados
	SET DELETED ON
EndIf
Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A035Int³ Autor ³ Vendas CRM               ³ Data ³ 15/09/2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Realiza integracao com a criterium ou outra integracao       ³±±
±±³          ³que utiliza o framework do SIGALOJA de integracao.            ³±±
±±³          ³ O parâmetro aIntSB1 normalmente é vazio.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A035Int()                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Momento da chamada, sendo:                             ³±±
±±³          ³           1: Antes de qualquer alteração                     ³±±
±±³          ³           2: Depois das alterações                           ³±±
±±³          ³ExpN2: Opção da rotina                                        ³±±
±±³          ³ExpA3: Array contendo o número do registro e adaptador do SE4.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A035Int( nMomento, nOpc, aIntSE4 )
Local lIntegra 		:= SuperGetMv("MV_LJGRINT", .F., .F.)	// Se há integração ou não
Local aArea			:= GetArea()

If lIntegra
	If nMomento == 1
		MsgRun( STR0029, STR0028, {|| A035IniInt( nOpc, aIntSE4 ) } ) // "Anotando registros para integração" "Aguarde"
	ElseIf nMomento == 2
		MsgRun( STR0030, STR0028, {|| A035FimInt( nOpc, aIntSE4 ) } ) // "Executando integração" "Aguarde"
	EndIf
EndIf
	
RestArea( aArea )
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A035IniInt   ³ Autor ³ Vendas CRM         ³ Data ³ 15/09/2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Faz o cache dos itens antes de serem excluídos, possibilitan-³±±
±±³          ³do o envio dos mesmos, mesmo após de serem apagados.          ³±±
±±³          ³ O parâmetro aIntSE4 normalmente é vazio.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A035IniInt()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Opção da rotina                                        ³±±
±±³          ³ExpA2: Array contendo o número do registro e adaptador do SE4.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A035IniInt( nOpc, aIntSE4 )
Local oFactory		:= LJCAdapXmlEnvFactory():New()
Local cChave		:= ""
	
	// Se houver integração e não for inclusão, anota todos os registros para exclusão, caso algum seja excluído
If nOpc != 3
	aIntSE4 :=	{ SE4->(Recno()), oFactory:Create( "SE4" ) }
	cChave 	:= xFilial( "SE4" ) + SE4->E4_CODIGO
	aIntSE4[2]:Inserir( "SE4", cChave, "1", "5" )
	aIntSE4[2]:Gerar()
EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A035FimInt   ³ Autor ³ Vendas CRM         ³ Data ³ 15/09/2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Envia os itens apagados e todos os outros itens.             ³±±
±±³          ³ O parâmetro aIntSE4 normalmente é vazio.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A035FimInt()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Opção da rotina                                        ³±±
±±³          ³ExpA2: Array contendo o número do registro e adaptador do SE4.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A035FimInt( nOpc, aIntSE4 )
Local oFactory		:= LJCAdapXmlEnvFactory():New( )	// Cria a fabrica de Adaptadores de envio
Local cChave		:= ""
	
// Verifica se houve algum registro apagado, e gera a integração desse registro
If nOpc != 3
	// Procura pelo registro do cabeçalho
	SE4->(DbGoTo( aIntSE4[1] ) )
		
	// Se não encontrar, significa que o cabeçalho foi apagado, então envia somente a exclusão do cabeçalho
	If SE4->( DELETED() )
		aIntSE4[2]:Finalizar()
	EndIf
EndIf
	
// Independente de ter registros apagados ou não, gera quando não for exclusão, todos os outros registros
If nOpc != 5
	aIntSE4 := { SE4->( Recno() ), oFactory:Create( "SE4" ) }
	cChave 	:= xFilial( "SE4" ) + SE4->E4_CODIGO
	aIntSE4[2]:Inserir( "SE4", cChave, "1", cValToChar( nOpc ) )
	aIntSE4[2]:Gerar()
	aIntSE4[2]:Finalizar()
EndIf
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³ IntegDef º Autor ³ Danilo Dias        º Data ³ 22/03/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Desc.    ³ Funcao de integracao com o adapter EAI para envio e        º±±
±±º          ³ recebimento do cadastro de condicoes de pagamento (SE4)    º±±
±±º          ³ utilizando o conceito de mensagem unica.                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Uso      ³ MATA360                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function IntegDef( cXML, nTypeTrans, cTypeMsg, cVersion, cTransaction, lJSon )
Local aRet 			:= {}

Default cXml 		:= ""
Default nTypeTrans 	:= ""
Default cTypeMessage:= ""
Default cVersion 	:= ""
Default cTransac 	:= ""
Default lJson 		:= .F.

//Apenas os tipos 1,2,4,5,6 são enviados atualmente.
//Todas as condições recebidas são criadas como tipo 1, sendo que
//o número máximo de parcelas é limitado ao tamanho do campo E4_COND
//que permite cerca de 11 parcelas no máximo.
If lJSon .And. FindFunction( "MATI360O" )
	aRet := MATI360O( cXML, nTypeTrans, cTypeMsg, cVersion, cTransaction )
Else	
	aRet := MATI360( cXML, nTypeTrans, cTypeMsg, cVersion, cTransaction )
Endif

Return aRet

/*/{Protheus.doc} FatLibMetric
Função utilizada para validar a data da LIB para ser utilizada na Telemetria
@type       Function
@author     CRM/Faturamento
@since      Maio/2021
@version    12.1.27
@return     __lMetric, lógico, se a LIB pode ser utilizada para Telemetria
/*/
Static Function FatLibMetric()

If __lMetric == Nil 
	__lMetric := FWLibVersion() >= "20210517"
EndIf

Return __lMetric
