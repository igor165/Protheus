#INCLUDE "fata350.ch"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FATA350  ³ Autor ³ Eduardo Riera         ³ Data ³11/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Implantar os titulos a pagar referente as despesas financei-³±±
±±³          ³ras com visitas     ( Reembolso de Despesas Financeiras )   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FATA350

Local aSays     := {}
Local aButtons  := {}
Local cCadastro := STR0001 //"Reembolso de despesas financeiras"
Local nOpca     := 0
Local oDlg,lReturn:=.T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                     ³
//³                                                          ³
//³ mv_par01            // Prefixo do Cta Pag ?              ³
//³ mv_par02            // Data de Vencto     ?              ³
//³ mv_par03            // Dt. da Visita   de ?              ³
//³ mv_par04            // Dt. da Visita ate  ?              ³
//³ mv_par05            // Vendedor de ?                     ³
//³ mv_par06            // Vendedor ate?                     ³
//³ mv_par07            // Contabiliza On-Line?              ³
//³ mv_par08            // Mostra Lct. Ctb ?                 ³
//³ mv_par09            // Operacao? Incluir/Excluir         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Pergunte("FTA350",.F.)
AADD(aSays,STR0002) //"    Esta rotina efetua o calculo e disponibiliza os titulos a pagar referente "
AADD(aSays,STR0003) //"ao reembolso das despesas financeira ocorridas nas visitas dos representantes "
AADD(aSays,STR0004) //"comerciais."
AADD(aButtons, { 5,.T.,{|| Pergunte("FTA350",.T. ) } } )
AADD(aButtons, { 1,.T.,{|o| nOpcA:= 1,o:oWnd:End()} })
AADD(aButtons, { 2,.T.,{|o| nOpcA:= 0,o:oWnd:End()} })

FormBatch( cCadastro, aSays, aButtons )

If nOpca == 1 .And. lReturn
	Processa({|lEnd| FtDespFin(@lEnd,;
		MV_PAR01,;
		MV_PAR02,;
		MV_PAR03,;
		MV_PAR04,;
		MV_PAR05,;
		MV_PAR06,;
		MV_PAR07==1,;
		MV_PAR08==1,;
		MV_PAR09==2) },,,.T.)
EndIf
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FtDespFin ³ Autor ³ Eduardo Riera         ³ Data ³10/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de calculo do reembolso de despesas financeiras      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpN1 := FtDespFin()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Flag de cancelamento do usuario                      ³±±
±±³          ³ExpC2: Prefixo do Titulo a pagar                            ³±±
±±³          ³ExpD3: Data de Vencto                                       ³±±
±±³          ³ExpD4: Data de visita inicial                               ³±±
±±³          ³ExpD5: Data de visita final                                 ³±±
±±³          ³ExpC6: Vendedor Inicial                                     ³±±
±±³          ³ExpC7: Vendedor Final                                       ³±±
±±³          ³ExpL8: Contabiliza On-Line                                  ³±±
±±³          ³ExpL9: Mostra Lcto Contabil                                 ³±±
±±³          ³ExpLA: Exclui titulos gerados                               ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³04/04/07  |Conrado Q      ³BOPS 122711: Alterada a utilização da cham. ³±±
±±³          ³               ³SubStr(cUsuario,7,15) por cUserName         ³±±
±±³13/08/2007³Norbert Waage  ³BOPS 130581: Criacao do ponto de entrada    ³±±
±±³          ³               ³FT350DSP, executado ao final da gravacao da ³±±
±±³          ³               ³despesa financeira.                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtDespFin(lEnd,cPrefixo,dVencto,dInicio,dFim,cCodVenI,cCodVenF,lCtbOnLine,lMostra,lExclui)

Local aArea     := GetArea()
Local aAreaSX3  := SX3->( GetArea() ) 	
Local aRegAD6   := {}
Local cQuery    := ""
Local cQuebra   := ""
Local cKey      := "AD5_FILIAL+AD5_VEND"
Local cAliasAD5 := "AD5"
Local cAliasAD6 := "AD6"
Local cArqInd   := ""
Local cLoteCtb  := ""
Local cArqCtb   := ""
Local cPadrao   := ""
Local nValor    := 0
Local nTotalCtb := 0
Local nHdlPrv   := 0
Local nX        := 0
Local nRecAD6   := 0
Local nRegAD6   := 0
Local nTitProc  := 0
Local nTitNoProc:= 0
Local nVlrProc  := 0
Local lCalcAD6	:= .T.
Local lFT350Fil := ExistBlock("FT350FIL")
Local lFT350Dsp	:= ExistBlock("FT350DSP")
Local lValido   := .T.
Local lPadrao   := .F.
Local lGrava	:= .T.
Local oDlg
Local oBmp
Local oBold
Local oBut                  
Local aDiasCTB	 :={}
Local cCodDiario := ""

DEFAULT lExclui := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se ha contabilizacao                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPadrao    := IIf(!lExclui,"510","515")
lPadrao   := VerPadrao(cPadrao)
lCtbOnLine := lPadrao .And. ( lCtbOnLine .Or. lExclui )

If lCtbOnLine 
	cCodDiario := CtbaVerDia()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o numero do lote contabil                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DBSelectArea("SX5")
DBSetOrder(1)
If MsSeek(xFilial()+"09TEC")
	cLoteCtb := AllTrim(X5Descri())
Else
	cLoteCtb := "FAT "
EndIf		
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa um execblock                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If At(UPPER("EXEC"),X5Descri()) > 0
	cLoteCtb := &(X5Descri())
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se as despesas financeiras devem ser contabilizadas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCtbOnLine
	nHdlPrv := HeadProva(cLoteCtb,"FATA350",Substr(cUserName,1,6),@cArqCtb)
	If nHdlPrv <= 0
		lCtbOnLine := .F.
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a operacao a ser executada                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lExclui

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Selecao dos registros validos para o processamento           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cAliasAD5 := "AtDespFin"
	cAliasAD6 := "AtDespFin"

	cQuery := "SELECT AD5.AD5_FILIAL,AD5.AD5_VEND,AD6.AD6_TOTAL,AD6.R_E_C_N_O_ AD6RECNO "
	cQuery += "FROM "+RetSqlName("AD5")+" AD5,"
	cQuery += RetSQLName("AD6")+" AD6 "
	cQuery += "WHERE AD5.AD5_FILIAL='"+xFilial("AD5")+"' AND "
	cQuery += "AD5.AD5_VEND>='"+cCodVenI+"' AND "
	cQuery += "AD5.AD5_VEND<='"+cCodVenF+"' AND "
	cQuery += "AD5.AD5_DATA>='"+Dtos(dInicio)+"' AND "
	cQuery += "AD5.AD5_DATA<='"+Dtos(dFim)+"' AND "	
	cQuery += "AD5.D_E_L_E_T_=' ' AND "
	cQuery += "AD6.AD6_FILIAL='"+xFilial("AD6")+"' AND "
	cQuery += "AD6.AD6_VEND=AD5.AD5_VEND AND "
	cQuery += "AD6.AD6_DATA=AD5.AD5_DATA AND "
	cQuery += "AD6.AD6_SEQUEN=AD5.AD5_SEQUEN AND "
	cQuery += "AD6.AD6_NUMERO='"+Space(Len(AD6->AD6_NUMERO))+"' AND "
	cQuery += "AD6.D_E_L_E_T_=' ' "
	cQuery += "ORDER BY "+SqlOrder(cKey)

	cQuery := ChangeQuery(cQuery)

	DBUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAD5,.T.,.T.)

	SX3->( DBSetOrder( 2 ) )
	SX3->(MsSeek("AD6_TOTAL"))
	TcSetField(cAliasAD6,"AD6_TOTAL","N",SX3->X3_TAMANHO,SX3->X3_DECIMAL)

	ProcRegua(AD5->(LastRec()))
	
	While ( (cAliasAD5)->( !Eof() ) .And. (cAliasAD5)->AD5_FILIAL == xFilial("AD5") )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula o valor da despesa financeira                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lFT350Fil
			lCalcAD6 := ExecBlock("FT350FIL",.F.,.F.,(cAliasAD6)->AD6RECNO)
		EndIf
		If lCalcAD6
			nValor  += (cAliasAD6)->AD6_TOTAL
			aadd(aRegAD6,(cAliasAD6)->AD6RECNO)
		Else
			lCalcAD6:= .T.
		EndIf
		
		cQuebra := (cAliasAD5)->AD5_VEND
		IncProc(RetTitle("AD5_VEND")+": "+(cAliasAD5)->AD5_VEND)		
		
		(cAliasAD5)->( DBSkip() ) 
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica a quebra de Tecnico                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (cAliasAD5)->( Eof() ) .Or. cQuebra <> (cAliasAD5)->AD5_VEND
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Gerar titulo financeiro                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nValor > 0
				
				Begin Transaction 

					SA3->( DBSetOrder(1) ) 

					lGrava := SA3->( MsSeek(xFilial("SA3")+cQuebra) ) .And. !Empty(SA3->A3_FORNECE) 
					
					If lGrava
						FaDespFin(SA3->A3_FORNECE,SA3->A3_LOJA,nValor,cPrefixo,Nil,Nil,Nil,Nil,dVencto,IIf(lCtbOnLine,{|| nTotalCtb+=DetProva(nHdlPrv,cPadrao,"FATA350",cLoteCtb)},),"FATA350")
						If ( UsaSeqCor() ) 
			              aAdd(aDiasCTB,{"SE2",SE2->(RECNO()),cCodDiario,"E2_NODIA","E2_DIACTB"})	                          
		    			EndIf
						nTitProc++
						nVlrProc += nValor
						For nX := 1 To Len(aRegAD6)
							AD6->(MsGoto(aRegAD6[nX])) 
							RecLock("AD6")
							AD6->AD6_PREFIX := SE2->E2_PREFIXO
							AD6->AD6_NUMERO := SE2->E2_NUM
							AD6->AD6_PARCEL := SE2->E2_PARCELA
							AD6->AD6_TIPO   := SE2->E2_TIPO
							AD6->AD6_CODFOR := SE2->E2_FORNECE
							AD6->AD6_LOJFOR := SE2->E2_LOJA
							MsUnLock() 
						Next nX
					EndIf
				End Transaction
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Ponto de entrada FT350DSP executado apos a gravacao do SE2³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lGrava .AND. lFT350Dsp
					ExecBlock("FT350DSP", .F., .F., { SE2->(Recno()) } )
				EndIf
				
			EndIf
			
			nValor  := 0 
			aRegAD6 := {}
			
		EndIf
		 
		If lEnd
			Exit
		EndIf
	
	EndDo
	
Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Selecao dos registros validos para o processamento           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
	cAliasAD5 := "AtDespFin"
	cAliasAD6 := "AtDespFin"

	cQuery := "SELECT DISTINCT AD5.AD5_FILIAL,AD5.AD5_VEND,AD6.AD6_PREFIX,AD6.AD6_NUMERO,AD6.AD6_PARCEL,AD6.AD6_TIPO,AD6.AD6_CODFOR,AD6.AD6_LOJFOR "
	cQuery += "FROM "+RetSqlName("AD5")+" AD5,"
	cQuery += RetSQLName("AD6")+" AD6 "
	cQuery += "WHERE AD5.AD5_FILIAL='"+xFilial("AD5")+"' AND "
	cQuery += "AD5.AD5_VEND>='"+cCodVenI+"' AND "
	cQuery += "AD5.AD5_VEND<='"+cCodVenF+"' AND "
	cQuery += "AD5.AD5_DATA>='"+Dtos(dInicio)+"' AND "
	cQuery += "AD5.AD5_DATA<='"+Dtos(dFim)+"' AND "	
	cQuery += "AD5.D_E_L_E_T_=' ' AND "
	cQuery += "AD6.AD6_FILIAL='"+xFilial("AD6")+"' AND "
	cQuery += "AD6.AD6_VEND=AD5.AD5_VEND AND "
	cQuery += "AD6.AD6_DATA=AD5.AD5_DATA AND "
	cQuery += "AD6.AD6_SEQUEN=AD5.AD5_SEQUEN AND "
	cQuery += "AD6.AD6_NUMERO<>'"+Space(Len(AD6->AD6_NUMERO))+"' AND "
	cQuery += "AD6.D_E_L_E_T_=' ' "
	cQuery += "GROUP BY AD5.AD5_FILIAL,AD5.AD5_VEND,AD6.AD6_PREFIX,AD6.AD6_NUMERO,AD6.AD6_PARCEL,AD6.AD6_TIPO,AD6.AD6_CODFOR,AD6.AD6_LOJFOR"

	cQuery := ChangeQuery(cQuery)

	DBUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAD5,.T.,.T.)
	
	ProcRegua(AD5->(LastRec()))
	
	While ( (cAliasAD5)->( !Eof() ) .And. (cAliasAD5)->AD5_FILIAL == xFilial("AD5") )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se a despesa financeira pode ser excluida           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SE2->( DBSetOrder( 1 ) )   
		
		If SE2->( MsSeek(xFilial("SE2")+(cAliasAD6)->AD6_PREFIX+(cAliasAD6)->AD6_NUMERO+(cAliasAD6)->AD6_PARCEL+(cAliasAD6)->AD6_TIPO+(cAliasAD6)->AD6_CODFOR+(cAliasAD6)->AD6_LOJFOR) )
		
			If FaCanDelCP("SE2","FATA350",.F.)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Exclui a despesa financeira                                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Begin Transaction
					
					AD6->( DBSetOrder( 2 ) )
					
					If AD6->( MsSeek(xFilial("AD6")+SE2->E2_PREFIXO+SE2->E2_NUM+SE2->E2_PARCELA+SE2->E2_TIPO+SE2->E2_FORNECE+SE2->E2_LOJA) )
						
						While AD6->( !Eof() ) .And. AD6->AD6_FILIAL == xFilial("AD6") .And.;
								AD6->AD6_PREFIX == SE2->E2_PREFIXO .And.;
								AD6->AD6_NUMERO == SE2->E2_NUM .And.;
								AD6->AD6_PARCEL == SE2->E2_PARCELA .And.;
								AD6->AD6_TIPO   == SE2->E2_TIPO .And.;
								AD6->AD6_CODFOR == SE2->E2_FORNECE .And.;
								AD6->AD6_LOJFOR == SE2->E2_LOJA

						
							AD6->( DBSkip() )
							nRegAD6 := AD6->( RecNo() )
							AD6->( DBSkip(-1) )

							RecLock("AD6")
							AD6->AD6_PREFIX := ""
							AD6->AD6_NUMERO := ""
							AD6->AD6_PARCEL := ""
							AD6->AD6_TIPO   := ""
							AD6->AD6_CODFOR := ""
							AD6->AD6_LOJFOR := ""
							MsUnLock()

							AD6->( DBGoto(nRegAD6) )
							
						EndDo
						
					EndIf
					
					If lCtbOnLine .And. "S"$SE2->E2_LA
						nTotalCtb+=DetProva(nHdlPrv,cPadrao,"FATA350",cLoteCtb)							
						If ( UsaSeqCor() ) 
      						aAdd(aDiasCTB,{"SE2",SE2->(RECNO()),cCodDiario,"E2_NODIA","E2_DIACTB"})	                          
						EndIf
					EndIf
				
					RecLock("SE2")
					FaAvalSE2(2,"FATA350")
					FaAvalSE2(3,"FATA350")
					SE2->(DBDelete())
					MsUnLock()				
				
				End Transaction
				
				nTitProc++
				nVlrProc += SE2->E2_VALOR
			
			Else
				nTitNoProc++
			EndIf
			
		EndIf
		
		IncProc(RetTitle("AD5_VEND")+": "+(cAliasAD5)->AD5_VEND)
		
		(cAliasAD5)->( DBSkip() )		
		
		If lEnd
			Exit
		EndIf
		
	EndDo
 
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contabilizacao das despesas financeiras                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCtbOnLine
	RodaProva(nHdlPrv,nTotalCtb)
	If nTotalCtb > 0
		PRIVATE INCLUI := .T.
		nTotalCtb := 0
		cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lMostra,.F.,,,,,,aDiasCTB)
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Exibe o resumo                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlg TITLE STR0005 FROM 9,0 TO 25,50 OF oMainWnd //"Resumo"
	@ 00,00 BITMAP oBmp RESNAME "LOGIN" oF oDlg SIZE 30, 120 NOBORDER WHEN .F. PIXEL
	DEFINE FONT oBold NAME "Arial" SIZE 0, -12 BOLD
	@ 08,38 SAY STR0006 FONT oBold PIXEL //"Resumo do processamento das despesas financeiras "
	@ 24,30 TO 26 ,400 LABEL '' OF oDlg   PIXEL
	@ 45,38 SAY STR0007 PIXEL OF oDlg //"Titulos processados: "
	@ 55,38 SAY STR0008 PIXEL OF oDlg //"Valor dos titulos  : "
	If lExclui
		@ 65,38 SAY STR0009 PIXEL OF oDlg //"Nao Estornados     :"
	EndIf
	@ 45,110 SAY nTitProc  SIZE 80, 10 PIXEL   RIGHT
	@ 55,110 SAY Transform( nVlrProc, "@e 999,999,999.99" ) SIZE 80, 10 PIXEL   RIGHT
	If lExclui
		@ 65,110 SAY nTitNoProc SIZE 80, 10 PIXEL   RIGHT
	EndIf
	@ 90,030 TO 92 ,400 LABEL '' OF oDlg PIXEL  	
	DEFINE SBUTTON oBut FROM 98,162  TYPE 1 ACTION ( oDlg:End() ) ENABLE of oDlg
ACTIVATE MSDIALOG oDlg CENTERED

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integriade da rotina                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
(cAliasAD5)->( DBCloseArea() )

RestArea( aAreaSX3 )
RestArea( aArea )

Return(.T.)