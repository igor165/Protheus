#INCLUDE "PROTHEUS.CH"
#INCLUDE "QUICKSEARCH.CH"
#INCLUDE "TECQ013.CH"

QSSTRUCT TECQ013 DESCRIPTION STR0001 MODULE 28 // "Postos descobertos para o apontamento de Materiais de Implantação" 

QSMETHOD INIT QSSTRUCT TECQ013
Local cWhere := ""

	::SetPKOrder('TFL', 3)

	QSTABLE "TFG" JOIN "TFF" ON "TFF.TFF_COD = TFG.TFG_CODPAI"
	QSTABLE "TFF" JOIN "TFL" ON "TFL.TFL_CODIGO = TFF.TFF_CODPAI"
	QSTABLE "TFL" JOIN "CN9" ON "CN9.CN9_NUMERO = TFL.TFL_CONTRT AND CN9.CN9_REVISA = TFL.TFL_CONREV"
	QSTABLE "CN9" JOIN "CNC" ON "CNC.CNC_NUMERO = CN9.CN9_NUMERO AND CNC.CNC_REVISA = CN9.CN9_REVISA"
	QSTABLE "TFG" LEFT JOIN "TFS" ON "TFS.TFS_CODTFG = TFG.TFG_COD"
	QSTABLE "TFS" LEFT JOIN "SB1" ON "SB1.B1_COD = TFS.TFS_PRODUT"
	QSTABLE "TFL" LEFT JOIN "ABS" ON "ABS.ABS_LOCAL = TFL.TFL_LOCAL"

	::RemovePrimaryKey({'CN9','CNC','TFS'}) // remove do GROUP BY
	
	// campos do SX3 e indices do SIX
	QSPARENTFIELD "TFL_LOCAL" INDEX ORDER 3 
	QSPARENTFIELD "TFG_PRODUT" INDEX ORDER 4
	
	// campos do SX3
	QSFIELD "TFL_LOCAL","ABS_DESCRI","TFG_PRODUT","B1_DESC"
	
	QSFIELD "PREVISTO" EXPRESSION "SUM(TFG.TFG_QTDVEN)" LABEL STR0007 FIELDS "TFG_QTDVEN" GROUP BY TYPE "N" SIZE 14 // "Qtde prevista"
	QSFIELD "APONTADO" EXPRESSION "COALESCE(SUM(TFS.TFS_QUANT),0)" LABEL STR0008 FIELDS "TFS_QUANT" GROUP BY TYPE "N" SIZE 14 // "Qtde alocada"

	QSACTION MENUDEF "TECA160" OPERATION 1 LABEL STR0002 // "Visualizar Posto"
	
	cWhere := "CN9.CN9_SITUAC = '05' AND (" +;
				"( TFG.TFG_PERINI <= '{1}' AND TFG.TFG_PERFIM >= '{2}' )" +; 
				"OR ( TFG.TFG_PERINI BETWEEN '{1}' AND '{2}' )" +;
				"OR ( TFG.TFG_PERFIM BETWEEN '{1}' AND '{2}' )" +;
				")"
	
	QSFILTER STR0003 WHERE StrTran( StrTran(cWhere, "{1}", DTOS(Date())), "{2}", DTOS(Date())) ;// "Hoje" 
	HAVING "TFG_QTDVEN > COALESCE(SUM(TFS.TFS_QUANT),0)"
	
	QSFILTER STR0004 WHERE StrTran( StrTran(cWhere, "{1}", DTOS(Date() - 7)), "{2}", DTOS(Date())) ;// "Últimos 7 dias" 
	HAVING "TFG_QTDVEN > COALESCE(SUM(TFS.TFS_QUANT),0)"
	
	QSFILTER STR0005 WHERE StrTran( StrTran(cWhere, "{1}", DTOS(Date() - 15)), "{2}", DTOS(Date())) ;// "Últimos 15 dias" 
	HAVING "TFG_QTDVEN > COALESCE(SUM(TFS.TFS_QUANT),0)"
	
	QSFILTER STR0006 WHERE StrTran( StrTran(cWhere, "{1}", DTOS(Date())), "{2}", DTOS(Date() + 7)) ;// "Próximos 7 dias"
	HAVING "TFG_QTDVEN > COALESCE(SUM(TFS.TFS_QUANT),0)"
	
	QSFILTER STR0009 WHERE StrTran( StrTran(cWhere, "{1}", DTOS(Date())), "{2}", DTOS(Date() + 15)) ;// "Próximos 15 dias"
	HAVING "TFG_QTDVEN > COALESCE(SUM(TFS.TFS_QUANT),0)"
Return
