#INCLUDE "PROTHEUS.CH"
#INCLUDE "QUICKSEARCH.CH"
#INCLUDE "TECQ015.CH"

QSSTRUCT TECQ015 DESCRIPTION STR0001 MODULE 28 // "Contratos a vencer com saldo de recursos humanos" 

QSMETHOD INIT QSSTRUCT TECQ015
Local cWhere := ""

	QSTABLE "CN9" JOIN "TFJ" ON "TFJ.TFJ_CONTRT = CN9.CN9_NUMERO AND TFJ.TFJ_CONREV = CN9.CN9_REVISA"
	QSTABLE "CN9" JOIN "CNC" ON "CNC.CNC_NUMERO = CN9.CN9_NUMERO AND CNC.CNC_REVISA = CN9.CN9_REVISA"
	QSTABLE "CN9" JOIN "ABQ" ON "ABQ.ABQ_CONTRT = CN9.CN9_NUMERO AND ABQ.ABQ_SALDO > 0"
	QSTABLE "CN9" JOIN "TFF" ON "TFF.TFF_CONTRT = CN9.CN9_NUMERO AND TFF.TFF_CONREV = CN9.CN9_REVISA"
	QSTABLE "CN9" JOIN "CN1" ON "CN1.CN1_CODIGO = CN9.CN9_TPCTO"
	QSTABLE "TFF" JOIN "SB1" ON "SB1.B1_COD = TFF.TFF_PRODUT" 
	
	// campos do SX3 e indices do SIX
	QSPARENTFIELD "CN9_NUMERO" INDEX ORDER 1
	QSPARENTFIELD "TFF_PRODUT" INDEX ORDER 4 
	
	// campos do SX3
	QSFIELD "CN9_NUMERO","CN9_DTFIM","B1_DESC","CN9_SALDO" 
	QSFIELD SUM "ABQ_SALDO"
	
	cWhere := "CN9.CN9_SITUAC = '05' AND CN9.CN9_DTFIM >= '"+ DTOS(Date())+"' AND CN9.CN9_DTFIM <= '{1}'"
	
	QSFILTER STR0002 WHERE "CN9.CN9_SITUAC = '05' AND CN9.CN9_DTFIM = '"+ DTOS(Date()) +"'" // "Hoje" 
	QSFILTER STR0003 WHERE StrTran(cWhere, "{1}", DTOS(Date() + 7)) // "Próximos 7 dias" 
	QSFILTER STR0004 WHERE StrTran(cWhere, "{1}", DTOS(Date() + 30)) // "Próximos 30 dias" 
	QSFILTER STR0005 WHERE StrTran(cWhere, "{1}", DTOS(Date() + 60)) // "Próximos 60 dias"
Return
