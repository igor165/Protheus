#INCLUDE "PROTHEUS.CH"
#INCLUDE "QUICKSEARCH.CH"
#INCLUDE "TECQ018.CH"

QSSTRUCT TECQ018 DESCRIPTION STR0001 MODULE 28 // "Contratos a vencer com saldo de locação de equipamento" 

QSMETHOD INIT QSSTRUCT TECQ018
Local cWhere := ""

	QSTABLE "CN9" JOIN "TFJ" ON "TFJ.TFJ_CONTRT = CN9.CN9_NUMERO AND TFJ.TFJ_CONREV = CN9.CN9_REVISA"
	QSTABLE "CN9" JOIN "CNC" ON "CNC.CNC_NUMERO = CN9.CN9_NUMERO AND CNC.CNC_REVISA = CN9.CN9_REVISA"
	QSTABLE "CN9" JOIN "TFL" ON "TFL.TFL_CONTRT = CN9.CN9_NUMERO AND TFL.TFL_CONREV = CN9.CN9_REVISA" 
	QSTABLE "TFL" JOIN "TFI" ON "TFI.TFI_CODPAI = TFL.TFL_CODIGO"
	QSTABLE "TFI" JOIN "TEV" ON "TEV.TEV_CODLOC = TFI.TFI_COD AND TEV.TEV_SLD > 0"
	QSTABLE "CN9" LEFT JOIN "CN1" ON "CN1.CN1_CODIGO = CN9.CN9_TPCTO" 
	QSTABLE "TFI" LEFT JOIN "SB1" ON "SB1.B1_COD = TFI.TFI_PRODUT"
	
	// campos do SX3 e indices do SIX
	QSPARENTFIELD "CN9_NUMERO" INDEX ORDER 1
	QSPARENTFIELD "TFI_PRODUT" INDEX ORDER 4
	
	// campos do SX3
	QSFIELD "CN9_NUMERO","CN9_DTFIM","B1_DESC","TEV_MODCOB"
	QSFIELD SUM "TEV_SLD"
	
	cWhere := "CN9.CN9_SITUAC = '05' AND CN9.CN9_DTFIM BETWEEN '"+ DTOS(Date()) +"' AND '{1}'"
	
	QSFILTER STR0002 WHERE "CN9.CN9_SITUAC='05' AND CN9.CN9_DTFIM='"+ DTOS(Date()) +"'" // "Hoje" 
	QSFILTER STR0003 WHERE StrTran(cWhere, "{1}", DTOS(Date() + 7)) // "Próximos 7 dias" 
	QSFILTER STR0004 WHERE StrTran(cWhere, "{1}", DTOS(Date() + 30)) // "Próximos 30 dias" 
	QSFILTER STR0005 WHERE StrTran(cWhere, "{1}", DTOS(Date() + 60)) // "Próximos 60 dias"
Return
