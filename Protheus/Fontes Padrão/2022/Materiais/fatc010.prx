#INCLUDE "FATC010.CH" 
#INCLUDE "PROTHEUS.CH" 
#INCLUDE "FWMVCDEF.CH"      
#INCLUDE "FATC010.CH"  
#include "msgraphi.ch"

/*


Ŀ
Program    FATC010   Autor  Sergio Silveira        Data 05/02/2001
Ĵ
Descrio  Pipeline - Administracao de vendas                         
Ĵ       
Retorno    Nenhum                                                     
Ĵ
Parametros                                                            
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Function FATC010()

PRIVATE cCadastro := OemToAnsi( STR0001 )  //"Pipeline - AFV"
PRIVATE aRotina   := MenuDef()

mBrowse( 6, 1,22,75,"AC1", , , , , 2 )       

Return( .T. ) 

/*


Ŀ
Program   Ftc010Pipe Autor  Sergio Silveira        Data 07/02/2001
Ĵ
Descrio  Pipeline - Administracao de vendas                         
Ĵ       
Retorno    Nenhum                                                     
Ĵ
Parametros                                                            
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Function Ftc010Pipe( cAlias, nRecNo, nOpc, xFil, aMens, cRepres ) 

Local aArea	      := GetArea()
Local aPosObj     := {} 
Local aObjects    := {}                        
Local aSize       := MsAdvSize( .F. ) 
Local aItensAC2   := {} 
Local aLegenda    := {}
Local aUsButtons  := {} 
LOCAL aCbx := { STR0016, STR0017, STR0018, STR0019, STR0020, STR0021,; //"Linha"###"rea"###"Pontos"###"Barras"###"Piramide"###"Cilindro"
			 STR0022, STR0023, STR0024,; //"Barras / Horizontal"###"Piramides / Horizontal"###"Cilindros / Horizontal"
			 STR0025 } //"Pizza"###
			 
LOCAL cCbx := ""

Local cTitulo     := STR0004 + Capital( AC1->AC1_DESCRI ) //"Pipeline - "

Local lPergunte   := .T.

Local nLinIni     := 0 
Local nRight      := 0 
Local nModelo     := 1 
Local nLoop       := 0 

Local oDlg
Local oGraph
Local oListBox  
Local oMenu 
Local oBold
Local oCbx             
Local oBut1 

PRIVATE nSerie 
                
lPergunte := If( Valtype( aMens ) == "A", .T., Pergunte( "FTC010", .T., STR0005 ) )  //"Parametros do Pipeline"

If lPergunte // "Parametros de filtro - Service Tracker"  
	
	DEFINE MSDIALOG oDlg TITLE STR0039 FROM 09,0 TO 20,40 OF oMainWnd //"Modelo de Grafico"
	
	DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD
	
	@  0, 0 BITMAP oBmp RESNAME "LOGIN" oF oDlg SIZE 40, 1000 NOBORDER WHEN .F. PIXEL 
	
	@ 03, 50 SAY STR0039 FONT oBold PIXEL  //"Modelo de Grafico"
	
	@ 14, 40 TO 16 ,400 LABEL '' OF oDlg   PIXEL
	
	@ 030, 50 SAY STR0038 of oDlg PIXEL  //"Modelo"
	
	@ 039, 50 MSCOMBOBOX oCbx VAR cCbx ITEMS aCbx SIZE 061, 65 OF oDlg PIXEL ON CHANGE nModelo := oCbx:nAt 
		                                                                                        
	DEFINE SBUTTON oBut1 FROM 062, 120 TYPE 1 ACTION ( nOpca := 1, oDlg:End() )  ENABLE of oDlg		 

	ACTIVATE MSDIALOG oDlg CENTERED  

	//Ŀ
	//Constroi a Janela da Consulta                                           
	//

	//Ŀ
	// Efetua os calculos de Auto Size                                        
	//
	aObjects := {} 
	AAdd( aObjects, { 100, 100, .T., .T., .T. } )
	AAdd( aObjects, {  94, 100, .F., .T. } )
	
	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
	aPosObj := MsObjSize( aInfo, aObjects, , .T. )
                              
	//Ŀ
	// Efetua os calculos de Auto Size ( direita da tela )                    
	//

	aObjects := {}                                
	AAdd( aObjects, { 100,  10, .T., .F. } )	
	AAdd( aObjects, { 100, 100, .T., .T., .T. } )
	AAdd( aObjects, { 100, 100, .T., .T., .T. } )	
	AAdd( aObjects, { 100,  35, .T., .F. } )
	
	aInfo    := { aPosObj[ 2, 2 ], aPosObj[ 2, 1 ], aPosObj[ 2, 4 ], aPosObj[ 2, 3 ], 3, 3 } 
	aPosObj2 := MsObjSize( aInfo, aObjects )
	
	//Ŀ
	// Ponto de entrada para a criacao de botoes de usuario                
	//
	If ExistBlock( "FC010BUT" ) 
		aUsButtons := AClone( ExecBlock( "FC010BUT", .F., .F. ) ) 
	EndIf 
	
	DEFINE MSDIALOG oDlg FROM aSize[7],00 TO aSize[6],aSize[5] TITLE cTitulo OF oMainWnd PIXEL
	                               
	nLinIni := aPosObj[2,1] 
	nRight  := aSize[5]/2
	
	@ aPosObj[2,1],aPosObj[2,2] TO aPosObj[2,3],aPosObj[2,4] OF oDlg PIXEL	 //"Mensagem"
	
	//Ŀ
	// Cria o objeto grafico                                                  
	//
	@ aPosObj[1,1], aPosObj[1,2] MSGRAPHIC oGraph SIZE aPosObj[1,3], aPosObj[1,4] OF oDlg 
	
	oGraph:SetMargins( 5, 5, 5, 5 )
	oGraph:SetGradient( GDBOTTOMTOP, CLR_HGRAY, CLR_WHITE )
	                                           
	Ftc010Disp( @oGraph, cRepres, @aItensAC2, nModelo ) 
	
	cLegenda := STR0006    //"Legenda"
	
	aLegenda := {}
	AEval( aItensAC2, { |x| AAdd( aLegenda, x[1] ) } )

	@ aPosObj2[1,1]+2,aPosObj2[1,2] SAY STR0006 OF oDlg PIXEL  //"Legenda"
	@ aPosObj2[2,1]  ,aPosObj2[2,2] LISTBOX oListBox VAR cLegenda ITEMS aLegenda PIXEL SIZE aPosObj2[2,3], aPosObj2[2,4] OF oDlg MULTI
	
	oScroll := TScrollBox():New( oDlg, aPosObj2[3,1], aPosObj2[3,2], aPosObj2[3,4],aPosObj2[3,3])
	
	DEFINE SBUTTON FROM aPosObj[2,3] - 17,nRight - 66 TYPE 5 DISABLE OF oDlg ACTION Ftc010Par( @oGraph, @oListBox, cRepres, @aItensAC2, nModelo )
	DEFINE SBUTTON FROM aPosObj[2,3] - 17,nRight - 36 TYPE 1 ENABLE OF oDlg ACTION ( oDlg:End() )
	
	Ftc010Remote( oDlg, oGRaph, oScroll )
	
	//Ŀ
	// Efetua a criacao dos botoes de usuario                              
	//
	If ValType( aUsButtons ) == "A"
		If !Empty( aUsButtons ) 
			For nLoop := 1 To Len( aUsButtons ) 
				TButton():New( aPosObj[2,3] - 31, nRight - 07 - nLoop * 29,OemToAnsi(AllTrim(aUsButtons[nLoop,1])), oDlg,aUsButtons[ nLoop, 2],28,12,,oDlg:oFont,.F.,.T.,.F.,,.F.,,,.F.)
			Next nLoop 
		EndIf	
	EndIf
	            
	oListBox:bRClicked  := { |o,nX,nY| oMenu:Activate( nX, nY, oListBox ) } // Posio x,y em relao a Dialog 

	//Ŀ
	// Define os itens do Menu PopUp                                          
	//
	MENU oMenu POPUP 
		MENUITEM STR0007   Action Ftc010Etp( oListBox, aItensAC2 )   //"Visualiza etapa"
		MENUITEM STR0008     Action FTC010Opor( oListBox, aItensAC2, cRepres )  //"Oportunidades"
	ENDMENU
	
	ACTIVATE MSDIALOG oDlg 

EndIf
	
//Ŀ
//Restaura a situacao de entrada                                          
//
RestArea( aArea ) 

Return(Nil)

/*


Ŀ
Program   Ftc010Col  Autor  Sergio Silveira        Data 08/02/2001
Ĵ
Descrio  Montagem das colunas                                       
Ĵ       
Sintaxe    Ftc010Col( ExpO1, [ ExpC1 ], ExpA1 )                       
Ĵ       
Retorno    Nenhum                                                     
Ĵ
Parametros ExpO1 -> Objeto grafico                                    
           ExpC1 -> Representante                                     
           ExpA1 -> Array da legenda                                  
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Function Ftc010Col( oGraph, cRepres, aItensAC2, aColors, nModelo ) 

Local aGrupos		:= {}
Local aDadosGRF		:= {} 
Local aFilBlocks	:= Array( 8 ) 
             
Local cSeekAC2		:= ""
Local cSeekAD1		:= "" 
Local cFilGrupo		:= ""
Local cQuery		:= ""

Local lPercent		:= ( MV_PAR02 == 2 )
Local lFirst		:= .T.
                                      
Local nQuant		:= 0 
Local nValor		:= 0 
Local nTotQuant		:= 0 
Local nTotValor		:= 0 
Local nTotMedia		:= 0 
Local nAltura		:= 0 
Local nColor		:= 1
Local nRelAcumul	:= 0
Local nRecnoAD1		:= 0  
Local nSoma			:= 0
Local nLoop			:= 0 

DEFAULT nModelo    := 0 

aItensAC2 := {}
AFill( aFilBlocks, { || .T. } ) 
                                   
cRepres := If( ValType( cRepres ) == "C", cRepres, "" ) 

AC2->( dbSetOrder( 1 ) ) 

cSeekAC2 := xFilial( "AC2" ) + AC1->AC1_PROVEN 
AC2->( dbSeek( cSeekAC2 + MV_PAR05, .T. ) )                 
	                  
aDadosGRF := {}        
nTotValor := 0 
nTotQuant := 0 
nTotMedia := 0 
	
While !AC2->( Eof() ) .And. cSeekAC2 == AC2->AC2_FILIAL + AC2->AC2_PROVEN .And. AC2->AC2_STAGE <= MV_PAR06   

	nQuant     := 0 
	nValor     := 0 
	
	nRelAcumul += ( AC2->AC2_RELEVA ) / 100
	
	Ftc010Stage( cRepres, @nQuant, @nValor, @nTotValor, @nTotQuant, @nTotMedia, @aDadosGRF, @lFirst, @aFilBlocks, nRelAcumul )
	
	//Ŀ
	// Adiciona ao array da legenda                                           
	//
	AAdd( aItensAC2, { AllTrim( AC2->AC2_STAGE ) + " - " + Capital( AC2->AC2_DESCRI ), AC2->( Recno() ) } ) 
	
	AC2->( dbSkip() ) 
	
EndDo
             
//Ŀ
//Identifica o modelo de grafico                                          
//
//nModelo := MV_PAR22
nSerie := oGraph:CreateSerie( nModelo )
nSoma  := 0
//Ŀ
// Percorre o array de colunas                                            
//
For nLoop := 1 To Len( aDadosGRF ) 

	nQuant := aDadosGRF[ nLoop, 1 ] 
	nValor := aDadosGRF[ nLoop, 2 ] 	
	                    
	If lPercent 
		Do Case
		Case MV_PAR01 == 1
			nAltura := ( nQuant / nTotQuant ) * 100
		Case MV_PAR01 == 2
			nAltura := ( nValor / nTotValor ) * 100
		Case MV_PAR01 == 3
			nAltura := ( ( nValor / nQuant ) / nTotMedia ) * 100
		EndCase		                                                                     
	Else
		Do Case
		Case MV_PAR01 == 1
			nAltura := nQuant
		Case MV_PAR01 == 2
			nAltura := nValor
		Case MV_PAR01 == 3
			nAltura := nValor / nQuant 
		EndCase		                                                                     
	EndIf 
	
	nSoma += nAltura

Next nLoop

If Empty(nSoma) .And. nModelo == 10
	Aviso( STR0041, STR0042, { STR0043 } ) //"Ateno" "No  possvel compor este modelo de grfico, pois todos os valores envolvidos na anlise esto zerados." "OK"
Else
	//Ŀ
	// Percorre o array de colunas                                            
	//
	For nLoop := 1 To Len( aDadosGRF ) 
	
		nQuant := aDadosGRF[ nLoop, 1 ] 
		nValor := aDadosGRF[ nLoop, 2 ] 	
		                    
		If lPercent 
			Do Case
			Case MV_PAR01 == 1
				nAltura := ( nQuant / nTotQuant ) * 100
			Case MV_PAR01 == 2
				nAltura := ( nValor / nTotValor ) * 100
			Case MV_PAR01 == 3
				nAltura := ( ( nValor / nQuant ) / nTotMedia ) * 100
			EndCase		                                                                     
		Else
			Do Case
			Case MV_PAR01 == 1
				nAltura := nQuant
			Case MV_PAR01 == 2
				nAltura := nValor
			Case MV_PAR01 == 3
				nAltura := nValor / nQuant 
			EndCase		                                                                     
		EndIf 
		
		//Ŀ
		// Cria a coluna do grafico                                               
		//
		oGraph:Add(nSerie,nAltura,aDadosGRF[ nLoop, 3 ],If( MV_PAR21 == 1, aColors[ nColor ], aColors[ 11 ] ))
	
		nColor++ 
		
		If nColor > 18 
			nColor := 1 
		EndIf
		
	Next nLoop
EndIf

Return( .T. ) 

/*


Ŀ
Program   Ftc010Disp Autor  Sergio Silveira        Data 08/02/2001
Ĵ
Descrio  Efetua o redesenho do grafico                              
Ĵ       
Sintaxe    Ftc010Disp( ExpO1, [ ExpC1 ], @ExpA1 )                     
Ĵ       
Retorno    Nenhum                                                     
Ĵ
Parametros ExpO1 -> Objeto grafico                                     
           ExpC1 -> Representante                                     
           ExpA1 -> Array da legenda                                  
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Function Ftc010Disp( oGraph, cRepres, aItensAC2, nModelo )
                                                
LOCAL aColors := {}
LOCAL cEixoX  := ""                               
LOCAL cModelo := ""
                             
// Adiciona as cores padrao

AAdd( aColors,  CLR_BLACK     )
AAdd( aColors,  CLR_BLUE      )
AAdd( aColors,  CLR_GREEN     )
AAdd( aColors,  CLR_CYAN      )
AAdd( aColors,  CLR_RED       )
AAdd( aColors,  CLR_MAGENTA   )
AAdd( aColors,  CLR_BROWN     )
AAdd( aColors,  CLR_HGRAY     )
AAdd( aColors,  CLR_LIGHTGRAY )
AAdd( aColors,  CLR_GRAY      )
AAdd( aColors,  CLR_HBLUE     )
AAdd( aColors,  CLR_HGREEN    )
AAdd( aColors,  CLR_HCYAN     )
AAdd( aColors,  CLR_HRED      )
AAdd( aColors,  CLR_HMAGENTA  )
AAdd( aColors,  CLR_YELLOW    )
AAdd( aColors,  CLR_WHITE     )

//Ŀ
//Zera os dados do grafico                                                
//

oGraph:l3D := ( MV_PAR22 == 1 ) 
oGraph:SetLegenProp( GRP_SCRTOP, CLR_YELLOW, GRPLASTVAL, .F. )

cModelo := Alltrim( Str( nModelo ) ) 
            
If cModelo $ "1/2/3/4/5/6/12"

	cEixoX := STR0012
	
	If MV_PAR02 == 2 
		cEixoY := STR0009 //"Percentual"
	Else	
		If MV_PAR01 == 1
			cEixoY := STR0008 //"Oportunidades"
		ElseIf MV_PAR01 == 2
			cEixoY := STR0010 + Capital( AllTrim( GetMV( "MV_MOEDA" + AllTrim( Str( MV_PAR25 ) ) ) ) ) //"Valores em "
		Else
			cEixoY := STR0011 + Capital( AllTrim( GetMV( "MV_MOEDA" + AllTrim( Str( MV_PAR25 ) ) ) ) )  //"Valores medios em "
	 	EndIf 		
	EndIf                         
	
ElseIf cModelo $ "7/8/9"	

	cEixoY := STR0012
	
	If MV_PAR02 == 2 
		cEixoX := STR0009 //"Percentual"
	Else	
		If MV_PAR01 == 1
			cEixoX := STR0008 //"Oportunidades"
		ElseIf MV_PAR01 == 2
			cEixoX := STR0010 + Capital( AllTrim( GetMV( "MV_MOEDA" + AllTrim( Str( MV_PAR25 ) ) ) ) ) //"Valores em "
		Else
			cEixoX := STR0011 + Capital( AllTrim( GetMV( "MV_MOEDA" + AllTrim( Str( MV_PAR25 ) ) ) ) )  //"Valores medios em "
	 	EndIf 		
	EndIf       
	
Else

	cEixoX := ""
	cEixoY := ""
	
EndIf 		                  

	
	
oGraph:SetTitle( cEixoY,   , CLR_HRED , A_LEFTJUST , GRP_TITLE )
oGraph:SetTitle( , cEixoX, CLR_GREEN, A_RIGHTJUS , GRP_FOOT  ) //"Etapas"

//Ŀ
//Efetua a montagem das colunas                                           
//
Ftc010Col( @oGraph, cRepres, @aItensAC2, aColors, nModelo )

Return( .t. )  

/*


Ŀ
Program   Ftc010Etp  Autor  Sergio Silveira        Data 06/03/2001
Ĵ
Descrio  Visualiza a etapa                                          
Ĵ       
Sintaxe    Ftc010Etp( ExpO1, ExpA1 )                                  
Ĵ       
Retorno    .T.                                                        
Ĵ
Parametros ExpO1 -> Objeto listbox                                     
           ExpA1 -> Array de recnos                                   
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Static Function Ftc010Etp( oListBox, aItensAC2 )

PRIVATE aRotina   := { { STR0012, "AxVisual", 0, 2 } }   //"Etapas"
PRIVATE cCadastro := STR0012  //"Etapas"
       
If !Empty( oListBox:nAt ) 
	AC2->( dbGoto( aItensAC2[ oListBox:nAt, 2 ] ) )  
	AxVisual( "AC2", AC2->( Recno() ), 1 ) 
EndIf
	
Return( .T. ) 
 
/*


Ŀ
Program   Ftc010Opor Autor  Sergio Silveira        Data 07/03/2001
Ĵ
Descrio  Visualiza as oportunidades da etapa                        
Ĵ       
Sintaxe    Ftc010Opor( ExpO1, ExpA1, ExpC1 )                          
Ĵ       
Retorno    .T.                                                        
Ĵ
Parametros ExpO1 -> Objeto listbox                                     
           ExpA1 -> Array de itens AC2                                 
           ExpC1 -> Codigo do representante                           
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Static Function Ftc010Opor( oListBox, aItensAC2, cRepres )

Local aFilBlocks  := Array( 8 ) 
Local aStruQuery  := {}
Local cProven     := ""
Local cStage      := ""
Local cDescri     := ""
Local cAlias      := ""
Local lFirst      := .T.
Local oDlg2	      := Nil     
Local oGetDB	  := Nil
Local oTempTable  := Nil

AFill( aFilBlocks, { || .T. } ) 

PRIVATE aRotina   := { { STR0012, "AxVisual", 0, 2 } }   //"Etapas"
PRIVATE aHeader   := {}
PRIVATE cCadastro := STR0012  //"Etapas"

Static cTmpTable	:= ""

nSel := oListBox:GetPos()
       
If !Empty( nSel  ) 
	AC2->( dbGoto( aItensAC2[ nSel, 2 ] ) )  
	                                                     
	dbSelectArea("SX3")
	dbSetOrder(1)
	dbSeek("AD1")
	While ( !Eof() .And. SX3->X3_ARQUIVO=="AD1" )
		If ( X3Uso(SX3->X3_USADO) .And.;
				 SX3->X3_TIPO!="M" .And.;
				 cNivel >= SX3->X3_NIVEL .And.;
				 SX3->X3_CONTEXT!="V" ) .Or. AllTrim(SX3->X3_CAMPO)=="AA3_CONTRT"
			AAdd(aStruQuery,{	SX3->X3_CAMPO,;
									SX3->X3_TIPO,;
									SX3->X3_TAMANHO,;
									SX3->X3_DECIMAL	})
			AAdd(aHeader,{ AllTrim(X3Titulo()),;
					SX3->X3_CAMPO,;
					SX3->X3_PICTURE,;
					SX3->X3_TAMANHO,;
					SX3->X3_DECIMAL,;
					SX3->X3_VALID,;
					SX3->X3_USADO,;
					SX3->X3_TIPO,;
					SX3->X3_ARQUIVO,;
					SX3->X3_CONTEXT } )
		EndIf
		dbSelectArea("SX3")
		dbSkip()
	EndDo                
	
	AAdd(aStruQuery,{"AD1_FLAG","L",1,0})
	
	//-------------------------------------------------------------------
	// Criao do objeto  
	//-------------------------------------------------------------------
	cTmpTable	:= GetNextAlias()
	oTempTable	:= FWTemporaryTable():New( cTmpTable )

	//-------------------------------------------------------------------
	// Atribui campos e ndices.  
	//-------------------------------------------------------------------
	oTempTable:SetFields( aStruQuery )
	oTempTable:AddIndex("1",{"AD1_NROPOR"})
	
	//------------------
	//Criao da tabela
	//------------------
	oTempTable:Create()
	
	//Ŀ
	// Chama a funcao que alimenta o arquivo de trabalho                      
	//
	Ftc010Stage( cRepres, , , , , , , @lFirst, @aFilBlocks, , 2, aStruQuery )
		
	If ( (cTmpTable)->( !Eof() ) )
	
		cProVen := AC2->AC2_PROVEN
		cStage  := AC2->AC2_STAGE
		cDescri := Capital( AC2->AC2_DESCRI ) 
	
		DEFINE MSDIALOG oDlg2 FROM	09,0 TO 28,80 TITLE cCadastro OF oMainWnd

			@ 008,008 SAY STR0013  SIZE 028,007 OF oDlg2 PIXEL //FONT oBold  //"Processo"
			@ 017,007 MSGET cProven When .F. SIZE 026,009 OF oDlg2 PIXEL
		                              
			@ 008,042 SAY STR0014    SIZE 024,007 OF oDlg2 PIXEL //FONT oBold //"Etapa"
			@ 017,042 MSGET cStage When .F. SIZE 018,009 OF oDlg2 PIXEL
			                          
			@ 008,077 SAY STR0015 SIZE 030,007 OF oDlg2 PIXEL //FONT oBold  //"Descricao"
			@ 017,077 MSGET cDescri When .F. SIZE 109,009 OF oDlg2 PIXEL

			oGetDB:=MsGetDB():New(038,005,139,312,1,,,,.F.,,,.F.,,cTmpTable)
			
			DEFINE SBUTTON 		FROM 005,284 TYPE 1  ENABLE OF oDlg2 ACTION ( oDlg2:End() )
		   	DEFINE SBUTTON oBtn FROM 020,284 TYPE 15 ENABLE OF oDlg2 ACTION ( oGetDB:oBrowse:SetFocus(),Ftc010ViOp()) 

		ACTIVATE MSDIALOG oDlg2 CENTERED
	Else
		Help( " ", 1, "ATC010OPOR" ) 
	EndIf  
 
	//------------------------------
	//Deleta a tabela temporaria
	//------------------------------
	If( valtype(oTempTable) == "O")
		oTempTable:Delete()
		freeObj(oTempTable)
		oTempTable := nil
	EndIf
	
	dbSelectArea( "AD1" ) 
	
EndIf		
	
Return( .T. ) 

/*


Ŀ
Program   Ftc010ViOp Autor  Sergio Silveira        Data 07/03/2001
Ĵ
Descrio  Faz a visualizacao de uma oportunidade                     
Ĵ       
Sintaxe    Ftc010ViOp( )                                              
Ĵ       
Retorno    .T.                                                        
Ĵ
Parametros Nenhum                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Static Function Ftc010ViOp()                   

DbSelectArea("AD1")
AD1->(DbSetOrder(1)) 
If AD1->(DbSeek(xFilial("AD1")+(cTmpTable)->AD1_NROPOR)) 
	FWExecView(Upper(STR0040),"VIEWDEF.FATA300",MODEL_OPERATION_VIEW,/*oDlg*/,/*bCloseOnOk*/,/*bOk*/,/*nPercReducao*/)  //"Visualizar"                    
EndIf 

Return( .T. )  

/*


Ŀ
Program   Ftc010Stag Autor  Sergio Silveira        Data 06/03/2001
Ĵ
Descrio  Retorna dados de uma etapa para grafico ou alimenta        
           arquivo de trabalho das oportunidades                      
Ĵ       
Sintaxe    Ftc010Stag( ExpC1, @ExpN1, @ExpN2, @ExpN3, @ExpN4, @ExpN5,  
              @ExpA1, @ExpL1, @ExpA2, @ExpN6, ExpN7, ExpA3 )          
Ĵ       
Retorno    .T.                                                        
Ĵ
Parametros ExpC1 -> Representante                                      
           ExpN1 -> Quantidade                                        
           ExpN2 -> Valor                                             
           ExpN3 -> Total valor                                       
           ExpN4 -> Total quantidade                                  
           ExpN5 -> Total media de valor                              
           ExpA1 -> Array de dados do grafico                         
           ExpL1 -> Controle de primeira chamada                      
           ExpA2 -> Array contendo code-blocks de filtro              
           ExpN6 -> Acumulado de relevancia                           
           ExpN7 -> Modelo : 1 - Dados do grafico                     
                             2 - Alimenta arquivo de oportunidades    
           ExpA3 -> Estrutura do Array de trabalho                    
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Function Ftc010Stage( cRepres, nQuant, nValor, nTotValor, nTotQuant, nTotMedia,;
		 aDadosGRF, lFirst, aFilBlocks, nRelAcumul, nModelo, aStruQuery )
  
Local bBlockMais	:= aFilBlocks[ 1 ] 
Local bBlockMenos	:= aFilBlocks[ 2 ] 
Local bFilGrupo		:= aFilBlocks[ 3 ] 
Local bWhileAD1		:= aFilBlocks[ 4 ] 
Local bFiltroAD1	:= aFilBlocks[ 5 ] 
Local bFilUsAD1		:= aFilBlocks[ 6 ] 
Local bFilUsSA3		:= aFilBlocks[ 7 ] 
Local bFilUsSB1		:= aFilBlocks[ 8 ] 
     
Local cAliasQry	:= GetNextAlias()                                             
Local cQuery		:= ""
Local cFilGrupo	:= ""
Local cSeekSD1	:= "" 

Local lInferiores  := ( MV_PAR23 == 1 )  
Local lInclui      := .F.

Local nLoop        := 0  
Local nCntFor      := 0 
              

DEFAULT aDadosGRF  := {}

DEFAULT nQuant     := 0 
DEFAULT nValor     := 0 
DEFAULT nTotValor  := 0 
DEFAULT nTotQuant  := 0 
DEFAULT nTotMedia  := 0                                      
DEFAULT nRelAcumul := 0 

nModelo            := If( ValType( nModelo ) == "N", nModelo, 1 ) 

//Ŀ
// Definicao dos blocos de filtro - efetuada apenas uma vez               
//

If lFirst 

	If MV_PAR09 == 1 
		bBlockMais  := { || .T. } 
	Else
		If MV_PAR09 <> 5      
			bBlockMais := { || AD1->AD1_STATUS == Str( MV_PAR09 - 1, 1 ) }  
		Else 		
			bBlockMais := { || AD1->AD1_STATUS == "9" }                     
		EndIf                                         
	EndIf 
	
	If MV_PAR10 == 1 
		bBlockMenos := { || .F. } 
	Else
		If MV_PAR10 <> 5      
			bBlockMenos := { || AD1->AD1_STATUS == Str( MV_PAR10 - 1, 1 ) }  
		Else 		
			bBlockMenos := { || AD1->AD1_STATUS == "9" }  
		EndIf                                         
	EndIf
	
	//Ŀ
	// Faz a chamada para definicao dos filtros de usuario  
	//
	If ExistBlock( "FT320FIL" ) 
		If ValType( aFiltro := ExecBlock( "FT320FIL", .F., .F. ) ) == "A"
			bFilUsAD1 := aFiltro[ 1 ]
			bFilUsSA3 := aFiltro[ 2 ]
			bFilUsSB1 := aFiltro[ 3 ]					
		EndIf 
	EndIf 
	
	//Ŀ
	// Faz a montagem do filtro de representantes considerando a estrutura    
	//
	bFilGrupo := { || .T. }     
	           
	If !Empty( cRepres )            
	
		//Ŀ
		// Adiciona o proprio representante                                       
		//
		cFilGrupo := "{ || SA3->A3_COD=='" + cRepres + "'"
		                                       
		If lInferiores
			//Ŀ
			// Adiciona os grupos que estao abaixo deste representante                
			//
			If !Empty( aGrupos := FtReprEst( cRepres ) )
				For nLoop := 1 to Len( aGrupos ) 
					cFilGrupo += ".Or.SA3->A3_GRPREP=='" + aGrupos[ nLoop, 1 ] + "'" 
				Next nLoop  
			EndIf	
		EndIf 		
		
		cFilGrupo += " } "
		bFilGrupo := &cFilGrupo  
	
	EndIf                           
	
	//Ŀ
	// Atualiza o array de code-blocks                      
	//
	aFilBlocks[ 1 ] := bBlockMais   
	aFilBlocks[ 2 ] := bBlockMenos 
	aFilBlocks[ 3 ] := bFilGrupo  
	aFilBlocks[ 4 ] := bWhileAD1 
	aFilBlocks[ 5 ] := bFiltroAD1
	aFilBlocks[ 6 ] := bFilUsAD1 
	aFilBlocks[ 7 ] := bFilUsSA3 
	aFilBlocks[ 8 ] := bFilUsSB1 
	
	lFirst := .F.

EndIf 
	
cQuery := "SELECT R_E_C_N_O_ AD1RECNO FROM " + RetSqlName( "AD1" ) + " "
cQuery += "WHERE "
cQuery += "AD1_DTINI>='"  + DToS(MV_PAR03)+"' AND AD1_DTFIM<='"+DToS(MV_PAR04)+"' AND "
cQuery += "AD1_VEND>='"   + MV_PAR07 + "' AND AD1_VEND<='"   + MV_PAR08 + "' AND "
cQuery += "AD1_CODPRO>='" + MV_PAR11 + "' AND AD1_CODPRO<='" + MV_PAR12 + "' AND "			
cQuery += "AD1_PROSPE>='" + MV_PAR19 + "' AND AD1_PROSPE<='" + MV_PAR20 + "' AND "			
cQuery += "AD1_PROVEN='"  + AC2->AC2_PROVEN + "' AND "			
cQuery += "AD1_STAGE='"   + AC2->AC2_STAGE  + "' AND "						
cQuery += "D_E_L_E_T_=' '"

dbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasQry, .F., .T. ) 			

TcSetField( cAliasQry, "AD1RECNO", "N", 8, 0 ) 
  
//Ŀ
// Percorre as oportunidades                                              
//

While ( cAliasQry )->( !Eof() )  

	nRecnoAD1 := AD1RECNO 
	AD1->( dbGoto( nRecnoAD1 ) )
            
	//Ŀ
	// Verifica as oportunidades considerando os parametros                   
	//
	If  Eval( bBlockMais ) .And. !Eval( bBlockMenos ) .And. Eval( bFilUsAD1 ) 

		//Ŀ
		// Verifica os representantes considerando os parametros                  
		//
		SA3->( dbSetOrder( 1 ) ) 
		If SA3->( MsSeek( xFilial( "SA3" ) + AD1->AD1_VEND ) ) 

			//Ŀ
			// Efetua o filtro baseado na estrutura de vendas vigente                 
			//
			If Eval( bFilGrupo ) .And. Eval( bFilUsSA3 ) .And. ( SA3->A3_REGIAO >= MV_PAR17 .And. SA3->A3_REGIAO <= MV_PAR18 ) 
                                 
				lInclui := .F. 
				If Empty( AD1->AD1_CODPRO ) 	
					lInclui := .T.
				Else
					//Ŀ
					// Verifica as produtos considerando os parametros                        
					//
					SB1->( dbSetOrder( 1 ) )     
					If SB1->( dbSeek( xFilial( "SB1" ) + AD1->AD1_CODPRO ) )
					    If SB1->B1_GRUPO >= MV_PAR15 .And. SB1->B1_GRUPO <= MV_PAR16 .And.;
								SB1->B1_TIPO >= MV_PAR13 .And. SB1->B1_TIPO <= MV_PAR14 .And. Eval( bFilUsSB1 ) 
							lInclui := .T. 
						EndIf 						
					EndIf 
				EndIf	
				
				If lInclui 
				
					If nModelo == 1 
						nQuant ++  
						//Ŀ
						// Converte para a moeda de analise 
						//
						nValor += xMoeda( AD1->AD1_VERBA, AD1->AD1_MOEDA, MV_PAR25, dDataBase )
					Else
						RecLock(cTmpTable,.T.)
						For nCntFor := 1 To Len(aStruQuery)
							If AllTrim( aStruQuery[nCntFor,1] ) <> "AD1_FLAG"
								FieldPut(nCntFor,AD1->(FieldGet(FieldPos(aStruQuery[nCntFor][1]))))
							EndIf 								
						Next nCntFor
						MsUnLock()
					EndIf 	
						
				EndIf
				
			EndIf
		EndIf					
		 	
	EndIf 	
	
	(cAliasQry)->( DBSkip() )

EndDo 	

(cAliasQry)->( DBCloseArea() )
      
If nModelo == 1                                                                        
	//Ŀ
	// Aplica o fator de Contribuicao                                         
	//
	If MV_PAR24 == 2 
		nQuant *= nRelAcumul 
		nValor *= nRelAcumul 
	EndIf 					
	
	//Ŀ
	// Alimenta os totalizadores                                              
	//
	nTotValor += nValor
	nTotQuant += nQuant
	nTotMedia += ( nValor / nQuant ) 
	
	//Ŀ
	// Alimenta o array de colunas do grafico                                 
	//
	AAdd( aDadosGRF, { nQuant, nValor, AC2->AC2_STAGE, AC2->AC2_DESCRI } )
	
EndIf

Return( .T. ) 

/*


Ŀ
Program   Ftc010Par  Autor  Sergio Silveira        Data 06/03/2001
Ĵ
Descrio  Funcao executada na mudanca de parametros                  
Ĵ       
Sintaxe    Ftc010Par( ExpO1, ExpO2, ExpC1, ExpA1 )                    
Ĵ       
Retorno    .T.                                                        
Ĵ
Parametros ExpO1 -> Objeto grafico                                     
           ExpO2 -> Objeto ListBox                                     
           ExpC1 -> Representante                                     
           ExpA1 -> Array itens AC2                                   
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Function Ftc010Par( oGraph, oListBox, cRepres, aItensAC2, nModelo )
                                                  
Local aLegenda := {} 

If Pergunte( "FTC010", .T., STR0005 ) //"Parametros do Pipeline"
	Ftc010Disp( @oGraph, cRepres, @aItensAC2, nModelo )
	AEval( aItensAC2, { |x| AAdd( aLegenda, x[1] ) } )        
	      
	oListBox:Reset()
	oListBox:SetItems( aLegenda ) 
	oListBox:Refresh()
EndIf
	
Return( .T. ) 

/*


Ŀ
Program   Ftc010Remo Autor  Sergio Silveira        Data 08/06/2001
Ĵ
Descrio  Controles do grafico                                       
Ĵ       
Sintaxe    Function Ftc010Remote( oDlg, oGraph, oContainer )          
Ĵ       
Retorno    .T.                                                        
Ĵ
Parametros                                                             
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Function Ftc010Remote( oDlg, oGraph, oContainer )

LOCAL aCbx := { STR0016, STR0017, STR0018, STR0019, STR0020, STR0021,; //"Linha"###"rea"###"Pontos"###"Barras"###"Piramide"###"Cilindro"
			 STR0022, STR0023, STR0024,; //"Barras / Horizontal"###"Piramides / Horizontal"###"Cilindros / Horizontal"
			 STR0025 } //"Pizza"###
			 
LOCAL cCbx := ""

LOCAL lCreateDlg := .T.  

LOCAL oBtn1
LOCAL oBtn2                         
LOCAL oBtn3
LOCAL oBtn4                         
LOCAL oBtn5
LOCAL oBtn6                                      
LOCAL oCbx  

LOCAL nSec := Seconds() 
           
If ValType( oContainer ) == "O"
	lCreateDlg := .F.
EndIf 	
 
If lCreateDlg
	DEFINE MSDIALOG oContainer FROM 100,100 TO 270,270 TITLE STR0031 OF oMainWnd PIXEL //"remote"
EndIf	
     
	@  5,   5 BUTTON oBtn1 PROMPT STR0032      SIZE 35,12 ACTION ( oGraph:ZoomIn() ) OF oContainer  PIXEL //"Ampliar"
	@  5,  45 BUTTON oBtn2 PROMPT STR0033      OF oContainer ACTION ( oGraph:ZoomOut() ) SIZE 35, 12 PIXEL  //"Reduzir"
	@ 20,   5 BUTTON oBtn5 PROMPT STR0036      OF oContainer ACTION ( oGraph:l3D := !oGraph:l3D  ) SIZE 35, 12 PIXEL       //"2D / 3D"
	@ 20,  45 BUTTON oBtn6 PROMPT STR0037      OF oContainer ACTION ( oGraph:lAxisVisib := !oGraph:lAxisVisib ) SIZE 35, 12 PIXEL      	 //"Eixos S/N"

	//@ 20,   5 BUTTON oBtn3 PROMPT STR0034      OF oContainer ACTION ( oGraph:ChgRotat( nSerie, 4, .F. ) ) SIZE 35, 12 PIXEL  //"Horario"
	//@ 20,  45 BUTTON oBtn4 PROMPT STR0035      OF oContainer ACTION ( oGraph:ChgRotat( nSerie, 4, .T. ) ) SIZE 35, 12 PIXEL  //"Anti-horario"
	//Btn3:SetEnable( oGraph:l3D )
	//Btn4:SetEnable( oGraph:l3D )          
	                              
	// RESERVADO PARA IMPLEMENTACAO FUTURA 
	// @ 048,  5 SAY STR0038 of oContainer PIXEL  //"Modelo"
	// @ 057,  5 MSCOMBOBOX oCbx VAR cCbx ITEMS aCbx SIZE 061, 65 OF oContainer PIXEL ON CHANGE Ftc010GrCh( @oGraph, @oCbx ) 
             
If lCreateDlg
	ACTIVATE MSDIALOG oContainer
EndIf	

Return( Nil ) 
                

/*


Ŀ
Program   Ftc010GrCh Autor  Sergio Silveira        Data 03/08/2001
Ĵ
Descrio  Mudancao do modelo do grafico                              
Ĵ       
Sintaxe    Ftc010GrCh( oGraph )                                       
Ĵ       
Retorno    .T.                                                        
Ĵ
Parametros                                                             
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
ٱ


*/

Function Ftc010GrCh( oGraph, oCbx ) 

Return( .T. ) 


/*/


Ŀ
Programa  MenuDef    Autor  Marco Bianchi          Data 01/09/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados          
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()
     
Private aRotina := {	{ OemToAnsi(STR0002) ,"AxPesqui"		,0,1,0,.F.},;		//"Pesquisar"	//"Pesquisar"
							{ OemToAnsi(STR0003 ),"Ftc010Pipe"	,0,2,0,NIL} }		//"Visual"		//"Pipeline"

If ExistBlock("FC010MNU")
	ExecBlock("FC010MNU",.F.,.F.)
EndIf

Return(aRotina)
