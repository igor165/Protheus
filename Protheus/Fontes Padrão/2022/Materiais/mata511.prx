#INCLUDE "MATA511.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³MATA511A  ³ Autor ³Eduardo Riera          ³ Data ³21.02.2002	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Preparacao de documentos de saida para retorno simbolico de   ³±±
±±³          ³material de terceiro                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATA511A()
Local aArea := GetArea()

PRIVATE aRotina   := MenuDef()
PRIVATE cCadastro := OemToAnsi(STR0005) //"Preparacao da Devolucao de Poder de Terceiro"
PRIVATE cCliFor := ""

dbSelectArea("SF2")
dbSetOrder(1)
MsSeek(xFilial("SF2"))

If Pergunte("MTA511",.T.)
	cCliFor := IIf(MV_PAR01==1,"C","F")
	mBrowse(7,4,20,74,"SF2")
Endif	

RestArea(aArea)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511P3Aut³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Interface do processamento dos Documentos de Saida(Poder 3) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ma511P3Aut()

Local aArea := GetArea()
Local nOpcA := 0
Local cSerie:= ""
Local cTpCliFor := cCliFor

Private lSDoc := SerieNFID("SF2", 3, "F2_SERIE") == "F2_SERIE"

If !ctbValiDt( Nil, dDataBase, .T., Nil, Nil, { "FAT003" }, Nil )
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01 Serie Inicial      ?                                          ³
//³ mv_par02 Serie Final        ?                                          ³
//³ mv_par03 Documento Inicial  ?                                          ³
//³ mv_par04 Documento Final    ?                                          ³
//³ mv_par05 Emissao Inicial    ?                                          ³
//³ mv_par06 Emissao Final      ?                                          ³
//³ mv_par07 Cli/Forn. Inicial  ?                                          ³
//³ mv_par08 Cli/Forn. Final    ?                                          ³
//³ mv_par09 Loja Inicial       ?                                          ³ 
//³ mv_par10 Loja Final         ?                                          ³
//³ mv_par11 Aglutina Documentos?  Documento/Dia/Periodo                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MT511A",.F.)

If Sx5NumNota(@cSerie,GetNewPar("MV_TPNRNFS","1"))
	FormBatch(OemToAnsi(STR0006),; //"Retorno"
			{	OemToAnsi(STR0007),; //"     Esta rotina efetua a preparacao dos documentos de saida com base nos documentos"
				OemToAnsi(STR0008),; //"de venda ou remessa para terceiros, conforme os parametros da rotina."
				OemToAnsi("")},;
				{  {5,.T.,{|o| Pergunte("MT511A",.T.)} },;
				{1,.T.,{|o| nOpcA:=1,o:oWnd:End()}  },;
				{2,.T.,{|o| nOpcA:=0,o:oWnd:End() }} })
	If ( nOpcA == 1 )
		Processa({|lEnd| Ma511P3Pro(lEnd,cSerie,cTpCliFor)},,OemToAnsi(STR0009),.T.) //"Prepara‡„o dos Doc's de Sa¡da/Expedi‡„o "
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a integridade da Rotina                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511P3Pro³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de processamento dos documentos de saida             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Controle de cancelamento                             ³±±
±±³          ³ExpC2: Serie da Nota a ser gerada                           ³±±
±±³          ³ExpC3: [C] Cliente                                          ³±±
±±³          ³       [F] Fornece                                          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ma511P3Pro(lEnd,cSerieNFS,cTpCliFor)

Local aArea     := GetArea()
Local aStruSD2  := SD2->(dbStruct())

Local aSD2      := {}
Local aSF2RecNo := {}
Local cQuebra   := ""
Local cCursor   := "MA511P3PRO"
Local cQuery    := ""
Local cKey      := ""

Local cArqTmp   := ""
Local cMensagem := ""

Local cCliente  := GetMv("MV_FTP3CL1")
Local cLojaCli  := GetMv("MV_FTP3CL2")
Local cFornece  := GetMv("MV_FTP3FO1")
Local cLojaFor  := GetMv("MV_FTP3FO2")
Local lMostraCtb:= .F.
Local lAglutCtb := .F.
Local lCtbOnLine:= .F.
Local lCtbCusto := .F.
Local lReajusta := .F.
Local lAtuSA7   := .F.
Local nX        := 0
Local nY        := 0
Local nCalAcrs  := 1
Local nArredPrcLis:= 1
Local bSD2      := {|nTipo,aSD2,cAliasSD2,cAliasSF4,cAliasSB1| Ma511P3SD2(nTipo,@aSD2,cAliasSD2,cAliasSF4,cAliasSB1,cTpCliFor)}
Local bFilSD2   := {|cAliasSD2,cAliasSF4,cAliasSB1| Ma511FilP3(cAliasSD2,cAliasSF4,cAliasSB1,cTpCliFor) }
Local bSF2      := {|nTipo| Ma511P3SF2(nTipo) }
Local bTTS      := {|| .T.}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem do Arquivo de Trabalho                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Verifica se os parametros com os dados do cliente e loja estão configurados, para não gerar nota sem esses dados
If !Ma511VldCli(cTpCliFor,cCliente,cLojaCli,cFornece,cLojaFor)
	Return
EndIf

Do Case
	Case MV_PAR11 == 1 //Documento
		cKey := "D2_FILIAL+D2_DOC+D2_SERIE"
	Case MV_PAR11 == 2 //Diario
		cKey := "D2_FILIAL+DTOS(D2_EMISSAO)"
	Case MV_PAR11 == 3 //Periodo
	   	cKey := "D2_FILIAL"
EndCase
	 	
For nX := 1 To Len(aStruSD2)
	Do Case
		Case AllTrim(aStruSD2[nX][1])=="D2_CLIENTE"
			cQuery += ","+StrTran(IIf(cTpCliFor=="F",cFornece,cCliente),"->",".")+" D2_CLIENTE "
		Case AllTrim(aStruSD2[nX][1])=="D2_LOJA"
			cQuery += ","+StrTran(IIf(cTpCliFor=="F",cLojaFor,cLojaCli),"->",".")+" D2_LOJA "
		Case AllTrim(aStruSD2[nX][1])=="D2_TES"
			cQuery += ",SF4.F4_TESP3 D2_TES "
		Case AllTrim(aStruSD2[nX][1])=="D2_TIPO"
			cQuery += ",D2_TIPO "
		Case AllTrim(aStruSD2[nX][1])=="D2_IDENTB6"
			cQuery += ",'"+Space(Len(SD2->D2_IDENTB6))+"' D2_IDENTB6 "
		OtherWise
			cQuery += ","+aStruSD2[nX][1]+" "
	EndCase
Next nX

cQuery := "SELECT SF2.R_E_C_N_O_ SF2RECNO "+cQuery
cQuery += "FROM "+RetSqlName("SF2")+" SF2,"
cQuery += RetSqlName("SD2")+" SD2,"
cQuery += RetSqlName("SB1")+" SB1,"	
cQuery += RetSqlName("SF4")+" SF4 "
cQuery += "WHERE SF2.F2_FILIAL='"+xFilial("SF2")+"' AND "

//Bruno Cremaschi - Projeto chave única
If Type("lSDoc") == "U"
	cQuery += "SF2.F2_SERIE>='"+MV_PAR01+"' AND "
	cQuery += "SF2.F2_SERIE<='"+MV_PAR02+"' AND "
Else	
	If lSDoc 
		cQuery += "SF2.F2_SERIE>='"+MV_PAR01+"' AND "
		cQuery += "SF2.F2_SERIE<='"+MV_PAR02+"' AND "
	Else
		cQuery += "SF2.F2_SDOC>='"+MV_PAR01+"' AND "
		cQuery += "SF2.F2_SDOC<='"+MV_PAR02+"' AND "
	EndIf
EndIf

cQuery += "SF2.F2_DOC>='"+MV_PAR03+"' AND "
cQuery += "SF2.F2_DOC<='"+MV_PAR04+"' AND "	
cQuery += "SF2.F2_EMISSAO>='"+Dtos(MV_PAR05)+"' AND "	
cQuery += "SF2.F2_EMISSAO<='"+Dtos(MV_PAR06)+"' AND "	
cQuery += "SF2.F2_CLIENTE>='"+MV_PAR07+"' AND "	
cQuery += "SF2.F2_CLIENTE<='"+MV_PAR08+"' AND "	
cQuery += "SF2.F2_LOJA>='"+MV_PAR09+"' AND "	
cQuery += "SF2.F2_LOJA<='"+MV_PAR10+"' AND "
cQuery += "SF2.D_E_L_E_T_=' ' AND "
cQuery += "SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
cQuery += "SD2.D2_DOC=SF2.F2_DOC AND "
cQuery += "SD2.D2_SERIE=SF2.F2_SERIE AND "
cQuery += "SD2.D2_CLIENTE=SF2.F2_CLIENTE AND "
cQuery += "SD2.D2_LOJA=SF2.F2_LOJA AND "
cQuery += "SD2.D2_TIPO=SF2.F2_TIPO AND "
cQuery += "SD2.D_E_L_E_T_=' ' AND "
cQuery += "SF4.F4_FILIAL='"+xFilial("SF4")+"' AND "
cQuery += "SF4.F4_CODIGO=SD2.D2_TES AND "
cQuery += "SF4.F4_TESP3<>'"+Space(Len(SF4->F4_TESP3))+"' AND "
cQuery += "SF4.D_E_L_E_T_=' ' AND "
cQuery += "SB1.B1_FILIAL='"+xFilial("SB1")+"' AND "	
cQuery += "SB1.B1_COD=SD2.D2_COD AND "
cQuery += "SB1.D_E_L_E_T_=' ' "
cQuery += "ORDER BY "+SqlOrder(cKey)
                               
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cCursor,.T.,.T.)

For nX := 1 To Len(aStruSD2)
	If aStruSD2[nX][2] <> "C"
		TcSetField(cCursor,aStruSD2[nX][1],aStruSD2[nX][2],aStruSD2[nX][3],aStruSD2[nX][4])
	EndIf
Next nX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificacao das perguntas da Preparacao do Documento de Saida          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01 Mostra Lan‡.Contab ?  Sim/Nao                        ³
//³ mv_par02 Aglut. Lan‡amentos ?  Sim/Nao                        ³
//³ mv_par03 Lan‡.Contab.On-Line?  Sim/Nao                        ³
//³ mv_par04 Contb.Custo On-Line?  Sim/Nao                        ³
//³ mv_par05 Reaj. na mesma N.F.?  Sim/Nao                        ³
//³ mv_par06 Taxa deflacao ICMS ?  Numerico                       ³
//³ mv_par07 Metodo calc.acr.fin?  Taxa defl/Dif.lista/% Acrs.ped ³
//³ mv_par08 Arred.prc unit vist?  Sempre/Nunca/Consumid.final    ³
//³ mv_par09 Agreg. liberac. de ?  Caracter                       ³
//³ mv_par10 Agreg. liberac. ate?  Caracter                       ³
//³ mv_par11 Aglut.Ped. Iguais  ?  Sim/Nao                        ³
//³ mv_par12 Valor Minimo p/fatu?                                 ³
//³ mv_par13 Transportadora de  ?                                 ³
//³ mv_par14 Transportadora ate ?                                 ³
//³ mv_par15 Atualiza Cli.X Prod?                                 ³
//³ mv_par16 Emitir             ?  Nota / Cupom Fiscal            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MT460A",.F.)
lMostraCtb  := MV_PAR01==1
lAglutCtb   := MV_PAR02==1
lCtbOnLine  := MV_PAR03==1
lCtbCusto   := MV_PAR04==1
lReajusta   := MV_PAR05==1
nCalAcrs    := MV_PAR06
nArredPrcLis:= MV_PAR08
lAtuSA7     := MV_PAR15==1
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processamento dos documentos de Saida                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSD2      := {}
aSF2RecNo := {}
nY        := 0

ProcRegua(SF2->(LastRec()))

dbSelectArea(cCursor)

While !Eof()
	aadd(aSd2,{})
	nY++
	For nX := 1 To Len(aStruSD2)
		aadd(aSD2[nY],(cCurSor)->(FieldGet(FieldPos(aStruSD2[nX][1]))))
	Next nX
	aadd(aSF2RecNo,(cCursor)->SF2RECNO)
         
    cQuebra := &(cKey)
	dbSelectArea(cCursor)
	dbSkip()
	If lEnd
		Exit
	EndIf
	
	If cQuebra <> &(cKey) .Or. (cCursor)->(Eof()) .Or. nY > 300
		//Bruno Cremaschi
		cSDoc := SerieNFID((cCursor), 2, "D2_SERIE")
		cMensagem := MaNfs2Nfs(	cSDoc,;
								(cCursor)->D2_DOC,;
								(cCursor)->D2_CLIENTE,;
								(cCursor)->D2_LOJA,;
								cSerieNFS,;
								lMostraCtb,;
								lAglutCtb,;
								lCtbOnLine,;
								lCtbCusto,;
								lReajusta,;
								nCalAcrs,;
								nArredPrcLis,;
								lAtuSA7,;
								.F.,;
								bFilSD2,;
								bSD2,;
								bSF2,;
								bTTS,;
								aSF2Recno,;
								aSD2)
		aSF2RecNo := {}
		aSD2      := {}
		nY        := 0	
	EndIf
	IncProc(STR0010+cSerieNFS+"/"+cMensagem) //"Preparados os Doc's: "
EndDo

	dbSelectArea(cCursor)
	dbCloseArea()
	dbSelectArea("SF2")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a integridade da Rotina                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511P3SD2³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de atualizacao do SD2                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Tipo                                                 ³±±
±±³          ³       [1] Apos a inclusao                                  ³±±
±±³          ³       [2] Apos a atualizacao                               ³±±
±±³          ³ExpA2: Array de um item do SD2 conforme estrutura           ³±±
±±³          ³ExpC3: Alias do SD2                                         ³±±
±±³          ³ExpC4: Alias do SF4                                         ³±±
±±³          ³ExpC5: Alias do SB1                                         ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma511P3SD2(nTipo,aSD2,cAliasSD2,cAliasSF4,cAliasSB1,cTpCliFor)

Local aArea    := GetArea()
Local aStruSD2 := SD2->(dbStruct())
Local nPTES      := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_TES"})
Local nPCliente  := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_CLIENTE"})
Local nPLoja     := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_LOJA"})
Local nPTipo     := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_TIPO"})
Local nPIdentB6  := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_IDENTB6"})
Local nPEstado   := Ascan(aStruSD2,{|x| AllTrim(x[1]) == "D2_EST"})

Local cCliente  := GetMv("MV_FTP3CL1")
Local cLojaCli  := GetMv("MV_FTP3CL2")
Local cFornece  := GetMv("MV_FTP3FO1")
Local cLojaFor  := GetMv("MV_FTP3FO2")

If nTipo == 1 .And. Ma511VldCli(cTpCliFor,cCliente,cLojaCli,cFornece,cLojaFor)

	cCliente := StrTran(cCliente,"SB1->"," "+cAliasSB1+"->")
	cCliente := StrTran(cCliente,"SF4->"," "+cAliasSF4+"->")
	cCliente := StrTran(cCliente,"SD2->"," "+cAliasSD2+"->")
	cLojaCli := StrTran(cLojaCli,"SB1->"," "+cAliasSB1+"->")
	cLojaCli := StrTran(cLojaCli,"SF4->"," "+cAliasSF4+"->")
	cLojaCli := StrTran(cLojaCli,"SD2->"," "+cAliasSD2+"->")
	cFornece := StrTran(cFornece,"SB1->"," "+cAliasSB1+"->")
	cFornece := StrTran(cFornece,"SF4->"," "+cAliasSF4+"->")
	cFornece := StrTran(cFornece,"SD2->"," "+cAliasSD2+"->")
	cLojaFor := StrTran(cLojaFor,"SB1->"," "+cAliasSB1+"->")
	cLojaFor := StrTran(cLojaFor,"SF4->"," "+cAliasSF4+"->")
	cLojaFor := StrTran(cLojaFor,"SD2->"," "+cAliasSD2+"->")
	
	cFornece := &cFornece
	cCliente := &cCliente
	cLojaFor := &cLojaFor
	cLojaCli := &cLojaCli

	aSD2[nPTES]     := (cAliasSF4)->F4_TESP3
	aSD2[nPCliente] := IIf(cTpCliFor=="F",cFornece,cCliente)
	aSD2[nPLoja]    := IIf(cTpCliFor=="F",cLojaFor,cLojaCli)
	aSD2[nPIdentB6] := ""

	If cTpCliFor=="F"
		SA2->(dbSetOrder(1))
		SA2->(MsSeek(xFilial("SA2")+cFornece+cLojaFor))
		aSD2[nPEstado] := SA2->A2_EST
	Else
		SA1->(dbSetOrder(1))
		SA1->(MsSeek(xFilial("SA1")+cCliente+cLojaCli))
		aSD2[nPEstado] := SA1->A1_EST
	EndIf

EndIf
RestArea(aArea)
Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511FilP3³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de seleccao do SD2                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Alias do SD2                                         ³±±
±±³          ³ExpC2: Alias do SF4                                         ³±±
±±³          ³ExpC2: Alias do SB1                                         ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma511FilP3(cAliasSD2,cAliasSF4,cAliasSB1,cTpCliFor)

Local lRetorno := .F.
Local cCliente  := GetMv("MV_FTP3CL1")
Local cLojaCli  := GetMv("MV_FTP3CL2")
Local cFornece  := GetMv("MV_FTP3FO1")
Local cLojaFor  := GetMv("MV_FTP3FO2")

If Ma511VldCli(cTpCliFor,cCliente,cLojaCli,cFornece,cLojaFor)
	
	cCliente := StrTran(cCliente,"SB1"," "+cAliasSB1)
	cCliente := StrTran(cCliente,"SF4"," "+cAliasSF4)
	cCliente := StrTran(cCliente,"SD2"," "+cAliasSD2)
	cLojaCli := StrTran(cLojaCli,"SB1"," "+cAliasSB1)
	cLojaCli := StrTran(cLojaCli,"SF4"," "+cAliasSF4)
	cLojaCli := StrTran(cLojaCli,"SD2"," "+cAliasSD2)
	cFornece := StrTran(cFornece,"SB1"," "+cAliasSB1)
	cFornece := StrTran(cFornece,"SF4"," "+cAliasSF4)
	cFornece := StrTran(cFornece,"SD2"," "+cAliasSD2)
	cLojaFor := StrTran(cLojaFor,"SB1"," "+cAliasSB1)
	cLojaFor := StrTran(cLojaFor,"SF4"," "+cAliasSF4)
	cLojaFor := StrTran(cLojaFor,"SD2"," "+cAliasSD2)
	
	cFornece := &cFornece
	cCliente := &cCliente
	cLojaFor := &cLojaFor
	cLojaCli := &cLojaCli
	
	lRetorno := !Empty((cAliasSF4)->F4_TESP3)  .And. !Empty(IIf(cTpCliFor=="F",cFornece,cCliente)) .And.;
				!Empty(IIf(cTpCliFor=="F",cLojaFor,cLojaCli))
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511P3SF2³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de atualizacao do SF2                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: nTipo                                                ³±±
±±³          ³       [1] Apos a inclusao                                  ³±±
±±³          ³       [2] Apos a atualizacao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma511P3SF2(nTipo)

Local aArea := GetArea()
Local cVend := "1"
Local nX    := 0
Local nVend := Fa440CntVen()
If nTipo == 1
	SF2->F2_TRANSP  := ""
	SF2->F2_REDESP  := ""
	For nX := 1 To nVend
		SF2->(FieldPut(FieldPos("F2_VEND"+cVend),""))
		cVend := Soma1(cVend,1)
	Next nX
	SF2->F2_OK      := ""
	SF2->F2_FIMP    := ""
	SF2->F2_DTLANC  := Ctod("")
	SF2->F2_DTREAJ  := Ctod("")
	SF2->F2_REAJUST := ""
	SF2->F2_DTBASE0 := Ctod("")
	SF2->F2_FATORB0 := 0
	SF2->F2_DTBASE1 := Ctod("")
	SF2->F2_FATORB1 := 0
	SF2->F2_VARIAC  := 0
	SF2->F2_NEXTDOC := ""
	SF2->F2_PDV     := ""
	SF2->F2_MAPA    := ""
	SF2->F2_ECF     := ""
	SF2->F2_PREFIXO := ""
	SF2->F2_DUPL    := ""
	SF2->F2_ORDPAGO := ""
	SF2->F2_NFCUPOM := ""
	SF2->F2_DESCCAB := 0
	SF2->F2_FRETE   := 0
	SF2->F2_SEGURO  := 0
	SF2->F2_DESPESA := 0
	SF2->F2_FRETAUT := 0
	SF2->F2_ICMAUTO := 0
	SF2->F2_CONTSOC := 0
EndIf
RestArea(aArea)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511P3Dev³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de retorno de Poder de terceiro                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Alias do Cabecalho do documento de saida             ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ma511P3Dev(cAliasSF2)

Local aArea     := GetArea()
Local cMensagem := ""
Local cSerie    := ""
Local cSerieId	:= ""
Local cTpCliFor := cCliFor
Local lMostraCtb:= .F.
Local lAglutCtb := .F.
Local lCtbOnLine:= .F.
Local lCtbCusto := .F.
Local lReajusta := .F.
Local lAtuSA7   := .F.
Local nCalAcrs  := 1
Local nArredPrcLis:= 1
Local bSD2      := {|nTipo,aSD2,cAliasSD2,cAliasSF4,cAliasSB1| Ma511P3SD2(nTipo,@aSD2,cAliasSD2,cAliasSF4,cAliasSB1,cTpCliFor)}
Local bFilSD2   := {|cAliasSD2,cAliasSF4,cAliasSB1| Ma511FilP3(cAliasSD2,cAliasSF4,cAliasSB1,cTpCliFor) }
Local bSF2      := {|nTipo| Ma511P3SF2(nTipo) }
Local bTTS      := {|| .T.}

If !ctbValiDt( Nil, dDataBase, .T., Nil, Nil, { "FAT003" }, Nil )
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificacao das perguntas da Preparacao do Documento de Saida          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01 Mostra Lan‡.Contab ?  Sim/Nao                        ³
//³ mv_par02 Aglut. Lan‡amentos ?  Sim/Nao                        ³
//³ mv_par03 Lan‡.Contab.On-Line?  Sim/Nao                        ³
//³ mv_par04 Contb.Custo On-Line?  Sim/Nao                        ³
//³ mv_par05 Reaj. na mesma N.F.?  Sim/Nao                        ³
//³ mv_par06 Taxa deflacao ICMS ?  Numerico                       ³
//³ mv_par07 Metodo calc.acr.fin?  Taxa defl/Dif.lista/% Acrs.ped ³
//³ mv_par08 Arred.prc unit vist?  Sempre/Nunca/Consumid.final    ³
//³ mv_par09 Agreg. liberac. de ?  Caracter                       ³
//³ mv_par10 Agreg. liberac. ate?  Caracter                       ³
//³ mv_par11 Aglut.Ped. Iguais  ?  Sim/Nao                        ³
//³ mv_par12 Valor Minimo p/fatu?                                 ³
//³ mv_par13 Transportadora de  ?                                 ³
//³ mv_par14 Transportadora ate ?                                 ³
//³ mv_par15 Atualiza Cli.X Prod?                                 ³
//³ mv_par16 Emitir             ?  Nota / Cupom Fiscal            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MT460A",.F.)
lMostraCtb  := MV_PAR01==1
lAglutCtb   := MV_PAR02==1
lCtbOnLine  := MV_PAR03==1
lCtbCusto   := MV_PAR04==1
lReajusta   := MV_PAR05==1
nCalAcrs    := MV_PAR06
nArredPrcLis:= MV_PAR08
lAtuSA7     := MV_PAR15==1

If Sx5NumNota(@cSerie,GetNewPar("MV_TPNRNFS","1"),,,,@cSerieId,dDataBase)	
	//Bruno Cremaschi - Projeto chave única.
	cSDoc := SerieNFID((cAliasSF2), 2, "F2_SERIE")
	cMensagem := MaNfs2Nfs( cSDoc,;
				(cAliasSF2)->F2_DOC,;
				(cAliasSF2)->F2_CLIENTE,;
				(cAliasSF2)->F2_LOJA,;
				cSerie,;
				lMostraCtb,;
				lAglutCtb,;
				lCtbOnLine,;
				lCtbCusto,;
				lReajusta,;
				nCalAcrs,;
				nArredPrcLis,;
				lAtuSA7,;
				.F.,;
				bFilSD2,;
				bSD2,;
				bSF2,;
				bTTS)
	If !Empty(cMensagem)
		dbSelectArea("SF2")
		dbSetOrder(1)
		If MsSeek(xFilial("SF2")+cMensagem+cSerieId)
			Mc090Visual("SF2",SF2->(RecNo()),1)
		EndIf
	Else
		Help(" ",1,"MA511NODOC")
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a integridade da Rotina                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³MATA511B  ³ Autor ³Eduardo Riera          ³ Data ³21.02.2002	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Preparacao de documentos de saida para retorno simbolico de   ³±±
±±³          ³material de terceiro                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATA511B()

Local aArea := GetArea()

PRIVATE aRotina   := {	{ STR0001,"AxPesqui"		,0,1},;		// "Pesquisar"
						{ STR0002,"Mc090Visual"	,0,2},;		// "Visual"
						{ STR0004,"Ma511RjAut"	,0,0}}		// "Automatico"

PRIVATE cCadastro := OemToAnsi(STR0011) //"Reajuste de Precos"

dbSelectArea("SF2")
dbSetOrder(1)
MsSeek(xFilial("SF2"))

mBrowse(7,4,20,74,"SF2")

RestArea(aArea)

Return(.T.)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511RjAut³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Interface do processamento dos Documentos de Saida(Reajuste)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ma511RjAut()

Local aArea := GetArea()
Local nOpcA := 0
Local cSerie:= ""

If !ctbValiDt( Nil, dDataBase, .T., Nil, Nil, { "FAT003" }, Nil )
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01 Serie Inicial      ?                                          ³
//³ mv_par02 Serie Final        ?                                          ³
//³ mv_par03 Documento Inicial  ?                                          ³
//³ mv_par04 Documento Final    ?                                          ³
//³ mv_par05 Emissao Inicial    ?                                          ³
//³ mv_par06 Emissao Final      ?                                          ³
//³ mv_par07 Cli/Forn. Inicial  ?                                          ³
//³ mv_par08 Cli/Forn. Final    ?                                          ³
//³ mv_par09 Loja Inicial       ?                                          ³ 
//³ mv_par10 Loja Final         ?                                          ³
//³ mv_par11 Aglutina Documentos?  Documento/Dia/Periodo                   ³
//³ mv_par12 Reajustar por      ?  Form.Reajuste/Preco de lista            ³
//³ mv_par13 Quanto aos Docs    ?  Todos/Em Terceiro                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MT511B",.F.)
If Sx5NumNota(@cSerie,GetNewPar("MV_TPNRNFS","1"))	
	FormBatch(OemToAnsi(STR0011),; //"Reajuste de precos"
			{	OemToAnsi(STR0007),; //"     Esta rotina efetua a preparacao dos documentos de saida com base nos documentos"
				OemToAnsi(STR0008),; //"de venda ou remessa para terceiros, conforme os parametros da rotina."
				OemToAnsi("")},;
				{  {5,.T.,{|o| Pergunte("MT511B",.T.)} },;
				{1,.T.,{|o| nOpcA:=1,o:oWnd:End()}  },;
				{2,.T.,{|o| nOpcA:=0,o:oWnd:End() }} })
	If ( nOpcA == 1 )
		Processa({|lEnd| Ma511RjPro(lEnd,cSerie)},,OemToAnsi(STR0012),.T.) //"Preparando complementos de preco"
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a integridade da Rotina                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511RjPro³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de processamento dos documentos de saida(Reajuste)   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Controle de cancelamento                             ³±±
±±³          ³ExpC2: Serie da Nota a ser gerada                           ³±±
±±³          ³ExpC3: [C] Cliente                                          ³±±
±±³          ³       [F] Fornece                                          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ma511RjPro(lEnd,cSerieNFS)

Local aArea     := GetArea()
Local aStruSD2  := SD2->(dbStruct())

Local aSD2      := {}
Local aSF2RecNo := {}
Local aSD2RecNo := {}
Local aVariacao := {}

Local cQuebra   := ""
Local cCursor   := "MA511RJPRO"
Local cQuery    := ""
Local cKey      := ""

Local cArqTmp   := ""
Local cMensagem := ""
Local lMostraCtb:= .F.
Local lAglutCtb := .F.
Local lCtbOnLine:= .F.
Local lCtbCusto := .F.
Local lReajusta := .F.
Local lAtuSA7   := .F.
Local nTpReaj   := MV_PAR12
Local nX        := 0
Local nY        := 0
Local nCalAcrs  := 1
Local nArredPrcLis:= 1
Local bSD2      := {|nTipo,aSD2,cAliasSD2,cAliasSF4,cAliasSB1,nRegSf2,aVar| Ma511RJSD2(nTipo,@aSD2,cAliasSD2,cAliasSF4,cAliasSB1,nRegSf2,nTpReaj,@aVar)}
Local bFilSD2   := {|cAliasSD2,cAliasSF4,cAliasSB1| Ma511FilRJ(cAliasSD2,cAliasSF4,cAliasSB1,nTpReaj) }
Local bSF2      := {|nTipo| Ma511RJSF2(nTipo) }
Local bTTS      := {|nTipo| Ma511RjTTS(aSF2Recno,aSD2Recno,aVariacao)}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem do Arquivo de Trabalho                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Do Case
	Case MV_PAR11 == 1 //Documento
		cKey := "D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA"
	Case MV_PAR11 == 2 //Diario
		cKey := "D2_FILIAL+DTOS(D2_EMISSAO)"
	Case MV_PAR11 == 3 //Periodo
	   	cKey := "D2_FILIAL"
EndCase

For nX := 1 To Len(aStruSD2)
	Do Case
		Case AllTrim(aStruSD2[nX][1])=="D2_TIPO"
			cQuery += ",'C' D2_TIPO "
		OtherWise
			cQuery += ","+aStruSD2[nX][1]+" "
	EndCase
Next nX

cQuery := "SELECT SF2.R_E_C_N_O_ SF2RECNO,SD2.R_E_C_N_O_ SD2RECNO "+cQuery
cQuery += "FROM "+RetSqlName("SF2")+" SF2,"
cQuery += RetSqlName("SD2")+" SD2 "
cQuery += "WHERE SF2.F2_FILIAL='"+xFilial("SF2")+"' AND "

//Bruno Cremaschi
If Type("lSDoc") == "U"
	cQuery += "SF2.F2_SERIE>='"+MV_PAR01+"' AND "
	cQuery += "SF2.F2_SERIE<='"+MV_PAR02+"' AND "
Else
	If lSDoc
		cQuery += "SF2.F2_SERIE>='"+MV_PAR01+"' AND "
		cQuery += "SF2.F2_SERIE<='"+MV_PAR02+"' AND "
	Else
		cQuery += "SF2.F2_SDOC>='"+MV_PAR01+"' AND "
		cQuery += "SF2.F2_SDOC<='"+MV_PAR02+"' AND "
	EndIf
EndIf

cQuery += "SF2.F2_DOC>='"+MV_PAR03+"' AND "
cQuery += "SF2.F2_DOC<='"+MV_PAR04+"' AND "	
cQuery += "SF2.F2_EMISSAO>='"+Dtos(MV_PAR05)+"' AND "	
cQuery += "SF2.F2_EMISSAO<='"+Dtos(MV_PAR06)+"' AND "	
cQuery += "SF2.F2_CLIENTE>='"+MV_PAR07+"' AND "	
cQuery += "SF2.F2_CLIENTE<='"+MV_PAR08+"' AND "	
cQuery += "SF2.F2_LOJA>='"+MV_PAR09+"' AND "	
cQuery += "SF2.F2_LOJA<='"+MV_PAR10+"' AND "

If nTpReaj == 1
	cQuery += "SF2.F2_TIPO NOT IN ('C', 'I', 'P') AND "	   
	cQuery += "SF2.F2_REAJUST <> '"+Space(Len(SF2->F2_REAJUST))+"' AND "
Else	                                                
	cQuery += "SF2.F2_TIPO NOT IN ('C', 'I', 'P', 'B') AND "	
Endif		

cQuery += "SF2.D_E_L_E_T_=' ' AND "
cQuery += "SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
cQuery += "SD2.D2_DOC=SF2.F2_DOC AND "
cQuery += "SD2.D2_SERIE=SF2.F2_SERIE AND "
cQuery += "SD2.D2_CLIENTE=SF2.F2_CLIENTE AND "
cQuery += "SD2.D2_LOJA=SF2.F2_LOJA AND "
cQuery += "SD2.D2_TIPO=SF2.F2_TIPO AND "
cQuery += "SD2.D_E_L_E_T_=' ' "

If MV_PAR13 == 2
	cQuery += "AND EXISTS (SELECT B6_SALDO "
	cQuery += "FROM "+RetSqlName("SB6")+" SB6 "
	cQuery += "WHERE SB6.B6_FILIAL='"+xFilial("SB6")+"' AND "
	cQuery += "SB6.B6_PRODUTO=SD2.D2_COD AND "
	cQuery += "SB6.B6_CLIFOR=SD2.D2_CLIENTE AND "
	cQuery += "SB6.B6_LOJA=SD2.D2_LOJA AND "
	cQuery += "SB6.B6_IDENT=SD2.D2_IDENTB6 AND "		
	cQuery += "SB6.B6_SALDO-SB6.B6_QULIB>0 AND "
	cQuery += "SB6.B6_TIPO='E' AND "		
	cQuery += "((SB6.B6_TPCF='F' AND SD2.D2_TIPO IN ('D','B')) OR "
	cQuery += " (SB6.B6_TPCF='C' AND SD2.D2_TIPO NOT IN ('D','B'))) AND "
	cQuery += "SB6.D_E_L_E_T_=' ' )"
EndIf		

cQuery += "ORDER BY "+SqlOrder(cKey)
                               
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cCursor,.T.,.T.)

For nX := 1 To Len(aStruSD2)
	If aStruSD2[nX][2] <> "C"
		TcSetField(cCursor,aStruSD2[nX][1],aStruSD2[nX][2],aStruSD2[nX][3],aStruSD2[nX][4])
	EndIf
Next nX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificacao das perguntas da Preparacao do Documento de Saida          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01 Mostra Lan‡.Contab ?  Sim/Nao                        ³
//³ mv_par02 Aglut. Lan‡amentos ?  Sim/Nao                        ³
//³ mv_par03 Lan‡.Contab.On-Line?  Sim/Nao                        ³
//³ mv_par04 Contb.Custo On-Line?  Sim/Nao                        ³
//³ mv_par05 Reaj. na mesma N.F.?  Sim/Nao                        ³
//³ mv_par06 Taxa deflacao ICMS ?  Numerico                       ³
//³ mv_par07 Metodo calc.acr.fin?  Taxa defl/Dif.lista/% Acrs.ped ³
//³ mv_par08 Arred.prc unit vist?  Sempre/Nunca/Consumid.final    ³
//³ mv_par09 Agreg. liberac. de ?  Caracter                       ³
//³ mv_par10 Agreg. liberac. ate?  Caracter                       ³
//³ mv_par11 Aglut.Ped. Iguais  ?  Sim/Nao                        ³
//³ mv_par12 Valor Minimo p/fatu?                                 ³
//³ mv_par13 Transportadora de  ?                                 ³
//³ mv_par14 Transportadora ate ?                                 ³
//³ mv_par15 Atualiza Cli.X Prod?                                 ³
//³ mv_par16 Emitir             ?  Nota / Cupom Fiscal            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MT460A",.F.)
lMostraCtb  := MV_PAR01==1
lAglutCtb   := MV_PAR02==1
lCtbOnLine  := MV_PAR03==1
lCtbCusto   := MV_PAR04==1
lReajusta   := MV_PAR05==1
nCalAcrs    := MV_PAR06
nArredPrcLis:= MV_PAR08
lAtuSA7     := MV_PAR15==1
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processamento dos documentos de Saida                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSD2      := {}
aSF2RecNo := {}
nY        := 0

ProcRegua(SF2->(LastRec()))
dbSelectArea(cCursor)

While !Eof() 

	If Eval(bFilSD2,cCursor,"SF4","SB1")
		aadd(aSd2,{})
		nY++
		For nX := 1 To Len(aStruSD2)
			aadd(aSD2[nY],(cCurSor)->(FieldGet(FieldPos(aStruSD2[nX][1]))))
		Next nX
		Eval(bSD2,1,aSD2[nY],cCursor,"SF4","SB1",(cCursor)->SF2RECNO,@aVariacao)
		aadd(aSF2RecNo,(cCursor)->SF2RECNO)
		aadd(aSD2RecNO,(cCursor)->SD2RECNO)
	EndIf	         
    cQuebra := &(cKey)
	dbSelectArea(cCursor)
	dbSkip()
	If lEnd
		Exit
	EndIf
	
	If cQuebra <> &(cKey) .Or. (cCursor)->(Eof()) .Or. nY > 300
		If Len(aSd2) > 0
			//Bruno cremaschi
			cSDoc := SerieNFID((cCursor), 2, "D2_SERIE")
			cMensagem := MaNfs2Nfs( cSDoc,;
								(cCursor)->D2_DOC,;
								(cCursor)->D2_CLIENTE,;
								(cCursor)->D2_LOJA,;
								cSerieNFS,;
								lMostraCtb,;
								lAglutCtb,;
								lCtbOnLine,;
								lCtbCusto,;
								lReajusta,;
								nCalAcrs,;
								nArredPrcLis,;
								lAtuSA7,;
								.F.,;
								bFilSD2,;
								bSD2,;
								bSF2,;
								bTTS,;
								aSF2Recno,;
								aSD2)
		EndIf
		aSF2RecNo := {}
		aSD2RecNo := {}
		aSD2      := {}
		nY        := 0	
		//Se for por Documento, limpo o array de variação
		If cKey == "D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA"
			aVariacao := {}
		EndIf	
	EndIf
	IncProc(STR0010+cSerieNFS+"/"+cMensagem) //"Preparados os Doc's: "
EndDo

	(cCursor)->( DbCloseArea() )
	dbSelectArea("SF2")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a integridade da Rotina                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511RJSD2³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de atualizacao do SD2 (Reajuste)                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Tipo                                                 ³±±
±±³          ³       [1] Apos a inclusao                                  ³±±
±±³          ³       [2] Apos a atualizacao                               ³±±
±±³          ³ExpA2: Array de um item do SD2 conforme estrutura           ³±±
±±³          ³ExpC3: Alias do SD2                                         ³±±
±±³          ³ExpC4: Alias do SF4                                         ³±±
±±³          ³ExpC5: Alias do SB1                                         ³±±
±±³          ³ExpN6: Registro do SF2                                      ³±±
±±³          ³ExpN7: Tipo de reajuste                                     ³±±
±±³          ³       [1] Formula                                          ³±±
±±³          ³       [2] Diferenca de tabela de preco                     ³±±
±±³          ³ExpA8: Variacao do preco unitario                           ³±±                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma511RJSD2(nTipo,aSD2,cAliasSD2,cAliasSF4,cAliasSB1,nRegSF2,nTpReaj,aVariacao)

Local aArea     := GetArea()
Local aSaldoSB6 := {}
Local aStruSD2  := SD2->(dbStruct())
Local nPTipo    := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_TIPO"})
Local nPQtdVen  := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_QUANT"})
Local nPPrcVen  := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_PRCVEN"})
Local nPPrUnit  := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_PRUNIT"})
Local nPVarPrUn := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_VARPRUN"})
Local nPValor   := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_TOTAL"})
Local nPDesc    := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_DESC"})
Local nPVlrDesc := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_DESCON"})
Local nPPedido  := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_PEDIDO"})
Local nPItemPV  := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_ITEMPV"})
Local nPProduto := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_COD"})
Local nPCliente := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_CLIENTE"})
Local nPLoja    := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_LOJA"})
Local nPNfOri   := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_NFORI"})
Local nPSeriOri := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_SERIORI"})
Local nPItemOri := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_ITEMORI"})
Local nPDoc     := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_DOC"})
//Bruno Cremaschi - Projeto chave única.
local cSDoc		:= SerieNFID("SD2", 3, "D2_SERIE")
Local nPSerie   := aScan(aStruSD2,{|x| AllTrim(x[1]) == cSDoc})
Local nPItem    := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_ITEM"})
Local nPIdent   := aScan(aStruSD2,{|x| AllTrim(x[1]) == "D2_IDENTB6"})
Local nQuant    := 0       
Local nPreco    := 0

If nTipo == 1
	If nTpReaj == 1
		SF2->(MsGoto(nRegSF2))
		If !Empty(SF2->F2_REAJUST)
			aadd(aVariacao,aSD2[nPPrcVen])
			aSD2[nPPrcVen]  := Formula(SF2->F2_REAJUST)
			aVariacao[Len(aVariacao)] -= aSD2[nPPrcVen]
		Else
			aadd(aVariacao,0)
		EndIf
	Else
		dbSelectarea("SC5")
		dbSetOrder(1)
		MsSeek(xFilial("SC5")+aSD2[nPPedido])
		
		If !Empty(aSD2[nPIdent])
			aSaldoSB6 := CalcTerc((cAliasSD2)->D2_COD,(cAliasSD2)->D2_CLIENTE,(cAliasSD2)->D2_LOJA,(cAliasSD2)->D2_IDENTB6,(cAliasSD2)->D2_TES,(cAliasSD2)->D2_TIPO)
			nQuant    := aSaldoSB6[1]-aSaldoSB6[2]
		Else
			nQuant    := aSD2[nPQtdVen]	
		Endif

		nPreco := (MaTabPrVen(SC5->C5_TABELA,aSD2[nPProduto],aSD2[nPQtdVen],aSD2[nPCliente],aSD2[nPLoja],SC5->C5_MOEDA)-aSD2[nPPrUnit])

		If SC5->C5_DESC1 <> 0 .Or. SC5->C5_DESC2 <> 0 .Or. SC5->C5_DESC3 <> 0 .Or. SC5->C5_DESC4 <> 0
			 nPreco :=FtDescCab(nPreco,{SC5->C5_DESC1,SC5->C5_DESC2,SC5->C5_DESC3,SC5->C5_DESC4})
		Endif
		
		nPreco -= aSD2[nPVarPrUn]
    
		If aSD2[nPDesc] > 0
			aSD2[nPPrcVen] := (nPreco - (nPreco * (aSD2[nPDesc] / 100))) * nQuant
		Else                                                                       
			aSD2[nPPrcVen] := nPreco * nQuant
		Endif	
			
		aadd(aVariacao,nPreco)
	EndIf
	aSD2[nPTipo]    := "C"
	aSD2[nPQtdVen]  := 0	
	aSD2[nPValor]   := aSD2[nPPrcVen]
	aSD2[nPDesc]    := 0
	aSD2[nPVlrDesc] := 0
	aSD2[nPNfOri]   := aSD2[nPDoc]
	aSD2[nPSeriOri] := aSD2[nPSerie]
	aSD2[nPItemOri] := aSD2[nPItem]
	aSD2[nPPedido]  := ""
	aSD2[nPItemPV]  := ""
	aSD2[nPPrUnit]	:= 0	
EndIf
RestArea(aArea)
Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511FilRJ³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de seleccao do SD2 (Reajuste)                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Alias do SD2                                         ³±±
±±³          ³ExpC2: Alias do SF4                                         ³±±
±±³          ³ExpC3: Alias do SB1                                         ³±±
±±³          ³ExpN4: Tipo de Reajuste                                     ³±±
±±³          ³       [1] Formula                                          ³±±
±±³          ³       [2] Diferenca de tabela                              ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma511FilRJ(cAliasSD2,cAliasSF4,cAliasSB1,nTpReaj)

Local aArea    := GetArea()                              
Local aSaldoSB6:= {}
Local lRetorno := .F.
Local nPreco   := 0

If nTpReaj == 1   
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Retorna .T. pois o filtro do SF2 ja esta sendo realizado               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lRetorno := .T.
Else
		dbSelectarea("SC5")
		dbSetOrder(1)
		If MsSeek(xFilial("SC5")+(cAliasSD2)->D2_PEDIDO)

			(cAliasSF4)->(DbSetOrder(1))
			(cAliasSF4)->( MsSeek(XFilial("SF4")+(cAliasSD2)->D2_TES) )	// alteracao minha
		
			If (cAliasSF4)->F4_PODER3 == "R" .And. !Empty((cAliasSD2)->D2_IDENTB6)
				aSaldoSB6 := CalcTerc((cAliasSD2)->D2_COD,(cAliasSD2)->D2_CLIENTE,(cAliasSD2)->D2_LOJA,(cAliasSD2)->D2_IDENTB6,(cAliasSD2)->D2_TES,(cAliasSD2)->D2_TIPO)			
				
				If aSaldoSB6[1]-aSaldoSB6[2] > 0
				nPreco := MaTabPrVen(SC5->C5_TABELA,(cAliasSD2)->D2_COD,(cAliasSD2)->D2_QUANT,(cAliasSD2)->D2_CLIENTE,(cAliasSD2)->D2_LOJA,SC5->C5_MOEDA)-(cAliasSD2)->D2_PRUNIT
				If SC5->C5_DESC1 <> 0 .Or. SC5->C5_DESC2 <> 0 .Or. SC5->C5_DESC3 <> 0 .Or. SC5->C5_DESC4 <> 0
					 nPreco :=FtDescCab(nPreco,{SC5->C5_DESC1,SC5->C5_DESC2,SC5->C5_DESC3,SC5->C5_DESC4})
				EndIf		
				nPreco -= (cAliasSD2)->D2_VARPRUN
				lRetorno := nPreco > 0
				
				Else 
					lRetorno := .F.	
				Endif	
			Else
			nPreco := MaTabPrVen(SC5->C5_TABELA,(cAliasSD2)->D2_COD,(cAliasSD2)->D2_QUANT,(cAliasSD2)->D2_CLIENTE,(cAliasSD2)->D2_LOJA,SC5->C5_MOEDA)-(cAliasSD2)->D2_PRUNIT
			If SC5->C5_DESC1 <> 0 .Or. SC5->C5_DESC2 <> 0 .Or. SC5->C5_DESC3 <> 0 .Or. SC5->C5_DESC4 <> 0
				 nPreco :=FtDescCab(nPreco,{SC5->C5_DESC1,SC5->C5_DESC2,SC5->C5_DESC3,SC5->C5_DESC4})
			EndIf		
			nPreco -= (cAliasSD2)->D2_VARPRUN
			lRetorno := nPreco > 0
		Endif
	Endif	
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511RJSF2³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de atualizacao do SF2 (Reajuste)                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: nTipo                                                ³±±
±±³          ³       [1] Apos a inclusao                                  ³±±
±±³          ³       [2] Apos a atualizacao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma511RJSF2(nTipo)

Local aArea := GetArea()

If nTipo == 1
	SF2->F2_TRANSP  := ""
	SF2->F2_REDESP  := ""
	SF2->F2_OK      := ""
	SF2->F2_FIMP    := ""
	SF2->F2_DTLANC  := Ctod("")
	SF2->F2_DTREAJ  := Ctod("")
	SF2->F2_REAJUST := ""
	SF2->F2_DTBASE0 := Ctod("")
	SF2->F2_FATORB0 := 0
	SF2->F2_DTBASE1 := Ctod("")
	SF2->F2_FATORB1 := 0
	SF2->F2_VARIAC  := 0
	SF2->F2_NEXTDOC := ""
	SF2->F2_PDV     := ""
	SF2->F2_MAPA    := ""
	SF2->F2_ECF     := ""
	SF2->F2_PREFIXO := ""
	SF2->F2_DUPL    := ""
	SF2->F2_ORDPAGO := ""
	SF2->F2_NFCUPOM := ""
	SF2->F2_DESCCAB := 0
	SF2->F2_FRETE   := 0
	SF2->F2_SEGURO  := 0
	SF2->F2_DESPESA := 0
	SF2->F2_FRETAUT := 0
	SF2->F2_ICMAUTO := 0
	SF2->F2_CONTSOC := 0
	SF2->F2_DESCONTO:= 0	
EndIf
RestArea(aArea)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma511RJTTs³ Autor ³Eduardo Riera          ³ Data ³21.02.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de atualizacao do documento de origem ( Reajuste )   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: Array com os registros do SF2                        ³±±
±±³          ³ExpN2: Array com os Recnos do SD2                           ³±±
±±³          ³ExpN3: Array com a variacao de preco                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma511RJTTS(aRegSF2,aRegSD2,aVariacao)

Local aArea    := GetArea()
Local aAreaSF2 := SF2->(GetArea())
Local aAreaSD2 := SD2->(GetArea())
Local aFeito   := {}
Local nX       := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona no SF2 de Origem                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 1 To Len(aRegSF2)
	If aScan(aFeito,aRegSF2[nX])==0
		SF2->(MsGoto(aRegSF2[nX]))
		RecLock("SF2")
		SF2->F2_NEXTDOC := ""
		SF2->F2_NEXTSER := ""
		MsUnLock()
		aadd(aFeito,aRegSF2[nX])
	EndIf
Next nX
For nX := 1 To Len(aRegSD2)
	SD2->(MsGoto(aRegSD2[nX]))
	RecLock("SD2")
	SD2->D2_VARPRUN += aVariacao[nX]
	MsUnLock()
Next nX
RestArea(aAreaSD2)
RestArea(aAreaSF2)
RestArea(aArea)
Return(Nil)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Marco Bianchi         ³ Data ³01/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()

Private aRotina   := {	{ STR0001,"AxPesqui"		,0,1,0,.F.},;			// "Pesquisar"
								{ STR0002,"Mc090Visual"	,0,2,0,NIL},;		// "Visual"
								{ STR0003,"Ma511P3Dev"	,0,4,0,NIL},;		// "Prep. Doc"
								{ STR0004,"Ma511P3Aut"	,0,0,0,NIL}}		// "Automatico"

If ExistBlock("MA511MNU")
	ExecBlock("MA511MNU",.F.,.F.)
EndIf

Return(aRotina)

//-------------------------------------------------------------------
/*/{Protheus.doc} MATA511

Função para validar os parametros responsaveis pelo preenchimento
do cliente e loja

@param cTpCliFor, 
@param cCliente, Caracter, Codigo do cliente
@param cLojaCli, Caracter, Codigo da loja
@param cFornece, Caracter, codigo do fornecedor
@param cLojaFor, Caracter, codigo da loja

@return lRet, retorno .T. se os parametros estão corretos

@author Serviços
@since 28/12/2015
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function Ma511VldCli(cTpCliFor,cCliente,cLojaCli,cFornece,cLojaFor)
Local lRet 		:= .T.

If cTpCliFor == "F"

	If Empty(cFornece) .OR. Empty(cLojaFor)
		Ma511ErrType(1) //Erro de Parametro Código/Loja do fornecedor em Branco
		lRet := .F.
	Else
		lRet := M511ExtCpo( cFornece ) .And. M511ExtCpo( cLojaFor ) 
				
		If !lRet 
			Ma511ErrType(2) // Parametro de Código/Loja do fornecedor incorreto
		EndIf
	EndIf
Else
	If Empty(cCliente) .OR. Empty(cLojaCli)
		Ma511ErrType(3) //Erro de Parametro Código/Loja do cliente em Branco
		lRet := .F.
	Else
		lRet := M511ExtCpo( cCliente ) .And. M511ExtCpo( cLojaCli ) 
		
		If !lRet 
			Ma511ErrType(4) // Parametros de Código/Loja do cliente incorreto
		EndIf
	EndIf
EndIf

Return lRet
//-------------------------------------------------------------------
/*/{Protheus.doc} M511ExtCpo

Função para validar se o conteúdo dos parametros MV_FTP3CL1, MV_FTP3CL2,
MV_FTP3FO1, MV_FTP3FO2 estão preenchidos corretamente. 

Ex.: SB1->B1_PROC

@param cParam 
@return lRet, retorno .T. se os parametros estão corretos

@author Renato Cunha
@since 13/01/2017
@version 12.1.16
/*/
//-------------------------------------------------------------------
Static Function M511ExtCpo( cParam )
Local 	aAreaSx3	:= {}
Local 	lRet		:= .F.
Local 	nPos		:= 0
Local	cField		:= ""
Default cParam		:= ""

If !Empty( cParam )
	
	aAreaSx3	:= SX3->(GetArea())	
	DbSelectArea("SX3")
	SX3->( DBSetOrder(2) )
	nPos 	:= At("-", cParam )
	cField 	:= SubStr( cParam, nPos+2, Len( cParam ) )
	If SX3->( DbSeek( cField ) )
		lRet := .T.
	EndIf
	RestArea(aAreaSx3)

EndIf

Return lRet
//-------------------------------------------------------------------
/*/{Protheus.doc} Ma511ErrType

Devolve a mensagem de erro específica
@param nError, Numerigo, código do erro 
1 - Código ou Loja do Fornecedor em branco
2 - Código ou Loja do Forncedor incorreto
3 - Código ou Loja do Cliente em branco
4 - Código ou Loja do Cliente incorreto 

@return Nil

@author Renato Cunha
@since 13/01/2017
@version 12.1.16
/*/
//-------------------------------------------------------------------
Static Function Ma511ErrType(nError)
Do case
	Case nError == 1 
		Aviso(STR0013, STR0014,{STR0015},2) //"Atenção""O processo não terá continuidade, parametros MV_FTP3FO1 e/ou MV_FTP3FO2  não estão preenchidos"                                                                                                                                                                                                                                                                                                                                                                                                    
	Case nError == 2
		Aviso(STR0013, STR0017,{STR0015},2) //"Atenção","O processo não terá continuidade, parametros MV_FTP3FO1 e/ou MV_FTP3FO2  não estão preenchidos corretamente. Preencha conforme Modelo: SB1->B1_PROC 
	Case nError == 3 
		Aviso(STR0013, STR0016,{STR0015},2)//"Atenção", "O processo não terá continuidade, parametros MV_FTP3CL1 e/ou MV_FTP3CL2  não estão preenchidos"                                                                                                                                                                                                                                                                                                                                                                      
	Case nError == 4
		Aviso(STR0013, STR0018,{STR0015},2)//"Atenção", "O processo não terá continuidade, parametros MV_FTP3CL1 e/ou MV_FTP3CL2  não estão preenchidos corretamente. Preencha conforme Modelo: SB1->B1_PROC
EndCase

Return Nil 
