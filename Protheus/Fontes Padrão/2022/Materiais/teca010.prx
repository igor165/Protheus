#INCLUDE "TECA010.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³TECA010   ³ Autor ³ Eduardo Riera         ³ Data ³ 22.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Manutencao do Cadastro de Ocorrˆncia/Problema              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function TECA010()         
          
Local aUsButtons:={}                             
                             
If ExistBlock( "AT010BUT" ) 
   aUsButtons := ExecBlock( "AT010BUT", .F., .F. )  
EndIf   

AxCadastro("AAG",STR0001,"At010VlDel()",,aUsButtons) //"Cadastro de Ocorrˆncia/Problema"/ 

Return(.T.) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At010VlDel³ Autor ³ Eduardo Riera         ³ Data ³ 22.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Exclusao da Ocorrˆncia/Problema               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: logico                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At010VlDel()
                     
Local cIndTrab := ""
Local cCond    := ""
Local nIndex   := 0 
Local lRetorno := .T.
Local cAliasAA6	:= "" 
Local cAliasAA9 := ""
Local cAliasABI := ""
Local cAliasABD := ""
Local cQuery	:=""
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica no FAQ                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("AAI")
dbSetOrder(2)
If ( dbSeek(xFilial("AAI")+AAG->AAG_CODPRB) )
	lRetorno := .F.
	Help(" ",1,"AT010DEL01")
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica nos Chamados Tecnicos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lRetorno )
	dbSelectArea("AB2")
	dbSetOrder(2)
	If ( dbSeek(xFilial("AB2")+AAG->AAG_CODPRB) )
		lRetorno := .F.
		Help(" ",1,"AT010DEL02")
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica nos Orcamentos                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lRetorno )
	dbSelectArea("AB4")
	dbSetOrder(3)
	If ( dbSeek(xFilial("AB4")+AAG->AAG_CODPRB) )
		lRetorno := .F.
		Help(" ",1,"AT010DEL03")
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica nas Ordens de Servico                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lRetorno )
	dbSelectArea("AB7")
	dbSetOrder(4)
	If ( dbSeek(xFilial("AB7")+AAG->AAG_CODPRB) )
		lRetorno := .F.
		Help(" ",1,"AT010DEL04")
	EndIf
EndIf	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica no Atendimento das Ordens de Servico                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lRetorno )
	dbSelectArea("AB9")
	dbSetOrder(2)
	If ( dbSeek(xFilial("AB9")+AAG->AAG_CODPRB) )
		lRetorno := .F.
		Help(" ",1,"AT010DEL04")
	EndIf
EndIf   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica na Amarracao Produto x Ocorrencia                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea( "AA7" ) 
cIndTrab := CriaTrab( , .F. ) 

cCond  := ""
cCond  += "AA7_FILIAL=='" + xFilial("AA7") + "' .AND. AA7_CODPRB=='" + AAG->AAG_CODPRB + "' "

IndRegua( "AA7", cIndTrab, AA7->( IndexKey() ), ,cCond ) 


#IFNDEF TOP
	nIndex := RetIndex("AA7")
	dbSetIndex(cIndTrab+OrdBagExt())
	dbSetOrder(nIndex+1)
#ENDIF

dbGotop()               

If !AA7->( Eof() ) 
	lRetorno := .F.
	Help(" ",1,"HELP", , STR0002, 3, 1 )  //"Nao e possivel a exclusao de uma ocorrencia com amarracao produto x ocorrencia"
EndIf

RetIndex( "AA7" )

#IFDEF TOP
	If lRetorno
	   cAliasAA6 := GetNextAlias()
	   cQuery    := ""
	
	   cQuery += " SELECT COUNT(*) TOT_OCORR "
	   cQuery += "   FROM " + RetSqlName( "AA6" )
	   cQuery += "  WHERE AA6_FILIAL='" + xFilial( "AA6" ) + "'"
	   cQuery += "    AND AA6_CODPRB = '" + AAG->AAG_CODPRB + "'"
	   cQuery += "    AND D_E_L_E_T_ = ' '"
	
	   cQuery := ChangeQuery( cQuery )
	   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAA6,.F.,.T. )
	
	   If (cAliasAA6)->TOT_OCORR > 0      
	      Help(" ",1,"HELP", , STR0003, 3, 1 )  //"Esta ocorrencia esta sendo utilizado por uma tabela (Kit de atendimento) e nao podera ser excluida."
	      lRetorno := .F.
	   Endif
	   (cAliasAA6)->(DbCloseArea()) 
   EndIf
    
   If lRetorno
	   cAliasAA9 := GetNextAlias()
	   cQuery    := ""
	
	   cQuery += " SELECT COUNT(*) TOT_OCORR "
	   cQuery += "   FROM " + RetSqlName( "AA9" )
	   cQuery += "  WHERE AA9_FILIAL='" + xFilial( "AA9" ) + "'"
	   cQuery += "    AND AA9_CODPRB = '" + AAG->AAG_CODPRB + "'"
	   cQuery += "    AND D_E_L_E_T_ = ' '"
	
	   cQuery := ChangeQuery( cQuery )
	   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAA9,.F.,.T. )
	
	   If (cAliasAA9)->TOT_OCORR > 0      
	      Help(" ",1,"HELP", , STR0004, 3, 1 )  //"Esta ocorrencia esta sendo utilizado por uma tabela (Plano de Manutenção) e nao podera ser excluida."
	      lRetorno := .F.
	   Endif
	   (cAliasAA9)->(DbCloseArea())  
   EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se a ocorrência existe nas etapas do projeto.				   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno
	   cAliasABI := GetNextAlias()
	   cQuery    := ""

	   cQuery += " SELECT COUNT(*) TOT_CODPRB "
	   cQuery += " FROM " + RetSqlName( "ABI" )
	   cQuery += " WHERE ABI_FILIAL='" + xFilial( "ABI" ) + "'"
	   cQuery += " AND ABI_CODPRB = '" + AAG->AAG_CODPRB + "'"
	   cQuery += " AND D_E_L_E_T_ = ' '"
	
	   cQuery := ChangeQuery( cQuery )
	   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasABI,.F.,.T. )
	
	   If (cAliasABI)->TOT_CODPRB > 0      
	      Help(" ",1,"NODELETA", , STR0005, 3, 1 )	//"Esta Ocorrência/Problema esta sendo utilizada pela rotina de Projetos."
	      lRetorno := .F.
	   Endif
	   (cAliasABI)->(dbCloseArea())   
	EndIf   
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se a ocorrência existe nas pendencias da base       		   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno
	   cAliasABD := GetNextAlias()
	   cQuery    := ""

	   cQuery += " SELECT COUNT(*) TOT_CODPRB "
	   cQuery += " FROM " + RetSqlName( "ABD" )
	   cQuery += " WHERE ABD_FILIAL='" + xFilial( "ABD" ) + "'"
	   cQuery += " AND ABD_CODPRB = '" + AAG->AAG_CODPRB + "'"
	   cQuery += " AND D_E_L_E_T_ = ' '"
	
	   cQuery := ChangeQuery( cQuery )
	   dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasABD,.F.,.T. )
	
	   If (cAliasABD)->TOT_CODPRB > 0      
	      Help(" ",1,"NODELETA", , STR0006, 3, 1 )	//"Esta Ocorrência/Problema esta sendo utilizada pela rotina de Pendências da Base de Atendimento."
	      lRetorno := .F.
	   Endif
	   (cAliasABD)->(dbCloseArea())   
	EndIf   
	

#ENDIF

Return(lRetorno)
